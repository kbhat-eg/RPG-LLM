             PGM        PARM(&PARM &MAPI &FILN)

/* Initiering av parametere                                          */

/*6.nu*/     DCL        VAR(&PARM)   TYPE(*CHAR) LEN(33)
             DCL        VAR(&FIRM)   TYPE(*CHAR) LEN(3)
             DCL        VAR(&MAPI)   TYPE(*CHAR) LEN(25)
             DCL        VAR(&FILN)   TYPE(*CHAR) LEN(25)
             DCL        VAR(&MAPX)   TYPE(*CHAR) LEN(30)
             DCL        VAR(&FRAFIL) TYPE(*CHAR) LEN(70)
             DCL        VAR(&TILFIL) TYPE(*CHAR) LEN(70)
             DCL        VAR(&CLIB) TYPE(*CHAR) LEN(10)

/* Henter verdier i fra LDA.                                         */

             RTVDTAARA  DTAARA(*LDA (944 03)) RTNVAR(&FIRM)
             CLRPFM     FILE(FNCPPF)

/* Kaller program for generering av poster til Diners                */

             CALL       PGM(FO194R)    PARM(&PARM &MAPI &FILN)

             DLTF       FILE(FNCPTMP)
             MONMSG     MSGID(CPF0000)

/* Lager til-katalogen p√• IFS hvis den ikke finnes                   */

             RTVJOBA    CURLIB(&CLIB)

             CHGVAR     VAR(&MAPX) VALUE('/home/' *TCAT &MAPI)
             CRTDIR     DIR(&MAPX) DTAAUT(*RWX) OBJAUT(*ALL)
             MONMSG     MSGID(CPF0000)

             CRTSRCPF   FILE(FNCPTMP) RCDLEN(1024) MBR(FNCPTMP) +
                          TEXT('Temp.fil Purch.') SIZE(*NOMAX)
             MONMSG     MSGID(CPF0000)

             CPYF       FROMFILE(*LIBL/FNCPPF) TOFILE(*LIBL/FNCPTMP) +
                          FROMMBR(FNCPPF) TOMBR(FNCPTMP) +
                          MBROPT(*REPLACE) FMTOPT(*CVTSRC)

             CHGVAR     VAR(&FRAFIL) VALUE('/qsys.lib/' *TCAT &CLIB +
                          *TCAT '.lib/FNCPTMP.file/FNCPTMP.mbr')
             CHGVAR     VAR(&TILFIL) VALUE(&MAPX)
             CHGVAR     VAR(&TILFIL) VALUE(&TILFIL *TCAT '/' *TCAT &FILN)

             CPYTOSTMF  FROMMBR(&FRAFIL) TOSTMF(&TILFIL) +
                          STMFOPT(*REPLACE) STMFCODPAG(*PCASCII)
             CHGAUT     OBJ(&TILFIL) USER(*PUBLIC) DTAAUT(*RWX) +
                          OBJAUT(*ALL)

             DLTF       FILE(FNCPTMP)

             ENDPGM
