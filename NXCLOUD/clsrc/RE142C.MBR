/*         Filen bør leges i /asp/xxx/Visma                                            */
/*         BAckup legges i   /asp/xxx/Visma/Backup/L_åååå_mm_dd_tt                     */
/*         Bane og filnavn lagres i dtaara/VismaPayR og hentes opp herfra neste gang   */
             PGM

             DCLF       FILE(RE130D) ALWVARLEN(*YES) ALWNULL(*YES)

             DCL        VAR(&O1FGR) TYPE(*CHAR) LEN(3)
             DCL        VAR(&MELD)  TYPE(*CHAR) LEN(60)
             DCL        VAR(&WMM1) TYPE(*CHAR) LEN(10) +
                          VALUE('LONN      ')
             DCL        VAR(&FIL)    TYPE(*CHAR) LEN(30)
             DCL        VAR(&WFEIL) TYPE(*CHAR) LEN(1) VALUE(' ')
             DCL        VAR(&WALI)   TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&WAPO)   TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&MLD1)   TYPE(*CHAR) LEN(40)
             DCL        VAR(&MLD2)   TYPE(*CHAR) LEN(40)
             DCL        VAR(&MLD3) TYPE(*CHAR) LEN(6) VALUE('Filen')
             DCL        VAR(&MLD4)  TYPE(*CHAR) LEN(21) value(' finnes ikke i mappe:')
             DCL        VAR(&grp)   TYPE(*CHAR) LEN(10) value('GRP_xxx')
             DCL        VAR(&dlib)  TYPE(*CHAR) LEN(10) value('ADBxxx')
             DCL        VAR(&sts)   TYPE(*CHAR) LEN(08) value(' ')
             dcl        var(&dato)  TYPE(*char) LEN(6)
             dcl        var(&nn)    TYPE(*dec)  LEN(2 0)
             dcl        var(&xx)    TYPE(*char) LEN(2)
             dcl        var(&fra)   TYPE(*char) LEN(60)
             dcl        var(&bfil)  TYPE(*char) LEN(60)
             dcl        var(&xfil)  TYPE(*char) LEN(60)

             RTVDTAARA  DTAARA(*LDA (944 03)) RTNVAR(&O1frm)
             RTVDTAARA  DTAARA(*LDA (931 03)) RTNVAR(&O1fgr)
             rtvdtaara  dtaara(*curlib/VismaPayR (01 36)) rtnvar(&o1map)
             monmsg     msgid(cpf1015) exec(do)
              crtdtaara  dtaara(*curlib/VismaPayR) type(*char) len(100)
             enddo
             rtvdtaara  dtaara(*curlib/VismaPayR (37 12)) rtnvar(&o1fil)
             CHGVAR     VAR(&FIL) VALUE('            ')
             CHGDTAARA  DTAARA(*LDA (211 90)) +
               VALUE('                                                              +
                                         ')
             chgvar     var(%sst(&wmm1 5 3)) value(&o1frm)
             chgvar     var(%sst(&grp  5 3)) value(&o1fgr)
             chgvar     var(%sst(&dlib 4 3)) value(&o1fgr)

             SNDRCVF    RCDFMT(O1WIN)
             chgdtaara  dtaara(*curlib/VismaPayR (01 36)) value(&O1MAP)
             chgdtaara  dtaara(*curlib/VismaPayR (37 30)) value(&O1fil)
             crtdir     dir(&o1map)
             monmsg     msgid(cpf0000)

             CHGVAR     VAR(&FIL) VALUE(&O1FIL)

             IF         COND(&IN03 *EQ '1') THEN(DO)
             GOTO       CMDLBL(ENDPGM)
             ENDDO

/* Test om arbeidsfiler finnes                                     */

             CHKOBJ     OBJ(*LIBL/REviPF) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
               Call     ag916c ('REVIPF    ' &o1fgr &grp ' ')
               chgpf    file(*curlib/REVIPF) maxmbrs(*nomax)
             enddo
             CHKOBJ     OBJ(*LIBL/REUTPF) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
               Call     ag916c ('REUTPF    ' &o1fgr &grp ' ')
               chgpf    file(*curlib/REUTPF) maxmbrs(*nomax)
             enddo


/* Bygger membere dersom de ikke finnes                              */
             CHGVAR     VAR(%SST(&WMM1 5 3)) VALUE(&o1frm)
             CHKOBJ     OBJ(*LIBL/REviPF) OBJTYPE(*FILE) MBR(&WMM1)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDPFM     FILE(*LIBL/REviPF) MBR(&WMM1) TEXT('Poster +
                          fra Visma Payroll')
                        ENDDO

             CHKOBJ     OBJ(*LIBL/REUTPF) OBJTYPE(*FILE) MBR(&WMM1)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDPFM     FILE(*LIBL/REUTPF) MBR(&WMM1) TEXT('Poster +
                          fra Visma lønn')
                        ENDDO

/* Sletter innholdet fra innlesingsfila                              */

             CLRPFM     FILE(REviPF) MBR(&WMM1)
             CLRPFM     FILE(REUTPF) MBR(&WMM1)

/*  Lager feilmelding                                                */

             CHGVAR     VAR(&MELD) VALUE(&MLD3 *CAT &FIL *tCAT &MLD4-
                        *CAT &o1map)
             MONMSG     MSGID(IWS1603 CPA0701)
             call       pgm(aspfrstmf)  parm(&fil &o1map &dlib 'revipf    ' &wmm1 &sts)
             MONMSG     MSGID(IWS1611) EXEC(DO)
               SNDUSRMSG  MSG(&MELD)
               GOTO       CMDLBL(ENDPGM)
             ENDDO
/*Debug*/    if         cond(&sts *ne ' ') then(do)
                goto      cmdlbl(endpgm)
             enddo

/*  Backup av innlesningsfil                                               */
             chgvar    var(&fra)   value(&o1map *tcat '/' *cat &fil)
             rtvjoba   date(&dato)
             chgvar    var(&bfil)  value(&o1map *tcat '/Backup')
             crtdir     dir(&bfil)
             monmsg     msgid(cpf0000)
             chgvar    var(&bfil)  value(&o1map *tcat '/Backup/L_20' +
                                   *cat %sst(&dato 5 2) *cat '_' +
                                   *cat %sst(&dato 3 2) *cat '_' +
                                   *cat %sst(&dato 1 2) *cat '_')
             chgvar     var(&nn)  value(00)
bloop:       chgvar     var(&nn)  value(&nn + 1)
             chgvar     var(&xx)  value(&nn)
             chgvar     var(&xfil) value(&bfil *tcat &xx)
             mov        obj(&fra) toobj(&xfil)
             monmsg     msgid(cpfA0A0) exec(do)
               goto     cmdlbl(bloop)
             enddo
/* Danner postering                                                  */

             OVRDBF     FILE(REviPF) TOFILE(*LIBL/REviPF) MBR(&WMM1)
             OVRDBF     FILE(REUTPF) TOFILE(*LIBL/REUTPF) MBR(&WMM1)

             CALL       PGM(RE142R) PARM(&O1FGR &O1FRM)
/*           MONMSG     MSGID(CPF0000)            */

/* Danner postering                                                  */

             CALL       PGM(RE131R) PARM(&WMM1)
/*           MONMSG     MSGID(CPF0000)            */
             CHGVAR     VAR(&WFEIL) VALUE(%SST(*LDA 211 1))
             IF         COND(&WFEIL *EQ '1') THEN(DO)
             CHGVAR     VAR(&WALI) VALUE(4)
             CHGVAR     VAR(&WAPO) VALUE(2)
             CHGVAR     VAR(&MLD1) VALUE('Visma Lønn poster Payroll')
             CHGVAR     VAR(&MLD2) VALUE('Feil firmagruppe/firma!')
             CALL       PGM(AA006C)  PARM(&WALI &WAPO &MLD1 &MLD2)
             DLYJOB     DLY(05)
             ENDDO

 SLUTT:
             DLTOVR     FILE(*ALL)

 ENDPGM:     ENDPGM
