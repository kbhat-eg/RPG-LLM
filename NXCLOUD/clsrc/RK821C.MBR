             PGM        PARM(&LPARAM)

/* AUTOGIRO-poster til BBS                                           */
/* Initiering av parametre.                                          */

             DCL        VAR(&LPARAM) TYPE(*CHAR) LEN(120)
             DCL        VAR(&FFIR)  TYPE(*CHAR) LEN(3)
             DCL        VAR(&BTCH)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&WSID)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&USER)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&MEMBX) TYPE(*CHAR) LEN(5)
             DCL        VAR(&MEMB)  TYPE(*DEC)  LEN(5 0)
             DCL        VAR(&WMM1)  TYPE(*CHAR) LEN(10) VALUE('RKA      ')
             DCL        VAR(&WMM2)  TYPE(*CHAR) LEN(10) VALUE('RKB      ')
             DCL        VAR(&WMMA)  TYPE(*CHAR) LEN(10) VALUE('AUTO   ')
             DCL        VAR(&WMMB)  TYPE(*CHAR) LEN(10) VALUE('AUTOG   ')

/* Henter verdier i fra LDA.                                         */

             RTVDTAARA  DTAARA(*LDA (911 10)) RTNVAR(&USER)
             RTVDTAARA  DTAARA(*LDA (944 03)) RTNVAR(&FFIR)
             RTVDTAARA  DTAARA(*LDA (864 5))  RTNVAR(&MEMBX)

             CHGVAR     VAR(&MEMB) VALUE(&MEMBX)
             CHGVAR     VAR(%SST(&WMM1 4 5)) VALUE(&MEMB)
             CHGVAR     VAR(%SST(&WMM2 4 5)) VALUE(&MEMB)
             CHGVAR     VAR(%SST(&WMMA 5 3)) VALUE(&FFIR)
             CHGVAR     VAR(%SST(&WMMB 6 3)) VALUE(&FFIR)

/* Utplukking av poster                                              */

             CLRPFM     FILE(RKW4PF) MBR(&WMM1)
             MONMSG     MSGID(CPF3100)

/* Bygger membere dersom de ikke finnes                              */

             CHKOBJ     OBJ(RKW4PF) OBJTYPE(*FILE) MBR(&WMM1)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDPFM     FILE(RKW4PF) MBR(&WMM1) TEXT('Poster til +
                          autogiro-liste (RK819R)')
             ENDDO
             CHKOBJ     OBJ(RKW4PF) OBJTYPE(*FILE) MBR(&WMM2)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDPFM     FILE(RKW4PF) MBR(&WMM2) TEXT('Poster fra +
                          autogiro-liste (RK821R)')
             ENDDO
             CHKOBJ     OBJ(RKW4LU) OBJTYPE(*FILE) MBR(&WMM2)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDLFM     FILE(RKW4LU) MBR(&WMM2) DTAMBRS((RKW4PF +
                          (&WMM2))) TEXT('Poster fra autogiro-liste +
                          (RK821R)')
             ENDDO

             CHKOBJ     OBJ(RKWcL2) OBJTYPE(*FILE) MBR(&WMMb)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDLFM     FILE(RKWcl2) MBR(&WMMb) DTAMBRS((RKWCPF +
                          (&WMMb))) TEXT('Poster fra autogiro-liste +
                          (RK821R)')
             ENDDO

             OVRDBF     FILE(RKWCPF) MBR(&WMMB)
             OVRDBF     FILE(RKW4PF) MBR(&WMM1)
             OVRDBF     FILE(RKWCL2) TOFILE(*LIBL/RKWCL2) MBR(&WMMB)
             CALL       PGM(RK820R) PARM(&LPARAM)

             DLTOVR     FILE(RKW4PF)

/* Call av program, utskrift av autogiro-kontroll                    */

             CALL       PGM(AP601C) PARM('RK821P' &BTCH)

/* Call av program, utskrift av autogiro                             */

             OVRDBF     FILE(RKW4PF) MBR(&WMM1)
             OVRDBF     FILE(RBUNLU) MBR(&WMMA)
             OVRDBF     FILE(RTRAPF) MBR(&WMMA)
             OVRDBF     FILE(RKW4LU) MBR(&WMM2)

             CALL       PGM(RK821R) PARM(&LPARAM)

/*                                                                   */
/* Program for å fjerne overstyring av printerfile                   */

             CALL       PGM(AP606C) PARM('RK821P')

/* Oppdatering av transaksjonsregister                               */

             CALL       PGM(RK822R) PARM(&LPARAM)

/* Kall av rutine for overføring til QDLS                            */

             CALL       PGM(RK830C)

             DLTOVR     FILE(RKW4LU)
             DLTOVR     FILE(RKW4PF)
             DLTOVR     FILE(RBUNLU)
             DLTOVR     FILE(RTRAPF)
             RMVM       FILE(*LIBL/RKW4LU) MBR(&WMM2)
             RMVM       FILE(*LIBL/RKW4PF) MBR(&WMM1)
             RMVM       FILE(*LIBL/RKW4PF) MBR(&WMM2)
             RMVM       FILE(*LIBL/RKWAPF) MBR(&WMMA)
             RMVM       FILE(*LIBL/RKWCLU) MBR(&WMMB)
             monmsg     msgid(cpf5815)
             RMVM       FILE(*LIBL/RKWCL2) MBR(&WMMB)
             RMVM       FILE(*LIBL/RKWCPF) MBR(&WMMB)
             MONMSG     MSGID(CPF3200)
 END:        ENDPGM
