/*                  UniM    BUSINESS                                */
/*7.00  09.11.20 sst Uni Micro                                       */
/*                                                                   */
             PGM

             DCL        VAR(&FILN)  TYPE(*CHAR) LEN(20)
             DCL        VAR(&FILS)  TYPE(*CHAR) LEN(40)
             DCL        VAR(&MAPX) TYPE(*CHAR) LEN(30)
             DCL        VAR(&CLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&IFSV) TYPE(*CHAR) LEN(65)
             DCL        VAR(&FILT) TYPE(*CHAR) LEN(65)
             DCL        VAR(&FILG) TYPE(*CHAR) LEN(3)
             DCL        VAR(&BIBL) TYPE(*CHAR) LEN(6)
             DCL        VAR(&BAN1) TYPE(*CHAR) LEN(5) +
                          VALUE('/asp/')
             DCL        VAR(&BAN2) TYPE(*CHAR) LEN(10) +
                          VALUE('/UniMicro/')
             DCL        VAR(&BAN3) TYPE(*CHAR) LEN(10) +
                          VALUE('/qsys.lib/')
             DCL        VAR(&BAN4) TYPE(*CHAR) LEN(31) +
                          VALUE('.lib/UniMBut.file/UniMBut.mbr')
/* Henter filgruppe og firmanummer:                                  */

             RTVDTAARA  DTAARA(*LDA (931 3)) RTNVAR(&FILG)
             RTVDTAARA  DTAARA(*LDA (934 6)) RTNVAR(&BIBL)

/* Lager til-katalogen på IFS hvis den ikke finnes                   */

             CHGVAR     VAR(&MAPX)     VALUE(&BAN1 *CAT &FILG +
                                             *CAT &BAN2)
             CRTDIR     DIR(&MAPX)
             MONMSG     MSGID(CPF0000)

/* Tømmer transaksjonsfil før danning av transaksjoner               */

             CLRPFM     FILE(RV02PF)
             CLRPFM     FILE(BOBFUT)
             CLRPFM     FILE(BQBWPF)
             CPYF       FROMFILE(BOMKPF) TOFILE(*libl/BQBWPF) MBROPT(*ADD) +
                          FMTOPT(*MAP *DROP)

/* Kaller program for danning av transaksjoner                       */
             CALL       PGM(RB007R)
             CALL       PGM(RB008R)

             CALL       PGM(RB044R)    /*  Kunder  */
             CALL       PGM(RB049R)    /*  Salgsposteringer  */

/* Test om overføringsregister finnes - bygg evt.                    */

             CHKOBJ     OBJ(*LIBL/UniMBUT) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
             CRTPF      FILE(UniMBUT) RCDLEN(800) +
                          TEXT('Posteringer til UniMicro')           +
                          MAXMBRS(*NOMAX)
             ENDDO

/* Kopier data til overføringsfil                                    */

             CPYF       FROMFILE(*libl/RV02PF) +
                          TOFILE(*libl/UniMBUT) FROMMBR(RV02PF) +
                          MBROPT(*REPLACE) FMTOPT(*NOCHK)

             MONMSG     MSGID(CPF2817)   EXEC(GOTO CMDLBL(END))


/* Starter kopiering til integret filsystem                          */
             CALL       PGM(LO763R) PARM(&FILN)

   CHGVAR VAR(&FILN)    VALUE((%sst(&filn 1 7)) *cat '_' +
                         *cat (%sst(&FILN 10 6)) *cat '.txt')
   CHGVAR VAR(&FILS) VALUE(&BAN1 *CAT &FILG *CAT &BAN2 *tcat &FILN)
   CHGVAR VAR(&FILT) VALUE(&BAN3 *CAT &BIBL *CAT &BAN4)

             CPYTOSTMF  +
                          FROMMBR(&FILT) TOSTMF(&FILS) +
                          STMFOPT(*REPLACE) STMFCODPAG(*PCASCII)
             CHGAUT     OBJ(&FILS) USER(*PUBLIC) DTAAUT(*RWX) +
                          OBJAUT(*ALL)

 END:        ENDPGM
