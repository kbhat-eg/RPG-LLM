             PGM

/* Rekompilert pga endringer på fil ANBLPF - gir feilmelding 160913 bsa */
/* Initiering av variabler                                           */

             DCL        VAR(&FTPNAV) TYPE(*CHAR) LEN(10) +
                          VALUE('NBSKUNDE')
             DCL        VAR(&STATUS) TYPE(*CHAR) LEN(1)  VALUE(' ')
             DCL        VAR(&TILNAV) TYPE(*CHAR) LEN(20) VALUE(' ')
             DCL        VAR(&VISMLD) TYPE(*CHAR) LEN(1)  VALUE('1')
             DCL        VAR(&CURLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&RAPMBR) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CHRFIR) TYPE(*CHAR) LEN(3)
             DCL        VAR(&JOBUSR) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOBTYP) TYPE(*CHAR) LEN(1)

             DCLF       FILE(ADB/ANBLPF)
             MONMSG     MSGID(CPF2103)

/* Henter bruker og jobbtype                                         */

             RTVJOBA    USER(&JOBUSR)  TYPE(&JOBTYP)

/* Setter biblioteksliste og current library dersom batch-jobb       */

/*           IF         COND(&JOBTYP *EQ '0') THEN(DO)               */
/*           CALL       PGM(AA700R) PARM('ASP_fgr   ')               */
/*           ENDDO                                                   */

/* Leser nettbutikk-tabell                                           */

 LES:        RCVF
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

/* Setter local dataarea og *curlib                                  */

             CHGVAR     VAR(&CHRFIR) VALUE(&AOLFIR)
             CHGVAR     VAR(&CURLIB) VALUE('ADB' *TCAT &AOLFGR)

             CALL       PGM(AA720C) PARM(&AOLFGR)

             CHGDTAARA  DTAARA(*LDA (944 03)) VALUE(&CHRFIR)
             CHGDTAARA  DTAARA(*LDA (931 03)) VALUE(&AOLFGR)
             CHGDTAARA  DTAARA(*LDA (934 10)) VALUE(&CURLIB)

             CHGCURLIB  CURLIB(&CURLIB)

/* Oppretter rapportfil hvis den ikke allerede finnes                */

             CHKOBJ     OBJ(*CURLIB/NETKRAPP) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(CRTSRCPF FILE(NETKRAPP) +
                          RCDLEN(200) MBR(*FILE) TEXT('Rapportfil +
                          for nettkundegenerering') SIZE(*NOMAX))

             CHGVAR     VAR(&RAPMBR) VALUE('FIRM' *TCAT &CHRFIR)

/* Blanker ut gammel nettkunde rapport member                        */

             CLRPFM     FILE(*CURLIB/NETKRAPP) MBR(&RAPMBR)
             MONMSG     MSGID(CPF3141) EXEC(ADDPFM +
                          FILE(*CURLIB/NETKRAPP) MBR(&RAPMBR))

/* Blanker ut gammel nettkundefil                                    */

             CLRPFM     FILE(*CURLIB/NETKUND) MBR(NETKUND)
             MONMSG     MSGID(CPF3141) EXEC(ADDPFM +
                          FILE(*CURLIB/NETKUND) MBR(NETKUND))

/* Kjører generering av ny nettvarefil                               */

             OVRDBF     FILE(NETKRAPP) TOFILE(*CURLIB/NETKRAPP) +
                          MBR(&RAPMBR)
             CALL       PGM(NN700R) PARM(&STATUS)
             DLTOVR     FILE(NETKRAPP)
             IF         COND(&STATUS *EQ 'R') THEN(DO)
             SNDMSG     MSG('Noen nettkunder ble utelatt fra sending. +
                          Se rapport' *BCAT &RAPMBR *BCAT ' i ADB' +
                          *TCAT &AOLFGR *BCAT 'for detaljer.') +
                          TOUSR(&JOBUSR)
             ENDDO
             ELSE IF    COND(&STATUS *NE ' ') THEN(DO)
             SNDMSG     MSG('Nettbutikk finnes ikke for firma' *BCAT +
                          &CHRFIR *BCAT 'i filgruppe ADB' *TCAT +
                          &AOLFGR) TOUSR(&JOBUSR)
             GOTO       CMDLBL(END)
             ENDDO

             CHGVAR     VAR(&STATUS) VALUE(' ')

/* Overfører nettvarefil til NBS via FTP                             */

             CALL       PGM(AF700C) PARM(&FTPNAV &STATUS &TILNAV +
                          &JOBTYP)

/* Gir beskjed ved feil                                                 */

             IF         COND(&STATUS *NE ' ') THEN(SNDMSG MSG('Feil +
                          ved sending av kundefil for firma' *BCAT +
                          &CHRFIR *BCAT 'i filgruppe' *BCAT +
                          &AOLFGR) TOUSR(&JOBUSR))

ENDLES:      GOTO       CMDLBL(LES)

END:         ENDPGM
