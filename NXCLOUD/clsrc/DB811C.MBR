             PGM        PARM(&FIGR &FIRM &BKON &SKIN &SAIN +
                             &VDIN &VDMN &BDIN &BDMN &BKIN +
                             &BEIN &BRI1 &BRI2 &TXIN &TRIN +
                             &DOBB &TIME &NRF &ARF &TELL)

             DCL        VAR(&L1KO) TYPE(*CHAR)  LEN(2)

             DCL        VAR(&FIGR) TYPE(*CHAR)  LEN(3)
             DCL        VAR(&FIRM) TYPE(*DEC)   LEN(3 0)
             DCL        VAR(&BKON) TYPE(*DEC)   LEN(11 0)
             DCL        VAR(&BIBL) TYPE(*CHAR)  LEN(10)

             DCL        VAR(&SKIN) TYPE(*CHAR)  LEN(1)
             DCL        VAR(&SAIN) TYPE(*CHAR)  LEN(15)
             DCL        VAR(&VDIN) TYPE(*CHAR)  LEN(6)
             DCL        VAR(&VDMN) TYPE(*CHAR)  LEN(2)
             DCL        VAR(&BDIN) TYPE(*CHAR)  LEN(4)
             DCL        VAR(&BDMN) TYPE(*CHAR)  LEN(2)
             DCL        VAR(&BKIN) TYPE(*CHAR)  LEN(1)
             DCL        VAR(&BEIN) TYPE(*CHAR)  LEN(15)
             DCL        VAR(&BRI1) TYPE(*CHAR)  LEN(4)
             DCL        VAR(&BRI2) TYPE(*CHAR)  LEN(2)
             DCL        VAR(&TXIN) TYPE(*CHAR)  LEN(7)
             DCL        VAR(&TRIN) TYPE(*CHAR)  LEN(6)
             DCL        VAR(&DOBB) TYPE(*CHAR)  LEN(1)
             DCL        VAR(&TIME) TYPE(*DEC)   LEN(6 0)

             DCL        VAR(&WCURLI) TYPE(*CHAR) LEN(10)

             DCL        VAR(&NRF) TYPE(*DEC)  LEN(11 0)
             DCL        VAR(&ARF) TYPE(*CHAR) LEN(9)
             DCL        VAR(&TELL) TYPE(*DEC)  LEN(6 0)

/* Dersom innholdet i posisjon 1 til 2 i *LDA er "01" eller "TS",    */
/* så kjøres ASBANK sammen med Løsning/1.                            */

             RTVDTAARA  DTAARA(*LDA (1 2)) RTNVAR(&L1KO)

    IF    COND(&L1KO *EQ '01') THEN(GOTO L1)
    IF    COND(&L1KO *EQ 'TS') THEN(GOTO L1)

/* ASOKON:                                                           */

/* Henter *CURLIB, og "redigerer" deretter det nye:                  */

             RTVDTAARA  DTAARA(*LDA (934 10)) RTNVAR(&BIBL)
             CHGVAR     VAR(%SST(&BIBL 4 3)) VALUE(&FIGR)
             GOTO  CMDLBL(OVER)

 L1:

/* Løsning/1:                                                        */

             CHGVAR     VAR(%SST(&BIBL 1 3)) VALUE('ADB')
             CHGVAR     VAR(%SST(&BIBL 4 2)) VALUE(&L1KO)
             CHGVAR     VAR(%SST(&BIBL 6 1)) VALUE(' ')

 OVER:

/* Bygger Databasefiler (Dersom de ikke finnes) for dobbel retur.    */

             CHKOBJ     OBJ(&BIBL/DDKIPF) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
             CRTPF      FILE(&BIBL/DDKIPF) SRCFILE(*LIBL/QDDFSRC) +
                          SIZE(10000 10000 1000)
             ENDDO

/* Før logosk file bygges må gjeldende CURLIB mellomlagres:          */

             RTVJOBA    CURLIB(&WCURLI)

             CHKOBJ     OBJ(&BIBL/DDKIL1) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
             CHGCURLIB  CURLIB(&BIBL)
             CRTLF      FILE(&BIBL/DDKIL1) SRCFILE(QDDFSRC)
             ENDDO

             CHGCURLIB  CURLIB(&WCURLI)
             MONMSG     MSGID(CPF0000)

/* Overstyrer databasefile:                                          */

             OVRDBF     FILE(DDKIL1) TOFILE(&BIBL/DDKIL1)
             OVRDBF     FILE(DDKIPF) TOFILE(&BIBL/DDKIPF)
             OVRDBF     FILE(DBNKPF) TOFILE(&BIBL/DBNKPF)
             OVRDBF     FILE(DBNKL4) TOFILE(&BIBL/DBNKL4)
             OVRDBF     FILE(DDOBPF) TOFILE(&BIBL/DDOBPF)

/* Call av overføringsprogram til DBNKPF ev. DDOBPF / DDKIPF:        */

             CALL       PGM(DB811R) PARM(&FIRM &BKON &SKIN +
                          &SAIN &VDIN &VDMN &BDIN &BDMN +
                          &BKIN &BEIN &BRI1 &BRI2 &TXIN +
                          &TRIN &DOBB &TIME &NRF &ARF &TELL)

 ENDPGM:     ENDPGM
