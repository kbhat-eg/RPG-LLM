pgm          parm(&flib &tlib)

             dcl        var(&flib)  type(*char)  len(10)
             dcl        var(&tlib)  type(*char)  len(10)

             dclf       file(nxc_distr/nx_dspobjd)

             monmsg     msgid(cpf2123)    /*  Type objekt finnes ikke        */

             clrpfm     file(nxc_distr/nx_dspobjd)
/*  Overføring av objekter             _____________________________________ */
             dspobjd    obj(&flib/*all) objtype(*pgm) output(*outfile) +
                          outfile(nxc_distr/nx_dspobjd)
             dspobjd    obj(&flib/*all) objtype(*msgf) output(*outfile) +
                          outfile(nxc_distr/nx_dspobjd) +
                          outmbr(*first *add)
             dspobjd    obj(&flib/*all) objtype(*cmd) output(*outfile) +
                          outfile(nxc_distr/nx_dspobjd) +
                          outmbr(*first *add)
             dspobjd    obj(&flib/*all) objtype(*qrydfn) output(*outfile) +
                          outfile(nxc_distr/nx_dspobjd) +
                          outmbr(*first *add)

rcvf1:       rcvf
             monmsg     msgid(cpf0864) exec(goto Filer)
             dltobj     obj(&tlib/&odobnm) objtype(&odobtp)
             monmsg    msgid(cpf0000)
             crtdupobj obj(&odobnm) fromlib(&flib) +
                       objtype(&odobtp) tolib(&tlib)
             monmsg    msgid(cpf2105 cpf2130)
             goto      cmdlbl(rcvf1)

/*  Overføring av Filer og source__________________________________________ */
Filer:
             close                           /*  Avslutter 1 runde med les  */

             dspobjd    obj(&flib/*all) objtype(*file) output(*outfile) +
                          outfile(nxc_distr/nx_dspobjd)

rcvf:        rcvf
             monmsg     msgid(cpf0864) exec(goto end)
             if         cond(&odobtp *eq '*FILE'      *and +
                             &odobat *eq 'PF'         *and +
                             %sst(&odobnm 1 1) = 'Q'  *and +
                            (%scan('SRC' &ODOBNM) > 0 *OR +
                             %scan('DDF' &ODOBNM) > 0 *OR +
                             %scan('SQL' &ODOBNM) > 0)) THEN(DO)
               cpysrcf  fromfile(&flib/&odobnm) +
                        tofile(&tlib/&odobnm) +
                        frommbr(*all) +
                        text(*frommbr) +
                        srctype(*frommbr)
               monmsg   msgid(cpf2817 cpf2668)
             enddo
             else       cmd(do)
               dltobj    obj(&tlib/&odobnm) objtype(*file)
               monmsg    msgid(cpf0000)
               crtdupobj obj(&odobnm) fromlib(&flib) +
                         objtype(*file) tolib(&tlib)
             enddo
             goto       cmdlbl(rcvf)
end:         endpgm
