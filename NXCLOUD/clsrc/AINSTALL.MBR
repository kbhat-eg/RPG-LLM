
/* Automatisk installasjonsrutine,                                   */
/* som setter systemverdier.                                         */

             PGM        PARM(&SNAV &ASES &DATO &TIME)
             DCL        VAR(&SNAV)     TYPE(*CHAR) LEN(8)
             DCL        VAR(&ASES)     TYPE(*DEC)  LEN(3 0)
             DCL        VAR(&DATO)     TYPE(*CHAR) LEN(6)
             DCL        VAR(&DATX)     TYPE(*CHAR) LEN(8)
             DCL        VAR(&TIME)     TYPE(*CHAR) LEN(8)
             DCL        VAR(&TIMX)     TYPE(*CHAR) LEN(6)
             DCL        VAR(&AJOB)     TYPE(*DEC)  LEN(3 0)
             DCL        VAR(&TJOB)     TYPE(*DEC)  LEN(3 0)
             DCL        VAR(&LEVL)     TYPE(*CHAR) LEN(2)
             DCL        VAR(&OKOK)     TYPE(*CHAR) LEN(2)
             DCL        VAR(&NULL)     TYPE(*CHAR) LEN(1)
             DCL        VAR(&FGRP)     TYPE(*CHAR) LEN(3)
             CHGVAR     VAR(&NULL)     VALUE(' ')
             CHGVAR     VAR(&FGRP)     VALUE('   ')

             MONMSG     MSGID(CPF0000)

/* Kontrollere brukertilgang                                         */

             CHGVAR     VAR(&LEVL)     VALUE('40')
             CHGVAR     VAR(&OKOK)     VALUE('  ')
             CALL       PGM(AA050C)    PARM(&LEVL &OKOK)
             IF         COND(&OKOK *NE 'OK') THEN(DO)
             CALL       PGM(AA004C)
             GOTO       CMDLBL(END)
             ENDDO

/* Setter verdier i variablene                                       */

             CHGVAR     VAR(&DATX)         VALUE(&DATO)
             CHGVAR     VAR(&TIMX)         VALUE(&TIME)
             CHGSYSVAL  SYSVAL(QCTLSBSD)   VALUE(QCTL)
             CHGSYSVAL  SYSVAL(QTIME)      VALUE(&TIMX)
             CHGSYSVAL  SYSVAL(QDATE)      VALUE(&DATX)
             CHGSYSVAL  SYSVAL(QSECURITY)  VALUE('30')
             CHGVAR     &AJOB (40+&ASES)
             CHGVAR     &TJOB (5*&AJOB)
             CHGSYSVAL  SYSVAL(QACTJOB)    VALUE(&AJOB)
             CHGSYSVAL  SYSVAL(QADLACTJ)   VALUE(10)
             CHGSYSVAL  SYSVAL(QTOTJOB)    VALUE(&TJOB)
             CHGSYSVAL  SYSVAL(QADLTOTJ)   VALUE(10)
             CHGSYSVAL  SYSVAL(QAUTOCFG)   VALUE('0')
             CHGSYSVAL  SYSVAL(QAUTOVRT)   VALUE(20)
             CHGSYSVAL  SYSVAL(QDEVNAMING) VALUE(*NORMAL)
             CHGSYSVAL  SYSVAL(QINACTITV)  VALUE('300')
             CHGSYSVAL  SYSVAL(QLMTDEVSSN) VALUE('0')
             CHGSYSVAL  SYSVAL(QLMTSECOFR) VALUE('0')
             CHGSYSVAL  SYSVAL(QMAXSGNACN) VALUE('2')
             CHGSYSVAL  SYSVAL(QMAXSIGN)   VALUE('5')
             CHGSYSVAL  SYSVAL(QPWRRSTIPL) VALUE('0')
             CHGSYSVAL  SYSVAL(QSPCENV)    VALUE(*NONE)

/* Legger inn systemnavn                                             */

             CHGNETA    SYSNAME(&SNAV)

/* Bygger aktuelle biblioteker                                       */

             CRTLIB     LIB(ASP.SAVF) TEXT('ASP Savefile-bibliotek +
                          for prog.inst.')
             CRTLIB     LIB(ADB.SAVF) TEXT('ASP Savefile-bibliotek +
                          for nattrutiner.')
             CRTLIB     LIB(ASPGPLK) TEXT('ASP Driftsbibliotek +
                          (kundetilpasn.)')
             CRTLIB     LIB(ADB) TEXT('ASP Database-bibliotek for +
                          systemadministrasjon')

/* Bygger aktuelle source-filer                                      */

             CRTSRCPF   FILE(ASPGPLK/QS36PRC) RCDLEN(112) +
                          TEXT('Source data base file for S36 Proc')
             CRTSRCPF   FILE(ASPGPLK/QS36SRC) RCDLEN(112) +
                          TEXT('Source data base file for S36 Source')
             CRTSRCPF   FILE(ASPGPLK/QCLLESRC) RCDLEN(92) +
                          TEXT('Source data base file for CL  Source')

/* Legger opp systemdelen av bibliotekslista                         */

             CHGSYSVAL  SYSVAL(QSYSLIBL) VALUE('QSYS QSYS2 QHLPSYS +
                          QUSRSYS ASPGPLK ASPGPL ADB')

/* Bygge utkø for brukerprofil ASP                                   */

             CRTOUTQ    OUTQ(QGPL/ASP) TEXT('Utkø for brukerprofil +
                          ASP')

/* Bygge brukerprofil for ASP                                        */

             CRTUSRPRF  USRPRF(ASP) PASSWORD(ASP) USRCLS(*SECOFR) +
                          TEXT('Brukerprofil for ASP') +
                          SPCAUT(*ALLOBJ *JOBCTL *SPLCTL *SAVSYS +
                          *IOSYSCFG *SERVICE *SECADM) OUTQ(QGPL/ASP)

/* Bygge utkø for brukerprofil SYSOPR                                */

             CRTOUTQ    OUTQ(QGPL/SYSOPR) TEXT('Utkø for +
                          brukerprofil SYSOPR')

/* Bygge brukerprofil for SYSOPR                                     */

             CRTUSRPRF  USRPRF(SYSOPR) PASSWORD(SYSOPR) +
                          USRCLS(*SECADM) TEXT('Brukerprofil for +
                          meldings-håndtering') SPCAUT(*ALLOBJ +
                          *JOBCTL *SPLCTL *SAVSYS *SERVICE *SECADM) +
                          MSGQ(QSYSOPR) DLVRY(*BREAK) OUTQ(QGPL/SYSOPR)

/* Bygge utkø for nattjobb i ASPGPLK                                 */

             CRTOUTQ    OUTQ(ASPGPLK/ASPNATT) TEXT('Utkø for +
                          rapporter fra Nattjobben')


/* Etablerer nødvendige filer i ADB                                  */
/* ----------------------------------                                */

             CALL       PGM(AG010C)    PARM(&FGRP &NULL)
             CALL       PGM(AG012C)    PARM(&FGRP &NULL)
             CALL       PGM(AG018C)    PARM(&FGRP &NULL)
             CALL       PGM(AG020C)    PARM(&FGRP &NULL)
             CALL       PGM(AG022C)    PARM(&FGRP &NULL)
             CALL       PGM(AG024C)    PARM(&FGRP &NULL)
             CALL       PGM(AG026C)    PARM(&FGRP &NULL)
             CALL       PGM(AG040C)    PARM(&FGRP &NULL)
             CALL       PGM(AG042C)    PARM(&FGRP &NULL)
             CALL       PGM(AG044C)    PARM(&FGRP &NULL)
             CALL       PGM(AG046C)    PARM(&FGRP &NULL)
             CALL       PGM(AG050C)    PARM(&FGRP &NULL)

             PWRDWNSYS  OPTION(*IMMED) RESTART(*YES)

 END:        ENDPGM
