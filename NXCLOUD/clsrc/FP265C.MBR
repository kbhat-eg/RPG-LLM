             PGM

/* Initiering av parametere                                          */
/*     Dette er en mal - legg inn korrekte parametre og legg inn     */
/*     i ASKUND_xxx hos kunden. Repeter sekvens ved flere lager      */
/* NB Denne bør kjøres i eget subsystem !!!!!!                       */

             DCL        VAR(&LAGR) TYPE(*DEC) LEN(2 0)
             DCL        VAR(&ALLE) TYPE(*CHAR) LEN(1) VALUE(' ')
             DCL        VAR(&MAPP) TYPE(*CHAR) LEN(10) VALUE('FTP')
/*7.01*/     dcl        var(&full) type(*dec) len(1 0) value(2)
             DCL        VAR(&MEMB) TYPE(*CHAR) LEN(10) +
                          VALUE('AUTOHYLLE')
             DCL        VAR(&OVRF1) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OVRF2) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FNA1)  TYPE(*CHAR) LEN(20) VALUE(' ')
             DCL        VAR(&FNA2)  TYPE(*CHAR) LEN(20) VALUE(' ')
             DCL        VAR(&TOFI)  TYPE(*CHAR) LEN(35)
             DCL        VAR(&STAT)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&SMSG)  TYPE(*CHAR) LEN(1) VALUE('0')
             DCL        VAR(&USER)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&LGRP)  TYPE(*CHAR) LEN(3)
             DCL        VAR(&t)     TYPE(*CHAR) LEN(20)

             RTVDTAARA  DTAARA(*LDA (931 3))   RTNVAR(&LGRP)

/* Setter biblioteksliste og verdier ut i fra bruker       */

             RTVJOBA    USER(&USER)
             CALL       PGM(AA722C) PARM(&USER)

/* Legger inn hardkodet filgruppe og firma da jobben ikke kjører */
/* når annet firma en 000 er brukt og ikke satt tilbake etterpå  */

             CHGDTAARA  DTAARA(*LDA (931 3)) VALUE('xxx')
             CHGDTAARA  DTAARA(*LDA (944 3)) VALUE('000')

/* ---------- LAGER 10 BEHANDLES ----------                          */

ny_runde:    CHGVAR     VAR(&LAGR) VALUE(10)
             CHGVAR     VAR(&OVRF1) VALUE('PRICERDT10')
             CHGVAR     VAR(&OVRF2) VALUE('PRICERMG10')

/*           Sjekk arbeidsregister                                   */

             CHKOBJ     OBJ(VETXPF) OBJTYPE(*FILE) MBR(&MEMB)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDPFM     FILE(VETXPF) MBR(&MEMB) TEXT('Varer til +
                          hylleforkanter ')
             ENDDO
             CLRPFM     FILE(VETXPF) MBR(&MEMB)

/* Call av utplukksprogram                                           */

             OVRDBF     FILE(VETXPF) TOFILE(VETXPF) MBR(&MEMB)

/*7.01*/     CALL       PGM(FP265R) PARM(&LAGR &ALLE &full &STAT)

/* Dersom poster er lagt ut - kopieres data til overføringsfil       */

             IF         COND(&STAT *EQ '0') +
             THEN(GOTO CMDLBL(END10))

             CPYF       FROMFILE(*CURLIB/VETXPF) +
                          TOFILE(*CURLIB/PRICER) FROMMBR(AUTOHYLLE) +
                          TOMBR(PRICER) MBROPT(*REPLACE) FMTOPT(*NOCHK)

/* Starter FTP overføring til server                                 */

 FTP:        CALL       PGM(AF700C) PARM(&OVRF1 &STAT &FNA1 &SMSG)
             CALL       PGM(AF700C) PARM(&OVRF2 &STAT &FNA2 &SMSG)

 END10:      RMVM       FILE(*LIBL/VETXPF) MBR(&MEMB)


/* Så looper vi for automatisk ny overføring                         */

             rtvsysval  sysval(qtime) rtnvar(&t)
             if         cond(%substring(&T 1 4) *lt '1900') then(do)
                        dlyjob     dly(300)
                        goto  cmdlbl(ny_runde)
             enddo

             ENDPGM
