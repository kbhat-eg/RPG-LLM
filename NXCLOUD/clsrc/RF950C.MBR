
/*   Hovedbok, fjerning av poster/journal - parameter                */

             PGM

             DCL        VAR(&FGRP)     TYPE(*CHAR) LEN(3)
             DCL        VAR(&BIBL)     TYPE(*CHAR) LEN(6)
             DCL        VAR(&FIRM)     TYPE(*CHAR) LEN(3)
             DCL        VAR(&FNAV)     TYPE(*CHAR) LEN(30)
             DCL        VAR(&MLD1)     TYPE(*CHAR) LEN(40)
             DCL        VAR(&MLD2)     TYPE(*CHAR) LEN(40)
             DCL        VAR(&MLD3)     TYPE(*CHAR) LEN(50)
             DCL        VAR(&MLD4)     TYPE(*CHAR) LEN(50)
             DCL        VAR(&SVAR)     TYPE(*CHAR) LEN(1)
             DCL        VAR(&IN03)     TYPE(*CHAR) LEN(1)
             DCL        VAR(&IN12)     TYPE(*CHAR) LEN(1)
             DCL        VAR(&WALI)     TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&WAPO)     TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&BTCH) TYPE(*CHAR) LEN(1)

/* Hente inn filgruppe/ databibliotek fra LDA                        */

             RTVDTAARA  DTAARA(*LDA (931 03)) RTNVAR(&FGRP)
             RTVDTAARA  DTAARA(*LDA (934  6)) RTNVAR(&BIBL)
             RTVDTAARA  DTAARA(*LDA (944  3)) RTNVAR(&FIRM)
             RTVDTAARA  DTAARA(*LDA (951  30)) RTNVAR(&FNAV)

/* Call av parameterprogram, fjerning av journal                     */

             CALL       PGM(RF950R) PARM(&IN03)

             IF         COND(&IN03 *EQ '1') THEN(DO)
             GOTO       CMDLBL(ENDPGM)
             ENDDO

/* Varselbilde med bekreftelse på om jobb skal settes i gang         */

             CHGVAR     VAR(&MLD3) +
                        VALUE('                                   oppdateres')
             CHGVAR     VAR(&MLD4) +
                        VALUE('       Bekreft at dette er riktig !')
             CHGVAR     VAR(%SST(&MLD3 1 3)) VALUE(&FIRM)
             CHGVAR     VAR(%SST(&MLD3 5 30)) VALUE(&FNAV)

             CALL       PGM(AA007R)    PARM(&MLD3 &MLD4 &SVAR &IN12)

             IF         COND(&IN12 *EQ '1' ) +
                          THEN(GOTO CMDLBL(ENDPGM))

             IF         COND(&SVAR *EQ ' ' *OR &SVAR *EQ 'J') +
                          THEN(GOTO CMDLBL(VIDERE))
             ELSE       CMD(GOTO CMDLBL(ENDPGM))

VIDERE:

/* Call av program for utskrift av journal                           */

/* Program for printerstyring                                        */

             CALL       PGM(AP600R) PARM('RH966 ' '1' '0' &IN03 &BTCH)

             IF         COND(&IN03 *EQ '1') THEN(GOTO CMDLBL(ENDPGM))

/* Varselbilde 1                                                     */

             IF         COND(&BTCH *NE '1') THEN(DO)
             CHGVAR     VAR(&WALI) VALUE(4)
             CHGVAR     VAR(&WAPO) VALUE(2)
             CHGVAR     VAR(&MLD1) VALUE(' Hovedbok pr. journal-nr.  ')
             CHGVAR     VAR(&MLD2) VALUE(' ****  H O V E D B O K *** ')
             CALL       PGM(AA006C)  PARM(&WALI &WAPO &MLD1 &MLD2)
             ENDDO

/* Call av utskriftsprogram                                          */

             CALL PGM(RH965C)

/* Varselbilde 2                                                     */

             CHGVAR     VAR(&WALI) VALUE(4)
             CHGVAR     VAR(&WAPO) VALUE(34)
             CHGVAR     VAR(&MLD1) VALUE('Fjerner poster/journal. ')
             CHGVAR     VAR(&MLD2) VALUE('**** REGNSKAP       **** ')
             CALL       PGM(AA006C)  PARM(&WALI &WAPO &MLD1 &MLD2)

/* Call av program som sletter poster på oppgitt journal             */

             CALL       PGM(RF951C)

 ENDPGM:     ENDPGM
