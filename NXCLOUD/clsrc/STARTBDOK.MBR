             PGM        parm(&fgr)
             /*                                              */
             /*  Endret 03.02.2021 sst Parameterstyrt        */
             /*                                              */
             DCL        VAR(&FEIL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&MEMB)  TYPE(*CHAR) LEN(10) -
             value('BYGDOKxxx')
             DCL        VAR(&JOB)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&USR)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&NBR)   TYPE(*CHAR) LEN(6)
             DCL        VAR(&t)     TYPE(*CHAR) LEN(20)
             DCL        VAR(&mapp)  TYPE(*CHAR) LEN(23) -
             value('/home/ADBxxx/byggdok')
             DCL        VAR(&mappf) TYPE(*CHAR) LEN(25) -
             value('/home/ADBxxx/byggdok/*')
             DCL        VAR(&filt)  TYPE(*CHAR) LEN(10)  value('.xml')
             DCL        VAR(&fils)  TYPE(*CHAR) LEN(1)   value('B')
             dcl        var(&fgr)   type(*char) len(3)
             dcl        var(&clib)  type(*char) len(10) value('ADBxxx')

             chgvar     var(%sst(&memb  07 3)) value(&fgr)
             chgvar     var(%sst(&mapp  10 3)) value(&fgr)
             chgvar     var(%sst(&mappf 10 3)) value(&fgr)
             chgvar     var(%sst(&clib  04 3)) value(&fgr)

             CALL       PGM(AA720C) PARM(&fgr)

             CHGCURLIB  CURLIB(&clib)
/*                                                                   */
             RTVJOBA    JOB(&JOB) USER(&USR) NBR(&NBR)
/*                                                                   */
/*Danner arbeidsfil for spool                                        */
/*                                                                   */
crtbrk:      CLRPFM     FILE(*LIBL/IWBRK) MBR(&MEMB)
             MONMSG     MSGID(CPF3142) EXEC(DO)
             CRTPF      FILE(*CURLIB/IWBRK) RCDLEN(132) MAXMBRS(*NOMAX)
             GOTO       CMDLBL(CRTBRK)
                        ENDDO
             MONMSG     MSGID(CPF3141) EXEC(DO)
             ADDPFM     FILE(*LIBL/IWBRK) MBR(&MEMB)
             MONMSG     MSGID(CPF3213 CPF7306 CPA0701) EXEC(DO)
             CHGPF      FILE(*LIBL/IWBRK) MAXMBRS(*NOMAX)
             GOTO       CMDLBL(CRTBRK)
                        ENDDO
                        ENDDO

/*                                                                   */
             dsplnk     obj(&mappf) output(*print)
/*                                                                   */
/*Kopierer faktura fra spool til arbeidsregister                     */
/*                                                                   */
             CPYSPLF    FILE(QSYSPRT) TOFILE(*LIBL/IWBRK) +
                          JOB(&NBR/&USR/&JOB) SPLNBR(*LAST) +
                          TOMBR(&MEMB)
             monmsg     msgid(CPA0000 CPF0000)
/*                                                                   */
/*Sletter spoolfile                                                  */
/*                                                                   */
             dltsplf    file(qsysprt) splnbr(*last) select(&usr)
/*                                                                   */
/* STARTBDOK2                                                        */
/* Les alle forekomster og kj√∏r jobb som behandler filen             */
/*                                                                   */
/* override */
             OVRDBF     FILE(IWBRK) TOFILE(*LIBL/IWBRK) MBR(&MEMB)

             CALL       PGM(*LIBL/STARTBDOK2) PARM(&MAPPF &FILT +
                          &FILS &FEIL)
/*                                                                */
             DLTOVR     file(iwbrk)

             IF         COND(&FEIL *EQ '1') THEN(GOTO CMDLBL(ENDPGM))

             rtvsysval  sysval(qtime) rtnvar(&t)
             if         cond(%substring(&T 1 4) *lt '1900') then(do)
                        dlyjob     dly(300)
                        goto  cmdlbl(crtbrk)
             enddo
/*                                                                   */
 ENDPGM:     ENDPGM
