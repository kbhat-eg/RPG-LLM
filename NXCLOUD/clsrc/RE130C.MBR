/* 10.12.14  Program omskrevet til IFS                               */
             PGM

/* gjør så den som kjører får lov til å kjøre enkelte kommandoer */
             DCLPRCOPT  USRPRF(*OWNER)

             DCLF       FILE(RE130D) ALWVARLEN(*YES) ALWNULL(*YES)
/*                                                                   */
             DCL        VAR(&WMM1) TYPE(*CHAR) LEN(10) +
                          VALUE('LONN      ')
             DCL        VAR(&WALI)   TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&WAPO)   TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&WFEIL) TYPE(*CHAR) LEN(1) VALUE(' ')
             DCL        VAR(&MLD1)   TYPE(*CHAR) LEN(40)
             DCL        VAR(&MLD2)   TYPE(*CHAR) LEN(40)
             DCL        VAR(&FIL)    TYPE(*CHAR) LEN(16)
             DCL        VAR(&CLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FRM)   TYPE(*CHAR) LEN(3)
             DCL        VAR(&FILG) TYPE(*CHAR) LEN(3)
             DCL        VAR(&BIBL) TYPE(*CHAR) LEN(6)
             dcl        var(&bane) type(*char) len(30) +
                                                 value('lonn')
             dcl        var(&file) type(*char) len(30)
             dcl        var(&fili) type(*char) len(20) +
                                                 value('REINPF')
             dcl        var(&ovstmap) type(*char) len(36) value(' ')
             dcl        var(&ovstfil) type(*char) len(16) value(' ')
             dcl        var(&i)       type(*dec)  len(3 0)
             dcl        var(&mbrsz)   type(*dec)  len(10 0)
             dcl        var(&sts)     type(*char) len(8)

/* Henter filgruppe og firmanummer:                                  */

             RTVDTAARA  DTAARA(*LDA (934 6)) RTNVAR(&BIBL)
             RTVDTAARA  DTAARA(*LDA (931 3)) RTNVAR(&FILG)
             RTVDTAARA  DTAARA(*LDA (944 3)) RTNVAR(&FRM)

/* Hent opp ev tidligere overstyrt mappe til denne filgruppe     */
             rtvdtaara  dtaara(&bibl/LONMAP (1 36)) rtnvar(&ovstmap)
               monmsg     msgid(cpf1015) exec(do)
               crtdtaara  dtaara(lonmap) type(*char) len(64) +
                          text('Mappe hvor lønnsposteringer hentes +
                          fra')
               enddo
             rtvdtaara  dtaara(&bibl/LONMAP (37 16)) rtnvar(&ovstfil)
             if         cond(&ovstmap *ne ' ') then(do)
               chgvar     var(&bane)   value(&ovstmap)
             enddo
             chgvar     var(&o1map)  value(&bane)
             chgvar     var(&o1fil)  value(&ovstfil)

             CHGVAR     VAR(&O1FRM) VALUE(&FRM)
NyttF:
             SNDRCVF    RCDFMT(O1WIN)
             chgvar     var(&in41)     value('0')

             IF         COND(&IN03 *EQ '1') THEN(DO)
             GOTO       CMDLBL(ENDPGM)
             ENDDO
/*   Sjekk om mappe finnes                                           */

             chgvar     var(&i) value(36)
nextI:       if         cond(%sst(&o1map &i 1) *eq ' ') then(do)
               chgvar   var(&i) value(&i - 1)
               goto     cmdlbl(nextI)
               enddo
             if         cond(%sst(&o1map &i 1) *ne '/') then(do)
               chgvar   var(&o1feil)   value('Bane må avsluttes med /')
               chgvar   var(&in41)     value('1')
               goto     cmdlbl(nyttF)
               enddo
             rtvdirinf  dir(&o1map)
             monmsg     msgid(CPD1EDC CPF1ED7) exec(do)
               chgvar   var(&o1feil)   value('Oppgitt bane +
                                              finnes ikke')
               chgvar   var(&in41)     value('1')
               goto     cmdlbl(nyttF)
               enddo

/*           CHGVAR     VAR(&FLGR) VALUE(&O1FGR)                     */
             CHGVAR     VAR(&FRM)  VALUE(&O1FRM)
             CHGVAR     VAR(&FIL)  VALUE(&O1FIL)
             CHGVAR     VAR(&bane) VALUE(&O1map)
             chgvar     var(&file) value(&o1fil)
             CHGVAR     VAR(%SST(&WMM1 5 3))  VALUE(&FRM)
             chgdtaara  dtaara(lonmap (01 36)) value(&o1map)
             chgdtaara  dtaara(lonmap (37 16)) value(&o1fil)

             CHKOBJ     OBJ(*LIBL/&fili) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
             CHGVAR     VAR(&WALI) VALUE(4)
             CHGVAR     VAR(&WAPO) VALUE(2)
             CHGVAR     VAR(&MLD1) VALUE('H&L Lønn poster ==> ASOKON')
             CHGVAR     VAR(&MLD2) VALUE('Fil REINPF finnes ikke!')
             CALL       PGM(AA006C)  PARM(&WALI &WAPO &MLD1 &MLD2)
             DLYJOB     DLY(10)
             GOTO       CMDLBL(SLUTT)
             ENDDO

             CHKOBJ     OBJ(*LIBL/REUTPF) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
             CHGVAR     VAR(&WALI) VALUE(4)
             CHGVAR     VAR(&WAPO) VALUE(2)
             CHGVAR     VAR(&MLD1) VALUE('H&L Lønn poster ==> ASOKON')
             CHGVAR     VAR(&MLD2) VALUE('Fil REUTPF finnes ikke!')
             CALL       PGM(AA006C)  PARM(&WALI &WAPO &MLD1 &MLD2)
             DLYJOB     DLY(10)
             GOTO       CMDLBL(SLUTT)
             ENDDO

             RTVJOBA    CURLIB(&CLIB)

/* Bygger membere dersom de ikke finnes                              */
             CHKOBJ     OBJ(*LIBL/&fili) OBJTYPE(*FILE) MBR(&WMM1)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDPFM     FILE(*LIBL/&fili) MBR(&WMM1) TEXT('Poster +
                          fra H&L lønn')
                        ENDDO

             CHKOBJ     OBJ(*LIBL/REUTPF) OBJTYPE(*FILE) MBR(&WMM1)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDPFM     FILE(*LIBL/REUTPF) MBR(&WMM1) TEXT('Poster +
                          fra H&L lønn')
                        ENDDO

/* Sletter innholdet fra innlesingsfila                              */

             CLRPFM     FILE(&fili)  MBR(&WMM1)
             CLRPFM     FILE(REUTPF) MBR(&WMM1)

             call       pgm(aspfrstmf) parm(+
                            &file    +
                            &bane    +
                            &clib    +
                            &fili    +
                            &wmm1    +
                            &sts)
             rtvmbrd    file(*libl/&fili) mbr(&wmm1) +
                                           nbrcurrcd(&mbrsz)
             if         cond(&mbrsz *eq 0) then(do)
               goto     cmdlbl(slutt)
             enddo
             if         cond(&sts *eq 'TOM FIL') then(do)
               goto     cmdlbl(slutt)
             enddo

/* Danner postering                                                  */

             OVRDBF     FILE(&fili)  TOFILE(*LIBL/&fili)  MBR(&WMM1)
             OVRDBF     FILE(REUTPF) TOFILE(*LIBL/REUTPF) MBR(&WMM1)

             CALL       PGM(RE130R) PARM(&FILG &FRM)


/* Danner postering                                                  */

             CALL       PGM(RE131R) PARM(&WMM1)
             CHGVAR     VAR(&WFEIL) VALUE(%SST(*LDA 211 1))
             IF         COND(&WFEIL *EQ '1') THEN(DO)
             CHGVAR     VAR(&WALI) VALUE(4)
             CHGVAR     VAR(&WAPO) VALUE(2)
             CHGVAR     VAR(&MLD1) VALUE('H&L Lønn poster ==> ASOKON')
             CHGVAR     VAR(&MLD2) VALUE('Feil firmagruppe/firma!')
             CALL       PGM(AA006C)  PARM(&WALI &WAPO &MLD1 &MLD2)
             DLYJOB     DLY(05)
             ENDDO
/********************************************************************/

 SLUTT:
             DLTOVR     FILE(*ALL)

 ENDPGM:     ENDPGM
