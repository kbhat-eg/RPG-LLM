/* FDIR = Fra dir. Mappe på IFS, f.eks. /home/asp/mappe1             */+
/* TDIR = Til dir. Mappe på IFS.                                     */
/* DELE = Slette etter henting? 1=Ja, 0=Nei.                         */
/* REXT = Nytt etternavn etter kopi til backup mappe.                */
/* BIBL = Bibliotek der AS400 filen ligger.                          */
/* FILE = Navn på AS400 filen.                                       */
/* MEMB = Navn på member i AS400 filen.                              */
/* PATT = Mønster for hvilke filer som skal hentes. Eks. *.*         */
/* STAT = Status på jobb. Blank betyr OK.                            */

             PGM        PARM(&FDIR &TDIR &DELE &REXT &BIBL &FILE +
                          &MEMB &PATT &STAT)

/* Initiering av parametre.                                          */

             DCL        VAR(&FDIR) TYPE(*CHAR) LEN(50)
             DCL        VAR(&TDIR) TYPE(*CHAR) LEN(50)
             DCL        VAR(&DELE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&REXT) TYPE(*CHAR) LEN(10)
             DCL        VAR(&BIBL) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FILE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MEMB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PATT) TYPE(*CHAR) LEN(20)
             DCL        VAR(&STAT) TYPE(*CHAR) LEN(1)

             DCL        VAR(&JAVA) TYPE(*CHAR) LEN(1000)

             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             CHGVAR     VAR(&STAT) VALUE('1')

             CRTPF      FILE(ADB/ALOGPF) RCDLEN(200) MAXMBRS(*NOMAX) +
                          SIZE(*NOMAX)
             MONMSG     MSGID(CPF0000)
             ADDPFM     FILE(ADB/ALOGPF) MBR(IFSGET)
             MONMSG     MSGID(CPF0000) EXEC(DO)
             CLRPFM     FILE(ADB/ALOGPF) MBR(IFSGET)
             ENDDO

             CHGVAR     VAR(&STAT) VALUE('2')

             OVRDBF     FILE(STDOUT) TOFILE(ADB/ALOGPF) MBR(IFSGET)

             CHGVAR     VAR(&JAVA) VALUE('java -cp +
                          /java:/qibm/proddata/os400/jt400/lib/jt400n+
                          ative.jar asp.system.As400GetIFSFiles "' +
                          *TCAT &FDIR *TCAT '" "' *TCAT &TDIR *TCAT +
                          '" "' *TCAT &DELE *TCAT '" "' *TCAT &REXT +
                          *TCAT '" "' *TCAT &FILE *TCAT '" "' *TCAT +
                          &BIBL *TCAT '" "' *TCAT &MEMB *TCAT '" "' +
                          *TCAT &PATT *TCAT '"')
             QSH        CMD(&JAVA)
             DLTOVR     FILE(STDOUT)

             CHGVAR     VAR(&STAT) VALUE(' ')
 END:
             ENDPGM
