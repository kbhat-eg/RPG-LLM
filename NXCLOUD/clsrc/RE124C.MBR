             PGM        /* Poster fra Ergosoft Lønn */

/* Initiering av parametre.                                          */

             DCL        VAR(&WALI)   TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&WAPO)   TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&MLD1)   TYPE(*CHAR) LEN(40)
             DCL        VAR(&MLD2)   TYPE(*CHAR) LEN(40)
             DCL        VAR(&FFIR)   TYPE(*CHAR) LEN(3)
             DCL        VAR(&FAVD)   TYPE(*CHAR) LEN(4)
             DCL        VAR(&MEMB) TYPE(*CHAR) LEN(10) VALUE('PCINxxx')
             DCL        VAR(&LON1) TYPE(*CHAR) LEN(10) VALUE('LO2RExxx')

             dcl        var(&fradoc) type(*char) len(30)
             dcl        var(&framap) type(*char) len(30)
             dcl        var(&tillib) type(*char) len(10)
             dcl        var(&tilfil) type(*char) len(10)
             dcl        var(&tilmbr) type(*char) len(10)
             dcl        var(&fgr)    type(*char) len(3)

/* Henter verdier i fra LDA.                                         */

             RTVDTAARA  DTAARA(*LDA (944 03)) RTNVAR(&FFIR)
             RTVDTAARA  DTAARA(*LDA (357 04)) RTNVAR(&FAVD)
             CHGVAR     VAR(%SST(&MEMB 5 3)) VALUE(&FFIR)
             CHGVAR     VAR(%SST(&LON1 6 3)) VALUE(&FFIR)

/* Varselbilde 2                                                     */

             CHGVAR     VAR(&WALI) VALUE(4)
             CHGVAR     VAR(&WAPO) VALUE(44)
             CHGVAR     VAR(&MLD1) VALUE('Vennligst vent litt .....')
             CHGVAR     VAR(&MLD2) VALUE('Overføring pågår ........')
             CALL       PGM(AA006C)  PARM(&WALI &WAPO &MLD1 &MLD2)

/* Bygger membere dersom de ikke finnes                              */

             CHKOBJ     OBJ(RE0IPF) OBJTYPE(*FILE) MBR(&MEMB)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDPFM     FILE(RE0IPF) MBR(&MEMB) TEXT('Poster fra +
                          Ergosoft Lønn        ')
             ENDDO

             CHKOBJ     OBJ(RE0HPF) OBJTYPE(*FILE) MBR(&MEMB)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDPFM     FILE(RE0HPF) MBR(&MEMB) TEXT('Konti fra +
                          Ergosoft Lønn        ')
             ENDDO

             CHKOBJ     OBJ(RE0KPF) OBJTYPE(*FILE) MBR(&MEMB)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDPFM     FILE(RE0KPF) MBR(&MEMB) TEXT('Kunder fra +
                          Ergosoft Lønn        ')
             ENDDO

             CHKOBJ     OBJ(RE0LPF) OBJTYPE(*FILE) MBR(&MEMB)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDPFM     FILE(RE0LPF) MBR(&MEMB) TEXT('Leverandører +
                          fra Ergosoft Lønn        ')
             ENDDO

             CHKOBJ     OBJ(RE0PPF) OBJTYPE(*FILE) MBR(&MEMB)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDPFM     FILE(RE0PPF) MBR(&MEMB) TEXT('Prosjekter fra +
                          Ergosoft Lønn        ')
             ENDDO

             CHKOBJ     OBJ(RE0TPF) OBJTYPE(*FILE) MBR(&MEMB)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDPFM     FILE(RE0TPF) MBR(&MEMB) TEXT('Poster fra +
                          Ergosoft Lønn        ')
             ENDDO


             CLRPFM     FILE(RE0IPF) MBR(&MEMB)
             CLRPFM     FILE(RE0TPF) MBR(&MEMB)
             CLRPFM     FILE(RE0HPF) MBR(&MEMB)
             CLRPFM     FILE(RE0KPF) MBR(&MEMB)
             CLRPFM     FILE(RE0LPF) MBR(&MEMB)
             CLRPFM     FILE(RE0PPF) MBR(&MEMB)
             MONMSG     MSGID(CPF3100)

/* Flytt poster fra mappe til file                                   */

/*           CPYFRMPCD  FROMFLR(PCDATA) TOFILE(ADB/RE0IPF) +   */
/*                        FROMDOC(&LON1) TOMBR(&MEMB)          */
    /*       Poster fra Eksterne system */
             rtvdtaara  dtaara(*lda (931 03)) rtnvar(&fgr)
             chgvar     var(&framap)  value('/ASP/' *cat +
                                             &fgr   *cat +
                                             '/PCDATA')
             chgvar     var(&fradoc)  value(&lon1)
             chgvar     var(&tillib)  value('ADB')
             chgvar     var(&tilfil)  value('RE0IPF')
             chgvar     var(&tilmbr)  value(&memb)
             call       pgm(aspfrstmf) parm(+
                            &fradoc +
                            &framap +
                            &tillib +
                            &tilfil +
                            &tilmbr)
             MONMSG     MSGID(CPF0000)

/* Overføring av poster                                              */

             OVRDBF     FILE(RE0IPF) MBR(&MEMB)
             OVRDBF     FILE(RE0TPF) MBR(&MEMB)
             CALL       PGM(RE124R)
             DLTOVR     FILE(RE0IPF)
             DLTOVR     FILE(RE0TPF)

/*  Oppdatering av kontoplan                                         */

             OVRDBF     FILE(RE0HPF) MBR(&MEMB)
             CALL       PGM(RE109R)
             DLTOVR     FILE(RE0HPF)

/*  Oppdatering av kunder                                            */

             OVRDBF     FILE(RE0KPF) MBR(&MEMB)
             CALL       PGM(RE110R)
             DLTOVR     FILE(RE0KPF)

/*  Oppdatering av leverandører                                      */

             OVRDBF     FILE(RE0LPF) MBR(&MEMB)
             CALL       PGM(RE111R)
             DLTOVR     FILE(RE0LPF)

/*  Oppdatering av prosjekter                                        */

             OVRDBF     FILE(RE0PPF) MBR(&MEMB)
             CALL       PGM(RE112R)
             DLTOVR     FILE(RE0PPF)

/*  Tilrettelegger poster for oppdatering                            */

             OVRDBF     FILE(RE0TPF) MBR(&MEMB)

             CALL       PGM(RE115R) PARM(&MEMB)

             DLTOVR     FILE(RE0TPF)

/*  Fjern Ergosoft-file (LO2RExxx)                                   */

             DLTDLO     DLO(&LON1) FLR(PCDATA)
             monmsg     msgid(cpf8A16)

 END:        ENDPGM
