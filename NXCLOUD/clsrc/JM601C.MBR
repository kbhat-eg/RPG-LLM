             PGM        PARM(&WPLIST)

/* Initiering av parametre.                                          */

             DCL        VAR(&WPLIST)   TYPE(*CHAR) LEN(8)

             DCL        VAR(&DSPROG)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&DSRAPP)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&DSOVRS)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&DSFORM)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&DSOUTQ)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&DSACPIX)  TYPE(*CHAR) LEN(4)
             DCL        VAR(&DSACPI)   TYPE(*DEC)  LEN(4 0)
             DCL        VAR(&DSAKOPX)  TYPE(*CHAR) LEN(6)
             DCL        VAR(&DSAKOP)   TYPE(*DEC)  LEN(6 0)
             DCL        VAR(&DSHLDQ)   TYPE(*CHAR) LEN(4)
             DCL        VAR(&DSALINX)  TYPE(*CHAR) LEN(3)
             DCL        VAR(&DSALIN)   TYPE(*DEC)  LEN(3 0)
             DCL        VAR(&DSPRTY)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&DSOFLWX)  TYPE(*CHAR) LEN(3)
             DCL        VAR(&DSOFLW)   TYPE(*DEC)  LEN(3 0)
             DCL        VAR(&DSBTCH)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&DSWSID)   TYPE(*CHAR) LEN(10)

/* Henter verdier i fra LDA.                                         */

             RTVDTAARA  DTAARA(*LDA (800 10)) RTNVAR(&DSPROG)
             RTVDTAARA  DTAARA(*LDA (810 10)) RTNVAR(&DSOUTQ)
             RTVDTAARA  DTAARA(*LDA (820 10)) RTNVAR(&DSFORM)
             RTVDTAARA  DTAARA(*LDA (830 4)) RTNVAR(&DSACPIX)
             RTVDTAARA  DTAARA(*LDA (834 4)) RTNVAR(&DSHLDQ)
             RTVDTAARA  DTAARA(*LDA (838 6)) RTNVAR(&DSAKOPX)
             RTVDTAARA  DTAARA(*LDA (845 1)) RTNVAR(&DSOVRS)
             RTVDTAARA  DTAARA(*LDA (846 10)) RTNVAR(&DSRAPP)
             RTVDTAARA  DTAARA(*LDA (856 3)) RTNVAR(&DSALINX)
             RTVDTAARA  DTAARA(*LDA (859 1)) RTNVAR(&DSPRTY)
             RTVDTAARA  DTAARA(*LDA (860 3)) RTNVAR(&DSOFLWX)
             RTVDTAARA  DTAARA(*LDA (863 1)) RTNVAR(&DSBTCH)

/* Antall kopier og CPI m√• konverteres til numerisk felt.            */

             CHGVAR     VAR(&DSAKOP) VALUE(&DSAKOPX)
             CHGVAR     VAR(&DSACPI) VALUE(&DSACPIX)
             CHGVAR     VAR(&DSALIN) VALUE(&DSALINX)
             CHGVAR     VAR(&DSOFLW) VALUE(&DSOFLWX)

/* Overrider printfile                                               */

             IF         COND(&DSOVRS *NE ' ') THEN(DO)
             OVRPRTF    FILE(&DSRAPP) TOFILE(*FILE) DEV(&DSOUTQ) +
                          PAGESIZE(&DSALIN) CPI(&DSACPI) +
                          OVRFLW(&DSOFLW) OUTQ(&DSOUTQ) +
                          FORMTYPE(&DSFORM) COPIES(&DSAKOP) +
                          HOLD(&DSHLDQ) OUTPTY(&DSPRTY)
             ENDDO

/* Utskrift.                                                         */

             CALL       PGM(JM601R) PARM(&WPLIST)

/* Sender melding dersom bakgrunnsjobb                               */

             IF         COND(&DSBTCH *EQ '1') THEN(DO)
             RTVDTAARA  DTAARA(*LDA (901 10)) RTNVAR(&DSWSID)
             SNDBRKMSG  MSG('ASVADM: Utskrift av kampanje er +
                          ferdig') TOMSGQ(&DSWSID)
             ENDDO

 END:        ENDPGM
