
/* Dette program utfører stop/start av utkøer/printere i henhold     */
/* til parametere som er satt i filen AENHPF i biblioteket ADB.      */

 STRPGM:     PGM        PARM(&PRID &QMSG &FORM &TILK)

             DCL        VAR(&PRID)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&QMSG)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&FORM)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&TILK)     TYPE(*DEC)  LEN(1 0)

             DCL        VAR(&MLD1)     TYPE(*CHAR) LEN(50)
             DCL        VAR(&MLD2)     TYPE(*CHAR) LEN(50)
             DCL        VAR(&TXT1)     TYPE(*CHAR) LEN(50) +
                                       VALUE('           ; Ved PC +
                                       benytt frakoble / tilkoble,')
             DCL        VAR(&TXT2)     TYPE(*CHAR) LEN(50) +
                                       VALUE('Ved Axis nettboks, +
                                       slå av, vent, og slå på igjen')
             DCL        VAR(&SVAR)     TYPE(*CHAR) LEN(1) VALUE('J')
             DCL        VAR(&IN12)     TYPE(*CHAR) LEN(1)

/* Behandling ved tilkobling = 0                                     */
/* Twinax                                                            */

             IF         COND(&TILK *EQ 0)  THEN(DO)
             ENDWTR     WTR(&PRID)     OPTION(*IMMED)
             MONMSG     MSGID(CPF0000)
             DLYJOB     DLY(5)
             STRPRTWTR  DEV(&PRID)     OUTQ(&PRID) +
                                       FORMTYPE(&FORM &QMSG)
             MONMSG     MSGID(CPF0000)
             ENDDO

/* Behandling ved tilkobling = 1                                     */
/* PC/Axis nettboks                                                  */

             IF         COND(&TILK *EQ 1)  THEN(DO)
             CHGVAR     VAR(&MLD1)             VALUE(&TXT1)
             CHGVAR     VAR(%SST(&MLD1 01 10)) VALUE(&PRID)
             CHGVAR     VAR(&MLD2)             VALUE(&TXT2)
             CALL       PGM(AA007R)    PARM(&MLD1 &MLD2 &SVAR &IN12)
             MONMSG     MSGID(CPF0000)
             ENDDO

/* Behandling ved tilkobling = 2                                     */
/* NC/Std. nettboks                                                  */

             IF         COND(&TILK *EQ 2)  THEN(DO)
             ENDWTR     WTR(&PRID)     OPTION(*IMMED)
             MONMSG     MSGID(CPF0000)
             DLYJOB     DLY(5)
             STRRMTWTR  OUTQ(&PRID)    FORMTYPE(&FORM &QMSG)
             MONMSG     MSGID(CPF0000)
             ENDDO

             ENDPGM

