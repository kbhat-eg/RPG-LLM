
/*           Generere   Dette program kalles der hvor                */
/*                      det skal h√•ndteres :                         */
/*                      1 fysisk file   (PF)                         */
/*                      3 logiske filer (LU L1 L2)                   */
/*           NB ! Denne versjon har reusedlt = *NO                   */
/*                ellers er den helt lik AG102C                      */

 STRPGM:     PGM        PARM(&FGRP &NULL &DBIB +
                             &SBIB &RGBF &RGPF &RGLU &RGL1 +
                             &RGL2)

             DCL        VAR(&FGRP) TYPE(*CHAR) LEN(3)
             DCL        VAR(&NULL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&DBIB) TYPE(*CHAR) LEN(6)
             DCL        VAR(&SBIB) TYPE(*CHAR) LEN(6)

             DCL        VAR(&RGBF) TYPE(*CHAR) LEN(6)
             DCL        VAR(&RGPF) TYPE(*CHAR) LEN(6)
             DCL        VAR(&RGLU) TYPE(*CHAR) LEN(6)
             DCL        VAR(&RGL1) TYPE(*CHAR) LEN(6)
             DCL        VAR(&RGL2) TYPE(*CHAR) LEN(6)

/*           Sjekk, Finnes back-register, JA=Slett registeret        */

             CHKOBJ     OBJ(&DBIB/&RGBF) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(GOTO TAG002)
             DLTF       FILE(&DBIB/&RGBF)

/*          Sletter indexer / Legger tilside registeret              */

 TAG002:     CHKOBJ     OBJ(&DBIB/&RGPF) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(GOTO TAG003)

             DLTF       FILE(&DBIB/&RGLU)
             MONMSG     MSGID(CPF2105)
             DLTF       FILE(&DBIB/&RGL1)
             MONMSG     MSGID(CPF2105)
             DLTF       FILE(&DBIB/&RGL2)
             MONMSG     MSGID(CPF2105)

             RNMOBJ     OBJ(&DBIB/&RGPF) OBJTYPE(*FILE) +
                          NEWOBJ(&RGBF)

/*           Start danning av registre                               */

/* PF:PF                                                             */

 TAG003:     CRTPF      FILE(&DBIB/&RGPF) +
                          SRCFILE(*LIBL/QDDFSRC) SRCMBR(*FILE) +
                          MAXMBRS(*NOMAX) SIZE(*NOMAX) +
                          REUSEDLT(*NO)   SRTSEQ(*LANGIDUNQ) +
                          LANGID(NOR)
             MONMSG     MSGID(CPF7302)

/* LF:LU                                                             */

             CRTLF      FILE(&DBIB/&RGLU) +
                          SRCFILE(*LIBL/QDDFSRC) MAXMBRS(*NOMAX) +
                          SRTSEQ(*LANGIDUNQ)     LANGID(NOR)
             MONMSG     MSGID(CPF7302)

/* LF:L1                                                             */

             CRTLF      FILE(&DBIB/&RGL1) +
                          SRCFILE(*LIBL/QDDFSRC) MAXMBRS(*NOMAX) +
                          SRTSEQ(*LANGIDUNQ)     LANGID(NOR)
             MONMSG     MSGID(CPF7302)

/* LF:L2                                                             */

             CRTLF      FILE(&DBIB/&RGL2) +
                          SRCFILE(*LIBL/QDDFSRC) MAXMBRS(*NOMAX) +
                          SRTSEQ(*LANGIDUNQ)     LANGID(NOR)
             MONMSG     MSGID(CPF7302)

/*           Kopierer fra gammelt til nytt register                  */

             CHKOBJ     OBJ(&DBIB/&RGBF) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(GOTO TAG005)
             CPYF       FROMFILE(&DBIB/&RGBF) +
                          TOFILE(&DBIB/&RGPF) FROMMBR(*ALL) +
                          TOMBR(*FROMMBR) MBROPT(*ADD) FMTOPT(*MAP +
                          *DROP)


/*           Sjekk, Finnes back-register, J=Slett registeret         */

             CHKOBJ     OBJ(&DBIB/&RGBF) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(GOTO TAG005)
             DLTF       FILE(&DBIB/&RGBF)

/*           Sletter innholdet i registeret                          */

 TAG005:     IF         COND(&NULL *EQ 'S') THEN(CLRPFM +
                          FILE(&DBIB/&RGPF))

 ENDPGM:     ENDPGM
