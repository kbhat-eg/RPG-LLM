             PGM

             DCL        VAR(&JOBB)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&USER)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&SBIB)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&FNAV)     TYPE(*CHAR) LEN(30)
             DCL        VAR(&BFIL)     TYPE(*CHAR) LEN(1)
             DCL        VAR(&BFIR)     TYPE(*CHAR) LEN(1)
             DCL        VAR(&SW12)     TYPE(*CHAR) LEN(1)
             DCL        VAR(&LBIB)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&LFGR)     TYPE(*CHAR) LEN(2)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(ERR))
             RTVJOBA    JOB(&JOBB)

/* Hente inn fra LDA                                                 */

             RTVDTAARA  DTAARA(*LDA (911 10)) RTNVAR(&USER)
             RTVDTAARA  DTAARA(*LDA (934 10)) RTNVAR(&SBIB)
             RTVDTAARA  DTAARA(*LDA (951 30)) RTNVAR(&FNAV)
             RTVDTAARA  DTAARA(*LDA (981 01)) RTNVAR(&BFIL)
             RTVDTAARA  DTAARA(*LDA (982 01)) RTNVAR(&BFIR)
             RTVDTAARA  DTAARA(*LDA (1001 2)) RTNVAR(&LFGR)

/* Call til valg av file-gruppe dersom                               */
/* bruker ikke er sperret for dette.                                 */

             IF         COND(&BFIL *NE '1') THEN(DO)
             CALL       PGM(AA001R) PARM(&USER)

/* Avslutte dersom F3/F12 er benyttet                                */

             RTVDTAARA  DTAARA(*LDA (984 01)) RTNVAR(&SW12)
             IF         COND(&SW12 *EQ '1') THEN(GOTO CMDLBL(END))

/* Fjerne eksisterende databibliotek  Økonomi-ordre og lønn          */

             CHGVAR     VAR(&LBIB)     VALUE('LONDAT' *CAT &LFGR)
             RMVLIBLE   LIB(&LBIB)
             MONMSG     MSGID(CPF0000)
             RMVLIBLE   LIB(&SBIB)
             MONMSG     MSGID(CPF0000)

/* Endre Data-bibliotek for Økonomi/Ordre                            */

             RTVDTAARA  DTAARA(*LDA (934 10)) RTNVAR(&SBIB)
             IF         COND(&SBIB *NE ' ') THEN(DO)
             CHGCURLIB  CURLIB(&SBIB)
             ENDDO
             ENDDO

/* Call til valg av firmanummer dersom                               */
/* bruker ikke er sperret for dette.                                 */

             IF         COND(&BFIR *NE '1') THEN(DO)
             CALL       PGM(AA002R) PARM(&USER)
             ENDDO

/* Sjekk om denne filgruppe kjører Service                           */

             CHKOBJ     OBJ(&SBIB/IROIPF) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
             GOTO       CMDLBL(END)
             ENDDO

/* Sette riktig vareregister for ordre-systemet                      */

             CALL       PGM(IA001C)

/* Sette riktig data-bibliotek for 3P-Lønn                           */

             CALL       PGM(IL001C)

             GOTO       CMDLBL(END)
 ERR:        SNDPGMMSG  MSG('Det oppstod en feil, se melding +
                          i jobbloggen !!!') TOMSGQ(&JOBB)

 END:        ENDPGM
