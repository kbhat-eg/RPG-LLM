/* Beskrivelse av parametre                                         */
/*   DLIB = Databibliotek, filgruppe                                */
/*   FIRM = Firmanummer                                             */
/*   NUMM = Ordrenummer                                             */
/*   SUFF = Suffix for ordre                                        */
/*   STAT = Status. STAT=0 - Alt ok, STAT=1 - Feil oppstått         */
/*                                                                  */
             PGM        PARM(&DLIB &FIRM &NUMM &SUFF &STAT)

             DCL        VAR(&DLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FIRM) TYPE(*DEC) LEN(3 0)
/*6.nu*/     DCL        VAR(&NUMM) TYPE(*DEC) LEN(8 0)
             DCL        VAR(&SUFF) TYPE(*DEC) LEN(2 0)
             DCL        VAR(&STAT) TYPE(*CHAR) LEN(1)

             DCL        VAR(&FGRP) TYPE(*CHAR) LEN(3)
             DCL        VAR(&CFIR) TYPE(*CHAR) LEN(3)
/*6.nu*/     DCL        VAR(&CNUM) TYPE(*CHAR) LEN(8)

             DCL        VAR(&HEAD) TYPE(*CHAR) LEN(200)
             DCL        VAR(&TEXT) TYPE(*CHAR) LEN(512)
             DCL        VAR(&TTYP) TYPE(*CHAR) LEN(1)


/* Setter biblioteksliste                                            */

             CHGVAR     VAR(&FGRP) VALUE(%SUBSTRING(&DLIB 4 3))
             CHGVAR     VAR(&CFIR) VALUE(&FIRM)
             CHGVAR     VAR(&CNUM) VALUE(&NUMM)

             CALL       PGM(ASPINI/AA724C) PARM(&FGRP)

             CHGCURLIB  CURLIB(&DLIB)
             CHGDTAARA  DTAARA(*LDA (944 3)) VALUE(&CFIR)

/* Starter transaksjon (commitment control)                          */

             STRCMTCTL  LCKLVL(*CHG) CMTSCOPE(*JOB)
             MONMSG     MSGID(CPF8351)

/* Legg inn alt som skal skje under her.                             */
/* Ved feil i programmet SKAL man hoppe til ERR etter å ha satt      */
/* &STAT.                                                            */
/* Alle MONMSG skal evt hoppe til ERR!!!                             */
/* ================================================================= */

             CALL       PGM(*LIBL/XP990R) PARM(&FIRM &NUMM &SUFF &STAT)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(ERR))

             IF         COND(&STAT *NE ' ') THEN(DO)
             GOTO       CMDLBL(ERR)
             ENDDO

/* ================================================================ */
/* Kjører COMMIT på database oppdateringer.                         */

             COMMIT

             CHGVAR     VAR(&HEAD) VALUE('Ordre' *BCAT &CNUM *BCAT +
                          'er behandlet av XP990C')
             CHGVAR     VAR(&TEXT) VALUE(&HEAD)
             CHGVAR     VAR(&TTYP) VALUE(' ')

 END:
/* Skrur av commitment kontroll for jobben.                          */
/* Husk at standard handling er ROLLBACK hvis ikke COMMIT er kjørt.  */

             ENDCMTCTL

/* Kaller program for å gi beskjed om at programmet er kjørt.        */

             CALL       PGM(XP900C) PARM(&HEAD &TEXT &TTYP)

/* Kaller opp program for evt. overføring av ByggDok                 */
             CALL       PGM(FO788R)

             RETURN

 ERR:
             CHGVAR     VAR(&HEAD) VALUE('Ordre' *BCAT &CNUM *BCAT +
                          'feilet ved behandling i XP990C!!')
             CHGVAR     VAR(&TEXT) VALUE(&HEAD)
             CHGVAR     VAR(&TTYP) VALUE('E')

             ROLLBACK

/* Status SKAL være satt ved feil. E = generell feil                */

             IF         COND('X' *TCAT &STAT *EQ 'X') THEN(CHGVAR +
                          VAR(&STAT) VALUE('E'))

             GOTO       CMDLBL(END)
             ENDPGM
