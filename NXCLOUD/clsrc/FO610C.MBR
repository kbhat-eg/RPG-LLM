             PGM        PARM(&MEMB)

/*----------------------------------------------------------------------------*/
/*7.01* 150920 bsa Endrer loggnivå, da ved store faktureringer kan jobbloggen */
/*7.01*            gå full.                                                   */
/*                                                                            */
/*8.01* 160224 bsa Start overføring til Logiq                                 */
/*                                                                            */
/*                                                                            */
/*                                                                            */
/*----------------------------------------------------------------------------*/


             DCL        VAR(&KODE)     TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&ONOF)     TYPE(*DEC)  LEN(1 0)
             DCL        VAR(&MEMB)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&PROG)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&VLG1)     TYPE(*CHAR) LEN(01)
             DCL        VAR(&VLG2)     TYPE(*CHAR) LEN(01)

/*8.01*/     dcl        var(&fix)       type(*char) len(3)
/*8.01*/     dcl        var(&fir)       type(*dec)  len(3 0) value(0)
/*8.01*/     dcl        var(&lag)       type(*dec)  len(3 0) value(0)
/*8.01*/     dcl        var(&fgr)       type(*char) len(3)
/*8.01*/     DCL        VAR(&CO402_KEY) TYPE(*CHAR) LEN(50)
/*8.01*/     DCL        VAR(&CO402_V1)  TYPE(*CHAR) LEN(1)
/*8.01*/     DCL        VAR(&CO402_V2)  TYPE(*CHAR) LEN(2048)

             CHGDTAARA  DTAARA(*LDA (540 20)) +
                          VALUE('                    ')

/*8.01*/     CHGVAR     VAR(&CO402_KEY) VALUE('SAMLEFAKTURERE_DEBET_KREDIT')
/*8.01*/     rtvdtaara  dtaara(*lda (944 3)) rtnvar(&fix)
/*8.01*/     rtvdtaara  dtaara(*lda (931 3)) rtnvar(&fgr)
/*8.01*/     chgvar     var(&fir)   value(&fix)

             ADDPFM     FILE(FPSOPF)   MBR(&MEMB)
             ADDLFM     FILE(FPSOLU)   MBR(&MEMB) +
                          DTAMBRS((*CURRENT/FPSOPF (&MEMB)))
             ADDLFM     FILE(FPSOL1)   MBR(&MEMB) +
                          DTAMBRS((*CURRENT/FPSOPF (&MEMB)))

             OVRDBF     FILE(FPM2LU)   TOFILE(*FILE) MBR(&MEMB)
             OVRDBF     FILE(FPSOLU)   TOFILE(*FILE) MBR(&MEMB)
             OVRDBF     FILE(FPSOL1)   TOFILE(*FILE) MBR(&MEMB)

             CHGJOB     LOG(0 20)  +
                          /*7.01*/

/* Kaller opp program                                                */

             CALL       PGM(FO614R)

             RTVDTAARA  DTAARA(*LDA (540 01)) RTNVAR(&VLG1)
             RTVDTAARA  DTAARA(*LDA (541 01)) RTNVAR(&VLG2)
             RTVDTAARA  DTAARA(*LDA (871 10)) RTNVAR(&PROG)

/* Kaller opp program for utskrift   (Ikke Fakt/Kred)                */

             IF         COND(&VLG1 *EQ '1') THEN(DO)
             CALL       PGM(&PROG)     PARM(&MEMB)
             ENDDO

/* Kaller opp program for oppdatering/utskrift  (Fakt/Kred)          */

             IF         COND(&VLG1 *EQ '2') THEN(DO)
             CALL       PGM(FO616C)    PARM(&MEMB)
             ENDDO

/* Kaller opp program for utskrift   (Resting)                       */

             IF         COND(&VLG2 *EQ '1') THEN(DO)
             CALL       PGM(FORESC)    PARM(&MEMB)
             ENDDO

/* Kaller opp program for evt. overføring av ByggDok                 */
/* 7.00      CALL       PGM(FO788R)                                  */

/* Kaller opp program for sletting av ordre etter utplukk/utskrift   */

             CALL       PGM(FO618R)

             DLTOVR     FILE(FPM2LU FPSOLU FPSOL1)

             RMVM       FILE(FPM2LU)   MBR(&MEMB)
             RMVM       FILE(FPM2PF)   MBR(&MEMB)
             RMVM       FILE(FPSOL1)   MBR(&MEMB)
             RMVM       FILE(FPSOLU)   MBR(&MEMB)
             RMVM       FILE(FPSOPF)   MBR(&MEMB)

/* Oppdater jobb-register med jobb-ferdig-flagg                      */

             CHGVAR     VAR(&KODE)     VALUE(01)
             CHGVAR     VAR(&ONOF)     VALUE(0)
             CALL       PGM(FA720R)    PARM(&KODE &ONOF)

             CHGJOB     LOG(4 00) /*7.01*/

/* Kaller opp overføring til Logiq, hvis fakturering                 */

/*8.01*/     IF         COND(&VLG1 *EQ '2') THEN(DO)
/*8.01*/     CALL       PGM(CO402R) PARM(&FGR &FIR &LAG &CO402_KEY +
/*8.01*/                  &CO402_V1 &CO402_V2)

/*8.01*/     if         cond(&CO402_V1 *EQ '1' *and &CO402_V2 *EQ '2') then(do)
/*8.01*/     call       pgm(fo176r)
/*8.01*/     enddo
/*8.01*/     ENDDO

             ENDPGM
