             PGM

/* Inkasso - retur fra inkassobyrå                                   */
/*           Retur hentes nå med SFTP m/Mads sine rutiner            */
/*           Denne legger ut filene i \asp\xxx\FFFF\inkasso\inn      */
/*                                                                   */

             DCL        VAR(&IN03)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&IN12)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&BTCH)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&MLD1)  TYPE(*CHAR) LEN(40)
             DCL        VAR(&MLD2)  TYPE(*CHAR) LEN(40)
             DCL        VAR(&MLD3)  TYPE(*CHAR) LEN(50)
             DCL        VAR(&MLD4)  TYPE(*CHAR) LEN(50)
             DCL        VAR(&SVAR)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&WALI)  TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&WAPO)  TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&FIRM)  TYPE(*CHAR) LEN(3)
             DCL        VAR(&MBER)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&filg)  TYPE(*CHAR) LEN(3)
             DCL        VAR(&fras)  TYPE(*CHAR) LEN(80) +
                                    value('/asp/xxx/')
             DCL        VAR(&frasb) TYPE(*CHAR) LEN(80)
             DCL        VAR(&tils)  TYPE(*CHAR) LEN(80)
             DCL        VAR(&tilo)  TYPE(*CHAR) LEN(80)
             DCL        VAR(&tilb)  TYPE(*CHAR) LEN(80)
             DCL        VAR(&tilf)  TYPE(*CHAR) LEN(60)
             DCL        VAR(&tlib)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&tmbr)  TYPE(*CHAR) LEN(10) value('RCNRPF')
             DCL        VAR(&posx)  TYPE(*dec)  LEN(2 0) value(5)
             DCL        VAR(&posy)  TYPE(*dec)  LEN(2 0) value(7)
             DCL        VAR(&i)     TYPE(*dec)  LEN(3 0)
             DCL        VAR(&j)     TYPE(*dec)  LEN(3 0)
             DCL        VAR(&s)     TYPE(*dec)  LEN(3 0)
             DCL        VAR(&mbrsz) TYPE(*dec)  LEN(10 0)
             dcl        var(&selsk) type(*char) LEN(10)
             dcl        var(&ok)    type(*char) LEN(1)
/*  NX   */
             DCL        VAR(&CO402_KEY) TYPE(*CHAR) LEN(50)
             DCL        VAR(&CO402_V1)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&CO402_V2)  TYPE(*CHAR) LEN(2048)
             DCL        VAR(&ccsid)     TYPE(*CHAR) LEN(10)
             dcl        var(&fgr)       type(*char) len(3)
             dcl        var(&fix)       type(*char) len(3)
             dcl        var(&fir)       type(*dec)  len(3 0) value(0)
             dcl        var(&lag)       type(*dec)  len(3 0) value(0)
             dcl        var(&bryt)      type(*char) len(1)

             dclf       file(*libl/Hentrcnr)

             rtvdtaara  dtaara(*lda (931 03))  rtnvar(&filg)
             rtvdtaara  dtaara(*lda (934 10))  rtnvar(&tlib)
             chgvar     var(%sst(&fras 6 03))  value(&filg)

/*   Sjekk om melding om hvilke byrå som kjøres mot skal gis   */
             call       pgm(rcinkf_kvi)  parm(&bryt)
             if         cond(&bryt *eq 'J') then(goto cmdlbl(endpgm))

/*   Hent inkassofirma fra Statuskoder og bygg mappestruktur       */
             call       pgm(rc720r)    parm(&selsk)
/* ________________________________________________________________________ */
/*   Sjekk om Lindorf, da kjøres RC120c             */
 /*          if       cond(%sst(&selsk 1 7) *eq 'Lindorf') then(do) */
 /*            call   pgm(RC120C)                                   */
 /*            goto   cmdlbl(endpgm)                                */
 /*          enddo                                                  */
/* ________________________________________________________________________ */
             chgvar     var(&fras)     value(&fras *tcat &selsk +
                                             *tcat '/Inkasso/Inn/')
             chgvar     var(&tils)     value(&fras *tcat 'Backup')
             chgvar     var(&tilb)     value(&tils)
/*   Her finnes alle filer fra Svea,  Må ev deles i forskj. typer  */

             chgvar     var(&fras)      value(&fras *tcat '*')
             dsplnk     obj(&fras)  output(*print)
             monmsg     msgid(CPFA0A9) exec(do)
               chgvar   var(&mld1) value('Mappe eksisterer ikke å må bygges.')
               chgvar   var(&mld2) value(&fras)
               call     pgm(aa006c) parm(&posx &posy &mld1 &mld2)
               dlyjob   dly(5)
               goto     cmdlbl(endpgm)
             enddo

logok:
      cpysplf    file(qsysprt) tofile(*curlib/Hentrcnr) +
                   splnbr(*last) jobsysname(*only)
      monmsg     msgid(cpf9812) exec(do)
        crtpf    file(*curlib/Hentrcnr) rcdlen(132)
        goto     cmdlbl(logok)
      enddo

      dltsplf    file(qsysprt) splnbr(*last)

             clrpfm     file(*libl/rcnrtmp)
             monmsg     msgid(cpf3142) exec(do)
               crtpf    file(rcnrtmp) rcdlen(1024) size(*nomax)
             enddo
       clrpfm     file(*libl/rcnrpf)
             chgvar     var(&frasb) value(&fras)
             chgvar     var(&s)     value(%scan('*' &frasb))
             if         cond(&s *gt 0) then(do)
               chgvar     var(%sst(&frasb &s 1)) value(' ')
             enddo
rcv:         rcvf
             monmsg     msgid(cpf0864) exec(goto lesok)
             chgvar     var(&i)  value(0)
nexti:       chgvar     var(&i)  value(&i + 1)
             if         cond(&i < 45) then(do)
               if       cond(%sst(&hentrcnr &i 4) = '.txt') then(do)
                 goto   cmdlbl(kopier)
               enddo
               goto cmdlbl(nexti)
             enddo
             goto   cmdlbl(rcv)

kopier:      chgvar     var(&j)    value(&i + 2)
 /*          chgvar     var(%sst(&fras 27 &j)) +      */
 /*                     value(%sst(&hentrcnr 2 &j))   */
             chgvar     var(&fras) +
                        value(&frasb *tcat %sst(&hentrcnr 2 &j))

             chgvar     var(&tilf) value('/qsys.lib/' *tcat &tlib +
                          *tcat '.lib/rcnrtmp.file/' +
                          *tcat 'rcnrtmp.mbr')

/*      Henter CCSID fra NX   >INKASSO_CCSID<                        */
             CallSubr   subr(getccsid)

             cpyfrmstmf fromstmf(&fras) tombr(&tilf) mbropt(*replace) +
                         stmfccsid(&ccsid)

             chgvar     var(&tilo) value(&tilb *tcat '/' +
                                *tcat %sst(&hentrcnr 2 &j))
/*           if         cond(%sst(&fras 27 3) *eq 'Rec') then(do)       */
/*             chgvar   var(%sst(&tilo 34 4)) value('Rec_')             */
/*             chgvar   var(%sst(&tilo 38 2)) value(%sst(&fras 45 2))   */
/*             chgvar   var(%sst(&tilo 40 1)) value('_')                */
/*             chgvar   var(%sst(&tilo 41 2)) value(%sst(&fras 41 2))   */
/*             chgvar   var(%sst(&tilo 43 1)) value('_')                */
/*             chgvar   var(%sst(&tilo 44 2)) value(%sst(&fras 39 2))   */
/*             chgvar   var(%sst(&tilo 46 1)) value('_')                */
/*             chgvar   var(%sst(&tilo 47 7)) value(%sst(&fras 47 7))   */
/*           enddo                                                      */
/*           if         cond(%sst(&fras 27 3) *eq 'Rem') then(do)       */
/*             chgvar   var(%sst(&tilo 34 4)) value('Rem_')             */
/*             chgvar   var(%sst(&tilo 38 2)) value(%sst(&fras 48 2))   */
/*             chgvar   var(%sst(&tilo 40 1)) value('_')                */
/*             chgvar   var(%sst(&tilo 41 2)) value(%sst(&fras 44 2))   */
/*             chgvar   var(%sst(&tilo 43 1)) value('_')                */
/*             chgvar   var(%sst(&tilo 44 2)) value(%sst(&fras 42 2))   */
/*             chgvar   var(%sst(&tilo 46 1)) value('_')                */
/*             chgvar   var(%sst(&tilo 47 7)) value(%sst(&fras 50 7))   */
/*           enddo                                                      */
             mov        obj(&fras) toobj(&tilo)

             cpyf       fromfile(*libl/rcnrtmp) +
                        tofile(&tlib/rcnrpf) +
                        tombr(&tmbr) +
                        mbropt(*add) +
                        fmtopt(*nochk)
/*           cpyf       fromfile(*libl/rcnrtmp) +     */
/*                      tofile(&tlib/rcnrpf_akk) +    */
/*                      tombr(&tmbr) +                */
/*                      mbropt(*add) +                */
/*                      fmtopt(*nochk)                */
             goto       rcv

lesok:
/* Ble det lest inn noen transer?                                   */
             rtvmbrd    file(RCNRPF) mbr(*first) nbrcurrcd(&mbrsz)
             if         cond(&mbrsz *eq 0) then(do)
              CHGVAR     VAR(&WALI) VALUE(4)
              CHGVAR     VAR(&WAPO) VALUE(2)
              CHGVAR     VAR(&MLD1) VALUE('Henting av Retur fra ' *tcat &selsk)
              CHGVAR     VAR(&MLD2) VALUE('Det var ikke noe å hente')
              CALL       PGM(AA011C)  PARM(&WALI &WAPO &MLD1 &MLD2)
              goto       endpgm
             enddo

/* Tar backup av returfil før videre behandling                     */

             DLTF       FILE(RCNRPF_BAC)
             MONMSG     MSGID(CPF2105)

             CPYF       FROMFILE(RCNRPF_b4) TOFILE(*CURLIB/RCNRPF_B5) +
                          MBROPT(*REPLACE) CRTFILE(*YES)
             monmsg     msgid(cpf0000)
             CPYF       FROMFILE(RCNRPF_b3) TOFILE(*CURLIB/RCNRPF_B4) +
                          MBROPT(*REPLACE) CRTFILE(*YES)
             monmsg     msgid(cpf0000)
             CPYF       FROMFILE(RCNRPF_b2) TOFILE(*CURLIB/RCNRPF_B3) +
                          MBROPT(*REPLACE) CRTFILE(*YES)
             monmsg     msgid(cpf0000)
             CPYF       FROMFILE(RCNRPF_b1) TOFILE(*CURLIB/RCNRPF_B2) +
                          MBROPT(*REPLACE) CRTFILE(*YES)
             monmsg     msgid(cpf0000)
             CPYF       FROMFILE(RCNRPF) toFILE(*CURLIB/RCNRPF_B1) +
                          MBROPT(*REPLACE) CRTFILE(*YES)
             monmsg     msgid(cpf0000)

             rtvdtaara  dtaara(*lda (944 3)) rtnvar(&firm)
             chgvar     var(%sst(&mber 1 4)) value('INKA')
             chgvar     var(%sst(&mber 5 3)) value(&firm)
/* Oppdaterer forsendelse og genererer poster til regnskap           */


/* Program for printerstyring                                        */

             CALL       PGM(AP600R) PARM('RC662 ' '0' '0' &IN03 &BTCH)

             IF         COND(&IN03 *EQ '1') THEN(GOTO CMDLBL(ENDPGM))

/* Overstyring av printerfile                                        */

             CALL       PGM(AP601C)    PARM('RC662P' &BTCH)

             CALL       PGM(RC662R) PARM(&MBER &selsk)

             CALL       PGM(AP606C)    PARM('RC662P')

/*  Det kom ev. med innbetalinger og Oppdatering må kjøres    */
             rtvdtaara  dtaara(*lda (1 1)) rtnvar(&ok)
             if         cond(&ok *ne 'J') then(do)
               chgvar   var(&mld3) value('Filen inneholdt bare kvitteringer')
               chgvar   var(&mld4) value('Da trenger du ikke kjøre oppdatering')
               call     pgm(aa007r)  parm(&mld3 &mld4 &svar &in12)
               clrpfm   file(rcnrpf)
               goto     cmdlbl(endpgm)
             enddo

/* Tildeler periode                                                  */

             CALL       PGM(RF121R) PARM(&MBER)

/* Legg ut regnskapsposteringer                                      */

             CALL       PGM(RC664R) PARM(&MBER &MBER)

             CALL       PGM(RF702R) PARM(&MBER)  /* RJOULU */

/* Tømmer returfil etter endt behandling                            */

             CLRPFM     FILE(RCNRPF)

             chgvar     var(&mld1) value('Innlesning av retur fra ' +
                                          *tcat &selsk *tcat ' ok.')
             chgvar     var(&mld2) value('Overføring regnskap gjennstår.')
             call       pgm(aa011c) parm(&posx &posy &mld1 &mld2)

             GOTO       CMDLBL(ENDPGM)

 ERROR:

/* Varselbilde FEIL                                                  */

             CHGVAR     VAR(&MLD3) VALUE('Overføring fra Inkasso feilet')
             CHGVAR     VAR(&MLD4) VALUE('   Vennligst bekreft.     ')
             CALL       PGM(AA007R)  PARM(&MLD3 &MLD4 &SVAR &IN12)

/*___________________________________________________________________________*/
/*  Finn hvilke CCSID filen skal sendes med               */
Subr         Subr(GETCCSID)
             CHGVAR   VAR(&CO402_KEY) VALUE('INKASSO_CCSID')
             rtvdtaara dtaara(*lda (944 3)) rtnvar(&fix)
             rtvdtaara dtaara(*lda (931 3)) rtnvar(&fgr)
             chgvar    var(&fir)   value(&fix)
             CALL     PGM(CO402R) PARM(&FGR &FIR &LAG &CO402_KEY +
                          &CO402_V1 &CO402_V2)
             IF       COND(&CO402_V1 *EQ '1') THEN(do)
               chgvar  var(&Ccsid)  value(&co402_v2)
             enddo
             IF       COND(&Ccsid *EQ ' ') THEN(do)
               chgvar  var(&Ccsid)  value('*stmf')
             enddo
ENDSUBR
/*___________________________________________________________________________*/
 ENDPGM:     ENDPGM
