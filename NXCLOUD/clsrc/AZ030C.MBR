             PGM
             DCLF       FILE(AZ030D)
             DCL        VAR(&STS) TYPE(*CHAR) LEN(1) VALUE(' ')

/*  Legger ut skjermbilde med valg på bruker og bibliotek            */


 A1SEND:     SNDRCVF    RCDFMT(A1BLD)

/* F3 + F12                                                          */
             IF         COND(&IN03) THEN(GOTO CMDLBL(ENDPGM))
             IF         COND(&IN12) THEN(GOTO CMDLBL(ENDPGM))
             IF         COND(&STS *EQ 'B') THEN(GOTO CMDLBL(RSTF))
             IF         COND(&STS *EQ 'T') THEN(GOTO CMDLBL(ENDPGM))

/*  Sjekk om savefile på valgt bruker finnes - Clear eller bygg      */

             CLRSAVF    FILE(ASP.SAVF/&A1USR)
             MONMSG     MSGID(CPF9812) EXEC(CRTSAVF +
                              FILE(ASP.SAVF/&A1USR))


/*  Motta nettverksfil til valgt bruker                              */

             RCVNETF    FROMFILE(&A1USR) TOFILE(ASP.SAVF/&A1USR) +
                          USER(ASPDIST)
             MONMSG     MSGID(CPF8060) EXEC(GOTO CMDLBL(NESTE2))
             MONMSG     MSGID(CPF2204) EXEC(GOTO CMDLBL(NESTE2))
             GOTO       CMDLBL(RSTF)

 NESTE2:     RCVNETF    FROMFILE(&A1USR) TOFILE(ASP.SAVF/&A1USR) +
                          USER(ASPDIST2)
             MONMSG     MSGID(CPF8060) EXEC(GOTO CMDLBL(NESTE3))
             MONMSG     MSGID(CPF2204) EXEC(GOTO CMDLBL(NESTE3))
             GOTO       CMDLBL(RSTF)

 NESTE3:     RCVNETF    FROMFILE(&A1USR) TOFILE(ASP.SAVF/&A1USR) +
                          USER(ASPDIST3)
             MONMSG     MSGID(CPF8060) EXEC(GOTO CMDLBL(NESTE4))
             MONMSG     MSGID(CPF2204) EXEC(GOTO CMDLBL(NESTE4))
             GOTO       CMDLBL(RSTF)

 NESTE4:     RCVNETF    FROMFILE(&A1USR) TOFILE(ASP.SAVF/&A1USR) +
                          USER(ASPDIST4)
             MONMSG     MSGID(CPF8060) EXEC(GOTO CMDLBL(FEIL1))
             MONMSG     MSGID(CPF2204) EXEC(GOTO CMDLBL(FEIL1))
             GOTO       CMDLBL(RSTF)

/*  Distribusjon eller bruker ikke funnet                            */

 FEIL1:      CHGVAR     VAR(&A1MSG) VALUE('Feil i distribusjon - +
                          bruker ikke funnet eller ingenting mottatt')
             GOTO       CMDLBL(A1SEND)


/*  Gjenoppretter valgt bibliotek fra save-file                      */

 RSTF:       RSTLIB     SAVLIB(&A1LIB) DEV(*SAVF) SAVF(ASP.SAVF/&A1USR)
             MONMSG     MSGID(CPF3773)
             MONMSG     MSGID(CPF3743) EXEC(DO)
             CHGVAR     VAR(&A1MSG) VALUE('Oversendt bibliotek   +
                          er sendt med for høyt versjonsnummer    ')
             CHGVAR     VAR(&STS) VALUE('T')
             GOTO       CMDLBL(A1SEND)
             ENDDO

             MONMSG     MSGID(CPF3806) EXEC(DO)
             CHGVAR     VAR(&A1MSG) VALUE('Angitt bibliotek ikke +
                          funnet i distribusjon - prøv evt. på nytt')
             CHGVAR     VAR(&STS) VALUE('B')
             GOTO       CMDLBL(A1SEND)
             ENDDO

             MONMSG     MSGID(CPF3780) EXEC(DO)
             CHGVAR     VAR(&A1MSG) VALUE('Angitt bibliotek ikke +
                          funnet i distribusjon - prøv evt. på nytt')
             CHGVAR     VAR(&STS) VALUE('B')
             GOTO       CMDLBL(A1SEND)
             ENDDO

             MONMSG     MSGID(CPF3707) EXEC(DO)
             CHGVAR     VAR(&A1MSG) VALUE('Ingenting er mottatt -+
                          sjekk feillogg (DSPDSTLOG)')
             CHGVAR     VAR(&STS) VALUE('T')
             GOTO       CMDLBL(A1SEND)
             ENDDO

 ENDPGM:     ENDPGM
