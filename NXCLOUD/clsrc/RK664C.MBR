             PGM        PARM(&PROG)

/* Initiering av parametre.                                          */

             DCL        VAR(&PROG)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&RAPP)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&FIRM)  TYPE(*CHAR) LEN(3)
             DCL        VAR(&BTCH)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&MEMBX) TYPE(*CHAR) LEN(5)
             DCL        VAR(&MEMB)  TYPE(*DEC)  LEN(5 0)
             DCL        VAR(&BGIR)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&WSID)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&WALI)  TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&WAPO)  TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&MLD1)  TYPE(*CHAR) LEN(40)
             DCL        VAR(&MLD2)  TYPE(*CHAR) LEN(40)
             DCL        VAR(&WMMA)  TYPE(*CHAR) LEN(10) VALUE('RKA      ')
             DCL        VAR(&WMM1)  TYPE(*CHAR) LEN(10) VALUE('RENT    ')

/* Henter verdier i fra LDA.                                         */

             RTVDTAARA  DTAARA(*LDA (944 03)) RTNVAR(&FIRM)
             RTVDTAARA  DTAARA(*LDA (863 1))  RTNVAR(&BTCH)
             RTVDTAARA  DTAARA(*LDA (864 5))  RTNVAR(&MEMBX)
             RTVDTAARA  DTAARA(*LDA (881 1))  RTNVAR(&BGIR)

             CHGVAR     VAR(&MEMB) VALUE(&MEMBX)
             CHGVAR     VAR(%SST(&WMMA 4 5)) VALUE(&MEMB)
             CHGVAR     VAR(%SST(&WMM1 5 3)) VALUE(&FIRM)

             CHGVAR     VAR(&RAPP) VALUE(&PROG)
             CHGVAR     VAR(%SST(&RAPP 6 1)) VALUE(P)

             CHGVAR     VAR(%SST(&PROG 6 1)) VALUE(R)

/* Varselbilde 2                                                     */

             IF         COND(&BTCH *NE '1') THEN(DO)
             CHGVAR     VAR(&WALI) VALUE(4)
             CHGVAR     VAR(&WAPO) VALUE(44)
             CHGVAR     VAR(&MLD1) VALUE('Vennligst vent litt .....')
             CHGVAR     VAR(&MLD2) VALUE('Utskrift pågår ..........')
             CALL       PGM(AA006C)  PARM(&WALI &WAPO &MLD1 &MLD2)
             ENDDO

/* Bygger membere dersom de ikke finnes                              */

             CHKOBJ     OBJ(RKW2PF) OBJTYPE(*FILE) MBR(&WMM1)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDPFM     FILE(RKW2PF) MBR(&WMM1) TEXT('Poster fra +
                         Rentenota')
             ENDDO

             CHKOBJ     OBJ(RKW2LU) OBJTYPE(*FILE) MBR(&WMM1)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDLFM     FILE(RKW2LU) MBR(&WMM1) DTAMBRS((RKW2PF +
                          (&WMM1))) TEXT('Poster fra +
                          Rentenota')
             ENDDO

             CHKOBJ     OBJ(RKW2L1) OBJTYPE(*FILE) MBR(&WMM1)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDLFM     FILE(RKW2L1) MBR(&WMM1) DTAMBRS((RKW2PF +
                          (&WMM1))) TEXT('Poster fra +
                          Rentenota')
             ENDDO

             CHKOBJ     OBJ(RKWBL2) OBJTYPE(*FILE) MBR(&WMM1)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDLFM     FILE(RKWBL2) MBR(&WMM1) DTAMBRS((RKWBPF +
                          (&WMM1))) TEXT('Poster fra +
                          Rentenota')
             ENDDO

             CHKOBJ     OBJ(RKWBLU) OBJTYPE(*FILE) MBR(&WMM1)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDLFM     FILE(RKWBLU) MBR(&WMM1) DTAMBRS((RKWBPF +
                          (&WMM1))) TEXT('Poster fra +
                          Rentenota')
             ENDDO

/* Bygger membere dersom de ikke finnes                              */



             CHKOBJ     OBJ(RGIRPF) OBJTYPE(*FILE) MBR(&WMMA)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDPFM     FILE(RGIRPF) MBR(&WMMA) TEXT('Poster til +
                          utskrift av giro  (RK664R)')
             ENDDO

             CLRPFM     FILE(RKW2PF) MBR(&WMM1)
             CLRPFM     FILE(RGIRPF) MBR(&WMMA)

/* Overstyring av printerfile                                        */

             CALL       PGM(AP601C)    PARM(&RAPP &BTCH)

/* Utskrift av kontrolliste                                          */

             OVRDBF     FILE(RKWBL2) MBR(&WMM1)
             OVRDBF     FILE(RKWBLU) MBR(&WMM1)
             OVRDBF     FILE(RKW2PF) MBR(&WMM1)
             OVRDBF     FILE(RGIRPF) MBR(&WMMA)

/* Sjekk om sum rente < rentegrense etter vedlikehold                */

             CALL       PGM(RK668R)

/* Skriv ut rentenota                                                */

             CALL       PGM(&PROG) PARM(&WMM1)

             CALL       PGM(AP606C)    PARM(&RAPP)

/* Egen giro skal skrives ?!?                                        */

             IF         COND(&BGIR *EQ '1') THEN(DO)
             CALL       PGM(RK617C)
             ENDDO

/* Oppdater rentedato i kundereskontro                               */

             CALL       PGM(RK669R)

             DLTOVR     FILE(RGIRPF)
             DLTOVR     FILE(RKWBL2)
             DLTOVR     FILE(RKWBLU)
             DLTOVR     FILE(RKW2PF)

             RMVM       FILE(*LIBL/RGIRPF) MBR(&WMMA)
             RMVM       FILE(*LIBL/RKWBLR) MBR(&WMM1)
             MONMSG     MSGID(CPF7310)
             RMVM       FILE(*LIBL/RKWBLU) MBR(&WMM1)
             MONMSG     MSGID(CPF7310)
             RMVM       FILE(*LIBL/RKWBL1) MBR(&WMM1)
             MONMSG     MSGID(CPF7310)
             RMVM       FILE(*LIBL/RKWBL2) MBR(&WMM1)
             MONMSG     MSGID(CPF7310)
             RMVM       FILE(*LIBL/RKWBL3) MBR(&WMM1)
             MONMSG     MSGID(CPF7310)
             RMVM       FILE(*LIBL/RKWBPF) MBR(&WMM1)
             MONMSG     MSGID(CPF7310)

             MONMSG     MSGID(CPF3200)

/* Melding dersom bakgrunnsjobb                                      */

             IF         COND(&BTCH *EQ '1') THEN(DO)
             RTVDTAARA  DTAARA(*LDA (901 10)) RTNVAR(&WSID)
             SNDBRKMSG  MSG('ASOKON: Rutinen for utskrift av rente+
                        nota er ferdig') TOMSGQ(&WSID)
             ENDDO

 END:        ENDPGM
