             PGM        PARM(&PARM)

/* Initiering av parametre for bygging av S/36 filenavn dersom       */
/* MDLØNN:                                                           */

             DCL        VAR(&FILG)   TYPE(*CHAR) LEN(3)
             DCL        VAR(&FIRM)   TYPE(*CHAR) LEN(3)
             DCL        VAR(&S36F)   TYPE(*CHAR) LEN(8)

/* Initiering av parametre for bygging av S/36 filenavn dersom       */
/* 3PLØNN:                                                           */

             DCL        VAR(&L12) TYPE(*CHAR) LEN(1) VALUE('0')
             DCL        VAR(&WCURLI) TYPE(*CHAR) LEN(10)

/* Initiering av parametre.                                          */

             DCL        VAR(&PARM)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&IN03)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&IN12) TYPE(*DEC) LEN(1 0) VALUE(0)

             DCL        VAR(&PROG) TYPE(*CHAR) LEN(10) +
                          VALUE('GL100R')

             DCL        VAR(&RAPP) TYPE(*CHAR) LEN(10) +
                          VALUE('GL100P')

             DCL        VAR(&VALG) TYPE(*CHAR) LEN(1) VALUE('0')

             DCL        VAR(&USER)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&BFNR) TYPE(*DEC) LEN(3 0) VALUE(000)

             DCL        VAR(&OVRS)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&FORM)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&OUTQ)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&ACPIX)  TYPE(*CHAR) LEN(4)
             DCL        VAR(&ACPI)   TYPE(*DEC)  LEN(4 0)
             DCL        VAR(&AKOPX)  TYPE(*CHAR) LEN(6)
             DCL        VAR(&AKOP)   TYPE(*DEC)  LEN(6 0)
             DCL        VAR(&HLDQ)   TYPE(*CHAR) LEN(4)
             DCL        VAR(&ALINX)  TYPE(*CHAR)  LEN(3)
             DCL        VAR(&ALIN)   TYPE(*DEC)  LEN(3 0)
             DCL        VAR(&PRTY)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&OFLWX)  TYPE(*CHAR)  LEN(3)
             DCL        VAR(&OFLW)   TYPE(*DEC)  LEN(3 0)
             DCL        VAR(&BTCH)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&LONN)   TYPE(*CHAR) LEN(1)

/* LDA skal ved oppkall av denne rutinen inneholde en verdi i        */
/* posisjon 844-844 for å fortelle om formularparametre skal         */
/* vises på skjermen (verdi="1") før de legges ut i LDA, eller       */
/* legges direkte ut i LDA fra formularregister (verdi= " ").        */

/* Oppdaterer LDA med navn på prinfile og kode for om formular-      */
/* parametre skal endres.                                            */

             CHGDTAARA  DTAARA(*LDA (800 10)) VALUE(&PROG)
             CHGDTAARA  DTAARA(*LDA (844 1)) VALUE(&VALG)
             CHGDTAARA  DTAARA(*LDA (846 10)) VALUE(&RAPP)

/* Oppkall av utskriftsparametre.                                    */

/*           CALL       PGM(AP100R) PARM(&IN03 &IN12) */

             CALL       PGM(AP600R) PARM('GL100' '1' '0' &IN03 &BTCH)

/* Dersom F3 eller F12 skal ikke rapporten skrives                   */

             IF         COND(&IN03 *EQ '1') THEN(GOTO CMDLBL(END))

/* Henter verdier i fra LDA.                                         */

             RTVDTAARA  DTAARA(*LDA (911 10)) RTNVAR(&USER)
             RTVDTAARA  DTAARA(*LDA (800 10)) RTNVAR(&PROG)
             RTVDTAARA  DTAARA(*LDA (810 10)) RTNVAR(&OUTQ)
             RTVDTAARA  DTAARA(*LDA (820 10)) RTNVAR(&FORM)
             RTVDTAARA  DTAARA(*LDA (830 4)) RTNVAR(&ACPIX)
             RTVDTAARA  DTAARA(*LDA (834 4)) RTNVAR(&HLDQ)
             RTVDTAARA  DTAARA(*LDA (838 6)) RTNVAR(&AKOPX)
             RTVDTAARA  DTAARA(*LDA (845 1)) RTNVAR(&OVRS)
             RTVDTAARA  DTAARA(*LDA (846 10)) RTNVAR(&RAPP)
             RTVDTAARA  DTAARA(*LDA (856 3)) RTNVAR(&ALINX)
             RTVDTAARA  DTAARA(*LDA (859 1)) RTNVAR(&PRTY)
             RTVDTAARA  DTAARA(*LDA (860 3)) RTNVAR(&OFLWX)
             RTVDTAARA  DTAARA(*LDA (863 1)) RTNVAR(&BTCH)

/* ANTALL KOPIER, LINJER og CPI må konverteres til numerisk felt.    */

             CHGVAR     VAR(&AKOP) VALUE(&AKOPX)
             CHGVAR     VAR(&ALIN) VALUE(&ALINX)
             CHGVAR     VAR(&OFLW) VALUE(&OFLWX)
             CHGVAR     VAR(&ACPI) VALUE(&ACPIX)

/* Tester om printfile skal overrides.                               */

             IF         COND(&OVRS *EQ ' ') THEN(GOTO +
                          CMDLBL(NOTOVRPRTF))

/* Endrer printfile ut i fra nye verdier.                            */

             OVRPRTF    FILE(&RAPP) TOFILE(*FILE) DEV(&OUTQ) +
                          PAGESIZE(&ALIN) CPI(&ACPI) +
                          OVRFLW(&OFLW) OUTQ(&OUTQ) +
                          FORMTYPE(&FORM) COPIES(&AKOP) +
                          HOLD(&HLDQ) OUTPTY(&PRTY)

NOTOVRPRTF:

/* Går videre til kall av rutine GL100R dersom IKKE lønnskjøring:    */

             IF         COND(&PARM *NE 'L') THEN(GOTO +
                          CMDLBL(VIDERE))

/* Henter prefiks for å bygge S/36-filnavn det skal testes på        */
/* om eksisterer.  Det er denne filen som inneholder lønns-          */
/* transaksjoner som skal overføres for MDLØNN:                      */

             RTVDTAARA  DTAARA(*LDA (931 3)) RTNVAR(&FILG)
             RTVDTAARA  DTAARA(*LDA (944 3)) RTNVAR(&FIRM)

             CHGVAR     VAR(%SST(&S36F 1 1)) VALUE('K')
             CHGVAR     VAR(%SST(&S36F 2 3)) VALUE(&FILG)
             CHGVAR     VAR(%SST(&S36F 5 4)) VALUE('.BET')

/* Henter opp i fra faste opplysninger om det skal kjøres            */
/* MDLØNN eller 3PLØNN.  Parameter LONN vil inneholde "M"          */
/* ved MDLØNN og "3" ved 3PLØNN:                                     */

             CALL       PGM(GL103R) PARM(&LONN)

/* Går henholvis til MDLØNN eller 3PLØNN, avhengig av vilket       */
/* system som det kjøres på:                                         */

             IF         COND(&LONN *EQ 'M') THEN(GOTO +
                          CMDLBL(MDLONN))

/* 3PLØNN:                                                           */
/* *******                                                           */

/* Setter riktig bibliotek for hvor lønnsfil er lagret i 3PLØNN:     */

             RTVJOBA    CURLIB(&WCURLI)
             CALL       PGM(LON) PARM(&L12)

/* Henter inn lønnstransaksjoner fra 3PLØNN.                         */

/* Varabel PARM får verdien "F" dersom file ikke finnes, og        */
/* feilmelding blir sendt opp i GL100R.                              */

             CHGVAR     VAR(&PARM) VALUE('F')

             CHKOBJ     OBJ(*LIBL/LONBFPF) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(GOTO CMDLBL(OVERA))

/* Bygger hjelpefile for å ta vare på sist lønnskjøring fra 3P:      */

/* FINNES IKKE...:                                                   */

             CHKOBJ     OBJ(GBACPF) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
             CPYF       FROMFILE(LONBFPF) TOFILE(&WCURLI/GBACPF) +
                          CRTFILE(*YES)
             MONMSG     MSGID(CPF0000)
             GOTO CMDLBL(OVERX)
             ENDDO

/* FINNES...:                                                        */

             CPYF       FROMFILE(LONBFPF) TOFILE(&WCURLI/GBACPF) +
                          MBROPT(*REPLACE) CRTFILE(*NO)
             MONMSG     MSGID(CPF0000)
OVERX:

/* OK!  File med lønnstransaksjoner kopierers til riktig file:       */

             CHGVAR     VAR(&PARM) VALUE('L')

/* Tømmer GLONPF:                                                    */

             CLRPFM     FILE(&WCURLI/GLONPF)
             MONMSG     MSGID(CPF0000)

/* CURLIB inneholder nå 3PLØNN biblioteket.  ADBxxx biblioteket      */
/* må nå legges først for å få tilslag på riktig file:               */

             RMVLIBLE   LIB(&WCURLI)
             MONMSG     MSGID(CPF0000)
             ADDLIBLE   LIB(&WCURLI) POSITION(*FIRST)
             MONMSG     MSGID(CPF0000)

             CALL       PGM(GL150R) PARM(&FILG &FIRM) /* Flytter +
                          poster fra LONBFPF til GLONPF. Det ryddes +
                          samtidig opp i LONBFPF og LNBXPF. */

OVERA:

/*   Bygger opp igjen "*CURRENT LIBRARY" (FØR 3PLØNN)                */

             IF    COND(&WCURLI *NE '*NONE    ') THEN(DO)
                   CHGCURLIB  CURLIB(&WCURLI)
             ENDDO

             GOTO       CMDLBL(VIDERE)

MDLONN:

/* MDLØNN:                                                           */
/* *******                                                           */
/* Henter inn lønnstransaksjoner dersom dette er valgt.  Det         */
/* tester på om file finnes.  Dersom dette ikke er tilfelle          */
/* vil variabel PARM få vierdien "F", som igjen fører til at       */
/* det sendes opp feilmelding i program GL100R:                      */

             CHGVAR     VAR(&PARM) VALUE('F')

             CHKOBJ     OBJ(QS36F/&S36F) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(GOTO CMDLBL(VIDERE))

/* OK!  File med lønnstransaksjoner kopierers i fra S/36-miljø       */
/*      til databasefile:                                            */

             CHGVAR     VAR(&PARM) VALUE('L')

             CPYF   FROMFILE(QS36F/&S36F) +
                    TOFILE(*CURLIB/GLONPF) MBROPT(*REPLACE) +
                    FMTOPT(*NOCHK)

VIDERE:

/* Kaller opp uttrekksprogram.                                       */

             CALL       PGM(GL100R) PARM(&PARM &USER &BFNR)

/* Ved lønnsforslag kalles vedlikeholprogram opp automatisk:       */

         IF         COND(&BFNR *NE 000) THEN(DO)
             CALL       PGM(GL104R) PARM(&BFNR)
         ENDDO

 END:        ENDPGM
