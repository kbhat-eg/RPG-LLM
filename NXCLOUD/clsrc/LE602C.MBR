             PGM        PARM(&LPPARM &LPMEBR)
/*                                                                   */
             DCL        VAR(&LPPARM) TYPE(*CHAR) LEN(128)
             DCL        VAR(&LPMEBR) TYPE(*CHAR) LEN(10)
             DCL        VAR(&BTCH) TYPE(*CHAR) LEN(1)
             DCL        VAR(&WSID) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SYKL) TYPE(*CHAR) LEN(1)

/* Danner members for parameterfilene                                */

             ADDPFM     FILE(LPSTPF) MBR(&LPMEBR)
             ADDLFM     FILE(LPSTLU) MBR(&LPMEBR) +
                          DTAMBRS((*CURRENT/LPSTPF (&LPMEBR)))
             ADDLFM     FILE(LPSTL1) MBR(&LPMEBR) +
                          DTAMBRS((*CURRENT/LPSTPF (&LPMEBR)))
             ADDLFM     FILE(LPSTL2) MBR(&LPMEBR) +
                          DTAMBRS((*CURRENT/LPSTPF (&LPMEBR)))

/* Override parameterfilene/membrene                                 */

             OVRDBF     FILE(LPSTLU) TOFILE(*FILE) MBR(&LPMEBR)
             OVRDBF     FILE(LPSTL1) TOFILE(*FILE) MBR(&LPMEBR)
             OVRDBF     FILE(LPSTL2) TOFILE(*FILE) MBR(&LPMEBR)

/* Sjekker om det er syklisk telling                                 */
             CHGVAR     VAR(&SYKL) VALUE(%SUBSTRING(&LPPARM 98 1))
/* Call til program for seleksjon                                    */


                       IF         COND(&SYKL *EQ '1') THEN(DO)
                                  CALL PGM(LE605R) PARM(&LPPARM)
                       ENDDO
                       ELSE       CMD(DO)
                                  CALL PGM(LE602R) PARM(&LPPARM)
                       ENDDO

/* Program for overstyring av printerfile                            */

             CALL       PGM(AP601C) PARM('LE603P' &BTCH)

/* Call til program for utskrift                                     */

             CALL       PGM(LE603R) PARM(&LPPARM)
/*                                                                   */
/* Override av parametermembrene oppheves                            */
/*                                                                   */
             DLTOVR     FILE(LPSTLU LPSTL1 LPSTL2)
/*                                                                   */
/* Parametermembrene slettes                                         */
/*                                                                   */
             RMVM       FILE(LPSTL2) MBR(&LPMEBR)
             RMVM       FILE(LPSTL1) MBR(&LPMEBR)
             RMVM       FILE(LPSTLU) MBR(&LPMEBR)
             RMVM       FILE(LPSTPF) MBR(&LPMEBR)

/* Melding dersom bakgrunnsjobb                                      */

             IF         COND(&BTCH *EQ '1') THEN(DO)
             RTVDTAARA  DTAARA(*LDA (901 10)) RTNVAR(&WSID)
             SNDBRKMSG  MSG('ASLAGR: Utskrift av telleliste +
                          er ferdig') TOMSGQ(&WSID)
             ENDDO

/* Program for Ã¥ fjerne overstyring av printerfile                   */

             CALL       PGM(AP606C) PARM('LE603P')

/*                                                                   */
             ENDPGM
