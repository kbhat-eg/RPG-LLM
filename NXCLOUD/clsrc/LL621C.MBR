             PGM        PARM(&PARM)

             DCL        VAR(&PARM) TYPE(*CHAR) LEN(128)

             DCL        VAR(&BTCH) TYPE(*CHAR) LEN(1)
             DCL        VAR(&WSID) TYPE(*CHAR) LEN(10)
             DCL        VAR(&WALI)  TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&WAPO)  TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&MLD1)  TYPE(*CHAR) LEN(40)
             DCL        VAR(&MLD2)  TYPE(*CHAR) LEN(40)
             DCL        VAR(&WMM1)  TYPE(*CHAR) LEN(10) VALUE('LL620    ')
             DCL        VAR(&MEMBX)  TYPE(*CHAR) LEN(5)
             DCL        VAR(&MEMB)   TYPE(*DEC)  LEN(5 0)

             RTVDTAARA  DTAARA(*LDA (864 5))  RTNVAR(&MEMBX)
             CHGVAR     VAR(&MEMB) VALUE(&MEMBX)
             CHGVAR     VAR(%SST(&WMM1 6 5)) VALUE(&MEMB)

/* Henter verdier i fra LDA.                                         */

             RTVDTAARA  DTAARA(*LDA (863 1))  RTNVAR(&BTCH)

/* Varselbilde 2                                                     */
             IF         COND(&BTCH *NE '1') THEN(DO)
             CHGVAR     VAR(&WALI) VALUE(6)
             CHGVAR     VAR(&WAPO) VALUE(30)
             CHGVAR     VAR(&MLD1) VALUE('Vennligst vent litt......')
             CHGVAR     VAR(&MLD2) VALUE('Utplukk pågår ...........')
             CALL       PGM(AA006C)  PARM(&WALI &WAPO &MLD1 &MLD2)
             ENDDO
/*                                                                   */
/* Utplukking av poster                                              */
/*                                                                   */
             CLRPFM     FILE(LWA1PF) MBR(&WMM1)
             MONMSG     MSGID(CPF3100)
/*                                                                   */
/* Bygger membere dersom de ikke finnes                              */
/*                                                                   */
             CHKOBJ     OBJ(LWA1PF) OBJTYPE(*FILE) MBR(&WMM1)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDPFM     FILE(LWA1PF) MBR(&WMM1) TEXT('Poster til +
                          varebeholdn.liste (LL623R')
             ADDLFM     FILE(LWA1LU) MBR(&WMM1) +
                          DTAMBRS((*CURRENT/LWA1PF (&WMM1)))
             ADDLFM     FILE(LWA1L1) MBR(&WMM1) +
                          DTAMBRS((*CURRENT/LWA1PF (&WMM1)))
             ENDDO
/*                                                                   */
             OVRDBF     FILE(LWA1LU) TOFILE(*FILE) MBR(&WMM1)
             OVRDBF     FILE(LWA1L1) TOFILE(*FILE) MBR(&WMM1)
/*                                                                   */
             CALL       PGM(LL622R) PARM(&PARM)
/* Varselbilde 2                                                     */
             IF         COND(&BTCH *NE '1') THEN(DO)
             CHGVAR     VAR(&WALI) VALUE(8)
             CHGVAR     VAR(&WAPO) VALUE(26)
             CHGVAR     VAR(&MLD1) VALUE('Vennligst vent litt......')
             CHGVAR     VAR(&MLD2) VALUE('Utskrift pågår...........')
             CALL       PGM(AA006C)  PARM(&WALI &WAPO &MLD1 &MLD2)
             ENDDO

/* Program for overstyring av printerfile                            */

             CALL       PGM(AP601C) PARM('LL623P' &BTCH)

/* Utskrift                                                          */

             CALL       PGM(LL623R) PARM(&PARM)

/* Melding dersom bakgrunnsjobb                                      */

             IF         COND(&BTCH *EQ '1') THEN(DO)
             RTVDTAARA  DTAARA(*LDA (901 10)) RTNVAR(&WSID)
             SNDBRKMSG  MSG('ASLAGR: Utskrift av lagerbeholdning+
                          sliste er ferdig') TOMSGQ(&WSID)
             ENDDO

/* Program for å fjerne overstyring av printerfile                   */

             CALL       PGM(AP606C) PARM('LL623P')

/*                                                                   */
             RMVM       FILE(LWA1LU) MBR(&WMM1)
             MONMSG     MSGID(CPF7310)
             RMVM       FILE(LWA1L1) MBR(&WMM1)
             MONMSG     MSGID(CPF7310)
             RMVM       FILE(LWA1PF) MBR(&WMM1)
             MONMSG     MSGID(CPF7310)

 END:        ENDPGM
