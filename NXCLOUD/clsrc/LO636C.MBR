             PGM        PARM(&PARM &MEMB)
/*                                                                   */
/* Initiering av parametere                                          */
/*                                                                   */
             DCL        VAR(&PARM)   TYPE(*CHAR) LEN(64)
             DCL        VAR(&MEMB)   TYPE(*CHAR) LEN(10)
/*                                                                   */
/* Initiering av parametere for utskrift                             */
/*                                                                   */
             DCL        VAR(&PRTF)   TYPE(*CHAR) LEN(10) +
                                     VALUE('LO636P')
             DCL        VAR(&OUTQ)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&FORM)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&ACPI)   TYPE(*DEC)  LEN(4 0)
             DCL        VAR(&HLDQ)   TYPE(*CHAR) LEN(4)
             DCL        VAR(&AKOP)   TYPE(*DEC)  LEN(6 0)
             DCL        VAR(&ALIN)   TYPE(*DEC)  LEN(3 0)
             DCL        VAR(&FLOW)   TYPE(*DEC)  LEN(3 0)
             DCL        VAR(&ACPIX)  TYPE(*CHAR) LEN(4)
             DCL        VAR(&AKOPX)  TYPE(*CHAR) LEN(6)
             DCL        VAR(&ALINX)  TYPE(*CHAR) LEN(3)
             DCL        VAR(&FLOWX)  TYPE(*CHAR) LEN(3)
/*                                                                   */
             OVRDBF     FILE(LPSOL1) TOFILE(*FILE) MBR(&MEMB)
/*                                                                   */
/* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - */
/* Henter verdier i fra LDA.                                         */
/*                                                                   */
             RTVDTAARA  DTAARA(*LDA (810 10)) RTNVAR(&OUTQ)
             RTVDTAARA  DTAARA(*LDA (820 10)) RTNVAR(&FORM)
             RTVDTAARA  DTAARA(*LDA (830 4))  RTNVAR(&ACPIX)
             RTVDTAARA  DTAARA(*LDA (834 4))  RTNVAR(&HLDQ)
             RTVDTAARA  DTAARA(*LDA (838 6))  RTNVAR(&AKOPX)
             RTVDTAARA  DTAARA(*LDA (856 3))  RTNVAR(&ALINX)
             RTVDTAARA  DTAARA(*LDA (860 3))  RTNVAR(&FLOWX)
/*                                                                   */
/* Antall kopier og CPI m√• konverteres til numerisk felt.            */
/*                                                                   */
             CHGVAR     VAR(&ACPI) VALUE(&ACPIX)
             CHGVAR     VAR(&AKOP) VALUE(&AKOPX)
             CHGVAR     VAR(&ALIN) VALUE(&ALINX)
             CHGVAR     VAR(&FLOW) VALUE(&FLOWX)
/*                                                                   */
/* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - */
/*                                                                   */
/* Endrer printfile ut i fra nye verdier.                            */
/*                                                                   */
             OVRPRTF    FILE(&PRTF)       TOFILE(*FILE) +
                          PAGESIZE(&ALIN) CPI(&ACPI) +
                          OVRFLW(&FLOW)   PRTQLTY(*DRAFT) +
                          OUTQ(&OUTQ)     FORMTYPE(&FORM) +
                          COPIES(&AKOP)   HOLD(&HLDQ)
/*                                                                   */
/* Kaller opp program                                                */
/*                                                                   */
             CALL       PGM(LO636R)       PARM(&PARM)
/*                                                                   */
             DLTOVR     FILE(LPSOL1)
/*                                                                   */
             ENDPGM
