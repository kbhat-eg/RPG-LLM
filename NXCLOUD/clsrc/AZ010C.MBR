             PGM
             DCL        VAR(&BIBL)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&SFIL)     TYPE(*CHAR) LEN(8)
             DCL        VAR(&NFRF)     TYPE(*CHAR) LEN(4)
             DCL        VAR(&KOD1)     TYPE(*CHAR) LEN(2)
             DCL        VAR(&KOD2)     TYPE(*CHAR) LEN(2)
             DCL        VAR(&KOD3)     TYPE(*CHAR) LEN(2)
             DCL        VAR(&BRUD)     TYPE(*DEC)  LEN(1 0)
             DCL        VAR(&IN03)     TYPE(*DEC)  LEN(1 0)

/* Initiering av parametere                                          */

/*   &RUTI = Nøkkelverdi til rutineregister.                         */
/*   &VALG = Verdi "1" dersom skjermbilde for vedlikehold av         */
/*             formularer skal vises.  Verdi "0" dersom bildet       */
/*             ikke skal vises.                                      */

             DCL        VAR(&RUTI)     TYPE(*CHAR) LEN(10) +
                                       VALUE('AZ020')
             DCL        VAR(&VALG)     TYPE(*CHAR) LEN(1) VALUE('1')
             DCL        VAR(&IN03)     TYPE(*DEC)  LEN(1 0)
             DCL        VAR(&IN12)     TYPE(*DEC)  LEN(1 0)

/* Initiering av parametere for utskrift                             */

             DCL        VAR(&PRTF)     TYPE(*CHAR) LEN(10) +
                                       VALUE('AZ020P')
             DCL        VAR(&OUTQ)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&FORM)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&ACPI)     TYPE(*DEC)  LEN(4 0)
             DCL        VAR(&HLDQ)     TYPE(*CHAR) LEN(4)
             DCL        VAR(&AKOP)     TYPE(*DEC)  LEN(6 0)
             DCL        VAR(&APOS)     TYPE(*DEC)  LEN(3 0)
             DCL        VAR(&ALIN)     TYPE(*DEC)  LEN(3 0)
             DCL        VAR(&FLOW)     TYPE(*DEC)  LEN(3 0)
             DCL        VAR(&ACPIX)    TYPE(*CHAR) LEN(4)
             DCL        VAR(&AKOPX)    TYPE(*CHAR) LEN(6)
             DCL        VAR(&APOSX)    TYPE(*CHAR) LEN(3)
             DCL        VAR(&ALINX)    TYPE(*CHAR) LEN(3)
             DCL        VAR(&FLOWX)    TYPE(*CHAR) LEN(3)

/* Tømme output-filen                                                */

             CLRPFM     FILE(*LIBL/ADDSPF)

/* Call parameter-program                                            */

             CALL       PGM(AZ010R)    PARM(&BIBL &SFIL &NFRF +
                                &KOD1 &KOD2 &KOD3 &BRUD &IN03)

/* Avslutt dersom F3                                                 */

             IF         COND(&IN03 *EQ 1) THEN(DO)
             GOTO       CMDLBL(END)
             ENDDO

/* Kopiere fra source-file                                           */
/* til file ADDSPF                                                   */

             CPYF       FROMFILE(&BIBL/&SFIL) +
                          TOFILE(*LIBL/ADDSPF) FROMMBR(&NFRF) +
                          TOMBR(*FIRST) MBROPT(*REPLACE) +
                          FMTOPT(*CVTSRC)

/* Oppdaterer LDA med navn på prinfile og kode for om formular-      */
/* parametre skal endres.                                            */

             CHGDTAARA  DTAARA(*LDA (800 10)) VALUE(&RUTI)
             CHGDTAARA  DTAARA(*LDA (844 1))  VALUE(&VALG)
             CHGDTAARA  DTAARA(*LDA (846 10)) VALUE(&PRTF)

/* Call av program for utskriftsparametre.                           */

             CALL       PGM(AP100R) PARM(&IN03 &IN12)

/* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - */
/* Henter verdier i fra LDA.                                         */

             RTVDTAARA  DTAARA(*LDA (810 10)) RTNVAR(&OUTQ)
             RTVDTAARA  DTAARA(*LDA (820 10)) RTNVAR(&FORM)
             RTVDTAARA  DTAARA(*LDA (830 4))  RTNVAR(&ACPIX)
             RTVDTAARA  DTAARA(*LDA (834 4))  RTNVAR(&HLDQ)
             RTVDTAARA  DTAARA(*LDA (838 6))  RTNVAR(&AKOPX)
             RTVDTAARA  DTAARA(*LDA (856 3))  RTNVAR(&ALINX)
             RTVDTAARA  DTAARA(*LDA (860 3))  RTNVAR(&FLOWX)
             RTVDTAARA  DTAARA(*LDA (887 3))  RTNVAR(&APOSX)

/* Antall kopier og CPI må konverteres til numerisk felt.            */

             CHGVAR     VAR(&ACPI) VALUE(&ACPIX)
             CHGVAR     VAR(&AKOP) VALUE(&AKOPX)
             CHGVAR     VAR(&APOS) VALUE(&APOSX)
             CHGVAR     VAR(&ALIN) VALUE(&ALINX)
             CHGVAR     VAR(&FLOW) VALUE(&FLOWX)

/* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - */

/* Endrer printfile ut i fra nye verdier.                            */

             OVRPRTF    FILE(&PRTF)       TOFILE(*FILE) +
                          PAGESIZE(&ALIN  &APOS) CPI(&ACPI) +
                          OVRFLW(&FLOW)   PRTQLTY(*DRAFT) +
                          OUTQ(&OUTQ)     FORMTYPE(&FORM) +
                          COPIES(&AKOP)   HOLD(&HLDQ)

/* Program for sletting av *-linjer                                  */

             CALL       PGM(AZ015R)    PARM(&KOD1 &KOD2 &KOD3)

/* Program for utskrift                                              */

             CALL       PGM(AZ020R)    PARM(&BIBL &NFRF &KOD1 +
                                            &KOD2 &KOD3 &BRUD)

 END:        ENDPGM
