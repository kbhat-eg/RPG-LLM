             PGM
             DCLF       FILE(AM80)
             DCL        VAR(&FNAV)     TYPE(*CHAR) LEN(30)
             DCL        VAR(&PRID)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&PF03)     TYPE(*CHAR) LEN(1)
             DCL        VAR(&SYSNAME)  TYPE(*CHAR) LEN(8)
             DCL        VAR(&MLD1)     TYPE(*CHAR) LEN(50)
             DCL        VAR(&TYPE)     TYPE(*CHAR) LEN(1)
             MONMSG     MSGID(CPF0001) EXEC(GOTO MELDE)
             MONMSG     MSGID(CPA0701) EXEC(GOTO START)
             MONMSG     MSGID(CPF0000) EXEC(GOTO START)
             MONMSG     MSGID(CPF9999) EXEC(GOTO START)

 START:      CHGVAR     VAR(&VALG) VALUE(0)
             RTVDTAARA  DTAARA(*LDA (951 30)) RTNVAR(&FNAV)
             CHGVAR     VAR(&A1FNAV)   VALUE(&FNAV)
             SNDRCVF    RCDFMT(A1MNU)

             RTVNETA    SYSNAME(&SYSNAME)
             CHGVAR     VAR(&MLD1) VALUE('Dennne funksjonen er ikke +
                          tilgjengelig')

/* Tester på funksjonstaster                                         */

/* F3                                                                */
             IF         COND(&IN03) THEN(GOTO CMDLBL(ENDPGM))

/* F5                                                                */
             IF         COND(&IN05) THEN(GOTO CMDLBL(START))

/* F6                                                                */
             IF         COND(&IN06) THEN(DSPMSG)

/* F12                                                               */
             IF         COND(&IN12) THEN(GOTO CMDLBL(ENDPGM))

/* Valg 1                                                            */
             IF         COND(&VALG *EQ 1) THEN(DO)
             WRKSPLF
             GOTO       CMDLBL(START)
             ENDDO

/* Valg 2                                                            */
             IF         COND(&VALG *EQ 2) THEN(DO)
             WRKUSRJOB
             GOTO       CMDLBL(START)
             ENDDO

/* Valg 3                                                            */
             IF         COND(&VALG *EQ 3) THEN(DO)
             DSPMSG
             GOTO       CMDLBL(START)
             ENDDO

/* Valg 4                                                            */
             IF         COND(&VALG *EQ 4) THEN(DO)
             WRKSBMJOB
             GOTO       CMDLBL(START)
             ENDDO

/* Valg 5                                                            */
             IF         COND(&VALG *EQ 5)   THEN(DO)
             CHGVAR     VAR(&TYPE)          VALUE('5')
             CALL       PGM(AA721R)         PARM(&PF03 &PRID &TYPE)
             IF         COND(&PF03 *EQ '1') THEN(DO)
             GOTO       CMDLBL(START)
             ENDDO
             WRKWTR     WTR(&PRID)
             GOTO       CMDLBL(START)
             ENDDO

/* Valg 6                                                            */
             IF         COND(&VALG *EQ 6)   THEN(DO)
             CHGVAR     VAR(&TYPE)          VALUE('6')
             CALL       PGM(AA721R)         PARM(&PF03 &PRID &TYPE)
             IF         COND(&PF03 *EQ '1') THEN(DO)
             GOTO       CMDLBL(START)
             ENDDO
             WRKOUTQ    OUTQ(&PRID)
             GOTO       CMDLBL(START)
             ENDDO

/* Valg 7                                                            */
             IF         COND(&VALG *EQ 7) THEN(DO)
             CHGPWD
             GOTO       CMDLBL(START)
             ENDDO

/* Valg 8                                                            */
             IF         COND(&VALG *EQ 8)   THEN(DO)
             CHGVAR     VAR(&TYPE)          VALUE('8')
             CALL       PGM(AA721R)         PARM(&PF03 &PRID &TYPE)
             IF         COND(&PF03 *EQ '1') THEN(DO)
             GOTO       CMDLBL(START)
             ENDDO
             IF         COND(&PRID *NE ' ') THEN(DO)
             ENDWTR     WTR(&PRID)          OPTION(*IMMED)
             GOTO       CMDLBL(START)
             ENDDO
             ENDDO

/* Valg 9                                                            */
             IF         COND(&VALG *EQ 9)   THEN(DO)
             CHGVAR     VAR(&TYPE)          VALUE('9')
             CALL       PGM(AA721R)         PARM(&PF03 &PRID &TYPE)
             IF         COND(&PF03 *EQ '1') THEN(DO)
             GOTO       CMDLBL(START)
             ENDDO
             IF         COND(&PRID *NE ' ') THEN(DO)
             ENDWTR     WTR(&PRID) OPTION(*IMMED)
             MONMSG     MSGID(CPF0000)
             DLYJOB     DLY(2)
             STRPRTWTR  DEV(&PRID)
             MONMSG     MSGID(CPF0000)
             DLYJOB     DLY(2)
             STRRMTWTR  OUTQ(&PRID)
             DLYJOB     DLY(2)
             GOTO       CMDLBL(START)
             ENDDO
             ENDDO

/* Valg 10                                                           */
             IF         COND(&VALG *EQ 10)  THEN(DO)
                IF         COND((&SYSNAME *EQ 'BYGGMAKK') *OR (&SYSNAME +
                             *EQ 'ASDRIFT') *OR (&SYSNAME *EQ 'XLBYGG') +
                             *OR (&SYSNAME *EQ 'OPTIMERA') ) THEN(DO)
                   CALL       PGM(AA005R) PARM(&MLD1)
                ENDDO
                ELSE       DO
                   CALL       QEZSNDMG
                ENDDO
             ENDDO

/* Valg 11                                                           */
             IF         COND(&VALG *EQ 11)  THEN(DO)
             RUNQRY     QRY(ASPQRY/QRYFINN) RCDSLT(*YES)
             ENDDO

/* Valg 90                                                           */
             IF         COND(&VALG *EQ 90) THEN(DO)
             SIGNOFF
             ENDDO

/* Gå til start                                                      */
             GOTO       CMDLBL(START)

 MELDE:      CALL       PGM(AA004C)
             GOTO       CMDLBL(START)

/* Slutt                                                             */
 ENDPGM:     ENDPGM
