             PGM        PARM(&MEMB)

             DCL        VAR(&MEMB)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&AMEM)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&VLG1)     TYPE(*CHAR) LEN(1)
             DCL        VAR(&VLG2)     TYPE(*CHAR) LEN(1)
             DCL        VAR(&VLG3)     TYPE(*CHAR) LEN(1)

/*  Blanker Dataara etter pga. omkjøring etc.                        */

             CHGDTAARA  DTAARA(*LDA (599 01)) VALUE(' ')

/*  Endrer variabel, Denne gjøres bare en gang                       */

             CHGVAR     VAR(&AMEM) VALUE(&MEMB)

/*  Endrer variabel, Denne gjøres for hver nytt alternativ           */

 START:      CHGVAR     VAR(&MEMB) VALUE(&AMEM)

/*  Blanker Dataara etter henting av variabel, pga. omkjøring etc.   */

             CHGDTAARA  DTAARA(*LDA (500 12)) VALUE('            ')

/*  Legger til records i parameter-registeret                        */

             ADDPFM     FILE(FPM2PF) MBR(&MEMB)
             ADDLFM     FILE(FPM2LU) MBR(&MEMB) +
                          DTAMBRS((*CURRENT/FPM2PF (&MEMB)))

/*  Override den valgte file til programfilen                        */

             OVRDBF     FILE(FPM2LU) TOFILE(*FILE) MBR(&MEMB) +
                          SHARE(*YES)

/*  Kaller opp program for start  av utskrift                        */

             CALL       PGM(FO612R)

             DLTOVR     FILE(FPM2LU)

             RTVDTAARA  DTAARA(*LDA (500 01)) RTNVAR(&VLG1)
             RTVDTAARA  DTAARA(*LDA (501 01)) RTNVAR(&VLG2)
             RTVDTAARA  DTAARA(*LDA (599 01)) RTNVAR(&VLG3)
             RTVDTAARA  DTAARA(*LDA (502 10)) RTNVAR(&AMEM)

             IF         COND(&VLG1 *EQ '1') THEN(CALL PGM(FO808C) +
                          PARM(&MEMB))

             IF         COND(&VLG1 *EQ '2') THEN(SBMJOB CMD(CALL +
                          PGM(FO808C) PARM(&MEMB)) JOB(ORDREUTSK))

             IF         COND(&VLG2 *EQ '1') THEN(GOTO CMDLBL(START))

             IF         COND(&VLG3 *EQ '1') THEN(DO)
             RMVM       FILE(FPM2LU) MBR(&MEMB)
             MONMSG     MSGID(CPF7310)
             RMVM       FILE(FPM2PF) MBR(&MEMB)
             MONMSG     MSGID(CPF7310)
             ENDDO

             ENDPGM
