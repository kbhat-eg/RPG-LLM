
/*           Generere   Kundekategorier                              */

 STRPGM:     PGM        PARM(&FGRP &NULL)

             DCL        VAR(&FGRP) TYPE(*CHAR) LEN(3)
             DCL        VAR(&NULL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&DBIB) TYPE(*CHAR) LEN(10) VALUE('ADB400')
             DCL        VAR(&SBIB) TYPE(*CHAR) LEN(10) VALUE('ASOKON')

             DCL        VAR(&RGBF) TYPE(*CHAR) LEN(10) VALUE('ra11aBF')
             DCL        VAR(&RGPF) TYPE(*CHAR) LEN(10) VALUE('ra11aPF')
             DCL        VAR(&RGLU) TYPE(*CHAR) LEN(10) VALUE('ra11aLU')
             DCL        VAR(&RGLR) TYPE(*CHAR) LEN(10) VALUE('ra11aLR')
             DCL        VAR(&RGL1) TYPE(*CHAR) LEN(10) VALUE('ra11aL1')

/*           Setter bibliotek til current                            */

             CHGVAR     VAR(%SST(&DBIB 4 3)) VALUE(&FGRP)
             CHGCURLIB  CURLIB(&DBIB)

/*           Kalle opp fellesprogram                                 */

             CALL       PGM(AG102C) PARM(&FGRP &NULL &DBIB +
                             &SBIB &RGBF &RGPF &RGLU &RGLR +
                             &RGL1)

/*           Sjekk, Finnes back-register, JA=Slett registeret        */

             CHKOBJ     OBJ(&DBIB/&RGBF) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(GOTO TAG002)
             DLTF       FILE(&DBIB/&RGBF)

/*          Sletter indexer / Legger tilside registeret              */

 TAG002:     CHKOBJ     OBJ(&DBIB/&RGPF) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(GOTO TAG003)

             DLTF       FILE(&DBIB/&RGLU)
             MONMSG     MSGID(CPF2105)
             DLTF       FILE(&DBIB/&RGL1)
             MONMSG     MSGID(CPF2105)
             DLTF       FILE(&DBIB/&RGLR)
             MONMSG     MSGID(CPF2105)

             RNMOBJ     OBJ(&DBIB/&RGPF) OBJTYPE(*FILE) +
                          NEWOBJ(&RGBF)

/*           Start danning av registre                               */

/* PF:PF                                                             */

 TAG003:     CRTPF      FILE(&DBIB/&RGPF) +
                          SRCFILE(*LIBL/QDDFSRC) SRCMBR(*FILE) +
                          MAXMBRS(*NOMAX) SIZE(*NOMAX) +
                          REUSEDLT(*YES)  SRTSEQ(*LANGIDUNQ) +
                          LANGID(NOR)
             MONMSG     MSGID(CPF7302)

/* LF:LU                                                             */

             CRTLF      FILE(&DBIB/&RGLU) +
                          SRCFILE(*LIBL/QDDFSRC) MAXMBRS(*NOMAX) +
                          SRTSEQ(*LANGIDUNQ)     LANGID(NOR)
             MONMSG     MSGID(CPF7302)

/* LF:L1                                                             */

             CRTLF      FILE(&DBIB/&RGL1) +
                          SRCFILE(*LIBL/QDDFSRC) MAXMBRS(*NOMAX) +
                          SRTSEQ(*LANGIDUNQ)     LANGID(NOR)
             MONMSG     MSGID(CPF7302)

/* LF:L2                                                             */

             CRTLF      FILE(&DBIB/&RGLR) +
                          SRCFILE(*LIBL/QDDFSRC) MAXMBRS(*NOMAX) +
                          SRTSEQ(*LANGIDUNQ)     LANGID(NOR)
             MONMSG     MSGID(CPF7302)

/*           Kopierer fra gammelt til nytt register                  */

             CHKOBJ     OBJ(&DBIB/&RGBF) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(GOTO TAG005)
             CPYF       FROMFILE(&DBIB/&RGBF) +
                          TOFILE(&DBIB/&RGPF) FROMMBR(*ALL) +
                          TOMBR(*FROMMBR) MBROPT(*ADD) FMTOPT(*MAP +
                          *DROP)


/*           Sjekk, Finnes back-register, J=Slett registeret         */

             CHKOBJ     OBJ(&DBIB/&RGBF) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(GOTO TAG005)
             DLTF       FILE(&DBIB/&RGBF)

/*           Sletter innholdet i registeret                          */

 TAG005:     IF         COND(&NULL *EQ 'S') THEN(CLRPFM +
                          FILE(&DBIB/&RGPF))


 ENDPGM:     ENDPGM
