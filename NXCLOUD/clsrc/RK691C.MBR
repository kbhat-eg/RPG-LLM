             PGM        PARM(&PROG)

/* Initiering av parametre.                                          */

             DCL        VAR(&PROG)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&RAPP)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&IN03)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&BTCH)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&MEMBX) TYPE(*CHAR) LEN(5)
             DCL        VAR(&MEMB)  TYPE(*DEC)  LEN(5 0)
             DCL        VAR(&WMM1)  TYPE(*CHAR) LEN(10) VALUE('RKA       ')
             DCL        VAR(&WALI)  TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&WAPO)  TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&MLD1)  TYPE(*CHAR) LEN(40)
             DCL        VAR(&MLD2)  TYPE(*CHAR) LEN(40)
             DCL        VAR(&PSKR)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&OCRK)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&BGIR)  TYPE(*CHAR) LEN(1)

/* Henter verdier i fra LDA.                                         */

             RTVDTAARA  DTAARA(*LDA (863 1))  RTNVAR(&BTCH)
             RTVDTAARA  DTAARA(*LDA (864 5))  RTNVAR(&MEMBX)
             RTVDTAARA  DTAARA(*LDA (881 1))  RTNVAR(&BGIR)

             CHGVAR     VAR(&MEMB) VALUE(&MEMBX)
             CHGVAR     VAR(%SST(&WMM1 4 5)) VALUE(&MEMB)

             CHGVAR     VAR(&RAPP) VALUE(&PROG)
             CHGVAR     VAR(%SST(&RAPP 6 1)) VALUE(P)

             CHGVAR     VAR(%SST(&PROG 6 1)) VALUE(R)

/* Varselbilde 2                                                     */

             IF         COND(&BTCH *NE '1') THEN(DO)
             CHGVAR     VAR(&WALI) VALUE(4)
             CHGVAR     VAR(&WAPO) VALUE(44)
             CHGVAR     VAR(&MLD1) VALUE('Vennligst vent litt .....')
             CHGVAR     VAR(&MLD2) VALUE('Utplukk pågår ...........')
             CALL       PGM(AA006C)  PARM(&WALI &WAPO &MLD1 &MLD2)
             ENDDO

/* Utplukking av poster                                              */

             CLRPFM     FILE(RKW2PF) MBR(&WMM1)
             MONMSG     MSGID(CPF3100)

/* Bygger membere dersom de ikke finnes                              */

             CHKOBJ     OBJ(RKW2PF) OBJTYPE(*FILE) MBR(&WMM1)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDPFM     FILE(RKW2PF) MBR(&WMM1) TEXT('Poster til +
                          generalnota  (RK691R)')
             ENDDO

             OVRDBF     FILE(RKW2PF) MBR(&WMM1)
             CALL       PGM(RK691R)

/* Varselbilde 3                                                     */

             IF         COND(&BTCH *NE '1') THEN(DO)
             CHGVAR     VAR(&WALI) VALUE(6)
             CHGVAR     VAR(&WAPO) VALUE(40)
             CHGVAR     VAR(&MLD1) VALUE('Vennligst vent litt .....')
             CHGVAR     VAR(&MLD2) VALUE('Utskrift av generalnota  ')
             CALL       PGM(AA006C)  PARM(&WALI &WAPO &MLD1 &MLD2)
             ENDDO

/* Bygger membere dersom de ikke finnes                              */

             CHKOBJ     OBJ(RGIRPF) OBJTYPE(*FILE) MBR(&WMM1)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDPFM     FILE(RGIRPF) MBR(&WMM1) TEXT('Giroutskrift +
                          fra generalnota (RK692R)')
             ENDDO

/* Call av program, utskrift av generalnota                          */

/* Overstyring av printerfile                                        */

             CALL       PGM(AP601C)    PARM(&RAPP &BTCH)

             OVRDBF     FILE(RGIRPF) MBR(&WMM1)
             CALL       PGM(&PROG)

             CALL       PGM(AP606C)    PARM(&RAPP)

/* Varselbilde 4                                                     */

             IF         COND(&BTCH *NE '1') THEN(DO)
             CHGVAR     VAR(&WALI) VALUE(8)
             CHGVAR     VAR(&WAPO) VALUE(36)
             CHGVAR     VAR(&MLD1) VALUE('Vennligst vent litt .....')
             CHGVAR     VAR(&MLD2) VALUE('Utskrift av journal .....')
             CALL       PGM(AA006C)  PARM(&WALI &WAPO &MLD1 &MLD2)
             ENDDO

/* Call av program, utskrift av generalnotajournal                   */

             CALL       PGM(AP600R) PARM('RK693 ' '0' '0' &IN03 &BTCH)

/* Overstyring av printerfile                                        */

             CALL       PGM(AP601C)    PARM('RK693P' &BTCH)

             OVRDBF     FILE(RGIRPF) MBR(&WMM1)
             CALL       PGM(RK693R)

             CALL       PGM(AP606C)    PARM('RK693P')

/* Egen giro skal skrives ?!?                                        */

             IF         COND(&BGIR *EQ '1') THEN(DO)
             CALL       PGM(RK617C)
             ENDDO

             DLTOVR     FILE(RGIRPF)
             DLTOVR     FILE(RKW2PF)
             RMVM       FILE(*LIBL/RKW2PF) MBR(&WMM1)
             RMVM       FILE(*LIBL/RGIRPF) MBR(&WMM1)
             MONMSG     MSGID(CPF3200)
 END:        ENDPGM
