             PGM        PARM(&FILE &DLIB &NULL)

             DCL        VAR(&FILE) TYPE(*CHAR) LEN(6)
             DCL        VAR(&DLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&NULL) TYPE(*CHAR) LEN(1)

             DCL        VAR(&SFIL) TYPE(*CHAR) LEN(6)
             DCL        VAR(&BACK) TYPE(*CHAR) LEN(6)
             DCL        VAR(&INDX) TYPE(*CHAR) LEN(6)
             DCL        VAR(&GRP)     TYPE(*CHAR) LEN(7)
             DCL        VAR(&GRPPRF)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&FGRP)    TYPE(*CHAR) LEN(3)
             DCL        VAR(&LVR)     TYPE(*CHAR) LEN(3)
             DCL        VAR(&FLST)    TYPE(*CHAR) LEN(5)
             DCL        VAR(&TBLE)    TYPE(*CHAR) LEN(10)

/* Setter databibliotek til current library                          */

             CHGCURLIB  CURLIB(&DLIB)

/* Finn grupppeprofil                                                */
             RTVUSRPRF  GRPPRF(&GRPPRF)
             CHGVAR     VAR(&GRP) VALUE(%SUBSTRING(&GRPPRF 1 7))

/* Finn filgruppe og lvr                                             */
             CHGVAR     VAR(&FGRP) VALUE(%SUBSTRING(&DLIB 4 3))
             CHGVAR     VAR(&LVR) VALUE(%SUBSTRING(&DLIB 4 3))

             CHGVAR     VAR(%SST(&TBLE 1 6)) VALUE(&FILE)

             IF         COND(&LVR *EQ 'LVR') THEN(DO)
/* Slett indexer  på tabell                                          */
                CALL       PGM(IDXDLT) PARM(' ' &DLIB ' ' &FILE ' ')

             ENDDO
             ELSE       DO
/* Slett triggere på tabell                                          */
                CALL       PGM(TRGDLT) PARM(&FGRP &FILE ' ')
/* Slett indexer  på tabell                                          */
                CALL       PGM(IDXDLT) PARM(&FGRP ' ' ' ' &FILE ' ')
/* Slett views    på tabell                                          */
                CALL       PGM(VIEWDLT) PARM(&FGRP &FILE ' ')
             ENDDO


/* Utleder navn på source-file, backup-tabell og index-prefix        */

             CHGVAR     VAR(&SFIL) VALUE(%SUBSTRING(&FILE 1 4) *CAT +
                          'SQ')
             CHGVAR     VAR(&BACK) VALUE(%SUBSTRING(&FILE 1 4) *CAT +
                          'BT')
             CHGVAR     VAR(&INDX) VALUE(%SUBSTRING(&FILE 1 4) *CAT +
                          'I*')
             CHGVAR     VAR(&FLST) VALUE(%SUBSTRING(&FILE 1 4) *CAT '*')

/* Tar vare på tabellinnholdet i temporær backup-tabell og sletter   */
/* eksisterende tabell og tilhørende indekser                        */

             CHKOBJ     OBJ(&DLIB/&FILE) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(GOTO CMDLBL(SQL))

             CHKOBJ     OBJ(&DLIB/&BACK) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(GOTO CMDLBL(BACKUP))

             DLTF       FILE(&DLIB/&BACK)

 BACKUP:     CPYF       FROMFILE(&DLIB/&FILE) TOFILE(&DLIB/&BACK) +
                          CRTFILE(*YES)

             DLTF       FILE(&DLIB/&INDX)
             MONMSG     MSGID(CPF2125)

             DLTF       FILE(&DLIB/&FILE)

/* Kjører SQL-statements for oppretting av tabell og indekser        */

 SQL:        RUNSQLSTM  SRCFILE(*LIBL/QSQLSRC) SRCMBR(&SFIL) +
                          NAMING(*SYS) DECMPT(*COMMA) +
                          SRTSEQ(*LANGIDUNQ) LANGID(NOR)

/* Kopierer tilbake tabellinnhold fra temporær backup-tabell         */

             CHKOBJ     OBJ(&DLIB/&BACK) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(GOTO END)

             IF         COND(&NULL *NE '1') THEN( +
             CPYF       FROMFILE(&DLIB/&BACK) +
                          TOFILE(&DLIB/&FILE) MBROPT(*ADD) +
                          FMTOPT(*MAP *DROP))

             DLTF       FILE(&DLIB/&BACK)

 STRSQL:

/* Legg på gruppe rettigheter                                        */
             IF         COND((&GRP *NE '*NONE') *AND (&GRP *NE ' ')) +
                          THEN(DO)
                GRTOBJAUT  OBJ(&DLIB/&FLST) OBJTYPE(*ALL) USER(&GRP) +
                             AUT(*ALL) REPLACE(*YES)

                GRTOBJAUT  OBJ(&DLIB/&FLST) OBJTYPE(*ALL) USER(*PUBLIC) +
                             AUT(*EXCLUDE) REPLACE(*YES)
             ENDDO

/* Journalfør tabellen                                               */
             STRJRNPF   FILE(&DLIB/&FILE) JRN(&DLIB/BUTIJRN) +
                          OMTJRNE(*OPNCLO)
             MONMSG     MSGID(CPF9801) EXEC(GOTO END) /* BUTIJRN finnes +
                          ikke */
             MONMSG     MSGID(CPF7030) /*  already being journaled  */


             IF         COND(&LVR *EQ 'LVR') THEN(DO)
/* Legg inn indexer  på tabell                                       */
                CALL       PGM(IDXCRT) PARM(' ' &DLIB ' ' &TBLE ' ')
             ENDDO
             ELSE       DO
/* Legg inn triggere på tabell                                       */
                CALL       PGM(TRGCRT) PARM(&FGRP &TBLE ' ')
/* Legg inn indexer  på tabell                                       */
                CALL       PGM(IDXCRT) PARM(&FGRP ' ' ' ' &TBLE ' ')
/* Legg inn views    på tabell                                       */
                CALL       PGM(VIEWCRT) PARM(&FGRP ' ' ' ' &TBLE ' ')
             ENDDO


 END:        ENDPGM
