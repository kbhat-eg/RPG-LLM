             PGM

             DCL        VAR(&MLD1)     TYPE(*CHAR) LEN(50)
             DCL        VAR(&MLD2)     TYPE(*CHAR) LEN(50)
             DCL        VAR(&SVAR)     TYPE(*CHAR) LEN(1)
             DCL        VAR(&IN12)     TYPE(*DEC)  LEN(1 0)

/* Varselbilde med bekreftelse på om jobb skal settes i gang         */

             CHGVAR     VAR(&MLD1) +
                        VALUE('Generering av subsystem og jobbkø    ')
             CHGVAR     VAR(&MLD2) +
                        VALUE('Bekreft at dette startes! ')
             CALL       PGM(AA007R)    PARM(&MLD1 &MLD2 &SVAR &IN12)

             IF         COND(&IN12 *EQ 1 ) +
                          THEN(GOTO CMDLBL(ENDPGM))

             IF         COND(&SVAR *EQ 'J') +
                          THEN(GOTO CMDLBL(TESTSUB))
             ELSE       CMD(GOTO CMDLBL(ENDPGM))

/* Sjekker om subsystem allerede er i gang og varsler                 */

 TESTSUB:    STRSBS     SBSD(*LIBL/NAVSYNC)
             MONMSG     MSGID(CPF1013)
             MONMSG     MSGID(CPF1010) EXEC(DO)
             CHGVAR     VAR(&SVAR) VALUE(' ')
             CHGVAR     VAR(&MLD1) +
                        VALUE('Subsystem allerede i gang, vil bli   ')
             CHGVAR     VAR(&MLD2) +
                        VALUE('avsluttet og restartet. Er dette ok?')
             CALL       PGM(AA007R)    PARM(&MLD1 &MLD2 &SVAR &IN12)

             IF         COND(&IN12 *EQ 1 ) +
                          THEN(GOTO CMDLBL(ENDPGM))

             IF         COND(&SVAR *EQ 'J') +
                          THEN(GOTO CMDLBL(VIDERE))
             ELSE       CMD(GOTO CMDLBL(ENDPGM))
             ENDDO

 VIDERE:     ENDSBS     SBS(NAVSYNC)
             MONMSG     MSGID(CPF0000)
             DLTSBSD    SBSD(NAVSYNC)
             MONMSG     MSGID(CPF0000)
             CRTSBSD    SBSD(QGPL/NAVSYNC) POOLS((1 *BASE)) +
                          MAXJOBS(5) TEXT('Subsystem for NAV til +
                          Nexstep synkronisering')
             MONMSG     MSGID(CPF1696)
             STRSBS     SBSD(*LIBL/NAVSYNC)
             MONMSG     MSGID(CPF1010)

             DLTJOBQ    JOBQ(*LIBL/NAVSYNC)
             MONMSG     MSGID(CPF0000)
             CRTJOBQ    JOBQ(QGPL/NAVSYNC) TEXT('Jobbkø for +
                          synkronisering NAV mot Nexstep')
             MONMSG     MSGID(CPF3323)
             DLTCLS     CLS(*LIBL/QBATCH)
             MONMSG     MSGID(CPF0000)
             CRTCLS     CLS(QGPL/QBATCH)

             ADDJOBQE   SBSD(*LIBL/NAVSYNC) +
                          JOBQ(*LIBL/NAVSYNC) MAXACT(5) +
                          MAXPTY1(0) MAXPTY2(0) MAXPTY3(0) +
                          MAXPTY4(5) MAXPTY5(0) MAXPTY6(0) +
                          MAXPTY7(0) MAXPTY8(0) MAXPTY9(0)
             MONMSG     MSGID(CPF1535 CPF1697)

             ADDRTGE    SBSD(*LIBL/NAVSYNC) SEQNBR(10) +
                          CMPVAL(*ANY) PGM(QSYS/QCMD) +
                          CLS(*LIBL/QBATCH)

             DLTJOBD    JOBD(*LIBL/NAVSYNC)
             MONMSG     MSGID(CPF0000)
             CRTJOBD    JOBD(QGPL/NAVSYNC) JOBQ(*LIBL/NAVSYNC) +
                          JOBPTY(4) TEXT('Jobb beskrivelse NAV til +
                          Nexstep synkronisering')
             MONMSG     MSGID(CPF1621)


 ENDPGM:     ENDPGM
