             PGM

             DCL        VAR(&PROG)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&RAPP)   TYPE(*CHAR) LEN(10) VALUE('ETIKETT ')
             DCL        VAR(&USER)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&HYLL)   TYPE(*CHAR) LEN(1)  VALUE('0')
             DCL        VAR(&OUTQ)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&FORM)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&HLDQ)   TYPE(*CHAR) LEN(4)
             DCL        VAR(&AKOPX)  TYPE(*CHAR) LEN(6)
             DCL        VAR(&AKOP)   TYPE(*DEC)  LEN(6 0)
             DCL        VAR(&IN03)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&IN12)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&BTCH)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&WALI)   TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&WAPO)   TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&MLD1)   TYPE(*CHAR) LEN(40)
             DCL        VAR(&MLD2)   TYPE(*CHAR) LEN(40)

             RTVDTAARA  DTAARA(*LDA (911 10)) RTNVAR(&USER)

/* Oppdaterer LDA med programnavn                                    */

             CHGDTAARA  DTAARA(*LDA (846 10)) VALUE(&RAPP)
             CHGDTAARA  DTAARA(*LDA (122 1)) VALUE(&HYLL)

/*           TÃ¸m arbeidregister                                      */

             CLRPFM     FILE(VAW1PF) MBR(&USER)
             MONMSG     MSGID(CPF3100)

/* Bygger membere dersom de ikke finnes                              */

             CHKOBJ     OBJ(VAW1PF) OBJTYPE(*FILE) MBR(&USER)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDPFM     FILE(VAW1PF) MBR(&USER) TEXT('Varer til +
                          etikettutskrift ')
             ENDDO

/* Call av program for formularparametre.                            */

             CALL       PGM(AP600R) PARM('FP290' '0' '0' &IN03 &BTCH)

             RTVDTAARA  DTAARA(*LDA (810 10)) RTNVAR(&OUTQ)
             RTVDTAARA  DTAARA(*LDA (820 10)) RTNVAR(&FORM)
             RTVDTAARA  DTAARA(*LDA (834 4))  RTNVAR(&HLDQ)
             RTVDTAARA  DTAARA(*LDA (838 6))  RTNVAR(&AKOPX)
             CHGVAR     VAR(&AKOP) VALUE(&AKOPX)

/* Call av program for varemottak                                    */

 TAG002:     OVRDBF     FILE(VAW1PF) TOFILE(*LIBL/VAW1PF) +
                          MBR(&USER)

             CALL       PGM(FP290R) PARM(&IN03 &PROG)

/* Avslutt dersom F3                                                 */

             IF         COND(&IN03 *EQ '1') THEN(DO)
             GOTO       CMDLBL(SLUTT)
             ENDDO

/* Avslutt dersom F3/F12                                             */

             IF         COND(&IN03 *EQ '1') THEN(DO)
             GOTO       CMDLBL(SLUTT)
             ENDDO

             RTVDTAARA  DTAARA(*LDA (863 1)) RTNVAR(&BTCH)

/* Varselbilde 1                                                     */

             IF         COND(&BTCH *NE '1') THEN(DO)
             CHGVAR     VAR(&WALI) VALUE(4)
             CHGVAR     VAR(&WAPO) VALUE(40)
             CHGVAR     VAR(&MLD1) VALUE('Utskrift av etiketter     ')
             CHGVAR     VAR(&MLD2) VALUE('***      ASOFAK       *** ')
             CALL       PGM(AA006C)  PARM(&WALI &WAPO &MLD1 &MLD2)
             ENDDO

/* Call av utskriftsprogram til batch/demand                         */

             OVRPRTF    FILE(&RAPP) TOFILE(QSYSPRT) DEV(&OUTQ) +
                          OUTQ(&OUTQ) +
                          FORMTYPE(&FORM) COPIES(&AKOP) +
                          HOLD(&HLDQ)

             OVRDBF     FILE(VAW1PF) TOFILE(*LIBL/VAW1PF) MBR(&USER)

             CALL       PGM(&PROG)

             CLRPFM     FILE(VAW1PF)   MBR(&USER)

             GOTO       CMDLBL(TAG002)

 SLUTT:

             DLTOVR     FILE(VAW1PF)

             RMVM       FILE(VAW1PF) MBR(&USER)

             ENDPGM
