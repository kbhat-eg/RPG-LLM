
/*  Rutinen brukes for å hente ut type bankforbindelse som firmaet   */
/*  har.  For å gjøre dette må det overrides til riktig bibliotek,   */
/*  hvor file for faste data finnes.  Deretter bygges DIRxx (hvor    */
/*  xx er bankkoden) i bibliotek ADB.  DIRxx inneholder poster for   */
/*  remittering bank.                                                */

/*  Dersom parameteren "BYGG" inneholder verdien "N", så skal ikke   */
/*  file bygges.  Da blir denne rutinen kun brukt for å hente ut     */
/*  type bankforbindelse.                                            */

/*  Fjernet unødvendige linjer. 29/8-06 ASP v/RHO                    */

             PGM        PARM(&FGRP &FIRM &BYGG &BANK)

             DCL        VAR(&BYGG) TYPE(*CHAR) LEN(1)
             DCL        VAR(&FGRP) TYPE(*CHAR) LEN(3)
             DCL        VAR(&FIRM) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&BANK) TYPE(*CHAR) LEN(2)
             DCL        VAR(&BIBL) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FILE) TYPE(*CHAR) LEN(10)

             DCL        VAR(&FREM) TYPE(*CHAR) LEN(8)
             DCL        VAR(&FMOT) TYPE(*CHAR) LEN(8)
             DCL        VAR(&FAVR) TYPE(*CHAR) LEN(8)
             DCL        VAR(&FOCR) TYPE(*CHAR) LEN(8)
             DCL        VAR(&FKTO) TYPE(*CHAR) LEN(8)
             DCL        VAR(&FINK) TYPE(*CHAR) LEN(8)
             DCL        VAR(&FTFF) TYPE(*CHAR) LEN(8)

/* Bygger biblioteksnavn (for å finne file for faste data - GFASPF): */

             CHGVAR     VAR(%SST(&BIBL 1 3)) VALUE('ADB')
             CHGVAR     VAR(%SST(&BIBL 4 3)) VALUE(&FGRP)

/* Overstyrer databasefile GFASPF, da denne filen er lokalisert      */
/* i bibliotek ADBxxx, hvor xxx er filgruppe, og henter deretter     */
/* ut firmaets bankforbindelse i fra faste data, og kopierer         */
/* videre DIRREM over til DIRxx, hvor xx=type bank:                  */

             OVRDBF     FILE(GFASPF) TOFILE(&BIBL/GFASPF)
             CALL       PGM(GL200R) PARM(&FIRM &BANK)

/* Avslutter dersom BYGG=N, d.v.s. DIRxx skal ikke behandles:      */

             IF         COND(&BYGG *EQ 'N') THEN(GOTO CMDLBL(SLUTT))

/* Henter navn på remitteringsfile fra GPCFPF:                       */

             CALL       PGM(GL320C) PARM(&FGRP &FIRM &FREM &FMOT +
                          &FAVR &FOCR &FKTO &FINK &FTFF)

/* Bygger navn på remitteringsfile:                                  */

             CHGVAR     VAR(%SST(&FILE 1 3)) VALUE('DIR')
             CHGVAR     VAR(%SST(&FILE 4 2)) VALUE(&BANK)
/*                                                                   */
/* Bygger remitteringsfile dersom den ikke finnes (fra GPCFPF):      */
/*                                                                   */
/*       IF         COND(&FREM *NE '        ') THEN(DO)              */
/*                                                                   */
/*           CHKOBJ     OBJ(&BIBL/&FREM) OBJTYPE(*FILE)              */
/*           MONMSG     MSGID(CPF9815 CPF9801) EXEC(DO)              */
/*           CPYF       FROMFILE(*LIBL/DIRREM) TOFILE(&BIBL/&FREM) + */
/*                        MBROPT(*REPLACE) CRTFILE(*YES)             */
/*           ENDDO                                                   */
/*                                                                   */
/* Kopierer fra DIRREM:                                              */
/*                                                                   */
/*           CPYF       FROMFILE(*LIBL/DIRREM) TOFILE(&BIBL/&FREM) + */
/*                        MBROPT(*REPLACE) CRTFILE(*NO)              */
/*           MONMSG     MSGID(CPF0000)                               */
/*                                                                   */
/*       ENDDO                                                       */

/* Bygger remitteringsfile dersom den ikke finnes (DIRxx):           */

             CHKOBJ     OBJ(&BIBL/&FILE) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9815 CPF9801) EXEC(DO)
             CPYF       FROMFILE(*LIBL/DIRREM) TOFILE(&BIBL/&FILE) +
                          MBROPT(*REPLACE) CRTFILE(*YES)
             ENDDO

/* Kopierer fra DIRREM til DIRxx:                                    */

             CPYF       FROMFILE(*LIBL/DIRREM) TOFILE(&BIBL/&FILE) +
                          MBROPT(*REPLACE) CRTFILE(*NO)
             MONMSG     MSGID(CPF0000)
 SLUTT:
             ENDPGM
