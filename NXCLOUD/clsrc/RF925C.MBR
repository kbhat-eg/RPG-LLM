
/*    ÅRSRUTINE av (ADBxxx) bibliotek definert i File-GRUPPE reg.  */


             PGM

             DCL        VAR(&FGRP) TYPE(*CHAR) LEN(3)
             DCL        VAR(&WALI) TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&WAPO) TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&MLD1) TYPE(*CHAR) LEN(40)
             DCL        VAR(&MLD2) TYPE(*CHAR) LEN(40)
             DCL        VAR(&MLD3)     TYPE(*CHAR) LEN(50)
             DCL        VAR(&MLD4)     TYPE(*CHAR) LEN(50)
             DCL        VAR(&VALG) TYPE(*CHAR) LEN(4) VALUE(ALLE)
             DCL        VAR(&SVAR)     TYPE(*CHAR) LEN(1)
             DCL        VAR(&IN12)     TYPE(*DEC)  LEN(1 0)

             DCLF       FILE(ADB/AFILPF)

/* Varselbilde med bekreftelse på om jobb skal settes i gang         */


             CALL       PGM(RF926R)    PARM(&VALG &SVAR &IN12)

             IF         COND(&IN12 *EQ 1 ) +
                          THEN(GOTO CMDLBL(ENDPGM))

             IF         COND(&SVAR *EQ ' ' *OR &SVAR *EQ 'J') +
                          THEN(GOTO CMDLBL(VIDERE))
             ELSE       CMD(GOTO CMDLBL(ENDPGM))

VIDERE:

/*   Leser File-gruppe / databibliotek                             */

 LESFIL:     RCVF

/*   Dersom EOF  gå til slutt av rutinen                           */

             MONMSG     MSGID(CPF0864) EXEC(GOTO CMDLBL(ENDPGM))

             CHGCURLIB  CURLIB(&AGBIBL)

/*   Dersom LIB  ikke finnes gå tilbake for å lese ny file-gruppe    */

             MONMSG     MSGID(CPF2110) EXEC(GOTO CMDLBL(LESFIL))
             CHGVAR     VAR(&FGRP) VALUE(%SST(&AGBIBL 4 3))

             IF         COND(&FGRP *EQ '   ') THEN(GOTO CMDLBL(LESFIL))

/*   START av rutiner pr. file-gruppe                                */

/* Varselbilde 1                                                     */

             CHGVAR     VAR(&WALI) VALUE(4)
             CHGVAR     VAR(&WAPO) VALUE(2)
             CHGVAR     VAR(&MLD1) VALUE('Å R S R U T I N E R   *****')
             CHGVAR     VAR(&MLD2) VALUE('File-GRUPPE = xxx  ******* ')
             CHGVAR     VAR(%SST(&MLD2 15 3)) VALUE(&FGRP)
             CALL       PGM(AA006C)  PARM(&WALI &WAPO &MLD1 &MLD2)

/* Call av årsrutiner til batch/demand                               */

             CALL       PGM(RK920C)
             CALL       PGM(RL920C)

/*   Tilbake for å lese neste file-gruppe                          */

             GOTO       CMDLBL(LESFIL)

ENDPGM:      ENDPGM
