             PGM

             DCL        VAR(&LPARAM) TYPE(*CHAR) LEN(120)
             DCL        VAR(&IN03) TYPE(*CHAR) LEN(1)
             DCL        VAR(&BTCH)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&WSID)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&FFIR)  TYPE(*CHAR) LEN(3)
             DCL        VAR(&USER)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&WMM1)  TYPE(*CHAR) LEN(10) VALUE('AUTO    ')
             DCL        VAR(&WALI) TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&WAPO) TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&MLD1) TYPE(*CHAR) LEN(40)
             DCL        VAR(&MLD2) TYPE(*CHAR) LEN(40)

/* Henter info fra LDA                                               */

             RTVDTAARA  DTAARA(*LDA (911 10)) RTNVAR(&USER)
             RTVDTAARA  DTAARA(*LDA (944 03)) RTNVAR(&FFIR)

/* Hent parameter fra autogiro-kontroll                              */

             CHGVAR     VAR(%SST(&WMM1 5 3)) VALUE(&FFIR)

/* Sjekk om det finnes poster for autogiro                           */

             CHKOBJ     OBJ(RKWAPF) OBJTYPE(*FILE) MBR(&WMM1)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             CHGVAR     VAR(&WALI) VALUE(14)
             CHGVAR     VAR(&WAPO) VALUE(2)
             CHGVAR     VAR(&MLD1) VALUE('Autogiro-poster finnes ikke')
             CHGVAR     VAR(&MLD2) VALUE('Rutinen avsluttes!!    ****')
             CALL       PGM(AA006C)  PARM(&WALI &WAPO &MLD1 &MLD2)
             DLYJOB     DLY(5)
             GOTO       CMDLBL(END)
             ENDDO

             OVRDBF     FILE(RKWAPF) MBR(&WMM1)
             CALL       PGM(RK815R) PARM(&LPARAM)

/* Program for printerstyring                                        */

             CALL       PGM(AP600R) PARM('RK821' '1' '0' &IN03 &BTCH)
             IF         COND(&IN03 *EQ '1') THEN(GOTO CMDLBL(END))

/* Sletter gammel autogirofil                                        */

             CLRPFM     FILE(RAUTPF)

/* Call av utskriftsprogram til batch/demand                         */

             IF         COND(&BTCH *EQ '1') THEN(SBMJOB CMD(CALL +
                          PGM(RK821C) PARM(&LPARAM)) JOB(AUTOGIRO) +
                          MSGQ(*NONE))
             ELSE       CMD(CALL PGM(*LIBL/RK821C) +
                         PARM(&LPARAM))

/* Melding dersom bakgrunnsjobb                                      */

             IF         COND(&BTCH *EQ '1') THEN(DO)
             RTVDTAARA  DTAARA(*LDA (901 10)) RTNVAR(&WSID)
             SNDBRKMSG  MSG('AUTOG: Autogiro-uttrekksfil er +
                          er generert') TOMSGQ(&WSID)
             ENDDO

 END:        ENDPGM
