/* Oppretting/oppdatering av tabeller og indekser på bakgrunn av SQL */
/* 090421 bof : utvidet med å takle gen på ADB                   */
             PGM        PARM(&FILE &FGRP &GRP &NULL)

             dcl        var(&ver)  type(*char) len(3) value(' ')
             dcl        var(&sys)  type(*char) len(12)

             DCL        VAR(&FILE) TYPE(*CHAR) LEN(6)
             DCL        VAR(&FGRP) TYPE(*CHAR) LEN(3)
             DCL        VAR(&GRP)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&asp)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&NULL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&MILJ) TYPE(*CHAR) LEN(3) VALUE('   ')

             DCL        VAR(&DLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&smbr) TYPE(*CHAR) LEN(6)
             DCL        VAR(&sfil) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FLST) TYPE(*CHAR) LEN(5)
             DCL        VAR(&TBLE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&INDX) TYPE(*CHAR) LEN(6)

             DCL        VAR(&WCURLI) TYPE(*CHAR) LEN(10)
             DCL        VAR(&debug)  TYPE(*CHAR) LEN(50)

             DCL        VAR(&txt)  type(*CHAR) LEN(100)

             MONMSG     MSGID(CPF2103) /*    Bibl finnes fra før    */

/*  Henter CURLIB                                                             */
             RTVJOBA    CURLIB(&WCURLI)
/*  Finner ut i hvilke bibliotek filen skal ligge           */
             call       pgm(getdlib)  parm(&file &dlib &sfil &fgrp)

             CHGCURLIB  CURLIB(&DLIB)

/*  databibliotek ADBxxx må ligge i biblisten. AG918C kan kalles med          */
/*  FGRP blank, som betyr ADB. Derfor må current library                      */
/*   legges     i listen                                                      */
             ADDLIBLE   LIB(&WCURLI)

             CHGVAR     VAR(&smbr) VALUE(%SUBSTRING(&FILE 1 4) *CAT 'SQ')
             CHGVAR     VAR(&FLST) VALUE(%SUBSTRING(&FILE 1 4) *CAT '*')
             CHGVAR     VAR(&INDX) VALUE(%SUBSTRING(&FILE 1 4) *CAT 'I*')
             CHGVAR     VAR(%SST(&TBLE 1 6)) VALUE(&FILE)

/* Finner hvilken versjon vi kjører                                  */

             rtvneta    sysname(&sys)
             call       pgm(ar701r) parm(&sys &fgrp &ver)

/* Sletter indexer                                                   */
             DLTF       FILE(&DLIB/&INDX)
             MONMSG     MSGID(CPF2125)
/* Slett triggere på tabell                                          */
             CALL       PGM(nxTRGDLT) PARM(&dlib &TBLE &ver)
/* Slett indexer  på tabell                                          */
             CALL       PGM(nxIDXDLT) PARM(&dlib ' ' ' ' &TBLE &ver)
/* Slett views    på tabell                                          */
             CALL       PGM(nxVIEWDLT) PARM(&dlib &TBLE &ver)

/*  Sletter ev overlevde indexer     */
             call       pgm(ag906dc)  parm(&file)
/*  Sletter ev overlevde indexer    NB!!!  Må kjøres 2 ganger   */
             call       pgm(ag906dc)  parm(&file)

/* Kjører SQL-statements for oppretting av tabell og indekser        */
 ADB:              RUNSQLSTM  SRCFILE(*LIBL/&sfil) SRCMBR(&smbr) +
                                NAMING(*SYS) DECMPT(*COMMA) +
                                SRTSEQ(*LANGIDUNQ) LANGID(NOR) +
                                commit(*none)

/* Legg på gruppe rettigheter                                        */

           chgvar     var(&asp)     value('ASP_' *cat &fgrp)
           rvkobjaut  obj(&dlib/&flst) objtype(*file) user(*all) +
                          aut(*all)
           rvkobjaut obj(&dlib/&flst) objtype(*file) user(qdftown) aut(*all)
           rvkobjaut obj(&dlib/&flst) objtype(*file) user(&asp) aut(*all)
           grtobjaut obj(&dlib/&flst) objtype(*file) user(&grp) aut(*all)
           grtobjaut obj(&dlib/&flst) objtype(*file) user(*public) aut(*exclude)
           rtvusrprf usrprf(&asp) grpprf(&grp)
           grtobjaut obj(&dlib/&flst) objtype(*file) user(&grp) aut(*all)

           chgobjown obj(&dlib/&file) objtype(*file) newown(&grp)

/* Journalfør tabellen                                               */
             STRJRNPF   FILE(&DLIB/&FILE) JRN(&DLIB/BUTIJRN) +
                          OMTJRNE(*OPNCLO)
             MONMSG     MSGID(CPF9801) /* BUTIJRN finnes ikke */
             MONMSG     MSGID(CPF7030) /*  already being journaled  */

/* Legg inn triggere på tabell                                       */
            CALL   PGM(nxTRGCRT) PARM(&dlib &TBLE &ver)
/* Legg inn indexer  på tabell                                       */
             CALL   PGM(nxIDXCRT) PARM(&dlib &fgrp ' ' ' ' &TBLE &ver)

             CHGCURLIB  CURLIB(&wcurli)
             commit
             monmsg     msgid(cpf8350)

 END:        ENDPGM
