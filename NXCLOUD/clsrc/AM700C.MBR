/*                                                                   */
/*-------------------------------------------------------------------*/
/* 250202 MHA:                                                       */
/*                                                                   */
             PGM        PARM(&SPOOLNAME &SENDTYPE &SENDNAME +
                         &TIL &FRA &SUBJ &STATUS)

/* Parametre                                                         */
             DCL        VAR(&SPOOLNAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SENDNAME)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&TIL)       TYPE(*CHAR) LEN(45)
             DCL        VAR(&FRA)       TYPE(*CHAR) LEN(45)
             DCL        VAR(&SUBJ)      TYPE(*CHAR) LEN(250)
             DCL        VAR(&SENDTYPE)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&STATUS)    TYPE(*CHAR) LEN(1)

/* Diverse variabler....                                             */
             DCL        VAR(&FRAFIL)    TYPE(*CHAR) LEN(100) VALUE(*BLANK)
             DCL        VAR(&TILFIL)    TYPE(*CHAR) LEN(100) VALUE(*BLANK)
             DCL        VAR(&MBRNAM)    TYPE(*CHAR) LEN(10)  VALUE(*BLANK)
             DCL        VAR(&FIRMA)     TYPE(*DEC)  LEN(3 0)
             DCL        VAR(&FIRMA2)    TYPE(*CHAR) LEN(3)
             DCL        VAR(&SERV)      TYPE(*CHAR) LEN(35)
             DCL        VAR(&JOBNAME)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&USRNAME)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOBNUMBER) TYPE(*CHAR) LEN(6)

/* Fil som inneholder mailserver på firma                            */
             DCLF       FILE(AMSRL1)

/* Henter ut jobb attributter som brukes for å hente ut riktig spool */
             RTVJOBA    JOB(&JOBNAME) USER(&USRNAME) NBR(&JOBNUMBER)

/* Henter firma fra LDA                                              */
             RTVDTAARA  DTAARA(*LDA (944 03)) RTNVAR(&FIRMA2)
             CHGVAR     VAR(&FIRMA) VALUE(&FIRMA2)

/* Sjekker om midlertidig spoolfile base finnes i ADB                */
             CHKOBJ     OBJ(ADB/ASPLPF) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
             CRTPF      FILE(ADB/ASPLPF) RCDLEN(120) MBR(*NONE) +
                          TEXT('Midlertidig lager for spoolfiles') +
                          MAXMBRS(*NOMAX)
             ENDDO

/* Kopierer spoolfile inn i midlertidig splf. base i ADB             */
             CHGVAR     VAR(&MBRNAM) VALUE('M' *TCAT +
                          &JOBNUMBER)
             CPYSPLF    FILE(&SPOOLNAME) TOFILE(ADB/ASPLPF) +
                          JOB(&JOBNUMBER/&USRNAME/&JOBNAME) +
                          SPLNBR(*LAST) TOMBR(&MBRNAM)
             MONMSG     MSGID(CPF0000) EXEC(DO)
             CHGVAR     VAR(&STATUS) VALUE('2')
             GOTO       CMDLBL(ENDPGM)
             ENDDO

/* Lager katalogen /TEMP på IFS hvis den ikke allerede finnes        */
             CRTDIR     DIR('/temp') DTAAUT(*RWX) OBJAUT(*ALL)
             MONMSG     MSGID(CPF0000)
/* Kopierer spoolfilen fra midl. splf. base til /TEMP på IFS         */
             CHGVAR     VAR(&TILFIL) VALUE('/temp/' *TCAT &MBRNAM)
             CHGVAR     VAR(&FRAFIL) VALUE('/qsys.lib/ADB.lib/+
                         ASPLPF.file/' *TCAT +
                         &MBRNAM *TCAT '.mbr')
             CPYTOSTMF  FROMMBR(&FRAFIL) TOSTMF(&TILFIL) +
                          STMFOPT(*REPLACE) STMFCODPAG(*PCASCII)
             MONMSG     MSGID(CPF0000) EXEC(DO)
             CHGVAR     VAR(&STATUS) VALUE('3')
             GOTO       CMDLBL(ENDPGM)
             ENDDO

/* Rydder opp i midlertidig spoolfile base i ADB                     */
             RMVM       FILE(ADB/ASPLPF) MBR(&MBRNAM)

/* Henter ut mailserver fra database basert på firma                 */
 LESFIL:     RCVF
             MONMSG     MSGID(CPF0000) EXEC(DO)
             CHGVAR     VAR(&STATUS) VALUE('4')
             GOTO       CMDLBL(ENDPGM)
             ENDDO
             IF         COND(&AMMFIR *eq &FIRMA) THEN(DO)
             CHGVAR     VAR(&SERV) VALUE(&AMMSVR)
             GOTO       CMDLBL(ENDLES)
             ENDDO
             GOTO       CMDLBL(LESFIL)
 ENDLES:

/* Sender mail med vedlegget i /TEMP                                 */
             SNDMAIL    SERVER(&SERV) RECIPIENT(&TIL) FROM(&FRA) +
                          ATTACH(&TILFIL) ATTTYPE(&SENDTYPE) +
                          SUBJECT(&SUBJ) MESSAGE(' ') SNDNAM(&SENDNAME)
/*                                                                   */
 ENDPGM:     ENDPGM
