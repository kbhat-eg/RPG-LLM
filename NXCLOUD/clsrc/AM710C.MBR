             PGM

             DCLF       FILE(AM710D)
             DCL        VAR(&FNAV)     TYPE(*CHAR) LEN(30)
             DCL        VAR(&MAILSERV) TYPE(*CHAR) LEN(35)
             DCL        VAR(&STATUS)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&USER)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&NAVN)     TYPE(*CHAR) LEN(20)
             DCL        VAR(&MAILADR)  TYPE(*CHAR) LEN(50)
             DCL        VAR(&STDADR)   TYPE(*CHAR) LEN(50) +
                                       VALUE('post@firma.no')
             DCL        VAR(&JAVA)     TYPE(*CHAR) LEN(1000)
             DCL        VAR(&JOBN)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&LOGN)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&ERRN)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&STS)      TYPE(*CHAR) LEN(1) VALUE('1')
             DCL        VAR(&EML1)     TYPE(*CHAR) LEN(30)
             DCL        VAR(&EML2)     TYPE(*CHAR) LEN(30)
             DCL        VAR(&POSX)     TYPE(*DEC) LEN(2 0) VALUE(5)
             DCL        VAR(&POSY)     TYPE(*DEC) LEN(2 0) VALUE(5)

             RTVDTAARA  DTAARA(*LDA (951 30)) RTNVAR(&FNAV)
             CHGVAR     VAR(&L_FNAV)   VALUE(&FNAV)

             RTVJOBA    JOB(&JOBN) USER(&USER)

 START:      SNDRCVF    RCDFMT(C2BLD)

             IF         COND(&IN03) THEN(DO)
             GOTO       CMDLBL(END)
             ENDDO

 /* Begynner sendingsprosessen                                               */

             CHGVAR     VAR(&EML1) VALUE('Sender mail...')
             CHGVAR     VAR(&EML2) VALUE('Vennligst vent et øyeblikk.')
             CALL       PGM(AA006C) PARM(&POSX &POSY &EML1 &EML2)
             MONMSG     MSGID(CPF0000)

 /* Henter ut mailserver                                              */

             CALL       PGM(AM705C) PARM(&MAILSERV &STATUS)
             IF         COND(&STATUS *NE ' ') THEN(DO)
             CHGVAR     VAR(&STS) VALUE('S')
             GOTO       CMDLBL(ERROR)
             ENDDO

 /* Henter mailadresse på selgeren                                         */

             CALL       PGM(FB710R) PARM(&USER &NAVN &MAILADR)
             IF         COND('X' *TCAT &MAILADR *EQ 'X') THEN(DO)
/*           CHGVAR     VAR(&STS) VALUE('A')                       */
/*           GOTO       CMDLBL(ERROR)                              */
             CHGVAR     VAR(&MAILADR) VALUE(&STDADR)
             ENDDO

             CHGVAR     VAR(&LOGN) VALUE('L' *TCAT &JOBN)
             CHGVAR     VAR(&ERRN) VALUE('E' *TCAT &JOBN)

             RMVM       FILE(ADB/ALOGPF) MBR(&ERRN)
             MONMSG     MSGID(CPF0000)

             CLRPFM     FILE(ADB/ALOGPF) MBR(&LOGN)
             MONMSG     MSGID(CPF0000) EXEC(ADDPFM FILE(ADB/ALOGPF) +
                          MBR(&LOGN) TEXT('Javalogg for mailsending'))

             CHGVAR     VAR(&JAVA) VALUE('java asp.mail.SendMail2 +
                          -server' *TCAT &MAILSERV *BCAT '-from' +
                          *TCAT &MAILADR *BCAT '"-subject' *TCAT +
                          &C2SUBJ *TCAT '" -errFileadb/alogpf.' +
                          *TCAT &ERRN)

/* Legge til mottagere                                                       */
             IF         COND('X' *TCAT &C2USR1 *NE 'X') THEN(DO)
             CHGVAR     VAR(&JAVA) VALUE(&JAVA *BCAT '-to' *TCAT +
                          &C2USR1)
             ENDDO
             IF         COND('X' *TCAT &C2USR2 *NE 'X') THEN(DO)
             CHGVAR     VAR(&JAVA) VALUE(&JAVA *BCAT '-to' *TCAT +
                          &C2USR2)
             ENDDO
             IF         COND('X' *TCAT &C2USR3 *NE 'X') THEN(DO)
             CHGVAR     VAR(&JAVA) VALUE(&JAVA *BCAT '-to' *TCAT +
                          &C2USR3)
             ENDDO

/* Legge til tekst                                                           */
             CHGVAR     VAR(&JAVA) VALUE(&JAVA *BCAT '"-text' *TCAT +
                          &C2MLD1 *TCAT '"')
             CHGVAR     VAR(&JAVA) VALUE(&JAVA *BCAT '"-text' *TCAT +
                          &C2MLD2 *TCAT '"')
             CHGVAR     VAR(&JAVA) VALUE(&JAVA *BCAT '"-text' *TCAT +
                          &C2MLD3 *TCAT '"')
             CHGVAR     VAR(&JAVA) VALUE(&JAVA *BCAT '"-text' *TCAT +
                          &C2MLD4 *TCAT '"')
             CHGVAR     VAR(&JAVA) VALUE(&JAVA *BCAT '"-text' *TCAT +
                          &C2MLD5 *TCAT '"')
             CHGVAR     VAR(&JAVA) VALUE(&JAVA *BCAT '"-text' *TCAT +
                          &C2MLD6 *TCAT '"')
             CHGVAR     VAR(&JAVA) VALUE(&JAVA *BCAT '"-text' *TCAT +
                          &C2MLD7 *TCAT '"')
             CHGVAR     VAR(&JAVA) VALUE(&JAVA *BCAT '"-text' *TCAT +
                          &C2MLD8 *TCAT '"')
             CHGVAR     VAR(&JAVA) VALUE(&JAVA *BCAT '"-text' *TCAT +
                          &C2MLD9 *TCAT '"')

             CHGVAR     VAR(&JAVA) VALUE(&JAVA *BCAT '"-textVennlig +
                          hilsen"' *BCAT '"-text' *TCAT &NAVN +
                          *TCAT '"')

             OVRDBF     FILE(STDOUT) TOFILE(ADB/ALOGPF) MBR(&LOGN)
             QSH        CMD(&JAVA)
             DLTOVR     FILE(STDOUT)

             RMVM       FILE(ADB/ALOGPF) MBR(&ERRN)
             MONMSG     MSGID(CPF0000) EXEC(DO)
             CHGVAR     VAR(&STS) VALUE('J')
             GOTO       CMDLBL(ERROR)
             ENDDO

             RMVM       FILE(ADB/ALOGPF) MBR(&LOGN)

 END:        RETURN

 ERROR:
             CHGVAR     VAR(&C2ERR1) VALUE('Error...')
             CHGVAR     VAR(&C2ERR2) VALUE('Noe gikk galt med jobben')
             IF         COND(&STS *EQ 'S') THEN(DO)
             CHGVAR     VAR(&C2ERR1) VALUE('Kunne ikke hente ut')
             CHGVAR     VAR(&C2ERR2) VALUE('mailserver fra database')
             ENDDO
             IF         COND(&STS *EQ 'A') THEN(DO)
             CHGVAR     VAR(&C2ERR1) VALUE('Kunne ikke hente ut adresse')
             CHGVAR     VAR(&C2ERR2) VALUE('for selger fra database')
             ENDDO
             IF         COND(&STS *EQ 'J') THEN(DO)
             CHGVAR     VAR(&C2ERR1) VALUE('Kunne ikke kjøre Java')
             CHGVAR     VAR(&C2ERR2) VALUE('program for mailsending')
             ENDDO

             CHGVAR     VAR(&W_ALIN) VALUE(3)
             CHGVAR     VAR(&W_ACOL) VALUE(3)
             SNDRCVF    RCDFMT(C2MSG)

             ENDPGM
