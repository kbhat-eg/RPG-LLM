
/*   Kunder, oppdatering av saldo/kjøp                           */

             PGM

             DCL        VAR(&FGRP)     TYPE(*CHAR) LEN(3)
             DCL        VAR(&BIBL)     TYPE(*CHAR) LEN(6)
             DCL        VAR(&FIRM)     TYPE(*CHAR) LEN(3)
             DCL        VAR(&FNAV)     TYPE(*CHAR) LEN(30)
             DCL        VAR(&MLD1)     TYPE(*CHAR) LEN(40)
             DCL        VAR(&MLD2)     TYPE(*CHAR) LEN(40)
             DCL        VAR(&MLD3)     TYPE(*CHAR) LEN(50)
             DCL        VAR(&MLD4)     TYPE(*CHAR) LEN(50)
             DCL        VAR(&SVAR)     TYPE(*CHAR) LEN(1)
             DCL        VAR(&IN03)     TYPE(*CHAR) LEN(1)
             DCL        VAR(&IN12)     TYPE(*DEC)  LEN(1 0)
             DCL        VAR(&WALI)     TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&WAPO)     TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&sp)       TYPE(*char) LEN(1)
             DCL        VAR(&rut)      TYPE(*char) LEN(50)

/* Hente inn filgruppe/ databibliotek fra LDA                        */

             RTVDTAARA  DTAARA(*LDA (931 03)) RTNVAR(&FGRP)
             RTVDTAARA  DTAARA(*LDA (934  6)) RTNVAR(&BIBL)
             RTVDTAARA  DTAARA(*LDA (944  3)) RTNVAR(&FIRM)
             RTVDTAARA  DTAARA(*LDA (951  30)) RTNVAR(&FNAV)

/*  Er denne rutina sperret?                                */
             chgvar    var(&rut)        value('SPERRE_BYGGE_SALDO_KUNDE')
             call      pgm(TestSperre)  parm(&sp &rut)
             if        cond(&sp *eq '1') then(do)
               call    pgm(aa011c)  parm(5  10 +
                               'Du kan ikke kjøre denne rutina' +
                               'Trykk Enter/Ok Jobben avbrytes')
               goto    endpgm
             enddo

/*   Send parameterbilde                                             */

             CALL       PGM(RK970R) PARM(&IN03)

             IF         COND(&IN03 *EQ '1') THEN(GOTO CMDLBL(ENDPGM))

/* Varselbilde med bekreftelse på om jobb skal settes i gang         */

             CHGVAR     VAR(&MLD3) +
                        VALUE('                                   oppdateres')
             CHGVAR     VAR(&MLD4) +
                        VALUE('       Bekreft at dette er riktig !')
             CHGVAR     VAR(%SST(&MLD3 1 3)) VALUE(&FIRM)
             CHGVAR     VAR(%SST(&MLD3 5 30)) VALUE(&FNAV)

             CALL       PGM(AA007R)    PARM(&MLD3 &MLD4 &SVAR &IN12)

             IF         COND(&IN12 *EQ 1 ) +
                          THEN(GOTO CMDLBL(ENDPGM))

             IF         COND(&SVAR *EQ ' ' *OR &SVAR *EQ 'J') +
                          THEN(GOTO CMDLBL(VIDERE))
             ELSE       CMD(GOTO CMDLBL(ENDPGM))

VIDERE:


/*   START av program for oppdatering                                */
/*   ----------------------------------------                        */

/* Varselbilde 1                                                     */

             CHGVAR     VAR(&WALI) VALUE(4)
             CHGVAR     VAR(&WAPO) VALUE(2)
             CHGVAR     VAR(&MLD1) VALUE('Vent litt ................ ')
             CHGVAR     VAR(&MLD2) VALUE('oppdatering pågår          ')
             CALL       PGM(AA006C)  PARM(&WALI &WAPO &MLD1 &MLD2)

/*   Oppdatering av saldo/kjøp                                       */

             CALL       PGM(RK971R)

ENDPGM:      ENDPGM
