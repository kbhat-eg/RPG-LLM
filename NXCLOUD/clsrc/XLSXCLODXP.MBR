             PGM        PARM(&SQLSTMT &POST_LINK &PREFIX_FIL)
/*************************************************************************/
/* 8.01 011122 bsa Fjernet endring av tilgang, da tilgangen allerede er  */
/* 8.01            den samme som blir forsøkt satt. Skaper feilmeldinger */
/* 8.01            siden brukerne ikke har spesialautorisasjoner.        */
/*                                                                       */
/*                                                                       */
/*                                                                       */
/*************************************************************************/

             DCL        VAR(&POST_LINK) TYPE(*CHAR) LEN(100)
             DCL        VAR(&PREFIX_FIL)    TYPE(*CHAR) LEN(20)

             DCL        VAR(&SQLSTMT)  TYPE(*CHAR) LEN(10000)
             DCL        VAR(&MAPPE)    TYPE(*CHAR) LEN(30) +
                          VALUE('EXCEL_COOLSPOOL')
             DCL        VAR(&XLSX_FIL)    TYPE(*CHAR) LEN(50)
             DCL        VAR(&XLSX_FILP)    TYPE(*CHAR) LEN(100)
             DCL        VAR(&EXP_FIL)    TYPE(*CHAR) LEN(100)
             DCL        VAR(&TYPE)     TYPE(*CHAR) LEN(4) VALUE('XLSX')
             DCL        VAR(&FILGRP) TYPE(*CHAR) LEN(3) VALUE('   ')
             DCL        VAR(&LINK)    TYPE(*CHAR) LEN(256)
             DCL        VAR(&EXP_MAPPE)    TYPE(*CHAR) LEN(30) +
                          VALUE('/home/NX_EXCEL/')

/*   Generer random tall                                                      */
             DCL        VAR(&SYSTIME) TYPE(*CHAR) LEN(20)
             Dcl        VAR(&RANNO) TYPE(*CHAR) LEN(7)
             RTVSYSVAL  SYSVAL(QDATETIME) RTNVAR(&SYSTIME)
             CHGVAR     VAR(&RANNO) VALUE(%SST(&SYSTIME 9 7))

/* Hente inn fra LDA                                                          */
             RTVDTAARA  DTAARA(*LDA (931 3)) RTNVAR(&FILGRP)


/*   Lager random navn på filen MAX 45 TEGN + .xlsx = 50 tegn                 */
             CHGVAR     VAR(&XLSX_FIL) VALUE(&PREFIX_FIL *tcat &RANNO)
             DLYJOB     DLY(1)
             RTVSYSVAL  SYSVAL(QDATETIME) RTNVAR(&SYSTIME)
             CHGVAR     VAR(&RANNO) VALUE(%SST(&SYSTIME 9 7))
             CHGVAR     VAR(&XLSX_FIL) VALUE(&XLSX_FIL *tcat &FILGRP *tcat +
                          &RANNO )

/*   Lager XLSX filen i /asp/filgruppe/&mappe                                 */
             CALL       PGM(ASPTOXLSX) PARM(&SQLSTMT &MAPPE &XLSX_FIL +
                          &TYPE)

 /*  Filen ble  laget under /ASP/"FGR", feks /ASP/H01/EXCEL_COOLSPOOL         */
             CHGVAR     VAR(&XLSX_FILP) VALUE('/asp/' *tcat  &FILGRP *tcat +
                          '/' *tcat &MAPPE *tcat '/' *tcat &XLSX_FIL )

/*   FLYTT TIL CLOUD EXPORT KATALOG. fjerner evt fil som finnes fra før       */
             CHGVAR     VAR(&EXP_FIL) VALUE(&EXP_MAPPE *tcat &XLSX_FIL )
             RMVLNK     OBJLNK(&EXP_FIL)
             MONMSG     MSGID(CPFA0A9)
             MOV        OBJ(&XLSX_FILP) TODIR(&EXP_MAPPE)

/*8.01   ENDRE RETTIGHETER                                                    */
/*8.01       CHGAUT     OBJ(&EXP_FIL) USER(*PUBLIC) DTAAUT(*R) +              */
/*8.01                    OBJAUT(*NONE)                                       */

/*   Viser link i grønt bilde  PCO                                            */
             CHGVAR     VAR(&LINK) VALUE(&POST_LINK *tcat &XLSX_FIL )
             CALL       AP628C PARM(&LINK)

 END:        ENDPGM
