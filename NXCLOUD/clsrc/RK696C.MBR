             PGM        PARM(&PROG)

/* Initiering av parametre.                                          */

             DCL        VAR(&PROG)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&RAPP)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&IN03)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&BTCH)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&MEMBX) TYPE(*CHAR) LEN(5)
             DCL        VAR(&MEMB)  TYPE(*DEC)  LEN(5 0)
             DCL        VAR(&WMM1)  TYPE(*CHAR) LEN(10) VALUE('RKA       ')
             DCL        VAR(&WALI)  TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&WAPO)  TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&MLD1)  TYPE(*CHAR) LEN(40)
             DCL        VAR(&MLD2)  TYPE(*CHAR) LEN(40)
             DCL        VAR(&PSKR)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&OCRK)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&BGIR)  TYPE(*CHAR) LEN(1)

/* Henter verdier i fra LDA.                                         */

             RTVDTAARA  DTAARA(*LDA (863 1))  RTNVAR(&BTCH)
             RTVDTAARA  DTAARA(*LDA (864 5))  RTNVAR(&MEMBX)
             RTVDTAARA  DTAARA(*LDA (881 1))  RTNVAR(&BGIR)

             CHGVAR     VAR(&MEMB) VALUE(&MEMBX)
             CHGVAR     VAR(%SST(&WMM1 4 5)) VALUE(&MEMB)

             IF         COND(&PROG *EQ 'RK696     ') THEN(DO)
             CHGVAR     VAR(&RAPP) VALUE(RK692)
             ENDDO
             IF         COND(&PROG *EQ 'RK697     ') THEN(DO)
             CHGVAR     VAR(&RAPP) VALUE(RK694)
             ENDDO

             CHGVAR     VAR(%SST(&RAPP 6 1)) VALUE(P)

             CHGVAR     VAR(%SST(&PROG 6 1)) VALUE(R)

/* Varselbilde 2                                                     */

             IF         COND(&BTCH *NE '1') THEN(DO)
             CHGVAR     VAR(&WALI) VALUE(6)
             CHGVAR     VAR(&WAPO) VALUE(40)
             CHGVAR     VAR(&MLD1) VALUE('Vennligst vent litt .....')
             CHGVAR     VAR(&MLD2) VALUE('Utskrift av generalnota +
                          kopi')
             CALL       PGM(AA006C)  PARM(&WALI &WAPO &MLD1 &MLD2)
             ENDDO

/* Bygger membere dersom de ikke finnes                              */

             CHKOBJ     OBJ(RGIRPF) OBJTYPE(*FILE) MBR(&WMM1)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDPFM     FILE(RGIRPF) MBR(&WMM1) TEXT('Giroutskrift +
                          fra generalnota (RK696R)')
             ENDDO

/* Call av program, utskrift av generalnota kopi                     */

/* Overstyring av printerfile                                        */

             CALL       PGM(AP601C)    PARM(&RAPP &BTCH)

             OVRDBF     FILE(RGIRPF) MBR(&WMM1)
             CALL       PGM(&PROG)

             CALL       PGM(AP606C)    PARM(&RAPP)

/* Egen giro skal skrives ?!?                                        */

             IF         COND(&BGIR *EQ '1') THEN(DO)
             CALL       PGM(RK617C)
             ENDDO

             DLTOVR     FILE(RGIRPF)
             RMVM       FILE(*LIBL/RGIRPF) MBR(&WMM1)
             MONMSG     MSGID(CPF3200)
 END:        ENDPGM
