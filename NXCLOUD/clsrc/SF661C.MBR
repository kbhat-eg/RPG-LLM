             PGM        PARM(&TYPE &WMM1)

/* Definere parametere og variabler                                  */

             DCL        VAR(&BTCH)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&TYPE)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&KODE)  TYPE(*CHAR) LEN(1) VALUE(' ')
             DCL        VAR(&WSID)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&WALI)  TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&WAPO)  TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&MLD1)  TYPE(*CHAR) LEN(40)
             DCL        VAR(&MLD2)  TYPE(*CHAR) LEN(40)
             DCL        VAR(&WMM1)  TYPE(*CHAR) LEN(10)

/* Henter verdier i fra LDA.                                         */

             RTVDTAARA  DTAARA(*LDA (863 1))  RTNVAR(&BTCH)

/* Overstyring av printerfile                                        */

             CALL       PGM(AP601C)    PARM('SF661P' &BTCH)

/* Varselbilde                                                       */

             IF         COND(&BTCH *NE '1') THEN(DO)
             CHGVAR     VAR(&WALI) VALUE(6)
             CHGVAR     VAR(&WAPO) VALUE(30)
             CHGVAR     VAR(&MLD1) VALUE('Vennligst vent litt......')
             CHGVAR     VAR(&MLD2) VALUE('Utplukk p책g책r ...........')
             CALL       PGM(AA006C)  PARM(&WALI &WAPO &MLD1 &MLD2)
             ENDDO

/* Sletter poster i arbeidsregister                                 */

             CLRPFM     FILE(SKWWPF) MBR(&WMM1)
             MONMSG     MSGID(CPF3100)

/* Sletter poster i Statistikk-PC-Register (member)                  */

             CLRPFM     FILE(SKPCPF) MBR(&TYPE)
             MONMSG     MSGID(CPF3100)

/* Sletter poster i Statistikk-PC-Register (member)                  */

             CLRPFM     FILE(SKPWPF) MBR(&TYPE)
             MONMSG     MSGID(CPF3100)

/* Bygger membere dersom de ikke finnes                              */

             CHKOBJ     OBJ(SKWWPF) OBJTYPE(*FILE) MBR(&WMM1)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDPFM     FILE(SKWWPF) MBR(&WMM1) TEXT('Poster til +
                          statistikk')
             ENDDO

/* Bygger Statistikk-PC-Register (member) dersom ikke finnes         */

             CHKOBJ     OBJ(SKPCPF) OBJTYPE(*FILE) MBR(&TYPE)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDPFM     FILE(SKPCPF) MBR(&TYPE) +
                          TEXT('Statistikk-PC-Register ')
             ENDDO

/* Bygger Statistikk-PC-Register (member) dersom ikke finnes         */

             CHKOBJ     OBJ(SKPWPF) OBJTYPE(*FILE) MBR(&TYPE)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDPFM     FILE(SKPWPF) MBR(&TYPE) +
                          TEXT('Statistikk-PC-Register ')
             ENDDO

/* Kopier parameter                                                  */

             OVRDBF     FILE(SKPWPF) MBR(&TYPE)
             CALL       PGM(SF610R) PARM(&TYPE)

             OVRDBF     FILE(SKWWPF) MBR(&WMM1)
             OVRDBF     FILE(SKPWPF) MBR(&TYPE)
             CALL       PGM(SF600R) PARM(&TYPE &KODE)

/* Varselbilde 2                                                     */

             IF         COND(&BTCH *NE '1') THEN(DO)
             CHGVAR     VAR(&WALI) VALUE(8)
             CHGVAR     VAR(&WAPO) VALUE(26)
             CHGVAR     VAR(&MLD1) VALUE('Vennligst vent litt......')
             CHGVAR     VAR(&MLD2) VALUE('Utskrift p책g책r...........')
             CALL       PGM(AA006C)  PARM(&WALI &WAPO &MLD1 &MLD2)
             ENDDO

/* Utskrift                                                          */

             OVRDBF     FILE(SKWWPF) MBR(&WMM1)
             OVRDBF     FILE(SKPCPF) MBR(&TYPE)
             CALL       PGM(SF661R) PARM(&TYPE)

             CALL       PGM(AP606C)    PARM('SF661P')

/* Slett ?                                                           */

             CALL       PGM(SF699R) PARM(&TYPE)
/*                                                                   */
             DLTOVR     FILE(SKWWPF)
             DLTOVR     FILE(SKPCPF)
             RMVM       FILE(SKWWPF) MBR(&WMM1)
             MONMSG     MSGID(CPF3200)
/*                                                                   */
             DLTOVR     FILE(SKPWPF)
             RMVM       FILE(SKPWPF) MBR(&TYPE)
             MONMSG     MSGID(CPF3200)

/* Melding dersom bakgrunnsjobb                                      */

             IF         COND(&BTCH *EQ '1') THEN(DO)
             RTVDTAARA  DTAARA(*LDA (901 10)) RTNVAR(&WSID)
             SNDMSG     MSG('ASSTAT  Rutinen for utskrift av stati+
                        stikk til PC er ferdig') TOMSGQ(&WSID)
             ENDDO
 END:        ENDPGM
