/*                                                                  */
/*                                                                  */
/*                                                                  */
       pgm             parm(&fgr)

             dcl       var(&ml1)  type(*char) len(50)
             dcl       var(&ml2)  type(*char) len(50)
             dcl       var(&sv)   type(*char) len(1)   value('J')
             dcl       var(&in12) type(*char) len(1)
             dcl       var(&tix)  type(*char) len(4)   value('0600')
             dcl       var(&slib) type(*char) len(10) value('ASPGPL')
             dcl       var(&usr)  type(*char) len(10) value('ASP_')
             dcl       var(&fgr)  type(*char) len(3)
             dcl       var(&sbs)  type(*char) len(10)  value('ASBSxxxMP')
             dcl       var(&jbq)  type(*char) len(10)  value('JOBQxxxMP')
             dcl       var(&jbd)  type(*char) len(10)  value('xxxMalProf')
             dcl       var(&jse)  type(*char) len(10)  value('xxxMalProf')
             dcl       var(&dir)  type(*CHAR) len(60)  +
                                              value('\asp\800\MalProff\XLB*')
             dcl       var(&lib)  type(*char) len(10)  value('ADBxxx')
             dcl       var(&d)    type(*CHAR) len(30)
             dcl       var(&grp) type(*CHAR) len(10)  value('GRP_xxx')

             chgvar    var(%sst(&sbs 5 3)) value(&fgr)
             chgvar    var(%sst(&jbq 5 3)) value(&fgr)
             chgvar    var(%sst(&jbd 1 3)) value(&fgr)
             chgvar    var(%sst(&jse 1 3)) value(&fgr)
             chgvar    var(%sst(&grp 5 3)) value(&fgr)

/*  Bygg ev directories og tildel autorisasjon                       */
             chgvar     var(%sst(&lib 4 3))  value(&fgr)
    /*       chgvar     var(%sst(&slib 5 3)) value(&fgr)      */
             chgvar     var(%sst(&dir 6 3))  value(&fgr)
             chgvar     var(%sst(&usr 5 3))  value(&fgr)

             chgvar     var(&d)  value(%sst(&dir 1 4))
             crtdir     dir(&d)  dtaaut(*RWX) objaut(*none)
             monmsg     msgid(cpf0000)
             chgvar     var(&d)  value(%sst(&dir 1 8))
             crtdir     dir(&d)  dtaaut(*RWX) objaut(*none)
             monmsg     msgid(cpf0000)
             chgvar     var(&d)  value(%sst(&dir 1 17))
             crtdir     dir(&d)  dtaaut(*RWX) objaut(*none)
             monmsg     msgid(cpf0000)
             chgvar     var(&d)  value(%sst(&dir 1 17) *cat '\Backup')
             crtdir     dir(&d)  dtaaut(*RWX) objaut(*none)
             monmsg     msgid(cpf0000)

             endsbs     sbs(&sbs)
             monmsg     msgid(cpf0000)
             dltsbsd    sbsd(&slib/&sbs)
             monmsg     msgid(cpf0000)
             crtsbsd    sbsd(&slib/&sbs) pools((1 *base)) +
                          text('Subsystem for overf. fra MalProff') +
                          maxjobs(2)
             monmsg     msgid(cpf1696)
strsb:       strsbs     sbsd(&slib/&sbs)
             monmsg     msgid(cpf1010) exec(do)
               dlyjob   dly(30)
               goto     cmdlbl(strsb)
             enddo
/*  NB!!!  AM00 -7-1-1-F6  Sette inn at delsystem startes under IPL */
/*           Gjøres nå automatisk her i rmp001R                     */
             call       pgm(rmp001r)

             dltjobq    jobq(&slib/&jbq)
             monmsg     msgid(cpf0000)
             crtjobq    jobq(&slib/&jbq) text('Jobbkø for +
                          overf. fra MalProff')
             monmsg     msgid(cpf3323)
             dltcls     cls(&slib/qbatch)
             monmsg     msgid(cpf0000)
             crtcls     cls(&slib/qbatch)
             addjobqe   sbsd(&slib/&sbs) +
                          jobq(&slib/&jbq)      maxact(3) +
                          maxpty1(0) maxpty2(0) maxpty3(0) +
                          maxpty4(1) maxpty5(0) maxpty6(3) +
                          maxpty7(0) maxpty8(0) maxpty9(0)
             monmsg     msgid(cpf1535 cpf1697)
  /*         addrtge    sbsd(&slib/MalProff) seqnbr(10) +     */
             addrtge    sbsd(&slib/&sbs) seqnbr(10) +
                          cmpval(*any) pgm(qsys/qcmd) +
                          cls(&slib/qbatch)
          /* monmsg     msgid(cpf1697)  */
             dltjobd    jobd(&slib/&jbd)
             monmsg     msgid(cpf0000)
             crtjobd    jobd(&slib/&jbd) +
                          jobq(&slib/&jbq) jobpty(4) +
                          text('Jobbeskrivelse for ovf. fra MalProff')
             monmsg     msgid(cpf1621)
/*  Slette gamle og legge inn nye jobber i jobbplan                 */
test:        rmvjobscde job(&jse) entrynbr(*all)
             monmsg     msgid(cpf1645)

             addjobscde job(&jse) cmd(call pgm(*libl/aabatch) +
                          parm(&fgr 'RmpGetIfs' '1')) +
                          frq(*weekly) scddate(*none) scdday(*ALL) +
                          scdtime(&tix) jobd(&slib/&jbd) +
                          jobq(&slib/&jbq) user(&usr) +
                          text('Overføring fra MalProff')

/*  Generere filer som brukes i løsningne                           */
             call   pgm(ag916c) parm('RMPIPF    ' &fgr &grp ' ')
             call   pgm(ag916c) parm('RMPFPF    ' &fgr &grp ' ')

endpgm:endpgm
/*                                                                  */
