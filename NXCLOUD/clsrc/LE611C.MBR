             PGM        PARM(&LPPARM &LPMEBR)

/*----------------------------------------------------------------------------------*/
/* 8.01 260922  bsa Laget ny versjon for å skrive til coolspool eller vanlig. Styres*/
/* 8.01             av switch.                                                      */
/*                                                                                  */
/*                                                                                  */
/*                                                                                  */
/*                                                                                  */
/*----------------------------------------------------------------------------------*/

             DCL        VAR(&LPPARM) TYPE(*CHAR) LEN(64)
             DCL        VAR(&LPMEBR) TYPE(*CHAR) LEN(10)
             DCL        VAR(&BTCH) TYPE(*CHAR) LEN(1)
             DCL        VAR(&WSID) TYPE(*CHAR) LEN(10)

/*8.01*/     DCL        VAR(&CO402_KEY) TYPE(*CHAR) LEN(50)
/*8.01*/     DCL        VAR(&CO402_V1)  TYPE(*CHAR) LEN(1)
/*8.01*/     DCL        VAR(&CO402_V2)  TYPE(*CHAR) LEN(2048)
/*8.01*/     dcl        var(&fgr)       type(*char) len(3)
/*8.01*/     dcl        var(&fix)       type(*char) len(3)
/*8.01*/     dcl        var(&fir)       type(*dec)  len(3 0) value(0)
/*8.01*/     dcl        var(&lag)       type(*dec)  len(3 0) value(0)


/* Danner members for parameterfilene                                */

             ADDPFM     FILE(LSORPF) MBR(&LPMEBR)
             ADDLFM     FILE(LSORLU) MBR(&LPMEBR) +
                          DTAMBRS((*CURRENT/LSORPF (&LPMEBR)))
             ADDLFM     FILE(LSORL1) MBR(&LPMEBR) +
                          DTAMBRS((*CURRENT/LSORPF (&LPMEBR)))
             ADDLFM     FILE(LSORL2) MBR(&LPMEBR) +
                          DTAMBRS((*CURRENT/LSORPF (&LPMEBR)))

/* Override parameterfilene/membrene                                 */

             OVRDBF     FILE(LSORLU) TOFILE(*FILE) MBR(&LPMEBR)
             OVRDBF     FILE(LSORL1) TOFILE(*FILE) MBR(&LPMEBR)
             OVRDBF     FILE(LSORL2) TOFILE(*FILE) MBR(&LPMEBR)

/* Call til program for seleksjon                                    */

             CALL       PGM(LE611R) PARM(&LPPARM)

/* Program for overstyring av printerfile                            */

             CALL       PGM(AP601C) PARM('LE612P' &BTCH)

/*8.01*/     chgvar     var(&CO402_KEY) value('PROA_VERSJON_AVVIKSLISTE')
/*8.01*/     rtvdtaara  dtaara(*lda (931 3)) rtnvar(&fgr)
/*8.01*/     rtvdtaara  dtaara(*lda (944 3)) rtnvar(&fix)
/*8.01*/     chgvar     var(&fir)   value(&fix)
/*8.01*/     CALL       PGM(CO402R) PARM(&FGR &FIR &LAG &CO402_KEY +
/*8.01*/                  &CO402_V1 &CO402_V2)

/* Call til program for utskrift                                     */

/*8.01*/     IF         COND(&CO402_V1 *EQ '1') THEN(do)
             CALL       PGM(LE612R) PARM(&LPPARM)
/*8.01*/     enddo
/*8.01*/     else    do
/*8.01*/     call       pgm(le692r) parm(&lpparm)
/*8.01*/     enddo
/*                                                                   */
/* Override av parametermembrene oppheves                            */
/*                                                                   */
             DLTOVR     FILE(LSORLU LSORL1 LSORL2)
/*                                                                   */
/* Parametermembrene slettes                                         */

             RMVM       FILE(LSORL2) MBR(&LPMEBR)
             RMVM       FILE(LSORL1) MBR(&LPMEBR)
             RMVM       FILE(LSORLU) MBR(&LPMEBR)
             RMVM       FILE(LSORPF) MBR(&LPMEBR)

/* Melding dersom bakgrunnsjobb                                      */

             IF         COND(&BTCH *EQ '1') THEN(DO)
             RTVDTAARA  DTAARA(*LDA (901 10)) RTNVAR(&WSID)
             SNDBRKMSG  MSG('ASLAGR: Utskrift av avviksliste +
                          er ferdig') TOMSGQ(&WSID)
             ENDDO

/* Program for å fjerne overstyring av printerfile                   */

             CALL       PGM(AP606C) PARM('LE612P')

/*                                                                   */
             ENDPGM
