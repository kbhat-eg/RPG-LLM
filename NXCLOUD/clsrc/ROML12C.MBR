/*                  VISMA    BUSINESS                                */
             PGM

             DCL        VAR(&FILN)  TYPE(*CHAR) LEN(20)
             DCL        VAR(&FILS)  TYPE(*CHAR) LEN(40)
             DCL        VAR(&MAPX) TYPE(*CHAR) LEN(30)
             DCL        VAR(&CLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&IFSV) TYPE(*CHAR) LEN(65)
             DCL        VAR(&FILT) TYPE(*CHAR) LEN(65)
             DCL        VAR(&FILG) TYPE(*CHAR) LEN(3)
             DCL        VAR(&BIBL) TYPE(*CHAR) LEN(6)
             DCL        VAR(&BAN1) TYPE(*CHAR) LEN(5) +
                          VALUE('/asp/')
             DCL        VAR(&BAN2) TYPE(*CHAR) LEN(7) +
                          VALUE('/visma/')
             DCL        VAR(&BAN3) TYPE(*CHAR) LEN(10) +
                          VALUE('/qsys.lib/')
             DCL        VAR(&BAN4) TYPE(*CHAR) LEN(31) +
                          VALUE('.lib/vismalag.file/vismalag.mbr')
/* Henter filgruppe og firmanummer:                                  */

             RTVDTAARA  DTAARA(*LDA (931 3)) RTNVAR(&FILG)
             RTVDTAARA  DTAARA(*LDA (934 6)) RTNVAR(&BIBL)

/* Lager til-katalogen på IFS hvis den ikke finnes                   */

             CHGVAR     VAR(&MAPX)     VALUE(&BAN1 *CAT &FILG +
                                             *CAT &BAN2)
             CRTDIR     DIR(&MAPX)
             MONMSG     MSGID(CPF0000)

/* Tømmer transaksjonsfil før danning av transaksjoner               */

             CLRPFM     FILE(RV12PF)
             CLRPFM     FILE(LOVIPF)
             CLRPFM     FILE(LOVUPF)
             OVRDBF     FILE(LOVFL1) TOFILE(*LIBL/LOMKL1)
             OVRDBF     FILE(LOVFL3) TOFILE(*LIBL/LOMKL3)

/* Kaller program for danning av transaksjoner                       */

             CALL       PGM(RV012R)

/* Test om overføringsregister finnes - bygg evt.                    */

             CHKOBJ     OBJ(*LIBL/VISMALAG) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
             CRTPF      FILE(VISMALAG) RCDLEN(800) +
                          TEXT('Posteringer til Visma')           +
                          MAXMBRS(*NOMAX)
             ENDDO

/* Kopier data til overføringsfil                                    */

             CPYF       FROMFILE(*libl/RV12PF) +
                          TOFILE(*libl/VISMALAG) FROMMBR(RV12PF) +
                           MBROPT(*REPLACE) FMTOPT(*NOCHK)


             MONMSG     MSGID(CPF2817)   EXEC(GOTO CMDLBL(END))


/* Starter kopiering til integret filsystem                          */

             CALL       PGM(LO764R) PARM(&FILN)

/* CHGVAR VAR(&FILS) VALUE(&BAN1 *CAT &FILG *CAT &BAN2 *bcat &FILN) */
   CHGVAR VAR(&FILS) VALUE(&BAN1 *CAT &FILG *CAT &BAN2 *bcat &FILN *CAT '.CSV')
   CHGVAR VAR(&FILT) VALUE(&BAN3 *CAT &BIBL *CAT &BAN4)
             CPYTOSTMF  +
                          FROMMBR(&FILT) TOSTMF(&FILS) +
                          STMFOPT(*REPLACE) STMFCODPAG(*PCASCII)
             CHGAUT      OBJ(&FILS) USER(*PUBLIC) DTAAUT(*RWX) +
                          OBJAUT(*ALL)


 END:        ENDPGM
