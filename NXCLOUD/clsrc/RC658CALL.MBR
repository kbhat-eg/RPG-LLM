             PGM
/* *************************************************************** */
/* *                                                             * */
/* *  INNBETALINGER: Rutinen henter alle innbetalinger på saker  * */
/* *                 sendt inkassobyrået                         * */
/* *************************************************************** */

/* Definere parametere og variabler                                  */

             DCL        VAR(&USER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&IN03)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&BTCH)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&WSID)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&FGRP)  TYPE(*CHAR) LEN(3)
             DCL        VAR(&MEMBX) TYPE(*CHAR) LEN(5)
             DCL        VAR(&MEMB)  TYPE(*DEC)  LEN(5 0)
             DCL        VAR(&WALI)  TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&WAPO)  TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&MLD1)  TYPE(*CHAR) LEN(40)
             DCL        VAR(&MLD2)  TYPE(*CHAR) LEN(40)
             DCL        VAR(&WMM1)  TYPE(*CHAR) LEN(10) VALUE('INK     ')

             DCLF       FILE(ADB/AFILPF)

/* Program for printerstyring                                        */

             CALL       PGM(AP600R) PARM('RC659 ' '1' '0' &IN03 &BTCH)

             IF         COND(&IN03 *EQ '1') THEN(GOTO CMDLBL(ENDPGM))

/* Henter verdier i fra LDA.                                         */

             RTVDTAARA  DTAARA(*LDA (911 7))  RTNVAR(&USER)
             RTVDTAARA  DTAARA(*LDA (864 5))  RTNVAR(&MEMBX)

             CHGVAR     VAR(&MEMB) VALUE(&MEMBX)
             CHGVAR     VAR(%SST(&WMM1 4 7)) VALUE(&USER)

/*                                                                 */
/*   Leser File-gruppe / databibliotek                             */

 LESFIL:     RCVF

/*   Dersom EOF  gå til slutt av rutinen                           */
/*                                                                 */
             MONMSG     MSGID(CPF0864) EXEC(GOTO CMDLBL(SLUTT))
/*                                                                 */
/*   Tildeler   "CURRENT LIBRARY"                                  */
/*                                                                 */
             CHGCURLIB  CURLIB(&AGBIBL)
/*                                                                 */
/*   Dersom LIB  ikke finnes gå tilbake for å lese ny file-gruppe  */
/*                                                                 */
             MONMSG     MSGID(CPF2110) EXEC(GOTO CMDLBL(LESFIL))
             CHGVAR     VAR(&FGRP) VALUE(%SST(&AGBIBL 4 3))
/*                                                                 */
             IF         COND(&FGRP *EQ '   ') THEN(GOTO CMDLBL(LESFIL))
/*                                                                 */
/*   START av ønskede rutiner pr. file-gruppe                      */
/*                                                                 */

/* Varselbilde 1                                                     */

             IF         COND(&BTCH *NE '1') THEN(DO)
             CHGVAR     VAR(&WALI) VALUE(10)
             CHGVAR     VAR(&WAPO) VALUE(36)
             CHGVAR     VAR(&MLD1) VALUE('Vennligst vent litt .....')
             CHGVAR     VAR(&MLD2) VALUE('Utskrift pågår ..........')
             CALL       PGM(AA006C)  PARM(&WALI &WAPO &MLD1 &MLD2)
             ENDDO

/* Overstyring av printerfile                                        */

             CALL       PGM(AP601C)    PARM('RC659P' &BTCH)

/* Utskrift                                                          */

             OVRDBF     FILE(RKWDPF) MBR(&WMM1)

             CALL       PGM(RC659R)

             DLTOVR     FILE(RKWDPF)
             MONMSG     MSGID(CPF3200)

             CALL       PGM(AP606C)    PARM('RC659P')

/*                                                                 */
/*   SLUTT av ønskede rutiner pr. file-gruppe                      */
/*                                                                 */
/*   Tilbake for å lese neste file-gruppe                          */
/*                                                                 */
             GOTO       CMDLBL(LESFIL)
/*                                                                 */
 SLUTT:
/*                                                                 */
 ENDPGM:     ENDPGM
