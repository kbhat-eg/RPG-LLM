             PGM        PARM(&PARM)
/*                                                                   */
             DCL        VAR(&TYPE)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&PARM)   TYPE(*CHAR) LEN(64)
             DCL        VAR(&SBIB)   TYPE(*CHAR) LEN(10)
/*                                                                   */
/* Initiering av parametere for utskrift                             */
/*                                                                   */
             DCL        VAR(&PRTF)   TYPE(*CHAR) LEN(10) +
                                       VALUE('LL119P')
             DCL        VAR(&OUTQ)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&FORM)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&ACPI)   TYPE(*DEC)  LEN(4 0)
             DCL        VAR(&HLDQ)   TYPE(*CHAR) LEN(4)
             DCL        VAR(&AKOP)   TYPE(*DEC)  LEN(6 0)
             DCL        VAR(&ALIN)   TYPE(*DEC)  LEN(3 0)
             DCL        VAR(&FLOW)   TYPE(*DEC)  LEN(3 0)
             DCL        VAR(&ACPIX)  TYPE(*CHAR) LEN(4)
             DCL        VAR(&AKOPX)  TYPE(*CHAR) LEN(6)
             DCL        VAR(&ALINX)  TYPE(*CHAR) LEN(3)
             DCL        VAR(&FLOWX)  TYPE(*CHAR) LEN(3)
/*                                                                   */
/* Henter inn bibliotek                                              */
/*                                                                   */
             RTVDTAARA  DTAARA(*LDA (934 10)) RTNVAR(&SBIB)
/*                                                                   */
/* Dersom BATCH må registre overrides på nytt                        */
/*                                                                   */
             RTVJOBA    TYPE(&TYPE)
             IF         COND(&TYPE *EQ '0') THEN(DO)
             CALL       PGM(FRMDØC)
             CALL       PGM(FRMDOC)
             ENDDO
/*                                                                   */
/* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - */
/* Henter verdier i fra LDA.                                         */
/*                                                                   */
             RTVDTAARA  DTAARA(*LDA (810 10)) RTNVAR(&OUTQ)
             RTVDTAARA  DTAARA(*LDA (820 10)) RTNVAR(&FORM)
             RTVDTAARA  DTAARA(*LDA (830 4))  RTNVAR(&ACPIX)
             RTVDTAARA  DTAARA(*LDA (834 4))  RTNVAR(&HLDQ)
             RTVDTAARA  DTAARA(*LDA (838 6))  RTNVAR(&AKOPX)
             RTVDTAARA  DTAARA(*LDA (856 3))  RTNVAR(&ALINX)
             RTVDTAARA  DTAARA(*LDA (860 3))  RTNVAR(&FLOWX)
/*                                                                   */
/* Antall kopier og CPI må konverteres til numerisk felt.            */
/*                                                                   */
             CHGVAR     VAR(&ACPI) VALUE(&ACPIX)
             CHGVAR     VAR(&AKOP) VALUE(&AKOPX)
             CHGVAR     VAR(&ALIN) VALUE(&ALINX)
             CHGVAR     VAR(&FLOW) VALUE(&FLOWX)
/*                                                                   */
/* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - */
/*                                                                   */
/* Endrer printfile ut i fra nye verdier.                            */
/*                                                                   */
             OVRPRTF    FILE(&PRTF) TOFILE(*FILE) PAGESIZE(72) +
                          CPI(&ACPI) OVRFLW(66) PRTQLTY(*DRAFT) +
                          OUTQ(&OUTQ) FORMTYPE(PAKK) COPIES(1)
/*                                                                   */
/* Kaller opp program                                                */
/*                                                                   */
             CALL       PGM(LL119R) PARM(&PARM)
/*                                                                   */
             ENDPGM
