
/*           Bestille   Rapporter til Altin                          */

 strpgm:     pgm

             dcl        var(&par)    type(*char) len(100)
             dcl        var(&lib)    type(*char) len(10)
             dcl        var(&frafil) type(*char) len(70)
             dcl        var(&tilfil) type(*char) len(100)
             dcl        var(&tilbac) type(*char) len(100)
             dcl        var(&dat)    type(*char) len(20)
             dcl        var(&firx)   type(*char) len(3)
             dcl        var(&utfil)  type(*char) len(10)
             dcl        var(&feil)   type(*char) len(1)
             dcl        var(&fgr)    type(*char) len(3)
             dcl        var(&antrec) type(*dec)  len(10 0)
             dcl        var(&al)     type(*dec)  len(2)    value(4)
             dcl        var(&ap)     type(*dec)  len(3)    value(10)
             dcl        var(&m1)     type(*char) len(30)
             dcl        var(&m2)     type(*char) len(30)
             dcl        var(&grp)    type(*char) len(10)   value('GRP_xxx')

             rtvjoba    curlib(&lib)
             rtvdtaara  dtaara(*lda (931 3)) rtnvar(&fgr)
             rtvdtaara  dtaara(*lda (944 3)) rtnvar(&firx)
       /*    chgvar     var(%sst(&grp 5 3))  value(&fgr)    */
             rtvusrprf  grpprf(&grp)

/*  Bygge dataarea for parameter                                 */

             crtdtaara  dtaara(*curlib/ax000par) type(*char) len(100)
             monmsg     msgid(cpf1023)
             chgdtaara  dtaara(*libl/ax000par (1 100)) value(' ')


/*  Sjekk om RXMLPF har innhold,  Hvis ikke kan ikke jobben kjøres  */
             rtvmbrd    file(*LIBL/RXMLPF) mbr(*first) +
                                   nbrcurrcd(&antrec)
             if         cond(&antrec *eq 0) then(do)
               chgvar   var(&M1)  value('XML-Master RXMLPF er tom. Den')
               chgvar   var(&M2)  value('må på plass før +
                                         rapportering.')
               call     pgm(aa011c)   parm(&al &ap &m1 &m2)
               goto     cmdlbl(endpgm)
             enddo


/*  Klargjøre register  */

             clrpfm     file(axmlmva)
             monmsg     msgid(cpf3142)  exec(do)
               crtpf    file(axmlmva) +
                        srcfile(qddfsrc) +
                        srcmbr(AXMLUT) +
                        text('SAF_T MVA-rapportering via ALTINN')
             enddo

/*           --------------------------------------                */
             chgpf      file(axmlmva) size(*nomax)
/*           --------------------------------------                */
             ovrdbf     file(AXMLUT) tofile(axmlmva)
/*           --------------------------------------                */

/*  legger ut momsoppgjør-linjer */

               call     pgm(ax400r)
               rtvdtaara  dtaara(ax000par (1 100)) rtnvar(&par)


/*  Legge ut fil i pc-område                                        */
             crtsrcpf   file(&lib/axmlutmp) rcdlen(256) +
                        mbr(axmlutmp) text(temp.fil) size(*nomax)
             monmsg     msgid(cpf0000)
             cpyf       fromfile(axmlmva) tofile(&lib/axmlutmp) +
                        frommbr(*first)  tombr(axmlutmp) +
                        mbropt(*replace) fmtopt(*cvtsrc)
             chgvar     var(&frafil) +
                        value('/qsys.lib/' *tcat &lib +
                        *tcat '.lib/axmlutmp.file/axmlutmp.mbr')
             rtvjoba    datetime(&dat)
             chgvar     var(&tilfil) +
                        value('\asp\' *cat &fgr *cat '\Altinn\fraasp\+
                        SAF-T_Mva_' *tcat %sst(&par 90 9) +
                        *tcat '_' *tcat %sst(&dat 1 14) +
                        *tcat '_1_1' +
                        *tcat '.xml')
             chgvar     var(&tilbac)    value(&tilfil)
               chgvar   var(&tilfil)  value('\asp\' *cat &fgr +
                                *cat '\Altinn')
               crtdir   dir(&tilfil) dtaaut(*indir) objaut(*indir)
               monmsg   msgid(cpf0000)
               chgaut   obj(&tilfil) user(&grp) dtaaut(*rwx) +
                        objaut(*all)
               monmsg   msgid(cpf0000)
               chgvar   var(&tilfil)  value(&tilfil *tcat '\fraasp')
               crtdir   dir(&tilfil)  dtaaut(*indir) objaut(*indir)
               monmsg   msgid(cpf0000)
               chgaut   obj(&tilfil) user(&grp) dtaaut(*rwx) +
                        objaut(*all)
               monmsg   msgid(cpf0000)
               chgvar   var(&tilfil)  value(&tilbac)
cpy2pc:      cpytostmf  frommbr(&frafil) tostmf(&tilfil) +
                        stmfopt(*replace) stmfcodpag(*stdascii)
             monmsg     msgid(cpfA097) exec(do)
/*   Ny filsti fra 26.10.2020 etter avtale med Marius             */
/*             goto     cpy2pc          */
             enddo
             chgaut     obj(&tilfil) user(*public) dtaaut(*none) +
                        objaut(*all)
             monmsg     msgid(cpf0000)
             chgaut     obj(&tilfil) user(&grp)    dtaaut(*rwx) +
                        objaut(*all)
             monmsg     msgid(cpf0000)

/*   Utlegging av Zippet fil            */

             chgvar     var(&tilfil) +
                        value('\asp\' *cat &fgr *cat '\Altinn\fraasp\+
                        SAF-T_Mva_' *tcat %sst(&par 90 9) +
                        *tcat '_' *tcat %sst(&dat 1 14) +
                        *tcat '_1_1' +
                        *tcat '.zip')
             cpytoarcf  fromfile(&frafil) toarcf(&tilfil)

             chgaut     obj(&tilfil) +
                        user(*public) dtaaut(*none) objaut(*all)
             monmsg     msgid(cpf0000)
             chgaut     obj(&tilfil) +
                        user(&grp) dtaaut(*rx) objaut(*all)

endpgm:      endpgm
