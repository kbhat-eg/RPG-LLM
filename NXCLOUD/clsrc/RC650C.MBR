             PGM

             DCL        VAR(&FIRM)  TYPE(*CHAR) LEN(3)
             DCL        VAR(&IN03) TYPE(*CHAR) LEN(1)
             DCL        VAR(&IN12) TYPE(*CHAR) LEN(1)
             DCL        VAR(&BTCH) TYPE(*CHAR) LEN(1)
             DCL        VAR(&WALI) TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&WAPO) TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&MLD1) TYPE(*CHAR) LEN(40)
             DCL        VAR(&MLD2) TYPE(*CHAR) LEN(40)
             DCL        VAR(&MLD3)  TYPE(*CHAR) LEN(50)
             DCL        VAR(&MLD4)  TYPE(*CHAR) LEN(50)
             DCL        VAR(&SVAR)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&WMM1) TYPE(*CHAR) LEN(10) VALUE('INKA    ')
             DCL        VAR(&mbrsz) TYPE(*dec)  LEN(10 0)
             DCL        VAR(&bryt)  TYPE(*char) LEN(1)
             DCL        VAR(&CO402_KEY) TYPE(*CHAR) LEN(50)
             DCL        VAR(&CO402_V1)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&CO402_V2)  TYPE(*CHAR) LEN(2048)
             DCL        VAR(&fgr)       TYPE(*CHAR) LEN(3)
             DCL        VAR(&fir)       TYPE(*dec)  LEN(3 0)
             DCL        VAR(&lag)       TYPE(*dec)  LEN(3 0)  value(0)

             RTVDTAARA  DTAARA(*LDA (944 3)) RTNVAR(&FIRM)
             CHGVAR     VAR(%SST(&WMM1 5 3)) VALUE(&FIRM)

             CHGVAR     VAR(&CO402_KEY) VALUE('INKASSO')
             rtvdtaara  dtaara(*lda (931 3)) rtnvar(&fgr)
             chgvar     var(&fir)       VALUE(&firm)
             CALL       PGM(CO402R) PARM(&FGR &fir &LAG &CO402_KEY +
                          &CO402_V1 &CO402_V2)

             IF         COND(&CO402_V1 *ne '1') THEN(do)
              CHGVAR     VAR(&WALI) VALUE(8)
              CHGVAR     VAR(&WAPO) VALUE(5)
              CHGVAR     VAR(&MLD3) VALUE('Dette firmaet er ikke satt opp +
                                             med inkasso.      ')
              CHGVAR     VAR(&MLD4) VALUE('Kontakt EG dersom du ønsker +
                                            inkasso-rutine')
              CALL       PGM(AA007R)  PARM(&mld3 &mld4 &svar &in12)
              goto       cmdlbl(endpgm)
             enddo

/* Sjekk om forrige utkjøring er ferdig behandlet                    */

             rtvmbrd    file(RCNSPF) mbr(*first) nbrcurrcd(&mbrsz)
             if         cond(&mbrsz *ne 0) then(do)
              CHGVAR     VAR(&WALI) VALUE(4)
              CHGVAR     VAR(&WAPO) VALUE(2)
              CHGVAR     VAR(&MLD3) VALUE('Forrige forslag er ikke sendt +
                                             til inkasso.        ')
              CHGVAR     VAR(&MLD4) VALUE('Ønsker du å skrive over dette?')
              CALL       PGM(AA007R)  PARM(&mld3 &mld4 &svar &in12)
              if        cond(&svar *ne 'J') then(do)
                goto    cmdlbl(endpgm)
              enddo
                clrpfm  file(*libl/rcnspf)
             enddo

/*   Sjekk om melding om hvilke byrå som kjøres mot skal gis   */
             call       pgm(rcinkf_kvi)  parm(&bryt)
             if         cond(&bryt *eq 'J') then(goto cmdlbl(endpgm))

/* Bygger membere dersom de ikke finnes                              */

             CHKOBJ     OBJ(RKWAPF) OBJTYPE(*FILE) MBR(&WMM1)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDPFM     FILE(RKWAPF) MBR(&WMM1) TEXT('Parameter for +
                          inkassovarsel (RC650R)')
             ENDDO

             clrpfm     file(rkwapf) mbr(&wmm1)

             chkobj     obj(Hentrcnr) objtype(*file)
             monmsg     msgid(cpf9801) exec(do)
               crtpf      file(Hentrcnr) rcdlen(132) +
                          TEXT('Mottak av retur fra Inkasso')
             enddo

/* Call av parameterprogram, utskrift av inkassovarsel               */

             OVRDBF     FILE(RKWAPF) MBR(&WMM1)
             CALL       PGM(RC650R) PARM(&IN03)

             IF         COND(&IN03 *EQ '1') THEN(DO)
             GOTO       CMDLBL(ENDPGM)
             ENDDO

/* Program for printerstyring                                        */

             CALL       PGM(AP600R) PARM('RC653 ' '1' '0' &IN03 &BTCH)

             IF         COND(&IN03 *EQ '1') THEN(GOTO CMDLBL(ENDPGM))

/* Varselbilde 1                                                     */

             IF         COND(&BTCH *NE '1') THEN(DO)
             CHGVAR     VAR(&WALI) VALUE(4)
             CHGVAR     VAR(&WAPO) VALUE(2)
             CHGVAR     VAR(&MLD1) VALUE('Inkasso - genererer forslag')
             CHGVAR     VAR(&MLD2) VALUE(' ****  KUNDERESKONTRO **** ')
/*              CALL       PGM(AA006C)  PARM(&WALI &WAPO &MLD1 &MLD2)         */
             ENDDO

/* Call av utskriftsprogram til batch/demand                         */

             IF         COND(&BTCH *EQ '1') THEN(SBMJOB +
                        CMD(CALL PGM(RC651C)) JOB(INKASSO) MSGQ(*NONE))

             ELSE       CMD(CALL PGM(RC651C))

 ENDPGM:     ENDPGM
