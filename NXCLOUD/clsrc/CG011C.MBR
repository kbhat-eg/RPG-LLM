             PGM
             DCL        VAR(&ANTR)     TYPE(*DEC)  LEN(10 0)
             DCL        VAR(&SVAR)     TYPE(*CHAR) LEN(3)
             DCL        VAR(&MLD1)     TYPE(*CHAR) LEN(50)
             DCL        VAR(&MLD2)     TYPE(*CHAR) LEN(50)

/* Sjekker om data i filen før evt. sletting av eksisterende         */

             RTVMBRD    FILE(CTVHST) NBRCURRCD(&ANTR)
             MONMSG     MSGID(CPF9999) EXEC(DO)
             GOTO       CMDLBL(GEN)
             ENDDO

             IF         COND(&ANTR *EQ 0) THEN(DO)
             GOTO       CMDLBL(DEL)
             ENDDO

             CHGVAR     VAR(&MLD1) VALUE('Fil inneholder data')
             CHGVAR     VAR(&MLD2) VALUE('Er det ok at disse blir +
                         slettet? ')
             CHGVAR     VAR(&SVAR) VALUE('J')
             CALL       PGM(AA007R) PARM(&MLD1 &MLD2 &SVAR 10)
             IF         COND(&SVAR *NE 'J') +
                          THEN(GOTO CMDLBL(END))

 DEL:        DLTF       FILE(CTVHIU)
             MONMSG     MSGID(CPF2105)
             DLTF       FILE(CTVHIR)
             MONMSG     MSGID(CPF2105)
             DLTF       FILE(CTVHI1)
             MONMSG     MSGID(CPF2105)
             DLTF       FILE(CTVHST)
             MONMSG     MSGID(CPF2105)

/* Kjører SQL-statements for oppretting av tabell og indekser        */

 GEN:        RUNSQLSTM  SRCFILE(*LIBL/QSQLSRC) SRCMBR(CTVHSQ) +
                          NAMING(*SYS) DECMPT(*COMMA) +
                          SRTSEQ(*LANGIDUNQ) LANGID(NOR)
             STRJRNPF   FILE(CTVHST) JRN(BUTIJRN)

 END:        ENDPGM
