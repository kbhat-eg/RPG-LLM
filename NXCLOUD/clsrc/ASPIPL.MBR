             PGM

/* Definisjon av variabler                                           */

             DCL        VAR(&KODE)     TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&BIBL)     TYPE(*CHAR) LEN(10)

/* Stopper alle skriverprogrammer                                    */

             ENDWTR     WTR(*ALL) OPTION(*IMMED)
             DLYJOB     DLY(600)

/* Starter egendefinert IPL-program                                  */

             CALL       PGM(*LIBL/ASPIP1)

/* Stopper QBATCH                                                    */

WAIT1:       DLYJOB     DLY(31)
             ENDSBS     SBS(QBATCH) OPTION(*IMMED)
             MONMSG     MSGID(CPF1054) EXEC(GOTO CMDLBL(WAIT2))
             MONMSG     MSGID(CPF1001)
             MONMSG     MSGID(CPF1056)
             GOTO       CMDLBL(WAIT1)

/* Stopper QINTER                                                    */

WAIT2:       DLYJOB     DLY(32)
             ENDSBS     SBS(QINTER) OPTION(*IMMED)
             MONMSG     MSGID(CPF1054) EXEC(GOTO CMDLBL(WAIT3))
             MONMSG     MSGID(CPF1001)
             MONMSG     MSGID(CPF1056)
             GOTO       CMDLBL(WAIT2)
WAIT3:

/* Oppdater historikk med melding om at IPL starter                  */

             CHGVAR     VAR(&KODE)  VALUE(07)
             CALL       PGM(AN050R) PARM(&KODE &BIBL)

/* PWRDWNSYS                                                         */

             PWRDWNSYS  OPTION(*IMMED) RESTART(*YES) IPLSRC(B)

             ENDPGM

/* Kommentar :                                                       */
/* ------------                                                      */
/* Dersom det er behov for å kjøre egne program i forbindelse IPL,   */
/* er dette løst på følgende måte:                                   */
/*                                                                   */
/*    ASPIP1 ligger som et skall i ASPGPL.  Dette kalles hver gang   */
/*    men ingenting utføres.  Programmet kan kopieres over i         */
/*    ASPGPLK og tilpasses der.  Det vil da bli startet opp fra      */
/*    ASPGPLK istedet, og utføre det som legges inn.                 */
/*                                                                   */
/*    ASPIP2 ligger som et skall i ASPGPL.  Dette program er tenkt   */
/*    benyttet der hvor man har behov for å kjøre spesielle rutiner  */
/*    i forbindelse med IPL.  Dette kan for eksempel være oppstarts- */
/*    rutiner som ligger definert i andre systemer.  ASPIP2 kan      */
/*    derfor kopieres over til ASPGPLK og tilpasses der.  Dette      */
/*    program kalles fra AN045R, det vil si i forbindelse med        */
/*    QSTRUP-programmet.                                             */
/*                                                                   */
/*    NB !  Husk oppsett av biblioteksliste der hvor spesielle       */
/*          program skal kjøres fra andre programbibliotek.          */
/*                                                                   */
