
             PGM        PARM(&AFIR &ADAT &ALAGE &BACKUP &TAPE)

/* Definering av parametre                                           */

             DCL        VAR(&AFIR)   TYPE(*CHAR) LEN(3)
             DCL        VAR(&ADAT)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&ALAGE)  TYPE(*CHAR) LEN(2)
             DCL        VAR(&BACKUP) TYPE(*CHAR) LEN(1)
             DCL        VAR(&TAPE)   TYPE(*CHAR) LEN(10)

/* Definering av variable                                            */

             DCL        VAR(&DATO)   TYPE(*CHAR) LEN(8)
             DCL        VAR(&MBR)    TYPE(*CHAR) LEN(10)
             DCL        VAR(&LIB)    TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSG)    TYPE(*CHAR) LEN(69)
             DCL        VAR(&MSGID)  TYPE(*CHAR) LEN(7)

/* Monitor-message                                                   */

             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

/* Oppretter backup-member                                           */

             CHGVAR     VAR(&DATO) VALUE(%SUBSTRING( &ADAT 3 8 ) )
             CHGVAR     VAR(&MBR) VALUE('BC' *CAT &DATO)

             ADDPFM     FILE(VHISPF) MBR(&MBR)

/* Override av logiske filer til *first-member                       */

             OVRDBF     FILE(VHISL1) TOFILE(*FILE) MBR(*FIRST)

/* Override av fysisk fil til backup-member                          */

             OVRDBF     FILE(VHISLU) TOFILE(*FILE) MBR(&MBR)

/* Kall av program for flytting av rader fra hoved-member til        */
/* backup-member                                                     */

             CALL       PGM(LH702R) PARM(&AFIR &ADAT &ALAGE)

/* Fjerner override                                                  */

             DLTOVR     FILE(VHISL1 VHISLU)

/* Kopierer backup-tabeller til valgt tapestasjon                    */

             IF         COND(&BACKUP *EQ '1') THEN(DO)
             RTVDTAARA  DTAARA(*LDA (934 10)) RTNVAR(&LIB)
             SAVOBJ     OBJ(VHISPF) LIB(&LIB) DEV(&TAPE) +
                          OBJTYPE(*FILE) ENDOPT(*REWIND) +
                          FILEMBR((VHISPF (&MBR)))
             ENDDO

/* Fjerner backup-member                                             */

             RMVM       FILE(VHISPF) MBR(&MBR)

 END:        ENDPGM
