             PGM        parm(&fgr)

/* Dette er en fellesrutine som overfører alle aktuelle filer        */
/* til Mestergruppens Proffportal hos LOGIQ                          */

/* Initiering av parametre.                                          */
             DCL        VAR(&FTPNAV) TYPE(*CHAR) LEN(10)
             DCL        VAR(&STATUS) TYPE(*CHAR) LEN(1)  VALUE(' ')
             DCL        VAR(&TILNAV) TYPE(*CHAR) LEN(20) VALUE(' ')
             DCL        VAR(&VISMLD) TYPE(*CHAR) LEN(1)  VALUE('0')
             DCL        VAR(&JOBTYP) TYPE(*CHAR) LEN(1)
             DCL        VAR(&CURLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&fgr)    TYPE(*CHAR) LEN(3)

/* Henter jobbtype                                                   */

             RTVJOBA    TYPE(&JOBTYP)

/* Setter local dataarea og *curlib                                  */

             CHGVAR     VAR(&CURLIB) VALUE('ADB' *cat &fgr)

             CALL       PGM(AA720C) PARM(&fgr)

             CHGDTAARA  DTAARA(*LDA (944 03)) VALUE('000')
             CHGDTAARA  DTAARA(*LDA (931 03)) VALUE(&fgr)
             CHGDTAARA  DTAARA(*LDA (934 10)) VALUE(&curlib)

             CHGCURLIB  CURLIB(&CURLIB)

/* Blanker ut gammel selgerfil                                       */

             CLRPFM     FILE(NETSELGER) MBR(NETSELGER)
             MONMSG     MSGID(CPF3141) EXEC(ADDPFM +
                          FILE(*CURLIB/NETSELGER) MBR(NETSELGER))

/* Kjører generering av ny selgerfil                                   */

             CALL       PGM(NM708R) PARM(&STATUS)
             IF         COND(&STATUS *NE ' ') THEN(DO)
             SNDPGMMSG  MSG('Nettbutikk finnes ikke for dette firma')
             GOTO       CMDLBL(END)
             ENDDO

             CHGVAR     VAR(&STATUS) VALUE(' ')

/* Overfører selgerfil til Logiq via FTP                                */

             CHGVAR     VAR(&FTPNAV) VALUE('LOGSELGER')
             CALL       PGM(AF700C) PARM(&FTPNAV &STATUS &TILNAV +
                          &VISMLD)

/* Gir beskjed ved feil                                                 */

             IF         COND(&STATUS *NE ' ') THEN(SNDPGMMSG +
                          MSG('Feil ved sending av selgerfil'))

/* Blanker ut gammel lagerfil                                        */

             CLRPFM     FILE(NETLAGER) MBR(NETLAGER)
             MONMSG     MSGID(CPF3141) EXEC(ADDPFM +
                          FILE(*CURLIB/NETLAGER) MBR(NETLAGER))

/* Kjører generering av ny lagerfil                                  */

             CALL       PGM(NM704R) PARM(&STATUS)
             IF         COND(&STATUS *NE ' ') THEN(DO)
             SNDPGMMSG  MSG('Nettbutikk finnes ikke for dette firma')
             GOTO       CMDLBL(END)
             ENDDO

             CHGVAR     VAR(&STATUS) VALUE(' ')

/* Overfører lagerfil til Logiq via FTP                                   */

             CHGVAR     VAR(&FTPNAV) VALUE('LOGLAGER')
             CALL       PGM(AF700C) PARM(&FTPNAV &STATUS &TILNAV +
                          &VISMLD)

/* Gir beskjed ved feil                                                 */

             IF         COND(&STATUS *NE ' ') THEN(SNDPGMMSG +
                          MSG('Feil ved sending av lagerfil'))

/* Blanker ut gammel leverandørfil                                   */

             CLRPFM     FILE(NETLEVER) MBR(NETLEVER)
             MONMSG     MSGID(CPF3141) EXEC(ADDPFM +
                          FILE(*CURLIB/NETLEVER) MBR(NETLEVER))

/* Kjører generering av ny leverandørfil                             */

             CALL       PGM(NM705R) PARM(&STATUS)
             IF         COND(&STATUS *NE ' ') THEN(DO)
             SNDPGMMSG  MSG('Nettbutikk finnes ikke for dette firma')
             GOTO       CMDLBL(END)
             ENDDO

             CHGVAR     VAR(&STATUS) VALUE(' ')

/* Overfører leverandørfil til Logiq via FTP                         */

             CHGVAR     VAR(&FTPNAV) VALUE('LOGLEVER')
             CALL       PGM(AF700C) PARM(&FTPNAV &STATUS &TILNAV +
                          &VISMLD)

/* Gir beskjed ved feil                                              */

             IF         COND(&STATUS *NE ' ') THEN(SNDPGMMSG +
                          MSG('Feil ved sending av leverandørfil'))

/* Blanker ut gammel nettkundefil                                    */

             CLRPFM     FILE(NETKUND) MBR(NETKUND)
             MONMSG     MSGID(CPF3141) EXEC(ADDPFM +
                          FILE(*CURLIB/NETKUND) MBR(NETKUND))

/* Oppretter rapportfil hvis den ikke allerede finnes                */

             CHKOBJ     OBJ(*CURLIB/NETKRAPP) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(CRTSRCPF FILE(NETKRAPP) +
                          RCDLEN(200) MBR(*FILE) TEXT('Rapportfil +
                          for nettkundegenerering') SIZE(*NOMAX))

/* Blanker ut gammel nettkunde rapport fil                           */

             CLRPFM     FILE(NETKRAPP) MBR(NETKRAPP)
             MONMSG     MSGID(CPF3141) EXEC(ADDPFM +
                          FILE(*CURLIB/NETKRAPP) MBR(NETKRAPP))

/* Kjører generering av diverseprosjekt på kunder som mangler        */

             CALL       PGM(FL900RX) PARM(&STATUS)

/* Kjører generering av ny nettkundefil                              */

             CALL       PGM(NM700R) PARM(&STATUS)
             IF         COND(&STATUS *EQ 'R') THEN(DO)
             SNDPGMMSG  MSG('Noen nettkunder ble utelatt fra +
                          sending. Se rapport for detaljer.')
             ENDDO
             ELSE IF    COND(&STATUS *NE ' ') THEN(DO)
             SNDPGMMSG  MSG('Nettbutikk finnes ikke for dette firma')
             GOTO       CMDLBL(END)
             ENDDO

             CHGVAR     VAR(&STATUS) VALUE(' ')

/* Overfører nettkundefil til NBS via FTP                            */

             CHGVAR     VAR(&FTPNAV) VALUE('LOGKUND')
             CALL       PGM(AF700C) PARM(&FTPNAV &STATUS &TILNAV +
                          &VISMLD)

/* Gir beskjed ved feil                                                 */

             IF         COND(&STATUS *NE ' ') THEN(SNDPGMMSG +
                          MSG('Feil ved sending av kundefil'))

/* Oppretter rapportfil varer hvis den ikke allerede finnes              */

             CHKOBJ     OBJ(*CURLIB/NETVRAPP) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(CRTSRCPF FILE(NETVRAPP) +
                          RCDLEN(200) MBR(*FILE) TEXT('Rapportfil +
                          for nettvaregenerering') SIZE(*NOMAX))

/* Blanker ut gammel nettvare rapport fil                            */

             CLRPFM     FILE(NETVRAPP) MBR(NETVRAPP)
             MONMSG     MSGID(CPF3141) EXEC(ADDPFM +
                          FILE(*CURLIB/NETVRAPP) MBR(NETVRAPP))

/* Blanker ut gammel nettvarefil                                     */

             CLRPFM     FILE(NETVARE) MBR(NETVARE)
             MONMSG     MSGID(CPF3141) EXEC(ADDPFM +
                          FILE(*CURLIB/NETVARE) MBR(NETVARE))

/* Bygger nytt sortiment basert på alle lagervarer                   */

             CLRPFM     FILE(CSODST)
             CALL       PGM(NX710R)

/* Kjører generering av ny nettvarefil                               */

             CALL       PGM(NM710R) PARM(&STATUS)
             IF         COND(&STATUS *EQ 'R') THEN(DO)
             SNDPGMMSG  MSG('Noen nettvarer ble utelatt fra sending. +
                          Se rapport for detaljer.')
             ENDDO
             ELSE IF    COND(&STATUS *NE ' ') THEN(DO)
             SNDPGMMSG  MSG('Nettbutikk finnes ikke for dette firma')
             GOTO       CMDLBL(END)
             ENDDO

             CHGVAR     VAR(&STATUS) VALUE(' ')

/* Overfører nettvarefil til NBS via FTP                             */

             CHGVAR     VAR(&FTPNAV) VALUE('LOGVARE')
             CALL       PGM(AF700C) PARM(&FTPNAV &STATUS &TILNAV +
                          &VISMLD)

/* Gir beskjed ved feil                                                 */

             IF         COND(&STATUS *NE ' ') THEN(SNDPGMMSG +
                          MSG('Feil ved sending av varefil'))

/* Blanker ut gammel vare/lagerfil                                   */

             CLRPFM     FILE(NETVARLAG) MBR(NETVARLAG)
             MONMSG     MSGID(CPF3141) EXEC(ADDPFM +
                          FILE(*CURLIB/NETVARLAG) MBR(NETVARLAG))

/* Kjører generering av ny vare/lagerfil                             */

             CALL       PGM(NM707R) PARM(&STATUS)
             IF         COND(&STATUS *NE ' ') THEN(DO)
             SNDPGMMSG  MSG('Nettbutikk finnes ikke for dette firma')
             GOTO       CMDLBL(END)
             ENDDO

             CHGVAR     VAR(&STATUS) VALUE(' ')

/* Overfører vare/lagerfil til Logiq via FTP                         */

             CHGVAR     VAR(&FTPNAV) VALUE('LOGVARLAG')
             CALL       PGM(AF700C) PARM(&FTPNAV &STATUS &TILNAV +
                          &VISMLD)

/* Gir beskjed ved feil                                                 */

             IF         COND(&STATUS *NE ' ') THEN(SNDPGMMSG +
                          MSG('Feil ved sending av vare/lagerfil'))

 END:        ENDPGM
