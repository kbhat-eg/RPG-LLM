
/* Factoring :                                                       */
/* Program for overføring av filer                                   */
/* fra database på AS/400 til IFS                                    */

/* Innhold i &TYPE  T = Transaksjonsfile                             */
/*                  K = Kunderegister                                */

             PGM        PARM(&TYPE &MAPI &FILN)

/* Initiering av parametere                                          */

             DCL        VAR(&TYPE)     TYPE(*CHAR) LEN(1)
             DCL        VAR(&MAPI)     TYPE(*CHAR) LEN(25)
             DCL        VAR(&FILN)     TYPE(*CHAR) LEN(25)

/* Initiering av variable                                            */

             DCL        VAR(&CLIB)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&MAPX)     TYPE(*CHAR) LEN(30)
             DCL        VAR(&FFIL)     TYPE(*CHAR) LEN(70)
             DCL        VAR(&TFIL)     TYPE(*CHAR) LEN(70)

/* Lager til-katalogen på IFS hvis den ikke finnes                   */

             RTVJOBA    CURLIB(&CLIB)
             CHGVAR     VAR(&MAPX)     VALUE('/home/' *TCAT &MAPI)
             CRTDIR     DIR(&MAPX)     DTAAUT(*RWX) OBJAUT(*ALL)
             MONMSG     MSGID(CPF0000)

/* Overfør til temporær fil                                          */

             CRTSRCPF   FILE(REFTMP) RCDLEN(1024) MBR(REFTMP) +
                          TEXT('Temp.fil Factoring.') SIZE(*NOMAX)
             MONMSG     MSGID(CPF0000)

             IF         COND(&TYPE *EQ 'T') THEN(DO)
             CPYF       FROMFILE(*LIBL/REF2PF) TOFILE(*LIBL/REFTMP) +
                          FROMMBR(REF2PF) TOMBR(REFTMP) +
                          MBROPT(*REPLACE) FMTOPT(*CVTSRC)
             ENDDO

             IF         COND(&TYPE *EQ 'K') THEN(DO)
             CPYF       FROMFILE(*LIBL/REK2PF) TOFILE(*LIBL/REFTMP) +
                          FROMMBR(REK2PF) TOMBR(REFTMP) +
                          MBROPT(*REPLACE) FMTOPT(*CVTSRC)
             ENDDO

/* Bygger opp navn på fra fil og til fil                             */

             CHGVAR     VAR(&FFIL) VALUE('/qsys.lib/' *TCAT &CLIB +
                                   *TCAT '.lib/REFTMP.file/REFTMP.mbr')

             CHGVAR     VAR(&TFIL) VALUE(&MAPX *TCAT '/' *TCAT &FILN)

/* Kopierer                                                          */

             CPYTOSTMF  FROMMBR(&FFIL) TOSTMF(&TFIL) +
                          STMFOPT(*REPLACE) STMFCODPAG(*PCASCII)
             CHGAUT     OBJ(&TFIL) USER(*PUBLIC) DTAAUT(*RWX) +
                          OBJAUT(*ALL)
             MONMSG     MSGID(CPFA0B1) /* hvis filen finnes fra før, og +
                          bruker ikke er eier, feiler chgaut */

             DLTF       FILE(REFTMP)

             ENDPGM
