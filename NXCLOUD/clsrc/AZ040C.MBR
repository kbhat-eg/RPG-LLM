             PGM
             DCLF       FILE(AZ040D)

/*  Legger ut skjermbilde med valg på bruker, biliotek og versjon    */


             CHGVAR     VAR(&A1MLD) VALUE('                         ')
             CHGVAR     VAR(&A1VER) VALUE('*CURRENT')
 SEND:       SNDRCVF    RCDFMT(A1BLD)

/* F3 + F12                                                          */
             IF         COND(&IN03) THEN(GOTO CMDLBL(ENDPGM))
             IF         COND(&IN12) THEN(GOTO CMDLBL(ENDPGM))

/*  Sjekk om savefile på valgt bruker finnes - Clear eller bygg      */

             CLRSAVF    FILE(ASP.SAVF/&A1USR)
             MONMSG     MSGID(CPF9812) EXEC(CRTSAVF +
                              FILE(ASP.SAVF/&A1USR))


/*  Lagrer til save-file med valgt versjon                           */

             SAVLIB     LIB(&A1LIB) DEV(*SAVF) SAVF(ASP.SAVF/&A1USR) +
                        TGTRLS(&A1VER)
             MONMSG     MSGID(CPF3781) EXEC(GOTO CMDLBL(ERR3))


/*  Sender save-file til ASP                                         */

             SNDNETF    FILE(ASP.SAVF/&A1USR) +
                        TOUSRID((&A1USR NO608ASP))

             MONMSG     MSGID(CPF9800) EXEC(GOTO CMDLBL(ERR1))
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(ERR2))
             CHGVAR     VAR(&A1MLD) VALUE('Distribusjon ok          ')
             GOTO       CMDLBL(SEND)

 ERR1:       CHGVAR     VAR(&A1MLD) VALUE('Problemer med lagringsfil')
             GOTO       CMDLBL(SEND)

 ERR2:       CHGVAR     VAR(&A1MLD) VALUE('Feil i distribusjon      ')
             GOTO       CMDLBL(SEND)

 ERR3:       CHGVAR     VAR(&A1MLD) VALUE('Ugyldig bibliotek angitt ')
             GOTO       CMDLBL(SEND)



 ENDPGM:     ENDPGM
