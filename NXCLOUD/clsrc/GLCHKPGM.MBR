             PGM        PARM(&PRGM &TYPE &AKTI)

/* Programmet bygger et member (programnavn) for å indikere at       */
/* programmet er aktivt.  Dersom memberet allerede finnes i filen    */
/* skal forsøk på å starte dette programmet avvises.                 */

/* Programmet brukes også til å slette aktuelt member.               */

/* Parameterbeskrivelse:                                             */
/* ********************                                              */

/*   PRGM  -  Program det skal kontrolleres om er aktiv.             */

/*   TYPE  -  Type test som skal utføres:                            */
/*            "0" = Kontrollere om program er aktiv - BYGG MEMBER.   */
/*            "1" = Kontrollere om program er aktiv - IKKE BYGG MEMB.*/
/*            "2" = Fjerne member i fra filen.                       */

/*   AKTI  -  Returnere flg. verdi til kallende program:             */
/*            "0" = program ER IKKE aktivt fra før (default).        */
/*            "1" = program ER aktivt fra før.                       */

             DCL        VAR(&PRGM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&TYPE) TYPE(*DEC) LEN(1 0)
             DCL        VAR(&AKTI) TYPE(*DEC) LEN(1 0)

/*   MEMB  -  Hjelpefelt for å indikere om member finnes:            */
/*            "0" = member finnes ikke i filen.                      */
/*            "1" = member finnes i filen (Defaultverdi).            */

             DCL        VAR(&MEMB) TYPE(*DEC) LEN(1 0) VALUE(1)

/*   AKTI  -  Defaultverdi ved oppstart er "0", d.v.s. rutinen       */
/*            er ikke aktiv.                                         */

             CHGVAR     VAR(&AKTI) VALUE(0)

/* Kontroll på om member finnes:                                     */

             CHKOBJ     OBJ(*LIBL/GAKTPF) OBJTYPE(*FILE) MBR(&PRGM)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             CHGVAR     VAR(&MEMB) VALUE(0) /* Member finnes ikke */
             ENDDO

/* &TYPE = "0" -> KONTROLL PÅ OM PROGRAM ER AKTIVT - BYGG MEMBER:  */

      IF   COND(&TYPE *EQ 0) THEN(DO)

/* Member finnes:                                                    */

           IF   COND(&MEMB *EQ 1) THEN(DO)
                CHGVAR   VAR(&AKTI) VALUE(1) /* Pgm er aktivt */
           ENDDO

/* Member finnes ikke, og kan defor bygges:                          */

           IF   COND(&MEMB *EQ 0) THEN(DO)
             ADDPFM   FILE(GAKTPF) MBR(&PRGM) TEXT('Kontrollmember')
             CHGVAR   VAR(&AKTI) VALUE(0) /* Pgm er ikke akivt */
           ENDDO

      ENDDO

/* &TYPE = "1" -> KONTROLL PÅ OM PROGRAM ER AKTIVT - IKKE BYGG MBR.*/

      IF   COND(&TYPE *EQ 1) THEN(DO)

/* Member finnes:                                                    */

           IF   COND(&MEMB *EQ 1) THEN(DO)
                CHGVAR   VAR(&AKTI) VALUE(1) /* Pgm er aktiv */
           ENDDO

/* Member finnes ikke:                                               */

           IF   COND(&MEMB *EQ 0) THEN(DO)
             CHGVAR   VAR(&AKTI) VALUE(0) /* Pgm er ikke akivt */
           ENDDO

      ENDDO

/* &TYPE = "2" -> FJERNE MEMBER I FRA FILEN:                       */

      IF   COND(&TYPE *EQ 2) THEN(DO)

         IF   COND(&MEMB *EQ 1) THEN(DO)
              RMVM FILE(*LIBL/GAKTPF) MBR(&PRGM)
         ENDDO

      ENDDO

 END:        ENDPGM
