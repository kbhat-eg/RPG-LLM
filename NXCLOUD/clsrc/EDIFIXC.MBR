             PGM

             DCL        VAR(&FGRP) TYPE(*CHAR) LEN(6)
             dcl        var(&stat) type(*char) len(1)
             DCLF       FILE(EDIFIXD)

/*  Legger ut bilde for innhenting av parametre                      */

             RTVDTAARA  DTAARA(*LDA (931 3)) RTNVAR(&A1FIGR)
             RTVDTAARA  DTAARA(*LDA (951 30)) RTNVAR(&A1FIRM)
             CHGVAR     VAR(&A1STJO) VALUE(1)
 BILDE:      SNDRCVF    RCDFMT(A1BLD)

/* F3                                                                */
             IF         COND(&IN03) THEN(GOTO CMDLBL(ENDPGM))
             IF         COND(&A1MELD *EQ ' ') THEN(GOTO CMDLBL(BILDE))
             IF         COND(&A1DATE *EQ 0) THEN(GOTO CMDLBL(BILDE))

             CHGVAR     VAR(&FGRP) VALUE('ADB' *CAT &A1FIGR)

/*  Behandler omkjøring av ordrebekreftelser                         */

             IF         COND(&A1MELD *NE 'OBKR') THEN(GOTO CMDLBL(FAKT))

/*  Skrive til arbeidsfil LWEOPF_ASP?                                */

             IF         COND(&A1ARFI *EQ 1) THEN(DO)
             CHKOBJ     OBJ(LWEOPF_ASP) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
             CPYF       FROMFILE(LWEOPF) TOFILE(&FGRP/LWEOPF_ASP) +
                          CRTFILE(*YES)
             ENDDO
             CLRPFM     FILE(LWEOPF_ASP)
             OVRDBF     FILE(LWEOPF) TOFILE(&FGRP/LWEOPF_ASP)
             ENDDO

             IF         COND(&A1ARFI *EQ 0) THEN(DO)
             CLRPFM     FILE(LWEOPF)
             ENDDO

/*  Kjører uthenting av meldingslinjer avhengig av parametervalg     */

             CALL       PGM(EDIFIX1R) +
                        PARM(&A1DATE &A1TIME &A1DOKN &A1REST)
             MONMSG     MSGID(CPF0000)
             IF         COND(&A1ARFI *EQ 1) THEN(DO)
             DLTOVR     FILE(LWEOPF)
             ENDDO

/*  Kjører oppdatering direkte dersom det er valgt                   */

             IF         COND(&A1ARFI *EQ 1) THEN(GOTO CMDLBL(ENDPGM))
             IF         COND(&A1STJO *EQ 0) THEN(GOTO CMDLBL(ENDPGM))
             CALL       PGM(LI701R) parm(&stat)

/*  Behandler omkjøring av fakturaer                                 */

 FAKT:       IF         COND(&A1MELD *NE 'FAKT') THEN(GOTO CMDLBL(BEST))

/*  Skrive til arbeidsfil LWEFPF_ASP?                                */

             IF         COND(&A1ARFI *EQ 1) THEN(DO)
             CHKOBJ     OBJ(LWEFPF_ASP) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
             CPYF       FROMFILE(LWEFPF) TOFILE(&FGRP/LWEFPF_ASP) +
                          CRTFILE(*YES)
             ENDDO
             CLRPFM     FILE(LWEFPF_ASP)
             OVRDBF     FILE(LWEFPF) TOFILE(&FGRP/LWEFPF_ASP)
             ENDDO

             IF         COND(&A1ARFI *EQ 0) THEN(DO)
             CLRPFM     FILE(LWEFPF)
             ENDDO

/*  Kjører uthenting av meldingslinjer avhengig av parametervalg     */

             CALL       PGM(EDIFIX2R) +
                        PARM(&A1DATE &A1TIME &A1DOKN &A1REST)
             MONMSG     MSGID(CPF0000)
             IF         COND(&A1ARFI *EQ 1) THEN(DO)
             DLTOVR     FILE(LWEFPF)
             ENDDO

/*  Kjører oppdatering direkte dersom det er valgt                   */

             IF         COND(&A1ARFI *EQ 1) THEN(GOTO CMDLBL(ENDPGM))
             IF         COND(&A1STJO *EQ 0) THEN(GOTO CMDLBL(ENDPGM))
             CALL       PGM(LI703R) parm(&stat)


/*  Behandler omkjøring av bestillinger                              */

 BEST:       IF         COND(&A1MELD *NE 'BEST') THEN(GOTO CMDLBL(ENDPGM))

/*  Skrive til arbeidsfil LWEBPF_ASP?                                */

             IF         COND(&A1ARFI *EQ 1) THEN(DO)
             CHKOBJ     OBJ(LWEBPF_ASP) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
             CPYF       FROMFILE(LWEBPF) TOFILE(&FGRP/LWEBPF_ASP) +
                          CRTFILE(*YES)
             ENDDO
             CLRPFM     FILE(LWEBPF_ASP)
             OVRDBF     FILE(LWEBPF) TOFILE(&FGRP/LWEBPF_ASP)
             ENDDO

             IF         COND(&A1ARFI *EQ 0) THEN(DO)
             CLRPFM     FILE(LWEBPF)
             ENDDO

/*  Kjører uthenting av meldingslinjer avhengig av parametervalg     */

             CALL       PGM(EDIFIX3R) +
                        PARM(&A1DATE &A1TIME &A1DOKN &A1REST)
             MONMSG     MSGID(CPF0000)
             IF         COND(&A1ARFI *EQ 1) THEN(DO)
             DLTOVR     FILE(LWEBPF)
             ENDDO

/*  Kjører oppdatering direkte dersom det er valgt                   */

             IF         COND(&A1ARFI *EQ 1) THEN(GOTO CMDLBL(ENDPGM))
             IF         COND(&A1STJO *EQ 0) THEN(GOTO CMDLBL(ENDPGM))
             CALL       PGM(LI704R) parm(&stat)

/*                                                                   */
 ENDPGM:     ENDPGM
