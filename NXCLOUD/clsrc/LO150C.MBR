STRPGM:      PGM

/* Initiering av parametere                                          */

             DCL        VAR(&KODE)     TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&ONOF)     TYPE(*DEC)  LEN(1 0)
             DCL        VAR(&MLD3)     TYPE(*CHAR) LEN(50)
             DCL        VAR(&MLD4)     TYPE(*CHAR) LEN(50)
             DCL        VAR(&SVAR)     TYPE(*CHAR) LEN(1)
             DCL        VAR(&IN12)     TYPE(*DEC)  LEN(1 0)
             DCL        VAR(&TOMT)     TYPE(*CHAR) LEN(1)

             DCL        VAR(&VALG)  TYPE(*DEC)  LEN(2 0) VALUE(0)
             DCL        VAR(&NXDR)  TYPE(*DEC)  LEN(1 0) VALUE(0)


             DCL        VAR(&MLD1) TYPE(*CHAR) LEN(50) +
                          VALUE('Ingen poster for overføring +
                          ble funnet')

/* Har denne bruker lov til å oppdatere regnskap ?                   */

             CHGVAR     VAR(&KODE)     VALUE(11)
             CHGVAR     VAR(&ONOF)     VALUE(0)
             CALL       PGM(LB700R)    PARM(&KODE &ONOF)

/* Sjekk om jobben skal avbrytes                                     */

             IF         COND(&ONOF *EQ 0) THEN(DO)
             CALL       PGM(AA004C)
             GOTO       CMDLBL(ENDPGM)
             ENDDO

/* Kjøres inngående faktura fra annen skjerm ?                       */

             CHGVAR     VAR(&KODE)     VALUE(05)
             CHGVAR     VAR(&ONOF)     VALUE(0)
             CALL       PGM(FA730R)    PARM(&KODE &ONOF)

/* Sjekk om jobben skal avbrytes                                     */

             IF         COND(&ONOF *EQ 1) THEN(DO)
             GOTO       CMDLBL(ENDPGM)
             ENDDO

/* Kjøres oppdatering av regnskap allerede ?                         */

             CHGVAR     VAR(&KODE)     VALUE(06)
             CHGVAR     VAR(&ONOF)     VALUE(0)
             CALL       PGM(FA730R)    PARM(&KODE &ONOF)

/* Sjekk om jobben skal avbrytes                                     */

             IF         COND(&ONOF *EQ 1) THEN(DO)
             GOTO       CMDLBL(ENDPGM)
             ENDDO

/* Finnes det poster ?, hvis ikke, avslutt                           */

             CHKOBJ     OBJ(LOVFPF)    OBJTYPE(*FILE) MBR(*FIRST)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             CALL       PGM(AA005R)    PARM(&MLD1)
             GOTO       CMDLBL(ENDPGM)
             ENDDO

/* Varselbilde med bekreftelse på om jobb skal settes i gang         */

             CHGVAR     VAR(&MLD3) +
                        VALUE('Overføring til regnskap settes igang ')
             CHGVAR     VAR(&MLD4) +
                        VALUE('Bekreft at dette ønskes ! ')
             CALL       PGM(AA007R)    PARM(&MLD3 &MLD4 &SVAR &IN12)

             IF         COND(&IN12 *EQ 1 ) +
                          THEN(GOTO CMDLBL(ENDPGM))

             IF         COND(&SVAR *EQ 'J') +
                          THEN(GOTO CMDLBL(VIDERE))
             ELSE       CMD(GOTO CMDLBL(ENDPGM))

/* Oppdater jobb-register med aktiv-flagg                            */

VIDERE:      CHGVAR     VAR(&KODE)     VALUE(06)
             CHGVAR     VAR(&ONOF)     VALUE(1)
             CALL       PGM(FA720R)    PARM(&KODE &ONOF)

             CALL       PGM(RF1TRANS)  PARM('LAGR')

/* Sjekker om Nexstep økonomi skal oppdateres                        */

             CALL       PGM(RV015R)  PARM(&VALG &NXDR)

             IF         COND(&NXDR *EQ 0) THEN(DO)
             CALL       PGM(RF1JOURN)  PARM('LAGR')
             ENDDO

/* Oppdater jobb-register med jobb-ferdig-flagg                      */

             CHGVAR     VAR(&KODE)     VALUE(06)
             CHGVAR     VAR(&ONOF)     VALUE(0)
             CALL       PGM(FA720R)    PARM(&KODE &ONOF)

/* Avslutt program                                                   */

ENDPGM:      ENDPGM
