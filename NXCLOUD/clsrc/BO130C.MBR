STRPGM:      PGM

/* Initiering av parametere                                          */

             DCL        VAR(&KODE)     TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&ONOF)     TYPE(*DEC)  LEN(1 0)
             DCL        VAR(&MLD3)     TYPE(*CHAR) LEN(50)
             DCL        VAR(&MLD4)     TYPE(*CHAR) LEN(50)
             DCL        VAR(&SVAR)     TYPE(*CHAR) LEN(1)
             DCL        VAR(&IN12)     TYPE(*DEC)  LEN(1 0)
             DCL        VAR(&TOMT)     TYPE(*CHAR) LEN(1)
             DCL        VAR(&PARM)     TYPE(*CHAR) LEN(1) VALUE('2')

             DCL        VAR(&VALG)     TYPE(*DEC)  LEN(2 0) VALUE(0)
             DCL        VAR(&NXDR)     TYPE(*DEC)  LEN(1 0) VALUE(0)
             dcl        var(&batch)    type(*char) len(1)
             dcl        var(&msg)      type(*char) len(100)
             dcl        var(&mj)       type(*char) len(3)
             dcl        var(&mi)       type(*dec)  len(3 0) value(0)
             dcl        var(&okkode)   type(*char) len(2)
             dcl        var(&oktext)   type(*char) len(30)

             DCL        VAR(&MLD1)     TYPE(*CHAR) LEN(50) +
                          VALUE('Ingen poster for overføring +
                          ble funnet')

/*  Kjøres jobben i batch                                             */

             rtvjoba    type(&batch)
             if         cond(&batch *eq '0') then(do)
               chgvar   var(&msg)   value('Dagsoppgjør,  Oppdatering av +
                                           Økonomi starter')
               CallSubr subr(melding)
             enddo


/* Kontrollere skjerm/kasse/bruker -tilgang                          */

             if         cond(&batch *eq '1') then(do)
               CALL       PGM(BOSJKR) PARM(&PARM)

               IF         COND(&PARM *EQ '9') THEN(DO)
               GOTO       CMDLBL(ENDPGM)
               ENDDO

/* Kjøres oppdatering av regnskap allerede ?                         */

               CHGVAR     VAR(&KODE)     VALUE(04)
               CHGVAR     VAR(&ONOF)     VALUE(0)
               CALL       PGM(FA730R)    PARM(&KODE &ONOF)

/* Sjekk om jobben skal avbrytes                                     */

               IF         COND(&ONOF *EQ 1) THEN(DO)
               GOTO       CMDLBL(ENDPGM)
               ENDDO

             enddo

/* Finnes det poster ?, hvis ikke, avslutt                           */

             CALL       PGM(BO795C)    PARM(&TOMT)
             IF         COND(&TOMT *EQ 'J') THEN(DO)
               if       cond(&batch *eq '1') then(do)
                 CALL       PGM(AA005R)    PARM(&MLD1)
                 enddo
               else do
                 chgvar var(&msg)  value('Dagsoppgjør,  Ingen poster +
                                          til oppdatering regnskap')
                 CallSubr subr(melding)
               enddo
               GOTO       CMDLBL(ENDPGM)
             ENDDO

/* Varselbilde med bekreftelse på om jobb skal settes i gang         */

             if       cond(&batch *eq '1') then(do)
               CHGVAR     VAR(&MLD3) +
                          VALUE('Overføring til regnskap settes igang ')
               CHGVAR     VAR(&MLD4) +
                          VALUE('Bekreft at dette ønskes ! ')
               CALL       PGM(AA007R)    PARM(&MLD3 &MLD4 &SVAR &IN12)

               IF         COND(&IN12 *EQ 1 ) +
                            THEN(GOTO CMDLBL(ENDPGM))

               IF         COND(&SVAR *EQ 'J') +
                            THEN(GOTO CMDLBL(VIDERE))
               ELSE       CMD(GOTO CMDLBL(ENDPGM))
             enddo

/* Oppdater jobb-register med aktiv-flagg                            */

VIDERE:      CHGVAR     VAR(&KODE)     VALUE(04)
             CHGVAR     VAR(&ONOF)     VALUE(1)
             CALL       PGM(FA720R)    PARM(&KODE &ONOF)

             CALL       PGM(RF1TRANS)  PARM('SHOP')

/* Sjekker om Nexstep økonomi skal oppdateres                        */

             CALL       PGM(RV015R)  PARM(&VALG &NXDR)

             if         cond(&valg *ne 0 *and +
                             &batch *eq '0') then(do)
               chgvar   var(&okkode) value(&valg)
               call     pgm(bo131r)  parm(&okkode &oktext)
               chgvar   var(&msg)  value('Dagsoppgjør,  Posteringer er +
                                          lagt ut til ' *cat &oktext)
               CallSubr subr(melding)
             enddo
             IF         COND(&NXDR *EQ 0) THEN(DO)
             CALL       PGM(RF1JOURN)  PARM('SHOP')
               chgvar   var(&msg)  value('Dagsoppgjør,  Posteringer er +
                                          Oppdatert i Nexstep')
               CallSubr subr(melding)
             ENDDO

/* Oppdater jobb-register med jobb-ferdig-flagg                      */

             CHGVAR     VAR(&KODE)     VALUE(04)
             CHGVAR     VAR(&ONOF)     VALUE(0)
             CALL       PGM(FA720R)    PARM(&KODE &ONOF)

/* ______________________________________________________________________*/
Subr         Subr(Melding)
             chgvar     var(&mi)              value(&mi + 1)
             chgvar     var(&mj)              value(&mi)
             call       pgm(egd97c) parm+
                        ('Dagsoppgj.' +
                         'BO130C    ' +
                         &mj +
                         &msg)
EndSubr

/* Avslutt program                                                   */
ENDPGM:      ENDPGM
