             PGM

/* Definere parametere og variabler                                  */

             DCL        VAR(&BTCH)     TYPE(*CHAR) LEN(1)
             DCL        VAR(&TYPE)     TYPE(*CHAR) LEN(1)
             DCL        VAR(&WSID)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&MEMBX)    TYPE(*CHAR) LEN(5)
             DCL        VAR(&MEMB)     TYPE(*DEC)  LEN(5 0)
             DCL        VAR(&WALI)     TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&WAPO)     TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&MLD1)     TYPE(*CHAR) LEN(40)
             DCL        VAR(&MLD2)     TYPE(*CHAR) LEN(40)
             DCL        VAR(&WMM1)     TYPE(*CHAR) LEN(10) VALUE('RHA   ')

/* Henter verdier i fra LDA.                                         */

             RTVDTAARA  DTAARA(*LDA (863 1))  RTNVAR(&BTCH)
             RTVDTAARA  DTAARA(*LDA (864 5))  RTNVAR(&MEMBX)

             CHGVAR     VAR(&MEMB) VALUE(&MEMBX)
             CHGVAR     VAR(%SST(&WMM1 4 5)) VALUE(&MEMB)

/* Varselbilde 1                                                     */

             IF         COND(&BTCH *NE '1') THEN(DO)
             CHGVAR     VAR(&WALI) VALUE(4)
             CHGVAR     VAR(&WAPO) VALUE(44)
             CHGVAR     VAR(&MLD1) VALUE('Vennligst vent litt......')
             CHGVAR     VAR(&MLD2) VALUE('Utplukk pågår ...........')
             CALL       PGM(AA006C)  PARM(&WALI &WAPO &MLD1 &MLD2)
             ENDDO
/*                                                                   */
/* Bygger membere dersom de ikke finnes                              */

             CHKOBJ     OBJ(RHW2PF) OBJTYPE(*FILE) MBR(&WMM1)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDPFM     FILE(RHW2PF) MBR(&WMM1) TEXT('Poster til +
                          revisjonsjournal  (RH651R')
             ENDDO
/*                                                                   */
             CHKOBJ     OBJ(RHJOPF) OBJTYPE(*FILE) MBR(&WMM1)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDPFM     FILE(RHJOPF) MBR(&WMM1) TEXT('Journal med +
                       på revisjonsjournal  (RH651R')
             ENDDO

/* Utplukking av poster                                              */

             CLRPFM     FILE(RHW2PF) MBR(&WMM1)
             MONMSG     MSGID(CPF3100)
/*                                                                   */
             CLRPFM     FILE(RHJOPF) MBR(&WMM1)
             MONMSG     MSGID(CPF3100)


             OVRDBF     FILE(RHW2PF) MBR(&WMM1)
             OVRDBF     FILE(RHJOPF) MBR(&WMM1)
             CALL       PGM(RH651R)

/* Varselbilde 2                                                     */

             IF         COND(&BTCH *NE '1') THEN(DO)
             CHGVAR     VAR(&WALI) VALUE(6)
             CHGVAR     VAR(&WAPO) VALUE(40)
             CHGVAR     VAR(&MLD1) VALUE('Vennligst vent litt......')
             CHGVAR     VAR(&MLD2) VALUE('Utskrift pågår...........')
             CALL       PGM(AA006C)  PARM(&WALI &WAPO &MLD1 &MLD2)
             ENDDO

/* Overstyring av printerfile                                        */

             CALL       PGM(AP601C)    PARM('RH652P' &BTCH)

/* Utskrift                                                          */

             OVRDBF     FILE(RHW2PF)  MBR(&WMM1)
             OVRDBF     FILE(RHJOPF)  MBR(&WMM1)
             CALL       PGM(RH652R)
/*                                                                   */
             DLTOVR     FILE(RHW2PF)
             RMVM       FILE(*LIBL/RHW2PF) MBR(&WMM1)
             MONMSG     MSGID(CPF3200)
/*                                                                   */
             DLTOVR     FILE(RHJOPF)
             RMVM       FILE(*LIBL/RHJOPF) MBR(&WMM1)
             MONMSG     MSGID(CPF3200)

/* Melding dersom bakgrunnsjobb                                      */

             IF         COND(&BTCH *EQ '1') THEN(DO)
             RTVDTAARA  DTAARA(*LDA (901 10)) RTNVAR(&WSID)
             SNDMSG     MSG('ASOKON  Rutinen for utskrift av revis+
                        jonsjournal er ferdig') TOMSGQ(&WSID)
             ENDDO

             CALL       PGM(AP606C)    PARM('RH652P')
 END:        ENDPGM
