             PGM

/* Definere parametere og variabler                                  */

             DCL        VAR(&BTCH)     TYPE(*CHAR) LEN(1)
             DCL        VAR(&TYPE)     TYPE(*CHAR) LEN(1)
             DCL        VAR(&WSID)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&MEMBX)    TYPE(*CHAR) LEN(5)
             DCL        VAR(&MEMB)     TYPE(*DEC)  LEN(5 0)
             DCL        VAR(&WALI)     TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&WAPO)     TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&MLD1)     TYPE(*CHAR) LEN(40)
             DCL        VAR(&MLD2)     TYPE(*CHAR) LEN(40)
             DCL        VAR(&WMM1)     TYPE(*CHAR) LEN(10) VALUE('RKA   ')

/* Henter verdier i fra LDA.                                         */

             RTVDTAARA  DTAARA(*LDA (863 1))  RTNVAR(&BTCH)
             RTVDTAARA  DTAARA(*LDA (864 5))  RTNVAR(&MEMBX)
             CHGVAR     VAR(&MEMB) VALUE(&MEMBX)
             CHGVAR     VAR(%SST(&WMM1 4 5)) VALUE(&MEMB)

/* Varselbilde 1                                                     */

             IF         COND(&BTCH *NE '1') THEN(DO)
             CHGVAR     VAR(&WALI) VALUE(4)
             CHGVAR     VAR(&WAPO) VALUE(44)
             CHGVAR     VAR(&MLD1) VALUE('Vennligst vent litt .....')
             CHGVAR     VAR(&MLD2) VALUE('Utplukk pågår ...........')
             CALL       PGM(AA006C)  PARM(&WALI &WAPO &MLD1 &MLD2)
             ENDDO

/* Utplukking av poster                                              */

             CLRPFM     FILE(RLW1PF) MBR(&WMM1)
             MONMSG     MSGID(CPF3100)

/* Bygger membere dersom de ikke finnes                              */

             CHKOBJ     OBJ(RLW1PF) OBJTYPE(*FILE) MBR(&WMM1)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDPFM     FILE(RLW1PF) MBR(&WMM1) TEXT('Leverandør til +
                        reskontroliste (RL641R)')
             ENDDO
/*                                                                   */
             CHKOBJ     OBJ(RLW1L1) OBJTYPE(*FILE) MBR(&WMM1)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDLFM     FILE(RLW1L1) MBR(&WMM1) DTAMBRS((RLW1PF +
                          (&WMM1))) TEXT('Leverandør til +
                        reskontroliste (RL641R)')
             ENDDO

/* Utplukk                                                           */

             OVRDBF     FILE(RLW1PF) MBR(&WMM1)
             CALL       PGM(RL641R)

/* Varselbilde 2                                                     */

             IF         COND(&BTCH *NE '1') THEN(DO)
             CHGVAR     VAR(&WALI) VALUE(6)
             CHGVAR     VAR(&WAPO) VALUE(40)
             CHGVAR     VAR(&MLD1) VALUE('Vennligst vent litt .....')
             CHGVAR     VAR(&MLD2) VALUE('Utskrift pågår ..........')
             CALL       PGM(AA006C)  PARM(&WALI &WAPO &MLD1 &MLD2)
             ENDDO

/* Call av program, utskrift av saldoliste - leverandør              */

/* Overstyring av printerfile                                        */

             CALL       PGM(AP601C)    PARM('RL646P' &BTCH)

             OVRDBF     FILE(RLW1L1) MBR(&WMM1)
             CALL       PGM(RL646R)
/*                                                                   */
             DLTOVR     FILE(RLW1L1)
             DLTOVR     FILE(RLW1PF)
             RMVM       FILE(*LIBL/RLW1L1) MBR(&WMM1)
             RMVM       FILE(*LIBL/RLW1PF) MBR(&WMM1)
             MONMSG     MSGID(CPF3200)

             CALL       PGM(AP606C)    PARM('RL646P')

/* Melding dersom bakgrunnsjobb                                      */

             IF         COND(&BTCH *EQ '1') THEN(DO)
             RTVDTAARA  DTAARA(*LDA (901 10)) RTNVAR(&WSID)
             SNDBRKMSG  MSG('ASOKON: Rutinen for utskrift av Reskontro+
                        liste er ferdig') TOMSGQ(&WSID)
             ENDDO
 END:        ENDPGM
