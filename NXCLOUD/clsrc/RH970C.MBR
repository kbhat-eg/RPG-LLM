             PGM

             DCL        VAR(&MBRSZ) TYPE(*DEC)  LEN(10 0)
             DCL        VAR(&MBRUT) TYPE(*CHAR) LEN(10)
             DCL        VAR(&USER)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&VALG) TYPE(*CHAR) LEN(1) VALUE('1')
             DCL        VAR(&IN03) TYPE(*CHAR) LEN(1)
             DCL        VAR(&BTCH) TYPE(*CHAR) LEN(1)
             DCL        VAR(&IN12) TYPE(*DEC)  LEN(1 0)
             DCL        VAR(&WALI) TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&WAPO) TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&MLD1) TYPE(*CHAR) LEN(40)
             DCL        VAR(&MLD2) TYPE(*CHAR) LEN(40)

             CHGDTAARA  DTAARA(*LDA (844 1))  VALUE(&VALG)
             RTVDTAARA  DTAARA(*LDA (911 10)) RTNVAR(&USER)

/* Bygger membere dersom de ikke finnes                              */

             CHKOBJ     OBJ(RBUNPF) OBJTYPE(*FILE) MBR(&USER)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDPFM     FILE(RBUNPF) MBR(&USER) TEXT('Poster for +
                          bruker (RF010C)')
             ENDDO

             CHKOBJ     OBJ(RTRAPF) OBJTYPE(*FILE) MBR(&USER)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDPFM     FILE(RTRAPF) MBR(&USER) TEXT('Poster for +
                          bruker (RF010C)')
             ENDDO

             CHKOBJ     OBJ(RBUNL1) OBJTYPE(*FILE) MBR(&USER)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDLFM     FILE(RBUNL1) MBR(&USER) DTAMBRS((RBUNPF +
                          (&USER))) TEXT('Poster for bruker +
                          (RF010C)')
             ENDDO

             CHKOBJ     OBJ(RBUNLU) OBJTYPE(*FILE) MBR(&USER)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDLFM     FILE(RBUNLU) MBR(&USER) DTAMBRS((RBUNPF +
                          (&USER))) TEXT('Poster for bruker +
                          (RF010C)')
             ENDDO

             CHKOBJ     OBJ(RTRAL1) OBJTYPE(*FILE) MBR(&USER)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDLFM     FILE(RTRAL1) MBR(&USER) DTAMBRS((RTRAPF +
                          (&USER))) TEXT('Poster for bruker +
                          (RF010C)')
             ENDDO

             CHKOBJ     OBJ(RTRALU) OBJTYPE(*FILE) MBR(&USER)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDLFM     FILE(RTRALU) MBR(&USER) DTAMBRS((RTRAPF +
                          (&USER))) TEXT('Poster for bruker +
                          (RF010C)')
             ENDDO

/* Sjekker om det finnes noe registrert på bunt eller transaksjon    */

             RTVMBRD    FILE(*LIBL/RBUNPF) MBR(&USER) NBRCURRCD(&MBRSZ)
             IF         COND(&MBRSZ *NE 0) THEN(DO)
             CHGVAR     VAR(&WALI) VALUE(6)
             CHGVAR     VAR(&WAPO) VALUE(10)
             CHGVAR     VAR(&MLD1) VALUE('Bilagsregistrering er i gang! ')
             CHGVAR     VAR(&MLD2) VALUE('Avslutt dette før du forsetter')
             CALL       PGM(AA006C)  PARM(&WALI &WAPO &MLD1 &MLD2)
             DLYJOB     DLY(10)
             GOTO       ENDPGM
             ENDDO

             RTVMBRD    FILE(*LIBL/RTRAPF) MBR(&USER) NBRCURRCD(&MBRSZ)
             IF         COND(&MBRSZ *NE 0) THEN(DO)
             CHGVAR     VAR(&WALI) VALUE(6)
             CHGVAR     VAR(&WAPO) VALUE(10)
             CHGVAR     VAR(&MLD1) VALUE('Bilagsregistrering er i gang! ')
             CHGVAR     VAR(&MLD2) VALUE('Avslutt dette før du forsetter')
             CALL       PGM(AA006C)  PARM(&WALI &WAPO &MLD1 &MLD2)
             DLYJOB     DLY(10)
             GOTO       ENDPGM
             ENDDO

/* Test om arbeidsfilen finnes, hvis ikke kan man jobbe videre       */

             CHKOBJ     OBJ(*LIBL/RTWMPF) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
             GOTO       BEGYNN
             ENDDO

/* Hvis fila finnes er det andre som holder på,og man kan ikke fortsette*/

             CHGVAR     VAR(&WALI) VALUE(6)
             CHGVAR     VAR(&WAPO) VALUE(10)
             CHGVAR     VAR(&MLD1) VALUE('En annen bruker denne rutinen ')
             CHGVAR     VAR(&MLD2) VALUE('Prøv igjen senere             ')
             CALL       PGM(AA006C)  PARM(&WALI &WAPO &MLD1 &MLD2)
             DLYJOB     DLY(10)
             GOTO       ENDPGM

BEGYNN:

 /* Henter alle member + informasjon til arbeidsfil                  */

             DSPFD      FILE(*LIBL/RTRAPF) TYPE(*MBR) +
                          OUTPUT(*OUTFILE) OUTFILE(*LIBL/RTWMPF)

 /* Henter et member og kopierer det til BRUKER                      */

             CALL       PGM(RH970R) PARM(&MBRUT &IN03)

             IF         COND(&IN03 *EQ '1') THEN(DO)
             GOTO       AVSLUTT
             ENDDO

             IF         COND(&MBRUT *EQ '          ') THEN(DO)
             GOTO       AVSLUTT
             ENDDO

 /* Sjekk om member inneholder transer                              */

             RTVMBRD    FILE(*LIBL/RTRAPF) MBR(&MBRUT) NBRCURRCD(&MBRSZ)

             IF         COND(&MBRSZ *EQ 0) THEN(DO)
             CHGVAR     VAR(&WALI) VALUE(6)
             CHGVAR     VAR(&WAPO) VALUE(10)
             CHGVAR     VAR(&MLD1) VALUE('Valgte member er tomt.  Dette ')
             CHGVAR     VAR(&MLD2) VALUE('slettes og jobben avsluttes.')
             CALL       PGM(AA006C)  PARM(&WALI &WAPO &MLD1 &MLD2)
             DLYJOB     DLY(5)
             GOTO       SLETTE
             ENDDO

 /* Et member er valgt og kopiering kan begynne                    */

 /* Bunt kopieres                                                    */

             CPYF       FROMFILE(*LIBL/RBUNPF) TOFILE(*LIBL/RBUNPF) +
                          FROMMBR(&MBRUT) TOMBR(&USER) MBROPT(*ADD)

 /* Trans kopieres                                                   */

             CPYF       FROMFILE(*LIBL/RTRAPF) TOFILE(*LIBL/RTRAPF) +
                          FROMMBR(&MBRUT) TOMBR(&USER) MBROPT(*ADD)

 /* Bunt slettes                                                     */

 SLETTE:

             RMVM       FILE(*LIBL/RBUNLU) MBR(&MBRUT)
             MONMSG     MSGID(CPF5815 CPF7310)

             RMVM       FILE(*LIBL/RBUNL1) MBR(&MBRUT)
             MONMSG     MSGID(CPF5815 CPF7310)

             RMVM       FILE(*LIBL/RBUNPF) MBR(&MBRUT)
             MONMSG     MSGID(CPF5815 CPF7310)

 /* Trans slettes                                                    */

             RMVM       FILE(*LIBL/RTRALU) MBR(&MBRUT)
             MONMSG     MSGID(CPF5815 CPF7310)

             RMVM       FILE(*LIBL/RTRAL1) MBR(&MBRUT)
             MONMSG     MSGID(CPF5815 CPF7310)

             RMVM       FILE(*LIBL/RTRAPF) MBR(&MBRUT)
             MONMSG     MSGID(CPF5815 CPF7310)

 AVSLUTT:
             DLTF       FILE(*LIBL/RTWMPF)

 ENDPGM:     ENDPGM
