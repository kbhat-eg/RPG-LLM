/*                                                                   */
/*-------------------------------------------------------------------*/
             PGM        PARM(&URL &REQFILE +
                         &RESFILE &USER &PW &AUTTYPE &STATUS)
             DCL        VAR(&URL) TYPE(*CHAR) LEN(200)
             DCL        VAR(&AUTTYPE) TYPE(*CHAR) LEN(5)
             DCL        VAR(&REQFILE) TYPE(*CHAR) LEN(100)
             DCL        VAR(&RESFILE) TYPE(*CHAR) LEN(100)
             DCL        VAR(&USER) TYPE(*CHAR) LEN(20)
             DCL        VAR(&PW) TYPE(*CHAR) LEN(50)
             DCL        VAR(&STATUS) TYPE(*DEC) LEN(1 0) VALUE(0)

             DCL        VAR(&DTAQ) TYPE(*CHAR) LEN(10) VALUE(SOAPREQ)
             DCL        VAR(&DTAQLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FLDLEN) TYPE(*DEC) LEN(4 0) VALUE(1000)
             DCL        VAR(&WTIM)  TYPE(*DEC) LEN(5 0) value(60)
             DCL        VAR(&KLEN)  TYPE(*DEC) LEN(3 0) value(10)
             DCL        VAR(&SLEN)  TYPE(*DEC) LEN(3 0) value(0)

             DCL        VAR(&JSON) TYPE(*CHAR) LEN(1000)
             DCL        VAR(&UID) TYPE(*CHAR) LEN(10)
             DCL        VAR(&KODE) TYPE(*CHAR) LEN(1) VALUE('0')
             DCL        VAR(&FIRM) TYPE(*DEC) LEN(3 0) VALUE(200)
             DCL        VAR(&CFIRM) TYPE(*CHAR) LEN(3)
             DCL        VAR(&FELL) TYPE(*CHAR) LEN(6) VALUE('DIVTEL')
             DCL        VAR(&TYPE) TYPE(*CHAR) LEN(2) VALUE('  ')
             DCL        VAR(&FAST) TYPE(*CHAR) LEN(10) +
                          VALUE('          ')
             DCL        VAR(&NUMM) TYPE(*DEC) LEN(8 0)

             RTVDTAARA  DTAARA(*LDA (944 03)) RTNVAR(&CFIRM)
             CHGVAR     VAR(&FIRM) VALUE(&CFIRM)
             RTVJOBA    CURLIB(&DTAQLIB)

/* Hente nummer for unik ident */
             CALL       PGM(AS100R) PARM(&KODE &FIRM &FELL &TYPE +
                          &FAST &NUMM)
             CHGVAR     VAR(&UID) VALUE(&NUMM)

             CHGVAR     VAR(&JSON) VALUE(+
                          '{ "uid":"' *CAT &UID *CAT '",' +
                          *BCAT '"url":"' *TCAT +
                          &URL *TCAT '",' +
                          *BCAT '"requestFile":"' *TCAT +
                          &REQFILE *TCAT '",' +
                          *BCAT '"responseFile":"' *TCAT +
                          &RESFILE *TCAT '",' +
                          *BCAT '"authenticationType":"' *TCAT +
                          &AUTTYPE *TCAT '",' +
                          *BCAT '"user":"' *TCAT +
                          &USER *TCAT '",' +
                          *BCAT '"password":"' *TCAT +
                          &PW *TCAT '"' +
                          *BCAT '}')
/*           SNDPGMMSG  MSG(&JSON)          */
             CALL       PGM(QSNDDTAQ) PARM(&DTAQ &DTAQLIB &FLDLEN +
                          &JSON)

             CHGVAR     VAR(&JSON) VALUE(' ')
             CHGVAR     VAR(&DTAQ) VALUE('SOAPRES')
             CALL       PGM(QRCVDTAQ) PARM(&DTAQ &DTAQLIB &FLDLEN +
                          &JSON &WTIM 'EQ' &KLEN &UID +
                          &SLEN '')
             IF         COND(&FLDLEN *EQ 0) THEN(GOTO CMDLBL(TIMO))

/* Svar fra Java er kommet. Les svar, og hÃ¥ndter feil */
             IF         COND(%SCAN('NOK' &JSON) > 0) THEN(DO)
             CHGVAR     VAR(&STATUS) VALUE(1)
             SNDPGMMSG  MSG(%SST(&JSON 1 &FLDLEN))
             ENDDO

             GOTO       CMDLBL(ENDPGM)

TIMO:
             CHGVAR     VAR(&STATUS) VALUE(9)

ENDPGM:      ENDPGM
