             PGM
             DCL        VAR(&APJOBB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&APOKOK) TYPE(*DEC) LEN(1 0)
             DCL        VAR(&APUSER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&APMENY) TYPE(*CHAR) LEN(6) VALUE(JM00)
             DCL        VAR(&APSBIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&APFNAV) TYPE(*CHAR) LEN(30)
             DCL        VAR(&APMELD) TYPE(*CHAR) LEN(50)
             MONMSG     MSGID(CPF2103) /*    Bibl finnes fra før     */
             MONMSG     MSGID(CPF2110) /*    Bibl ble ikke funnet    */
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(ERR))
             RTVJOBA    JOB(&APJOBB) USER(&APUSER)

/* Call til bruker-file                                              */

             CALL       PGM(AA000R) PARM(&APOKOK &APUSER &APJOBB)

/* Sjekk om bruker ble funnet                                        */

             IF         COND(&APOKOK *EQ 1) THEN(DO)
             CHGVAR     VAR(&APMELD) VALUE('Denne bruker ble ikke +
                          funnet i brukerregister')
             CALL       PGM(AA005R) PARM(&APMELD)
             GOTO       CMDLBL(END)
             ENDDO

/* Endre biblioteksliste                                             */

             RTVDTAARA  DTAARA(*LDA (934 10)) RTNVAR(&APSBIB)
             IF         COND(&APSBIB *NE ' ') THEN(DO)
             CHGCURLIB  CURLIB(&APSBIB)
             ENDDO

/* Endre datastructure slik at riktig Økonomisystem benyttes         */

/* Blank = MDØKON                                                    */
/* A     = ASOKON                                                    */

             CHGDTAARA  DTAARA(*LDA (999 1)) VALUE('A')

/* Finne meny                                                 */

             RTVDTAARA  DTAARA(*LDA (951 30)) RTNVAR(&APFNAV)
             IF         COND(&APMENY *NE ' ') THEN(DO)
             CALL       PGM(&APMENY)
             ENDDO

             GOTO       CMDLBL(END)

 ERR:        SNDPGMMSG  MSG('Det oppstod feil i tilordning av +
                          biblioteksliste!!!') TOMSGQ(&APJOBB)

 END:        ENDPGM
