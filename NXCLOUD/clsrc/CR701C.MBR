             PGM        PARM(&DIRE &FILE &ZIPF &NFIL)

/* Definering av parametre.                                          */

             DCL        VAR(&DIRE) TYPE(*CHAR) LEN(50)
             DCL        VAR(&FILE) TYPE(*CHAR) LEN(50)
             DCL        VAR(&NFIL) TYPE(*CHAR) LEN(50)
             DCL        VAR(&ZFIL) TYPE(*CHAR) LEN(50)
             DCL        VAR(&ZIPF) TYPE(*CHAR) LEN(1)

/* Definering av variable                                            */

             DCL        VAR(&CLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&IFIL) TYPE(*CHAR) LEN(50) VALUE(' ')
             DCL        VAR(&DFIL) TYPE(*CHAR) LEN(70)
             DCL        VAR(&JAVA) TYPE(*CHAR) LEN(1000)

/* Fjerner dummy-verdi bakerst i mottatte parametre                  */

             CHGVAR     VAR(&DIRE) VALUE(%SUBSTRING(&DIRE 1 49))
             CHGVAR     VAR(&FILE) VALUE(%SUBSTRING(&FILE 1 49))
             CHGVAR     VAR(&NFIL) VALUE(%SUBSTRING(&NFIL 1 49))

/* Pakker ut zipp                                                    */

             IF         COND(&ZIPF *EQ '1') THEN(DO)
             CHGVAR     VAR(&JAVA) VALUE('java asp.zip.UnpackZip -z' +
                          *TCAT &DIRE *TCAT '/' *TCAT &FILE *BCAT +
                          '-d' *TCAT &DIRE *TCAT '/')
             QSH        CMD(&JAVA)
             CHGVAR     VAR(&ZFIL) VALUE(&DIRE *TCAT '/' *TCAT &FILE)
             RMVLNK OBJLNK(&ZFIL)
             CHGVAR     VAR(&FILE) VALUE(&NFIL)
             ENDDO

/* Henter current library                                            */

             RTVJOBA    CURLIB(&CLIB)

/* Oppretter temporær fil og kopierer mottatt fil inn i denne        */

             DLTF       FILE(FTPRLX)
             MONMSG     MSGID(CPF2105)
             CRTPF      FILE(FTPRLX) RCDLEN(80) TEXT('Temporær +
                          fil') SIZE(*NOMAX)
             CHGVAR     VAR(&IFIL) VALUE(&DIRE *TCAT '/' *TCAT &FILE)
             CHGVAR     VAR(&DFIL) VALUE('/qsys.lib/' *TCAT &CLIB +
                          *TCAT '.lib/FTPRLX.file/FTPRLX.mbr')
             CPYFRMSTMF FROMSTMF(&IFIL) TOMBR(&DFIL) MBROPT(*REPLACE)

/* Behandler mottatt fil                                             */

             CPYF       FROMFILE(FTPRLX) TOFILE(NRLXPF) +
                          MBROPT(*REPLACE) FMTOPT(*NOCHK)
             CALL       PGM(CR701R)
             DEL        OBJLNK(&IFIL)
             MONMSG     MSGID(CPF2900)
             GOTO       CMDLBL(END)

 END:        ENDPGM
