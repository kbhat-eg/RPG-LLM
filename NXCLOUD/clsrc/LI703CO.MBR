             PGM

/* Initiering av parametre.                                          */

             DCL        VAR(&OVRF) TYPE(*CHAR) LEN(10) +
                          VALUE('NBSEDIHNTO')
             DCL        VAR(&FILN)  TYPE(*CHAR) LEN(20)
             DCL        VAR(&STAT)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&SMSG)  TYPE(*CHAR) LEN(1) VALUE('1')
             DCL        VAR(&MLD1)  TYPE(*CHAR) LEN(50)
             DCL        VAR(&MLD2)  TYPE(*CHAR) LEN(50)
             DCL        VAR(&SVAR)  TYPE(*CHAR) LEN(1)

/* Bygger temporær fil for mottak - avbryter om den allerede finnes  */

             CRTPF      FILE(*CURLIB/LEDITMP) RCDLEN(80) SIZE(*NOMAX)
             MONMSG     MSGID(CPF7302) EXEC(DO)
             CHGVAR     VAR(&MLD1) VALUE('Data fra forrige overføring +
                        ligger igjen.')
             CHGVAR     VAR(&MLD2) VALUE('Varsle EG Retail support +
                         før du prøver på nytt!')
             CHGVAR     VAR(&SVAR) VALUE('J')
             CALL       PGM(AA007R) PARM(&MLD1 &MLD2 &SVAR 10)
             GOTO       CMDLBL(END)
             ENDDO

/* Henter meldinger via FTP fra NBS                                  */

             CALL       PGM(AF700C) PARM(&OVRF &STAT &FILN &SMSG)

/* Kopierer temporær fil til mottaksfil                              */

             CPYF       FROMFILE(*CURLIB/LEDITMP) TOFILE(LWERPF) +
                          MBROPT(*ADD) FMTOPT(*NOCHK)

/* Sjekk om temporærfil er tom                                       */

             MONMSG     MSGID(CPC2957) EXEC(GOTO CMDLBL(DEL))

/* Sjekk om kopiering feilet                                         */

             MONMSG     MSGID(CPF0000) EXEC(DO)
             CHGVAR     VAR(&MLD1) VALUE('Noe gikk galt i kopiering +
                         av mottaksfil.')
             CHGVAR     VAR(&MLD2) VALUE('Varsle EG Retail support +
                         før du prøver på nytt!')
             CHGVAR     VAR(&SVAR) VALUE('J')
             CALL       PGM(AA007R) PARM(&MLD1 &MLD2 &SVAR 10)
             GOTO       CMDLBL(END)
             ENDDO

/* Sletter temporær fil etter vellykket mottak                       */

 DEL:        DLTF       FILE(LEDITMP)

 END:        ENDPGM
