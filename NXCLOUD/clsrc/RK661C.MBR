             PGM

/* Initiering av parametre.                                          */

             DCL        VAR(&FIRM)  TYPE(*CHAR) LEN(3)
             DCL        VAR(&BTCH)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&WALI)  TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&WAPO)  TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&MLD1)  TYPE(*CHAR) LEN(40)
             DCL        VAR(&MLD2)  TYPE(*CHAR) LEN(40)
             DCL        VAR(&WMM1)  TYPE(*CHAR) LEN(10) VALUE('RENT    ')
             DCL        VAR(&PSKR)  TYPE(*CHAR) LEN(1)

/* Henter verdier i fra LDA.                                         */

             RTVDTAARA  DTAARA(*LDA (863 1))  RTNVAR(&BTCH)
             RTVDTAARA  DTAARA(*LDA (944 03)) RTNVAR(&FIRM)
             CHGVAR     VAR(%SST(&WMM1 5 3)) VALUE(&FIRM)

/* Varselbilde 2                                                     */

             IF         COND(&BTCH *NE '1') THEN(DO)
             CHGVAR     VAR(&WALI) VALUE(4)
             CHGVAR     VAR(&WAPO) VALUE(44)
             CHGVAR     VAR(&MLD1) VALUE('Vennligst vent litt .....')
             CHGVAR     VAR(&MLD2) VALUE('Utplukk pågår ...........')
             CALL       PGM(AA006C)  PARM(&WALI &WAPO &MLD1 &MLD2)
             ENDDO

/* Utplukking av poster                                              */

             CLRPFM     FILE(RKWBPF) MBR(&WMM1)
             MONMSG     MSGID(CPF3100)

/* Bygger membere dersom de ikke finnes                              */

             CHKOBJ     OBJ(RKWBPF) OBJTYPE(*FILE) MBR(&WMM1)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDPFM     FILE(RKWBPF) MBR(&WMM1) TEXT('Poster fra +
                         Renteberegning - forslag')
             ENDDO

             CHKOBJ     OBJ(RKWBLU) OBJTYPE(*FILE) MBR(&WMM1)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDLFM     FILE(RKWBLU) MBR(&WMM1) DTAMBRS((RKWBPF +
                          (&WMM1))) TEXT('Poster fra +
                          Renteberegning - forslag')
             ENDDO

             CHKOBJ     OBJ(RKWBL1) OBJTYPE(*FILE) MBR(&WMM1)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDLFM     FILE(RKWBL1) MBR(&WMM1) DTAMBRS((RKWBPF +
                          (&WMM1))) TEXT('Poster fra +
                          Renteberegning - forslag')
             ENDDO

             CHKOBJ     OBJ(RKWBL2) OBJTYPE(*FILE) MBR(&WMM1)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDLFM     FILE(RKWBL2) MBR(&WMM1) DTAMBRS((RKWBPF +
                          (&WMM1))) TEXT('Poster fra +
                          Renteberegning - forslag')
             ENDDO

             CLRPFM     FILE(RKWBPF) MBR(&WMM1)

/* Utplukking av poster                                              */

             OVRDBF     FILE(RKWBPF) MBR(&WMM1)
             OVRDBF     FILE(RKWBLU) MBR(&WMM1)
             CALL       PGM(RK661R)

             RTVDTAARA  DTAARA(*LDA (465 1))  RTNVAR(&PSKR)

/* Utskrift av kontrolliste - nye saker                              */

             CALL       PGM(RK662C)

             DLTOVR     FILE(RKWBPF)
             DLTOVR     FILE(RKWBLU)
             MONMSG     MSGID(CPF3200)

 END:        ENDPGM
