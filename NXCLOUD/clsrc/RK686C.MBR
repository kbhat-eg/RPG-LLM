             PGM

/* Initiering av parametre.                                          */

             DCL        VAR(&FIRM)  TYPE(*CHAR) LEN(3)
             DCL        VAR(&WMM1)  TYPE(*CHAR) LEN(10) VALUE('FACT    ')
             DCL        VAR(&IN03)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&WSID)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&BTCH)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&WALI)  TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&WAPO)  TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&MLD1)  TYPE(*CHAR) LEN(40)
             DCL        VAR(&MLD2)  TYPE(*CHAR) LEN(40)

/* Henter verdier i fra LDA.                                         */

             RTVDTAARA  DTAARA(*LDA (863 1))  RTNVAR(&BTCH)
             RTVDTAARA  DTAARA(*LDA (944 3))  RTNVAR(&FIRM)

             CHGVAR     VAR(%SST(&WMM1 5 3)) VALUE(&FIRM)

/* Hent inn parameter fra siste kj√∏ring av forslag                   */

             OVRDBF     FILE(RKWAPF) MBR(&WMM1)
             CALL       PGM(RC654R)

/* Sjekk om det finnes poster for uttrekk factoring                  */

             CHKOBJ     OBJ(RKWBPF) OBJTYPE(*FILE) MBR(&WMM1)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             CHGVAR     VAR(&WALI) VALUE(14)
             CHGVAR     VAR(&WAPO) VALUE(2)
             CHGVAR     VAR(&MLD1) VALUE('Factoring uttrekk finnes ikke')
             CHGVAR     VAR(&MLD2) VALUE('Rutinen avsluttes!!    ****')
             CALL       PGM(AA006C)  PARM(&WALI &WAPO &MLD1 &MLD2)
             DLYJOB     DLY(10)
             GOTO       CMDLBL(ENDPGM)
             ENDDO

             CALL       PGM(AP600R) PARM('RK682 ' '1' '0' &IN03 &BTCH)

             IF         COND(&IN03 *EQ '1') THEN(GOTO CMDLBL(ENDPGM))


/* Overstyring av printerfile                                        */

             CALL       PGM(AP601C)    PARM('RK682P' &BTCH)

/* Utskrift av kontrolliste                                          */

             OVRDBF     FILE(RKWBL2) MBR(&WMM1)
             CALL       PGM(RK682R)

             CALL       PGM(AP606C)    PARM('RK682P')

/* Melding dersom bakgrunnsjobb                                      */

             IF         COND(&BTCH *EQ '1') THEN(DO)
             RTVDTAARA  DTAARA(*LDA (901 10)) RTNVAR(&WSID)
             SNDBRKMSG  MSG('ASOKON: Rutinen for utskrift av facto+
                        ring uttrekk er ferdig') TOMSGQ(&WSID)
             ENDDO

             DLTOVR     FILE(RKWAPF)
             DLTOVR     FILE(RKWBL2)
             MONMSG     MSGID(CPF3200)

 ENDPGM:     ENDPGM
