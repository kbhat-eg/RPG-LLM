             PGM        PARM(&P_FIRM &P_VSOR &P_DATO &P_KAT1 &P_KAT2 +
                             &P_KAT3 &P_KAT4 &P_KAT5 &P_MEMB &P_LAGE)

/* Initiering av parametre                                           */

             DCL        VAR(&P_FIRM) TYPE(*DEC)  LEN(3 0)
             DCL        VAR(&P_VSOR) TYPE(*CHAR) LEN(10)
             DCL        VAR(&P_LAGE) TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&P_DATO) TYPE(*CHAR) LEN(10)
             DCL        VAR(&P_KAT1) TYPE(*DEC)  LEN(2)
             DCL        VAR(&P_KAT2) TYPE(*DEC)  LEN(2)
             DCL        VAR(&P_KAT3) TYPE(*DEC)  LEN(2)
             DCL        VAR(&P_KAT4) TYPE(*DEC)  LEN(2)
             DCL        VAR(&P_KAT5) TYPE(*DEC)  LEN(2)
             DCL        VAR(&P_MEMB) TYPE(*CHAR) LEN(10)

             DCL        VAR(&WALI)   TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&WAPO)   TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&MLD1)   TYPE(*CHAR) LEN(40)
             DCL        VAR(&MLD2)   TYPE(*CHAR) LEN(40)
             DCL        VAR(&LGRP)   TYPE(*CHAR) LEN(3)
             DCL        VAR(&LFIR)   TYPE(*CHAR) LEN(3)
             DCL        VAR(&LLAG)   TYPE(*CHAR) LEN(2)
             DCL        VAR(&FFLRU)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&FFLRI)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&tild)  TYPE(*CHAR) LEN(25)
             DCL        VAR(&tilm)  TYPE(*CHAR) LEN(20)

/* Bygger opp mappenavn av filgruppe,firma og lager                  */

             RTVDTAARA  DTAARA(*LDA (931 3))   RTNVAR(&LGRP)
             RTVDTAARA  DTAARA(*LDA (944 3))   RTNVAR(&LFIR)
             RTVDTAARA  DTAARA(*LDA (1001 2))  RTNVAR(&LLAG)
             CHGVAR     VAR(&FFLRU) VALUE(&LGRP *CAT &LFIR  +
                            *CAT &LLAG *CAT '.U')
             CHGVAR     VAR(&FFLRI) VALUE(&LGRP *CAT &LFIR  +
                            *CAT &LLAG *CAT '.I')

/* Prøver å opprette mappe for inn og ut hvis de ikke finnes.        */

             CRTFLR     FLR(&FFLRU) TEXT('Mappe for håndterminal ut')
             MONMSG     MSGID(CPF8A85)
             MONMSG     MSGID(CPF8A18)
             CRTFLR     FLR(&FFLRI) TEXT('Mappe for håndterminal inn')
             MONMSG     MSGID(CPF8A85)
             MONMSG     MSGID(CPF8A18)

/* Tømmer tabell/register                                            */

             CLRPFM     FILE(HBVAPF)
             CLRPFM     FILE(HBKUPF)

/* Kaller program for danning av database - vareregister             */

             ADDPFM     FILE(VVAWPF) MBR(&P_MEMB)
             OVRDBF     FILE(VVAWPF) MBR(&P_MEMB) OVRSCOPE(*CALLLVL)

             CHGVAR     VAR(&WALI) VALUE(4)
             CHGVAR     VAR(&WAPO) VALUE(34)
             CHGVAR     VAR(&MLD1) VALUE('Data til håndterminal.   ')
             CHGVAR     VAR(&MLD2) VALUE('Vareinformasjon.         ')
             CALL       PGM(AA006C)  PARM(&WALI &WAPO &MLD1 &MLD2)
             CALL       PGM(*LIBL/HV701R) PARM(&P_FIRM &P_VSOR +
                          &P_DATO &P_LAGE)

             RMVM       FILE(VVAWPF) MBR(&P_MEMB)

/* Kaller program for danning av database - kunderegister            */

             CHGVAR     VAR(&WALI) VALUE(6)
             CHGVAR     VAR(&WAPO) VALUE(30)
             CHGVAR     VAR(&MLD1) VALUE('Data til håndterminal.   ')
             CHGVAR     VAR(&MLD2) VALUE('Kundeinformasjon.        ')
             CALL       PGM(AA006C)  PARM(&WALI &WAPO &MLD1 &MLD2)
             CALL       PGM(*LIBL/HK701R) PARM(&P_FIRM &P_KAT1 +
                          &P_KAT2 &P_KAT3 &P_KAT4 &P_KAT5)

/* Reorganiserer tabell/register for å få rikig sortering            */

             RGZPFM     FILE(*LIBL/HBVAPF) KEYFILE(*FILE)
             MONMSG     MSGID(CPF2981)
             RGZPFM     FILE(*LIBL/HBKUPF) KEYFILE(*FILE)
             MONMSG     MSGID(CPF2981)

/* Kopierer tabell/register til mappe for kommunikasjon med PC       */

             DLTDLO     DLO(EAN.TXT) FLR(&FFLRU)
             MONMSG     MSGID(CPF8A16)
             DLTDLO     DLO(LVNR.TXT) FLR(&FFLRU)
             MONMSG     MSGID(CPF8A16)
/*           CPYTOPCD   FROMFILE(HBVAPF) TOFLR(&FFLRU) +     */
/*                        TODOC(VARER.TXT) REPLACE(*YES)     */
             chgvar     var(&tild) value('VARER')
             chgvar     var(&tilm) value(&FFLRU)
             call       pgm(asptostmf) +
                        parm('HBVAPF    ' +
                             'HBVAPF    ' +
                             &tilm +
                             &tild   +
                             'TXT')

/*           CPYTOPCD   FROMFILE(HBKUPF) TOFLR(&FFLRU) +     */
/*                        TODOC(KUNDER.TXT) REPLACE(*YES)    */
             chgvar     var(&tild) value('KUNDER')
             chgvar     var(&tilm) value(&FFLRU)
             call       pgm(asptostmf) +
                        parm('HBKUPF    ' +
                             'HBKUPF    ' +
                             &tilm +
                             &tild   +
                             'TXT')

 END:        ENDPGM
