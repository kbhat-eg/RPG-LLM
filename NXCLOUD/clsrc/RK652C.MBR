             PGM

/* Initiering av parametre.                                          */

             DCL        VAR(&FIRM)  TYPE(*CHAR) LEN(3)
             DCL        VAR(&WMM1)  TYPE(*CHAR) LEN(10) VALUE('BETV    ')
             DCL        VAR(&IN03)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&WSID)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&BTCH)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&WALI)  TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&WAPO)  TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&MLD1)  TYPE(*CHAR) LEN(40)
             DCL        VAR(&MLD2)  TYPE(*CHAR) LEN(40)
             DCL        VAR(&PSKR)  TYPE(*CHAR) LEN(1)

/* Henter verdier i fra LDA.                                         */

             RTVDTAARA  DTAARA(*LDA (863 1))  RTNVAR(&BTCH)
             RTVDTAARA  DTAARA(*LDA (944 3))  RTNVAR(&FIRM)
             RTVDTAARA  DTAARA(*LDA (465 1))  RTNVAR(&PSKR)

             CHGVAR     VAR(%SST(&WMM1 5 3)) VALUE(&FIRM)


/* Sjekk om det finnes poster for vedlikehold av kontroll-varsel     */

             CHKOBJ     OBJ(RKWBPF) OBJTYPE(*FILE) MBR(&WMM1)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             CHGVAR     VAR(&WALI) VALUE(14)
             CHGVAR     VAR(&WAPO) VALUE(2)
             CHGVAR     VAR(&MLD1) VALUE('Betalingsvarsel finnes ikke')
             CHGVAR     VAR(&MLD2) VALUE('Rutinen avsluttes!!    ****')
             CALL       PGM(AA006C)  PARM(&WALI &WAPO &MLD1 &MLD2)
             DLYJOB     DLY(10)
             GOTO       CMDLBL(ENDPGM)
             ENDDO

             IF         COND(&PSKR *EQ 'J') THEN(DO)
             CALL       PGM(AP600R) PARM('RK652 ' '1' '0' &IN03 &BTCH)
             ENDDO

             IF         COND(&IN03 *EQ '1') THEN(GOTO CMDLBL(ENDPGM))


/* Overstyring av printerfile                                        */

             CALL       PGM(AP601C)    PARM('RK652P' &BTCH)

/* Utskrift av kontrolliste                                          */

             OVRDBF     FILE(RKWBL2) MBR(&WMM1)
             CALL       PGM(RK652R)

             CALL       PGM(AP606C)    PARM('RK652P')

/* Melding dersom bakgrunnsjobb                                      */

             IF         COND(&BTCH *EQ '1') THEN(DO)
             RTVDTAARA  DTAARA(*LDA (901 10)) RTNVAR(&WSID)
             SNDBRKMSG  MSG('ASOKON: Rutinen for utskrift av betal+
                        ingsvarsel er ferdig') TOMSGQ(&WSID)
             ENDDO

             DLTOVR     FILE(RKWBL2)
             MONMSG     MSGID(CPF3200)
 ENDPGM:     ENDPGM
