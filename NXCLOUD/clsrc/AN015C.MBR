             PGM        PARM(&VALG)

             DCL        VAR(&VALG)     TYPE(*CHAR) LEN(128)
             DCL        VAR(&SPES)     TYPE(*CHAR) LEN(10) +
                          VALUE('ANBACK')
             DCL        VAR(&APTEXT)   TYPE(*CHAR) LEN(30) +
                                VALUE('Natt-rutine, Backup')

/* Sjekk om det er nattjobb 1 eller 2 eller 3                        */

             DCL        VAR(&NATT)     TYPE(*CHAR) LEN(01)
             CHGVAR     VAR(&NATT)     VALUE(%SST(&VALG 26 1))

             IF         COND(&NATT *EQ '1') THEN(DO)
             CHGVAR     VAR(&SPES)     VALUE('ANBAC1')
             CHGVAR     VAR(&APTEXT)   VALUE('Natt-rutine, Backup 1')
             ENDDO

             IF         COND(&NATT *EQ '2') THEN(DO)
             CHGVAR     VAR(&SPES)     VALUE('ANBAC2')
             CHGVAR     VAR(&APTEXT)   VALUE('Natt-rutine, Backup 2')
             ENDDO

             IF         COND(&NATT *EQ '3') THEN(DO)
             CHGVAR     VAR(&SPES)     VALUE('ANBAC3')
             CHGVAR     VAR(&APTEXT)   VALUE('Natt-rutine, Backup 3')
             ENDDO

/* Sjekk om program finnes fra f√∏r                                   */

             CHKOBJ     OBJ(ASPGPLK/QCLLESRC) OBJTYPE(*FILE) +
                          MBR(&SPES) AUT(*ALL)
             MONMSG     MSGID(CPF9815) EXEC(GOTO CMDLBL(START))
             RMVM       FILE(ASPGPLK/QCLLESRC) MBR(&SPES)

/* Bygge opp Cl-member                                               */

 START:      ADDPFM     FILE(ASPGPLK/QCLLESRC) MBR(&SPES) +
                          TEXT(&APTEXT) SRCTYPE(CLLE)
             OVRDBF     FILE(QCLLESRC) TOFILE(ASPGPLK/QCLLESRC) +
                          MBR(&SPES)

/* Call til RPG-program                                              */

             CALL       PGM(AN015R) PARM(&VALG)
             DLTOVR     FILE(QCLLESRC)

/* Create program                                                    */

             CRTBNDCL   PGM(ASPGPLK/&SPES) SRCFILE(ASPGPLK/QCLLESRC) +
                          TEXT(&APTEXT) OPTION(*NOXREF *NOSECLVL)

 END:        ENDPGM
