             PGM

             DCL        VAR(&LPARAM) TYPE(*CHAR) LEN(120)
             DCL        VAR(&IN03) TYPE(*CHAR) LEN(1)
             DCL        VAR(&BTCH)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&FFIR)  TYPE(*CHAR) LEN(3)
             DCL        VAR(&WSID)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&USER)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&MEMBX) TYPE(*CHAR) LEN(5)
             DCL        VAR(&MEMB)  TYPE(*DEC)  LEN(5 0)
             DCL        VAR(&WMM1)  TYPE(*CHAR) LEN(10) VALUE('RKA      ')
             DCL        VAR(&WMM2)  TYPE(*CHAR) LEN(10) VALUE('RKB      ')
             DCL        VAR(&WMMA)  TYPE(*CHAR) LEN(10) VALUE('AUTOG   ')
             DCL        VAR(&WMMB)  TYPE(*CHAR) LEN(10) VALUE('AUTO    ')
             DCL        VAR(&WALI)  TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&WAPO)  TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&MLD1)  TYPE(*CHAR) LEN(40)
             DCL        VAR(&MLD2)  TYPE(*CHAR) LEN(40)

/* Program for printerstyring                                        */

             CALL       PGM(AP600R) PARM('RK813' '1' '0' &IN03 &BTCH)

             IF         COND(&IN03 *EQ '1') THEN(GOTO CMDLBL(END))


/* Henter info fra LDA                                               */

             RTVDTAARA  DTAARA(*LDA (911 10)) RTNVAR(&USER)
             RTVDTAARA  DTAARA(*LDA (944 03)) RTNVAR(&FFIR)
             RTVDTAARA  DTAARA(*LDA (864 5))  RTNVAR(&MEMBX)

             CHGVAR     VAR(&MEMB) VALUE(&MEMBX)
             CHGVAR     VAR(%SST(&WMM1 4 5)) VALUE(&MEMB)
             CHGVAR     VAR(%SST(&WMM2 4 5)) VALUE(&MEMB)
             CHGVAR     VAR(%SST(&WMMA 6 3)) VALUE(&FFIR)
             CHGVAR     VAR(%SST(&WMMB 5 3)) VALUE(&FFIR)

/* Sjekk om det finnes poster for vedlikehold av kontroll-varsel     */

             CHKOBJ     OBJ(RKWCPF) OBJTYPE(*FILE) MBR(&WMMA)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             CHGVAR     VAR(&WALI) VALUE(14)
             CHGVAR     VAR(&WAPO) VALUE(2)
             CHGVAR     VAR(&MLD1) VALUE('Kontroll-varsel finnes ikke')
             CHGVAR     VAR(&MLD2) VALUE('Rutinen avsluttes!!    ****')
             CALL       PGM(AA006C)  PARM(&WALI &WAPO &MLD1 &MLD2)
             DLYJOB     DLY(5)
             GOTO       CMDLBL(END)
             ENDDO

             CLRPFM     FILE(RKW4PF) MBR(&WMM1)
             MONMSG     MSGID(CPF3100)

/* Bygger membere dersom de ikke finnes                              */

             CHKOBJ     OBJ(RKWAPF) OBJTYPE(*FILE) MBR(&WMMB)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDPFM     FILE(RKWAPF) MBR(&WMMB) TEXT('Parameter for +
                          autogiro (RK811R)')
             ENDDO
             CHKOBJ     OBJ(RKW4PF) OBJTYPE(*FILE) MBR(&WMM1)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDPFM     FILE(RKW4PF) MBR(&WMM1) TEXT('Poster til +
                          autogiro (RK812R)')
             ENDDO
             CHKOBJ     OBJ(RKWCPF) OBJTYPE(*FILE) MBR(&WMMA)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDPFM     FILE(RKWCPF) MBR(&WMMA) TEXT('Poster fra +
                          autogiro-kontroll')
             ENDDO
             CHKOBJ     OBJ(RKWCLU) OBJTYPE(*FILE) MBR(&WMMA)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDLFM     FILE(RKWCLU) MBR(&WMMA) DTAMBRS((RKWCPF +
                          (&WMMA))) TEXT('Poster fra +
                          autogiro-kontroll')
             ENDDO
             CHKOBJ     OBJ(RKWCL2) OBJTYPE(*FILE) MBR(&WMMA)
             MONMSG     MSGID(CPF9815) EXEC(DO)
             ADDLFM     FILE(RKWCL2) MBR(&WMMA) DTAMBRS((RKWCPF +
                          (&WMMA))) TEXT('Poster fra +
                          autogiro-kontroll')
             ENDDO
             CLRPFM     FILE(RKW4PF) MBR(&WMM1)

/* Henter parameter                                                  */

             OVRDBF     FILE(RKWAPF) MBR(&WMMB)
             CALL       PGM(RK815R) PARM(&LPARAM)

/* Utplukking av poster                                              */

             OVRDBF     FILE(RKTRPF) TOFILE(*LIBL/RKWCPF) MBR(&WMMA)
             OVRDBF     FILE(RKTRL2) TOFILE(*LIBL/RKWCL2) MBR(&WMMA)
             OVRDBF     FILE(RKWCPF) MBR(&WMMA)
             OVRDBF     FILE(RKWCL2) TOFILE(*LIBL/RKWCL2) MBR(&WMMA)
             OVRDBF     FILE(RKW4PF) MBR(&WMM1)
             CALL       PGM(RK814R) PARM(&LPARAM)
             CLRPFM     FILE(RKWCPF) MBR(&WMMA)

/* Call av program, utskrift av autogiro-kontroll                    */

             CALL       PGM(AP601C) PARM('RK813P' &BTCH)

             OVRDBF     FILE(RKWAPF) MBR(&WMMB)
             OVRDBF     FILE(RKWCPF) MBR(&WMMA)
             CALL       PGM(RK813R) PARM(&LPARAM)

/* Melding dersom bakgrunnsjobb                                      */

             IF         COND(&BTCH *EQ '1') THEN(DO)
             RTVDTAARA  DTAARA(*LDA (901 10)) RTNVAR(&WSID)
             SNDBRKMSG  MSG('AUTOG: Autogiro utskrift er +
                          er ferdig') TOMSGQ(&WSID)
             ENDDO

/* Program for Ã¥ fjerne overstyring av printerfile                   */

             CALL       PGM(AP606C) PARM('RK813P')

             DLTOVR     FILE(RKW4PF)
             DLTOVR     FILE(RKWCPF)
             RMVM       FILE(*LIBL/RKW4PF) MBR(&WMM1)
             MONMSG     MSGID(CPF3200)

 END:        ENDPGM
