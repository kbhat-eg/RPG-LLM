             PGM        parm(&p &f &n)

/*    NB!!!  Søk på Foreløpig og fjern denne linja før virkelig test */
/*                                                                   */
/*   NB!!!!  Denne jobben RA200COMKJ er laget for å kjre om dersom   */
/*           noe går galt.                                           */
/*           Dagens member kopieres da fra RADBPF til RADRPF først.  */
/*           Kjør så:  Call RA200comkj ('   ' '          ' '      ') */
/*                                                                   */


             dcl        var(&filg)  type(*char) len(3)
             dcl        var(&firm)  type(*char) len(3)
             dcl        var(&wfir)  type(*dec)  len(3 0)
             dcl        var(&frem)  type(*char) len(20)
             dcl        var(&fmot)  type(*char) len(20)
             dcl        var(&favr)  type(*char) len(20)
             dcl        var(&focr)  type(*char) len(20)
             dcl        var(&fkto)  type(*char) len(20)
             dcl        var(&fink)  type(*char) len(20)
             dcl        var(&ftff)  type(*char) len(20)
             dcl        var(&bibl)  type(*char) len(6)
             dcl        var(&bank)  type(*char) len(2) value('DN')
             dcl        var(&komm)  type(*char) len(4)
             dcl        var(&in03)  type(*dec)  len(1 0)
             dcl        var(&linj)  type(*dec) len(2 0) value(2)
             dcl        var(&posi)  type(*dec) len(3 0) value(43)
             dcl        var(&fbane) type(*char) len(50)
             dcl        var(&tbane) type(*char) len(50) +
                          value('/qsys.lib/      .lib/+
                                 radrpf.file/radrpf.mbr')
             dcl        var(&mbr)    type(*char)  len(10)
             dcl        var(&usr)    type(*char)  len(10)
             dcl        var(&mbrtxt) type(*char)  len(40)
             dcl        var(&mn)     type(*dec)   len(1 0)
             dcl        var(&mx)     type(*char)  len(1)
             dcl        var(&dat)    type(*char)  len(6)
             dcl        var(&tid)    type(*char)  len(6)
             dcl        var(&mbra)   type(*char)  len(10) Value('TELE')
             dcl        var(&tra)    type(*char)  len(1)
             dcl        var(&p)      type(*char)  len(3)
             dcl        var(&f)      type(*char)  len(10)
             dcl        var(&n)      type(*char)  len(10)

/*  Sjekk om det finnes transer fra TELE fra før                    */
             rtvdtaara  dtaara(*lda (944 03)) rtnvar(&firm)
             chgvar     var(%sst(&mbra 5 3))  value(&firm)
             call       pgm(ra202r)  parm(&mbra &tra)
             if         cond(&tra *eq 'J') then(do)
               call     pgm(aa007r) parm('Det finnes Banktranser +
                        fra før på dette firma     ' +
                        'Behandle disse før du kjører innlesning +
                         på nytt' ' ' ' ')
                goto cmdlbl(slutt)
                enddo

             goto       cmdlbl(noback)

/* Tømmer transfil fra Adra:                                         */

             clrpfm     file(*libl/radrpf)
             monmsg   msgid(cpf3142) exec(do)
               crtpf    file(radrpf) rcdlen(512) text('Mottak av +
                          data fra Adra-Match')
             enddo

             if         cond(&p *ne 'IFS') then(goto cmdlbl(Tmbr))

/* Hente bane hvor inputfil hentes fra                               */

             call       pgm(gl119r) parm(&komm &bank &in03 +
                          &linj &posi &fbane)
/* Tester på om program skal avsluttes.                              */

             if         cond(&in03 *eq 1) then(do)
               goto   cmdlbl(slutt)
             enddo

/* Hente filnavn på inputfil fra Adra                                */
             rtvdtaara  dtaara(*lda (911 10)) rtnvar(&usr)
             rtvdtaara  dtaara(*lda (931 03)) rtnvar(&filg)
             chgvar     var(&wfir)  value(&firm)
             call       pgm(gl320c) parm(&filg &wfir &frem +
                          &fmot &favr &focr &fkto &fink &ftff)
             chgvar     var(&fbane) value(&fbane *tcat &focr)
/* Henter *curlib:                                                   */

             rtvdtaara  dtaara(*lda (934 6)) rtnvar(&bibl)

/*   Bygger path til bruk i IFS-kommandoen:                          */
             chgvar     var(%sst(&tbane 11 6)) value(&bibl)

/* Henter inn og kopierer Adra-transer                               */
             cpyfrmstmf fromstmf(&fbane) tombr(&tbane) mbropt(*add) +
                          stmfcodpag(*PCASCII)
             monmsg     msgid(cpf0000)

/* Sletter input fra inputområdet                                    */
             rmvlnk     objlnk(&fbane)
             monmsg     msgid(cpf0000)
tst2:
/* Ta backup av innlest fil til RADBPF m/member = Maammddnn          */

/*  Sjekk om backupfil finnes                                        */
             chkobj     obj(*libl/radbpf)  objtype(*file)
             monmsg     msgid(cpf9815 cpf9801) exec(do)
               crtpf    file(*curlib/radbpf) rcdlen(512) +
                        maxmbrs(*nomax) size(*nomax) +
                        text('Backup av transer fra Adra')
             enddo
/*  Lage membernavn i backupfil                                      */
             chgvar     var(&mn) value(9)
             chgvar     var(&mx) value('9')
             rtvjoba    date(&dat)
             rtvsysval  sysval(qtime)  rtnvar(&tid)
mbrnext:     chgvar     var(&mbr)  value('M' *cat %sst(&dat 5 2) +
                        *cat %sst(&dat 3 2) *cat %sst(&dat 1 2) +
                        *cat (&mx))
             chgvar     var(&mbrtxt) value('Dato:' *cat &dat *cat +
                        '     Kl.:' *cat &tid *cat +
                        '    Bruker:' *cat &usr)
             chkobj     obj(*libl/radbpf) objtype(*file) mbr(&mbr)
             monmsg     msgid(cpf9815) exec(do)
               addpfm   file(*libl/radbpf) mbr(&mbr) text(&mbrtxt)
               goto     cmdlbl(mbrok)
             enddo
             if         cond(&mn *eq 0) then(goto cmdlbl(noback))
             chgvar     var(&mn) value(&mn - 1)
             chgvar     var(&mx) value(&mn)
             goto       cmdlbl(mbrnext)

/*   Ta backup av inputfil                                           */
mbrok:       cpyf       fromfile(*libl/radrpf) tofile(*curlib/radbpf) +
                        frommbr(*first) tombr(&mbr) mbropt(*add) +
                        fmtopt(*nochk)
             monmsg     msgid(cpf2817 cpf2863)

             goto       cmdlbl(noback)

/*  Hente inn tildigere innlest fil                                  */

 tmbr:       cpyf       fromfile(&f) tofile(radrpf) frommbr(&n) +
                          mbropt(*replace) fmtopt(*nochk)
noback:
/*    Bygge økonomitranser                                           */
             clrpfm     file(re0tpf) mbr(&mbra)
             monmsg     msgid(cpf3141) exec(do)
               addpfm   file(re0tpf) mbr(&mbra) +
                        text('Innbetalinger fra Adra ')
             enddo
             ovrdbf     file(re0tpf)  mbr(&mbra)
             call       pgm(ra200r)

/*  Tilrettelegger poster for oppdatering                            */

             ovrdbf     file(re0tpf) mbr(&mbra)
             call       pgm(re115r)  parm(&mbra)
             dltovr     file(re0tpf)

slutt:       ENDPGM
