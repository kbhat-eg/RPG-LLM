             PGM        PARM(&PARM)
/* 070715 ASK: Utvidet feltlengde &NUMM  etter utv.         */

             DCL        VAR(&PARM)     TYPE(*CHAR) LEN(1000)

             DCL        VAR(&MEMBER)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&WIDTH)    TYPE(*CHAR) LEN(4)
             DCL        VAR(&LENGTH)   TYPE(*CHAR) LEN(4)
             DCL        VAR(&TIL)      TYPE(*CHAR) LEN(50)
             DCL        VAR(&FRA)      TYPE(*CHAR) LEN(50)
             DCL        VAR(&TXT1)     TYPE(*CHAR) LEN(50)
             DCL        VAR(&TXT2)     TYPE(*CHAR) LEN(50)
             DCL        VAR(&TXT3)     TYPE(*CHAR) LEN(50)
             DCL        VAR(&TXT4)     TYPE(*CHAR) LEN(50)
             DCL        VAR(&SUBJ)     TYPE(*CHAR) LEN(250)
             DCL        VAR(&FOTNOTE)  TYPE(*CHAR) LEN(50)
             DCL        VAR(&KONTAKT)  TYPE(*CHAR) LEN(50)
             DCL        VAR(&SETUPF)   TYPE(*CHAR) LEN(100)
             DCL        VAR(&SENDNAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOBNAME)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&KUND)     TYPE(*CHAR) LEN(6)
             DCL        VAR(&NUMM)     TYPE(*CHAR) LEN(8)

             DCL        VAR(&PDFP)     TYPE(*CHAR) LEN(96)
             DCL        VAR(&PDFNAME)  TYPE(*CHAR) LEN(111)
             DCL        VAR(&LOGFILE)  TYPE(*CHAR) LEN(110)
             DCL        VAR(&MAILSERV) TYPE(*CHAR) LEN(35)
             DCL        VAR(&MSTATUS)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&STATUS)   TYPE(*CHAR) LEN(1) VALUE('1')

             DCL        VAR(&JAVA)     TYPE(*CHAR) LEN(1000)
             DCL        VAR(&JAVALOG)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&JAVAERR)  TYPE(*CHAR) LEN(10)

             DCL        VAR(&USERNAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CURL)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&FIRM)     TYPE(*CHAR) LEN(3)
             DCL        VAR(&NFIRM) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&JOBNR)    TYPE(*CHAR) LEN(6)

             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(ERROR))

             RTVJOBA    JOB(&JOBNAME) USER(&USERNAME) NBR(&JOBNR) +
                          CURLIB(&CURL)

             RTVDTAARA  DTAARA(*LDA (944 3)) RTNVAR(&FIRM)
             CHGVAR     VAR(&NFIRM) VALUE(&FIRM)

/* Tar ut individuelle parametre fra hovedparm                       */
             CHGVAR     VAR(&MEMBER)   VALUE(%SST(&PARM 1 10))
             CHGVAR     VAR(&WIDTH)    VALUE(%SST(&PARM 11 4))
             CHGVAR     VAR(&LENGTH)   VALUE(%SST(&PARM 15 4))
             CHGVAR     VAR(&TIL)      VALUE(%SST(&PARM 19 50))
             CHGVAR     VAR(&FRA)      VALUE(%SST(&PARM 69 50))
             CHGVAR     VAR(&SUBJ)     VALUE(%SST(&PARM 119 250))
             CHGVAR     VAR(&FOTNOTE)  VALUE(%SST(&PARM 369 50))
             CHGVAR     VAR(&KONTAKT)  VALUE(%SST(&PARM 419 50))
             CHGVAR     VAR(&SETUPF)   VALUE(%SST(&PARM 469 100))
             CHGVAR     VAR(&SENDNAME) VALUE(%SST(&PARM 569 10))
             CHGVAR     VAR(&KUND)     VALUE(%SST(&PARM 579 6))
             CHGVAR     VAR(&NUMM)     VALUE(%SST(&PARM 585 8))
             CHGVAR     VAR(&TXT1)     VALUE(%SST(&PARM 593 50))
             CHGVAR     VAR(&TXT2)     VALUE(%SST(&PARM 643 50))
             CHGVAR     VAR(&TXT3)     VALUE(%SST(&PARM 693 50))
             CHGVAR     VAR(&TXT4)     VALUE(%SST(&PARM 743 50))

/* Lager katalogen /HOME på IFS hvis den ikke allerede finnes.       */
/* PDF filene genereres i denne mappen før de sendes.                */
             CRTDIR     DIR('/home') DTAAUT(*INDIR) OBJAUT(*INDIR)
             MONMSG     MSGID(CPFA0A0)
/* Lager underkatalog for filgruppe under /home                      */
             CHGVAR     VAR(&PDFP) VALUE('/home/' *TCAT &CURL)
             CRTDIR     DIR(&PDFP) DTAAUT(*INDIR) OBJAUT(*INDIR)
             MONMSG     MSGID(CPFA0A0)
/* Lager underkatalog for sesjonen under /home/&CURL                 */
             CHGVAR     VAR(&PDFP) VALUE('/home/' *TCAT &CURL *TCAT +
                          '/' *TCAT &USERNAME)
             CRTDIR     DIR(&PDFP) DTAAUT(*INDIR) OBJAUT(*INDIR)
             MONMSG     MSGID(CPFA0A0)
/* Lager underkatalog for sesjonen under /home/&CURL/&USERNAME       */
             CHGVAR     VAR(&PDFP) VALUE(&PDFP *TCAT '/' *TCAT +
                          &JOBNAME *TCAT &JOBNR)
             CRTDIR     DIR(&PDFP) DTAAUT(*INDIR) OBJAUT(*INDIR)
             MONMSG     MSGID(CPFA0A0)

/* Setter opp java logging til en fil                                */
             CHGVAR     VAR(&JAVALOG) VALUE('J' *TCAT &USERNAME +
                          *TCAT &JOBNAME)
             CLRPFM     FILE(ADB/ALOGPF) MBR(&JAVALOG)
             MONMSG     MSGID(CPF0000) EXEC(ADDPFM FILE(ADB/ALOGPF) +
                          MBR(&JAVALOG) TEXT('Logger Java ved +
                          mailsending av PDF'))
             OVRDBF     FILE(STDOUT) TOFILE(ADB/ALOGPF) MBR(&JAVALOG)

/* Fjerner evt. gammel testfil for PDF generering                    */
             CHGVAR     VAR(&LOGFILE) VALUE(&PDFP *TCAT '/' *TCAT +
                          &SENDNAME *TCAT '.pdf.log')
             RMVLNK     OBJLNK(&LOGFILE)
             MONMSG     MSGID(CPFA093 CPFA0A9)

/* Setter sammen Java kall                                           */
             CHGVAR     VAR(&JAVA) VALUE('java asp.pdf.CreatePdfFile +
                          -Lqgpl -Ftmpspool -M' *TCAT &MEMBER *BCAT +
                          '-P' *TCAT &PDFP *TCAT '/' *TCAT +
                          &SENDNAME *TCAT '.pdf -ll' *TCAT &WIDTH +
                          *BCAT '-ln' *TCAT &LENGTH *BCAT '"-fc' +
                          *TCAT &FOTNOTE *TCAT '" +
                          -f/java/asp/pdf/courier.ttf')
             QSH        CMD(&JAVA)

 /*  Fjerner Spoolfile member fra spool DB fil                        */
             RMVM       FILE(QGPL/TMPSPOOL) MBR(&MEMBER)

 /*  Hvis loggfilen finnes så var det suksess                         */
             RMVLNK     OBJLNK(&LOGFILE)
             MONMSG     MSGID(CPFA0A9) EXEC(GOTO CMDLBL(ERROR))

             CHGVAR     VAR(&STATUS) VALUE('2')

 /* Henter ut mailserver                                              */
             CALL       PGM(AM705C) PARM(&MAILSERV &MSTATUS)

             CHGVAR     VAR(&STATUS) VALUE('3')

/* Navn på testmember. Brukes for å se at Java har gått bra           */
/*           CHGVAR     VAR(&JAVAERR) VALUE('E' *TCAT &JOBNAME)       */
/*           RMVM       FILE(ADB/ALOGPF) MBR(&JAVAERR)                */
/*           MONMSG     MSGID(CPF0000)                                */

             CHGVAR     VAR(&PDFNAME) VALUE(&PDFP *TCAT '/' *TCAT +
                          &SENDNAME *TCAT '.pdf')

 /* Setter sammen java kall for mailsending                           */
             CHGVAR     VAR(&JAVA) VALUE('java asp.mail.SendMail2 +
                          "-server' *TCAT &MAILSERV *TCAT '"' *BCAT +
                          '"-file' *TCAT &PDFNAME *TCAT '" +
                          -firmaNr' *TCAT &FIRM *BCAT '-dataLib' +
                          *TCAT &CURL)
 /*          CHGVAR     VAR(&JAVA) VALUE(&JAVA *BCAT +              */
 /*                       '"-errFileadb/alogpf.' *TCAT &JAVAERR +   */
 /*                       *TCAT '"')                                */

             IF         COND('X' *TCAT &TXT1 *NE 'X') THEN(CHGVAR +
                          VAR(&JAVA) VALUE(&JAVA *BCAT '"-text' +
                          *TCAT &TXT1 *TCAT '"'))
             IF         COND('X' *TCAT &TXT2 *NE 'X') THEN(CHGVAR +
                          VAR(&JAVA) VALUE(&JAVA *BCAT '"-text' +
                          *TCAT &TXT2 *TCAT '"'))
             IF         COND('X' *TCAT &TXT3 *NE 'X') THEN(CHGVAR +
                          VAR(&JAVA) VALUE(&JAVA *BCAT '"-text' +
                          *TCAT &TXT3 *TCAT '"'))
             IF         COND('X' *TCAT &TXT4 *NE 'X') THEN(CHGVAR +
                          VAR(&JAVA) VALUE(&JAVA *BCAT '"-text' +
                          *TCAT &TXT4 *TCAT '"'))
             CHGVAR     VAR(&JAVA) VALUE(&JAVA *BCAT '"-text"')
             IF         COND('X' *TCAT &SETUPF *NE 'X') THEN(CHGVAR +
                          VAR(&JAVA) VALUE(&JAVA *BCAT +
                          '"-setupFile' *TCAT &SETUPF *TCAT '"'))
             IF         COND('X' *TCAT &TIL *NE 'X') THEN(CHGVAR +
                          VAR(&JAVA) VALUE(&JAVA *BCAT '"-to' *TCAT +
                          &TIL *TCAT '"'))
             IF         COND('X' *TCAT &FRA *NE 'X') THEN(CHGVAR +
                          VAR(&JAVA) VALUE(&JAVA *BCAT '"-from' +
                          *TCAT &FRA *TCAT '"'))
             IF         COND('X' *TCAT &SUBJ *NE 'X') THEN(CHGVAR +
                          VAR(&JAVA) VALUE(&JAVA *BCAT '"-subject' +
                          *TCAT &SUBJ *TCAT '"'))
             IF         COND('X' *TCAT &KONTAKT *NE 'X') THEN(CHGVAR +
                          VAR(&JAVA) VALUE(&JAVA *BCAT '"-text " +
                          "-text' *TCAT &KONTAKT *TCAT '"'))
             IF         COND('X' *TCAT &KUND *NE 'X') THEN(CHGVAR +
                          VAR(&JAVA) VALUE(&JAVA *BCAT '-kundenr' +
                          *TCAT &KUND))
             IF         COND('X' *TCAT &NUMM *NE 'X') THEN(CHGVAR +
                          VAR(&JAVA) VALUE(&JAVA *BCAT '-ordrenr' +
                          *TCAT &NUMM))
             CHGVAR     VAR(&JAVA) VALUE(&JAVA *BCAT '"-text"')

             STRQSH     CMD(&JAVA)

/*           RMVM       FILE(ADB/ALOGPF) MBR(&JAVAERR)                       */
/*           MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(ERROR))              */

/* Avslutter programmet.                                                     */
SLUTT:
             DLTOVR     FILE(STDOUT)
             MONMSG     MSGID(CPF0000)
             RMVLNK     OBJLNK(&PDFNAME)
             RMVM       FILE(ADB/ALOGPF) MBR(&JAVALOG)
             CHGVAR     VAR(&STATUS) VALUE(' ')
             RETURN

ERROR:
             IF         COND(&STATUS *EQ '1') THEN(SNDMSG MSG('Kunne +
                          ikke lage PDF fil') TOUSR(&USERNAME))
             IF         COND(&STATUS *EQ '2') THEN(SNDMSG MSG('Kunne +
                          ikke hente ut mailserver') TOUSR(&USERNAME))
             IF         COND(&STATUS *EQ '3') THEN(SNDMSG MSG('Kunne +
                          ikke sende mail') TOUSR(&USERNAME))

             DLTOVR     FILE(STDOUT)
             MONMSG     MSGID(CPF0000)
             ENDPGM
