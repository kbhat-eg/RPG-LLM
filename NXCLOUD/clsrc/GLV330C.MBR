/*                                                                   */
             PGM

             DCL        VAR(&WALI)  TYPE(*DEC) LEN(2 0)
             DCL        VAR(&WAPO)  TYPE(*DEC) LEN(2 0)
             DCL        VAR(&MLD1)  TYPE(*CHAR) LEN(40)
             DCL        VAR(&MLD2)  TYPE(*CHAR) LEN(40)
             DCL        VAR(&A1FGRP) TYPE(*CHAR) LEN(3) VALUE('   ')
             DCL        VAR(&A1NULL) TYPE(*CHAR) LEN(1) VALUE(' ')

             DCLF       FILE(ADB/AFILPF)

             CHGVAR     VAR(&WALI) VALUE(2)
             CHGVAR     VAR(&WAPO) VALUE(04)
             CHGVAR     VAR(&MLD1) VALUE('KONVERTERING AV ASTELE ')
             CHGVAR     VAR(&MLD2) VALUE('TIL VERSJON 3.30 :-)   ')
             CALL       PGM(AA006C) PARM(&WALI &WAPO &MLD1 &MLD2)
             DLYJOB     DLY(5)
/*                                                                   */
/*  Tar kopi av filene GATRPF, GBTRPF og GAVTPF                      */
/*                                                                   */
             CPYF       FROMFILE(ADB/GATRPF) TOFILE(ADB/GATRPFV320) +
                          CRTFILE(*YES)
             MONMSG     MSGID(CPF0000)
/*                                                                   */
             CPYF       FROMFILE(ADB/GBTRPF) TOFILE(ADB/GBTRPFV320) +
                          CRTFILE(*YES)
             MONMSG     MSGID(CPF0000)
/*                                                                   */
             CPYF       FROMFILE(ADB/GAVTPF) TOFILE(ADB/GAVTPFV320) +
                          CRTFILE(*YES)
             MONMSG     MSGID(CPF0000)

             CHGVAR     VAR(&WALI) VALUE(2)
             CHGVAR     VAR(&WAPO) VALUE(42)
             CHGVAR     VAR(&MLD1) VALUE('Generering av database-')
             CHGVAR     VAR(&MLD2) VALUE('filer i ADB pågår...   ')
             CALL       PGM(AA006C) PARM(&WALI &WAPO &MLD1 &MLD2)
             DLYJOB     DLY(1)
/*                                                                   */
             CALL       PGM(GGATRC) PARM(&A1FGRP &A1NULL)
             CALL       PGM(GGBTRC) PARM(&A1FGRP &A1NULL)
             CALL       PGM(GGAVTC) PARM(&A1FGRP &A1NULL)
             CALL       PGM(GGRETC) PARM(&A1FGRP &A1NULL)
             CALL       PGM(GGISOC) PARM(&A1FGRP &A1NULL)

/*  Leser systemfil med filgrupper:                                  */

LESFIL:      RCVF
             MONMSG     MSGID(CPF0864)   EXEC(GOTO CMDLBL(NESTE))

/*           Tester på om biblioteket finnes:                     */

             CHKOBJ     OBJ(&AGBIBL) OBJTYPE(*LIB)
             MONMSG     MSGID(CPF9801) EXEC(GOTO CMDLBL(LESFIL))
             CHGCURLIB  CURLIB(&AGBIBL)

/*           Tester på om remitteringsfiler finnes.  Dette gjøres    */
/*           ved å teste på fil GUBFPF:                              */

             CHKOBJ     OBJ(*CURLIB/GUBFPF) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(GOTO CMDLBL(LESFIL))
/*                                                                   */
             CHGVAR     VAR(&WALI) VALUE(4)
             CHGVAR     VAR(&WAPO) VALUE(40)
             CHGVAR     VAR(&MLD1) VALUE('Generering av database-')
             CHGVAR     VAR(&MLD2) VALUE('filer for bibliotek ADB' *CAT &AGFGRP)
             CALL       PGM(AA006C) PARM(&WALI &WAPO &MLD1 &MLD2)
             DLYJOB     DLY(1)
/*                                                                   */
             CALL       PGM(GGTRTC) PARM(&AGFGRP &A1NULL)
             CALL       PGM(GGFASC) PARM(&AGFGRP &A1NULL)
             CALL       PGM(GGUBFC) PARM(&AGFGRP &A1NULL)
             CALL       PGM(DGDKIC) PARM(&A1FGRP &A1NULL)
/*                                                                   */
             GOTO       CMDLBL(LESFIL)
NESTE:
/*                                                                   */
/* Legger inn Soneinformasjon i GAVTPF, for alle avtaler.            */
/*                                                                   */
             CHGVAR     VAR(&WALI) VALUE(6)
             CHGVAR     VAR(&WAPO) VALUE(38)
             CHGVAR     VAR(&MLD1) VALUE('Etablerer soneinform. i')
             CHGVAR     VAR(&MLD2) VALUE('avtaleregisteret...    ')
             CALL       PGM(AA006C) PARM(&WALI &WAPO &MLD1 &MLD2)
             DLYJOB     DLY(5)
             CALL       PGM(GL231C)
/*                                                                   */
/* Konvertering GATRPF/GBTRPF til sonekontonummer, for alle avtaler. */
/*                                                                   */
             CHGVAR     VAR(&WALI) VALUE(8)
             CHGVAR     VAR(&WAPO) VALUE(36)
             CHGVAR     VAR(&MLD1) VALUE('Legger inn soneinform. ')
             CHGVAR     VAR(&MLD2) VALUE('i GATRPF og GBTRPF...  ')
             CALL       PGM(AA006C) PARM(&WALI &WAPO &MLD1 &MLD2)
             DLYJOB     DLY(5)
             CALL       PGM(GL230R)

             CHGVAR     VAR(&WALI) VALUE(8)
             CHGVAR     VAR(&WAPO) VALUE(04)
             CHGVAR     VAR(&MLD1) VALUE('ASTELE ER NÅ KONVERTERT')
             CHGVAR     VAR(&MLD2) VALUE('TIL VERSJON 3.30 :-)   ')
             CALL       PGM(AA006C) PARM(&WALI &WAPO &MLD1 &MLD2)
             DLYJOB     DLY(5)


END:         ENDPGM
