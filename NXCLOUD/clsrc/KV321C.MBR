
/*           Generere   Dette program regenererer statistikkfilen    */
/*                      (CHGPF) og bygger 3 nye logiske filer.       */
/*                      Benyttes ved overgang fra V3.10 til V3.20    */


             PGM

             DCL        VAR(&FGRP)     TYPE(*CHAR) LEN(3)
             DCL        VAR(&BIBL)     TYPE(*CHAR) LEN(6)
             DCL        VAR(&MLD1)     TYPE(*CHAR) LEN(40)
             DCL        VAR(&MLD2)     TYPE(*CHAR) LEN(40)
             DCL        VAR(&MLD3)     TYPE(*CHAR) LEN(50)
             DCL        VAR(&MLD4)     TYPE(*CHAR) LEN(50)
             DCL        VAR(&SVAR)     TYPE(*CHAR) LEN(1)
             DCL        VAR(&IN12)     TYPE(*DEC)  LEN(1 0)
             DCL        VAR(&WALI)     TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&WAPO)     TYPE(*DEC)  LEN(2 0)

/* Hente inn filgruppe/ databibliotek fra LDA                        */

             RTVDTAARA  DTAARA(*LDA (931 03)) RTNVAR(&FGRP)
             RTVDTAARA  DTAARA(*LDA (934  6)) RTNVAR(&BIBL)

/* Varselbilde med bekreftelse på om jobb skal settes i gang         */

             CHGVAR     VAR(&MLD3) +
                        VALUE('       er valgt som databibliotek ')
             CHGVAR     VAR(&MLD4) +
                        VALUE('       Bekreft at dette er riktig !')
             CHGVAR     VAR(%SST(&MLD3 1 6)) VALUE(&BIBL)

             CALL       PGM(AA007R)    PARM(&MLD3 &MLD4 &SVAR &IN12)

             IF         COND(&IN12 *EQ 1 ) +
                          THEN(GOTO CMDLBL(ENDPGM))

             IF         COND(&SVAR *EQ ' ' *OR &SVAR *EQ 'J') +
                          THEN(GOTO CMDLBL(VIDERE))
             ELSE       CMD(GOTO CMDLBL(ENDPGM))

VIDERE:

             IF         COND(&FGRP *EQ '   ') THEN(DO)
             GOTO       CMDLBL(ENDPGM)
             ENDDO

/*   START av ønskede rutiner pr. file-gruppe                        */
/*   ----------------------------------------                        */

/* Varselbilde 1                                                     */

             CHGVAR     VAR(&WALI) VALUE(4)
             CHGVAR     VAR(&WAPO) VALUE(2)
             CHGVAR     VAR(&MLD1) VALUE('Regenererer statistikk ... ')
             CHGVAR     VAR(&MLD2) VALUE('File-GRUPPE = xxx   ASSTAT ')
             CHGVAR     VAR(%SST(&MLD2 15 3)) VALUE(&FGRP)
             CALL       PGM(AA006C)  PARM(&WALI &WAPO &MLD1 &MLD2)


/*          Regenererer statistikk ved hjelp av CHGPF                */

/*                                                                   */
             CHGPF      FILE(*LIBL/SSTAPF) SRCFILE(*LIBL/QDDFSRC)

/* Varselbilde 2                                                     */

             CHGVAR     VAR(&WALI) VALUE(6)
             CHGVAR     VAR(&WAPO) VALUE(6)
             CHGVAR     VAR(&MLD1) VALUE('Bygger logiske filer ..... ')
             CHGVAR     VAR(&MLD2) VALUE('File-GRUPPE = xxx   ASSTAT ')
             CHGVAR     VAR(%SST(&MLD2 15 3)) VALUE(&FGRP)
             CALL       PGM(AA006C)  PARM(&WALI &WAPO &MLD1 &MLD2)


/*          Sletter indexer dersom de finnes fra før                 */

/*                                                                   */
             DLTF       FILE(&BIBL/SSTALE)
             MONMSG     MSGID(CPF2105)
             DLTF       FILE(&BIBL/SSTALF)
             MONMSG     MSGID(CPF2105)
             DLTF       FILE(&BIBL/SSTALG)
             MONMSG     MSGID(CPF2105)


/*           Start danning av registre                               */

/* LF:SSTALE                                                         */

             CRTLF      FILE(&BIBL/SSTALE) +
                          SRCFILE(*LIBL/QDDFSRC) MAXMBRS(*NOMAX)
             MONMSG     MSGID(CPF7302)
/*                                                                   */
/* LF:SSTALF                                                         */
/*                                                                   */
             CRTLF      FILE(&BIBL/SSTALF) +
                          SRCFILE(*LIBL/QDDFSRC) MAXMBRS(*NOMAX)
             MONMSG     MSGID(CPF7302)
/*                                                                   */
/* LF:SSTALG                                                         */
/*                                                                   */
             CRTLF      FILE(&BIBL/SSTALG) +
                          SRCFILE(*LIBL/QDDFSRC) MAXMBRS(*NOMAX)
             MONMSG     MSGID(CPF7302)
/*                                                                   */
 ENDPGM:     ENDPGM
