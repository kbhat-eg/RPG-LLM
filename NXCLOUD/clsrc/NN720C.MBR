             PGM

/* Initiering av variabler                                           */

             DCL        VAR(&FTPNAV) TYPE(*CHAR) LEN(10) +
                          VALUE('NBSLIST')
             DCL        VAR(&STATUS) TYPE(*CHAR) LEN(1)  VALUE(' ')
             DCL        VAR(&TILNAV) TYPE(*CHAR) LEN(20) VALUE(' ')
             DCL        VAR(&VISMLD) TYPE(*CHAR) LEN(1)  VALUE('1')
             DCL        VAR(&JOBTYP) TYPE(*CHAR) LEN(1)
             DCL        VAR(&MEMB)   TYPE(*CHAR) LEN(10)

/* Henter jobbtype                                                   */

             RTVJOBA    TYPE(&JOBTYP)

/* Setter biblioteksliste og current library dersom batch-jobb       */

/*           IF         COND(&JOBTYP *EQ '0') THEN(DO)               */
/*           CALL       PGM(AA700R) PARM('ASP_fgr   ')               */
/*           ENDDO                                                   */

/* Oppretter rapportfil hvis den ikke allerede finnes                */

             CHKOBJ     OBJ(*CURLIB/NETVRAPP) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(CRTSRCPF FILE(NETVRAPP) +
                          RCDLEN(200) MBR(*FILE) TEXT('Rapportfil +
                          for nettvaregenerering') SIZE(*NOMAX))

/* Blanker ut gammel nettvare rapport fil                            */

             CLRPFM     FILE(NETVRAPP) MBR(NETVRAPP)
             MONMSG     MSGID(CPF3141) EXEC(ADDPFM +
                          FILE(*CURLIB/NETVRAPP) MBR(NETVRAPP))

/* Blanker ut gammel nettlistfil                                     */

             CLRPFM     FILE(NETLIST) MBR(NETLIST)
             MONMSG     MSGID(CPF3141) EXEC(ADDPFM +
                          FILE(*CURLIB/NETLIST) MBR(NETLIST))

/* Oppretter og overrider member i arbeidsfil                        */

             CALL       PGM(CRTMEMB) PARM(&MEMB)
             ADDPFM     FILE(VVAWPF) MBR(&MEMB)
             OVRDBF     FILE(VVAWPF) MBR(&MEMB) OVRSCOPE(*CALLLVL)


/* Kjører generering av ny nettvarefil                               */

             CALL       PGM(NN720R) PARM(&STATUS)
             IF         COND(&STATUS *EQ 'R') THEN(DO)
             SNDPGMMSG  MSG('Noen nettvarer ble utelatt fra sending. +
                          Se rapport for detaljer.')
             ENDDO
             ELSE IF    COND(&STATUS *NE ' ') THEN(DO)
             SNDPGMMSG  MSG('Nettbutikk finnes ikke for dette firma')
             GOTO       CMDLBL(END)
             ENDDO

             CHGVAR     VAR(&STATUS) VALUE(' ')

/* Fjerner member i arbeidsfil                                       */

             RMVM       FILE(VVAWPF) MBR(&MEMB)

/* Overfører nettvarefil til NBS via FTP                             */

             CALL       PGM(AF700C) PARM(&FTPNAV &STATUS &TILNAV +
                          &JOBTYP)

/* Gir beskjed ved feil                                                 */

             IF         COND(&STATUS *NE ' ') THEN(SNDPGMMSG +
                          MSG('Feil ved sending av handlelistefil'))

END:         ENDPGM
