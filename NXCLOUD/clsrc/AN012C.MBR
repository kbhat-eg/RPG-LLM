             PGM        PARM(&HELE)
             DCL        VAR(&HELE) TYPE(*CHAR) LEN(128)
             DCL        VAR(&TIME) TYPE(*CHAR) LEN(06) +
                                   VALUE('000000')
             DCL        VAR(&RORG) TYPE(*CHAR) LEN(01)
             DCL        VAR(&BACK) TYPE(*CHAR) LEN(01)
             DCL        VAR(&PRC1) TYPE(*CHAR) LEN(01)
             DCL        VAR(&PRC2) TYPE(*CHAR) LEN(01)
             DCL        VAR(&NATT) TYPE(*CHAR) LEN(01)
/*                                                                   */
             DCL        VAR(&NAT1) TYPE(*CHAR) LEN(10) +
                                   VALUE('ASNAT1')
             DCL        VAR(&NAT2) TYPE(*CHAR) LEN(10) +
                                   VALUE('ASNAT2')
             DCL        VAR(&NAT3) TYPE(*CHAR) LEN(10) +
                                   VALUE('ASNAT3')
             DCL        VAR(&TXT1) TYPE(*CHAR) LEN(30) +
                                   VALUE('Natt-rutine 1')
             DCL        VAR(&TXT2) TYPE(*CHAR) LEN(30) +
                                   VALUE('Natt-rutine 2')
             DCL        VAR(&TXT3) TYPE(*CHAR) LEN(30) +
                                   VALUE('Natt-rutine 3')

/* Bygge opp variabler                                               */

             CHGVAR     VAR(&TIME) VALUE(%SST(&HELE 01 6))
             CHGVAR     VAR(&RORG) VALUE(%SST(&HELE 18 1))
             CHGVAR     VAR(&BACK) VALUE(%SST(&HELE 19 1))
             CHGVAR     VAR(&PRC1) VALUE(%SST(&HELE 21 1))
             CHGVAR     VAR(&PRC2) VALUE(%SST(&HELE 22 1))
             CHGVAR     VAR(&NATT) VALUE(%SST(&HELE 26 1))

/* Sjekk om program finnes fra før for ASNAT1                        */

             IF         COND(&NATT *EQ '1') THEN(DO)
             CHKOBJ     OBJ(ASPGPLK/QCLLESRC) OBJTYPE(*FILE) +
                          MBR(&NAT1) AUT(*ALL)
             MONMSG     MSGID(CPF9815) EXEC(GOTO CMDLBL(START1))
             RMVM       FILE(ASPGPLK/QCLLESRC) MBR(&NAT1)

/* Bygge opp Cl-member                                               */

 START1:     ADDPFM     FILE(ASPGPLK/QCLLESRC) MBR(&NAT1) +
                          TEXT(&TXT1) SRCTYPE(CLLE)
             OVRDBF     FILE(QCLLESRC) TOFILE(ASPGPLK/QCLLESRC) +
                          MBR(&NAT1)

/* Call til RPG-program                                              */

             CALL       PGM(AN012R) PARM(&HELE)
             DLTOVR     FILE(QCLLESRC)

             CRTBNDCL   PGM(ASPGPLK/&NAT1) SRCFILE(ASPGPLK/QCLLESRC) +
                          TEXT(&TXT1) OPTION(*NOXREF *NOSECLVL)
             ENDDO

/* Sjekk om program finnes fra før for ASNAT2                        */

             IF         COND(&NATT *EQ '2') THEN(DO)
             CHKOBJ     OBJ(ASPGPLK/QCLLESRC) OBJTYPE(*FILE) +
                          MBR(&NAT2) AUT(*ALL)
             MONMSG     MSGID(CPF9815) EXEC(GOTO CMDLBL(START2))
             RMVM       FILE(ASPGPLK/QCLLESRC) MBR(&NAT2)

/* Bygge opp Cl-member                                               */

 START2:     ADDPFM     FILE(ASPGPLK/QCLLESRC) MBR(&NAT2) +
                          TEXT(&TXT2) SRCTYPE(CLLE)
             OVRDBF     FILE(QCLLESRC) TOFILE(ASPGPLK/QCLLESRC) +
                          MBR(&NAT2)

/* Call til RPG-program                                              */

             CALL       PGM(AN012R) PARM(&HELE)
             DLTOVR     FILE(QCLLESRC)

             CRTBNDCL   PGM(ASPGPLK/&NAT2) SRCFILE(ASPGPLK/QCLLESRC) +
                          TEXT(&TXT2) OPTION(*NOXREF *NOSECLVL)
             ENDDO

/* Sjekk om program finnes fra før for ASNAT3                        */

             IF         COND(&NATT *EQ '3') THEN(DO)
             CHKOBJ     OBJ(ASPGPLK/QCLLESRC) OBJTYPE(*FILE) +
                          MBR(&NAT3) AUT(*ALL)
             MONMSG     MSGID(CPF9815) EXEC(GOTO CMDLBL(START3))
             RMVM       FILE(ASPGPLK/QCLLESRC) MBR(&NAT3)

/* Bygge opp Cl-member                                               */

 START3:     ADDPFM     FILE(ASPGPLK/QCLLESRC) MBR(&NAT3) +
                          TEXT(&TXT3) SRCTYPE(CLLE)
             OVRDBF     FILE(QCLLESRC) TOFILE(ASPGPLK/QCLLESRC) +
                          MBR(&NAT3)

/* Call til RPG-program                                              */

             CALL       PGM(AN012R) PARM(&HELE)
             DLTOVR     FILE(QCLLESRC)

             CRTBNDCL   PGM(ASPGPLK/&NAT3) SRCFILE(ASPGPLK/QCLLESRC) +
                          TEXT(&TXT3) OPTION(*NOXREF *NOSECLVL)
             ENDDO

/* Call opp CL-program for spesial-prosedyrer                        */

             IF         COND(&RORG *EQ '1')  THEN(CALL +
                          PGM(AN014C)        PARM(&HELE))

             IF         COND(&BACK *EQ '1')  THEN(CALL +
                          PGM(AN015C)        PARM(&HELE))

             IF         COND(&PRC1 *EQ '1')  THEN(CALL +
                          PGM(AN018C)        PARM(&HELE))

             IF         COND(&PRC2 *EQ '1')  THEN(CALL +
                          PGM(AN016C)        PARM(&HELE))

/* Generere program for kontroll av tape                             */
/* Nb! Programmet genereres her ubetinget,                           */
/* men det er genereringen av ASNAT1-2-3,                            */
/* som styrer om det skal kalles eller ikke.                         */

             CALL       PGM(AN022C)          PARM(&HELE)


/* Generere Nattjobb 1 for jobbplanen                                */

             IF         COND(&NATT *EQ '1') THEN(DO)
             RMVJOBSCDE JOB(&NAT1) ENTRYNBR(*ONLY)
             MONMSG     MSGID(CPF1628)

             ADDJOBSCDE JOB(&NAT1) CMD(CALL PGM(ASPGPLK/&NAT1)) +
                                   FRQ(*WEEKLY) +
                                   SCDDATE(*NONE) +
                                   SCDDAY(*TUE *WED *THU *FRI *SAT) +
                                   SCDTIME(&TIME) +
                                   JOBQ(ASPGPL/ASPBATCH) +
                                   JOBD(*LIBL/ASNATT) +
                                   USER(ASPNATT)
/*           Nattjobb 1 fra tirsdag til lørdag                       */
             ENDDO

/* Generere Nattjobb 2 for jobbplanen                                */

             IF         COND(&NATT *EQ '2') THEN(DO)
             RMVJOBSCDE JOB(&NAT2) ENTRYNBR(*ONLY)
             MONMSG     MSGID(CPF1628)

             ADDJOBSCDE JOB(&NAT2) CMD(CALL PGM(ASPGPLK/&NAT2)) +
                                   FRQ(*WEEKLY) +
                                   SCDDATE(*NONE) +
                                   SCDDAY(*SUN) +
                                   SCDTIME(&TIME) +
                                   JOBQ(ASPGPL/ASPBATCH) +
                                   JOBD(*LIBL/ASNATT) +
                                   USER(ASPNATT)
/*           Nattjobb 2 for søndag                                   */
             ENDDO

/* Generere Nattjobb 3 for jobbplanen                                */

             IF         COND(&NATT *EQ '3') THEN(DO)
             RMVJOBSCDE JOB(&NAT3) ENTRYNBR(*ONLY)
             MONMSG     MSGID(CPF1628)

             ADDJOBSCDE JOB(&NAT3) CMD(CALL PGM(ASPGPLK/&NAT3)) +
                                   FRQ(*WEEKLY) +
                                   SCDDATE(*NONE) +
                                   SCDDAY(*MON) +
                                   SCDTIME(&TIME) +
                                   JOBQ(ASPGPL/ASPBATCH) +
                                   JOBD(*LIBL/ASNATT) +
                                   USER(ASPNATT)
/*           Nattjobb 3 for mandag                                   */
             ENDDO

 END:        ENDPGM
