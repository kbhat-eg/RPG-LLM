             PGM        PARM(&PFILE &PFLDR &PFEIL)

/* Initiering av parametre.                                          */

             DCL        VAR(&PFILE)    TYPE(*CHAR) LEN(12)
             DCL        VAR(&PFLDR)    TYPE(*CHAR) LEN(20)
             DCL        VAR(&PFEIL)    TYPE(*CHAR) LEN(1)
             DCL        VAR(&HOME) TYPE(*CHAR) LEN(10) VALUE('ASP')
             DCL        VAR(&IFIL) TYPE(*CHAR) LEN(50) VALUE(' ')
             DCL        VAR(&DFIL) TYPE(*CHAR) LEN(70)
             DCL        VAR(&CLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FGRP) TYPE(*CHAR) LEN(3)
             DCL        VAR(&SLAS) TYPE(*CHAR) LEN(1) VALUE('/')

/* Initierer feil-flagg                                              */

             CHGVAR     VAR(&PFEIL)    VALUE('0')

/* Henter current library                                            */

             RTVJOBA    CURLIB(&CLIB)
             CHGVAR     VAR(&FGRP) VALUE(%SUBSTRING(&CLIB 4 3))

/* Tømmer arbeidsfil                                                 */

             CLRPFM     FILE(FLVWPF)

/* Oppretter temporær fil                                            */
/* rettelse pga. sikkerhet */
/*           DLTF       FILE(FLVWFLAT)  */
             CLRPFM FILE(FLVWFLAT)
             MONMSG MSGID(CPF2105)
/*           CRTPF      FILE(FLVWFLAT) RCDLEN(1000) +     */
/*                       TEXT('Temporær fil') SIZE(*NOMAX) */

/* Kopierer leverandør-fil til temporær fil                          */

             CHGVAR     VAR(&IFIL) VALUE(&SLAS *TCAT &HOME *TCAT +
                          &SLAS *TCAT &FGRP *TCAT &SLAS *TCAT +
                          &PFLDR *TCAT &SLAS *TCAT &PFILE)
             CHGVAR     VAR(&DFIL) VALUE('/qsys.lib/' *TCAT &CLIB +
                          *TCAT '.lib/FLVWFLAT.file/FLVWFLAT.mbr')
             CPYFRMSTMF FROMSTMF(&IFIL) TOMBR(&DFIL) +
                          MBROPT(*REPLACE) STMFCODPAG(*PCASCII)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(FEIL))


/* Kopierer temporær-fil til arbeidsfil                              */

             CPYF       FROMFILE(FLVWFLAT) TOFILE(FLVWPF) +
                          MBROPT(*REPLACE) FMTOPT(*NOCHK)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(FEIL))

             GOTO       CMDLBL(END)

 FEIL:       CHGVAR     VAR(&PFEIL) VALUE('1')

 END:        ENDPGM
