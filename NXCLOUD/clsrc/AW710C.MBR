/*  Scriptet setter opp miljø for web service tjenester              */
/*  Rutinen kjæres pr. filgruppen, men mye er felles pr. server      */
/*  Det sjekkes om subsyst. mm er i gang før det genereres           */

             PGM        PARM(&FGRP)

             DCL        VAR(&FGRP)     TYPE(*CHAR) LEN(3)
             DCL        VAR(&FGRD)     TYPE(*CHAR) LEN(7)
             DCL        VAR(&FGRJ)     TYPE(*CHAR) LEN(8)
             DCL        VAR(&MLD1)     TYPE(*CHAR) LEN(50)
             DCL        VAR(&MLD2)     TYPE(*CHAR) LEN(50)
             DCL        VAR(&exec)     TYPE(*CHAR) LEN(500)
             DCL        VAR(&parm)     TYPE(*CHAR) LEN(500)
             DCL        VAR(&SVAR)     TYPE(*CHAR) LEN(1)
             DCL        VAR(&IN12)     TYPE(*DEC)  LEN(1 0)

             if         cond(&fgrp *eq ' ') then(do)
               rtvdtaara  dtaara(*lda (931 3)) rtnvar(&fgrp)
             enddo

             CHGVAR     VAR(&FGRD) VALUE('NXWS' *CAT &FGRP)
             CHGVAR     VAR(&FGRJ) VALUE('NXWS' *CAT &FGRP *CAT 'J')
             CHGVAR     VAR(&EXEC) VALUE('JAVA +
                          CLASS(''/java/asp/nx-ws-client/nx-ws-server.jar'') +
                           PARM(''--as400.filegroup=adb' *tcat &FGRP *tcat +
                          ''' ''--logging.file=/home/adb' *tcat &FGRP +
                          *tcat '/nx-ws-client.log'') OPTIMIZE(40) JOB(' +
                          *tcat &FGRJ *tcat ') OUTPUT(*NONE)')
             CHGVAR     VAR(&PARM) VALUE('--as400.filegroup=ADB' *tcat +
                          &FGRP *bcat '--logging.file=/home/ADB' *tcat +
                          &FGRP *tcat '/nx-ws-client.log')

/* Varselbilde med bekreftelse på om jobb skal settes i gang         */

             CHGVAR     VAR(&MLD1) +
                        VALUE('Generering av subsystem og jobbkø    ')
             CHGVAR     VAR(&MLD2) +
                        VALUE('Bekreft at dette startes! ')
             CALL       PGM(AA007R)    PARM(&MLD1 &MLD2 &SVAR &IN12)

             IF         COND(&IN12 *EQ 1 ) +
                          THEN(GOTO CMDLBL(ENDPGM))

             IF         COND(&SVAR *EQ 'J') +
                          THEN(GOTO CMDLBL(TESTSUB))
             ELSE       CMD(GOTO CMDLBL(ENDPGM))

/* Sjekker om subsystem allerede er i gang - bygger og starter        */

 TESTSUB:    STRSBS     SBSD(QGPL/ASPJAVAJBS)
             MONMSG     MSGID(CPF1013)
             MONMSG     MSGID(CPF1010) EXEC(DO)
                        GOTO CMDLBL(TESTJOBQ)
             ENDDO

             CRTSBSD    SBSD(QGPL/ASPJAVAJBS) POOLS((1 *BASE)) MAXJOBS(5) +
                          TEXT('Subsystem for Web service tjenester')
             MONMSG     MSGID(CPF1696)
             STRSBS     SBSD(QGPL/ASPJAVAJBS)
             MONMSG     MSGID(CPF1010)

/* Sjekker om jobbkø allerede er bygget - bygger med tillegg          */

 TESTJOBQ:   CRTJOBQ    JOBQ(QGPL/ASPJAVAJBS) TEXT('Jobbkø for +
                          Web service tjenester')
             MONMSG     MSGID(CPF3323) EXEC(DO)
                        GOTO CMDLBL(TESTJOBD)
             ENDDO

             CRTCLS     CLS(QGPL/QBATCH)
             MONMSG     MSGID(CPF1064)

             ADDJOBQE   SBSD(QGPL/ASPJAVAJBS) JOBQ(QGPL/ASPJAVAJBS) +
                          MAXACT(*NOMAX) MAXPTY1(*NOMAX) +
                          MAXPTY2(*NOMAX) MAXPTY3(*NOMAX) +
                          MAXPTY4(*NOMAX) MAXPTY5(*NOMAX) +
                          MAXPTY6(*NOMAX) MAXPTY7(*NOMAX) +
                          MAXPTY8(*NOMAX) MAXPTY9(*NOMAX)
             MONMSG     MSGID(CPF1535 CPF1697)

             ADDRTGE    SBSD(QGPL/ASPJAVAJBS) SEQNBR(10) +
                          CMPVAL(*ANY) PGM(QSYS/QCMD) +
                          CLS(QGPL/QBATCH)

/* Bygger jobb beskrivelse for valgt filgruppe                        */

 TESTJOBD:   CRTJOBD    JOBD(QGPL/&FGRD) JOBQ(ASPJAVAJBS) TEXT('Webservice +
                          klient') USER(ASP) RTGDTA(QCMD) RQSDTA(&EXEC) +
                          JOBMSGQFL(*WRAP) INQMSGRPY(*DFT)
             MONMSG     MSGID(CPF1621)

/* Legger til autostartjobb i subsystem                                 */

             ADDAJE     SBSD(ASPJAVAJBS) JOB(&FGRD) JOBD(QGPL/&FGRD)
             MONMSG     MSGID(CPD1455 CPF1697)

/* Bygger datakøer                                                      */

             CRTDTAQ    DTAQ(SOAPRES) MAXLEN(10000) SEQ(*KEYED) KEYLEN(10)
             MONMSG     MSGID(CPF9870)
             CRTDTAQ    DTAQ(RESTRES) MAXLEN(10000) SEQ(*KEYED) KEYLEN(10)
             MONMSG     MSGID(CPF9870)

/* Submitter job for øyeblikkelig oppstart                              */

             SBMJOB     JOB(*JOBD) JOBD(QGPL/&FGRD) JOBQ(*JOBD) +
                          PRTDEV(*JOBD) OUTQ(*JOBD) USER(*JOBD) +
                          RTGDTA(*JOBD) RQSDTA(*JOBD)

 ENDPGM:     ENDPGM
