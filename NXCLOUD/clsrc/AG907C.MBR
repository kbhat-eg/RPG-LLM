/* Oppretting/oppdatering av tabeller på bakgrunn av DDF             */

/* Tabeller opprettes med:                                           */
/*   SIZE(*NOMAX) REUSEDLT(*NO) SRTSEQ(*LANGIDUNQ) LANGID(NOR)       */

             PGM        PARM(&FILE &DLIB &NULL)

             DCL        VAR(&FILE) TYPE(*CHAR) LEN(6)
             DCL        VAR(&DLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&NULL) TYPE(*CHAR) LEN(1)

             DCL        VAR(&BACK) TYPE(*CHAR) LEN(6)

/* Setter databibliotek til current library                          */

             CHGCURLIB  CURLIB(&DLIB)

/* Utleder navn på backup-tabell                                     */

             CHGVAR     VAR(&BACK) VALUE(%SUBSTRING(&FILE 1 4) *CAT +
                          'BF')

/* Tar vare på tabellinnholdet i temporær backup-tabell og sletter   */
/* eksisterende tabell                                               */

             CHKOBJ     OBJ(&DLIB/&FILE) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(GOTO CMDLBL(DDF))

             CHKOBJ     OBJ(&DLIB/&BACK) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(GOTO CMDLBL(BACKUP))

             DLTF       FILE(&DLIB/&BACK)

 BACKUP:     CPYF       FROMFILE(&DLIB/&FILE) TOFILE(&DLIB/&BACK) +
                          CRTFILE(*YES)

             DLTF       FILE(&DLIB/&FILE)

/* Opretter tabell                                                   */

 DDF:        CRTPF      FILE(&DLIB/&FILE) SRCFILE(QDDFSRC) +
                          SIZE(*NOMAX) REUSEDLT(*NO) +
                          SRTSEQ(*LANGIDUNQ) LANGID(NOR)

/* Kopierer tilbake tabellinnhold fra temporær backup-tabell         */

             CHKOBJ     OBJ(&DLIB/&BACK) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(GOTO END)

             IF         COND(&NULL *NE '1') THEN( +
             CPYF       FROMFILE(&DLIB/&BACK) +
                          TOFILE(&DLIB/&FILE) MBROPT(*ADD) +
                          FMTOPT(*MAP *DROP))

             DLTF       FILE(&DLIB/&BACK)

 END:        ENDPGM
