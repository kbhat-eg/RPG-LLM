             PGM        PARM(&AARR &FATY &FNUM &TNUM +
                             &OMKJ &RAPP &ANTA &FEIL +
                             &VKUN &KKUN &FILK)

/* Initiering av parametere                                          */

             DCL        VAR(&AARR)   TYPE(*DEC)  LEN(4)
             DCL        VAR(&FATY)   TYPE(*DEC)  LEN(2)
             DCL        VAR(&FNUM)   TYPE(*DEC)  LEN(8)
             DCL        VAR(&TNUM)   TYPE(*DEC)  LEN(8)
             DCL        VAR(&OMKJ)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&RAPP)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&ANTA)   TYPE(*DEC)  LEN(4)
             DCL        VAR(&FEIL)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&FIRM)   TYPE(*CHAR) LEN(3)
             DCL        VAR(&VKUN)   TYPE(*CHAR) LEN(6)
             DCL        VAR(&KKUN)   TYPE(*CHAR) LEN(6)
             DCL        VAR(&FILK)   TYPE(*CHAR) LEN(1)

             DCL        VAR(&OVRF) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FILN)  TYPE(*CHAR) LEN(20)
             DCL        VAR(&STAT)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&SMSG)  TYPE(*CHAR) LEN(1) VALUE('1')
             DCL        VAR(&FRAFIL) TYPE(*CHAR) LEN(70)
             DCL        VAR(&TILFIL) TYPE(*CHAR) LEN(70)
             DCL        VAR(&TILDIR) TYPE(*CHAR) LEN(40)
             DCL        VAR(&CLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&IN03)     TYPE(*CHAR) LEN(1)
             DCL        VAR(&BTCH)     TYPE(*CHAR) LEN(1)

/* Henter verdier i fra LDA.                                         */

             RTVDTAARA  DTAARA(*LDA (944 03)) RTNVAR(&FIRM)
             RTVJOBA    CURLIB(&CLIB)

/* Tømmer midlertidig EDI-flatfil                                    */

             CLRPFM     FILE(FNAXPF)

/* Program for utskriftsparametre.                                   */

             IF         COND(&RAPP *EQ '1') THEN(DO)
             CALL       PGM(AP600R) PARM('FO183' '0' '0' &IN03 &BTCH)
             IF         COND(&IN03 *EQ '1') THEN(GOTO CMDLBL(END))
             ENDDO

/* Program for overstyring av printerfile                            */

             IF         COND(&RAPP *EQ '1') THEN(DO)
             CALL       PGM(AP601C)    PARM('FO183P' &BTCH)
             ENDDO

/* Kaller prog. for gen. av midl. EDI-flatfil                        */

             CALL       PGM(FO184R) PARM(&AARR &FATY &FNUM &TNUM +
                                         &OMKJ &RAPP &ANTA &FEIL +
                                         &VKUN &FILN &FILK)
             IF         COND(&FILK *NE '1') THEN(GOTO CMDLBL(ENDPGM))

/* Tømmer EFO/NELFO flatfil                                          */

             CLRPFM     FILE(FNAUPF)

/* Kaller prog. for gen. av EFO/NELFO fakt. flatfil                  */

             CALL       PGM(FO185R) PARM(&AARR &FATY &FNUM &TNUM +
                                         &OMKJ &RAPP &ANTA &FEIL +
                                         &VKUN &FILN &FILK)

/* Autofakt-fil til IFS (kundenr)                                    */

             CRTSRCPF   FILE(FNAUTTMP) RCDLEN(1200) MBR(FNAUTTMP) +
                          TEXT('Temp.fil') SIZE(*NOMAX)
             MONMSG     MSGID(CPF0000)

             CPYF       FROMFILE(FNAUPF) TOFILE(FNAUTTMP) +
                          FROMMBR(FNAUPF) TOMBR(FNAUTTMP) +
                          MBROPT(*REPLACE) FMTOPT(*CVTSRC)

             CHGVAR     VAR(&FRAFIL) VALUE('/qsys.lib/' *TCAT &CLIB +
                          *TCAT '.lib/FNAUTTMP.file/FNAUTTMP.mbr')

             IF         COND(&KKUN *NE ' ') THEN(DO)
             CHGVAR     VAR(&TILDIR) VALUE('/home/' *TCAT +
                                           &KKUN *TCAT '/')
             ENDDO
             ELSE       CMD(DO)
             CHGVAR     VAR(&TILDIR) VALUE('/home/' *TCAT +
                                           &VKUN *TCAT '/')
             ENDDO

/* Lager til-katalogen på IFS hvis den ikke finnes                   */

             CRTDIR     DIR(&TILDIR) DTAAUT(*RWX) OBJAUT(*ALL)
             MONMSG     MSGID(CPF0000)

             IF         COND(&FILN *NE ' ') THEN(DO)
             CHGVAR     VAR(&TILFIL) VALUE(&TILDIR *TCAT &FILN)
             ENDDO
             ELSE       CMD(DO)
             CHGVAR     VAR(&TILFIL) VALUE(&TILDIR *TCAT 'autof.dat')
             ENDDO

             CPYTOSTMF  FROMMBR(&FRAFIL) TOSTMF(&TILFIL) +
                          STMFOPT(*ADD) STMFCODPAG(*STDASCII)
             CHGAUT     OBJ(&TILFIL) USER(*PUBLIC) DTAAUT(*RWX) +
                          OBJAUT(*ALL)

             DLTF       FILE(FNAUTTMP)

/*           CHGVAR     VAR(&FILN)  VALUE(&WMM1)                     */
/*           CALL       PGM(AF700C) PARM(&OVRF &STAT &FILN &SMSG)    */

 ENDPGM:
             IF         COND(&RAPP *EQ '1') THEN(DO)
             CALL       PGM(AP606C)    PARM('FO183P')
             ENDDO

 END:        ENDPGM
