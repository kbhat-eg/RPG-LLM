             PGM        PARM(&DLIB &FIRM &USER &KASS &STAT)

             DCL        VAR(&DLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FIRM) TYPE(*CHAR) LEN(3)
             DCL        VAR(&USER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&KASS) TYPE(*CHAR) LEN(3)
             DCL        VAR(&STAT) TYPE(*CHAR) LEN(8)

             DCL        VAR(&FGRP) TYPE(*CHAR) LEN(3)
             DCL        VAR(&SEKV) TYPE(*DEC) LEN(4 0)
             DCL        VAR(&CSEK) TYPE(*CHAR) LEN(4)

/* Setter biblioteksliste                                            */

             CHGVAR     VAR(&FGRP) VALUE(%SUBSTRING(&DLIB 4 3))

             CALL       PGM(AA720C) PARM(&FGRP)

/* Initierer miljø                                                   */

             CALL       PGM(BINIT) PARM(&DLIB &KASS &FIRM &USER)

/* Sørger for at jobkø finnes                                        */

             CHKOBJ     OBJ(&DLIB/BUTIJOBQ) OBJTYPE(*JOBQ)
             MONMSG     MSGID(CPF9801) EXEC(DO)
             CRTJOBQ    JOBQ(&DLIB/BUTIJOBQ) TEXT('Kassens jobbkø +
                          for prosessering av bonger')
             SNDMSG     MSG('Opprettet jobbkø BUTIJOBQ i' *BCAT +
                          &DLIB) TOUSR(ASP)

/* Legge jobbkø inn i QBATCH. Begynner med sekvens nummer 100, og     */
/* fortsetter å øke sekvensnummer med 1 og 1 til kommandoen fungerer, */
/* eller går i feil hvis sekvensnummeret blir mer enn 150.            */
/* Vet at CFP1697 kan være andre feil enn sekvensnummeret, men det får */
/* bare være..                                                         */
/*K00 7.01   Utvidet sekvensgrense fra 149 til 200 pga. stopp på XLBYGG */
/*K00 8.01   Utvidet sekvensgrense fra 200 til 300 pga. stopp på XLBYGG */

             CHGVAR     VAR(&SEKV) VALUE(100)
ADDER:       ADDJOBQE   SBSD(QBATCH) JOBQ(&DLIB/BUTIJOBQ) SEQNBR(&SEKV)
             MONMSG     MSGID(CPF1697) EXEC(DO)
             CHGVAR     VAR(&SEKV) VALUE(&SEKV + 1)
             IF         COND(&SEKV > 300) THEN(DO)

             ADDJOBQE   SBSD(QBATCH) JOBQ(&DLIB/BUTIJOBQ) +
                          SEQNBR(&SEKV) /* Sørger for at MSGW +
                          oppstår hvis JOBQE absolutt ikke kan +
                          opprettes */
             CHGVAR     VAR(&CSEK) VALUE(&SEKV)
             SNDMSG     MSG('La til JOBQE for' *BCAT &DLIB *TCAT +
                          '/BUTIJOBQ til subsystem QBATCH med +
                          sekvensnummer' *BCAT &CSEK) TOUSR(ASP)
             GOTO       CMDLBL(END)
             ENDDO

             GOTO       CMDLBL(ADDER) /* Looper tilbake til ADDER +
                          for å telle opp sekvensnummer */
             ENDDO      /* MONMSG for ADDJOBQE */

             CHGVAR     VAR(&CSEK) VALUE(&SEKV)
             SNDMSG     MSG('La til JOBQE for' *BCAT &DLIB *TCAT +
                          '/BUTIJOBQ til subsystem QBATCH med +
                          sekvensnummer' *BCAT &CSEK) TOUSR(ASP)

             ENDDO      /* Slutt på oppretting av jobbkø */

/* Submitter program BU991C til jobbkø BUTIJOBQ.                       */

/*           SBMJOB     CMD(CALL PGM(BU991C) PARM(&DLIB &FIRM &USER + */
/*                        &KASS &STAT)) JOB(BU990R) +                 */
/*                        JOBQ(&DLIB/BUTIJOBQ)                        */

             CALL       PGM(BU991C) PARM(&DLIB &FIRM &USER &KASS +
                         &STAT)

END:         ENDPGM
