/*********************************************************************/
/*                                                                  */
/*_______________________________________________________________   */
/* ENDRINGER :                                                      */
/* DATO      INZ  UTFØRT                                            */
/* ---------------------------------------------------------------  */
/* 10.03.03  MHA  NYTT PROG                                         */
/*                                                                  */
/* Beskrivelse av parametre:                                        */
/* FILE=Spoolfile.                                                  */
/* JOB=Jobbnavn, bruker og jobbnummer.                              */
/* SPLNBR=Spoolfile nummer. NB: Ikke i bruk.                        */
/* PDFPATH=Mappe på IFS der PDF filen legges. (Eks. /temp)          */
/* PDF=Navnet på PDF filen (uten .pdf).                             */
/* NOTES=Fotnoten som kommer i PDF dokumentet.                      */
/*****************************************************************  */
             PGM        PARM(&FILE &JOB &SPLNBR &PDFPATH &PDF &NOTES +
                         &STATUS)

             DCL        VAR(&MESSAGE) TYPE(*CHAR) LEN(70)
             DCL        VAR(&JAVA) TYPE(*CHAR) LEN(300)
             DCL        VAR(&MEMBER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COUNTDEC) TYPE(*DEC) LEN(9)
             DCL        VAR(&COUNTCHAR) TYPE(*CHAR) LEN(9)
             DCL        VAR(&FILE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOB) TYPE(*CHAR) LEN(26)
             DCL        VAR(&JOBNAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&USER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOBNBR) TYPE(*CHAR) LEN(6)
             DCL        VAR(&SPLNBR) TYPE(*DEC) LEN(6)
             DCL        VAR(&FILENBR) TYPE(*CHAR) LEN(6)
             DCL        VAR(&PDFPATH) TYPE(*CHAR) LEN(96)
             DCL        VAR(&PDF) TYPE(*CHAR) LEN(10)
             DCL        VAR(&NOTES) TYPE(*CHAR) LEN(50)
             DCL        VAR(&LOGFILE) TYPE(*CHAR) LEN(110)

             DCL        VAR(&LENGTH) TYPE(*CHAR) LEN(4)
             DCL        VAR(&WIDTH) TYPE(*CHAR) LEN(4)
             DCL        VAR(&BINNBR) TYPE(*CHAR) LEN(4)
             DCL        VAR(&RCVLEN) TYPE(*CHAR) LEN(4)
             DCL        VAR(&RECEIVER) TYPE(*CHAR) LEN(840)
             DCL        VAR(&STATUS) TYPE(*CHAR) LEN(1)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(ERROR))

             /* Prepare spooled file attributes    */
             CHGVAR     VAR(&STATUS) VALUE('1')
             IF         COND(&JOB = '*') THEN(   +
               RTVJOBA    JOB(&JOBNAME) USER(&USER) NBR(&JOBNBR))
             ELSE (DO)
               CHGVAR     VAR(&JOBNAME) VALUE(%SST(&JOB 1 10))
               CHGVAR     VAR(&USER) VALUE(%SST(&JOB 11 10))
               CHGVAR     VAR(&JOBNBR) VALUE(%SST(&JOB 21 6))
             ENDDO
/* Jukser litt, og forutsetter at vi alltid vil ha siste spoolfile som  */
/* passer til jobb beskrivelsen. Derfor setter jeg SPLNBR til -1.       */
             CHGVAR     VAR(&SPLNBR) VALUE(-1)
             CHGVAR     VAR(&FILENBR) VALUE(&SPLNBR)

             /* Retreive spooled file attributes     */
             CHGVAR     VAR(%BIN(&BINNBR)) VALUE(&FILENBR)
             CHGVAR     VAR(%BIN(&RCVLEN)) VALUE(840)
             CALL       PGM(QUSRSPLA) PARM(&RECEIVER &RCVLEN +
                          'SPLA0100' &JOB ' ' ' ' &FILE &BINNBR)
             CHGVAR     VAR(&LENGTH ) VALUE(%BIN(&RECEIVER 425 4))
             CHGVAR     VAR(&WIDTH  ) VALUE(%BIN(&RECEIVER 429 4))

             /* Dummy change to see if the spooled file exists */
             CHGSPLFA   FILE(&FILE) JOB(&JOBNBR/&USER/&JOBNAME) +
                          SPLNBR(*LAST)

             /* Test the path length                            */
             IF COND('0' *BCAT &PDFPATH *eq '0') THEN(SNDPGMMSG +
                          MSGID(CPF9898) MSGF(QCPFMSG) +
                          MSGDTA('Wrong PDF Path') MSGTYPE(*ESCAPE))

             /* Dummy change to see if the directory exists */
             CHGVAR     VAR(&STATUS) VALUE('2')
             CHGVAR     VAR(&PDFPATH) VALUE(&pdfpath *tcat '/')
             CD         DIR(&PDFPATH)

             /* Test the pdf name length                    */
             IF         COND('0' *BCAT &PDF *EQ '0') THEN(SNDPGMMSG +
                          MSGID(CPF9898) MSGF(QCPFMSG) +
                          MSGDTA('Wrong PDF name') MSGTYPE(*ESCAPE))

             /* Test the notes length                    */
     /*      IF         COND('0' *BCAT &NOTES *EQ '0') THEN(SNDPGMMSG + */
     /*                   MSGID(CPF9898) MSGF(QCPFMSG) +                */
     /*                   MSGDTA('Notes are blank') MSGTYPE(*ESCAPE))   */

             /* Delete the log file                         */
             CHGVAR     VAR(&LOGFILE) VALUE(&pdfpath *tcat &pdf +
                          *tcat '.pdf.log')
             RMVLNK     OBJLNK(&LOGFILE)
             MONMSG     MSGID(CPFA093 CPFA0A9)

             /* Create a temp file member to store spooled files */
             CHGVAR     VAR(&STATUS) VALUE('3')
             CHKOBJ     OBJ(QGPL/TMPSPOOL) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(CRTPF +
                          FILE(QGPL/TMPSPOOL) RCDLEN(199) +
                          MBR(M000000000) MAXMBRS(*NOMAX) SIZE(*NOMAX))

             /* Add a new member using counter increament   */
             RTVMBRD    FILE(QGPL/TMPSPOOL) MBR(*LAST) RTNMBR(&MEMBER)
             CHGVAR     VAR(&COUNTCHAR) VALUE(%SST(&MEMBER 2 9) )
             CHGVAR     VAR(&COUNTDEC) VALUE(&COUNTCHAR)
             CHGVAR     VAR(&COUNTDEC) VALUE(&COUNTDEC+1)
             CHGVAR     VAR(&COUNTCHAR) VALUE(&COUNTDEC)
             CHGVAR     VAR(&MEMBER) VALUE('M' *CAT &COUNTCHAR)
             ADDPFM     FILE(QGPL/TMPSPOOL) MBR(&MEMBER)

             /* Copy spooled file to temp file member      */
             CHGVAR     VAR(&STATUS) VALUE('4')
             CPYSPLF    FILE(&FILE) TOFILE(QGPL/TMPSPOOL) +
                          JOB(&JOBNBR/&USER/&JOBNAME) SPLNBR(*LAST) +
                          TOMBR(&MEMBER) MBROPT(*REPLACE) +
                          CTLCHAR(*FCFC)

             /* Prepare java call                 */
             CHGVAR     VAR(&STATUS) VALUE('5')
             CD         DIR(&PDFPATH)
             CHKOBJ     OBJ(QTEMP/STDOUT) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(CRTPF FILE(QTEMP/STDOUT) +
                          RCDLEN(96))
             CLRPFM     FILE(QTEMP/STDOUT)
             OVRDBF     FILE(STDOUT) TOFILE(QTEMP/STDOUT)
             CHGVAR     VAR(&JAVA) VALUE('java asp.pdf.CreatePdfFile +
                          -Lqgpl -Ftmpspool -M' *TCAT &MEMBER *BCAT +
                          '-P' *TCAT &PDFPATH *TCAT &PDF *TCAT +
                          '.pdf -ll' *TCAT &WIDTH *BCAT '-ln' *TCAT +
                          &LENGTH *BCAT '"-fc' *TCAT &NOTES *TCAT +
                          '" -f/java/asp/pdf/courier.ttf')
             STRQSH     CMD(&JAVA)

            /*  Remove temp file member                */
             RMVM       FILE(QGPL/TMPSPOOL) MBR(&MEMBER)

             /* If log file exists then the PDF file has created */
             RMVLNK     OBJLNK(&LOGFILE)
             MONMSG     MSGID(CPFA0A9) EXEC(GOTO CMDLBL(FAILED))

             SNDPGMMSG  MSG('Spooled file' *BCAT &FILE *BCAT +
                          'converted to' *BCAT &PDFPATH *TCAT &PDF +
                          *TCAT '.PDF')
             CHGVAR     VAR(&STATUS) VALUE(' ')
             RETURN

 FAILED:     SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Spooled +
                   file' *BCAT &FILE *BCAT 'conversion failed')
             RETURN



 ERROR:      IF         COND(&STATUS = '1') THEN(CHGVAR +
                          VAR(&MESSAGE) VALUE('Spooled File not exist'))
             ELSE       CMD(IF COND(&STATUS = '2') THEN(CHGVAR +
                          VAR(&MESSAGE) VALUE('PDF Path not exist')))
             ELSE       CMD(IF COND(&STATUS = '3') THEN(CHGVAR +
                          VAR(&MESSAGE) VALUE('Error in Temporary +
                          File')))
             ELSE       CMD(IF COND(&STATUS = '4') THEN(CHGVAR +
                          VAR(&MESSAGE) VALUE('Error during Spooled +
                          file Process')))
             ELSE       CMD(IF COND(&STATUS = '5') THEN(CHGVAR +
                          VAR(&MESSAGE) VALUE('Error during Java +
                          Process')))
             SNDPGMMSG  MSG(&MESSAGE)

/* Avslutter programmet.                                                     */
SLUTT3:      ENDPGM
