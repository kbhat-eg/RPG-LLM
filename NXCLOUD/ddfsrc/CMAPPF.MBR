      *
      * MAP varevurdering
      *
     A          R CMAPPFR
      *
     A            CMFIRM         3S 0       TEXT('Firma')
     A                                      COLHDG('Firma')
     A            CMLAGE         2S 0       TEXT('Lager')
     A                                      COLHDG('Lager')
     A            CMPAAR         4S 0       TEXT('År')
     A                                      COLHDG('År')
     A            CMPMND         2S 0       TEXT('Mnd')
     A                                      COLHDG('Mnd')
     A            CMVARE         8A         TEXT('Varenr')
     A                                      COLHDG('Varenr')
      *
     A            CMKDAT          L         TEXT('Kjøredato')
     A                                      COLHDG('Kjøredato')
     A            CMKTIM          T         TEXT('Kjøre tidspunkt')
     A                                      COLHDG('Kjøre' 'tidspunkt')
      *
     A            CMTEK1        35A         TEXT('Varetekst-1')
     A                                      COLHDG('Varetekst-1')
     A            CMLENH         3A         TEXT('Lagerenhet')
     A                                      COLHDG('Lager' 'enhet')
     A            CMGKPR         9S 2       TEXT('Gml. kostpris')
     A                                      COLHDG('Gml.' 'kostpris')
     A            CMGMAP         9S 2       TEXT('Gml. MAP')
     A                                      COLHDG('Gml.' 'MAP')
     A            CMGBEH        11S 3       TEXT('Gml. beholdning')
     A                                      COLHDG('Gml.' 'beholdning')
     A            CMGVER        11S 2       TEXT('Gml. lagerverdi')
     A                                      COLHDG('Gml.' 'lagerverdi')
      *
     A            CMVANT        11S 3       TEXT('Ant. varemottatt')
     A                                      COLHDG('Ant.' 'varemottatt')
     A            CMKVER        11S 2       TEXT('Verdi varemottatt')
     A                                      COLHDG('Verdi' 'varemottatt')
     A            CMVFRA         9S 2       TEXT('Verdi frakt')
     A                                      COLHDG('Verdi' 'frakt')
     A            CMTVER        11S 2       TEXT('Totalverdi varemottatt')
     A                                      COLHDG('Totalverdi' 'varemottatt')
     A            CMGJVE         9S 2       TEXT('Gj.snittsverdi varemottatt')
     A                                      COLHDG('Gj.verdi' 'varemottatt')
     A            CMVMDA          L         TEXT('Siste varemottaksdato')
     A                                      COLHDG('Siste' 'varemottak' 'dato')
      *
     A            CMNKPR         9S 2       TEXT('Ny kostpris')
     A                                      COLHDG('Ny kostpris')
     A            CMNMAP         9S 2       TEXT('Ny MAP')
     A                                      COLHDG('Ny MAP')
     A            CMBEHL        11S 3       TEXT('Beholdning')
     A                                      COLHDG('Beholdning')
     A            CMNVER        11S 2       TEXT('Ny lagerverdi')
     A                                      COLHDG('Ny lagerverdi')
      *
     A            CMN0PR         5S 2       TEXT('Nedskrivning %')
     A                                      COLHDG('Nedskr %')
     A            CMN0KR         9S 0       TEXT('Nedskrivning kr')
     A                                      COLHDG('Nedskr kr')
     A            CMN1PR         5S 2       TEXT('1mnd nedskrivning %')
     A                                      COLHDG('1mnd' 'nedskr %')
     A            CMN1KR         9S 0       TEXT('1mnd nedskrivning kr')
     A                                      COLHDG('1mnd' 'nedskr kr')
     A            CMN2PR         5S 2       TEXT('2mnd nedskrivning %')
     A                                      COLHDG('2mnd' 'nedskr %')
     A            CMN2KR         9S 0       TEXT('2mnd nedskrivning kr')
     A                                      COLHDG('2mnd' 'nedskr kr')
     A            CMN3PR         5S 2       TEXT('3mnd nedskrivning %')
     A                                      COLHDG('3mnd' 'nedskr %')
     A            CMN3KR         9S 0       TEXT('3mnd nedskrivning kr')
     A                                      COLHDG('3mnd' 'nedskr kr')
     A            CMKUKR         5A         TEXT('3mnd ukuransendring')
     A                                      COLHDG('3mnd' 'ukuransendr.')
      *                                     Ny
      *                                     Endr.
     A            CMN4PR         5S 2       TEXT('4mnd nedskrivning %')
     A                                      COLHDG('4mnd' 'nedskr %')
     A            CMN4KR         9S 0       TEXT('4mnd nedskrivning kr')
     A                                      COLHDG('4mnd' 'nedskr kr')
     A            CMN5PR         5S 2       TEXT('5mnd nedskrivning %')
     A                                      COLHDG('5mnd' 'nedskr %')
     A            CMN5KR         9S 0       TEXT('5mnd nedskrivning kr')
     A                                      COLHDG('5mnd' 'nedskr kr')
     A            CMN6PR         5S 2       TEXT('6mnd nedskrivning %')
     A                                      COLHDG('6mnd' 'nedskr %')
     A            CMN6KR         9S 0       TEXT('6mnd nedskrivning kr')
     A                                      COLHDG('6mnd' 'nedskr kr')
