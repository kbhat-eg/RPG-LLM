      *  Kopi av utbetalingsforslag som blir slettet ved 2.g.retur:
6.NU  * 220515 UTVIDELSE NUMMERSERIE
      *
      *  For nærmere feltbeskrivelse, se definisjon av file
      *  RLTRPF for RO.... felter.
     A                                      REF(*LIBL/RFRF)
     A          R GKBFPFR
     A            GKSTAT    R               REFFLD(RTSTAT)
     A            GKFIRM    R               REFFLD(ROFIRM)
      * Forslagnummer:
     A            GKBFNR    R               REFFLD(ROBFNR)
      * Forslagslinjenummer:
     A            GKBFLI    R               REFFLD(ROBFLI)
     A            GKLEVR    R               REFFLD(ROLEVR)
     A            GKBDAT    R               REFFLD(ROBDAT)
     A            GKTIST    R               REFFLD(ROTIST)
      * Leverandørnavn:
     A            GKNAVN    R               REFFLD(RLNAVN)
      * Valutakode - betaling
     A            GKVALK    R               REFFLD(ROVALK)
      * Valutakode - inngående
     A            GKFVAL    R               REFFLD(ROVALK)
      * Attestasjonskode
     A            GKATTS    R               REFFLD(ROATTS)
      * Bilagskode
     A            GKBILK    R               REFFLD(ROBILK)
      * Fakturabeløp
     A            GKBELØ    R               REFFLD(ROBELØ)
      * Restbeløp
     A            GKREST    R               REFFLD(ROREST)
      * Bilagsnummer
     A            GKBILN    R               REFFLD(ROBILN)
      * Valutabeløp faktura
     A            GKVALB    R               REFFLD(ROVALB)
      * Restbeløp faktura
     A            GKVALR    R               REFFLD(ROVALR)
      * Betalingskode
     A            GKBETK    R               REFFLD(ROBETK)
     A            GKRAAR    R               REFFLD(RORAAR)
     A            GKRPER    R               REFFLD(RORPER)
     A            GKJOUR    R               REFFLD(ROJOUR)
     A            GKSAMB    R               REFFLD(ROSAMB)
     A            GKREFN    R               REFFLD(ROREFN)
     A            GKTEXT    R               REFFLD(ROTEXT)
     A            GKVIRK    R               REFFLD(ROVIRK)
      * Forfallsdato
     A            GKFDAT    R               REFFLD(ROFDAT)
      * Betalingsreferanse-1
     A            GKRRF1    R               REFFLD(RORRF1)
      * Betalingsreferanse-2
     A            GKRRF2    R               REFFLD(RORRF2)
      * Deklarasjonskode:
     A            GKDECK    R               REFFLD(RODECK)
      * KID-feltet kan inneholde både KID og ikke KID.
     A            GKKIDI    R               REFFLD(ROKIDI)
      * Feltet GKKIDS vil inneholde "*" dersom feltet KID-feltet
      * GKKIDI inneholde KID.
     A            GKKIDS    R               REFFLD(RTSTAT)
     A            GKAVDE    R               REFFLD(ROAVDE)
     A            GKREKL    R               REFFLD(ROREKL)
      * Kode for om det er delbetalt
     A            GKDELB    R               REFFLD(RODELB)
      * Betalingsbetingelse
     A            GKBETB    R               REFFLD(ROBETB)
     A            GKREMK    R               REFFLD(RLREMK)
      * 3 rabattbeløp i NOK
     A            GKRBN1    R               REFFLD(ROBELØ)
     A            GKRBN2    R               REFFLD(ROBELØ)
     A            GKRBN3    R               REFFLD(ROBELØ)
      * 3 rabattbeløp i valuta
     A            GKRBV1    R               REFFLD(ROBELØ)
     A            GKRBV2    R               REFFLD(ROBELØ)
     A            GKRBV3    R               REFFLD(ROBELØ)
      * Rabattdato for rabattbeløp 1
     A            GKRDA1    R               REFFLD(ROBDAT)
      * Rabattdato for rabattbeløp 2
     A            GKRDA2    R               REFFLD(ROBDAT)
      * Rabattdato for rabattbeløp 3
     A            GKRDA3    R               REFFLD(ROBDAT)
      * Kode for om faktura er rabattberegnet ("R")
     A            GKRABB    R               REFFLD(RTSTAT)
      * Opprinnelig fakturabeløp i NOK og valuta.
     A            GKOPBN    R               REFFLD(ROBELØ)
     A                                      TEXT('Opprinnelig fakturabeløp NOK')
     A            GKOPBV    R               REFFLD(ROBELØ)
     A                                      TEXT('Opprinnelig fakturabeløp Val')
      * Opprinnelig forfallsdato før ev. endinger p.g.a.
      * rabatt o.l.
     A            GKOPFD    R               REFFLD(ROFDAT)
      *
     A            GKBGIR    R               REFFLD(RLBGIR)
     A                                      TEXT('Bankgironummer v/lønnsutbet.')
     A            GKTENR         6
     A                                      TEXT('Terminkontraktsnummer.......')
     A            GKTEKU         8S 4
     A                                      TEXT('Terminkontraktskurs.........')
     A            GKAVTK         8S 4
     A                                      TEXT('Avtalt kurs.................')
     A            GKAVTM         6
     A                                      TEXT('Avtalt med..................')
     A            GKBEVA         1
     A                                      TEXT('Belaste valutakonto (J/N)...')
     A            GKREKO         1
     A                                      TEXT('Returkode-> Blank, 1 eller F')
     A            GKLFAK    R               REFFLD(ROLFAK)
     A                                      TEXT('Leverandørs Fakturanummer...')
     A            GKOPDA    R               REFFLD(ROBDAT)
     A                                      TEXT('Opprettet dato .............')
     A            GKOPAV        10
     A                                      TEXT('Opprettet av................')
      * Nøkkel
     A          K GKFIRM
     A          K GKBFNR
     A          K GKBFLI
