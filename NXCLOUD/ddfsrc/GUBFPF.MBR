      *  Utbetalingsforslag - Leverandører
      *
      *  For nærmere feltbeskrivelse, se definisjon av file
      *  RLTRPF for RO.... felter.
     A                                      REF(*LIBL/RFRF)
     A          R GABFPFR
     A            GASTAT    R               REFFLD(RTSTAT)
     A            GAFIRM    R               REFFLD(ROFIRM)
      * Forslagnummer:
     A            GABFNR    R               REFFLD(ROBFNR)
      * Forslagslinjenummer:
     A            GABFLI    R               REFFLD(ROBFLI)
     A            GALEVR    R               REFFLD(ROLEVR)
     A            GABDAT    R               REFFLD(ROBDAT)
     A            GATIST    R               REFFLD(ROTIST)
      * Leverandørnavn:
     A            GANAVN    R               REFFLD(RLNAVN)
      * Valutakode - betaling
     A            GAVALK    R               REFFLD(ROVALK)
      * Valutakode - inngående
     A            GAFVAL    R               REFFLD(ROVALK)
      * Attestasjonskode
     A            GAATTS    R               REFFLD(ROATTS)
      * Bilagskode
     A            GABILK    R               REFFLD(ROBILK)
      * Fakturabeløp
     A            GABELØ    R               REFFLD(ROBELØ)
      * Restbeløp
     A            GAREST    R               REFFLD(ROREST)
      * Bilagsnummer
     A            GABILN    R               REFFLD(ROBILN)
      * Valutabeløp faktura
     A            GAVALB    R               REFFLD(ROVALB)
      * Restbeløp faktura
     A            GAVALR    R               REFFLD(ROVALR)
      * Betalingskode
     A            GABETK    R               REFFLD(ROBETK)
     A            GARAAR    R               REFFLD(RORAAR)
     A            GARPER    R               REFFLD(RORPER)
     A            GAJOUR    R               REFFLD(ROJOUR)
     A            GASAMB    R               REFFLD(ROSAMB)
     A            GAREFN    R               REFFLD(ROREFN)
     A            GATEXT    R               REFFLD(ROTEXT)
     A            GAVIRK    R               REFFLD(ROVIRK)
      * Forfallsdato
     A            GAFDAT    R               REFFLD(ROFDAT)
      * Betalingsreferanse-1
     A            GARRF1    R               REFFLD(RORRF1)
      * Betalingsreferanse-2
     A            GARRF2    R               REFFLD(RORRF2)
      * Deklarasjonskode:
     A            GADECK    R               REFFLD(RODECK)
      * KID-feltet kan inneholde både KID og ikke KID.
     A            GAKIDI    R               REFFLD(ROKIDI)
      * Feltet GAKIDS vil inneholde "*" dersom feltet KID-feltet
      * GAKIDI inneholde KID.
     A            GAKIDS    R               REFFLD(RTSTAT)
     A            GAAVDE    R               REFFLD(ROAVDE)
     A            GAREKL    R               REFFLD(ROREKL)
      * Kode for om det er delbetalt
     A            GADELB    R               REFFLD(RODELB)
      * Betalingsbetingelse
     A            GABETB    R               REFFLD(ROBETB)
     A            GAREMK    R               REFFLD(RLREMK)
      * 3 rabattbeløp i NOK
     A            GARBN1    R               REFFLD(ROBELØ)
     A            GARBN2    R               REFFLD(ROBELØ)
     A            GARBN3    R               REFFLD(ROBELØ)
      * 3 rabattbeløp i valuta
     A            GARBV1    R               REFFLD(ROBELØ)
     A            GARBV2    R               REFFLD(ROBELØ)
     A            GARBV3    R               REFFLD(ROBELØ)
      * Rabattdato for rabattbeløp 1
     A            GARDA1    R               REFFLD(ROBDAT)
      * Rabattdato for rabattbeløp 2
     A            GARDA2    R               REFFLD(ROBDAT)
      * Rabattdato for rabattbeløp 3
     A            GARDA3    R               REFFLD(ROBDAT)
      * Kode for om faktura er rabattberegnet ("R")
     A            GARABB    R               REFFLD(RTSTAT)
      * Opprinnelig fakturabeløp i NOK og valuta.
     A            GAOPBN    R               REFFLD(ROBELØ)
     A                                      TEXT('Opprinnelig fakturabeløp NOK')
     A            GAOPBV    R               REFFLD(ROBELØ)
     A                                      TEXT('Opprinnelig fakturabeløp Val')
      * Opprinnelig forfallsdato før ev. endinger p.g.a.
      * rabatt o.l.
     A            GAOPFD    R               REFFLD(ROFDAT)
      *
     A            GABGIR    R               REFFLD(RLBGIR)
     A                                      TEXT('Bankgironummer v/lønnsutbet.')
     A            GATENR         6
     A                                      TEXT('Terminkontraktsnummer.......')
     A            GATEKU         8S 4
     A                                      TEXT('Terminkontraktskurs.........')
     A            GAAVTK         8S 4
     A                                      TEXT('Avtalt kurs.................')
     A            GAAVTM         6
     A                                      TEXT('Avtalt med..................')
     A            GABEVA         1
     A                                      TEXT('Belaste valutakonto (J/N)...')
     A            GAREKO         1
     A                                      TEXT('Returkode-> Blank, 1 eller F')
     A            GALFAK    R               REFFLD(ROLFAK)
     A                                      TEXT('Leverandørs Fakturanummer...')
     A            GAOPDA    R               REFFLD(ROBDAT)
     A                                      TEXT('Opprettet dato .............')
     A            GAOPAV        10
     A                                      TEXT('Opprettet av................')
