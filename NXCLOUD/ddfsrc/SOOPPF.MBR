      *
      * Optimera statistikkfil
      *
     A          R SOOPPFR
     A            AAFGRP         3A         TEXT('Filgruppe')
     A                                      COLHDG('Fil' 'grp')
     A            AAFNAV        30A         TEXT('Forhandler')
     A                                      COLHDG('Forhandler')
     A            AAFFTN         9S 0       TEXT('Forhandler org.nr')
     A                                      COLHDG('Forhandler' 'org.nr')
     A                                      EDTCDE(Z)
     A            SOPAAR         4S 0       TEXT('Periode år')
     A                                      COLHDG('Periode' 'år')
     A                                      EDTCDE(Z)
     A            SOPMND         2S 0       TEXT('Periode mnd')
     A                                      COLHDG('Periode' 'mnd')
     A                                      EDTCDE(Z)
     A            SOSAVD         4S 0       TEXT('Avd. salg')
     A                                      COLHDG('Avd.' 'salg')
     A                                      EDTCDE(Z)
     A            RAGTXT        30A         TEXT('Avdeling')
     A                                      COLHDG('Avdeling')
     A            SOVKUN         6S 0       TEXT('Kundenr')
     A                                      COLHDG('Kundenr')
     A                                      EDTCDE(Z)
     A            RKNAVN        30A         TEXT('Kunde')
     A                                      COLHDG('Kunde')
     A            RKFTNR         9S 0       TEXT('Kunde org.nr')
     A                                      COLHDG('Kunde' 'org.nr')
     A                                      EDTCDE(Z)
     A            RKKATG         4S 0       TEXT('Kundekategori')
     A                                      COLHDG('Kundekat.')
     A                                      EDTCDE(Z)
     A            RAKTXT        20A         TEXT('Kundekategori beskrivelse')
     A                                      COLHDG('Kundekat.' ' beskrivelse')
     A            SORKAT         3S 0       TEXT('Rabattkagegori')
     A                                      COLHDG('Rab.kat.')
     A                                      EDTCDE(Z)
     A            RAFTXT        20A         TEXT('Rabattkategori beskrivelse')
     A                                      COLHDG('Rab.kat.' ' beskrivelse')
     A            SOVARE        15A         TEXT('Varenummer')
     A                                      COLHDG('Varenr')
     A            VVTEK1        35A         TEXT('Varetekst 1')
     A                                      COLHDG('Varetekst 1')
     A            VVTEK2        35A         TEXT('Varetekst 2')
     A                                      COLHDG('Varetekst 2')
     A            SOVTYP         1A         TEXT('Varetype')
     A                                      COLHDG('Vare' 'type')
     A            SOLVRE         1S 0       TEXT('VA-registrert')
     A                                      COLHDG('VA')
     A                                      EDTCDE(Z)
     A            SOLDOR         6S 0       TEXT('Leverandørnr')
     A                                      COLHDG('Lev.nr')
     A                                      EDTCDE(Z)
     A            RLNAVN        30A         TEXT('Leverandør')
     A                                      COLHDG('Leverandør')
     A            SOSUMK        13S 2       TEXT('Sum kost')
     A                                      COLHDG('Sum kost')
     A                                      EDTCDE(M)
     A            SOSUMB        13S 2       TEXT('Sum brutto')
     A                                      COLHDG('Sum brutto')
     A                                      EDTCDE(M)
     A            SORAB1        13S 2       TEXT('Sum rabatt 1')
     A                                      COLHDG('Sum rabatt 1')
     A                                      EDTCDE(M)
     A            SORAB2        13S 2       TEXT('Sum rabatt 2')
     A                                      COLHDG('Sum rabatt 2')
     A                                      EDTCDE(M)
     A            SORABO        13S 2       TEXT('Sum ordrerabatt')
     A                                      COLHDG('Sum ordrerabatt')
     A                                      EDTCDE(M)
     A            SOSUMN        13S 2       TEXT('Sum netto')
     A                                      COLHDG('Sum netto')
     A                                      EDTCDE(M)
     A            SODEKN         5S 2       TEXT('Dekningsgrad')
     A                                      COLHDG('DG')
     A                                      EDTCDE(M)
