     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JV720R
      * Beskrivelse . . : Oppdater fra ODA, oppdaterer matche-registeret
      *
      * Leser gjennom matche-registeret, og oppdatererer varer med
      * informasjon fra vareregisteret.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       211298 HKO  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjmatl1    if   e           k disk    rename(jmatpfr:jmatl1r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjmatlu    uf   e           k disk    rename(jmatpfr:jmatlur)
      *
      * Lokal data area (*LDA)
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variable i nøkkler
      *
     d jvarl1_vare     s                   like(jvvare)
     d jmatlu_var1     s                   like(jovar1)
      *
      * Definering av variable
      *
     d w_firm          s                   like(jofirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     jmatl1_key    setll     jmatl1
     c     jmatl1_key    reade     jmatl1                                 90
     c                   dow       *in90 = *off
      *
     c                   if        jovar2 = *blank
     c                   goto      neste_vare
     c                   endif
      *
      * Oppdaterer informasjon fra vareregisteret (LVR)
      *
     c                   eval      jvarl1_vare = jovar2
     c     jvarl1_key    chain     jvarl1                             91
     c                   if        *in91 = *off and
     c                             joldo2 <> jvldor
     c                   eval      jmatlu_var1 = jovar1
     c     jmatlu_key    chain     jmatlur                            91
     c                   if        *in91 = *off
     c                   eval      joldo2 = jvldor
     c                   update    jmatlur
     c                   endif
     c                   endif
      *
     c     neste_vare    tag
      *
     c     jmatl1_key    reade     jmatl1                                 90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for les på matche-register
      *
     c     jmatl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på vare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for oppdatering av matche-register
      *
     c     jmatlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jmatlu_var1
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
