      /TITLE  Utskrift av kundeinformasjon
     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RK637R                                              *
      * Beskrivelse . . : Utskrift av kundeinformasjon                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
T5.30 * V5.50 071105 KOB  Nytt program
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     faforl1    if   e           k disk    rename(aforpfr:aforl1r)
     frk637p    o    e             printer oflind(*in30)
      *
      /eject
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      * Data struktur
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      *
     d w_firm          s                   like(rkfirm)
      *
     d w_prfi          s              1  0
     d w_ffir          s              3  0
     d w_avde          s              4  0
     d w_fnav          s             30
     d w_fad1          s             30
     d w_fad2          s             30
     d w_fste          s             30
     d w_fftx          s             30
     d w_tfax          s              8
     d w_ftlf          s             11
     d w_ffax          s             11
     d w_tbnk          s             10
     d w_fbgi          s             13
      *
     d w_ckod          s                   like(rkbetb)
     d w_ctxt          s             20
     d w_kkod          s                   like(rkkatg)
     d w_ktxt          s             20
     d w_ponr          s                   like(rkponr)
     d w_sted          s                   like(rksted)
     d w_stat          s              1
      *
     d p_kund          s                   like(rkkund)
     d p_valg          s              1
      *
     d rkunl1_kund     s                   like(rkkund)
     d aforl1_ruti     s                   like(afruti)
      *
      * Local data
      *
     d                uds
     d l_ruti                800    809
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      /eject
      *
      * Leser kunde
      *
     c                   eval      rkunl1_kund = p_kund
     c     rkunl1_key    chain     rkunl1                             61
      *
      * Hent tekst
      *
     c                   exsr      hent
      *
      * Skriv ut faste data
      *
     c                   eval      a1valg = p_valg
     c                   write     head
      *
      * Skriv ut kundeinformasjon
      *
     c                   write     linje
      *
     c     slutt         tag
      *
     c                   eval      *inlr = *on                                  ..............
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for henting av tekster                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     hent          begsr
      *
      * Kundekategori
      *
     c                   eval      w_kkod = rkkatg
     c                   call      'RS711R'
     c                   parm                    w_stat
     c                   parm                    w_kkod
     c                   parm                    w_ktxt
      *
     c                   move      w_ktxt        d1ktxt
      *
      * Betalingsbetingelse
      *
     c                   eval      w_ckod = rkbetb
     c                   call      'RS703R'
     c                   parm                    w_stat
     c                   parm                    w_ckod
     c                   parm                    w_ctxt
      *
     c                   move      w_ctxt        d1ctxt
      *
      * Poststed
      *
     c                   eval      w_ponr = rkponr
     c                   call      'RS730R'
     c                   parm                    w_stat
     c                   parm                    w_ponr
     c                   parm                    w_sted
      *
     c                   movel     w_sted        d1bstd
      *
      * Beregn kreditgrense
      *
     c                   eval      d1grns = rkgrns * 1000
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_kund
     c                   parm                    p_valg
      *
     c                   eval      w_firm = l_firm
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for rutine-register
      *
     c     aforl1_key    klist
     c                   kfld                    aforl1_ruti
      *
      * Hente inn formular-register
      *
     c                   eval      aforl1_ruti = l_ruti
     c     aforl1_key    chain     aforl1r
      *
     c                   if        not%found
     c                   eval      afprfi = 1
     c                   endif
      *
      *  Hent inn firma opplysninger dersom
      *  kode for det er satt i rutineregister
      *
     c                   if        afprfi = 1
     c                   eval      w_prfi = afprfi
     c                   eval      w_ffir = w_firm
     c                   eval      w_avde = 0
      *
     c                   call      'RÃ…707R'
     c                   parm                    w_prfi
     c                   parm                    w_ffir
     c                   parm                    w_avde
     c                   parm                    w_fnav
     c                   parm                    w_fad1
     c                   parm                    w_fad2
     c                   parm                    w_fste
     c                   parm                    w_fftx
     c                   parm                    w_tfax
     c                   parm                    w_ftlf
     c                   parm                    w_ffax
     c                   parm                    w_tbnk
     c                   parm                    w_fbgi
      *
     c                   eval      a1fnav = w_fnav
     c                   eval      a1fad1 = w_fad1
     c                   eval      a1fad2 = w_fad2
     c                   eval      a1fste = w_fste
     c                   eval      a1fftx = w_fftx
     c                   eval      a1tfax = w_tfax
     c                   eval      a1ftlf = w_ftlf
     c                   eval      a1ffax = w_ffax
     c                   eval      a1tbnk = w_tbnk
     c                   eval      a1fbgi = w_fbgi
      *
      * Skal firmanavn droppes ?
      *
     c                   if        w_prfi = 0
     c                   eval      a1fnav = *blank
     c                   eval      a1fad1 = *blank
     c                   eval      a1fad2 = *blank
     c                   eval      a1fste = *blank
     c                   eval      a1fftx = *blank
     c                   eval      a1tfax = *blank
     c                   eval      a1ftlf = *blank
     c                   eval      a1ffax = *blank
     c                   eval      a1tbnk = *blank
     c                   eval      a1fbgi = *blank
     c                   endif
      *
     c                   endif
      *
     c                   endsr
      *
