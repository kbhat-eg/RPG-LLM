     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RL113R
      * beskrivelse . . : Endre attestasjonskode
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbilnrivelse
      * ----- ------ ---- -------------------------------------------------
      *       160602 BOF  Nytt program
T5.00 * R5.00 140203 HFL  Rettet feil i bilagsdato.
T5.00 *                   Bruker inntastet år.
T5.30 * R5.30 041004 HFL  Bruker RLTRL9 og RLTRLA, bare åpne poster.
T5.30 * R5.40 190505 KOB  Lagt ut bruker og dato for attestasjon på post.
T5.30 * R5.50 220107 KOB  Lagt ut defaultverdi for år
6.10  * V6.10 230909 BSA  Lagt inn sporingsfelter
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
E5.30frltrl9    if   e           k disk    rename(rltrpfr:rltrl9r)
E5.30frltrla    if   e           k disk    rename(rltrpfr:rltrlar)
     frltrlu    uf a e           k disk    rename(rltrpfr:rltrlur)
      *
     frl113d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
E5.30d rltrl9_levr     s                   like(rolevr)
E5.30d rltrla_biln     s                   like(robiln)
     d rltrlu_levr     s                   like(rolevr)
     d rltrlu_bdat     s                   like(robdat)
     d rltrlu_tist     s                   like(rotist)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_valg_ok       s              1    inz(*off)
     d b_forn          s              1    inz(*off)
      *
     d w_firm          s                   like(rofirm)
4.51 d w_raar          s              4  0
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
     d                 ds
     d w_dato                         6  0
     d w_dag                          2  0 overlay(w_dato:1)
4.51 d w_mnd                          2  0 overlay(w_dato:3)
4.51 d w_aar                          2  0 overlay(w_dato:5)
      *
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * d_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2levr <> 0 or
     c                             b2biln <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        d_fcrn <> 0
     c     d_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
E5.30c                   eval      rltrla_biln = b1biln
E5.30c     rltrla_key    setll     rltrla
     c                   other
E5.30c                   eval      rltrl9_levr = b1levr
E5.30c     rltrl9_key    setll     rltrl9
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Leverandørnummer
      *
     c                   if        b2levr <> 0
     c                   eval      w_seqe = *blank
E5.30c                   eval      rltrl9_levr = b2levr
E5.30c     rltrl9_key    setll     rltrl9
     c                   goto      end_pos
     c                   endif
      *
      * bilagsnummer
      *
     c                   if        b2biln <> 0
     c                   eval      w_seqe = 'B'
E5.30c                   eval      rltrla_biln = b2biln
E5.30c     rltrla_key    setll     rltrla
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2levr = 0
     c                   eval      b2biln = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = d_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre attestasjonskode
      *
     c                   if        b1valg = 1
     c                   eval      rltrlu_levr = b1levr
     c                   eval      rltrlu_bdat = b1bdat
     c                   eval      rltrlu_tist = b1tist
      *
     c     rltrlu_key    chain     rltrlu
      *
     c                   if        %found(rltrlu)
V5.40c                   eval      roattu = l_user
V5.40c                   time                    roadat
     c                   eval      roatts = '1'
6.10 c                   time                    roedat
6.10 c                   time                    roetim
6.10 c                   eval      roeusr = l_user
     c                   update    rltrlur
     c                   endif
      *
     c                   eval      b1valg = 0
     c                   eval      b_forn = *on
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
E5.30c                   read      rltrla
     c                   other
E5.30c                   read      rltrl9
     c                   endsl
      *
     c                   if        %eof or
     c                             rofirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
E5.00c                   if        rorest <> *zero and
T5.00c                             roraar >= w_raar
     c                   if        roatts = '0' or
     c                             roatts = *blank
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
E5.30c                   eval      rltrla_biln = robiln
     c                   other
E5.30c                   eval      rltrl9_levr = rolevr
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1levr = rolevr
     c                   eval      b1biln = robiln
     c     *dmy          move      robdat        b1bida
S5.00c*                  eval      b1bdat = b1bdat
T5.00c                   eval      b1bdat = robdat
     c                   eval      b1text = rotext
     c                   eval      b1belØ = robelØ
     c     *dmy          move      rofdat        b1fdat
     c                   eval      b1tist = rotist
      *
     c                   write     b1sfl
      *
     c                   endif
     c                   endif
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'B'
E5.30c     rltrla_key    setgt     rltrla
E5.30c                   readp     rltrla
     c                   other
E5.30c     rltrl9_key    setgt     rltrl9
E5.30c                   readp     rltrl9
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'B'
E5.30c                   readp     rltrla
     c                   other
E5.30c                   readp     rltrl9
     c                   endsl
      *
     c                   if        %eof or
     c                             rofirm <> w_firm
     c                   leave
     c                   endif
      *
      * test om det er rette året og at att.kode = 0
      *
     c                   if        roatts = '0' or
     c                             roatts = *blank
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
E5.30c                   eval      rltrla_biln = robiln
     c                   other
E5.30c                   eval      rltrl9_levr = rolevr
     c                   endsl
      *
     c                   endif
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'B'
E5.30c     rltrla_key    setll     rltrla
     c                   other
E5.30c     rltrl9_key    setll     rltrl9
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sette inn årstall (N1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xn1win        begsr
      *
     c     n1tagb        tag
      *
     c                   exfmt     n1win
      *
     c                   if        *inkc or *inkl
     c*                  eval      b_avslutt = *on
     c                   goto      end_n1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
     c                   if        n1raar = *zero  or
     c                             n1raar < 1990
     c                   eval      *in31 = *on
     c                   goto      n1tagb
     c                   endif
      *
     c     end_n1win     endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på leverandør
      *
E5.30c     rltrl9_key    klist
     c                   kfld                    w_firm
E5.30c                   kfld                    rltrl9_levr
      *
      * Key for posisjonering på bilagsnummer
      *
E5.30c     rltrla_key    klist
     c                   kfld                    w_firm
E5.30c                   kfld                    rltrla_biln
      *
      * Key for oppdatering av attestasjonskode
      *
     c     rltrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rltrlu_levr
     c                   kfld                    rltrlu_bdat
     c                   kfld                    rltrlu_tist
      *
      *
      *
      * Henter parametre, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
      * Henter periode
      *
     c                   eval      n1raar = *year
     c                   exsr      xn1win
     c                   eval      w_raar      = n1raar
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
E5.30c                   eval      rltrl9_levr = 0
E5.30c     rltrl9_key    setll     rltrl9
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
