     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASNAVIOKO
      * Program . . . . : IT113R
      * Beskrivelse . . : Oppdaterer betalingsbetingelser fra NAV
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       201114 ask  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fra03lu    uf a e           k disk    rename(ra03pfr:ra03lur)
      *
      * Parameter-inn
      *
     d p_firm          s                   like(racfir)
     d p_comd          s             20
     d p_parm          s           2048
     d p_user          s             10
     d p_stat          s              1
      *
      * Definering av variabler i nøkler
      *
     d ra03lu_ckod     s                   like(rackod)
      *
      *  Definering av datastrukturer for parameter
      *
     d d_parm          ds          2048
     d  d_ckod                        2
     d  d_besk                       40
     d  d_frim                        3
     d  d_kdag                        3
     d  d_kr1d                        3
     d  d_kr1p                        5
     d  d_kr2d                        3
     d  d_kr2p                        5
     d  d_kr3d                        3
     d  d_kr3p                        5
      *
      * Definering av variable
      *
     d w_firm          s                   like(racfir)
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_ckod          s                   like(rackod)
     d w_rabp          s              4  2
     d w_hela          s              2
     d w_heln          s              2  0
     d w_desa          s              2
     d w_desn          s              2  0
     d w_desm          s              2  2
     d w_pos1          s              2  0
     d w_pos2          s              2  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram - oppdaterer informasjon på ckod., setter statusflagg
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Redigerer parameterdata før behandling
      *
     c                   eval      d_parm = p_parm
     c                   exsr      red_num
     c                   exsr      red_flt
      *
      * Velger oppdatering avhengig av kommando
      *
     c                   select
     c                   when      p_comd = 'INSERT'
     c                   exsr      nyrec
     c                   when      p_comd = 'UPDATE'
     c                   exsr      oppdrec
     c                   when      p_comd = 'DELETE'
     c                   exsr      sletrec
     c                   endsl
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     slutt         tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Opprette kodepost
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     nyrec         begsr
      *
     c                   eval      w_firm = p_firm
     c                   eval      ra03lu_ckod = w_ckod
     c     ra03lu_key    chain     ra03lu
     c                   if        not %found
     c                   eval      racfir = w_firm
     c                   eval      rackod = w_ckod
     c                   eval      ractxt = %subst(d_besk:1:20)
     c                   eval      ractx2 = %subst(d_besk:21:20)
     c                   eval      racdag = %dec(d_kdag:3:0)
     c                   eval      racjan = d_frim
     c                   eval      racrab = w_rabp
     c                   eval      racda1 = %dec(d_kr1d:3:0)
     c                   eval      racdat = w_date
     c                   eval      ractim = w_time
     c                   eval      racusr = p_user
     c                   write     ra03lur
     c                   eval      p_stat = '1'
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdatere kodepost
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdrec       begsr
      *
     c                   eval      w_firm = p_firm
     c                   eval      ra03lu_ckod = w_ckod
     c     ra03lu_key    chain     ra03lu
     c                   if        %found
     c                   eval      ractxt = %subst(d_besk:1:20)
     c                   eval      ractx2 = %subst(d_besk:21:20)
     c                   eval      racdag = %dec(d_kdag:3:0)
     c                   eval      racjan = d_frim
     c                   eval      racrab = w_rabp
     c                   eval      racda1 = %dec(d_kr1d:3:0)
     c                   eval      racdat = w_date
     c                   eval      ractim = w_time
     c                   eval      racusr = p_user
     c                   update    ra03lur
     c                   eval      p_stat = '1'
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Slett kodepost
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sletrec       begsr
      *
     c                   eval      w_firm = p_firm
     c                   eval      ra03lu_ckod = w_ckod
     c     ra03lu_key    chain     ra03lu
     c                   if        %found
     c                   delete    ra03lur
     c                   eval      p_stat = '1'
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Bytter ut blanke med nuller i numeriske felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     red_num       begsr
      *
     c                   if        d_kdag = *blanks
     c     ' ':'0'       xlate     d_kdag        d_kdag
     c                   endif
     c                   if        d_kr1d = *blanks
     c     ' ':'0'       xlate     d_kr1d        d_kr1d
     c                   endif
     c                   if        d_kr1p = *blanks
     c     ' ':'0'       xlate     d_kr1p        d_kr1p
     c                   endif
     c                   if        d_kr2d = *blanks
     c     ' ':'0'       xlate     d_kr2d        d_kr2d
     c                   endif
     c                   if        d_kr2p = *blanks
     c     ' ':'0'       xlate     d_kr2p        d_kr2p
     c                   endif
     c                   if        d_kr3d = *blanks
     c     ' ':'0'       xlate     d_kr3d        d_kr3d
     c                   endif
     c                   if        d_kr3p = *blanks
     c     ' ':'0'       xlate     d_kr3p        d_kr3p
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Redigerer numeriske felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     red_flt       begsr
      *
      * Kodefelt
      *
     c                   eval      w_ckod = %dec(d_ckod:2:0)
      *  Rabatt%
     c                   eval      w_pos1 = %scan(',':d_kr1p)
     c                   eval      w_pos2 = %scan(' ':d_kr1p)
      *
     c                   if        w_pos1 = 0 and
     c                             w_pos2 = 0
     c                   eval      w_rabp = 0
     c                   goto      end_flt
     c                   endif
      *
     c                   if        w_pos1 = 0
     c                   eval      w_hela = %subst(d_kr1p:1:w_pos2-1)
     c                   eval      w_desa = '00'
     c                   goto      bygg_flt
     c                   endif
      *
     c                   if        w_pos1 = 2
     c                   eval      w_hela = %subst(d_kr1p:1:w_pos1-1)
     c                   if        w_pos2 = 4
     c                   eval      w_desa = %subst(d_kr1p:w_pos1+1:1) + '0'
     c                   endif
     c                   if        w_pos2 = 5
     c                   eval      w_desa = %subst(d_kr1p:w_pos1+1:2)
     c                   endif
     c                   endif
      *
     c                   if        w_pos1 = 3
     c                   eval      w_hela = %subst(d_kr1p:1:w_pos1-1)
     c                   if        w_pos2 = 5
     c                   eval      w_desa = %subst(d_kr1p:w_pos1+1:1)
     c                   endif
     c                   if        w_pos2 = 0
     c                   eval      w_desa = %subst(d_kr1p:w_pos1+1:2)
     c                   endif
     c                   endif
      *
     c     bygg_flt      tag
     c                   eval      w_heln = %dec(w_hela:2:0)
     c                   eval      w_desn = %dec(w_desa:2:0)
     c                   eval      w_desm = w_desn / 100
     c                   eval      w_rabp = w_heln + w_desm
      *
     c     end_flt       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ERROR-rutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *pssr         begsr
      *
     c                   eval      p_stat = ' '
     c                   eval      *inlr = *on
     c                   return
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_comd
     c                   parm                    p_parm
     c                   parm                    p_user
     c                   parm                    p_stat
      *
      * Key for posisjonering på valgt kode
      *
     c     ra03lu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra03lu_ckod
      *
      * Setter "timestamp" for postoppdateringer
      *
     c                   time                    w_date
     c                   time                    w_time
      *
     c                   endsr
