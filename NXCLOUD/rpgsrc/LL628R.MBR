      /TITLE  Utskrift av min-/max-beholdning
     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : LL628R
      * Beskrivelse . . : Utskrift av min-/max-beholdning
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.
      * --------- ------  --------------------------------------------------
      *           020900  Nytt program                             KOB
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flwa1l1    if   e           k disk    rename(lwa1pfr:lwa1l1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
      *
     fll628p    o    e             printer
      *
      *  Parameter-rec
      *
     d                 ds
     d p_prec                       128
     d  p_fvar                        8  0 overlay(p_prec)
     d  p_tvar                        8  0 overlay(p_prec:9)
     d  p_flag                        2  0 overlay(p_prec:17)
     d  p_tlag                        2  0 overlay(p_prec:19)
     d  p_fogr                        2  0 overlay(p_prec:21)
     d  p_togr                        2  0 overlay(p_prec:23)
     d  p_fhgr                        2  0 overlay(p_prec:25)
     d  p_thgr                        2  0 overlay(p_prec:27)
     d  p_fugr                        3  0 overlay(p_prec:29)
     d  p_tugr                        3  0 overlay(p_prec:32)
     d  p_levn                        6  0 overlay(p_prec:35)
     d  p_sakb                        6  0 overlay(p_prec:41)
     d  p_vari                        1  0 overlay(p_prec:45)
      *
      * Local Data Area
      *
     d                uds
     d l_wsid                901    910
     d l_user                        10
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variable i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d w_parm          s            128
     d w_firm          s                   like(vvfirm)
     d w_dato          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_vare          s              8  0
     d w_adrs          s             30
     d w_ponr          s              4  0
     d w_pste          s             30
     d w_avde          s              4  0
     d w_tell          s              3  0
      *
     d b_feil          s              1    inz(*off)
     d b_init          s              1    inz(*on)
      *
      * - - - - - - - - - -
      * Benyttede formater
      * - - - - - - - - - -
      *
      * A1HDR  - Heading A1 på rapport
      * D1DET  - Detail  D1 på rapport
      *
      * Skriv første heading
      *
     c                   write     a1hdr                                30
      *
      * Lese file
      *
     c                   read      lwa1l1                                 90
      *
     c                   dow       *in90 = *off
      *
      * Utskrift av detaljer
      *
     c                   movel     mlvare        d1vare
     c                   movel     mlvare        vvarl1_vare
     c     vvarl1_key    chain     vvarl1                             91
     c                   if        *in91 = *off
     c                   eval      d1tek1 = vvtek1
     c                   else
     c                   eval      d1tek1 = 'Ukjent vare'
     c                   endif
      *
     c                   eval      d1enhl = vvenhl
     c                   eval      d1lage = mllage
      *
     c                   eval      d1mini = mlmini
     c                   eval      d1maks = mlmaks
     c                   eval      d1behl = mlbehl
     c                   eval      d1kpri = mlkpri
     c                   eval      d1kost = mlkost
     c                   eval      d1spri = mlspri
     c                   eval      d1omst = mlomst
      *
     c                   write     d1det                                30
     c                   if        *in30 = *on
     c                   exsr      overflow
     c                   endif
      *
     c                   read      lwa1l1                                 90
      *
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  OVFLOW    Behandling av overflow
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     overflow      begsr
      *
      *  Skriver heading
      *
     c                   write     a1hdr                                30
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for les av vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    w_parm
     c                   eval      p_prec = w_parm
      *
     c                   time                    w_dato
     c                   time                    w_time
     c                   eval      w_firm = l_firm
      *
      * Redigere heading
      *
     c     *dmy          move      w_dato        a1date
     c     *hms          move      w_time        a1time
     c                   eval      a1user = l_user
     c                   eval      a1wsid = l_wsid
     c                   eval      a1firm = l_firm
     c                   eval      a1fnav = l_fnav
      *
     c                   eval      *in71  = p_vari = 1
     c                   eval      *in72  = p_vari = 2
     c                   endsr
