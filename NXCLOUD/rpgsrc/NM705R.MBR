     h option(*nodebugio) datedit(*dmy)
     h alwnull(*usrctl)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : NM705R
      * Beskrivelse . . : Ehandel MGR, eksport av leverandører
      *
      * Spesialversjon. : MGR Ehandelsløsning
      * Tilpasset for . : Mestergruppen
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       090316 ASK  Nytt program
9.01  * 9.01  121117 ASK  Legger ut butikkid på filen
9.02  * 9.02  100418 ASK  Legger ut leverandører med kategori 2010 i tillegg
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fclrei1    if   e           k disk    rename(clrest:clrei1r)
     fanbll3    if   e           k disk    rename(anblpfr:anbll3r)
     fanbhl1    if   e           k disk    rename(anbhpfr:anbhl1r)
      *
     fnetlever  uf a e           k disk
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
     d l_fgr                 931    933
      *
      * Definering av datastrukturer
      *
      * Recordbeskrivelse - meldingshode
      *
     d                 ds
     d d_unh                         41
     d  d_unhrcd                      3    overlay(d_unh: 1) inz('UNH')
     d  d_unhfill1                    1    overlay(d_unh: 4) inz(';')
     d  d_unhrefno                   26    overlay(d_unh: 5)
     d  d_unhfill2                    1    overlay(d_unh:31) inz(';')
     d  d_unhtypeid                   6    overlay(d_unh:32) inz('IMLIMP')
     d  d_unhfill3                    1    overlay(d_unh:38) inz(';')
     d  d_unhversion                  3    overlay(d_unh:39) inz('1.0')
      *
      * Recordbeskrivelse - meldingsstart
      *
     d                 ds
     d d_bgm                         20
     d  d_bgmrcd                      3    overlay(d_bgm: 1) inz('BGM')
     d  d_bgmfill1                    1    overlay(d_bgm: 4) inz(';')
     d  d_bgmetypekod                10    overlay(d_bgm: 5) inz('IMLDATALEV')
     d  d_bgmfill2                    2    overlay(d_bgm:15) inz(';;')
     d  d_bgmfunction                 1    overlay(d_bgm:17) inz('1')
      *
      * Recordbeskrivelse - organisasjon
      *
     d                 ds
     d d_org                        126
     d  d_orgrcd                      3    overlay(d_org: 1) inz('ORG')
     d  d_orgfill1                    1    overlay(d_org: 4) inz(';')
     d  d_orgid                      30    overlay(d_org: 5)
     d  d_orgfill2                    1    overlay(d_org:35) inz(';')
     d  d_orgstoreid                 30    overlay(d_org:36)
     d  d_orgfill3                    1    overlay(d_org:66) inz(';')
     d  d_orgname                    60    overlay(d_org:67)
      *
      * Recordbeskrivelse - leverandør
      *
     d                 ds
     d d_lev                        100
     d  d_levrcd                      3    overlay(d_lev:  1) inz('LEV')
     d  d_levfill1                    1    overlay(d_lev:  4) inz(';')
     d  d_levnumm                     6  0 overlay(d_lev:  5)
     d  d_levfill2                    1    overlay(d_lev: 11) inz(';')
     d  d_levnavn                    30    overlay(d_lev: 12)
     d  d_levfill3                    1    overlay(d_lev: 42) inz(';')
     d  d_levorg                      9  0 overlay(d_lev: 43)
     d  d_levfill4                    1    overlay(d_lev: 52) inz(';')
     d  d_levgln                     14  0 overlay(d_lev: 53)
9.01 d  d_levfill5                    1    overlay(d_lev: 67) inz(';')
9.01 d  d_levorgid                   25    overlay(d_lev: 68)
      *
      * Recordbeskrivelse - leverandør/region
      *
     d                 ds
     d d_rel                        100
     d  d_relrcd                      3    overlay(d_rel:  1) inz('REL')
     d  d_relfill1                    1    overlay(d_rel:  4) inz(';')
     d  d_relnumm                     6  0 overlay(d_rel:  5)
     d  d_relfill2                    1    overlay(d_rel: 11) inz(';')
     d  d_relreg                      2  0 overlay(d_rel: 12)
      *
      * Recordbeskrivelse - meldingshale
      *
     d                 ds
     d d_unt                         43
     d  d_untrcd                      3    overlay(d_unt: 1) inz('UNT')
     d  d_untfill1                    1    overlay(d_unt: 4) inz(';')
     d  d_untnosegmen                12  0 overlay(d_unt: 5)
     d  d_untfill2                    1    overlay(d_unt:17) inz(';')
     d  d_untrefno                   26    overlay(d_unt:18)
      *
      * Definering av variabler i nøkler
      *
     d anbll3_fgr      s                   like(aolfgr)
     d anbll3_fir      s                   like(aolfir)
     d clrei1_lev      s                   like(clrlev)
      *
      * Definering av variable
      *
     d w_firm          s                   like(rlfirm)
     d w_tell          s             12  0
     d w_tmst          s               z
     d w_dato          s               d
      *
      * Definering av parametre
      *
     d p_stat          s              1
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      p_stat = *blank
      *
      * Henter data om nettbutikk og eier. Avslutter hvis ingen finnes.
      *
     c                   eval      anbll3_fgr = l_fgr
     c                   eval      anbll3_fir = l_firm
     c     anbll3_key    chain     anbll3r
     c                   if        not %found
     c                   eval      p_stat = 'E'
     c                   goto      avslutt
     c                   endif
     c     aoleie        chain     anbhl1r
     c                   if        not %found
     c                   eval      p_stat = 'E'
     c                   goto      avslutt
     c                   endif
      *
      * Meldingshode
      *
     c                   time                    w_tmst
     c                   time                    w_dato
     c                   move      w_tmst        d_unhrefno
      *
     c                   eval      data = d_unh
     c                   write     netleverr
      *
      * Meldingsstart
      *
     c                   eval      data = d_bgm
     c                   write     netleverr
      *
      * Organisasjon
      *
     c                   eval      d_orgid = aoheie
     c                   eval      d_orgstoreid = aolbut
     c                   eval      d_orgname = aohnav
      *
     c                   eval      data = d_org
     c                   write     netleverr
      *
      * Leverandører
      *
     c     rlevl1_key    setll     rlevl1
     c     rlevl1_key    reade     rlevl1r
     c                   dow       %eof = *off
      *
      * Tar bare med leverandører med angitt kategori
      *
     c                   if        rlkatg <> 1001 and
     c                             rlkatg <> 1010 and
     c                             rlkatg <> 2001 and
9.02 c                             rlkatg <> 2010 and
     c                             rlkatg <> 3001 and
     c                             rlkatg <> 4001
     c                   goto      neste_lev
     c                   endif
      *
     c                   eval      d_levnumm = rllevr
     c                   eval      d_levnavn = rlnavn
     c                   eval      d_levorg  = rlftnr
     c                   eval      d_levgln  = rleanl
9.01 c                   eval      d_levorgid = aoheie
      *
     c                   eval      data = d_lev
     c                   write     netleverr
     c                   eval      w_tell = w_tell + 1
      *
     c                   exsr      levreg
      *
     c     neste_lev     tag
     c     rlevl1_key    reade     rlevl1r
     c                   enddo
      *
      * Meldingshale
      *
     c                   eval      d_untnosegmen = w_tell
     c                   eval      d_untrefno = d_unhrefno
      *
     c                   eval      data = d_unt
     c                   write     netleverr
      *
      *
      * Avslutt
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegg av regionssperre på leverandør
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     levreg        begsr
      *
     c                   eval      clrei1_lev = rllevr
     c     clrei1_key    setll     clrei1
     c     clrei1_key    reade     clrei1r
     c                   dow       %eof = *off
      *
     c                   eval      d_relnumm = clrlev
     c                   eval      d_relreg  = clrreg
      *
     c                   eval      data = d_rel
     c                   write     netleverr
     c                   eval      w_tell = w_tell + 1
      *
     c     clrei1_key    reade     clrei1r
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_stat
      *
      * Key for les på leverandører
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for les på kobling leverandør/region
      *
     c     clrei1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    clrei1_lev
      *
      * Key for oppslag på nettbutikk
      *
     c     anbll3_key    klist
     c                   kfld                    anbll3_fgr
     c                   kfld                    anbll3_fir
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
