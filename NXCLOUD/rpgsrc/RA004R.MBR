     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RA004R
      * Beskrivelse . . : Statuskoder, vedlikehold sist brukte nummer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       050502 KOB  Nytt program
      *       110803 KOB  Tatt i bruk utg.fakturanr. som purrenummer
      *       110803 KOB  Fjernet inng.faktura og butikkfaktura
      *       310505 KOB  Lagt inn sist brukte kontaktperson
      *       231005 KOB  Lagt inn sist brukte inngående faktura
      *       310108 KOB  Fjernet ubrukte felt i sist brukte nr.
6.10  * R6.10 030809 NHO  Nye felt i forb med sporingsref og avkr.
6.10  *
7.xx  * 7.00  090516 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fraa4l1    if   e           k disk    rename(raa4pfr:raa4l1r)
     fraa4lr    if   e           k disk    rename(raa4pfr:raa4lrr)
     fraa4lu    uf a e           k disk    rename(raa4pfr:raa4lur)
     fraa1l1    if   e           k disk    rename(raa1pfr:raa1l1r)
      *
     fra004d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d raa4l1_4aar     s                   like(ra4aar)
     d raa4lr_4aar     s                   like(ra4aar)
     d raa4lu_4aar     s                   like(ra4aar)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(ra4fir)
      *
     d s_4aar          s                   like(ra4aar)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(13)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * d_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * C3BLD  - C3 Skjermbilde for sist bruket kunde-/leverandørnummer
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b24aar <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        d_fcrn <> 0
     c     d_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      raa4l1_4aar = b14aar
     c     raa4l1_key    setll     raa4l1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Regnskapsår
      *
     c                   if        b24aar <> 0
     c                   eval      w_seqe = *blank
     c                   eval      raa4l1_4aar = b24aar
     c     raa4l1_key    setll     raa4l1
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b24aar = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = d_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre - sist brukte kunde-/leverandør-nummer
      *
     c                   if        b1valg = 2 and b14aar = 0
     c                   eval      c14aar = 0
     c                   exsr      xc3bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Endre - sist brukte nummer pr. år
      *
     c                   if        b1valg = 2 and b14aar <> 0
     c                   eval      c14aar = b14aar
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k14aar = b14aar
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d14aar = b14aar
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vise - sist brukte kunde-/leverandør-nummer
      *
     c                   if        b1valg = 5 and b14aar = 0
     c                   eval      c14aar = 0
     c                   exsr      xc3bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vise - sist brukte nummer pr. år
      *
     c                   if        b1valg = 5 and b14aar <> 0
     c                   eval      c14aar = b14aar
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   eval      b14aar = 0
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c14aar = b24aar
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c14aar = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      raa4lr_4aar = c14aar
     c     raa4lr_key    chain     raa4lr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c14aar = 0
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b24aar = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
     c                   eval      c24aar = c14aar
      *
     c                   eval      raa4lr_4aar = c14aar
     c     raa4lr_key    chain     raa4lr
      *
     c                   if        %found
     c                   eval      c24sam = ra4sam
     c                   eval      c24sno = ra4sno
     c                   eval      c24sug = ra4sug
     c                   eval      c24sig = ra4sig
     c                   eval      c24sjÅ = ra4sjÅ
6.10 c                   eval      c24spo = ra4spo
6.10 c                   eval      c24avk = ra4avk
     c                   eval      c24sgb = ra4sgb
     c***                eval      c24sub = ra4sub
     c***                eval      c24sln = ra4sln
     c***                eval      c24suf = ra4suf
     c                   eval      c24sif = ra4sif
     c                   eval      c2eusr = ra4usr
      *
     c                   if        ra4dat = *loval
     c                   eval      c2edat = 0
     c                   else
     c     *dmy          move      ra4dat        c2edat
     c                   endif
      *
     c                   else
     c                   eval      c24sam = 0
     c                   eval      c24sno = 0
     c                   eval      c24sug = 0
     c                   eval      c24sig = 0
     c                   eval      c24sjÅ = 0
6.10 c                   eval      c24spo = c14aar * 1000000
6.10 c                   eval      c24avk = c14aar * 1000000
     c                   eval      c24sgb = 0
     c***                eval      c24sub = 0
     c***                eval      c24sln = 0
     c***                eval      c24suf = 0
     c                   eval      c24sif = 0
     c                   eval      c2eusr = *blank
     c                   eval      c2edat = 0
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c14aar = k14aar
6.10 c                   eval      c24spo = c14aar * 1000000
6.10 c                   eval      c24avk = c14aar * 1000000
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c2taga
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   raa4lur
      *
     c                   eval      raa4lu_4aar = c14aar
     c     raa4lu_key    chain     raa4lur
      *
     c                   eval      ra4sam = c24sam
     c                   eval      ra4sno = c24sno
     c                   eval      ra4sug = c24sug
     c                   eval      ra4sig = c24sig
     c                   eval      ra4sjÅ = c24sjÅ
6.10 c                   eval      ra4spo = c24spo
6.10 c                   eval      ra4avk = c24avk
     c                   eval      ra4sgb = c24sgb
     c***                eval      ra4sub = c24sub
     c***                eval      ra4sln = c24sln
     c***                eval      ra4suf = c24suf
     c                   eval      ra4sif = c24sif
     c                   eval      ra4knr = 0
     c                   eval      ra4lnr = 0
      *
     c                   time                    ra4dat
     c                   time                    ra4tim
     c                   eval      ra4usr = l_user
      *
     c                   if        %found
     c                   update    raa4lur
     c                   else
     c                   eval      ra4fir = w_firm
     c                   eval      ra4aar = c14aar
     c                   write     raa4lur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      c14aar = 0
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C3BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc3bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      raa4lr_4aar = 0
     c     raa4lr_key    chain     raa4lr
      *
     c                   if        %found
     c                   eval      c34knr = ra4knr
     c                   eval      c34lnr = ra4lnr
     c                   eval      c34pnr = ra4pnr
     c                   eval      c34krt = ra4krt
     c                   eval      c3eusr = ra4usr
     c     *dmy          move      ra4dat        c3edat
     c                   else
     c                   eval      c34knr = 0
     c                   eval      c34lnr = 0
     c                   eval      c34pnr = 0
     c                   eval      c34krt = 0
     c                   eval      c3eusr = *blank
     c                   eval      c3edat = 0
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c3taga        tag
      *
     c                   exfmt     c3bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c3bld
     c                   endif
      *
      * Sjekk mot grenser
      *
     c                   if        c34knr > ra1hrs or
     c                             c34knr <= ra1hkt
     c                   eval      *in31 = *on                                  Pos.cur.
     c                   goto      c3taga
     c                   endif
      *
     c                   if        c34lnr < ra1hrs
     c                   eval      *in32 = *on                                  Pos.cur.
     c                   goto      c3taga
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   raa4lur
      *
     c                   eval      raa4lu_4aar = 0
     c     raa4lu_key    chain     raa4lur
      *
     c                   eval      ra4sam = 0
     c                   eval      ra4sno = 0
     c                   eval      ra4sug = 0
     c                   eval      ra4sig = 0
     c                   eval      ra4sjÅ = 0
     c                   eval      ra4sgb = 0
     c***                eval      ra4sub = 0
     c***                eval      ra4sln = 0
     c***                eval      ra4suf = 0
      *
     c                   move      c34knr        ra4knr
     c                   move      c34lnr        ra4lnr
     c                   move      c34pnr        ra4pnr
     c                   move      c34krt        ra4krt
      *
     c                   time                    ra4dat
     c                   time                    ra4tim
     c                   eval      ra4usr = l_user
      *
     c                   if        %found
     c                   update    raa4lur
     c                   else
     c                   eval      ra4fir = w_firm
     c                   eval      ra4aar = 0
     c                   write     raa4lur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b_oppr = *on
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c3bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      raa4lr_4aar = d14aar
     c     raa4lr_key    chain     raa4lr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      raa4lu_4aar = d14aar
     c     raa4lu_key    chain     raa4lur
      *
     c                   delete    raa4lur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_4aar = k14aar
      *
      * Fyller kopierings-bildet
      *
     c                   eval      raa4lr_4aar = k14aar
     c     raa4lr_key    chain     raa4lr
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k14aar = 0
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      raa4lr_4aar = k14aar
     c     raa4lr_key    chain     raa4lr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c24aar = k14aar
     c                   eval      c14aar = s_4aar
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      raa4l1
      *
     c                   if        %eof or
     c                             ra4fir <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b14aar = ra4aar
      *
     c                   if        ra4aar = 0
     c                   eval      b14txt = 'Kunde/Leverandør '
     c                   else
     c                   eval      b14txt = 'Årlig nummerserie'
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     raa4l1_key    setgt     raa4l1
     c                   readp     raa4l1
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     raa4l1
      *
     c                   if        %eof or
     c                             ra4fir <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   eval      raa4l1_4aar = ra4aar
      *
     c                   enddo
      *
     c                   if        %eof
     c     raa4l1_key    setll     raa4l1
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for posisjonering på rabattgruppe
      *
     c     raa4l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    raa4l1_4aar
      *
      * Key for les på rabattgruppe
      *
     c     raa4lr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    raa4lr_4aar
      *
      * Key for oppdatering av rabattgruppe
      *
     c     raa4lu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    raa4lu_4aar
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
      *
      * Key for oppslag på statuskode 1
      *
     c     raa1l1_key    klist
     c                   kfld                    w_firm
      *
      * Henter statuskode 1
      *
     c     raa1l1_key    chain     raa1l1                             90
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      raa4l1_4aar = 0
     c     raa4l1_key    setll     raa4l1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
