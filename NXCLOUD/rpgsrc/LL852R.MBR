     h decedit ('0,') datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LL852R
      *  Beskrivelse . . : Uttrekk salg- & kjøpsstat til Excel v.2
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       121102 GRO  Nytt program
      *       150404 GRO  Endr. ifm varegruppe og rapportheading (NBH)
5.70  *       021208 bof  Utvidet med prisgruppe
6.10  * R6.10 201109 bsa  Overføring til VL712R feiler
6.21  * 6.20  190511 bof  Utvidet vl710r med div. info
6.30  * 6.30  170918 bkv  Utvidet med trelast sortiment
8.01  *PRG2   150622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     fsstal2    if   e           k disk    rename(sstapfr:sstal2r)
     flwexpf    o  a e           k disk    rename(lexcpfr:lexcpfr)
6.30 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
      * Definering av arrays
      *
      * Antalls- & beløpsmatrise
     d a_saar          s              4  0 dim(36)
     d a_smnt          s              2  0 dim(36)
     d a_smtx          s              8    dim(36)
     d a_psal          s              8  1 dim(36)
     d a_psalb         s             11  2 dim(36)
     d a_prtx          s              8    dim(36)
      *
      *  Local Data Area
      *
     d                uds
     d l_firm                944    946  0
      *
      *  Parameter-record
      *
     d                 ds
     d p_prec                        55
     d  p_fvar                        8  0 overlay(p_prec)
     d  p_tvar                        8  0 overlay(p_prec:9)
     d  p_flag                        2  0 overlay(p_prec:17)
     d  p_tlag                        2  0 overlay(p_prec:19)
     d  p_fogr                        2  0 overlay(p_prec:21)
     d  p_togr                        2  0 overlay(p_prec:23)
     d  p_fhgr                        2  0 overlay(p_prec:25)
     d  p_thgr                        2  0 overlay(p_prec:27)
     d  p_fugr                        3  0 overlay(p_prec:29)
     d  p_tugr                        3  0 overlay(p_prec:32)
     d  p_anbl                        1  0 overlay(p_prec:35)
     d  p_kjop                        1  0 overlay(p_prec:36)
     d  p_snul                        1  0 overlay(p_prec:37)
     d  p_blan                        1  0 overlay(p_prec:38)
     d  p_star                        4  0 overlay(p_prec:39)
     d  p_stmn                        2  0 overlay(p_prec:43)
     d  p_slar                        4  0 overlay(p_prec:45)
     d  p_slmn                        2  0 overlay(p_prec:49)
     d  p_favd                        4  0 overlay(p_prec:51)
     d  p_vtyp                        1    overlay(p_prec:55)
      *
      * Definering av variable i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vlagl1_lage     s                   like(vllage)
     d vlagl1_vare     s                   like(vlvare)
     d sstal2_vare     s                   like(sfvare)
     d sstal2_paar     s                   like(sfpaar)
     d sstal2_bida     s                   like(sfbida)
      *
      * Definering av outputrecord
      *
     d d_orec          s           1024
      *
      *   Definering av øvrige variable
      *
     d w_firm          s                   like(vvfirm)
     d s_vare          s                   like(vvvare)
     d s_tek1          s                   like(vvtek1)
     d s_tek2          s                   like(vvtek2)
     d s_enhe          s                   like(sfenhe)
5.70 d w_vtyp          s                   like(vvvtyp)
     d s_lety          s                   like(sflety)
6.10 d s_lage          s                   like(vllage)
6.10 d w_lage          s              2
6.21 d w_udat          s                   like(vvudat)
6.21 d w_ldor          s                   like(vvldor)
6.21 d b_inlr          s              1    inz(*off)
6.30 d w_lv_nobb       s                   like(vvlnob)
6.30 d w_lv_modn       s                   like(vvlmod)
6.30 d w_lv_lldo       s                   like(vvlnle)
6.30 d w_lv_llva       s                   like(vvllva)
     d s_avde          s              4
     d s_ogrp          s              2
     d s_hgrp          s              2
     d s_ugrp          s              3
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_dmnd          s              2  0
     d w_aaar          s              4
     d w_daar          s              4  0
     d w_faar          s              4  0
     d w_mtel          s              2  0
     d w_fvar          s             15
     d w_tvar          s             15
     d w_parm          s             55
     d w_tmnt          s              4  0
     d w_caar          s              4  0
     d w_cmnt          s              2  0
     d w_cidx          s              2  0
     d w_psal          s              8  1 dim(3)
     d w_pkjo          s              8  1 dim(3)
     d w_psalb         s             11  2 dim(3)
     d w_pkjob         s             11  2 dim(3)
     d w_bidx          s             10
     d w_bida          s                   like(sfbida)
     d w_aant          s             10
     d w_belp          s             13
     d w_star          s              4
     d w_stmn          s              2
     d w_sapr          s              9  2
     d w_bupr          s              9  2
     d w_tipr          s              9  2
     d w_kopr          s              9  2
     d w_behl          s             11  3
     d w_lvrd          s             11  3
     d w_inlr          s              1
5.70 d w_avde          s              4  0
8.01 d w_prgr          s              2
      *
      * Salgsvariable - antall
     d w_mxsa          s              8  1
     d w_misa          s              8  1
     d w_snsa          s              8  1
     d w_ttsa          s              8  1
     d w_tts1          s              8  1
     d w_tts2          s              8  1
     d w_tts3          s              8  1
     d w_mxsx          s             10
     d w_misx          s             10
     d w_snsx          s             10
     d w_ttsx          s             10
      *
      * Kjøpsvariable - antall
     d w_mxkj          s              8  1
     d w_mikj          s              8  1
     d w_snkj          s              8  1
     d w_ttkj          s              8  1
      *
      * Salgsvariable - beløp
     d w_mxsab         s             11  2
     d w_misab         s             11  2
     d w_snsab         s             11  2
     d w_ttsab         s             11  2
     d w_tts1b         s             11  2
     d w_tts2b         s             11  2
     d w_tts3b         s             11  2
      *
      * Boolske variable/"brytere"
     d b_numf          s              1    inz(*off)
     d b_head          s              1    inz(*off)
     d b_parm          s              1    inz(*off)
     d b_found         s              1    inz(*off)
      *
      *  Konstanter
     d c_fsep          c                   ';'
     d c_mmax          c                   36
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   exsr      lag_hdng
      *
     c     sstal2_key    setll     sstal2
     c                   read      sstal2
     c                   dow       not %eof and
     c                             sfvare <= w_tvar
      *
     c                   exsr      behandling
      *
     c                   read      sstal2
     c                   enddo
      *
     c                   if        b_numf = *on
     c                   exsr      skriv_vlin
     c                   endif
      *
     c                   eval      exclin = *blank
     c                   write     lexcpfr
     c                   write     lexcpfr
      *
     c                   eval      d_orec = *blank
     c                   eval      d_orec = 'Utplukk - Varenr.:' +
     c                                      %char(p_fvar) + '-' +
     c                                      %char(p_tvar) + '   Lager/Avd.:' +
     c                                      %char(p_flag) + '/' +
     c                                      %char(p_favd) +
     c                                      '   Varegrp.:' +
     c                                      %char(p_fogr) + ' ' +
     c                                      %char(p_fhgr) + ' ' +
     c                                      %char(p_fugr) + ' - ' +
     c                                      %char(p_togr) + ' ' +
     c                                      %char(p_thgr) + ' ' +
     c                                      %char(p_tugr)
      *
     c                   if        p_vtyp <> *blank
     c                   eval      d_orec = %trim(d_orec) +
     c                                      '  Varetype: ' + p_vtyp
     c                   else
     c                   eval      d_orec = %trim(d_orec) +
     c                                      '  Varetype: Alle'
     c                   endif
      *
     c                   if        p_kjop > 0
     c                   eval      d_orec = %trim(d_orec) + '  Salg/kjøp'
     c                   else
     c                   eval      d_orec = %trim(d_orec) + '  Salg'
     c                   endif
      *
     c                   if        p_anbl > 0
     c                   eval      d_orec = %trim(d_orec) + ' - Beløp'
     c                   else
     c                   eval      d_orec = %trim(d_orec) + ' - Antall'
     c                   endif
      *
     c                   eval      exclin = d_orec
     c                   write     lexcpfr
      *
     c                   eval      w_inlr = ' '
     c                   exsr      hent_kpris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å behandling av statistikk-post
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
     c                   if        sfbida < w_bida
     c                   leavesr
     c                   endif
      *
     c                   if        sfvare <> vvvare
     c                   eval      vvarl1_vare = sfvare
     c     vvarl1_key    chain     vvarl1r
     c                   if        not %found
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        p_vtyp <> *blank
      *
      * henter varetype
      *
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    sfvare
5.70 c                   parm                    sflage
5.70 c                   parm                    w_vtyp
6.21 c                   parm                    w_udat
6.21 c                   parm                    w_ldor
6.21 c                   parm                    b_inlr
6.30 c                   parm                    w_lv_nobb
6.30 c                   parm                    w_lv_modn
6.30 c                   parm                    w_lv_lldo
6.30 c                   parm                    w_lv_llva
      *
5.70 c                   if        w_vtyp <> p_vtyp
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   exsr      sjekk_param
     c                   if        b_parm = *off
     c                   leavesr
     c                   endif
      *
     c                   if        sfvare <> s_vare and
     c                             s_vare <> *blank
     c                   exsr      skriv_vlin
     c                   endif
      *
      * Finn igjen riktig "kolonne" ihht år/måned
     c                   eval      w_cidx = 0
     c                   eval      b_found = *off
     c                   dow       w_cidx < c_mmax and
     c                             b_found = *off
     c                   eval      w_cidx = w_cidx + 1
     c                   if        a_saar(w_cidx) = sfpaar and
     c                             a_smnt(w_cidx) = sfpmnd
     c                   eval(h)   a_psal(w_cidx) = a_psal(w_cidx) + sfanta
     c                   eval      a_psalb(w_cidx) = a_psalb(w_cidx) +
     c                             (sfsumb - (sfrab1 + sfrab2 + sfrabo))
     c                   eval      b_found = *on
     c                   eval      b_numf  = *on
     c                   endif
     c                   enddo
      *
     c                   eval      s_vare = sfvare
     c                   eval      s_tek1 = vvtek1
     c                   eval      s_tek2 = vvtek2
     c                   eval      s_enhe = sfenhe
     c                   eval      s_lety = sflety
     c                   move      sflage        s_lage
     c                   move      sfsavd        s_avde
     c                   move      vvogrp        s_ogrp
     c                   move      vvhgrp        s_hgrp
     c                   move      vvugrp        s_ugrp
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for skriving av output-record v/brudd
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_vlin    begsr
      *
     c                   eval      d_orec = %trim(s_vare) + c_fsep +
     c                                      %trim(s_tek1)
     c                   if        s_tek2 <> *blank
     c                   eval      d_orec = %trim(d_orec) + ' ' +
     c                                      %trim(s_tek2) + c_fsep
     c                   else
     c                   eval      d_orec = %trim(d_orec) + c_fsep
     c                   endif
      *
     c                   eval      d_orec = %trim(d_orec) +
     c*                                     s_ogrp + ' ' +
     c                                      s_ogrp + c_fsep +
     c*                                     s_hgrp + ' ' +
     c                                      s_hgrp + c_fsep +
     c                                      s_ugrp + c_fsep
      *
6.10 c                   move      s_lage        w_lage
6.10 c                   eval      d_orec = %trim(d_orec) + w_lage +
     c                                      c_fsep + s_avde + c_fsep
      *
      * Henter kostpris fra vareregisteret
      *
     c                   eval      w_inlr = '1'
     c                   exsr      hent_kpris
      *
      * Henter lagerbeholdning
      *
     c                   eval      vlagl1_vare = s_vare
     c                   move      s_lage        vlagl1_lage
     c     vlagl1_key    chain     vlagl1
     c                   if        %found
     c                   eval      w_behl = vlbehl
     c                   else
     c                   eval      w_behl = 0
     c                   endif
      *
      * Summering av totaler
      *
     c                   eval      w_mxsa = 0
     c                   eval      w_mxsab = 0
     c                   eval      w_misa = 0
     c                   eval      w_misab = 0
     c                   eval      w_snsa = 0
     c                   eval      w_snsab = 0
     c                   eval      w_ttsa = 0
     c                   eval      w_ttsab = 0
     c                   eval      w_tts1 = 0
     c                   eval      w_tts1b = 0
     c                   eval      w_tts2 = 0
     c                   eval      w_tts2b = 0
     c                   eval      w_tts3 = 0
     c                   eval      w_tts3b = 0
      *
     c                   eval      w_cidx = 0
     c                   dow       w_cidx < w_mtel
     c                   eval      w_cidx = w_cidx + 1
     c                   eval      w_ttsa = w_ttsa + a_psal(w_cidx)
     c                   eval      w_ttsab = w_ttsab + a_psalb(w_cidx)
     c                   if        w_cidx < 13
     c                   eval      w_tts1 = w_tts1 + a_psal(w_cidx)
     c                   eval      w_tts1b = w_tts1b + a_psalb(w_cidx)
     c                   endif
     c                   if        w_cidx > 12 and
     c                             w_cidx < 25
     c                   eval      w_tts2 = w_tts2 + a_psal(w_cidx)
     c                   eval      w_tts2b = w_tts2b + a_psalb(w_cidx)
     c                   endif
     c                   if        w_cidx > 24
     c                   eval      w_tts3 = w_tts3 + a_psal(w_cidx)
     c                   eval      w_tts3b = w_tts3b + a_psalb(w_cidx)
     c                   endif
     c                   if        a_psal(w_cidx) > w_mxsa
     c                   eval      w_mxsa = a_psal(w_cidx)
     c                   endif
     c                   if        a_psal(w_cidx) < w_misa
     c                   eval      w_misa = a_psal(w_cidx)
     c                   endif
     c                   if        a_psalb(w_cidx) > w_mxsab
     c                   eval      w_mxsab = a_psalb(w_cidx)
     c                   endif
     c                   if        a_psalb(w_cidx) < w_misab
     c                   eval      w_misab = a_psalb(w_cidx)
     c                   endif
     c                   enddo
     c                   eval(h)   w_snsa = w_ttsa / w_mtel
     c                   eval(h)   w_snsab = w_ttsab / w_mtel
      *
      * > Høyeste verdi
     c                   eval      w_aant = %editc(w_mxsa : 'P')
     c                   eval      w_belp = %editc(w_mxsab : 'P')
     c                   exsr      skr_anblp
      *
      * > Minste verdi
     c                   eval      w_aant = %editc(w_misa : 'P')
     c                   eval      w_belp = %editc(w_misab : 'P')
     c                   exsr      skr_anblp
      *
      * > Gjennomsnitt
     c                   eval      w_aant = %editc(w_snsa : 'P')
     c                   eval      w_belp = %editc(w_snsab : 'P')
     c                   exsr      skr_anblp
      * > Kostpris
     c                   eval      d_orec = %trim(d_orec) +
     c                                      %char(w_kopr) + c_fsep
      * > Lagerbeholdning
     c                   eval      d_orec = %trim(d_orec) +
     c                                      %char(w_behl) + c_fsep
      * > Lagerverdi
     c                   eval(h)   w_lvrd = w_behl * w_kopr
     c                   eval      d_orec = %trim(d_orec) +
     c                                      %char(w_lvrd) + c_fsep
      * > Grand-total
     c                   eval      w_aant = %editc(w_ttsa : 'P')
     c                   eval      w_belp = %editc(w_ttsab : 'P')
     c                   exsr      skr_anblp
      *
      * > Periodiske totaler
     c                   eval      w_aant = %editc(w_tts1 : 'P')
     c                   eval      w_belp = %editc(w_tts1b : 'P')
     c                   exsr      skr_anblp
      *
     c                   if        w_mtel > 12
     c                   eval      w_aant = %editc(w_tts2 : 'P')
     c                   eval      w_belp = %editc(w_tts2b : 'P')
     c                   exsr      skr_anblp
     c                   endif
      *
     c                   if        w_mtel > 24
     c                   eval      w_aant = %editc(w_tts3 : 'P')
     c                   eval      w_belp = %editc(w_tts3b : 'P')
     c                   exsr      skr_anblp
     c                   endif
      *
      * > Månedsantall/beløp enkeltvis
     c                   eval      w_cidx = 0
     c                   dow       w_cidx < w_mtel
     c                   eval      w_cidx = w_cidx + 1
     c                   eval      w_aant =
     c                             %editc(a_psal(w_cidx) : 'P')
     c                   eval      w_belp =
     c                             %editc(a_psalb(w_cidx) : 'P')
     c                   exsr      skr_anblp
     c                   enddo
      *
     c                   if        p_blan > 0
     c                   eval      exclin = *blank
     c                   write     lexcpfr
     c                   endif
      *
     c                   eval      exclin = d_orec
     c                   write     lexcpfr
      *
     c                   eval      a_psal = 0
     c                   eval      a_psalb = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for kall til vareprisberegning
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_kpris    begsr
      *
5.70 c                   exsr      hent_prisgr
      *
     c                   call      'VP700R'
     c                   parm                    w_firm
     c                   parm                    s_vare
5.70 c                   parm                    w_prgr
     c                   parm                    s_enhe
     c                   parm                    s_lety
     c                   parm                    w_date
     c                   parm                    w_time
     c                   parm                    w_inlr
     c                   parm                    w_sapr
     c                   parm                    w_bupr
     c                   parm                    w_tipr
     c                   parm                    w_kopr
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for plassering av antall/beløp i output-record
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_anblp     begsr
      *
     c                   if        p_anbl > 0
     c                   eval      d_orec = %trim(d_orec) +
     c                                      %trim(w_belp) + c_fsep
     c                   else
     c                   eval      d_orec = %trim(d_orec) +
     c                                      %trim(w_aant) + c_fsep
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finne redigere månedsheading
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lag_hdng      begsr
      *
      * Finn start måned og år
      *
     c                   extrct    w_date:*m     w_dmnd
     c                   extrct    w_date:*y     w_daar
     c                   eval      w_dmnd = w_dmnd - 1
     c                   eval      w_faar = w_daar - 1
     c                   move      w_faar        w_aaar
      *
     c                   eval      w_tmnt = ((p_slar - p_star) - 1) * 12
     c                   eval      w_tmnt = w_tmnt +
     c                                      ((12 - p_stmn) + 1) + p_slmn
     c                   if        w_tmnt < 1 or
     c                             w_tmnt > c_mmax
     c                   eval      w_tmnt = c_mmax
     c                   endif
      *
     c                   exsr      lag_mhtxt
      *
      * Lager kolonneoverskriftslinje
      *
     c                   eval      w_cidx = 0
     c                   eval      w_mtel = 0
     c                   dow       w_cidx < c_mmax
     c                   eval      w_cidx = w_cidx + 1
     c                   if        a_smtx(w_cidx) <> *blank
     c                   eval      w_mtel = w_mtel + 1
     c                   endif
     c                   enddo
      *
     c                   eval      d_orec = *blank
     c                   eval      d_orec = 'Varenr' + c_fsep +
     c                                      'V.navn' + c_fsep +
     c*                                     'Varegrp' + c_fsep +
     c                                      'Ogrp' + c_fsep +
     c                                      'Hgrp' + c_fsep +
     c                                      'Ugrp' + c_fsep +
     c                                      'Lager' + c_fsep +
     c                                      'Avdeling' + c_fsep +
     c                                      'Høyest' + c_fsep +
     c                                      'Lavest' + c_fsep +
     c                                      'Gj.snitt' + c_fsep +
     c                                      'Kostpris' + c_fsep +
     c                                      'Lagerbeh.' + c_fsep +
     c                                      'Lagerverdi' + c_fsep +
     c                                      'Totalt' + c_fsep +
     c                                      'Totalt 1-12' + c_fsep
      *
     c                   if        w_mtel > 12
     c                   eval      d_orec = %trim(d_orec) +
     c                                      'Totalt 13-24' + c_fsep
     c                   endif
     c                   if        w_mtel > 24
     c                   eval      d_orec = %trim(d_orec) +
     c                                      'Totalt 25-36' + c_fsep
     c                   endif
      *
     c                   eval      w_cidx = 0
     c                   dow       w_cidx < w_mtel
     c                   eval      w_cidx = w_cidx + 1
     c                   if        a_prtx(w_cidx) <> *blank
     c                   eval      d_orec = %trim(d_orec) +
     c                                      %trim(a_prtx(w_cidx)) + c_fsep
     c                   endif
     c                   enddo
      *
     c                   eval      exclin = d_orec
     c                   write     lexcpfr
      *
     c                   eval      d_orec = *blank
     c                   eval      d_orec = c_fsep +
     c                                      c_fsep +
     c                                      c_fsep +
     c                                      c_fsep +
     c                                      c_fsep +
     c                                      c_fsep +
     c                                      c_fsep +
     c                                      c_fsep +
     c                                      c_fsep +
     c                                      c_fsep +
     c                                      c_fsep +
     c                                      c_fsep +
     c                                      c_fsep +
     c                                      c_fsep
      *
     c                   if        w_mtel > 12
     c                   eval      d_orec = %trim(d_orec) +
     c                                      c_fsep
     c                   endif
     c                   if        w_mtel > 24
     c                   eval      d_orec = %trim(d_orec) +
     c                                      c_fsep
     c                   endif
      *
     c                   eval      w_cidx = 0
     c                   dow       w_cidx < w_mtel
     c                   eval      w_cidx = w_cidx + 1
     c                   if        a_smtx(w_cidx) <> *blank
     c                   eval      d_orec = %trim(d_orec) +
     c                                      %trim(a_smtx(w_cidx)) + c_fsep
     c                   endif
     c                   enddo
      *
     c                   eval      b_head = *on
      *
     c                   eval      exclin = d_orec
     c                   write     lexcpfr
     c                   eval      exclin = *blank
     c                   write     lexcpfr
      *
     c                   movel     p_fvar        w_fvar
     c                   movel     p_tvar        w_tvar
     c                   eval      sstal2_vare = w_fvar
     c                   eval      sstal2_paar = p_star
     c                   eval      sstal2_bida = w_bida
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å sette månedsnavn/år i header
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lag_mhtxt     begsr
      *
     c                   move      p_star        w_star
     c                   move      p_stmn        w_stmn
     c                   eval      w_bidx = w_star + '-' + w_stmn + '-01'
     c                   movel     w_bidx        w_bida
      *
     c                   eval      w_caar = p_slar
     c                   eval      w_cmnt = p_slmn
     c                   eval      w_cidx = 0
      *
     c                   dow       w_cidx < w_tmnt
     c                   eval      w_cidx = w_cidx + 1
     c                   eval      a_saar(w_cidx) = w_caar
     c                   eval      a_smnt(w_cidx) = w_cmnt
     c                   eval      a_prtx(w_cidx) = 'Per-' + %char(w_cidx)
     c                   select
     c                   when      w_cmnt = 1
     c                   eval      a_smtx(w_cidx) = 'Jan-' + %char(w_caar)
     c                   when      w_cmnt = 2
     c                   eval      a_smtx(w_cidx) = 'Feb-' + %char(w_caar)
     c                   when      w_cmnt = 3
     c                   eval      a_smtx(w_cidx) = 'Mar-' + %char(w_caar)
     c                   when      w_cmnt = 4
     c                   eval      a_smtx(w_cidx) = 'Apr-' + %char(w_caar)
     c                   when      w_cmnt = 5
     c                   eval      a_smtx(w_cidx) = 'Mai-' + %char(w_caar)
     c                   when      w_cmnt = 6
     c                   eval      a_smtx(w_cidx) = 'Jun-' + %char(w_caar)
     c                   when      w_cmnt = 7
     c                   eval      a_smtx(w_cidx) = 'Jul-' + %char(w_caar)
     c                   when      w_cmnt = 8
     c                   eval      a_smtx(w_cidx) = 'Aug-' + %char(w_caar)
     c                   when      w_cmnt = 9
     c                   eval      a_smtx(w_cidx) = 'Sep-' + %char(w_caar)
     c                   when      w_cmnt = 10
     c                   eval      a_smtx(w_cidx) = 'Okt-' + %char(w_caar)
     c                   when      w_cmnt = 11
     c                   eval      a_smtx(w_cidx) = 'Nov-' + %char(w_caar)
     c                   when      w_cmnt = 12
     c                   eval      a_smtx(w_cidx) = 'Des-' + %char(w_caar)
     c                   endsl
      *
     c                   if        w_cmnt = 1
     c                   eval      w_cmnt = 12
     c                   eval      w_caar = w_caar - 1
     c                   else
     c                   eval      w_cmnt = w_cmnt - 1
     c                   endif
      *
     c                   enddo
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for sjekk av parameterverdier
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_param   begsr
      *
     c                   eval      b_parm = *on
      *
     c                   movel     p_fvar        w_fvar
     c                   movel     p_tvar        w_tvar
      *
     c                   if        sfvare < w_fvar or
     c                             sfvare > w_tvar
     c                   eval      b_parm = *off
     c                   leavesr
     c                   endif
      *
     c                   if        p_flag <> 0
     c                   if        sflage <> p_flag
     c                   eval      b_parm = *off
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        p_favd <> 0
     c                   if        sfsavd <> p_favd
     c                   eval      b_parm = *off
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        vvogrp < p_fogr or
     c                             vvogrp > p_togr
     c                   eval      b_parm = *off
     c                   leavesr
     c                   endif
      *
     c                   if        vvhgrp < p_fhgr or
     c                             vvhgrp > p_thgr
     c                   eval      b_parm = *off
     c                   leavesr
     c                   endif
      *
     c                   if        vvugrp < p_fugr or
     c                             vvugrp > p_tugr
     c                   eval      b_parm = *off
     c                   leavesr
     c                   endif
      *
     c                   endsr
5 70  *
5.70  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5 70  * Subrutine for å hente prisgruppe
5.70  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.70  *
5.70 c     hent_prisgr   begsr
5.70  *
5.70 c                   eval      w_avde = 0
5.70 c                   call      'VL712R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    s_lage
5.70 c                   parm                    w_avde
5.70 c                   parm                    w_prgr
5.70  *
5.70 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for les av VARE
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for les av salgsstatistikk
      *
     c     sstal2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sstal2_vare
     c                   kfld                    sstal2_paar
     c                   kfld                    sstal2_bida
      *
      *  Key for les av LAGER
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl1_vare
     c                   kfld                    vlagl1_lage
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    w_parm
     c                   eval      p_prec = w_parm
      *
      *  Legge inn firmanummer i nøklene
      *
     c                   eval      w_firm = l_firm
     c                   time                    w_date
     c                   time                    w_time
      *
     c                   endsr
