     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : NO751R
      * Beskrivelse . . : Ekstern ordre-hode, henter BA-kalk fil (SDV-fil)
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       280705 bø   Nytt program
      *       070506 HKO  Leser fra definert input-fil
      *       130808 bof  bruker enhetskonvertering-register
      * 6.10  250609 BSA  Oppdatert med sporingsinformasjon
6.nu  * R6.30 170614 bof  Programmet gjennomgått mtp nummerutvidelser.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fneowpf    if   e           k disk    rename(neowpfr:neowpfr)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fnkeol1    if   e           k disk    rename(nkeopfr:nkeol1r)
     ffenklr    if   e           k disk    rename(fenkpfr:fenklrr)
     fnohelu    uf a e           k disk    rename(nohepfr:nohelur)
     fnodtpf    o  a e             disk
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_kund          s              6  0
     d p_kpro          s              6  0
     d p_lety          s              1  0
     d p_mapp          s             50
     d p_file          s             50
     d p_feil          s              1
      *
      * Definering av variabler
      *
     d ar              s              3  0 DIM(26)
     d w_bla           s              3  0 DIM(3)
      *
     d w_tkod          s              1    inz('0')
     d w_ttyp          s              2    inz(*blank)
     d w_fast          s             10    inz(*blank)
6.nu d w_numm          s              8  0
      *
     d x_behk          s              1    dim(26) ctdata perrcd(1)
     d x_behf          s              6    dim(26) alt(x_behk)
     d wfeil           s              1    inz(*off)
     d i               s              2  0
     d w_firm          s                   like(ndfirm)
     d w_line          s                   like(ndline)
     d w_fsys          s                   like(nofsys)
      *
      * Definering av variabler i nøkler
      *
     d nkeol1_kode     s                   like(nkkode)
     d rkunl1_kund     s                   like(rkkund)
     d fenklr_enhf     s                   like(feenhf)
      *
      *  Definere felter i bakalk
      *
     d w_prec          ds
     d w_rtyp                         1
     d w_kapi                         2
     d w_post                        18
     d w_prid                        18
     d w_upid                        18
     d w_kart                        10
     d w_vare                        20
     d w_besk                        70
     d w_enh1                         4
     d w_anta                         9  1
     d w_lngd                         9  3
     d w_mngd                         9  2
     d w_pris                         7  2
     d w_kost                        10  2
     d w_dkon                        12
     d w_dktx                        50
     d w_refk                         5
     d w_reft                        41
     d w_wbk1                        10
     d w_wbt1                        41
     d w_wbk2                        10
     d w_wbt2                        41
     d w_sdat                         8  0
     d w_edat                         8  0
     d w_adat                         8  0
     d w_nobb                         8
      *
     d w_alf           s            100
     d w_113           s             11  3
     d w_112           s             11  2
     d w_111           s             11  1
     d w_11x           s             11
     d w_11y           s             11
     d w_arf           s              3  0
     d w_art           s              3  0
     d w_arx           s              3
     d w_sts           s             20
      *
     d len             s              3  0
     d st              s              3  0
     d sl              s              3  0
      *
     d Lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d Up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      w_fsys = 'BAK'
      *
     c                   eval      p_feil = *off
      *
      * Kaller CL-program for innlesing av kundeordre
      *
     c                   call      'NO751C'
     c                   parm                    p_mapp
     c                   parm                    p_file
     c                   parm                    p_feil
      *
     c                   if        p_feil = *on
     c                   goto      avslutt
     c                   endif
      *
      * Oppretter ordre-hode
      *
     c                   exsr      ordre_hode
      *
     c                   if        p_feil = *on
     c                   goto      avslutt
     c                   endif
      *
      * Leser og oppretter ordre-linjer
      *
     c                   eval      w_line = 0
      *
     c                   eval      w_sts = *blank
     c                   clear                   w_prec
      *
     c                   eval      w_alf  = *blank
     c                   eval      w_113  = *zero
     c                   eval      w_112  = *zero
     c                   eval      w_111  = *zero
      *
     c                   read      neowpf
     c                   dow       not %eof
      *
     c     ';'           scan      nwirad        ar                       90
      ** behandler felt 1 - 3 (er alle innenfor semikolon)
     c     1             subst     nwirad:1      w_rtyp
     c     2             subst     nwirad:2      w_kapi
     c     4             subst     nwirad:16     w_post
      *
      * sjekker om det er type K
     c                   if        w_rtyp = 'K'
      *
      *  Behandling av Kalkyle linjeposter
      *
     c     2             subst     nwirad:1      w_rtyp
      *
      * felt 1 - 24
      *
     c                   for       w_arf = 1 to 24
     c                   eval      w_art = w_arf + 1
      *
     c                   eval      len     = ar(w_art) - ar(w_arf) - 1
     c                   eval      st      = ar(w_arf) + 1
     c                   if        ar(w_art) > 0 and
     c                             len       > 0
      *
      * Plukker ut neste felt
      *
     c                   eval      w_alf = *blank
     c     len           subst     nwirad:st     w_alf
      *
      * behandler felt avhengig av behandlingskode
      *
     c                   select
     c                   when      x_behk(w_art) = 'A'
     c                   exsr      behandl_alfa
     c                   when      x_behk(w_art) = '3'
     c                   exsr      behandl_num
     c                   when      x_behk(w_art) = '2'
     c                   exsr      behandl_num
     c                   when      x_behk(w_art) = '1'
     c                   exsr      behandl_num
     c                   when      x_behk(w_art) = '0'
     c                   exsr      behandl_num
     c                   endsl
      *
     c                   endif
      *
     c                   endfor
      *
      * tester om dette er et gyldig varenr.
      * kun varenr. som er siffer
     c                   if        %subst(w_vare:1:1) >= '0' and
     c                             %subst(w_vare:1:1) <= '9' and
     c                             %subst(w_vare:2:1) >= '0' and
     c                             %subst(w_vare:2:1) <= '9' and
     c                             %subst(w_vare:3:1) >= '0' and
     c                             %subst(w_vare:3:1) <= '9' and
     c                             %subst(w_vare:4:1) >= '0' and
     c                             %subst(w_vare:4:1) <= '9' and
     c                             %subst(w_vare:5:1) >= '0' and
     c                             %subst(w_vare:5:1) <= '9' and
     c                             %subst(w_vare:6:1) >= '0' and
     c                             %subst(w_vare:6:1) <= '9' and
     c                             %subst(w_vare:7:1) >= '0' and
     c                             %subst(w_vare:7:1) <= '9' and
     c                             %subst(w_vare:8:1) >= '0' and
     c                             %subst(w_vare:8:1) <= '9'
      *
      * Skriv detalj-linje til Ekstern ordre - detaljer                     ke m
      *
     c                   clear                   nodtpfr
     c                   eval      ndfirm = w_firm
     c                   eval      ndfsys = nofsys
     c                   eval      ndtell = notell
     c                   eval      ndnref = nonref
     c                   eval      w_line = w_line + 1
     c                   eval      ndline = w_line
     c                   movel     w_vare        ndvare
     c     Lo:Up         xlate     w_enh1        w_enh1
     c                   movel     w_enh1        fenklr_enhf
     c     fenklr_key    chain     fenklr
     c                   if        %found(fenklr)
     c                   eval      ndenhe = feenht
     c                   else
     c                   movel     w_enh1        ndenhe
     c                   endif
     c*                  movel     w_nobb        fdnobb
4.54 c     x'1A':'Æ'     xlate     w_besk        w_besk
4.42 c     x'46':'Æ'     xlate     w_besk        w_besk
4.42 c     x'9E':'Æ'     xlate     w_besk        w_besk
4.42 c     x'31':'æ'     xlate     w_besk        w_besk
4.42 c     x'A0':'æ'     xlate     w_besk        w_besk
4.54 c     x'BE':'æ'     xlate     w_besk        w_besk
4.54 c     x'14':'Ø'     xlate     w_besk        w_besk
4.54 c     x'1D':'Ø'     xlate     w_besk        w_besk
4.42 c     x'3B':'ø'     xlate     w_besk        w_besk
4.42 c     x'70':'Ø'     xlate     w_besk        w_besk
4.42 c     x'77':'Ø'     xlate     w_besk        w_besk
4.61 c     x'80':'Ø'     xlate     w_besk        w_besk
4.42 c     x'90':'ø'     xlate     w_besk        w_besk
4.42 c     x'E0':'Ø'     xlate     w_besk        w_besk
4.54 c     x'EE':'ø'     xlate     w_besk        w_besk
4.54 c     x'1B':'Å'     xlate     w_besk        w_besk
4.42 c     x'2A':'Å'     xlate     w_besk        w_besk
4.42 c     x'47':'Å'     xlate     w_besk        w_besk
4.63 c     x'67':'Å'     xlate     w_besk        w_besk
4.42 c     x'9F':'Å'     xlate     w_besk        w_besk
4.42 c     x'06':'å'     xlate     w_besk        w_besk
4.42 c     x'EF':'å'     xlate     w_besk        w_besk
4.42 c     x'09':'²'     xlate     w_besk        w_besk
4.42 c     x'17':'à'     xlate     w_besk        w_besk
     c                   movel     w_besk        ndtek1
      * i antall ligger det 0, i mengde ligger antallet, er det noen regler
      * for dette ?
     c                   eval      ndanta = w_mngd
     c                   eval      ndntpr = w_pris
     c                   eval      ndldat = *loval
      *
     c                   time                    ndodat
6.10 c                   time                    ndotim
6.10 c                   eval      ndousr = l_user
     c                   time                    ndedat
     c                   time                    ndetim
6.10 c                   eval      ndeusr = l_user
      *
     c                   write     nodtpfr
      *
     c                   endif
     c                   endif
      *
     c                   read      neowpf
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av alfanumeriske felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandl_alfa  begsr
      *
     c                   select
     c                   when      w_art = 2
     c                   movel     w_alf         w_prid
     c                   when      w_art = 3
     c                   movel     w_alf         w_upid
     c                   when      w_art = 4
     c                   movel     w_alf         w_kart
     c                   when      w_art = 5
     c                   movel     w_alf         w_vare
     c                   when      w_art = 6
     c                   movel     w_alf         w_besk
     c                   when      w_art = 7
     c                   movel     w_alf         w_enh1
     c                   when      w_art = 13
     c                   movel     w_alf         w_dkon
     c                   when      w_art = 14
     c                   movel     w_alf         w_dktx
     c                   when      w_art = 15
     c                   movel     w_alf         w_refk
     c                   when      w_art = 16
     c                   movel     w_alf         w_reft
     c                   when      w_art = 17
     c                   movel     w_alf         w_wbk1
     c                   when      w_art = 18
     c                   movel     w_alf         w_wbt1
     c                   when      w_art = 19
     c                   movel     w_alf         w_wbk2
     c                   when      w_art = 20
     c                   movel     w_alf         w_wbt2
     c                   when      w_art = 24
     c                   movel     w_alf         w_nobb
     c                   endsl
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av numeriske felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandl_num   begsr
      *
      *
     c                   if        len     > 0
     c                   eval      st = 12 - len
     c                   eval      w_11x = *blank
     c                   eval      %subst(w_11x:st:len) = %subst(w_alf:1:len)
     c                   move      w_11x         w_111
     c                   move      w_11x         w_112
     c                   move      w_11x         w_113
     c                   else
     c                   eval      w_111 = *zero
     c                   eval      w_112 = *zero
     c                   eval      w_113 = *zero
     c                   endif
      *
     c                   select
      *  Numeriske felter
     c                   when      w_art = 8
     c                   eval      w_anta = w_111
     c                   when      w_art = 9
     c                   eval      w_lngd = w_113
     c                   when      w_art = 10
     c                   eval      w_mngd = w_112
     c                   when      w_art = 11
     c                   eval      w_pris = w_112
     c                   when      w_art = 12
     c                   eval      w_kost = w_112
     c                   endsl
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Error rutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *pssr         begsr
      *
     c                   eval      p_feil = *on
      *
     c                   goto      avslutt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppretter ordre-hode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ordre_hode    begsr
      *
      * Skriver til ekstern ordre-hode
      *
     c                   eval      nofirm = w_firm
5.10 c                   eval      nofsys = w_fsys
5.10 c                   eval      notell = 0
     c                   eval      nonref = *blank
     c                   eval      nofrsp = 0
     c                   eval      nolage = 0
     c                   eval      nouser = *blank
     c                   eval      nokund = p_kund
      *
      * Henter kundeinformasjon
      *
     c                   eval      rkunl1_kund = p_kund
     c     rkunl1_key    chain     rkunl1
     c                   if        %found
     c                   eval      noknav = rknavn
     c                   eval      nokpro = 0
     c                   eval      nopnav = *blank
     c                   eval      nonavn = rknavn
     c                   eval      noadr1 = rkgate
     c                   eval      noadr2 = rkadr2
     c                   movel     rkponr        noponr
     c                   eval      nosted = rksted
     c                   else
     c                   eval      noknav = *blank
     c                   eval      nokpro = 0
     c                   eval      nopnav = *blank
     c                   eval      nonavn = *blank
     c                   eval      noadr1 = *blank
     c                   eval      noadr2 = *blank
     c                   movel     *blank        noponr
     c                   endif
      *
     c                   eval      nolety = p_lety
     c                   eval      noldat = *loval
     c                   eval      nodref = *blank
     c                   eval      norekv = *blank
     c                   eval      nomeld = *blank
     c                   eval      nounav = *blank
     c                   eval      noutlf = *blank
     c                   eval      noumob = *blank
     c                   eval      noufax = *blank
     c                   eval      noumai = *blank
     c                   eval      nonumm = 0
     c                   eval      noeusr = l_user
      *
     c                   time                    nobdat
     c                   time                    noodat
     c                   time                    nootim
     c                   time                    noedat
     c                   time                    noetim
      *
     c                   move      *blank        nonref
     c                   move      0             nolage
      *
     c                   if        nonavn = *blank and
     c                             noadr1 <> *blank
     c                   eval      nonavn = noadr1
     c                   eval      noadr1 = *blank
     c                   endif
      *
      * Henter neste nummer fra nummer-serie
      *
     c                   eval      nkeol1_kode = w_fsys
     c     nkeol1_key    chain     nkeol1r
     c                   if        not %found or
     c                             nkteln = *blank
     c                   eval      p_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   call      'AS100R'
     c                   parm                    w_tkod
     c                   parm                    w_firm
     c                   parm                    nkteln
     c                   parm                    w_ttyp
     c                   parm                    w_fast
     c                   parm                    w_numm
      *
     c                   if        w_numm = 0
     c                   eval      p_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   eval      notell = w_numm
      *
      * Skriver til fil
      *
6.10 c                   time                    noodat
6.10 c                   time                    nootim
6.10 c                   eval      noousr = l_user
6.10 c                   time                    noedat
6.10 c                   time                    noetim
6.10 c                   eval      noeusr = l_user
     c                   write     nohelur
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_kund
     c                   parm                    p_kpro
     c                   parm                    p_lety
     c                   parm                    p_mapp
     c                   parm                    p_file
     c                   parm                    p_feil
      *
      * Key for oppslag på ordre-status
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på ekstern ordre-kode
      *
     c     nkeol1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    nkeol1_kode
      *
      * Key for oppslag på enhetskonvertering
      *
     c     fenklr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fenklr_enhf
      *
      *
      * Henter verdier fra parametre
      *
     c                   eval      w_firm = p_firm
      *
      * Henter kode-informasjon
      *
     c     fstsl1_key    chain     fstsl1
      *
     c                   endsr
**
ANWTYPE    01  Type/kapittel/postnr.
ANWPRID    02  Prosjektid
ANWUPID    03  Underprosjekt
ANWKART    04  Kostnadsart
ANWVARE    05  Artikkelnummer
ANWBESK    06  Varebeskrivelse
ANWENH1    07  Enhet
1NWANTA    08  Antall
3NWLNGD    09  Lengde
2NWMNGD    10  Mengde
2NWPRIS    11  Artikkelpris
2NWKOST    12  Kostnad (mengde*artikkelpris)
ANWDKON    13  Prosess/Delkons.kode
ANWDKTX    14  Beskrivelse
ANWREFK    15  Referansekode
ANWREFK    16  Referansebeskrivelse
ANWWBK1    17  WBS-kode 1
ANWWBT1    18  WBS-beskrivelse 1
ANWWBK2    19  WBS-kode 2
ANWWBT2    20  WBS-beskrivelse 2
0NWSDAT    21  startdato
0NWEDAT    22  sist endret
0NWADAT    23  anbudsfrist eller annen dato
ANWNOBB    24  Nobbnummer
