     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FD110R
      * Beskrivelse . . : Ordre-linje, vedlikehold av tekstlinje
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       111201 HKO  Nytt program
      * 6.10  240609 BSA  Oppdatert med sporingsinformasjon
6.NU  * R6.30 260514 bsa  Programmet gjennomgått mtp nummerutvidelser.
7.01  * R7.00 190319 bkv  Vasker vekk ulovlige tegn
8.01  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     fvltyl1    if   e           k disk    rename(vltypfr:vltyl1r)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     ffodtlu    uf a e           k disk    rename(fodtpfr:fodtlur)
      *
     ffd110d    cf   e             workstn
      *
      * Definering av parametre
      *
     d p_kode          s              1
     d p_firm          s                   like(fofirm)
     d p_numm          s                   like(fonumm)
     d p_suff          s                   like(fosuff)
     d p_line          s                   like(fdline)
     d p_ltyp          s                   like(fdltyp)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
      *
      * Definering av variable i nøkler
      *
     d vltyl1_ltyp     s                   like(valtyp)
      *
      * Definering av variable
      *
     d w_firm          s                   like(fdfirm)
6.NU d w_numm          s                   like(fdnumm)
     d w_suff          s                   like(fdsuff)
     d w_line          s                   like(fdline)
      *
     d w_ldat          s                   like(fdldat)
      *
      * Definering av konstanter
      *
     d lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer skjermbildet
      *
     c     fodtl1_key    chain     fodtl1
     c                   if        %found
     c                   eval      c1text = fdtek1 + fdtek2
7.01 c     x'1C':' '     xlate     c1text        c1text
     c                   eval      c1ltyp = fdltyp
     c                   eval      c1lass = fdlass
     c                   eval      c1ldat = 0
     c                   if        fdldat <> *loval
     c     *dmy          move      fdldat        c1ldat
     c                   endif
     c                   else
     c                   eval      c1text = *blank
     c                   eval      c1ltyp = p_ltyp
     c                   eval      c1lass = 0
     c                   eval      c1ldat = 0
     c                   if        c1ltyp = *blank
     c                   eval      c1ltyp = faatty
     c                   endif
     c                   endif
      *
      * Viser registreringbildet
      *
     c     c1tag         tag
      *
     c                   exfmt     c1win
      *
     c                   if        *inkc or *inkl
     c                   goto      avslutt
     c                   endif
      *
      * Validering
      *
      * Linjetype
      *
     c                   if        c1ltyp = *blank
     c                   eval      c1ltyp = faatty
     c                   endif
      *
     c                   eval      vltyl1_ltyp = c1ltyp
     c     vltyl1_key    chain     vltyl1
     c                   if        not %found
     c                   eval      *in31 = *on
     c                   goto      c1tag
     c                   endif
      *
     c                   if        valktx = 0
     c                   eval      *in32 = *on
     c                   goto      c1tag
     c                   endif
      *
      * Leveringsdato
      *
     c                   if        c1ldat <> 0
     c     *dmy          test(d)                 c1ldat                 33
     c                   if        *in31 = *on
     c                   goto      c1tag
     c                   endif
     c                   endif
      *
      * Leveringsdato kan ikke være eldre enn ordredato
      *
     c                   if        c1ldat <> 0
     c     *dmy          move      c1ldat        w_ldat
     c                   if        w_ldat < fobdat
     c                   eval      *in34 = *on
     c                   goto      c1tag
     c                   endif
     c                   endif
      *
      * Oppdaterer ordre-linje
      *
     c                   clear                   fodtlur
      *
     c     fodtlu_key    chain     fodtlur
      *
     c                   eval      fdotyp = footyp
     c                   eval      fdlage = folage
      *
     c                   eval      fdkund = fokund
     c                   if        folkun <> 0
     c                   eval      fdkund = folkun
     c                   endif
      *
7.01 c     x'1C':' '     xlate     c1text        c1text
     c                   eval      fdtek1 = %subst(c1text:1:35)
     c                   eval      fdtek2 = %subst(c1text:36:35)
      *
     c                   if        faahoy = 1
     c     lo:up         xlate     fdtek1        fdtek1
     c     lo:up         xlate     fdtek2        fdtek2
     c                   endif
      *
     c                   eval      fdltyp = c1ltyp
     c                   eval      fdlass = c1lass
      *
     c                   eval      fdldat = *loval
     c                   if        c1ldat <> 0
     c     *dmy          move      c1ldat        fdldat
     c                   endif
      *
     c                   if        not %found(fodtlu)
     c                   eval      fdfirm = w_firm
     c                   eval      fdnumm = w_numm
     c                   eval      fdsuff = w_suff
     c                   eval      fdline = w_line
6.10 c                   time                    fdodat
6.10 c                   time                    fdotim
6.10 c                   eval      fdoous = l_user
6.10 c                   time                    fdedat
6.10 c                   time                    fdetim
6.10 c                   eval      fdeusr = l_user
     c                   write     fodtlur
     c                   else
6.10 c                   time                    fdedat
6.10 c                   time                    fdetim
6.10 c                   eval      fdeusr = l_user
     c                   update    fodtlur
     c                   endif
      *
      * Legger inn kode som sier at minst en linje er registrert
      *
     c                   eval      p_kode = *on
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_kode
     c                   parm                    p_firm
6.NU c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_line
     c                   parm                    p_ltyp
      *
      * Key for oppslag på ordre-hode
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
      *
      * Key for oppslag på kode-register
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på linjetype
      *
     c     vltyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltyl1_ltyp
      *
      * Key for oppslag på ordre-linje
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
     c                   kfld                    w_line
      *
      * Key for oppdatering av ordre-linje
      *
     c     fodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
     c                   kfld                    w_line
      *
      * Henter parametre
      *
     c                   eval      w_firm = p_firm
6.NU c                   eval      w_numm = p_numm
     c                   eval      w_suff = p_suff
     c                   eval      w_line = p_line
      *
      * Hent opplysninger fra ordre-hode-register
      *
     c     fohel1_key    chain     fohel1r                            90
      *
      * Hent opplysninger fra kode-register
      *
     c     fstsl1_key    chain     fstsl1r                            90
      *
      *
     c                   endsr
