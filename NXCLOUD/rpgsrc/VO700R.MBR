     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VO700R
      * Beskrivelse . . : Vare-omregninmg
      *
      *                   Input:  Firma, vare, fra enhet, fra antall, enhet
      *                           til enhet, til antall
      *                   Output: til antall
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       230114 bsa  Nytt program
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
      *
      * Definering av pamametre
      *
     d p_vare          s                   like(vevare)
     d p_enh1          s                   like(veenhe)
     d p_enh2          s                   like(veenhe)
     d p_anta          s             11  3
     d p_antu          s             11  3
      *
      * Definering av variable i nøkler
      *
     d vvenl1_vare     s                   like(vevare)
     d vvenl1_enhe     s                   like(veenhe)
     d vvarl1_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d b_inlr          s              1    inz(*off)
      *
     d w_firm          s                   like(vefirm)
     d w_omr1          s                   like(veomre)
     d w_omr2          s                   like(veomre)
     d w_fakt          s                   like(veomre)
      *
      *
     d                uds
     d l_firm                944    946  0
     d l_lage               1001   1002  0
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   eval      p_antu = p_anta
     c                   goto      avslutt
     c                   endif
      *
      * Finner omregningsfaktor for enhet 1
      *
     c                   eval      vvenl1_vare = p_vare
     c                   eval      vvenl1_enhe = p_enh1
     c     vvenl1_key    chain     vvenl1
     c                   if        %found
     c                   eval      w_omr1 = veomre
     c                   else
     c                   eval      p_antu = p_anta
     c                   goto      avslutt
     c                   endif
      *
      * Finner omregningsfaktor for enhet 2 (ut)
      *
     c                   eval      vvenl1_vare = p_vare
     c                   eval      vvenl1_enhe = p_enh2
     c     vvenl1_key    chain     vvenl1
     c                   if        %found
     c                   eval      w_omr2 = veomre
     c                   else
     c                   eval      p_antu = p_anta
     c                   goto      avslutt
     c                   endif
      *
      * Finner omregningsfaktor volum for enhet 1 og 2
      *
     c                   if        w_omr1 = 0
     c                   eval      w_omr1 = 1
     c                   endif
      *
     c                   if        w_omr2 = 0
     c                   eval      w_omr2 = 1
     c                   endif
      *
     c                   eval(h)   w_fakt = w_omr1 / w_omr2
     c                   eval(h)   p_antu = p_anta * w_fakt
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_vare
     c                   parm                    p_enh1
     c                   parm                    p_anta
     c                   parm                    p_enh2
     c                   parm                    p_antu
      *
      * Key for les på vare-enhet-register
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
     c                   kfld                    vvenl1_enhe
      *
      * Key for oppslag på vare-register
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = l_firm
      *
      * Initierer dato
      *
     c                   eval      p_antu = 0
      *
     c                   endsr
