      /TITLE  ASOFAK: Skriver prisetiketter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FP213R
      * Beskrivelse . . : Skriver prisetiketter
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       201198 KOB  Nytt program
      * V5.00 040303 JAF  Meto mi-4 ta HEAT og PRTSPEED fra
      *                   styrestreng 1 og 2 i Enhetsreg
      * V5.30 080304 KOB  Blanker pris hvis parameter sier uten pris
      * V5.60 020408 JAF  Ny BM etikett
      *                   Støtte for Datamax Ex2
      * V5.60 210109 KOB  Lagt inn støtte for Datamax EX-2 som egen skrivertype
6.10  * V6.10 031110 BSA  Feil breddeangivelse på etikett.
6.30  * 6.30  280514 BKV  LVAR økt fra 15 til 20
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvaw1pf    if   f  512        disk
      *
     fEtikett   o    f  132        Printer oflind(*inof)
     *
     *+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     *               :  LOCAL DATA AREA                                 +
     *+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     *
     D                uds
     *
     d l_etpr                112    112  0
     d l_hex1                760    779
     d l_hex2                780    799
     d lcskty                891    891  0
     d lcuser                911    920
     d lcfirm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variable
      *
     d w_skty          s                   like(lcskty)
     d w_hyle          s                   like(vahyle)
      *
     d w_anta_1        s              1  0
     d w_anta_2        s              2  0
     d w_anta_3        s              3  0
      *
     ivaw1pf    ns  01
      *
     i                                  1    4 0vaanta
     i                                  5   19  vattxt
     i                                 20   25  vatvar
     i                                 26   30  vatenh
     i                                 31   37  vatpri
     i                                 38   45  vadato
     i                                 59   66 0vavare
     i                                 74   86 0vaeann
     i                                 87   99  valvar
6.30 i                                107  141  vatek1
6.30 i                                142  176  vatek2
6.30 i                                177  179  vaenh1
6.30 i                                180  187  vapri1
6.30 i                                188  190  vaenh2
6.30 i                                191  198  vapri2
6.30 i                                199  203  vabesk
6.30 i                                204  206  vabpun
6.30 i                                207  209  vabmen
6.30 i                                210  210 0vahyle
6.30 i                                211  211 0valvko
      *
     i                                256  256  vadumy
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   dow       *in61 = *off
      *
      * Blank pris
      *
     C     l_etpr        ifeq      1
     c                   eval      vapri1 = *blank
     C                   endif
      *
      * Skrivertype Meto 8100 Twinax
      *
     C     w_skty        ifeq      0
     c                   except    Æpgm0
     C                   endif
      *
      * Skrivertype Meto 8100 Standard
      *
     C     w_skty        ifeq      1
     c                   except    Æpgm1
     C                   endif
      *
      * Skrivertype Meto 8100 Twinax
      *
     C     w_skty        ifeq      0
     c                   except    Ædata0
     C                   endif
      *
      * Skrivertype Meto 8100 Standard
      *
     C     w_skty        ifeq      1
     c                   except    Ædata1
     C                   endif
      *
      * Skrivertype Meto Bandit
      *
     C     w_skty        ifeq      2
      *
     c                   eval      *in81 = *off
     c                   eval      *in82 = *off
     c                   eval      *in83 = *off
      *
     c                   select
     c                   when      vaanta <= 9
     c                   eval      w_anta_1 = vaanta
     c                   eval      *in81 = *on
     c                   when      vaanta <= 99
     c                   eval      w_anta_2 = vaanta
     c                   eval      *in82 = *on
     c                   when      vaanta <= 500
     c                   eval      w_anta_3 = vaanta
     c                   eval      *in83 = *on
     c                   other
     c                   eval      w_anta_3 = 500
     c                   eval      *in83 = *on
     c                   endsl
      *
     c                   except    Ædata2
     C                   endif
      *
      * Skrivertype Eltron
      *
     c     w_skty        ifeq      3
     c                   except    Æpgm3
      *
     c                   except    Ædata3
     c                   endif
      *
      * Skrivertype Meto mi-4
      *
     c     w_skty        ifeq      4
     c                   except    Ædata4
     c                   endif
      *
      * Skrivertype Datamax Ex-2 (Bruker samme utskrift som  Meto mi-4)
      *
     c     w_skty        ifeq      6
     c                   eval      *in66 = *on
     c                   except    Ædata4
     c                   endif
      *
     c     neste         tag
      *
     c                   read      vaw1pf                                 61
     c                   enddo
      *
     c                   goto      avslutt
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
      * Skrivertype Meto Bandit
      *
     C     w_skty        ifeq      2
     c                   except    Æend2
     C                   endif
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c                   read      vaw1pf                                 61
      *
      * Hent skriverinformasjon
      *
     C                   eval      w_skty = lcskty
      *
      * Skrivertype Meto Bandit
      *
     C     w_skty        ifeq      2
     c                   except    Æpgm2
     C                   endif
      *
     c                   endsr
      *
      * Twinax-skriver
      *                                                                ***
     OEtikett   e            Æpgm0
      *                                                                ***
      * Halo printer here am I, are you ready?
     o                                              '&02&20&03'
      *
      * Clear LLU 000005
      *
     O          e            Æpgm0
     o                                              '&02&53000005&03'
      *
      * Clear PGM-buffer 05
      *
     O          e            Æpgm0
     o                                              '&02&5405&03'
      *
      * Write program
      *
     O          e            Æpgm0
     o                                              '&02&4405062'
     o                                              '&04'
      *
      * Fast tekst
      *
     O          e            Æpgm0
     o                                              '&02'
     O                                              '&0D005022015012322'
     o                                              '&04'
      *
      * "Bar-code" (EAN-nr.)
      *
     o          e            Æpgm0
     o                                              '&02'
     o                                              '&0D006013000008021R100'
     o                                              '&04'
      *
      * Eget varenr.
      *
     O          e            Æpgm0
     o                                              '&02'
     O                                              '&0D012011008006122'
     o                                              '&04'
      *
      * Pris1
      *
     O          e            Æpgm0
     o                                              '&02'
     O                                              '&0D008003010018522'
     o                                              '&04'
      *
      * Dato
      *
     O          e            Æpgm0
     o                                              '&02'
     O                                              '&0D002006008006112'
     o                                              '&04'
      *
      * Dummy
      *
     O          e            Æpgm0
     o                                              '&02'
     O                                              '&0D001001001006112'
     o                                              '&04'
      *
      *
      *
     O          e            Æpgm0
     o                                              '&02'
     o                                              '&0D'
     o                                              '&0D'
     o                                              '&0D'
     o                                              '&0D'
     o                                              '&0D'
     o                                              '&0D'
     o                                              '&0D'
     o                                              '&03'
      *
      * Write data to  LLU-000005
      *
     O          e            Ædata0
     o                                              '&02&4300000605&0D'
     o                                              '&04'
      *
      * Fast tekst
      *
     O          e            Ædata0
     o                                              '&02'
     O                       vattxt
     o                                              '&0D'
     o                                              '&04'
      *
      * EAN-nr.
      *
     O          e            Ædata0
     o                                              '&02'
     O                       vaeann
     o                                              '&0D'
     o                                              '&04'
      *
      * Eget varenr.
      *
     O          e            Ædata0
     o                                              '&02'
     O                       vavare
     o                                              '&0D'
     o                                              '&04'
      *
      * Pris
      *
     O          e            Ædata0
     o                                              '&02'
     O                       vapri1
     o                                              '&0D'
     o                                              '&04'
      *
      * Dato
      *
     O          e            Ædata0
     o                                              '&02'
     O                       vadato
     o                                              '&0D'
     o                                              '&04'
      *
      * Dummy
      *
     O          e            Ædata0
     o                                              '&02'
     O                       vadumy
     o                                              '&0D'
     o                                              '&04'
      *
     O          e            Ædata0
     o                                              '&02'
     o                                              '&0D'
     o                                              '&03'
      *
      * Write data to label
      *
     O          e            Ædata0
     o                                              '&02&66'
     O                       vaanta
     o                                              '&03'
      *
      * Standard-skriver
      *                                                                ***
     OEtikett   e            Æpgm1
      *                                                                ***
      * Halo printer here am I, are you ready?
     O                                              '{ & } '
      *
      * Clear LLU 000005
      *
     O          e            Æpgm1
     O                                              '{&S000005} '
      *
      * Clear PGM-buffer 05
      *
     O          e            Æpgm1
     O                                              '{&T05} '
      *
      * Write program
      *
     O          e            Æpgm1
     O                                              '{&D05062'
     O                                              '!  '
      *
      * Fast tekst
      *
     O          e            Æpgm1
     O                                              '{'
     O                                              '\005022015012322'
     O                                              '! '
      *
      * "Bar-code" (EAN-nr.)
      *
     o          e            Æpgm1
     O                                              '{'
     O                                              '\006013000008021R100'
     O                                              '! '
      *
      * Eget varenr.
      *
     O          e            Æpgm1
     O                                              '{'
     O                                              '\012011008006122'
     O                                              '! '
      *
      * Pris1
      *
     O          e            Æpgm1
     O                                              '{'
     O                                              '\008003010018522'
     O                                              '! '
      *
      * Dato
      *
     O          e            Æpgm1
     O                                              '{'
     O                                              '\002006008006112'
     O                                              '! '
      *
      * Dummy
      *
     O          e            Æpgm1
     O                                              '{'
     O                                              '\001001001006112'
     O                                              '! '
      *
      * Avslutt
      *
     O          e            Æpgm1
     O                                              '{\\\\\\\}'
      *
      * Write data to  LLU-000005
      *
     O          e            Ædata1
     O                                              '{&C00000605\ '
     O                                              '! '
      *
      * Fast tekst
      *
     O          e            Ædata1
     O                                              '{'
     O                       vattxt
     O                                              '\'
     O                                              '!'
      *
      * EAN-nr.
      *
     O          e            Ædata1
     O                                              '{'
     O                       vaeann
     O                                              '\'
     O                                              '!'
      *
      * Eget varenr.
      *
     O          e            Ædata1
     O                                              '{'
     O                       vavare
     O                                              '\'
     O                                              '!'
      *
      * Pris
      *
     O          e            Ædata1
     O                                              '{'
     O                       vapri1
     O                                              '\'
     O                                              '!'
      *
      * Dato
      *
     O          e            Ædata1
     O                                              '{'
     O                       vadato
     O                                              '\'
     O                                              '!'
      *
      * Dummy
      *
     O          e            Ædata1
     O                                              '{'
     O                       vadumy
     O                                              '\'
     O                                              '!'
      *
      * Avslutt
      *
     O          e            Ædata1
     O                                              '{'
     O                                              '\'
     O                                              '}'
      *
      * Write data to label
      *
     O          e            Ædata1
     O                                              '{&f'
     O                       vaanta
     O                                              '}'
      *
      * Bandit-skriver
      *                                                                ***
     oEtikett   e            Æpgm2
     O                                              '{'
      *
     O          e            Æpgm2
     O                                              '[Ct "0"]'
      *
     O          e            Æpgm2
     O                                              '[N m "m"]'
      *
     O          e            Æpgm2
     O                                              '[M n "swpaper"]'
      *
     O          e            Æpgm2
     O                                              '[M w "40.0:0:0:1"]'
     O          e            Æpgm2
     O                                              '[M l "25.0:0:0:1:direct '
     O                                              'table 100"]'
     O          e            Æpgm2
     O                                              '[M c "0"]'
     O          e            Æpgm2
     O                                              '[M m "0:20:1"]'
     O          e            Æpgm2
     O                                              '[M a "swpaper"]'
     O          e            Æpgm2
     O                                              '[D W "1" "5" "3" '
     O                                              '"swpaper" "0,0:0,0" "\N"'
      *
      * Avslutt innhold
      *
     O          e            Æpgm2
     O                                              '"\N"]'
      *
      * Etiketter
      *                                                                ***
     O          e            Ædata2
     O                                              '[D W "1" "5" "3" '
     O                                              '"swpaper" "0,0:0,0" "\N"'
      *
      * Fast tekst
      *
     O          e            Ædata2
     O                                              '"C" "04.0:22.0:1" "00" '
     O                                              '"4000001:3" "15:0" '
     O                                              '"" "" "1:1:2:1:1" "\N"'
      *
      * "Bar-code" (EAN-nr.)
      *
     O          e            Ædata2
     O                                              '"A" "4.0:12.0:1" "00"  '
     O                                              '"00:-:2" "76" "08" '
     O                                              '"\N"'
      *
      * Eget varenr.
      *
     O          e            Ædata2
     O                                              '"C" "09.0:11.0:1" "00" '
     O                                              '"4000001:1" "08:0" '
     O                                              '"" "" "1:1:2:1:1" "\N"'
      *
      * Pris1
      *
     O          e            Ædata2
     O                                              '"C" "08.0:03.0:1" "00" '
     O                                              '"4000001:5" "8:0" '
     O                                              '"" "" "1:1:2:1:1" "\N"'
      *
      * Dato
      *
     O          e            Ædata2
     O                                              '"C" "05.0:6.0:1" "90" '
     O                                              '"4000001:1" "8:0" '
     O                                              '"" "" "1:1:2:1:1" "\N"'
      *
     O          e            Ædata2
     O                                              '"\N"]'
      *
     O          e            Ædata2
     O                                              '[D w "LLU""1""\N"'
      *
      * Fast tekst
      *
     O          e            Ædata2
     O                                              '"'
     O                       vattxt
     O                                              '"'
      *
      * "Bar-code" (EAN-nr.)
      *
     O          e            Ædata2
     O                                              '"'
     O                       vaeann
     O                                              '"'
      *
      * Eget varenr.
      *
     O          e            Ædata2
     O                                              '"'
     O                       vavare
     O                                              '"'
      *
      * Pris1
      *
     O          e            Ædata2
     O                                              '"'
     O                       vapri1
     O                                              '"'
      *
      * Dato
      *
     O          e            Ædata2
     O                                              '"'
     O                       vadato
     O                                              '"'
      *
      * Avslutt innhold
      *
     O          e            Ædata2
     O                                              '"\N"]'
      *
      * Antall etiketter
      *
     O          e            Ædata2
     O                                            6 '[P b "'
     O               81      w_anta_1             7
     O               82      w_anta_2             8
     O               83      w_anta_3             9
     O               81                          19 ':1:swpaper"]'
     O               82                          20 ':1:swpaper"]'
     O               83                          21 ':1:swpaper"]'
      *
     O          e            Æend2
     O                                              '}'
      *
      * Behandle Eltron-skriver
      *                                                                ***
     oEtikett   e            Æpgm3
     O                                              'OD'
      *
     O          e            Æpgm3
     O                                              'US'
      *
     O          e            Æpgm3
     O                                              'FK"2"'
      *
     O          e            Æpgm3
     O                                              'FS"2"'
      *
     O          e            Æpgm3
     O                                              'V00,35,N,"Fast tekst"'
      *
      *
     O          e            Æpgm3
     O                                              'V01,08,N,"Dato"'
      *
     O          e            Æpgm3
     O                                              'V02,13,N,"EAN-nr."'
      *
     O          e            Æpgm3
     O                                              'V03,08,N,"Eget varenr."'
      *
     O          e            Æpgm3
     O                                              'V04,08,N,"Pris 1"'
      *
      * Speed Select
      *
     O          e            Æpgm3
     O                                              'S2'
      *
      * Density
      *
     O          e            Æpgm3
     O                                              'D10'
      *
      * Set Label Width (Bredde) 38 mm x 8
      *
     O          e            Æpgm3
     O                                              'q308'
      *
      * Set Form Lengt Black Line Sensor (Lengde P1 97 x 8, 3 x 8)
      *
     O          e            Æpgm3
     O                                              'Q800,B16+10'
      *
      * Print Direction
      *
     O          e            Æpgm3
     O                                              'ZB'
      *
      * Fast tekst
      *
     O          e            Æpgm3
     O                                              'A020,740,3,4,2,1,N,V00'
      *
      * Dato
      *
     O          e            Æpgm3
     O                                              'A090,050,0,4,1,1,N,V01'
      *
      * "Bar-code" (EAN-nr.)
      *
     O          e            Æpgm3
     O                                              'B160,730,3,E30,3,9,80,B,'
     O                                              'V02'
      *
      * Eget varenr.
      *
     O          e            Æpgm3
     O                                              'B160,730,3,2,4,9,80,B,V03'
      *
      * Pris1
      *
     O          e            Æpgm3
     O                                              'A210,250,3,4,3,1,N,V04'
      *
      * Store End
      *
     O          e            Æpgm3
     O                                              'FE'
      *
      * Behandle Eltron-skriver
      *                                                                ***
     oEtikett   e            Ædata3
     O                                              'N'
      *
      * Start data
      *
     O          e            Ædata3
     O                                              'FR"2"'
      *
      * Start innhold
      *
     O          e            Ædata3
     O                                              '?'
      *
      * Fast tekst
      *
     O          e            Ædata3
     O                       vattxt
     O                                           50 'X'
      *
      * Dato
      *
     O          e            Ædata3
     O                       vadato
     O                                           50 'X'
      *
      * "Bar-code" (EAN-nr.)
      *
     O          e            Ædata3
     O                       vaeann
     O                                           50 'X'
      *
      * Eget varenr.
      *
     O          e            Ædata3
     O                       vavare
     O                                           50 'X'
      *
      * Pris1
      *
     O          e            Ædata3
     O                       vapri1
     O                                           50 'X'
      *
      * Antall etiketter
      *
     O          e            Ædata3
     O                                              'P'
     O                       vaanta
      *
      * Behandle Meto mi-4
      *                                                                ***
     oEtikett   e            Ædata4
     O                                              '~n'
      *
     o          e            Ædata4
     O                                              '~c0000'
      *
     O          e            Ædata4
     O                                              '~e'
      *
     O          e            Ædata4
     O                                              '~M2055'
      *
     O          e            Ædata4
     O                                              '~ySPM'
      *
     O          e            Ædata4
     O                                              '~Kf0040'
      *
      * Kode for Datamax Ex-2
      *
     O          e    66      Ædata4
6.10 O**                                            '~KcLW100'
6.10 O                                              '~KcLW158'
      *
     O          e            Ædata4
     O                                              '~L'
      *
     O          e            Ædata4
     O                                              'z'
      *
     O          e            Ædata4
     O                                              'D11'
      *
     O          e            Ædata4
     O                       l_hex1
      *
     O          e            Ædata4
     O                       l_hex2
      *
      * Antall etiketter
      *
     O          e            Ædata4
     O                                              'Q'
     O                       vaanta
      *
      * Fast tekst
      *
     O          e            Ædata4
     O                                           15 '1311A1000750030'
     O                       vattxt
      *
      * Dato
      *
     O          e            Ædata4
     O                                           15 '4211A1000150025'
     O                       vadato
      *
      * "Bar-code" (EAN-nr.)
      *
     O          e            Ædata4
     O                                           15 '1f2202900350030'
     O                       vaeann
      *
      * Eget varenr.
      *
     O          e            Ædata4
     O                                           15 '121100000250050'
     O                       vavare
      *
      * Pris1
      *
     O          e            Ædata4
     O                                           15 '1411A1800010030'
     O                       vapri1
      *
     O          e            Ædata4
     O                                              'E'
      *
