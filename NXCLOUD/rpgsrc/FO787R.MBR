     H decedit('0.') datedit(*dmy-) option(*nodebugio)
     h DftActgrp(*No) BndDir('CGIDEV2/CGIDEV2')
      /copy hspecsbnd
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO787R
      * Beskrivelse . . : Lager xml til byggdok
      *
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       230412 bsa  Nytt program
6.10  *       200812 bsa  Scanner streng for ugyldige tegn
6.21  *       300712 bof  Legge ut prosjektnavn i stedet for prosjektnr.
6.22  *       140812 bof  Hvis mail på kundeprosjekt er utfylt brukes dette
6.23  *       200812 bof  legger ut GTIN-nr pr. leverandør - salgsenh1.
6.24  *       120912 bsa  Kan ikke sende GTIN tagg hvis 0 i GTIN nummer
6.25  *       130912 bof  Hvis gtin = Treindustriens gtin - legg ut 0. Dette
6.25  *                   fordi Treindustrien ikke har dokumentasjon.
6.26  *       220113 bof  Bruker ekstern prosjekt hvis dette finnes
6.27  *       060213 bof  Legger kun ut foretaksnr hvis <>  0
6.30  *       230414 bof  JVPPPF utgår, erstattet av JVPKPF
6.31  * 6.30  280115 bsa  Skriver til datakø for å trigge java sniffer
6.32  * 6.30  050615 bsa  Nullstill minne for bruk av XML
6.33  * 6.30  210915 bsa  Dette skal ikke innom datakøen. Går ikke inn i PROA
6.33  *                   Reverserer endring 6.31
6.34  * 6.30  121018 bof  Utvidet med trelast - sortiment
      *
7.01  * 6.30  070119 sst  Byggdok overføring til F.Kunde
      *
8.01  *       140623 bof  Utvide teller fra 999 til 9999
8.02  *K0000  220823 bsa  Bruker varekunde og ikke fakturakunde i rapportering
8.02  *K0000              Switchstyres
8.03  *K0000  220823 bsa  Utvidet teller
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffbdol1    uf   e           k disk    rename(fbdopfr:fbdol1r)
     fraa8l1    if   e           k disk    rename(raa8pfr:raa8l1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
6.21 ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     fafpslr    if   e           k disk    rename(afpspfr:afpslrr)
6.23 fvvenl2    if   e           k disk    rename(vvenpfr:vvenl2r)
6.23 fvvarl3    if   e           k disk    rename(vvarpfr:vvarl3r)
6.30 fjvpkl3    if   e           k disk    rename(jvpkpfr:jvpkl3r)
7.01 ffkp2l1    if   e           k disk    rename(fkp2pfr:fkp2l1r)
7.01 frukpl1    if   e           k disk    rename(rukppfr:rukpl1r)
      *
     d p_bnum          s             15
6.10 d p_str_in        s            512
6.10 d p_str_out       s            512
6.10 d p_lr            s              1
6.31 d p_filg          s             10
6.31 d p_xmlf          s            256
      *
      * Definering av variabler i nøkler
      *
     d rkunl1_kund     s                   like(rkkund)
     d fbdol1_bnum     s                   like(fwbnum)
     d afpslr_ufil     s                   like(l_filg)
     d afpslr_uide     s                   like(afuide)
6.21 d fkprl1_kund     s                   like(flkund)
6.21 d fkprl1_kpro     s                   like(flkpro)
6.23 d vvenl2_vare     s                   like(vevare)
6.23 d vvenl2_saen     s                   like(vesaen)
6.23 d vvarl3_nobb     s                   like(vvnobb)
6.30 d jvpkl3_gtin     s                   like(jwgtin)
7.01 d fkp2l1_kund     s                   like(flkund)
7.01 d fkp2l1_kpro     s                   like(flkpro)
7.01 d rukpl1_kund     s                   like(rukund)
      *
8.03 d w_tell          s             11  0
     d w_firm          s                   like(rkfirm)
     d w_path          s             50
     d w_xmlp          s             50
     d w_dato          s              8
6.10 d w_bnav          s             50
6.10 d w_bper          s             50
6.10 d*w_pnav          s             40
6.10 d w_pnav          s             50
6.23 d w_gtin          s             14  0
6.23 d w_stat          s              1
6.27 d w_ftnr          s             50
6.31 d w_dtaq          s              1
6.34 d w_lv_vare       s                   like(vvvare)
7.01 d w_bkun          s                   like(rkkund)
      *
      *  UDS Local Data Area
      *
     d                uds
     d l_user                911    920
     d l_filg                931    933
     d l_firm                944    946  0
      *
     d b_first         s              1    inz(*off)
      *
     d XML_Output      s            256a   Varying
     d XML_path        s            256a
     d Out_path        s            256a
      *
     d tmpdate         s               d
      *
     d d_dato          ds            10
     d  d_aar                         4    overlay(d_dato:1)
     d  d_mnd                         2    overlay(d_dato:6)
     d  d_dag                         2    overlay(d_dato:9)
      *
      /copy prototypeb
      /copy usec
      *
      * Eksternt program
8.02   dcl-s co402_verdi1  char(1);
8.02   dcl-s co402_verdi2  char(2048);
8.02   DCL-PR CO402R EXTPGM('CO402R');
8.02     p_filgrp            char(3)     const;
8.02     p_firm              packed(3:0) const;
8.02     p_lager             packed(2:0) const;
8.02     p_nokkel            char(50)    const;
8.02     p_verdi1            char(1);
8.02     p_verdi2            char(2048);
8.02   END-PR;
      *
8.02 d u_vark          s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.34 C/Exec SQL
6.34 C+  Set Option DatFmt=*ISO
6.34 C/End-Exec
      *
     c                   eval      afpslr_ufil = l_filg
     c                   eval      afpslr_uide = 'BYGGMAL   '
     c     afpslr_key    chain     afpslrr
     c                   if        not %found(afpslr)
     c                   goto      avslutt
     c                   endif
     c*                  eval      XML_path='/home/asp/print/maler/byggdok.XML'
     c                   eval      XML_path=afudss
      *
     c                   eval      afpslr_ufil = l_filg
     c                   eval      afpslr_uide = 'BYGGOUT   '
     c     afpslr_key    chain     afpslrr
     c                   if        not %found(afpslr)
     c                   goto      avslutt
     c                   endif
     c                   eval      OUT_path=afudss
6.31  *
6.31  * Finn ut om vi skal skrive til datakø
6.31  *
6.31 c*6.33              eval      w_dtaq = '0'
6.31 c*6.33              eval      afpslr_uide = 'DATAQ     '
6.31 c*6.33afpslr_key    chain     afpslrr
6.31 c*6.33              if        %found
6.31 c*6.33              movel     afudss        w_dtaq
6.31 c*6.33              endif
      *
      * Byggdok varer
      *
     c     fbdol1_key    setll     fbdol1r
     c     fbdol1_key    reade     fbdol1r
     c                   dow       not %eof
      *
     c                   if        fwbovf = 0
      *
     c                   if        b_first = *on
     c                   exsr      xml_env
     c                   exsr      xml_header
     c                   endif
      *
6.23 c                   exsr      hent_gtin
     c                   exsr      xml_line
      *
     c                   eval      fwbovf = 1
     c                   update    fbdol1r
      *
     c                   endif
      *
     c     fbdol1_key    reade     fbdol1r
     c                   enddo
      *
      * Skriv xml til IFS (NB Kun hvis det er funnet noe å skrive)
      *
     c                   if        w_tell > 0
     c                   exsr      xml_end
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av mal
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xml_env       begsr
      *
      * Hent inn xml templaten
      *
     c                   eval      w_xmlp = %trim(XML_path)
      *
     c                   callp     GetHTMLIFS(w_xmlp :'<AS400>')
6.32 c                   callp     ClrHtmlBuffer
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for håndtering av mal og hode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xml_header    begsr
      *
      * Hent inn xml templaten
      *
     c                   eval      w_xmlp = %trim(XML_path)
      *
     c                   callp     GetHTMLIFS(w_xmlp :'<AS400>')
      *
      * Hent inn info fra e-handels register
      *
     c     raa8l1_key    chain     raa8l1
     c                   if        not %found
     c                   eval      ra8ean = 0
     c                   endif
7.01  *
7.01  *  Finn F-kunde fra fkp2pf
7.01  *
8.02 c                   if        u_vark = *off
7.01  *
7.01 c                   eval      w_bkun = fwbkun
7.01 c                   eval      fkp2l1_kund = fwbkun
7.01 c                   eval      fkp2l1_kpro = fwbkpr
7.01 c     fkp2l1_key    chain     fkp2l1
7.01 c                   if        %found(fkp2l1)
7.01 c                             and flkunf <> 0
8.02 c**                 eval      fwbkun = flkunf
8.02 c                   eval      w_bkun = flkunf
7.01 c                   endif
7.01  *
7.01  *  Finn Mailadresse fra F-kunde
7.01  *
8.02 c**                 eval      rukpl1_kund = fwbkun
8.02 c                   eval      rukpl1_kund = w_bkun
7.01 c     rukpl1_key    setll     rukpl1
7.01 c     rukpl1_key    reade     rukpl1
7.01 c                   dow       not %eof(rukpl1)
7.01 c                             and rukrol <> 'BYGGDOK'
7.01 c     rukpl1_key    reade     rukpl1
7.01 c                   enddo
7.01 c                   if        not %eof(rukpl1)
7.01 c                             and rukrol =  'BYGGDOK'
7.01 c                             and rumail <> ' '
7.01 c                   eval      fwbmai = rumail
7.01 c                   eval      fwbper = ruenav
7.01 c                   endif
8.02  * Vi bruker kundenummer fra FBDO
8.02 c                   else
8.02 c                   eval      w_bkun = fwbkun
8.02 c                   endif
      *
      * Hent inn info fra kunderegisteret
      *
8.02 c**                 eval      rkunl1_kund = fwbkun
8.02 c                   eval      rkunl1_kund = w_bkun
     c     rkunl1_key    chain     rkunl1
     c                   if        not %found
     c                   eval      rkftnr = 0
7.01 c                   else
7.01 c                   eval      fwbnav = rknavn
     c                   endif
      *
     c                   movel     fwbtms        d_dato
     c                   eval      w_dato = d_aar + d_mnd + d_dag
6.10  *
6.10  * Scann av strenger - navn
6.10  *
6.10 c                   eval      p_str_in = *blank
6.10 c                   eval      p_lr = '0'
6.10 c                   movel     fwbnav        p_str_in
6.10 c                   call      'AP616R'
6.10 c                   parm                    p_str_in
6.10 c                   parm                    p_str_out
6.10 c                   parm                    p_lr
6.10 c                   movel     p_str_out     w_bnav
6.10  *
6.10  * Scann av strenger - kontaktperson
6.10  *
6.10 c                   eval      p_str_in = *blank
6.10 c                   eval      p_lr = '0'
6.10 c                   movel     fwbper        p_str_in
6.10 c                   call      'AP616R'
6.10 c                   parm                    p_str_in
6.10 c                   parm                    p_str_out
6.10 c                   parm                    p_lr
6.10 c                   movel     p_str_out     w_bper
6.21  *
6.21  * Hent inn prosjekt-tekst
6.21  *
7.01 c                   eval      fkprl1_kund = w_bkun
6.21 c                   eval      fkprl1_kpro = fwbkpr
6.21 c     fkprl1_key    chain     fkprl1
6.21 c                   if        not %found(fkprl1)
6.21 c                   movel     fwbkpr        w_pnav
6.21 c                   else
6.26 c                   if        flepro <> *blank
6.26 c                   eval      w_pnav = %trim(flepro)
6.26 c                   else
6.21 c                   eval      w_pnav = %char(flkpro) + ' ' +
6.21 c                                      %trim(flnavn)
6.26 c                   endif
6.22 c                   if        flmail <> *blank
6.22 c                   movel     flmail        fwbmai
6.22 c                   endif
6.21 c                   endif
6.10  *
6.10  * Scann av strenger - prosjektnavn
6.10  *
6.10 c                   eval      p_str_in = *blank
6.10 c                   eval      p_lr = '0'
6.10 c                   movel     w_pnav        p_str_in
6.10 c                   call      'AP616R'
6.10 c                   parm                    p_str_in
6.10 c                   parm                    p_str_out
6.10 c                   parm                    p_lr
6.10 c                   movel     p_str_out     w_pnav
6.27  * Scann av strenger - foretaksnr
6.27  *
6.27 c                   if        rkftnr <> 0
6.27 c                   eval      p_str_in = *blank
6.27 c                   eval      p_lr = '0'
6.27 c                   movel     rkftnr        p_str_in
6.27 c                   call      'AP616R'
6.27 c                   parm                    p_str_in
6.27 c                   parm                    p_str_out
6.27 c                   parm                    p_lr
6.27 c                   movel     p_str_out     w_ftnr
6.27 c                   else
6.27 c                   eval      w_ftnr = *blank
6.27 c                   endif
      *
      /Free
           // Skriv standard topp
               WrtSection('Topp');

           // Skriv ordrehode informasjon
               UpdHTMLVar('Ordernumber':             fwbnum);
               UpdHTMLVar('Orderdate':               w_dato);
6.10    //     UpdHTMLVar('Projectno':        %char(fwbkpr));
6.10           UpdHTMLVar('Projectno':               w_pnav);
               UpdHTMLVar('Buyer':            %char(ra8ean));
               UpdHTMLVar('BPartyidentifier': %char(ra8ean));
8.02      //   UpdHTMLVar('DPartyidentifier': %char(fwbkun));
8.02           UpdHTMLVar('DPartyidentifier': %char(w_bkun));
6.10      //   UpdHTMLVar('Partyname':               fwbnav);
6.10           UpdHTMLVar('Partyname':               w_bnav);
6.10      //   UpdHTMLVar('Contactname':             fwbper);
6.10           UpdHTMLVar('Contactname':             w_bper);
               UpdHTMLVar('Contactemailadr':         fwbmai);
6.27      //   UpdHTMLVar('VATNumber':        %char(rkftnr));
6.27           UpdHTMLVar('VATNumber':              w_ftnr);
               WrtSection('Orderheader');

      /End-free
      *
     c                   eval      b_first = *off
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for håndtering av linjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xml_end       begsr
      *
      /Free
           // Skriv siste tagg
               WrtSection('Footer');

      /End-free
      *
      * Lag navn til xml fila
      *
     c                   eval      w_path = %trim(Out_path)
     c                   eval      XML_Output = %trim(w_path) + 'Byggdok_'+
     c                             %char(ra8ean) + '_' + %trim(fwbnum) +
     c                             '.xml'
      *
     c                   callp     WrtHtmlToStmf(XML_Output: 819)
6.31  * Skriver til datakø for å aktivere sniffer, hvis dataq er aktiv
6.31 c*6.33              if        w_dtaq = '1'
6.31 c*6.33              eval      p_xmlf = xml_output
6.31 c*6.33              eval      p_filg = 'ADB'+l_filg
6.31 c*6.33              call      'AP629C'
6.31 c*6.33              parm                    p_filg
6.31 c*6.33              parm                    p_xmlf
6.31 c*6.33              endif
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for håndtering av linjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xml_line      begsr
      *
     c                   eval      w_tell = w_tell + 1
      *
6.24  /Free
6.24       // Skriv ordrelinje starttagger
6.24           UpdHTMLVar('Linenumber':    %char(w_tell));
6.24           WrtSection('OrderLineStart');
6.24  /End-free
      *
6.23 c                   if        w_gtin <> 0
6.23  * hvis gtin er utfylt brukes dette utlegget
      *
      /Free
           // Skriv ordrehode informasjon
               UpdHTMLVar('GTINNumber':    %char(w_gtin));
6.24           WrtSection('OrderLineGTIN');

      /End-free
      *
6.24 c                   endif
6.23  *
6.23  * hvis gtin er 0 brukes dette utlegget
6.23  *
6.23  /Free
6.23       // Skriv ordrehode informasjon
6.23           UpdHTMLVar('NOBBNumber':    %char(fwbnob));
6.23           UpdHTMLVar('Quantity':                '1');
6.23
6.24           WrtSection('OrderLineRest');
6.23
6.23  /End-free
6.23  *
     c                   endsr
6.23  *
6.23  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.23  * Subrutine for å hente gtin-nr
6.23  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.23  *
6.23 c     hent_gtin     begsr
6.23  *
6.34  * sjekker om logistikk-vare
6.34 c                   eval      w_lv_vare = *blank
6.34 c/exec sql
6.34 c+ select vvlvnr
6.34 c+ into   :w_lv_vare
6.34 c+ from   vvlepf
6.34 c+ where  vvlnob = :fwbnob
6.34 c+ fetch first 1 rows only
6.34 c/end-exec
6.34 c                   eval      w_gtin = 0
6.34 c                   if        w_lv_vare <> *blank
6.34 c                   eval      vvvare = w_lv_vare
6.34 c                   else
6.23 c                   eval      vvarl3_nobb = fwbnob
6.23 c     vvarl3_key    chain     vvarl3
6.34 c                   endif
6.34 c                   if        vvvare <> *blank
6.23 c                   eval      vvenl2_vare = vvvare
6.23 c                   eval      vvenl2_saen = 1
6.23 c     vvenl2_key    chain     vvenl2
6.23 c                   if        %found(vvenl2)
6.23 c                   call      'VE714R '
6.23 c                   parm                    w_firm
6.23 c                   parm                    vvvare
6.23 c                   parm                    veenhe
6.23 c                   parm                    w_gtin
6.23 c                   parm                    w_stat
6.23 c                   endif
6.23 c                   endif
6.25  *
6.25  * sjekker om gtin-nr er Trelastindustriens gtin-nr -
6.25  * da skal det ikke legges fordi disse ikke har dok.
6.25  *
6.25 c                   if        w_gtin <> 0
6.30 c                   eval      jvpkl3_gtin = w_gtin
6.30 c     jvpkl3_key    setll     jvpkl3
6.30 c     jvpkl3_key    reade     jvpkl3
6.30 c                   dow       not %eof(jvpkl3)
6.30 c                   if        jwldor = 60764
6.25 c                   eval      w_gtin = 0
6.25 c                   leavesr
6.25 c                   endif
6.30 c     jvpkl3_key    reade     jvpkl3
6.25 c                   enddo
6.25 c                   endif
6.23  *
6.23 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_bnum
      *
      * Key for oppslag på FBDO
      *
     c     fbdol1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fbdol1_bnum
      *
      * Key for oppslag på RKUN
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på RAA8
      *
     c     raa8l1_key    klist
     c                   kfld                    w_firm
      *
      * Key for propertiesfil
      *
     c     afpslr_key    klist
     c                   kfld                    afpslr_ufil
     c                   kfld                    afpslr_uide
6.21  *
6.21  * Key for kundeprosjekt
6.21  *
6.21 c     fkprl1_key    klist
6.21 c                   kfld                    w_firm
6.21 c                   kfld                    fkprl1_kund
6.21 c                   kfld                    fkprl1_kpro
6.23  *
6.23  * Key for les av vare enhet
6.23  *
6.23 c     vvenl2_key    klist
6.23 c                   kfld                    w_firm
6.23 c                   kfld                    vvenl2_vare
6.23 c                   kfld                    vvenl2_saen
6.23  *
6.23  * Key for les av vare
6.23  *
6.23 c     vvarl3_key    klist
6.23 c                   kfld                    w_firm
6.23 c                   kfld                    vvarl3_nobb
6.25  *
6.25  * Key for les av vare
6.25  *
6.30 c     jvpkl3_key    klist
6.30 c                   kfld                    jvpkl3_gtin
7.01  *
7.01  * Key for les av fkp2 register
7.01  *
7.01 c     fkp2l1_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    fkp2l1_kund
7.01 c                   kfld                    fkp2l1_kpro
7.01  *
7.01  * Key for les av rukp register
7.01  *
7.01 c     rukpl1_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    rukpl1_kund
      *
     c                   eval      w_firm = l_firm
     c                   eval      fbdol1_bnum = p_bnum
     c                   eval      b_first = *on
     c                   eval      w_tell = 0
8.02  *
8.02  * Endring 7.2i Tillatt med returordre og kontantkunde
8.02  *
8.02   CallP CO402R(l_filg:w_firm:0:'BYGGDOK_VAREKUNDE':
8.02                   co402_verdi1:co402_verdi2);
8.02   if co402_verdi1 = '1';
8.02     u_vark = *on;
8.02   endif;
      *
     c                   endsr
