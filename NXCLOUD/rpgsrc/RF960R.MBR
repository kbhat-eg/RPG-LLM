      /TITLE  ASOKON Opprydding i filer
     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON
      * Program . . . . : RF960R
      * Beskrivelse . . : Endre postnummer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       060307 KOB  Nytt program
6.10  * V6.10 230909 BSA  Lagt inn sporingsfelter
6.11  * V6.10 060510 BSA  Endret funksjonalitet rundt sporing
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frf960d    cf   e             workstn
     frkunlu    uf   e           k disk    rename(rkunpfr:rkunlur)
     frlevlu    uf   e           k disk    rename(rlevpfr:rlevlur)
     fapospf    if   e           k disk
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d w_firm          s              3  0
     d w_ponr          s                   like(rkponr)
     d w_apnr          s              4
     d w_text          s             20
      *
     d apospf_ponr     s                   like(apponr)
      *
      /eject
      *
     c     dummy         tag
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * A1BLD Behandle bilde for valg av utskriftskriteria              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     a1taga        tag
      *
     c                   exfmt     a1bld
      *
     c                   eval      *in31 = *off
      *
      * Gå ut
      *
     c                   if        *inkc = *on
     c                   goto      xslutt
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd = *on
     c                   exsr      spr_rut
     c                   goto      a1taga
     c                   endif
      *
      * Oppdater
      *
     c                   if        *inkg = *on
      *
     c                   exsr      oppdat
      *
     c                   eval      a1fpnr = 0
     c                   eval      a1tpnr = 0
     c                   eval      a1ftxt  = ' '
     c                   eval      a1ttxt  = ' '
      *
     c                   goto      a1taga
     c                   endif
      *
      * Sjekke om postnr. er utfyllt
      *
     c                   if        a1fpnr = *zero  or
     c                             a1tpnr = *zero
     c                   move      *on           *in66
     c                   move      *on           *in81
     c                   goto      a1taga
     c                   endif
      *
      * Hent poststed fra
      *
     c                   eval      w_ponr = a1fpnr
     c                   exsr      hent_sted
     c                   eval      a1ftxt  = w_text
      *
      * Hent poststed til
      *
     c                   eval      w_ponr = a1tpnr
     c                   exsr      hent_sted
     c                   eval      a1ttxt  = w_text
      *
     c                   goto      a1taga
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for forespørsel
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spr_rut       begsr
      *
      * Call program for gammelt post-sted
      *
     c                   if        w_afld = 'A1FPNR'
     c                   call      'AF010R'
     c                   parm                    w_ponr
      *
     c                   if        w_ponr <> *zero
     c                   eval      a1fpnr = w_ponr
     c                   exsr      hent_sted
     c                   eval      a1ftxt  = w_text
     c                   endif
      *
     c                   move      *on           *in32
     c                   goto      end_spr_rut
     c                   endif
      *
      * Call program for nytt post-sted
      *
     c                   if        w_afld = 'A1TPNR'
     c                   call      'AF010R'
     c                   parm                    w_ponr
      *
     c                   if        w_ponr <> *zero
     c                   eval      a1tpnr = w_ponr
     c                   exsr      hent_sted
     c                   eval      a1ttxt  = w_text
     c                   endif
      *
     c                   move      *on           *in32
     c                   goto      end_spr_rut
     c                   endif
      *
     c     end_spr_rut   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hent poststed
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_sted     begsr
      *
     c                   move      w_ponr        apospf_ponr
     c     apospf_key    chain     apospfr
      *
     c                   if        %found
     c                   eval      w_text = apsted
     c                   else
     c                   eval      w_text  = 'Ugyldig poststed'
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av registere
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdat        begsr
      *
      * Kunderegister
      *
     c     rkunlu_key    setll     rkunlu                                 90
     c     rkunlu_key    reade     rkunlu                                 90
     c                   dow       *in90 = *off
      *
     c                   if        rkponr = a1fpnr
     c                   eval      rkponr = a1tpnr
     c                   move      a1tpnr        w_apnr
     c                   eval      rksted = w_apnr + ' ' + a1ttxt
6.10 c*                  time                    rkedat
6.10 c*                  time                    rketim
6.10 c*                  eval      rkesig = l_user
6.11 c                   time                    rk2dpt
6.11 c                   eval      rlepgm = 'RF960R    '
     c                   update    rkunlur
     c                   endif
      *
     c     rkunlu_key    reade     rkunlu                                 90
     c                   enddo
      *
      * Leverandørregister
      *
     c     rlevlu_key    setll     rlevlu                                 90
     c     rlevlu_key    reade     rlevlu                                 90
     c                   dow       *in90 = *off
      *
     c                   if        rlponr = a1fpnr
     c                   eval      rlponr = a1tpnr
     c                   move      a1tpnr        w_apnr
     c                   eval      rlsted = w_apnr + ' ' + a1ttxt
6.10 c*                  time                    rledat
6.10 c*                  time                    rletim
6.10 c*                  eval      rlesig = l_user
6.11 c                   time                    rl2dpt
6.11 c                   eval      rlepgm = 'RF960R    '
     c                   update    rlevlur
     c                   endif
      *
     c     rlevlu_key    reade     rlevlu                                 90
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for postnummer-register
      *
     c     apospf_key    klist
     c                   kfld                    apospf_ponr
      *
     c     rkunlu_key    klist
     c                   kfld                    l_firm
      *
     c     rlevlu_key    klist
     c                   kfld                    l_firm
      *
     c                   endsr
