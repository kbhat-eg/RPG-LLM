     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASSTAT
      * Program . . . . : SF850R
      * Beskrivelse . . : Salgsstatistikk, eksport til fil - parameter
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       211101 HKO  Nytt program
      *       220402 HKO  Lagt inn utplukk på avdeling
      *                   Jobben kjøres i batch
      *       120603 HKO  Kan velge å ta med tidligere rapporterte salg
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fra07pf    if   e           k disk    rename(ra07pfr:ra07pfr)
      *
     fsf850d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Datastruktur for parameterliste -ut
      *
     d                 ds
     d d_list                        40
     d  d_firm                        3  0 overlay(d_list:1)
     d  d_avde                        4  0 overlay(d_list:4)
     d  d_fdat                         d   overlay(d_list:8)  datfmt(*iso)
     d  d_tdat                         d   overlay(d_list:18) datfmt(*iso)
     d  d_tidl                        1  0 overlay(d_list:28)
      *
      * Definering av variable i nøkler
      *
     d ra07pf_gkod     s                   like(ragkod)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s              3  0
     d w_fdat          s               d   datfmt(*iso)
     d w_tdat          s               d   datfmt(*iso)
     d w_posi          s              2  0
     d w_leng          s              2  0
     d w_suff          s             12
      *
     d w_gtxt          s                   like(ragtxt)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Behandler bildet
      *
     c     b1taga        tag
      *
     c                   exfmt     b1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   endsl
      *
      * Spørring
      *
     c                   if        *inkd
     c                   exsr      spØrring
     c                   goto      b1taga
     c                   endif
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1taga
     c                   endif
      *
      * Tømmer fil
      *
     c                   eval      b1file = %trim(b1file)
     c                   eval      b1fldr = %trim(b1fldr)
      *
     c                   call      'SF850C'
     c                   parm                    b1file
     c                   parm                    b1fldr
     c                   parm                    b_feil
      *
     c                   if        b_feil = *on
     c                   exfmt     b1msg
     c                   goto      b1taga
     c                   endif
      *
      * Kaller program for oppretting av eksportfil
      *
     c                   eval      d_firm = w_firm
     c                   eval      d_avde = b1avde
     c                   eval      d_fdat = w_fdat
     c                   eval      d_tdat = w_tdat
     c                   eval      d_tidl = b1tidl
      *
     c                   call      'SF851C'
     c                   parm                    d_list
     c                   parm                    b1file
     c                   parm                    b1fldr
      *
     c                   exfmt     b1sbm
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
     c                   if        w_afld = 'B1AVDE'
     c                   call      'RA507R'
     c                   parm                    w_firm
     c                   parm                    b1avde
     c                   parm                    w_gtxt
     c                   goto      end_spØrr
     c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
     c                   if        b1avde <> 0
     c                   eval      ra07pf_gkod = b1avde
     c     ra07pf_key    chain     ra07pf
     c                   if        %found = *off
     c                   eval      *in40 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
     c                   if        b1fdat = 0
     c                   eval      *in31 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c     *dmy          test(d)                 b1fdat                 32
     c                   if        *in32 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c                   if        b1tdat = 0
     c                   eval      *in33 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c     *dmy          test(d)                 b1tdat                 34
     c                   if        *in34 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c     *dmy          move      b1fdat        w_fdat
     c     *dmy          move      b1tdat        w_tdat
     c                   if        w_fdat > w_tdat
     c                   eval      *in35 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c                   if        b1file = *blank
     c                   eval      *in36 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c                   if        b1fldr = *blank
     c                   eval      *in37 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * Sjekker syntaks for filnavn
      *
     c                   eval      b1file = %trim(b1file)
      *
     c     '.'           scan      b1file        w_posi
     c                   if        w_posi = 0
     c                   eval      w_leng = %len(%trim(b1file))
     c                   else
     c                   eval      w_leng = w_posi - 1
     c                   endif
      *
     c                   if        w_leng = 0 or
     c                             w_leng > 8
     c                   eval      *in38 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c                   if        w_posi > 0
     c                   eval      w_suff = %subst(b1file:w_posi+1:12-w_posi)
     c                   eval      w_leng = %len(%trim(w_suff))
     c                   if        w_leng = 0 or
     c                             w_leng > 3
     c                   eval      *in38 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på avdeling
      *
     c     ra07pf_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra07pf_gkod
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
      * Initierer skjermbildet
      *
     c                   eval      b1file = 'Sxxyyyy.TXT'
     c                   eval      b1fldr = 'NOBB'
      *
     c                   endsr
