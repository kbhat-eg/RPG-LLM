      /TITLE  Utskrift av kundefordringer  - kunder
     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RK648R                                              *
      * Beskrivelse . . : Utskrift av kundefordringer  - kunder               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      * V5.50     120907  Bare comp.                                     HFL  *
6.10  * V6.10     051160  Test på om det er innen fra periode/år      *       *
pdf   * R6.10 291110 BSA  Innført PDF utskrift. Innebærer primært å finne     *
pdf   *                   generert spool, og kall av AP620R med dette som     *
pdf   *                   parametre.                                          *
6.20  * R6.20     090911  Rekompilert pga endring i RPARRKDS            *NHO
6.21  * 6.20  061212 bsa  Tillegg for å berike metataggger
6.30  * R6.30 180816 bsa  Rokkert på rekkefølge av close printerfile
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frkw1l1    if   e           k disk    rename(rkw1pfr:rkw1l1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     frktrl1    if   e           k disk    rename(rktrpfr:rktrl1r)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     fra01l1    if   e           k disk    rename(ra01pfr:ra01l1r)
     frk648p    o    e             printer oflind(*in30)
pdf  f                                     usropn
      *
     d txt             s             20    dim(4) ctdata perrcd(1)              Tekst ukjent
     d kna             s             30    dim(10)                              KUNDE NAVN
      *
      /eject
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      * Data struktur
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      *
     d                 ds
     d wkwrec                  2    192
     d  wkfirm                        3  0 overlay(wkwrec)
     d  wkkund                        6  0 overlay(wkwrec:4)
     d  wknavn                       30    overlay(wkwrec:10)
      *
     d w_sald          s                   like(rnbelØ)
     d w_gsal          s                   like(rnbelØ)
     d w_gval          s                   like(rnvalb)
     d w_kjØp          s                   like(rnvalb)
     d bkjØp           s                   like(rnbelØ)
     d btink           s                   like(rnbelØ)
     d btsal           s                   like(rnbelØ)
     d tkjØp           s                   like(rnbelØ)
     d w_pdat          s              6  0
     d w_sdat          s              6  0
     d w_bdat          s              6  0
     d w_fdat          s              6  0
6.10 d w_gyld          s              1
      *
     d brsum           s              1
     d brtxt           s             20
     d n               s              2  0
     d wkun            s              7  0
      *
     d pavdsx          s              3
     d pkatsx          s              3
     d pselsx          s              3
     d psvalx          s              3
     d psortx          s             10
      *
     d w_antkund       s              6  0
     d w_antusal       s              6  0
     d w_antpost       s              6  0
      *
     d wfrper          s              2  0
     d wrmnd           s              2  0
      *
     d w_firm          s                   like(rkfirm)
      *
     d rkunl1_kund     s                   like(rkkund)
     d rktrl1_kund     s                   like(rkkund)
     d ra01l1_akod     s                   like(raakod)
      *
pdf  d p_btyp          s            100
      *
pdf  d splfname2       s             10a
pdf  d jobname2        s             10a
pdf  d username2       s             10a
pdf  d jobnbr2         s              6a
pdf  d splfnbr2        s             10i 0
pdf  d splfname3       s             10a
pdf  d jobname3        s             10a
pdf  d username3       s             10a
pdf  d jobnbr3         s              6a
pdf  d splfnbr3        s             10i 0
6.21 d p_tagg0         s             20
6.21 d p_tagg0val      s             50
6.21 d p_tagg1         s             20
6.21 d p_tagg1val      s             50
6.21 d p_tagg2         s             20
6.21 d p_tagg2val      s             50
6.21 d p_tagg3         s             20
6.21 d p_tagg3val      s             50
6.21 d p_tagg4         s             20
6.21 d p_tagg4val      s             50
6.21 d p_tagg5         s             20
6.21 d p_tagg5val      s             50
6.21 d p_tagg6         s             20
6.21 d p_tagg6val      s             50
6.21 d p_tagg7         s             20
6.21 d p_tagg7val      s             50
6.21 d p_tagg8         s             20
6.21 d p_tagg8val      s             50
6.21 d p_tagg9         s             20
6.21 d p_tagg9val      s             50
6.21 d p_refn          s             50
6.21 d p_dspl          s            100
pdf   *
pdf  d qsprilsp        pr                  extpgm('QSPRILSP')
pdf  d rcvvar                     32767a   options(*varsize)
pdf  d rcvvarlen                     10i 0 const
pdf  d format                         8a   const
pdf  d errorcode                  32767a   options(*varsize)
pdf   *
pdf  d sprl0100        ds
pdf  d  bytesrtn                     10i 0
pdf  d  bytesavl                     10i 0
pdf  d  splfname                     10a
pdf  d  jobname                      10a
pdf  d  username                     10a
pdf  d  jobnbr                        6a
pdf  d  splfnbr                      10i 0
pdf  d  systemname                    8a
pdf  d  createdate                    7a
pdf  d                                1a
pdf  d  createtime                    6a
pdf   *
pdf  d errorcode       ds
pdf  d  bytesprov                    10i 0 inz(0)
pdf  d  byteavail                    10i 0 inz(0)
pdf   *
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      * LOCAL DATA
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     d/COPY QCPYLESRC,RPARRKDS
pdf  d l_poff                584    584
pdf  d l_bach                595    635
pdf  d l_skje                637    637  0
      *
      /eject
      *
      * Leser transfile
6.10 c                   eval      w_gyld = ' '
      *
     c                   write     h101
      *
     c     nyles         tag
      *
     c                   read      rkw1l1                                 60
      *
     c                   if        *in60 = *on
     c                   eval      *inlr = *on
     c                   goto      slutt
     c                   endif
      *
     c                   movel     rwkrec        wkwrec
     c                   eval      page = 0
      *
      * Leser kunde
      *
     c                   move      wkkund        rkunl1_kund
     c     rkunl1_key    chain     rkunl1                             61
      *
      * Leser kundeposteringer
      *
     c                   exsr      les_kuntr
      *
      * Telle kunder
      *
     c                   eval      w_antkund = w_antkund + 1
      *
     c                   if        w_sald <> *zeros
     c                   eval      w_antmsal = w_antmsal + 1
     c                   else
     c                   eval      w_antusal = w_antusal + 1
     c                   endif
      *
      * Skriv ut totaler pr kunde
      *
     c                   if        w_antpost = 0 or
KOB  c                             w_sald <> 0
     c***                write     h102
     c                   endif
      *
     c                   if        w_antpost > 0
KOB  c***                          w_sald <> 0
     c                   eval      btsal = btsal + w_sald
     c                   write     h102
     c                   endif
      *
     c                   eval      w_sald = *zeros                              Saldo
      *
     c                   goto      nyles
      *
     c     slutt         tag
      *
6.10  * Skriver header dersom det er gyldige poster
6.10 c                   if        w_gyld <> *blank
6.10  *
6.10 c                   write     h101
6.10  *
6.10 c                   write     t101
6.10 c                   write     t102
6.10  *
6.10  * Skriver  parameter - Utvalg enkeltkunder
6.10  *
6.10 c                   eval      n = n + 1
6.10 c                   dou       n > 10                                       ..............
6.10 c                   if        kun(n) <> *zeros                                          .
6.10 c                   eval      kun01 = kun(n)
6.10 c                   eval      kna01 = kna(n)
6.10 c                   write     t102k                                                     .
6.10 c                   endif                                                               .
6.10 c                   eval      n = n + 1                                                 .
6.10 c                   enddo                                                  ..............
6.10 c                   end
      *
pdf   * Generer xml for videre utskrift
pdf  clr                 if        l_poff = '1'
pdf  clr                 exsr      spoolnbr
6.30 clr                 close     rk648p
6.30 clr                 exsr      xml_create
pdf  clr                 exsr      pdf_preview
pdf   * Avslutt jobbiden
pdf  clr                 call      'AB710R'
pdf  c                   parm                    l_bach
pdf  clr                 endif
      *
      /eject
      *
      * Leser kundetransaksjoner
      *
     c     les_kuntr     begsr
      *
     c                   eval      w_antpost = *zeros
     c                   eval      w_sdat = *zeros
     c                   eval      w_pdat = *zeros
     c                   eval      *in41 = *off
      *
     c                   eval      rktrl1_kund = rkkund
     c     rktrl1_key    setll     rktrl1
     c     rktrl1_key    reade     rktrl1                                 62
      *
     c                   dow       *in62 = *off
      *
6.10  * År/periode..fra
6.10 c                   if        rnraar < paarf
6.10 c                   goto      nesttr
6.10 c                   endif
      *
6.10 c                   if        rnraar = paarf and
6.10 c                             rnrper < pperf
6.10 c                   goto      nesttr
6.10 c                   endif
      *
      * År/periode
      *
     c                   if        rnraar > paart
     c                   goto      nesttr
     c                   endif
      *
     c                   if        rnraar = paart and
     c                             rnrper > ppert
     c                   goto      nesttr
     c                   endif
      *
      * Posten er oppgjort
      *
     c                   if        rnrest = *zeros
     c                   goto      nesttr
     c                   endif
      *
      * Beregn saldo
      *
     c                   eval      w_sald = w_sald + rnrest                     Saldo
      *
      * Skriver heading ny side
      *
     c                   if        *in41 = *off
      *
      * Skriver kundeinformasjon
      *
     c                   write     h10s
      *
     c                   eval      *in41 = *on
     c                   endif
      *
      * Overflow ?
      *
     c                   if        *in30 = *on
     c                   write     h101
     c                   eval      *in30 = *off
     c                   endif
      *
      * Skriver detalj-linje
      *
     c     *dmy          move      rnbdat        w_bdat
     c     *dmy          move      rnfdat        w_fdat
     c                   eval      w_antpost = w_antpost + 1
6.10 c                   eval      w_gyld = '1'
      *
     c                   write     d101
      *
     c     nesttr        tag
      *
     c     rktrl1_key    reade     rktrl1                                 62
     c                   enddo                                                     fordeling
      *
     c                   endsr
      *
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Finner spool som er generert av jobben
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     spoolnbr      begsr
pdf   /Free
pdf
pdf            QSPRILSP( SPRL0100
pdf                    : %size(SPRL0100)
pdf                    : 'SPRL0100'
pdf                    : errorcode );
pdf   /End-free
pdf  c*
pdf   *
pdf  c                   endsr
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Kaller eget program for generering av xml fil. Eget pgm         *
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     xml_create    begsr
pdf   *
pdf  c                   eval      splfname2 = *blanks
pdf  c                   eval      splfname3 = *blanks
pdf  c                   eval      jobname2 = *blanks
pdf  c                   eval      jobname3 = *blanks
pdf  c                   eval      username2 = *blanks
pdf  c                   eval      username3 = *blanks
pdf  c                   eval      jobnbr2 = *blanks
pdf  c                   eval      jobnbr3 = *blanks
pdf  c                   eval      splfnbr2 = *zeros
pdf  c                   eval      splfnbr3 = *zeros
pdf  c                   eval      p_btyp = 'ÅPEN POST LISTE'
6.21 c                   eval      p_dspl = 'Åpen post liste kunder, '+
6.21 c                                      ' kjørt dato: '+
6.21 c                                      %char(udate)
6.21 c                   eval      p_refn = 'KAP-' + %char(udate)
6.21 c                   eval      p_tagg0 = *blanks
6.21 c                   eval      p_tagg0val = *blanks
6.21 c                   eval      p_tagg1 = *blanks
6.21 c                   eval      p_tagg1val = *blanks
6.21 c                   eval      p_tagg2 = *blanks
6.21 c                   eval      p_tagg2val = *blanks
6.21 c                   eval      p_tagg3 = *blanks
6.21 c                   eval      p_tagg3val = *blanks
6.21 c                   eval      p_tagg4 = *blanks
6.21 c                   eval      p_tagg4val = *blanks
6.21 c                   eval      p_tagg5 = *blanks
6.21 c                   eval      p_tagg5val = *blanks
6.21 c                   eval      p_tagg6 = *blanks
6.21 c                   eval      p_tagg6val = *blanks
6.21 c                   eval      p_tagg7 = *blanks
6.21 c                   eval      p_tagg7val = *blanks
6.21 c                   eval      p_tagg8 = *blanks
6.21 c                   eval      p_tagg8val = *blanks
6.21 c                   eval      p_tagg9 = *blanks
6.21 c                   eval      p_tagg9val = *blanks
pdf   *
6.21 c**                 call      'AP620R'
6.21 c                   call      'AP627R'
pdf  c                   parm                    splfname
pdf  c                   parm                    jobname
pdf  c                   parm                    username
pdf  c                   parm                    jobnbr
pdf  c                   parm                    splfnbr
pdf  c                   parm                    splfname2
pdf  c                   parm                    jobname2
pdf  c                   parm                    username2
pdf  c                   parm                    jobnbr2
pdf  c                   parm                    splfnbr2
pdf  c                   parm                    splfname3
pdf  c                   parm                    jobname3
pdf  c                   parm                    username3
pdf  c                   parm                    jobnbr3
pdf  c                   parm                    splfnbr3
6.21 c                   parm                    p_tagg0
6.21 c                   parm                    p_tagg0val
6.21 c                   parm                    p_tagg1
6.21 c                   parm                    p_tagg1val
6.21 c                   parm                    p_tagg2
6.21 c                   parm                    p_tagg2val
6.21 c                   parm                    p_tagg3
6.21 c                   parm                    p_tagg3val
6.21 c                   parm                    p_tagg4
6.21 c                   parm                    p_tagg4val
6.21 c                   parm                    p_tagg5
6.21 c                   parm                    p_tagg5val
6.21 c                   parm                    p_tagg6
6.21 c                   parm                    p_tagg6val
6.21 c                   parm                    p_tagg7
6.21 c                   parm                    p_tagg7val
6.21 c                   parm                    p_tagg8
6.21 c                   parm                    p_tagg8val
6.21 c                   parm                    p_tagg9
6.21 c                   parm                    p_tagg9val
pdf  c                   parm                    l_bach
pdf  c                   PARM                    p_btyp
6.21 c                   PARM                    p_refn
6.21 c                   PARM                    p_dspl
pdf   *
pdf  c                   endsr
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Subrutiner for XML/jasper integrasjon - Skjermvisning           *
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     pdf_preview   begsr
pdf  c                   if        l_skje = 1
pdf   * Vi kaller program for launch av PDF i browser
pdf  c                   call      'AP621R'
pdf  c                   parm                    l_bach
pdf   *
pdf  c                   endif
pdf   *
pdf  c                   endsr
pdf   *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c                   eval      brtxt = *blank
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
     c     rktrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rktrl1_kund
      *
     c     ra01l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra01l1_akod
      *
      * Henter kodeopplysninger ######################### START ######
      *
pdf  c                   open      rk648p
     c                   eval      *in05 = *off
     c                   eval      *in06 = *off
     c                   eval      *in07 = *off
     c                   eval      *in08 = *off
     c                   eval      *in09 = *off
     c                   eval      *in10 = *off
      *
     c                   move      l_firm        w_firm
      *
     c     afirl1_key    chain     afirl1                             99
     c                   eval      *in05 = *on
      *
     c                   if        *in99 = *off                                          .....
      *
     c                   if        aaftlf <> *blanks                                         .
     c                   eval      *in06 = *on                                               .
     c                   endif                                                               .
      *                                                                 .
     c                   if        aaffax <> *blanks                                         .
     c                   eval      *in07 = *on                                               .
     c                   endif                                                               .
      *                                                                 .
     c                   if        aafbgi <> *zeros                                          .
     c                   eval      *in08 = *on                                               .
     c                   endif                                                               .
      *                                                                 .
     c                   if        aafftn <> *zeros                                          .
     c                   eval      *in09 = *on                                               .
     c                   endif                                                               .
      *                                                                 .
     c                   if        aamvap = 0                                                .
     c                   eval      *in10 = *on                                               .
     c                   endif                                                               .
      *                                                                 .
     c                   endif                                                           .....
      *
      * Henter kodeopplysninger ######################### SLUTT ######
      *
      * Sjekker om det ønskes bruddsum ################## START ######
      *
     c                   if        pavds <> *blank
     c                   movel     'X'           brsum
     c                   endif
      *
     c                   if        pkats <> *blank
     c                   movel     'X'           brsum
     c                   endif
      *
     c                   if        psels <> *blank
     c                   movel     'X'           brsum
     c                   endif
      *
      * Valutabeløp skrives
      *
     c                   if        pvalu = *blank                                        .....
     c                   movel     'Nei'         psvalx                                      .
     c                   eval      *in15 = *off                                              .
     c                   else                                                                .
     c                   eval      *in15 = *on                                               .
     c                   movel     'Ja '         psvalx                                      .
     c                   endif                                                           .....
      *
      * Enkeltkunder
      *
     c                   xfoot     kun           wkun
     c                   eval      n = 1
      *
     c                   if        wkun <> 0                                             .....
      *
     c                   dou       n > 10                                                    .
      *
     c                   if        kun(n) <> *zeros                                          .
     c                   move      kun(n)        rkunl1_kund                                 .
     c     rkunl1_key    chain     rkunl1                             61                     .
      *
     c                   if        *in61 = *off
     c                   move      rknavn        kna(n)                                      .
     c                   else
     c                   move      txt(4)        kna(n)                                      .
     c                   endif                                                               .
      *                                                                 .
     c                   endif                                                               .
     c                   eval      n = n + 1                                                 .
     c                   enddo                                                               .
     c                   endif                                                           .....
      *                                                                 .
     c                   eval      n = 1
      *
      * Sum pr. avdeling
      *                                                                 .
     c                   if        pavds = *blank                                        .....
     c                   movel     'Nei'         pavdsx                                      .
     c                   else                                                                .
     c                   movel     'Ja '         pavdsx                                      .
     c                   endif                                                           .....
      *                                                                 .
      * Sum pr. kategori
      *                                                                 .
     c                   if        pkats = *blank                                        .....
     c                   movel     'Nei'         pkatsx                                      .
     c                   else                                                                .
     c                   movel     'Ja '         pkatsx                                      .
     c                   endif                                                           .....
      *                                                                 .
      * Sum pr. selger
      *                                                                 .
     c                   if        psels = *blank                                        .....
     c                   movel     'Nei'         pselsx                                      .
     c                   else                                                                .
     c                   movel     'Ja '         pselsx                                      .
     c                   endif                                                           .....
      *                                                                 .
      * Sortering
      *                                                                 .
     c                   if        psort = ' '                                           .....
     c                   movel     'Nummeris'    psortx                                      .
     c                   move      'k '          psortx                                      .
     c                   else                                                                .
     c                   movel     'Alfabeti'    psortx                                      .
     c                   move      'sk'          psortx                                      .
     c                   endif                                                           .....
      *
     c                   endsr
      **
**   MELDINGER
Avdeling
Kategori
Selger
*** U K J E N T ***
