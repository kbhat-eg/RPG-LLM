     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JM100R
      * Beskrivelse . . : Kampanje-hode, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       270997 HKO  Nytt program
5.60  *       060109 bof  Rettelse vedr. dato
6.30  *       070714 bkv  Første gang overført dato og opprettet dato
6.40  *       110516 bof  Utvidet med prisgruppe
7.xx  * 7.00  030117 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
8.02  *PRG2   130622 bof utvidet prisgruppe fra 1 til 2 pos.
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjkahl1    if   e           k disk    rename(jkahpfr:jkahl1r)
     fjkahl2    if   e           k disk    rename(jkahpfr:jkahl2r)
     fjkahl3    if   e           k disk    rename(jkahpfr:jkahl3r)
     fjkahl4    if   e           k disk    rename(jkahpfr:jkahl4r)
     fjkahlr    if   e           k disk    rename(jkahpfr:jkahlrr)
     fjkahlu    uf a e           k disk    rename(jkahpfr:jkahlur)
     fjkavlr    if   e           k disk    rename(jkavpfr:jkavlrr)
     fjkavlu    uf a e           k disk    rename(jkavpfr:jkavlur)
8.02 fra30lr    if   e           k disk    rename(ra30pfr:ra30lrr)
      *
     fjm100d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d jkahl1_kamp     s                   like(jmkamp)
     d jkahl4_prgr     s                   like(jmprgr)
     d jkahl2_fdat     s                   like(jmfdat)
     d jkahl3_ansv     s                   like(jmansv)
     d jkahlr_kamp     s                   like(jmkamp)
     d jkahlr_prgr     s                   like(jmprgr)
     d jkahlu_kamp     s                   like(jmkamp)
     d jkahlu_prgr     s                   like(jmprgr)
     d jkavlr_vkam     s                   like(jmvkam)
     d jkavlr_vprg     s                   like(jmvprg)
     d jkavlu_vkam     s                   like(jmvkam)
     d jkavlu_vprg     s                   like(jmvprg)
8.02 d ra30lr_dkod     s                   like(redkod)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_fdat          s               d   datfmt(*iso)
     d w_tdat          s               d   datfmt(*iso)
      *
     d w_firm          s                   like(jmfirm)
8.02 d w_pnav          s                   like(redtxt)
     d w_prgr          s                   like(jmprgr)
      *
     d s_kamp          s                   like(jmkamp)
8.02 d s_prgr          s                   like(jmprgr)

      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inka
     c                   exsr      filter
     c                   goto      b2tagb
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2kamp <> *blank or
     c                             b2prgr <> *blank or
     c                             b2fdat <> 0 or
     c                             b2ansv <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'F'
     c     *dmy          move      b1fdat        jkahl2_fdat
     c     jkahl2_key    setll     jkahl2
     c                   when      w_seqe = 'A'
     c                   eval      jkahl3_ansv = b1ansv
     c     jkahl3_key    setll     jkahl3
     c                   when      w_seqe = 'P'
     c                   eval      jkahl4_prgr = b1prgr
     c     jkahl3_key    setll     jkahl3
     c                   other
     c                   eval      jkahl1_kamp = b1kamp
     c     jkahl1_key    setll     jkahl1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Priskode
      *
     c                   if        b2kamp <> *blank
     c                   eval      w_seqe = *blank
     c                   eval      jkahl1_kamp = b2kamp
     c     jkahl1_key    setll     jkahl1
     c                   goto      end_pos
     c                   endif
      *
      * Fra-dato
      *
     c                   if        b2fdat <> 0
     c     *dmy          test (d)                b2fdat                 70
     c                   if        *in70
     c                   goto      end_pos
     c                   endif
     c                   eval      w_seqe = 'F'
     c     *dmy          move      b2fdat        jkahl2_fdat
     c     jkahl2_key    setll     jkahl2
     c                   goto      end_pos
     c                   endif
      *
      * Ansvarlig
      *
     c                   if        b2ansv <> *blank
     c                   eval      w_seqe = 'A'
     c                   eval      jkahl3_ansv = b2ansv
     c     jkahl3_key    setll     jkahl3
     c                   goto      end_pos
     c                   endif
      *
      * Prisgruppe
      *
     c                   if        b2prgr <> *blank
     c                   eval      w_seqe = 'P'
     c                   eval      jkahl4_prgr = b2prgr
     c     jkahl4_key    setll     jkahl4
     c                   goto      end_pos
     c                   endif

      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2kamp = *blank
     c                   eval      b2fdat = 0
     c                   eval      b2ansv = *blank
     c                   eval      b2prgr = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
8.02 c                   eval      c1kamp = b1kamp
     c                   eval      c1prgr = b1prgr
     c                   exsr      xc2bld
8.02 c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
8.02 c                   eval      k1kamp = b1kamp
     c                   eval      k1prgr = b1prgr
     c                   exsr      xk1win
8.02 c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
8.02 c                   eval      d1kamp = b1kamp
     c                   eval      d1prgr = b1prgr
     c                   exsr      xd1win
8.02 c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1kamp = b1kamp
     c                   eval      c1prgr = b1prgr
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vedlikehold av kampanjepriser
      *
     c                   if        b1valg = 7
     c                   call      'JM110R'
     c                   parm                    w_firm
     c                   parm                    b1kamp
     c                   parm                    b1prgr
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off

      *
     c                   eval      c1kamp = b2kamp
     c                   eval      c1prgr = b2prgr
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Spørring
      *
     c                   if        *inkd
8.02 c                   exsr      spØrring
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1kamp = *blank
     c                   goto      end_c1bld
     c                   endif
8.02  * Valider
8.02  *
8.02  * validering prisgruppe
8.02  *
8.02 c                   if        c1prgr <> *blank
8.02 c                   eval      ra30lr_dkod = c1prgr
8.02 c     ra30lr_key    chain     ra30lr
8.02 c                   if        not %found(ra30lr)
8.02 c                   eval      *in32 = *on
8.02 c                   goto      c1taga
8.02 c                   endif
8.02 c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      jkahlr_kamp = c1kamp
     c                   eval      jkahlr_prgr = c1prgr
     c     jkahlr_key    chain     jkahlr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
8.02  *
     c                   eval      c1kamp = *blank
8.02 c                   eval      c1prgr = *blank
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2kamp = *blank
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      jkahlr_kamp = c1kamp
     c                   eval      jkahlr_prgr = c1prgr
     c     jkahlr_key    chain     jkahlr
      *
     c                   if        %found
     c*                  eval      c2besk = jmbesk
     c                   movel     jmbesk        c2besk
     c                   eval      c2ansv = jmansv
     c                   eval      c2eusr = jmeusr
     c                   eval      c2odat = 0
     c                   if        jmfdat <> *loval
     c     *dmy          move      jmfdat        c2fdat
     c                   else
     c                   eval      c2fdat = 0
     c                   endif
     c                   if        jmtdat <> *loval
     c     *dmy          move      jmtdat        c2tdat
     c                   else
     c                   eval      c2tdat = 0
     c                   endif
6.30 c                   if        jmooda <> *loval
6.30 c     *dmy          move      jmooda        c2ooat
6.30 c                   else
6.30 c                   eval      c2ooat = 0
6.30 c                   endif
     c                   if        jmedat <> *loval
     c     *dmy          move      jmedat        c2edat
     c                   else
     c                   eval      c2edat = 0
     c                   endif
     c                   if        jmodat <> *loval
     c     *dmy          move      jmodat        c2odat
     c                   endif
6.30 c                   if        jmfoda <> *loval
6.30 c     *dmy          move      jmfoda        c2foda
6.30 c                   endif
6.30 c                   else
     c                   eval      c2besk = *blank
     c                   eval      c2ansv = *blank
     c                   eval      c2fdat = 0
     c                   eval      c2tdat = 0
     c                   eval      c2odat = 0
6.30 c                   eval      c2foda = 0
     c                   eval      c2edat = 0
     c                   eval      c2eusr = *blank
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1kamp = k1kamp
     c                   eval      c1prgr = k1prgr
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2besk = *blank
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
     c                   if        c2fdat = 0
     c                   eval      *in32 = *on
     c                   goto      c2taga
     c                   endif
      *
     c     *dmy          test (d)                c2fdat                 33
     c                   if        *in33 = *on
     c                   goto      c2taga
     c                   endif
      *
     c                   if        c2tdat = 0
     c                   eval      *in34 = *on
     c                   goto      c2taga
     c                   endif
      *
     c     *dmy          test (d)                c2tdat                 35
     c                   if        *in35 = *on
     c                   goto      c2taga
     c                   endif
      *
     c     *dmy          move      c2fdat        w_fdat
     c     *dmy          move      c2tdat        w_tdat
     c                   if        w_fdat> w_tdat
     c                   eval      *in36 = *on
     c                   goto      c2taga
     c                   endif
      *
     c                   if        c2ansv = *blank
     c                   eval      *in37 = *on
     c                   goto      c2taga
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   jkahlur
      *
     c                   eval      jkahlu_kamp = c1kamp
8.02 c                   eval      jkahlu_prgr = c1prgr
     c     jkahlu_key    chain     jkahlur
      *
     c     *dmy          move      c2fdat        jmfdat
      *
     c*                  eval      jmbesk = c2besk
     c                   movel     c2besk        jmbesk
     c                   eval      jmansv = c2ansv
      *
     c     *dmy          move      c2fdat        jmfdat
     c     *dmy          move      c2tdat        jmtdat
     c                   time                    jmedat
     c                   time                    jmetim
     c                   eval      jmeusr = l_user
      *
     c                   if        %found
     c                   update    jkahlur
     c                   else
     c                   eval      jmfirm = w_firm
     c                   eval      jmkamp = c1kamp
     c                   eval      jmprgr = c1prgr
     c                   write     jkahlur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c*                  eval      b1besk = jmbesk
     c                   movel     jmbesk        b1besk
     c                   eval      b1ansv = jmansv
     c     *dmy          move      jmfdat        b1fdat
     c     *dmy          move      jmtdat        b1tdat
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      jkahlr_kamp = d1kamp
     c                   eval      jkahlr_prgr = d1prgr
     c     jkahlr_key    chain     jkahlr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      jkahlu_kamp = d1kamp
     c                   eval      jkahlu_prgr = d1prgr
     c     jkahlu_key    chain     jkahlur
      *
     c                   delete    jkahlur
      *
      * Sletter tilhørende kampanjepriser
      *
     c                   eval      jkavlu_vkam = jkahlu_kamp
     c                   eval      jkavlu_vprg = jkahlu_prgr
     c     jkavlu_key    setll     jkavlu
     c     jkavlu_key    reade     jkavlur
     c                   dow       not %eof
     c                   delete    jkavlur
     c     jkavlu_key    reade     jkavlur
     c                   enddo
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_kamp = k1kamp
     c                   eval      s_prgr = k1prgr
      *
      * Fyller kopierings-bildet
      *
     c                   eval      jkahlr_kamp = k1kamp
     c                   eval      jkahlr_prgr = k1prgr
     c     jkahlr_key    chain     jkahlr
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1kamp = *blank
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      jkahlr_kamp = k1kamp
     c                   eval      jkahlr_prgr = k1prgr
     c     jkahlr_key    chain     jkahlr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1kamp = s_kamp
     c                   eval      c1prgr = s_prgr
     c                   exsr      xc2bld
      *
      * Dersom kampanje er opprettet, kopieres kampanjepriser
      *
     c                   eval      jkahlr_kamp = k1kamp
     c                   eval      jkahlr_prgr = k1prgr
     c     jkahlr_key    chain     jkahlr
     c                   if        %found
     c                   eval      jkavlr_vkam = s_kamp
     c                   eval      jkavlr_vprg = s_prgr
     c     jkavlr_key    setll     jkavlr
     c     jkavlr_key    reade     jkavlr
     c                   dow       not %eof
     c                   eval      jmvkam = k1kamp
     c                   eval      jmvprg = k1prgr
     c                   write     jkavlur
     c     jkavlr_key    reade     jkavlr
     c                   enddo
     c                   endif
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'F'
     c                   read      jkahl2
     c                   when      w_seqe = 'A'
     c                   read      jkahl3
     c                   when      w_seqe = 'P'
     c                   read      jkahl4
     c                   other
     c                   read      jkahl1
     c                   endsl
      *
     c                   if        %eof or
     c                             jmfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'F'
     c                   eval      jkahl2_fdat = jmfdat
     c                   when      w_seqe = 'A'
     c                   eval      jkahl3_ansv = jmansv
     c                   when      w_seqe = 'P'
     c                   eval      jkahl4_prgr = jmprgr
     c                   other
     c                   eval      jkahl1_kamp = jmkamp
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1kamp = jmkamp
     c                   eval      b1prgr = jmprgr
     c*                  eval      b1besk = jmbesk
     c                   movel     jmbesk        b1besk
     c                   eval      b1ansv = jmansv
     c                   if        jmfoda <> *loval
     c                   eval      b1foda = 'X'
     c                   else
     c                   eval      b1foda = ''
     c                   endif
     c                   eval      b1odat = 0
5.60 c                   if        jmfdat <> *loval
     c     *dmy          move      jmfdat        b1fdat
5.60 c                   else
5.60 c                   eval      b1fdat = 0
5.60 c                   endif
5.60 c                   if        jmtdat <> *loval
     c     *dmy          move      jmtdat        b1tdat
5.60 c                   else
5.60 c                   eval      b1tdat = 0
5.60 c                   endif
      *
     c                   if        jmodat <> *loval
     c     *dmy          move      jmodat        b1odat
5.60 c                   else
5.60 c                   eval      b1odat = 0
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'F'
     c     jkahl2_key    setgt     jkahl2
     c                   readp     jkahl2
     c                   when      w_seqe = 'A'
     c     jkahl3_key    setgt     jkahl3
     c                   readp     jkahl3
     c                   when      w_seqe = 'P'
     c     jkahl4_key    setgt     jkahl4
     c                   readp     jkahl4
     c                   other
     c     jkahl1_key    setgt     jkahl1
     c                   readp     jkahl1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'F'
     c                   readp     jkahl2
     c                   when      w_seqe = 'A'
     c                   readp     jkahl3
     c                   when      w_seqe = 'P'
     c                   readp     jkahl4
     c                   other
     c                   readp     jkahl1
     c                   endsl
      *
     c                   if        %eof or
     c                             jmfirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'F'
     c                   eval      jkahl2_fdat = jmfdat
     c                   when      w_seqe = 'A'
     c                   eval      jkahl3_ansv = jmansv
     c                   when      w_seqe = 'P'
     c                   eval      jkahl4_prgr = jmprgr
     c                   other
     c                   eval      jkahl1_kamp = jmkamp
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'F'
     c     jkahl2_key    setll     jkahl2
     c                   when      w_seqe = 'A'
     c     jkahl3_key    setll     jkahl3
     c                   when      w_seqe = 'P'
     c     jkahl4_key    setll     jkahl4
     c                   other
     c     jkahl1_key    setll     jkahl1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
8.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.02  * Subrutine for behandling av spørring
8.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.02  *
8.02 c     spØrring      begsr
8.02  *
8.02  * prisgruppe
8.02  *
8.02 c                   if        w_afld = 'C1PRGR'
8.02 c                   call      'RA530R'
8.02 c                   parm                    w_firm
8.02 c                   parm                    c1prgr
8.02 c                   parm                    w_pnav
8.02 c                   leavesr
8.02 c                   endif
8.02  *
8.02 c                   if        w_afld = 'K1PRGR'
8.02 c                   call      'RA530R'
8.02 c                   parm                    w_firm
8.02 c                   parm                    k1prgr
8.02 c                   parm                    w_pnav
8.02 c                   leavesr
8.02 c                   endif
8.02 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Filter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     filter        begsr
      *
     c     f1tagb        tag
      *
     c                   exfmt     f1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
8.02  *
8.02  * validering prisgruppe
8.02  *
8.02 c                   if        w_prgr <> *blank
8.02 c                   eval      ra30lr_dkod = w_prgr
8.02 c     ra30lr_key    chain     ra30lr
8.02 c                   if        not %found(ra30lr)
8.02 c                   eval      *in34 = *on
8.02 c                   goto      f1tagb
8.02 c                   endif
8.02 c                   endif
8.02 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for posisjonering på priskode
      *
     c     jkahl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jkahl1_kamp
      *
      * Key for posisjonering på fra-dato
      *
     c     jkahl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jkahl2_fdat
      *
      * Key for posisjonering på ansvarlig
      *
     c     jkahl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jkahl3_ansv
      *
      * Key for posisjonering på ansvarlig
      *
     c     jkahl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jkahl4_prgr
      *
      * Key for les på priskode
      *
     c     jkahlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jkahlr_kamp
     c                   kfld                    jkahlr_prgr
      *
      * Key for oppdatering av priskode
      *
     c     jkahlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jkahlu_kamp
     c                   kfld                    jkahlu_prgr
      *
      * Key for les på kampanje-vare
      *
     c     jkavlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jkavlr_vkam
     c                   kfld                    jkavlr_vprg
      *
      * Key for oppdatering av kampanje-vare
      *
     c     jkavlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jkavlu_vkam
     c                   kfld                    jkavlu_vprg
8.02  *
8.02  * Key for oppslag på prisgruppe
8.02  *
8.02 c     ra30lr_key    klist
8.02 c                   kfld                    w_firm
8.02 c                   kfld                    ra30lr_dkod
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
8.02  *
8.02  * Henter prisgruppe
8.02  *
8.02 c                   call      'VL711R   '
8.02 c                   parm                    w_prgr
8.02  *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      jkahl1_kamp = *blank
     c     jkahl1_key    setll     jkahl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
