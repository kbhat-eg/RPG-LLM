     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : AN042R
      * Beskrivelse . . : Legger inn callprogram i QSTRUP
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
5.31  *  5.31 210205 AMD  Endret fra AN045R til AN044C
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fqclasp    ip   f  096        disk
     fqcllesrc  o  a f  096        disk
      *
      * Definering av datastrukturer
      *
     d                 ds
     d d_dato                         6  0
     d  d_aaar                        2  0 overlay(d_dato)
     d  d_mmnd                        2  0 overlay(d_dato:3)
     d  d_ddag                        2  0 overlay(d_dato:5)
      *                                                                     -
      * Definering av variable
      *                                                                     -
     d w_anta          s              4  0
     d w_tell          s              6  0
     d w_dato          s              6  0
      *
     iqclasp    ns  01
     i                                  1   96  record
     i                                 35   40  aspsbs
      *
      * Sjekker om ANTALL ER UTFYLT
      *
     c                   if        w_anta = 2
     c                   add       1             w_tell
     c                   except    add001
     c                   add       1             w_tell
     c                   except    add002
     c                   endif
      *
      * Sjekker om ANTALL ER UTFYLT
      *
     c                   if        w_anta <> *zero
     c                   add       1             w_anta
     c                   endif
      *
      * Sjekker om QBATCH finnes i QSTRUP
      *
     c                   if        w_anta = *zero
     c                   if        aspsbs = 'QBATCH'
     c                   z-add     1             w_anta
     c                   endif
     c                   endif
      *
     c                   add       1             w_tell
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     slutt         tag
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c                   eval      w_dato = *zero
     c                   eval      w_tell = *zero
     c                   eval      w_anta = *zero
      *
     c                   eval      d_aaar = uyear
     c                   eval      d_mmnd = umonth
     c                   eval      d_ddag = uday
     c                   eval      w_dato = d_dato
      *
     c                   endsr
      *
     oqcllesrc  dadd 01
     o                       record              96
     o                       w_tell               6
      *
     o          eadd         add001
     o                       w_tell               6
     o                       w_dato              12
     o                                           21 'CALL'
5.31 o                                           39 'PGM(*LIBL/AN044C)'
      *
     o          eadd         add002
     o                       w_tell               6
     o                       w_dato              12
     o                                           38 'MONMSG MSGID(CPF0000)'
