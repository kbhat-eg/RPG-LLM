     h option(*nodebugio : *srcstmt) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FD510R
      * Beskrivelse . . : Kopiere varelinjer fra en ordre til en annen
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       180919 BKV  Nytt program
      *
8.01  * K0000 040123 bsa  Håndterer skaffevarer
8.02  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsodtl1    if   e           k disk    rename(sodtpfr:sodtl1r)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     ffodtlu    uf a e           k disk    rename(fodtpfr:fodtlur)
     ffodtlr    if   e           k disk    rename(fodtpfr:fodtlrr)
8.01 fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
      *
     ffd510d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av parametre
      *
     d p_nummv         s                   like(fdnumm)
     d p_nummo         s                   like(fdnumm)
     d p_vare          s                   like(sdvare)
     d p_nobb          s                   like(fdnobb)
     d p_lvar          s                   like(fdlvar)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d fodtl1_line     s                   like(fdline)
     d fodtlu_line     s                   like(fdline)
     d fodtlr_line     s                   like(fdline)
     d sodtl1_aarr     s                   like(sdaarr)
     d sodtl1_binr     s                   like(sdbinr)
     d sodtl1_numm     s                   like(sdnumm)
     d sodtl1_suff     s                   like(sdsuff)
     d sodtlu_ktxt     s                   like(sdktxt)
     d sodtlu_line     s                   like(sdline)
8.01 d vvarl1_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_subf          s              1    inz(*off)
      *
     d w_firm          s                   like(fdfirm)
      *
     d sqlquery        s           4096
     d w_frisok        s             30
     d b_open_sok      s              1    inz(*off)
     d w_tell          s              5  0
     d sqlwhere        s            512
     d w_fakt          s              1    inz(*off)
      *
     d w_fdvare        s                   like(fdvare)
     d w_fdotyp        s                   like(fdotyp)
     d w_fdline        s                   like(fdline)
     d w_sdaarr        s                   like(sdaarr)
     d w_sdbinr        s                   like(sdbinr)
     d w_sdktxt        s                   like(sdktxt)
     d w_fdnumm        s                   like(fdnumm)
     d w_fdsuff        s                   like(fdsuff)
     d w_fdtek1        s                   like(fdtek1)
     d w_fdtek2        s                   like(fdtek2)
     d w_fdanta        s                   like(fdanta)
     d w_fdenh1        s                   like(fdenh1)
     d w_fdsapr        s                   like(fdsapr)
     d w_fdlety        s                   like(fdlety)
     d w_folmat        s                   like(folmat)
      *
     d w_numm          s                   like(fdnumm)
     d w_suff          s                   like(fdsuff)
     d w_line          s                   like(fdline)
      *
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(19)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste recordnummer på siden
      * w_curn - Current recordnummer på siden
      * w_virn - Siden som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
     C/Exec SQL
     C+  Set Option DatFmt=*ISO, commit=*none, DlyPrp=*YES
     C+  , SRTSEQ=*LANGIDUNQ, LANGID=NOR
     C/End-Exec
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c*                   when      *in11
     c*                   exsr      bck_subfile
     c*                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c*                   if        b2dkod <> ' ' or
     c*                             b2dtxt <> *blank
     c*                   exsr      posisjoner
     c*                   goto      b2tagb
     c*                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   if        b_subf = *on
     c                   goto      b2taga
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_sfrn <> 0
     c     w_sfrn        chain     b1sfl
     c                   endif
      *
     c*                   select
     c*                   when      w_seqe = 'B'
     c*                   eval      RA50l2_dtxt = b1dtxt
     c*     RA50l2_key    setll     RA50l2
     c*                   other
     c*                   eval      RA50l1_dkod = b1dkod
     c*     RA50l1_key    setll     RA50l1
     c*                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Mobiloperatør
      *
     c*                   if        b2dkod <> ' '
     c*                   eval      w_seqe = *blank
     c*                   eval      RA50l1_dkod = b2dkod
     c*     RA50l1_key    setll     RA50l1
     c*                   goto      end_pos
     c*                   endif
      **
      ** Beskrivelse
      **
     c*                   if        b2dtxt <> *blank
     c*                   eval      w_seqe = 'B'
     c*                   eval      RA50l2_dtxt = b2dtxt
     c*     RA50l2_key    setll     RA50l2
     c*                   goto      end_pos
     c*                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c*                   eval      b2dkod = ' '
     c*                   eval      b2dtxt = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
     c                   eval      b_subf = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      b_subf = *on
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Valg
      *
     c                   if        b1vant <> 0
     c                   if        w_fakt = *on
     c                   exsr      kopier_faktu
     c                   else
     c                   exsr      kopier_ordre
     c                   endif
     c                   eval      b1tekt = '**'+%editw(b1vant:'     0 ,  -')+
     c                                      'kopiert **' + %trim(b1tekt)
     c                   eval      b1vant = 0
     c                   eval      *in55 = *on
     c                   update    b1sfl
     c                   eval      *in55 = *off
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     tell_ordre    begsr
      *
     c/exec sql
     c+ PREPARE SQL_CNT FROM :sqlquery
     c/end-exec
     c/exec sql
     c+ DECLARE C_CNT CURSOR FOR SQL_CNT
     c/end-exec
     c/exec sql
     c+ open C_CNT USING :w_firm, :w_numm
     c/end-exec
     c/exec sql
     c+ fetch C_CNT into  :w_tell
     c/end-exec
     c/exec sql
     c+ close C_CNT
     c/end-exec
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   eval      *in51 = *off
      *
      * Initierer SQL for søk
      *
     c                   if        b_open_sok = *on
     c/exec sql
     c+ close STMT
     c/end-exec
     c
     c                   eval      w_fakt = *off
     c
     c                   eval      sqlwhere  =
     c                                 ' where fdfirm = ? and +
     c                                   fdnumm = ? +
     c                                 '
     c                   eval      sqlquery =
     c                              'select count(*) from fodtpf ' +
     c                               %trim(sqlwhere)
     c
     c                   exsr      tell_ordre
      * sjekker om funner ordre
     c                   if        w_tell > 0
     c                   eval      sqlquery  = ' +
     c                                 select +
     c                                   0 , 0 , fdnumm, +
     c                                  fdsuff,fdline,fdvare,fdtek1, +
     c                                  fdtek2,fdanta,fdenh1,fdsapr, 0 , +
     c                                  fdlety,folmat +
     c                                from fodtpf +
     c                                inner join fohepf on fofirm=fdfirm and +
     c                                 fonumm=fdnumm and fosuff=fdsuff +
     c                              '
     c                   eval      sqlquery  = %trim(sqlquery) +
     c                                 sqlwhere
     c                   else
      * hvis ikke ordre, sjekk faktura
     c                   eval      w_fakt = *on
     c                   eval      sqlquery  = ' +
     c                                 select +
     c                                  sdaarr, sdbinr, sdnumm, +
     c                                  sdsuff,sdline,sdvare,sdtek1, +
     c                                  sdtek2,sdanta,sdenh1,sdsapr,sdktxt, +
     c                                  sdlety,solmat +
     c                                from sodtpf s1 +
     c                                inner join sohepf on sofirm=sdfirm and +
     c                                 sobinr=sdbinr and soaarr=sdaarr and +
     c                                 sonumm=sdnumm and sosuff=sdsuff +
     c                              '
     c                   eval      sqlwhere  =
     c                                 ' where sdfirm = ? and +
     c                                   sdbinr = ? +
     c                                   and sdaarr = +
     c                                  (select max(s2.soaarr) from sohepf s2 +
     c                                    where s2.sobinr = s1.sdbinr and +
     c                                    s2.sofirm = s1.sdfirm) +
     c                              '
     c*                   eval      sqlwhere  =
     c*                                 ' where sdfirm = ? and +
     c*                                   SDBINR = +
     c*                                   (select s3.sobinr +
     c*                                     from sohepf s3 +
     c*                                    where s3.sonumm = ? and s3.sofirm = +
     c*                                    s1.sdfirm order by s3.soaarr  desc +
     c*                                    fetch first 1 rows only ) +
     c*                                   and sdaarr = +
     c*                                  (select max(s2.soaarr) from sohepf s2 +
     c*                                    where s2.sonumm = s1.sdnumm)  +
     c*                              '
     c                   eval      sqlquery  = %trim(sqlquery) +
     c                                 sqlwhere
     c                   endif
     c                   eval      sqlquery  = %trim(sqlquery) +
     c                                 '  order by      +
     c                                   5     +
     c                                   for read only +
     c                                   '
     c/exec sql
     c+ PREPARE SQL_STMT FROM :sqlquery
     c/end-exec
     c
     c/exec sql
     c+ DECLARE STMT CURSOR FOR SQL_STMT
     c/end-exec
     c
     c                   if        b2fris <> *blank
     c/exec sql
     c+ OPEN STMT using
     c+                 :w_firm, :w_numm
     c+                ,:w_frisok, :w_frisok
     c+                ,:w_frisok, :w_frisok
     c+                ,:b2fris  , :b2fris
     c/end-exec
     c                   else
     c/exec sql
     c+ OPEN STMT using
     c+                 :w_firm, :w_numm
     c/end-exec
     c                   endif
     c                   endif
      *
     c                   if        sqlcod = 100 or sqlcod < 0
     c                   eval      *in15 = *on
     c                   leavesr
     c                   endif
      *
     c                   dou       w_srrn = w_spge
      *
     c/exec sql
     c+ fetch STMT
     c+ into  :w_sdaarr,:w_sdbinr,:w_fdnumm,:w_fdsuff,:w_fdline,:w_fdvare
     c+      ,:w_fdtek1,:w_fdtek2,:w_fdanta,:w_fdenh1,:w_fdsapr,:w_sdktxt
     c+      ,:w_fdlety,:w_folmat
     c/end-exec
     c                   if        sqlcod = 100 or sqlcod < 0
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
      **
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1vant = 0
     c                   eval      b1vare = w_fdvare
     c                   eval      b1line = w_fdline
     c                   eval      b1tekt = %trim(w_fdtek1) + %trim(w_fdtek2)
     c                   eval      b1anta = w_fdanta
     c                   eval      b1enhe = w_fdenh1
     c                   eval      b1sapr = w_fdsapr
     c                   eval      b1suff = w_fdsuff
     c                   eval      b1numm = w_fdnumm
     c                   eval      b1aarr = w_sdaarr
     c                   eval      b1ktxt = w_sdktxt
     c                   eval      b1binr = w_sdbinr
     c                   eval      b1lety = w_fdlety
     c                   eval      b1lmat = w_folmat
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   eval      b_open_sok = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kopiering av ordre linje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kopier_ordre  begsr
      *
     c                   eval      w_suff = 0
     c                   eval      w_numm = p_nummo
     c                   exsr      hent_linjenr
     c                   eval      w_line = w_line + 10
      *
     c                   eval      w_numm = p_nummv
     c                   eval      w_suff = b1suff
     c                   eval      fodtl1_line = b1line
     c     fodtl1_key    chain     fodtl1
     c                   if        %found
     c                   eval      fdnumm = p_nummo
     c                   eval      fdsuff = 0
     c                   eval      fdline = w_line
     c                   eval      fdanta = b1vant
     c
     c                   write     fodtlur
     c
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kopiering av ordre linje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kopier_faktu  begsr
      *
     c                   eval      w_suff = 0
     c                   eval      w_numm = p_nummo
     c                   exsr      hent_linjenr
     c                   eval      w_line = w_line + 10
      *
     c                   eval      sodtl1_aarr = b1aarr
     c                   eval      sodtl1_binr = b1binr
     c                   eval      sodtl1_numm = b1numm
     c                   eval      sodtl1_suff = b1suff
     c                   eval      sodtlu_ktxt = b1ktxt
     c                   eval      sodtlu_line = b1line
     c     sodtl1_key    chain     sodtl1
     c                   if        %found
     c                   eval      fdnumm = p_nummo
     c                   eval      fdsuff = 0
     c                   eval      fdline = w_line
     c                   eval      fdanta = b1vant
     c
     c*                   if        vaokre = '-'
     c*                   eval      sdanta = sdanta * -1
     c*                   eval      sdsalg = sdsalg * -1
     c*                   eval      sdkost = sdkost * -1
     c*                   eval      sdrkr1 = sdrkr1 * -1
     c*                   eval      sdrkr2 = sdrkr2 * -1
     c**** kun for        eval      sdbrk1 = sdbrk1 * -1
     c**** dok.sjon       eval      sdbrk2 = sdbrk2 * -1
     c*                   eval      sdrkro = sdrkro * -1
     c*                   endif
      *
     c                   eval      fdltyp = sdltyp
     c                   eval      fdlety = sdlety
     c                   eval      fdvare = sdvare
     c                   eval      fdenh1 = sdenh1
     c                   eval      fdlage = sdlage
     c                   eval      fdkpri = sdkpri
     c                   eval      fdavde = sdavde
     c                   eval      fdkont = sdkont
     c                   eval      fdspes = sdspes
     c                   eval      fdproj = sdproj
     c                   eval      fdakti = sdakti
     c                   eval      fdalme = sdalme
     c                   eval      fdbrty = sdbrty
     c                   eval      fdpriv = sdpriv
     c                   eval      fdtek1 = sdtek1
     c                   eval      fdtek2 = sdtek2
     c*                   eval      fdanta = sdanta
     c                   eval      fdoppr = sdoppr
     c                   eval      fdsapr = sdsapr
     c                   eval      fdkopr = sdkopr
     c                   eval      fdrab1 = sdrab1
     c                   eval      fdrab2 = sdrab2
     c                   eval      fdbrr1 = sdbrr1
     c                   eval      fdbrr2 = sdbrr2
     c                   eval      fdpasl = sdpasl
     c                   eval      fdrkr1 = sdrkr1
     c                   eval      fdrkr2 = sdrkr2
     c                   eval      fdsums = sdsalg
     c                   eval      fdsumr = sdrkr1 + sdrkr2 + sdrkro
     c                   eval      fdsumk = sdkost
     c                   eval      fdomva = sdomva
     c*                  eval      fdlvre = sdlvre
     c*                   if        b_skaf = *on
     c*                   eval      fdlvre = 1
     c*                   else
8.01 c**                 eval      fdlvre = 0
     c*                   end
8.01  *
8.01  * Hvis det er skaffevare tidligere, sjekk om den fortsatt er det.
8.01  *
8.01 c                   eval      fdlvre = sdlvre
8.01 c                   if        sdlvre = 1
8.01 c                   eval      vvarl1_vare = sdvare
8.01 c     vvarl1_key    chain     vvarl1
8.01 c                   if        %found
8.01 c                   eval      fdlvre = 0
8.01 c                   endif
8.01 c                   endif
8.01  *
     c                   eval      fdogrp = sdogrp
     c                   eval      fdhgrp = sdhgrp
     c                   eval      fdugrp = sdugrp
     c                   eval      fdedih = sdedih
     c                   eval      fdedil = sdedil
      * Hent inn nobb og lev.varenummer
     c                   eval      p_vare = sdvare
     c                   eval      p_nobb = *zeros
     c                   eval      p_lvar = *blanks
     c                   call      'VV710R'
     c                   parm                    p_vare
     c                   parm                    p_nobb
     c                   parm                    p_lvar
      *
     c                   eval      fdnobb = p_nobb
     c                   eval      fdlvar = p_lvar
     c
     c
     c                   write     fodtlur
     c
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å hente høyeste linjenummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_linjenr  begsr
      *
     c                   eval      w_line = 0
     c                   eval      fodtlr_line = 9999
     c     fodtlr_key    setll     fodtlr
     c                   readp     fodtlr
     c                   if        not %eof and
     c                             fdfirm = w_firm and
     c                             fdnumm = w_numm and
     c                             fdsuff = w_suff
     c                   eval      w_line = fdline
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_nummv
     c                   parm                    p_nummo
      *
      * Key for les på ordre-linje
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
     c                   kfld                    fodtl1_line
      *
      * Key for oppdatering av ordre-linje
      *
     c     fodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
     c                   kfld                    fodtlu_line
      *
      * Key for les/oppslag på ordre-linje
      *
     c     fodtlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
     c                   kfld                    fodtlr_line
      *
      * Key for file : HISTORISK-FAKTURA-REGISTER LINJE
      *
     c     sodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sodtl1_aarr
     c                   kfld                    sodtl1_binr
     c                   kfld                    sodtl1_numm
     c                   kfld                    sodtl1_suff
     c                   kfld                    sodtlu_ktxt
     c                   kfld                    sodtlu_line
8.01  *
8.01  * Key for oppslag på EVR
8.01  *
8.01 c     vvarl1_key    klist
8.01 c                   kfld                    w_firm
8.01 c                   kfld                    vvarl1_vare
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_numm = p_nummv
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c*                   eval      RA50l1_dkod = ' '
     c*     RA50l1_key    setll     RA50l1
      *
     c                   eval      b_open_sok = *on
     c                   exsr      crt_subfile
      *
     c                   endsr
