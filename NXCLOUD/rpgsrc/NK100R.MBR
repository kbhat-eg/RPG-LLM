     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : NK100R
      * Beskrivelse . . : Ekstern ordre-kode, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       300903 MHA  Nytt program
      *       011205 HKO  Utvidet med styringsinformasjon
      * 6.10  250609 BSA  Oppdatert med sporingsinformasjon
7.xx  * 7.00  150316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fnkeol1    if   e           k disk    rename(nkeopfr:nkeol1r)
     fnkeolr    if   e           k disk    rename(nkeopfr:nkeolrr)
     fnkeolu    uf a e           k disk    rename(nkeopfr:nkeolur)
      *
     fnk100d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d nkeol1_kode     s                   like(nkkode)
     d nkeolr_kode     s                   like(nkkode)
     d nkeolu_kode     s                   like(nkkode)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_stat          s              1  0
     d w_retu          s              1
     d w_ltxt          s             20
     d w_ldat          s               d   datfmt(*iso)
     d w_udat          s               d   datfmt(*iso)
      *
     d w_firm          s                   like(nkfirm)
      *
      * Definering av konstanter
      *
7.xx d c_sfil          s              2  0 inz(13)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   eval      *in30 = *off
     c                   eval      c1kode = *blank
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *inkg
     c                   call      'AA017R'
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
5.10 c                   if        b2kode <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      nkeol1_kode = b1kode
     c     nkeol1_key    setll     nkeol1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   eval      b_forn = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Kode
      *
5.10 c                   if        b2kode <> *blank
     c                   eval      w_seqe = *blank
     c                   eval      nkeol1_kode = b2kode
     c     nkeol1_key    setll     nkeol1
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
5.10 c                   eval      b2kode = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c2kode = b1kode
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1kode = b1kode
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c2kode = b1kode
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for Ny kode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c     xc1tag1       tag
      *
     c                   exfmt     c1bld
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   if        c1kode = *blank
     c                   goto      xc1tag1
     c                   endif
      *
     c                   eval      nkeolr_kode = c1kode
     c     nkeolr_key    chain     nkeolr
     c                   if        %found
     c                   goto      xc1tag1
     c                   endif
      *
     c                   eval      c2kode = c1kode
     c                   exsr      xc2bld
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
5.10 c                   eval      nkeolr_kode = c2kode
     c     nkeolr_key    chain     nkeolr
     c                   if        %found
     c                   eval      c2besk = nkbesk
     c                   eval      c2pgmc = nkpgmc
     c                   eval      c2teln = nkteln
     c                   eval      c2manu = nkmanu
     c                   eval      c2form = nkform
     c                   eval      c2zipp = nkzipp
     c                   eval      c2ftpn = nkftpn
     c                   eval      c2mapp = nkmapp
     c                   eval      c2link = nklink
     c                   eval      c2file = nkfile
     c                   eval      c2kprs = nkkprs
     c                   eval      c2tlfn = nktlfn
     c                   eval      c2mobi = nkmobi
     c                   eval      c2mail = nkmail
     c                   eval      c2eusr = nkeusr
     c     *dmy          move      nkodat        c2odat
     c     *dmy          move      nkedat        c2edat
     c                   else
     c                   eval      c2besk = *blank
     c                   eval      c2pgmc = *blank
     c                   eval      c2teln = *blank
     c                   eval      c2manu = 0
     c                   eval      c2form = 0
     c                   eval      c2zipp = 0
     c                   eval      c2ftpn = *blank
     c                   eval      c2mapp = *blank
     c                   eval      c2link = *blank
     c                   eval      c2file = *blank
     c                   eval      c2kprs = *blank
     c                   eval      c2tlfn = *blank
     c                   eval      c2mobi = *blank
     c                   eval      c2mail = *blank
     c                   eval      c2eusr = l_user
     c                   eval      c2odat = *loval
     c                   eval      c2edat = *loval
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
     c                   if        b1valg = 5
     c                   goto      end_c2bld
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2besk = *blank
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
     c                   if        c2pgmc = *blank
     c                   eval      *in32 = *on
     c                   goto      c2taga
     c                   endif
      *
     c                   if        c2teln = *blank
     c                   eval      *in33 = *on
     c                   goto      c2taga
     c                   endif
      *
      * Oppdatering av kode
      *
     c                   clear                   nkeolur
      *
     c                   eval      nkeolu_kode = c2kode
     c     nkeolu_key    chain     nkeolur
      *
     c                   eval      nkbesk = c2besk
     c                   eval      nkpgmc = c2pgmc
     c                   eval      nkteln = c2teln
     c                   eval      nkmanu = c2manu
     c                   eval      nkform = c2form
     c                   eval      nkzipp = c2zipp
     c                   eval      nkftpn = c2ftpn
     c                   eval      nkmapp = c2mapp
     c                   eval      nklink = c2link
     c                   eval      nkfile = c2file
     c                   eval      nkkprs = c2kprs
     c                   eval      nktlfn = c2tlfn
     c                   eval      nkmobi = c2mobi
     c                   eval      nkmail = c2mail
     c                   eval      nkeusr = l_user
      *
     c                   time                    nkedat
     c                   time                    nketim
      *
     c                   if        %found(nkeolu)
6.10 c                   time                    nkedat
6.10 c                   time                    nketim
6.10 c                   eval      nkeusr = l_user
     c                   update    nkeolur
     c                   else
     c                   time                    nkodat
     c                   time                    nkotim
6.10 c                   eval      nkousr = l_user
6.10 c                   time                    nkedat
6.10 c                   time                    nketim
6.10 c                   eval      nkeusr = l_user
     c                   eval      nkkode = c2kode
     c                   eval      nkfirm = w_firm
     c                   write     nkeolur
     c                   eval      b_forn = *on
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1besk = nkbesk
     c                   eval      b1pgmc = nkpgmc
     c                   eval      b1teln = nkteln
     c                   eval      b1manu = nkmanu
     c                   eval      b1form = nkform
     c                   eval      b1zipp = nkzipp
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      nkeolr_kode = d1kode
     c     nkeolr_key    chain     nkeolr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter kode
      *
     c                   eval      nkeolu_kode = d1kode
     c     nkeolu_key    chain     nkeolur
     c                   delete    nkeolur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      nkeol1
      *
     c                   if        %eof or
     c                             nkfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1kode = nkkode
     c                   eval      b1besk = nkbesk
     c                   eval      b1pgmc = nkpgmc
     c                   eval      b1teln = nkteln
     c                   eval      b1manu = nkmanu
     c                   eval      b1form = nkform
     c                   eval      b1zipp = nkzipp
     c                   eval      b1odat = nkodat
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     nkeol1_key    setgt     nkeol1
     c                   readp     nkeol1
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     nkeol1
      *
     c                   if        %eof or
     c                             nkfirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   enddo
      *
     c                   if        %eof
     c     nkeol1_key    setll     nkeol1
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c*    *entry        plist
     c*                  parm                    p_fsys
      *
      * Key for posisjonering på kode
      *
     c     nkeol1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    nkeol1_kode
      *
      * Key for les på kode
      *
     c     nkeolr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    nkeolr_kode
      *
      * Key for les / skriv på kode
      *
     c     nkeolu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    nkeolu_kode
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
      * Initierer skjermbildet
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      nkeol1_kode = *blank
     c     nkeol1_key    setll     nkeol1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
