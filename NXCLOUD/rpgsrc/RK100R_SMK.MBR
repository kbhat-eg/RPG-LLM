     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RK100R
      * Beskrivelse . . : Kunder, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       230702 KOB  Nytt program
T5.00 * V5.00 131202 HFL  Feil lengde på parameter til rabattkategori.        *
      * V5.10 100603 KOB  Endret sortering ved alfasøk fra kundenr til alfa
T5.10 * V5.20 241003 KOB  Fjernet bruk av spesialtabell for alfafelt          *
T5.30 * V5.30 120704 HFL  Rettet feil i kopiering av kunde.                   *
T5.30 * v5.30 040205 HFL  Sletter spesialavtale og notatblokk.
T5.30 * v5.30 030505 HFL  F11 viser foretaksnr.
T5.40 * V5.40 150605 HFL  Leser RKLAL2 i stedet for RKLALU.
T5.40 * V5.40 230705 KOB  Endret slik at mobilnr vises i stedet for fax F11
T5.40 * V5.41 270805 KOB  Lagt inn mulighet for å vise bare hurtigopprettet
T5.30 * V5.50 021106 KOB  Kan skrive kontoutdrag på enkeltkunde.
T5.30 * V5.50 290507 KOB  Blanker e-mail adresse ved kopiering
5.60  * R5.60 271107 AMD  Lagt inn løsning for kontroll av grense for
      *                   almenningsrabatter
      * R5.60 191208 KOB  Korrigert posisjonering etter sletting
6.10  * R6.10 060510 BSA  Endret logging ev endringer
pdf   * R6.10     041010  Henter fra ASP arkiv. Mulighet for valg mellom BSA  *
pdf   *                   ASP og Multiarkiv gjøres i AP622C. Det finnes  BSA  *
pdf   *                   to varianter av dette programmet.              BSA  *
6.20  * R6.20 070611 BSA  Starter arkivet
6.21  * R6.20 140611 BSA  Memosaldo flyttet til egen fil
6.22  * R6.20     210711  Legger ut firmanummer i skjultfelt for         bsa  *
6.22  *                   spørring mot ASP-arkiv fra Jacada              bsa  *
6.23  * R6.20     061212  Endret metode for oppslag mot arkiv            bsa  *
6.24  * R6.20     260413  Pros er ikke lenger nummerisk                  bsa  *
6.30  * R6.30 110914 bsa  Felt flyttet fra RKUN til RKME (rkkjal)             *
6.3q  *       260515 bof  Datoformat sql
7.xx  * 7.00  150216 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 7.00  130218 bkv  Vasker vekk . pga Newlook problem
7.02  * 7.00  040718 bkv  Kontaktperson søk
7.03  * 7.00  040718 bkv  Kontaktperson søk rettelse
7.04  * 7.00  040718 bkv  Kontaktperson søk rettelse II
7.05  * 7.00  270918 bsa  Integrasjon med Smartkalk.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frkunlr    if   e           k disk    rename(rkunpfr:rkunlrr)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     frkunl2    if   e           k disk    rename(rkunpfr:rkunl2r)
     frkunl3    if   e           k disk    rename(rkunpfr:rkunl3r)
     frkunlu    uf a e           k disk    rename(rkunpfr:rkunlur)
6.21 frkmelu    uf a e           k disk    rename(rkmepfr:rkmelur)
6.21 f                                     prefix(rx:2)
6.21 frkmel1    if   e           k disk    rename(rkmepfr:rkmel1r)
     frksolu    uf   e           k disk    rename(rksopfr:rksolur)
T5.30frksalu    uf   e           k disk    rename(rksapfr:rksalur)
T5.40frklal2    uf   e           k disk    rename(rklapfr:rklal2r)
     frktrl1    if   e           k disk    rename(rktrpfr:rktrl1r)
     fraa1l1    if   e           k disk    rename(raa1pfr:raa1l1r)
     fapospf    if   e           k disk
7.05 fkeysiu    uf a e           k disk    rename(keysst:keysiur)
7.05 fopefiu    uf a e           k disk    rename(opefst:opefiur)
7.05 frukpl1    if   e           k disk    rename(rukppfr:rukpl1r)
7.05 fafpslr    if   e           k disk    rename(afpspfr:afpslrr)
7.05 famodl1    if   e           k disk    rename(amodpfr:amodl1r)
      *
     frk100d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
7.05 d l_filg                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d rkunlr_kund     s                   like(rkkund)
     d rkunl1_kund     s                   like(rkkund)
     d rkunl2_alfa     s                   like(rkalfa)
     d rkunl3_ponr     s                   like(rkponr)
     d rkunlu_kund     s                   like(rkkund)
     d rksolu_skun     s                   like(rkskun)
     d rktrl1_kund     s                   like(rnkund)
     d apospf_ponr     s                   like(apponr)
T5.30d rksalu_skun     s                   like(rkkund)
T5.40d rklal2_syst     s                   like(rxsyst)
T5.40d rklal2_pros     s                   like(rxpros)
T5.40d rklal2_akti     s                   like(rxakti)
T5.40d rklal2_kund     s                   like(rxkont)
7.05 d keysiu_ftnr     s                   like(kyftnr)
7.05 d opefiu_ftnr     s                   like(opftnr)
7.05 d rukpl1_kund     s                   like(rukund)
7.05 d rukpl1_levr     s                   like(rulevr)
7.05 d rukpl1_kpnr     s                   like(rukpnr)
7.05 d afpslr_ufil     s                   like(l_filg)
7.05 d afpslr_uide     s                   like(afuide)
7.05 d amodl1_modu     s                   like(axmodu)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d b_open_sok      s              1    inz(*off)
      *
     d w_udat          s               d   datfmt(*iso)
     d w_valg          s              1  0
     d w_retk          s              1
     d w_4knr          s              6  0
     d w_ste1          s             35
     d w_ste2          s             35
     d w_ste3          s             35
     d w_ste4          s             35
     d w_ste5          s             35
7.02 d w_stek          s             35
7.02 d w_stek1         s             35
7.02 d w_stek2         s             35
7.02 d w_stek3         s             35
7.02 d w_stek4         s             35
7.02 d w_stek5         s             35
7.02 d sqlquery        s           4096
     d w_posi          s              3  0
     d s_stek          s             35
7.02 d s_steko         s             35
7.01 d w_len           s              3  0
7.01 d w_teller        s              3  0
7.03 d w_kotps         s              1    inz(*off)
      *
     d w_firm          s                   like(rkfirm)
     d w_kund          s                   like(rkkund)
     d w_kuna          s              6
     d w_1bek          s                   like(ra1bek)
     d w_1bkk          s                   like(ra1bkk)
7.05 d w_mld1          s             50
      *
6.23 d w_type          s             50
pdf  d w_modu          s             10    inz('PROA    ')
pdf  d*w_modu          s             10    inz('MSI     ')
     d w_disp          s              1    inz('1')
     d w_tilg          s              1
6.23 d w_refn          s             50
      *
7.05 d p_uuid          s             36
7.05 d p_kund          s              6  0
7.05 d p_kref          s             30
7.05 d p_kpnr          s              6  0
7.05 d p_emal          s             50
7.05 d p_rec1          s             50
7.05 d p_rec2          s             50
7.05 d p_rec3          s             50
7.05 d p_send          s             50
7.05 d p_subj          s            100
7.05 d p_txt1          s            100
7.05 d p_txt2          s            100
7.05 d p_txt3          s            100
7.05 d p_txt4          s            100
7.05 d p_avsl          s            100
      *
      * Definering av parametre MultiArcive
      *
     d w_pgrp          s              2
     d w_aara          s              4
     d w_bila          s              6
     d w_afir          s              3
      *
     d p_stat          s              1
     d p_bkod          s                   like(rkbetm)
     d p_btxt          s             20
     d p_ckod          s                   like(rkbetb)
     d p_ctxt          s             20
S5.00d*p_fkod          s                   like(rkslgr)
T5.00d p_fkod          s                   like(rkrabk)
     d p_ftxt          s             20
E5.30d p_ikod          s                   like(rkslgr)
     d p_itxt          s             20
     d p_kkod          s                   like(rkkatg)
     d p_ktxt          s             20
      *
     d ww1bek          s                   like(ra1bek)
     d ww1bkk          s                   like(ra1bkk)
      *
      * Definering av konstanter
      *
     d lo              c                   'abcdefghijklmnopqrstuvwxyzæøåÆØÅ'
     d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÆØÅ'
     d*uh              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZ²ÔÖ²ÔÖ'
      *
     d s_kund          s                   like(rkkund)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(12)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste recordnummer på siden
      * w_curn - Current recordnummer på siden
      * w_virn - Siden som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.3q C/Exec SQL
6.3q C+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR
6.3q C/End-Exec
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * F5=Vis alle/aktive
      *
     c                   if        *inke
     c                   if        *in55 = *on
     c                   eval      *in55 = *off
     c                   else
     c                   eval      *in55 = *on
     c                   endif
     c                   exsr      forny
     c                   goto      b2tagb
     c                   endif
      *
      * F7=Vis bare hurtigopprettede kunder
      *
     c                   if        *inkg
     c                   if        *in56 = *on
     c                   eval      *in56 = *off
     c                   else
     c                   eval      *in56 = *on
     c                   endif
     c                   exsr      forny
     c                   goto      b2tagb
     c                   endif
      *
      * F8=Spørring på posteringer
      *
     c                   if        *inkh = *on
     c                   eval      w_valg = b1valg
     c                   call      'RK110R'
     c                   goto      b2tagb
     c                   endif
      *
      * F9=Gjenta søk
      *
     c                   if        *inki = *on
     c                   eval      *in22 = *on
     c                   eval      b2stek = s_stek
7.02 c                   eval      b2stko = s_steko
     c                   goto      b2tagb
     c                   endif
      *
      * F11=Bytter på å vise informasjon i subfien
      * 1. gang vises postnr./sted og telefonnr.
      * 2. gang vises gateadresse  og telefaxnr.
T5.30 * 3. gang vises postnr./sted og foretaksnr.
      *
     c                   if        *inkk = *on
     c                   if        *in80 = *on
     c                   eval      *in80 = *off
T5.30c                   eval      *in81 = *on
     c                   else
T5.30c                   if        *in81 = *on
T5.30c                   eval      *in81 = *off
T5.30c                   else
     c                   eval      *in80 = *on
T5.30c                   endif
     c                   endif
     c                   exsr      forny
     c                   goto      b2taga
     c                   endif
      *
      * Søking
      *
     c                   if        b2stek <> *blank
     c                   exsr      sØk
     c                   goto      b2tagb
     c                   endif
      *
      * Posisjonering
      *
     c                   if        b2kund <> 0 or
     c                             b2alfa <> *blank or
     c                             b2ponr <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_sfrn <> 0
     c     w_sfrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'S'
     c                   eval      b_open_sok = *on
     c                   when      w_seqe = 'A'
     c                   eval      rkunl2_alfa = b1alfa
     c     rkunl2_key    setll     rkunl2
     c                   when      w_seqe = 'P'
     c                   eval      rkunl3_ponr = b1ponr
     c     rkunl3_key    setll     rkunl3
     c                   other
     c                   eval      rkunl1_kund = b1kund
     c     rkunl1_key    setll     rkunl1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for søk i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sØk           begsr
      *
     c                   eval      w_seqe = 'S'
     c                   eval      b_open_sok = *on
      *
     c                   eval      s_stek = b2stek
7.02 c                   eval      s_steko = b2stko
      *
      * Utleder søketekster ut av søketekst
      *
     c                   call      'AS700R'
     c                   parm                    b2stek
     c                   parm                    w_ste1
     c                   parm                    w_ste2
     c                   parm                    w_ste3
     c                   parm                    w_ste4
     c                   parm                    w_ste5
7.02 c                   movel     b2stko        w_stek
7.02 c                   call      'AS700R'
7.02 c                   parm                    w_stek
7.02 c                   parm                    w_stek1
7.02 c                   parm                    w_stek2
7.02 c                   parm                    w_stek3
7.02 c                   parm                    w_stek4
7.02 c                   parm                    w_stek5
7.02 c                   eval      w_stek1 = %ScanRpl('%' : '' : w_stek1)
7.02 c                   eval      w_stek1 = %Trim(w_stek1)
7.02 c                   if        w_stek1 <> *blank
7.02 c                   eval      w_stek1 = '%'+%trim(w_stek1)+'%'
7.03 c                   eval      w_kotps = *on
     c                   else
7.03 c                   eval      w_kotps = *off
7.02 c                   endif
7.02 c                   if        w_stek2 <> *blank
7.02 c                   eval      w_stek2 = '%'+%trim(w_stek2)+'%'
7.02 c                   endif
      *
      * Blanker subfile, og fyller opp i.h.t. søk
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker søkefelt
      *
     c                   eval      b2stek = *blank
7.02 c                   eval      b2stko = *blank
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjonerer på kunde
      *
     c                   if        b2kund <> 0
     c                   eval      w_seqe = *blank
     c                   eval      rkunl1_kund = b2kund
     c     rkunl1_key    setll     rkunl1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjonerer på alfafelt
      *
     c                   if        b2alfa <> *blank
     c                   eval      w_seqe = 'A'
     c                   eval      rkunl2_alfa = b2alfa
     c     rkunl2_key    setll     rkunl2
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på postnummer
      *
     c                   if        b2ponr <> 0
     c                   eval      w_seqe = 'P'
     c                   eval      rkunl3_ponr = b2ponr
     c     rkunl3_key    setll     rkunl3
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2kund = 0
     c                   eval      b2alfa = *blank
     c                   eval      b2ponr = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1kund = b1kund
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1kund = b1kund
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1kund = b1kund
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1kund = b1kund
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Utskrift
      *
     c                   if        b1valg = 6
     c                   eval      *in30  = *on
      *
     c                   call      'RK643C'
     c                   parm                    b1kund
      *
     c                   eval      b1valg = *zero
     c                   update    b1sfl
     c                   iter
     c                   endif
7.05  *
7.05  * Smartkal
7.05  *
7.05 c                   if        b1valg = 7
7.05  * Er modul aktivert ??
7.05 c                   eval      amodl1_modu = 'SMARTKALK'
7.05 c     amodl1_key    chain     amodl1
7.05 c                   if        not %found
7.05 c                   eval      w_mld1 = 'Modul er ikke innstallert. +
7.05 c                                     Kontakt EG'
7.05 c                   call      'AA005R'
7.05 c                   parm                    w_mld1
7.05 c                   eval      b1valg = 0
7.05 c                   update    b1sfl
7.05 c                   iter
7.05 c                   endif
7.05  *
7.05 c                   eval      c1navn = *blanks
7.05 c                   eval      c1skey = *blanks
7.05 c                   eval      c1kprs = *blanks
7.05 c                   eval      c1kund = b1kund
7.05 c                   exsr      xc7bld
7.05 c                   eval      b1valg = 0
7.05 c                   update    b1sfl
7.05 c                   iter
7.05 c                   endif
      *
      * Vise posteringer
      *
     c                   if        b1valg = 8
     c                   eval      *in30  = *on
     c                   exsr      posteringer
     c                   eval      b1valg = *zero
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis arkiv
      *
     c                   if        b1valg = 9
      *
      * Kontrollerer gyldig tilgang
      *
     c                   call      'AX700R'
     c                   parm                    w_modu
     c                   parm                    w_disp
     c                   parm                    w_tilg
      *
     c                   if        w_tilg = '0'
      *
6.23 c**                 eval      w_type = 'ARKIV  '
6.23 c                   eval      w_type = 'FAKTURA'
     c                   move      w_firm        w_afir
     c                   eval      w_pgrp = '40'
     c                   eval      w_aara = '9999'
     c                   eval      w_bila = %char(b1kund)
6.23 c                   eval      w_refn = *blank
      *
pdf  c                   call      'AP622C'
pdf  c                   parm                    w_type
6.23 c                   parm                    w_refn
6.23 c**                 parm                    w_aara
6.23 c**                 parm                    w_bila
6.23 c**                 parm                    w_pgrp
pdf  c**                 call      'AH720C'
pdf  c**                 parm                    w_pgrp
pdf  c**                 parm                    w_afir
pdf  c**                 parm                    w_aara
pdf  c**                 parm                    w_bila
      *
     c                   endif
      *
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
      * Hent sist brukte kundenummer
      *
     c                   eval      w_retk = ' '
     c                   call      'RS001R'
     c                   parm                    w_retk
     c                   parm                    w_firm
     c                   parm                    w_4knr
      *
     c                   if        w_retk = *blank
     c                   eval      c1kund = w_4knr
     c                   else
     c                   eval      *in37 = *on
     c                   goto      c1taga
     c                   endif
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Reduser sist brukte kundenummer
      *
     c                   select
     c                   when      *inkc or *inkl or
     c                             c1kund <> w_4knr
     c                   eval      w_retk = 'S'
     c                   call      'RS001R'
     c                   parm                    w_retk
     c                   parm                    w_firm
     c                   parm                    w_4knr
      *
     c                   endsl
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Sjekk mot grenser
      *
     c                   if        c1kund > ra1hrs or
     c                             c1kund <= ra1hkt
     c                   eval      *in38 = *on
     c                   goto      c1taga
     c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      rkunlr_kund = c1kund
     c     rkunlr_key    chain     rkunlr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      b1kund = c1kund
     c                   exsr      xc2bld
      *
     c                   eval      c1kund = 0
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_kund = b1kund
     c                   eval      w_valg = b1valg
     c                   call      'RK101R'
     c                   parm                    w_kund
     c                   parm                    w_valg
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      rkunlr_kund = w_kund
     c     rkunlr_key    chain     rkunlr                             90
     c                   if        *in90 = *off
     c                   eval      b1navn = rknavn
     c                   eval      b1alfa = rkalfa
     c                   eval      b1sted = rksted
     c                   eval      b1tlfn = rktlfn
     c                   if        *in80 = *on
     c                   eval      b1sted = rkgate
     c                   eval      b1tlfn = rktlfx
     c                   endif
T5.30c                   if        *in81 = *on
T5.30c                   eval      b1tlfn = *blank
T5.30c                   if        rkftnr <> *zero
T5.30c                   movel     rkftnr        b1tlfn
T5.30c                   endif
T5.30c                   endif
7.01 c                   exsr      newlo_vask
     c                   endif
     c                   endif
      *
     c                   eval      b2kund = *zeros
     c                   eval      b2ponr = *zeros
     c                   eval      b2alfa = *blank
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
     c                   eval      *in31  = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      rkunlr_kund = d1kund
     c     rkunlr_key    chain     rkunlr
6.21  * Hent inn informasjon om memosaldo
6.21 c     rkunlr_key    chain     rkmel1
      *
     c     d1tagb        tag
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
      * Sjekk om saldo = 0
      *
     c                   if        rksald <> 0
     c                   eval      *in32 = *on
     c                   goto      d1tagb
     c                   endif
      *
      * Sjekk om memo-saldo = 0
      *
5.60 c                   if        rkmems <> 0 or
5.60 c                             rkmema <> 0
     c                   eval      *in33 = *on
     c                   goto      d1tagb
     c                   endif
      *
      * Sjekk om det finnes posteringer på kunde
      *
     c                   eval      rktrl1_kund = d1kund
     c     rktrl1_key    chain     rktrl1                             91
     c                   if        *in91 = *off
     c                   eval      *in34 = *on
     c                   goto      d1tagb
     c                   endif
      *
      * Sjekk om det finnes ordre på kunde
      *
     c                   eval      w_kund = d1kund
     c                   call      'RK792C'
     c                   parm                    w_firm
     c                   parm                    w_kund
     c                   parm                    p_stat
     c                   if        p_stat <> ' '
     c                   eval      *in35 = *on
     c                   goto      d1tagb
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter kunde
      *
     c                   eval      rkunlu_kund = d1kund
     c     rkunlu_key    chain     rkunlur
      *
     c                   delete    rkunlur
      *
      * Sletter kunde fra frisøkregister
      *
     c                   eval      rksolu_skun = d1kund
     c     rksolu_key    chain     rksolu
     c*
     c                   delete    rksolur
      *
T5.30 * Sletter kundens spesialavtaler
T5.30c                   eval      rksalu_skun = d1kund
T5.30c     rksalu_key    chain     rksalu                             92
T5.30c                   if        *in92 = *off
T5.30c                   delete    rksalur
T5.30c                   endif
      *
T5.30 * Sletter notatblokk
      *
T5.40c                   eval      rklal2_syst = 'K'
6.24 c*                  eval      rklal2_pros = *zeros
6.24 c                   eval      rklal2_pros = *blanks
T5.40c                   eval      rklal2_akti = *blanks
T5.40c                   eval      rklal2_kund = d1kund
T5.40c     rklal2_key    setll     rklal2                                 94
      *
T5.30c                   if        *in94 = *on
T5.40c     rklal2_key    reade     rklal2                                 95
T5.30c                   dow       *in95 = *off
T5.40c                   delete    rklal2r
T5.40c     rklal2_key    reade     rklal2                                 95
T5.30c                   enddo
T5.30c                   endif
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Hent data fra kunderegister
      *
     c                   eval      rkunlr_kund = k1kund
     c     rkunlr_key    chain     rkunlr
      *
      * Hent sist brukte kundenummer
      *
     c                   eval      w_retk = ' '
     c                   call      'RS001R'
     c                   parm                    w_retk
     c                   parm                    w_firm
     c                   parm                    w_4knr
      *
     c                   if        w_retk = *blank
     c                   eval      k1kund = w_4knr
     c                   else
     c                   eval      *in37 = *on
     c                   goto      k1taga
     c                   endif
      *
     c                   eval      k1alfa = rkalfa
     c                   eval      k1navn = rknavn
     c                   eval      k1gate = rkgate
     c                   eval      k1adr2 = rkadr2
     c                   eval      k1sted = rksted
     c                   eval      k1ponr = rkponr
     c                   eval      k1tlfn = rktlfn
     c                   eval      k1tlfx = rktlfx
     c                   eval      k1betb = rkbetb
     c                   eval      k1betm = rkbetm
     c                   eval      k1katg = rkkatg
     c                   eval      k1slgr = rkslgr
T400 c                   eval      k1grns = rkgrns
T400 c                   eval      k1rabk = rkrabk
T400 c                   eval      k1ptab = rkptab
T400 c                   eval      k1spfa = rkspfa
T400 c                   eval      k1spl1 = rkspl1
T400 c                   eval      k1spl2 = rkspl2
T400 c                   eval      k1spl3 = rkspl3
T400 c                   eval      k1spl4 = rkspl4
T400 c                   eval      k1spl5 = rkspl5
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
      * Reduser sist brukte kundenummer
      *
     c                   if        *inkc or *inkl
     c                   eval      w_retk = 'S'
     c                   call      'RS001R'
     c                   parm                    w_retk
     c                   parm                    w_firm
     c                   parm                    w_4knr
      *
     c                   goto      end_k1win
     c                   endif
      *
      * Spørring på poststed
      *
     c                   if        *inkd = *on and
     c                             w_afld = 'K1PONR'
     c                   call      'AF010R'
     c                   parm                    k1ponr
     c                   goto      k1taga
     c                   endif
      *
      * Spørring på betalingsbetingelse
      *
     c                   if        *inkd = *on and
     c                             w_afld = 'K1BETB'
     c                   call      'RA503R'
     c                   parm                    w_firm
     c                   parm                    p_ckod
     c                   parm                    p_ctxt
      *
     c                   eval      k1betb = p_ckod
     c                   goto      k1taga
     c                   endif
      *
      * Spørring på betalingsmåte
      *
     c                   if        *inkd = *on and
     c                             w_afld = 'K1BETM'
     c                   call      'RA502R'
     c                   parm                    w_firm
     c                   parm                    p_bkod
     c                   parm                    p_btxt
      *
     c                   eval      k1betm = p_bkod
     c                   goto      k1taga
     c                   endif
      *
      * Spørring på kundekategori
      *
     c                   if        *inkd = *on and
     c                             w_afld = 'K1KATG'
     c                   call      'RA511R'
     c                   parm                    w_firm
     c                   parm                    p_kkod
     c                   parm                    p_ktxt
      *
     c                   eval      k1katg = p_kkod
     c                   goto      k1taga
     c                   endif
      *
      * Spørring på selger
      *
     c                   if        *inkd = *on and
     c                             w_afld = 'K1SLGR'
     c                   call      'RA509R'
     c                   parm                    w_firm
     c                   parm                    p_ikod
     c                   parm                    p_itxt
      *
     c                   eval      k1slgr = p_ikod
     c                   goto      k1taga
     c                   endif
      *
T400  * Spørring på rabattkategori
      *
T400 c                   if        *inkd = *on and
T400 c                             w_afld = 'K1RABK'
T400 c                   call      'RA506R'
     c                   parm                    w_firm
     c                   parm                    p_fkod
     c                   parm                    p_ftxt
      *
     c                   eval      k1rabk = p_fkod
T400 c                   goto      k1taga
T400 c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   if        k1kund <> 0
     c                   eval      rkunlr_kund = k1kund
     c     rkunlr_key    chain     rkunlr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
     c                   endif
      *
      * Kontroll av skjermbildet
      *
     c                   exsr      sjekk_input
      *
     c                   if        b_feil = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kontroller kundenummer mot grenseverdier, hvis utfyldt i bilde
      *
     c                   if        k1kund <> 0
     c                   if        k1kund <= ra1hkt
     c                   eval      *in37 = *on
     c                   goto      k1taga
     c                   else
     c                   if        k1kund >= ra1hrs
     c                   eval      *in37 = *on
     c                   goto      k1taga
     c                   endif
     c                   endif
     c                   endif
      *
      * Konvertere alfafelt til store bokstaver
      *
     c                   if        k1alfa = *blank
     c                   eval      k1alfa = k1navn
     c                   endif
      *
     c***  lo:uh         xlate     k1alfa        k1alfa
      *
      * Konvertere navn/adresse til store bokstaver, dersom statuskode=0
      *
     c                   if        w_1bkk = 0
     c     lo:up         xlate     k1navn        k1navn
     c     lo:up         xlate     k1gate        k1gate
     c     lo:up         xlate     k1adr2        k1adr2
     c     lo:up         xlate     k1sted        k1sted
     c                   endif
      *
      * Oppdater file
      *
     c                   eval      rkunlu_kund = k1kund
     c     rkunlu_key    chain     rkunlur                            90
     c                   eval      rkkund = k1kund
     c                   eval      rkalfa = k1alfa
     c                   eval      rknavn = k1navn
     c                   eval      rkgate = k1gate
     c                   eval      rkadr2 = k1adr2
     c                   eval      rksted = k1sted
     c                   eval      rkponr = k1ponr
     c                   eval      rktlfn = k1tlfn
     c                   eval      rktlfx = k1tlfx
      *
     c                   if        k1betb = 0
     c                   eval      rkbetb = w_1bek
     c                   else
     c                   eval      rkbetb = k1betb
     c                   endif
      *
     c                   eval      rkbetm = k1betm
     c                   eval      rkkatg = k1katg
     c                   eval      rkslgr = k1slgr
     c                   eval      rkptab = k1ptab
     c                   eval      rkspfa = k1spfa
     c                   eval      rkspl1 = k1spl1
     c                   eval      rkspl2 = k1spl2
     c                   eval      rkspl3 = k1spl3
     c                   eval      rkspl4 = k1spl4
     c                   eval      rkspl5 = k1spl5
      *
     c                   eval      rklagr = 0
     c                   eval      rkdist = 0
     c                   eval      rkbgir = 0
     c                   eval      rkrenk = 0
     c                   eval      rksald = 0
     c                   eval      rkkjØp = 0
     c                   eval      rkktid = 0
     c                   eval      rkkjØf = 0
     c                   eval      rkfaku = 0
     c                   eval      rkvalk = *blank
     c                   eval      rksprk = *blank
     c                   eval      rkmkod = 0
     c                   eval      rkladr = 0
     c                   eval      rklbet = 0
     c                   eval      rkform = 0
     c                   eval      rkland = *blank
     c                   eval      rksped = 0
     c                   eval      rkorab = 0
E400 c                   eval      rkgrns = k1grns
     c                   eval      rksaif = 0
     c                   eval      rkfrie = *blank
     c                   eval      rksamm = *blank
6.21 c**                 eval      rkmems = 0
6.21 c**                 eval      rkmema = 0
6.30 c**                 eval      rkkjal = 0
5.60 c                   eval      rkgral = 0
     c                   eval      rkfacn = 0
     c                   eval      rkavde = 0
     c                   eval      rkkjØr = 0
E400 c                   eval      rkrabk = k1rabk
     c                   eval      rkordb = 0
     c                   eval      rkkres = 0
     c                   eval      rkpurr = 0
     c                   eval      rkspav = 0
     c                   eval      rkprpa = 0
     c                   eval      rksamf = 0
     c                   eval      rkfagb = 0
     c                   eval      rkekgb = 0
     c                   eval      rkfrgb = 0
     c                   eval      rkrasp = 0
     c                   eval      rkfrsk = *blank
     c                   eval      rkpers = *blank
     c                   eval      rkftnr = 0
     c                   eval      rksekv = 0
     c                   eval      rkfagr = 0
     c                   eval      rkafda = *loval
     c                   eval      rkatda = *loval
     c                   eval      rksent = 0
     c                   eval      rktilb = 0
     c                   eval      rkfcop = 0
     c                   eval      rkedik = *blank
     c                   eval      rkeank = 0
     c                   eval      rkeanf = 0
     c                   eval      rkinkk = *blank
     c                   eval      rkkomn = 0
     c                   eval      rkkoku = 0
     c                   eval      rkemal = *blank
     c                   eval      rkndat = w_udat
     c                   eval      rknsig = l_user
     c                   eval      rkedat = w_udat
     c                   eval      rkesig = l_user
      *
     c                   time                    rkntim
     c                   time                    rketim
6.10 c                   time                    rk2dpt
6.10 c                   eval      rkepgm = 'RK100R    '
      *
     c                   write     rkunlur
6.21  * Oppretter memosaldo
6.21 c     rkunlu_key    chain     rkmelur
6.21  *
6.21 c                   if        not %found
6.21 c                   eval      rxfirm = l_firm
6.21 c                   eval      rxkund = rkkund
6.21 c                   eval      rxmems = *zero
6.21 c                   eval      rxmema = *zero
6.30 c                   eval      rxkjal = *zero
6.21 c                   time                    rxmeda
6.21 c                   time                    rxmoda
6.21 c                   time                    rxmeti
6.21 c                   time                    rxmoti
6.21 c                   eval      rxmeus = l_user
6.21 c                   write     rkmelur                                      Skriv memosaldo
6.21 c                   endif
6.21  *
     c                   call      'RK750R'
     c                   parm                    w_firm
     c                   parm                    rkkund
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1kund = s_kund
     c                   eval      b1kund = k1kund
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
      * Initierer SQL for søk
      *
     c                   if        w_seqe = 'S' and
     c                             b_open_sok = *on
7.04 c/exec sql
7.04 c+ close STMT
7.04 c/end-exec
7.02 c                   eval      sqlquery = 'select rksfir, +
7.02 c                              rkstek, +
7.02 c                              rkskun  +
7.02 c                               from   rksopf a  +
7.02 c                              where  rksfir = ? and +
7.03 c                              rkstek like ? '
7.03 c
7.03 c                   if        w_kotps = *on
7.03 c                   eval      sqlquery = %trim(sqlquery) +
7.03 c                              ' and exists (  +
7.02 c                              select rufirm from rukppf +
7.02 c                              where rufirm = a.rksfir +
7.02 c                               and rukund = a.rkskun  +
7.03 c*                               and (rualfa LIKE ?) +
7.03 c*                               and (  (? = '''') OR +
7.03 C*                                 (rualfa LIKE ?)) +
7.03 c                               and (rualfa LIKE '''+%trim(w_stek1)+''') +
7.03 c                               and (  ('''+%trim(w_stek2)+''' = '''') OR +
7.03 C                                 (rualfa LIKE '''+%trim(w_stek2)+''')) +
7.03 c                               ) '
7.03 c                   endif
7.03 c                   eval      sqlquery = %trim(sqlquery) +
7.03 c                              ' order by rksfir, rkstek  +
     c                              for read only'
      *
7.02 c/exec sql
7.02 c+ PREPARE SQL_STMT FROM :sqlquery
7.02 c/end-exec
7.02 c/exec sql
7.02 c+ DECLARE STMT CURSOR FOR SQL_STMT
7.02 c/end-exec
7.02 c                   if        sqlcod = 100 or sqlstt = '02000' or sqlcod<0
7.02 c                   eval      *in15 = *on
7.02 c                   leavesr
7.02 c                   endif
7.03 c*                   if        w_kotps = *on
7.03 c*/exec sql
7.03 c*+ OPEN STMT using :w_firm, :w_ste1, :w_stek1, :w_stek2, :w_stek2
7.03 c*/end-exec
7.03 c*                   else
7.02 c/exec sql
7.02 c+ OPEN STMT using :w_firm, :w_ste1
7.02 c/end-exec
7.03 c*                   endif
      *
7.02 c*/exec sql
7.02 c*+                  declare sok cursor for
7.02 c*+                  select rksfir,
7.02 c*+                         rkstek,
7.02 c*+                         rkskun
7.02 c*+                  from   rksopf a
7.02 c*+                  where  rksfir = :w_firm and
7.02 c*+                         rkstek like :w_ste1
7.02 c*+                         and exists (
7.02 c*+                            select rufirm from rukppf
7.02 c*+                            where rufirm = a.rksfir and rukund = a.rkskun
7.02 c*+                            and (:w_stek1 = '' OR (rualfa LIKE :w_stek1))
7.02 c*+                            and (:w_stek2 = '' OR (rualfa LIKE :w_stek2))
7.02 c*+                         )
7.02 c*+                  order by rksfir, rkstek
7.02 c*+                  for read only
7.02 c*/end-exec
7.02 c*/exec sql
7.02 c*+                  close sok
7.02 c*/end-exec
7.02 c*/exec sql
7.02 c*+                  open sok
7.02 c*/end-exec
      *
     c                   endif
      *
      * Fyller opp subfile
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'S'
7.02 c*/exec sql
7.02 c*+                  fetch next
7.02 c*+                  from  sok
7.02 c*+                  into  :rksfir,
7.02 c*+                        :rkstek,
7.02 c*+                        :rkskun
7.02 c*/end-exec
7.02 c/exec sql
7.04 c+ fetch next from STMT
7.02 c+                  into  :rksfir,
7.02 c+                        :rkstek,
7.02 c+                        :rkskun
7.02 c/end-exec
7.02 c                   if        sqlcod = 100 or sqlstt = '02000' or sqlcod<0
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'A'
     c                   read      rkunl2                                 15
     c                   when      w_seqe = 'P'
     c                   read      rkunl3                                 15
     c                   other
     c                   read      rkunl1                                 15
     c                   endsl
      *
     c                   if        *in15
     c                   goto      end_crt
     c                   endif
      *
     c                   if        w_seqe = 'S'
      *
     c                   if        w_ste2 <> *blank
      *
     c                   eval      w_posi = %scan(%trim(w_ste2):%trim(rkstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
      *
     c                   if        w_ste3 <> *blank
     c                   eval      w_posi = %scan(%trim(w_ste3):%trim(rkstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        w_ste4 <> *blank
     c                   eval      w_posi = %scan(%trim(w_ste4):%trim(rkstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        w_ste5 <> *blank
     c                   eval      w_posi = %scan(%trim(w_ste5):%trim(rkstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
     c                   endif
      *
     c                   endif
      *
     c                   eval      rkunl1_kund = rkskun
     c     rkunl1_key    chain     rkunl1                             91
     c                   if        *in91 = *on
     c                   iter
     c                   endif
      *
     c                   endif
      *
     c                   if        rkfirm <> w_firm
     c                   eval      *in15 = *on
     c                   goto      end_crt
     c                   endif
      *
      * Utelat passive kunder
      *
     c                   if        *in55 = *on and
     c                             rkpass = 'J'
     c                   iter
     c                   endif
      *
      * Vis bare kunder som er hurtigopprettet
      *
     c                   if        *in56 = *on
     c                   if        rkndat <> rkedat or
     c                             rkntim <> rketim or
     c                             rknsig <> rkesig
     c                   iter
     c                   endif
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1kund = rkkund
     c                   eval      b1navn = rknavn
     c                   eval      b1sted = rksted
     c                   eval      b1tlfn = rktlfn
     c                   eval      b1alfa = rkalfa
     c                   eval      b1ponr = rkponr
      *
     c                   if        *in80 = *on
     c                   eval      b1sted = rkgate
     c                   eval      b1tlfn = rkmobn
     c                   endif
T5.30c                   if        *in81 = *on
T5.30c                   eval      b1tlfn = *blank
T5.30c                   if        rkftnr <> *zero
T5.30c                   movel     rkftnr        b1tlfn
T5.30c                   endif
T5.30c                   endif
7.01  *
7.01 c                   exsr      newlo_vask
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   eval      b_open_sok = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   select
     c                   when      w_seqe = 'A' and
     c                             %eof(rkunl2)
     c     rkunl2_key    setgt     rkunl2
     c                   readp     rkunl2
     c                   when      w_seqe = 'P' and
     c                             %eof(rkunl3)
     c     rkunl3_key    setgt     rkunl3
     c                   readp     rkunl3
     c                   when      w_seqe = *blank and
     c                             %eof(rkunl1)
     c     rkunl1_key    setgt     rkunl1
     c                   readp     rkunl1
     c                   endsl
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'A'
     c                   readp     rkunl2
     c                   when      w_seqe = 'P'
     c                   readp     rkunl3
     c                   other
     c                   readp     rkunl1
     c                   endsl
      *
     c                   if        %eof or
     c                             rkfirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'A'
     c                   eval      rkunl2_alfa = rkalfa
     c                   when      w_seqe = 'P'
     c                   eval      rkunl3_ponr = rkponr
     c                   other
     c                   eval      rkunl1_kund = rkkund
     c                   endsl
      *
     c                   enddo
      *
     c                   select
     c                   when      w_seqe = 'A' and
     c                             %eof(rkunl2)
     c     rkunl2_key    setll     rkunl2
     c                   when      w_seqe = 'P' and
     c                             %eof(rkunl3)
     c     rkunl3_key    setll     rkunl3
     c                   when      w_seqe = *blank and
     c                             %eof(rkunl1)
     c     rkunl1_key    setll     rkunl1
     c                   endsl
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      /eject
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  * Newlook vask av data
7.01  *  Fjerner tegn som gjør at Newlook feiltolker
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *
7.01   begsr newlo_vask;
7.01     for w_teller = 1 to 5 ;
7.01       w_len = %len(%trim(b1navn));
7.01       if (w_len > 0);
7.01       b1navn = %ScanRpl('.':'':b1navn:w_len); // newlook tåler ikke . på slutten
7.01       endif;
7.01     endfor ;
7.01   endsr;
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekk av input ved kopiering
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
      * Sjekker postnummer
      *
     c                   if        k1ponr <> 0
     c                   eval      apospf_ponr = k1ponr
     c     apospf_key    chain     apospf                             32
     c                   if        *in32 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      k1sted = *blank
     c                   movel     apponr        k1sted
     c                   move      apsted        k1sted
     c                   endif
      *
      * Sjekke om betalingsbetingelse er gyldig
      *
     c                   if        k1betb = 0
     c                   eval      k1betb = w_1bek
     c                   endif
      *
     c                   eval      p_ckod = k1betb
     c                   call      'RS703R'
     c                   parm                    p_stat
     c                   parm                    p_ckod
     c                   parm                    p_ctxt
      *
     c                   if        p_stat = 'N'
     c                   eval      *in33 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * Sjekke om betalingsmåte er gyldig
      *
     c                   if        k1betm <> 0
     c                   eval      p_bkod = k1betm
     c                   call      'RS702R'
     c                   parm                    p_stat
     c                   parm                    p_bkod
     c                   parm                    p_btxt
      *
     c                   if        p_stat = 'N'
     c                   eval      *in34 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
      * Sjekke om kundekategori er gyldig
      *
     c                   if        k1katg <> 0
     c                   eval      p_kkod = k1katg
     c                   call      'RS711R'
     c                   parm                    p_stat
     c                   parm                    p_kkod
     c                   parm                    p_ktxt
      *
     c                   if        p_stat = 'N'
     c                   eval      *in35 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
      * Sjekke om selger gyldig
      *
     c                   if        k1slgr <> 0
     c                   eval      p_ikod = k1slgr
     c                   call      'RS709R'
     c                   parm                    p_stat
     c                   parm                    p_ikod
     c                   parm                    p_itxt
      *
     c                   if        p_stat = 'N'
     c                   eval      *in36 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
T400  * Sjekke om rabattkategori er gyldig
      *
T400 c                   if        k1rabk <> 0
T400 c                   eval      p_fkod = k1rabk
T400 c                   call      'RS706R'
     c                   parm                    p_stat
     c                   parm                    p_fkod
     c                   parm                    p_ftxt
      *
     c                   if        p_stat = 'N'
T400 c                   eval      *in39 = *on
T400 c                   eval      b_feil = *on
T400 c                   goto      end_sjekk
T400 c                   endif
T400 c                   endif
      *
     c     end_sjekk     endsr
      *
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Vise posteringer for kunde
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posteringer   begsr
      *
     c                   call      'RK112R'
     c                   parm                    w_valg
     c                   parm                    b1kund
      *
     c                   eval      b2kund = *zeros
     c                   eval      b2alfa = *blank
     c                   eval      *in31  = *on
     c                   endsr
      *
      /eject
7.05  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7 05  * Subrutine for å behandle bilde for Smartkalk
7.05  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.05  *
7.05 c     xc7bld        begsr
7.05  *
7.05 c     c7tag1        tag
7.05 c                   eval      c1kund = b1kund
7.05 c                   eval      c1navn = *blanks
7.05 c                   eval      c1skey = *blanks
7.05 c                   eval      c1pass = *blanks
7.05 c                   eval      c1kprs = *blanks
7.05 c                   eval      c1kmai = *blanks
7.05 c                   eval      *in34  = *off
7.05 c                   eval      rkunlr_kund = b1kund
7.05 c     rkunlr_key    chain     rkunlr
7.05 c                   if        %found
7.05 c                   eval      c1navn = rknavn
7.05 c                   endif
7.05  *
7.05  * Har kunde allerede fått generert nøkkel
7.05  *
7.05  *
7.05 c                   eval      keysiu_ftnr = rkftnr
7.05 c     keysiu_key    chain     keysiu
7.05 c                   if        %found
7.05 c                   eval      c1skey = kysecr
7.05 c                   if        kypass = 'P'
7.05 c                   eval      c1pass = 'Passiv!'
7.05 c                   eval      *in34  = *on
7.05 c                   end
7.05 c                   unlock    keysiu
7.05 c                   endif
7.05  *
7.05 c                   eval      opefiu_ftnr = rkftnr
7.05 c     opefiu_key    chain     opefiu
7.05 c                   if        %found
7.05 c                   unlock    opefiu
7.05 c                   eval      rukpl1_kund = rkkund
7.05 c                   eval      rukpl1_levr = 0
7.05 c                   eval      rukpl1_kpnr = opkpnr
7.05 c     rukpl1_key    chain     rukpl1
7.05 c                   if        %found
7.05 c                   eval      c1kprs = rualfa
7.05 c                   eval      c1kmai = rumail
7.05 c                   endif
7.05 c                   endif
7.05  *
7.05 c     c7taga        tag
7.05  *
7.05 c                   exfmt     c7bld
7.05  *
7.05 c                   select
7.05 c                   when      *inkc or *inkl
7.05 c                   goto      end_c7bld
7.05 c                   when      *inkg
7.05 c                   if        rkftnr = 0
7.05 c                   eval      *in31 = *on
7.05 c                   goto      c7taga
7.05 c                   endif
7.05 c                   if        c1skey <> '  '
7.05 c                   goto      c7taga
7.05 c                   endif
7.05 c                   exsr      lag_key
7.05 c                   goto      c7taga
7.05 c                   when      *inkd
7.05 c                   exsr      hent_person
7.05 c                   goto      c7taga
7.05 c                   when      *inkj
7.05 c                   if        c1pass =  ' '
7.05 c                   exsr      lag_mail
7.05 c                   endif
7.05 c                   goto      c7taga
7.05 c                   when      *inkk
7.05 c                   exsr      pas_key
7.05 c                   goto      c7tag1
7.05 c                   endsl
7.05  *
7.05  * Sjekk at all info er tigjengelig før vi lagrer
7.05  *
7.05 c                   if        c1skey <> *blanks and
7.05 c                             c1kmai = *blanks
7.05 c                   eval      *in33 = *on
7.05 c                   goto      c7taga
7.05 c                   endif
7.05  *
7.05 c                   if        c1skey = *blanks
7.05 c                   eval      *in32 = *on
7.05 c                   goto      c7taga
7.05 c                   endif
7.05  *
7.05  * Opprette post med nøkkel dersom den ikke finnes fra før
7.05  *
7.05 c                   eval      keysiu_ftnr = rkftnr
7.05 c     keysiu_key    chain     keysiu
7.05  *
7.05 c                   time                    kyedat
7.05 c                   time                    kyetim
7.05 c                   eval      kyeusr = l_user
7.05  *
7.05 c                   if        not %found
7.05 c                   clear                   keysiur
7.05 c                   eval      kyftnr = rkftnr
7.05 c                   eval      kysecr = c1skey
7.05  *
7.05 c                   time                    kyodat
7.05 c                   time                    kyotim
7.05 c                   eval      kyousr = l_user
7.05  *
7.05 c                   write     keysiur
7.05 c                   endif
7.05  *
7.05  * Opprette post med kontaktperson
7.05  *
7.05 c                   eval      opefiu_ftnr = rkftnr
7.05 c     opefiu_key    chain     opefiu
7.05  *
7.05 c                   time                    opodat
7.05 c                   time                    opetim
7.05 c                   eval      opeusr = l_user
7.05  *
7.05 c                   if        not %found
7.05 c                   clear                   opefiur
7.05 c                   eval      opftnr = rkftnr
7.05 c                   eval      opkpnr = p_kpnr
7.05  *
7.05 c                   time                    opodat
7.05 c                   time                    opotim
7.05 c                   eval      opousr = l_user
7.05  *
7.05 c                   write     opefiur
7.05  * Send mail til kontaktperson
7.05 c                   exsr      lag_mail
7.05  *
7.05 c                   else
7.05 c                   eval      opkpnr = p_kpnr
7.05 c                   update    opefiur
7.05 c                   endif
7.05  *
7.05 c     end_c7bld     tag
7.05  *
7.05 c                   endsr
7.05  *
7.05  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.05  * Lag nøkkel til Smartkalk
7.05  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.05  *
7.05 c     lag_key       begsr
7.05  *
7.05 c                   eval      p_uuid = *blanks
7.05 c                   call      'UU001R'
7.05 c                   parm                    p_uuid
7.05  *
7.05 c                   eval      c1skey = p_uuid
7.05  *
7.05 c                   endsr
7.05  *
7.05  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.05  * Send mail til  Smartkalk
7.05  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.05  *
7.05 c     lag_mail      begsr
7.05  * henter mailadresse fra
7.05 c                   eval      afudss = *blank
7.05 c                   eval      afpslr_ufil = l_filg
7.05 c                   eval      afpslr_uide = 'NOMAIL   '
7.05 c     afpslr_key    chain     afpslr
7.05 c                   if        %found
7.05  *
7.05 c                   eval      p_rec1 = c1kmai
7.05 c                   eval      p_rec2 = *blanks
7.05 c                   eval      p_rec3 = *blanks
7.05 c                   eval      p_subj = *blanks
7.05 c                   eval      p_txt1 = *blanks
7.05 c                   eval      p_txt2 = *blanks
7.05 c                   eval      p_txt3 = *blanks
7.05 c                   eval      p_txt4 = *blanks
7.05 c                   eval      p_avsl = *blanks
7.05  *
7.05 c                   eval      p_send  = afudss
7.05 c                   eval      p_subj  = 'Smartkalk nøkkel'
7.05 c                   eval      p_txt1  = 'Denne nøkkelen er blitt generert +
7.05 c                                        til dere - ' + %trim(c1skey)
7.05 c                   eval      p_txt2  = 'Legg inn nøkkel i Smartkalk i hen+
7.05 c                                       hold til Holtes instruksjoner før'
7.05 c                   eval      p_txt3  = 'oppkobling mot byggvarehus.'
7.05 c                   eval      p_txt4  = 'Vennlig hilsen'
7.05 c                   eval      p_avsl  = %trim(l_fnav)
7.05 c*                  eval      p_avsl  = %trim(l_user)
7.05  *
7.05 c                   call      'AF726R'
7.05 c                   parm                    p_rec1
7.05 c                   parm                    p_rec2
7.05 c                   parm                    p_rec3
7.05 c                   parm                    p_send
7.05 c                   parm                    p_subj
7.05 c                   parm                    p_txt1
7.05 c                   parm                    p_txt2
7.05 c                   parm                    p_txt3
7.05 c                   parm                    p_txt4
7.05 c                   parm                    p_avsl
7.05  *
7.05 c                   endif
7.05  *
7.05 c                   endsr
7.05  *
7.05  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.05  * Sett nøkkel til Smartkalk aktiv/passiv
7.05  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.05  *
7.05 c     pas_key       begsr
7.05  *
7.05 c                   eval      keysiu_ftnr = rkftnr
7.05 c     keysiu_key    chain     keysiu
7.05  *
7.05 c                   time                    kyedat
7.05 c                   time                    kyetim
7.05 c                   eval      kyeusr = l_user
7.05  *
7.05 c                   if        %found
7.05  *
7.05 c                   if        kypass = ' '
7.05 c                   eval      kypass = 'P'
7.05 c                   else
7.05 c                   eval      kypass = ' '
7.05 c                   endif
7.05  *
7.05 c                   time                    kyodat
7.05 c                   time                    kyotim
7.05 c                   eval      kyousr = l_user
7.05  *
7.05 c                   update    keysiur
7.05 c                   endif
7.05  *
7.05 c                   endsr
7.05  *
7.05  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.05  * Hent kontaktperson
7.05  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.05  *
7.05 c     hent_person   begsr
7.05  *
7.05 c                   eval      p_kund = rkkund
7.05  *
7.05 c                   call      'RK503R'
7.05 c                   parm                    w_firm
7.05 c                   parm                    p_kund
7.05 c                   parm                    p_kref
7.05 c                   parm                    p_kpnr
7.05 c                   parm                    p_emal
7.05  *
7.05 c                   eval      c1kmai = p_emal
7.05 c                   eval      c1kprs = p_kref
7.05  *
7.05 c                   endsr
7.05  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på kunde
      *
     c     rkunlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunlr_kund
      *
      * Key for posisjonering på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for posisjonering på alfa
      *
     c     rkunl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl2_alfa
      *
      * Key for posisjonering på postnummer
      *
     c     rkunl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl3_ponr
      *
      * Key for oppdatering av kunde
      *
     c     rkunlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunlu_kund
      *
      * Key for oppdatering av kunde-søketekst
      *
     c     rksolu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rksolu_skun
      *
      * Key for oppslag på kundeposter
      *
     c     rktrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rktrl1_kund
      *
      * Key for oppslag på statuskode 1
      *
     c     raa1l1_key    klist
     c                   kfld                    w_firm
      *
      * Key for postnummer-register
      *
     c     apospf_key    klist
     c                   kfld                    apospf_ponr
      *
T5.30 * Key for avtalespesifikasjon
      *
T5.30c     rksalu_key    klist
T5.30c                   kfld                    w_firm
T5.30c                   kfld                    rksalu_skun
      *
T5.30 * Key for notatblokk
      *
T5.40c     rklal2_key    klist
T5.30c                   kfld                    w_firm
T5.40c                   kfld                    rklal2_syst
T5.40c                   kfld                    rklal2_pros
T5.40c                   kfld                    rklal2_akti
T5.40c                   kfld                    rklal2_kund
7.05  *
7.05  * Key for Smartkalk
7.05  *
7.05 c     keysiu_key    klist
7.05 c                   kfld                    keysiu_ftnr
7.05  *
7.05  * Key for Smartkalk
7.05  *
7.05 c     opefiu_key    klist
7.05 c                   kfld                    opefiu_ftnr
7.05  *
7.05  * Key for kontaktperson
7.05  *
7.05 c     rukpl1_key    klist
7.05 c                   kfld                    w_firm
7.05 c                   kfld                    rukpl1_kund
7.05 c                   kfld                    rukpl1_levr
7.05 c                   kfld                    rukpl1_kpnr
7.05  *
7.05  * Key for properties
7.05  *
7.05 c     afpslr_key    klist
7.05 c                   kfld                    afpslr_ufil
7.05 c                   kfld                    afpslr_uide
7.05  *
7.05  * Key for modul
7.05  *
7.05 c     amodl1_key    klist
7.05 c                   kfld                    amodl1_modu
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
6.22 c                   eval      b2firm = w_firm
      *
      * Henter statuskode 1
      *
     c     raa1l1_key    chain     raa1l1                             90
     c                   if        *in90 = *off
     c                   eval      w_1bek = ra1bek
     c                   eval      w_1bkk = ra1bkk
     c                   else
     c                   eval      w_1bek = 15
     c                   eval      w_1bkk = 0
     c                   endif
      *
      * Henter dagens dato
      *
     c                   time                    w_udat
      *
     c                   eval      *in22 = *on
     c                   eval      *in55 = *off
     c                   eval      *in56 = *off
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      rkunl1_kund = 0
     c     rkunl1_key    setll     rkunl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
