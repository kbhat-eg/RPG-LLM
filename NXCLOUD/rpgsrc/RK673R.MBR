      /TITLE  Utskrift av utelatte kunder
     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RK673R                                        *
      * Beskrivelse . . : Utskrift av kunder utelatt fra renter/purring *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
T5.30 * V5.30 140904 HFL  Kan skrive kladdeblokk.
T5.40 * V5.40 150605 HFL  Leser RKLAL2 i stedet for RKLALU.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KA = F1  = Valg av firma                                  *
      *       KC = F3  = Exit                                           *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
E5.40frklal2    if   e           k disk    rename(rklapfr:rklal2r)
     frk673p    o    e             printer oflind(*in30)
      *
      * Definering av Local data area
      *
     d                uds
      *
T5.30d l_klad                301    301
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d w_type          s              1
     d w_edat          s               d   datfmt(*iso)
      *
     d w_tota          s              7  0
     d w_anta          s              5  0
     d w_firm          s                   like(l_firm)
     d w_slett         s              1
T5.30d w_syst          s                   like(rxsyst)
T5.30d w_pros          s                   like(rxpros)
T5.30d w_akti          s                   like(rxakti)
T5.30d w_kont          s                   like(rxkont)
      *
      * Les kunderegister
      *
     c     rkunl1_key    setll     rkunl1                                                    .
     c     rkunl1_key    reade     rkunl1                                 90
      *
     c                   dow       *in90 = *off
      *
     c                   exsr      kontroll
      *
     c                   if        w_slett = 'X'
      *
     c                   exsr      utskrift
T5.30c                   if        l_klad = 'J'
T5.30c                   exsr      kladde
T5.30c                   endif
      *
     c                   endif
      *
     c     rkunl1_key    reade     rkunl1                                 90
     c                   enddo
      *
     c     slutt         tag
      *
     c                   eval      *inlr = *on
     c                   eval      t1tota = w_tota
     c                   eval      t1anta = w_anta
      *
     c                   write     t1tot
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kontroll
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kontroll      begsr
      *
      * Tell opp antall kunder
      *
     c                   eval      w_tota = w_tota + 1
     c                   eval      w_slett = ' '
      *
      * Sjekk om kunden er merket (Inkasso)
      *
     c                   if        w_type = 'I' and
     c                             rkrenk = 2
     c                   eval      w_slett = 'X'
     c                   endif
      *
      * Sjekk om kunden er merket (Purring)
      *
     c                   if        w_type = 'P'
     c                   if        rkrenk = 1 or
     c                             rkrenk = 2
     c                   eval      w_slett = 'X'
     c                   endif
     c                   endif
      *
      * Sjekk om kunden er merket (Renteberegning)
      *
     c                   if        w_type = 'R' and
     c                             rkrenk = 1
     c                   eval      w_slett = 'X'
     c                   endif
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     utskrift      begsr
      *
      * Tell antall kunder som kan/blir slettet
      *
     c                   eval      w_anta = w_anta + 1
      *
      * Sjekker om overflow
      *
     c                   if        *in30 = *on                                               .
     c                   write     a1hdr                                                     .
     c                   eval      *in30 = *off                                              .
     c                   endif                                                           .....
      *
     c                   write     d1det
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
T5.30 * Subrutine for kladdeblokk                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
T5.30c     kladde        begsr
T5.30 * Finnes notatblokk på kunden?
      *
T5.30c                   eval      w_syst = 'K'                                              .
T5.30c                   eval      w_pros = *zeros                                           .
T5.30c                   eval      w_akti = *blanks                                          .
T5.30c                   eval      w_kont = rkkund                                           .
E5.40c     keynot        setll     rklal2                                 60
      *
T5.30c                   if        *in60 = *on
      *
E5.40c     keynot        reade     rklal2                                 61
T5.30c                   dow       *in61 = *off
T5.30c                   eval      wnotat = rxtext
      *
T5.30 * Sjekker om overflow
      *
T5.30c                   if        *in30 = *on                                               .
T5.30c                   write     a1hdr                                                     .
T5.30c                   eval      *in30 = *off                                              .
T5.30c                   endif                                                           .....
T5.30c                   write     notatb                                                    .
E5.40c     keynot        reade     rklal2                                 61
T5.30c                   enddo                                                           .....
T5.30c                   endif                                                               .
T5.30c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    w_type
     c                   parm                    w_edat
      *
      * Key for kunderegister
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
      *
T5.30 * Key for notatblokk
      *
T5.30c     keynot        klist
T5.30c                   kfld                    w_firm
T5.30c                   kfld                    w_syst
T5.30c                   kfld                    W_pros
T5.30c                   kfld                    W_akti
T5.30c                   kfld                    W_kont
T5.30c*                  kfld                    w_spes
T5.30c*                  kfld                    w_avde
T5.30c*                  kfld                    w_tist
T5.30c*                  kfld                    w_linj
      *
     c                   eval      w_firm = l_firm
      *
      * Sjekk om kunden er merket (Inkasso)
      *
     c                   if        w_type = 'I'
     c                   eval      *in11 = *on
     c                   endif
      *
      * Sjekk om kunden er merket (Purring)
      *
     c                   if        w_type = 'P'
     c                   eval      *in12 = *on
     c                   endif
      *
      * Sjekk om kunden er merket (Renteberegning)
      *
     c                   if        w_type = 'R'
     c                   eval      *in13 = *on
     c                   endif
      *
      * Skriv heading på første side
      *
     c                   write     a1hdr
      *
     c                   endsr
      *
