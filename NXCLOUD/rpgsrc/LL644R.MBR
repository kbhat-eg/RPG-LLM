      /TITLE  Utskrift av hyllevarmere
     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * Program . . . . : LL644R                                        *
      * Beskrivelse . . : Utskrift av hyllevarmere                      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
6.30  * 6.30      240914  Endret logikk ved avslutning av program  bsa  *
6.30  *                   Småjustering av layout.                       *
      *                                                                 *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     flwa2l3    if   e           k disk    rename(lwa2pfr:lwa2l3r)
     flwa2l1    if   e           k disk    rename(lwa2pfr:lwa2l1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
      *
     fll644p    o    e             printer
      *
      /eject
      *
      * Local Data Area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variable i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d lwa2l1_ogrp     s                   like(mwogrp)
     d lwa2l1_hgrp     s                   like(mwhgrp)
     d lwa2l1_ugrp     s                   like(mwugrp)
     d lwa2l1_vare     s                   like(mwvare)
      *
      *  Parameter-rec
      *
     d                 ds
     d p_prec                       128
     d  p_fvar                        8  0 overlay(p_prec)
     d  p_tvar                        8  0 overlay(p_prec:9)
     d  p_flag                        2  0 overlay(p_prec:17)
     d  p_tlag                        2  0 overlay(p_prec:19)
     d  p_fogr                        2  0 overlay(p_prec:21)
     d  p_togr                        2  0 overlay(p_prec:23)
     d  p_fhgr                        2  0 overlay(p_prec:25)
     d  p_thgr                        2  0 overlay(p_prec:27)
     d  p_fugr                        3  0 overlay(p_prec:29)
     d  p_tugr                        3  0 overlay(p_prec:32)
     d  p_levn                        6  0 overlay(p_prec:35)
     d  p_sakb                        4    overlay(p_prec:41)
     d  p_vari                        1  0 overlay(p_prec:45)
     d  p_type                        1  0 overlay(p_prec:46)
6.30 d  p_hori                        1  0 overlay(p_prec:47)
      *
     d w_parm          s            128
     d totlve          s             13  2
     d totoms          s             13  2
     d w_lave          s             15  2
     d w_omse          s             15  2
      *
     d w_firm          s                   like(mwfirm)
     d w_dato          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_ogrp          s                   like(mwogrp)
     d w_hgrp          s                   like(mwhgrp)
     d w_ugrp          s                   like(mwugrp)
     d w_oogr          s                   like(vgoogr)
     d w_otxt          s                   like(vgotxt)
     d w_tek1          s                   like(vvtek1)
     d w_hogr          s                   like(vghogr)
     d w_hhgr          s                   like(vghhgr)
     d w_htxt          s                   like(vghtxt)
     d w_uogr          s                   like(vguogr)
     d w_uhgr          s                   like(vguhgr)
     d w_uugr          s                   like(vguugr)
     d w_utxt          s                   like(vgutxt)
      *
      /eject
      *
      * Behandling av detalj-records
      *
     c     les           tag
      *
     c                   read      lwa2l3r                                80
6.30 c**                 if        *in80 = *on or
6.30 c*                            mwvare = *zeros
6.30 c                   if        *in80 = *on
     c                   goto      slutt
     c                   endif
      *
6.30 c                   if        mwvare = *zeros
6.30 c                   goto      les
6.30 c                   endif
      *
      * Finner varetekst
      *
     c                   movel     mwvare        vvarl1_vare
     c     vvarl1_key    chain     vvarl1                             80
     c                   eval      w_tek1 = *blank
     c                   if        *in80 = *off
     c                   movel     vvtek1        w_tek1
     c                   endif
      *                                                                 .
      * Legger ut varelinje
      *
     c                   eval      d1vare = mwvare
     c                   eval      d1tek1 = w_tek1
     c                   eval      d1enhl = mwenhl
     c                   eval      d1omst = mwomst
     c                   eval      d1lagv = mwlagv
      *
      * Beregner akkumulatorer og prosenter
      *
     c                   eval      w_omse = mwomst * 100
      *
     c                   if        totoms <> *zero
     c                   if        w_omse / totoms > 999
     c                   eval      d1pro1 = 999
     c                   else
     c                   eval(h)   d1pro1 = w_omse / totoms
     c                   endif
     c                   endif
      *
     c                   eval      w_lave = mwlagv * 100
      *
     c                   if        totlve <> *zero
     c                   if        w_lave / totlve > 999
     c                   eval      d1pro2 = 999
     c                   else
     c                   eval(h)   d1pro2 = w_lave / totlve
     c                   endif
     c                   endif
      *
     c                   eval      d1akk1 = d1akk1 + mwomst
     c                   eval      d1akk2 = d1akk2 + mwlagv
      *
     c                   eval      w_omse = d1akk1 * 100
      *
     c                   if        totoms <> *zero
     c                   if        w_omse / totoms > 999
     c                   eval      d1apr1 = 999
     c                   else
     c                   eval(h)   d1apr1 = w_omse / totoms
     c                   endif
     c                   endif
      *
     c                   eval      w_lave = d1akk2 * 100
      *
     c                   if        totlve <> *zero
     c                   if        w_lave / totlve > 999
     c                   eval      d1apr2 = 999
     c                   else
     c                   eval(h)   d1apr2 = w_lave / totlve
     c                   endif
     c                   endif
      *
     c                   eval      d1tell = d1tell + 1
      *
      * Skriv heading ved overflow
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
      *
      * Skriv detaljlinje
      *
     c                   write     d1det                                30
      *
     c                   goto      les
      /SPACE 3
      *
      *   Slutt
      *
     c     slutt         tag
      *
     c                   eval      *inlr = *on
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    w_parm
     c                   eval      p_prec = w_parm
      *
      * Legge inn firmanummer
      *
     c                   move      l_firm        w_firm
     c                   move      l_firm        mwfirm
      *
      * Riktig selektering
      *
     c                   eval      *in71 = p_vari = 1
      *
     c                   eval      *in91 = p_type = 1
      *
     c                   eval      totoms = 0
     c                   eval      totlve = 0
      *
     c                   eval      mwvare = *zeros
     c                   eval      mwogrp = *zeros
     c                   eval      mwhgrp = *zeros
     c                   eval      mwugrp = *zeros
      *
     c     lwa2l1_key    setll     lwa2l1
      *
      * Les første record med totaler
      *
     c                   read      lwa2l1r                                80
      *
     c                   eval      totoms = mwomst
     c                   eval      totlve = mwlagv
      *
      * Key for arbeidsregister
      *
     c     lwa2l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lwa2l1_ogrp
     c                   kfld                    lwa2l1_hgrp
     c                   kfld                    lwa2l1_ugrp
     c                   kfld                    lwa2l1_vare
      *
      * Key for vareregister
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Redigere heading
      *
     c                   time                    w_dato
     c                   time                    w_time
      *
     c     *dmy          move      w_dato        a1date
     c     *hms          move      w_time        a1time
      *
     c                   eval      a1user = l_user
     c                   eval      a1wsid = l_wsid
     c                   eval      a1firm = l_firm
     c                   eval      a1fnav = l_fnav
6.30 c                   eval      a1hori = p_hori
      *
      * Skriv heading
      *
     c                   write     a1hdr
      *
     c                   endsr
