      /TITLE  Omberegning av budsjett - parameter
     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * Program . . . . : RH127R                                        *
      * Beskrivelse . . : Omberegning av budsjett - parameter           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
      *                   Nytt program april 2001                   KOB *
T5.30 * V5.30     101204  Kostnadsbærer fra/til lagt ut i LDA.      HFL *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KA = F1  = Valg av firma                                  *
      *       KC = F3  = Exit                                           *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     fausrl1    if   e           k disk
     frh127d    cf   e             workstn
      *
     d                uds
     d rhpaar                300    303  0
     d rhpavf                         4  0
     d rhpavt                         4  0
     d rhpspf                         4  0
     d rhpspt                         4  0
     d rhpktf                         5  0
     d rhpktt                         5  0
     d rhpkod                         2  0
     d rhptyp                         1
      *
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d wpakod          s              2  0
     d p_in03          s              1
     d w_firm          s                   like(l_firm)
     d w_user          s                   like(l_user)
     d w_navn          s                   like(l_fnav)
      *
     c     dummy         tag
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av felter                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      *
      * A2BLD  - A2 Bilde for valg av parametre
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * A2BLD Behandle bilde for valg av utskriftskriteria              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     a2taga        tag
      *
      * Bare data
      *
     c     a2tagb        tag
      *
     c                   exfmt     a2bld
      *
      * Gå ut
      *
     c                   if        *inkc = *on                                  .....
     c                   eval      p_in03 = '1'                                     .
     c                   goto      xslutt                                           .
     c                   endif                                                  .....
      *
      *  Kontroll avdeling
      *
     c                   if        a2pavf > a2pavt                                       .....
     c                   eval      *in31 = *on                                               .
     c                   goto      a2taga                                                    .
     c                   endif                                                           .....
      *
      *  Kontroll konto
      *
     c                   if        a2pktf > a2pktt                                       .....
     c                   eval      *in32 = *on                                               .
     c                   goto      a2taga                                                    .
     c                   endif                                                           .....
      *
      *  Kontroll av år
      *
     c                   if        a2paar = *zeros                                       .....
     c                   eval      *in34 = *on                                               .
     c                   goto      a2taga                                                    .
     c                   endif                                                           .....
      *
      * Skriv parameter til LDA
      *
     c                   move      a2paar        rhpaar
     c                   move      a2pavf        rhpavf
     c                   move      a2pavt        rhpavt
T5.30c                   move      a2pspf        rhpspf
T5.30c                   move      a2pspt        rhpspt
     c                   move      a2pktf        rhpktf
     c                   move      a2pktt        rhpktt
     c                   move      a2pkod        rhpkod
     c                   move      a2ptyp        rhptyp
      *
      /space 3
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c                   eval      wpakod = *zero
      *
      *
      * Legge inn firma-
      * nummer og navn
      * fra parameter
      *
     c                   move      l_user        w_user
     c                   move      l_firm        w_firm
     c                   move      l_fnav        w_navn
      *
     c                   eval      a2pavf = *zeros
     c                   eval      a2pavt = 9999
     c                   eval      a2pspf = *zeros
     c                   eval      a2pspt = 9999
     c                   eval      a2pktf = *zeros
     c                   eval      a2pktt = 99999
     c                   eval      a2ptyp = 'B'
      *
      * Beregner år
      *
     c                   move      *year         a2paar
      *
      * Hent bruker-record
      *
     c     ausrl1_key    chain     ausrl1                             63
      *
      *  Sjekk om begrenset til avdeling
      *
     c                   if        abbavd = '1'                                          .....
     c                   eval      *in30 = *on                                               .
     c                   move      absavd        a2pavf                                      .
     c                   move      absavd        a2pavt                                      .
     c                   endif                                                           .....
      *
     c                   eval      p_in03 = ' '
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_in03
      *
      * Key for bruker-file
      *
     c     ausrl1_key    klist
     c                   kfld                    w_user
      *
     c                   endsr
