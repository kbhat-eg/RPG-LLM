     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : NXCLOUD
      * Program . . . . : RL133R
      * Beskrivelse . . : Vedlikehold av vurdering av kreditt grenser
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       011231 ASK  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fra33i1    if   e           k disk    rename(ra33st:ra33i1r)
     fra33ir    if   e           k disk    rename(ra33st:ra33irr)
     fra33iu    uf a e           k disk    rename(ra33st:ra33iur)
      *
     fra133d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d ra33i1_type     s                   like(rkktyp)
     d ra33ir_kuid     s                   like(rkkuid)
     d ra33iu_kuid     s                   like(rkkuid)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
      *
     d w_firm          s                   like(rkkfir)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   eval      b_oppr = *on
     c                   exsr      xc2bld
     c                   exsr      forny
     c                   eval      b_oppr = *off
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   eval      ra33i1_type = '    '
     c     ra33i1_key    setll     ra33i1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      ra33ir_kuid = b1uid
     c     ra33ir_key    chain     ra33ir
      *
     c                   if        %found and
     c                             b_oppr = *off
     c                   eval      c2type = rkktyp
     c                   eval      c2scr1 = rkksc1
     c                   eval      c2grn1 = rkkgr1
     c                   eval      c2scr2 = rkksc2
     c                   eval      c2grn2 = rkkgr2
     c                   eval      c2scr3 = rkksc3
     c                   eval      c2grn3 = rkkgr3
     c                   eval      c2scr4 = rkksc4
     c                   eval      c2grn4 = rkkgr4
     c                   eval      c2scr5 = rkksc5
     c                   eval      c2grn5 = rkkgr5
     c                   eval      *in30 = *on
     c                   else
     c                   eval      c2type = '   '
     c                   eval      c2scr1 = 0
     c                   eval      c2grn1 = 0
     c                   eval      c2scr2 = 0
     c                   eval      c2grn2 = 0
     c                   eval      c2scr3 = 0
     c                   eval      c2grn3 = 0
     c                   eval      c2scr4 = 0
     c                   eval      c2grn4 = 0
     c                   eval      c2scr5 = 0
     c                   eval      c2grn5 = 0
     c                   eval      b1uid  = 0
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   eval      *in31 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in31
     c                   goto      end_c2bld
     c                   endif
      *
      * Sjekk at type ikke er brukt før
      *
     c                   if        b_oppr = *on
     c                   eval      ra33i1_type = c2type
     c     ra33i1_key    chain     ra33i1r
     c                   if        %found(ra33i1)
     c                   exsr      xc2msg1
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
      * Sjekk at score er i stigende orden
      *
     c                   if        c2scr2 > 0 and
     c                             c2scr2 < c2scr1
     c                   exsr      xc2msg2
     c                   goto      c2taga
     c                   endif
      *
     c                   if        c2scr3 > 0 and
     c                             c2scr3 < c2scr2
     c                   exsr      xc2msg2
     c                   goto      c2taga
     c                   endif
      *
     c                   if        c2scr4 > 0 and
     c                             c2scr4 < c2scr3
     c                   exsr      xc2msg2
     c                   goto      c2taga
     c                   endif
      *
     c                   if        c2scr5 > 0 and
     c                             c2scr5 < c2scr4
     c                   exsr      xc2msg2
     c                   goto      c2taga
     c                   endif
      *
      * Oppdatering
      *
     c                   eval      ra33iu_kuid = b1uid
     c     ra33iu_key    chain     ra33iur
      *
     c                   eval      rkksc1 = c2scr1
     c                   eval      rkkgr1 = c2grn1
     c                   eval      rkksc2 = c2scr2
     c                   eval      rkkgr2 = c2grn2
     c                   eval      rkksc3 = c2scr3
     c                   eval      rkkgr3 = c2grn3
     c                   eval      rkksc4 = c2scr4
     c                   eval      rkkgr4 = c2grn4
     c                   eval      rkksc5 = c2scr5
     c                   eval      rkkgr5 = c2grn5
      *
     c                   time                    rkketi
     c                   time                    rkkeda
     c                   eval      rkkeus = l_user
      *
     c                   if        %found
     c                   update    ra33iur
     c                   else
     c                   eval      rkkfir = w_firm
     c                   eval      rkktyp = c2type
     c                   write     ra33iur
     c                   endif
      *
     c                   eval      b_forn = *on
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
     c                   eval      *in31  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av feilmelding
      * Selskapstype er brukt før
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2msg1       begsr
      *
     c                   exfmt     c2msg1
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av feilmelding
      * Feil i score rekkefølge
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2msg2       begsr
      *
     c                   exfmt     c2msg2
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      ra33ir_kuid = b1uid
     c     ra33ir_key    chain     ra33ir
     c                   if        %found
     c                   eval      d1type = rkktyp
     c                   else
     c                   goto      end_d1win
     c                   endif
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
      * Sletter rad
      *
     c                   eval      ra33iu_kuid = b1uid
     c     ra33iu_key    chain     ra33iu
      *
     c                   if        %found
     c                   delete    ra33iu
     c                   endif
      *
     c                   eval      b_forn = *on
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      ra33i1
      *
     c                   if        %eof or
     c                             rkkfir <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1uid  = rkkuid
     c                   eval      b1type = rkktyp
     c                   eval      b1scr1 = rkksc1
     c                   eval      b1grn1 = rkkgr1
     c                   eval      b1scr2 = rkksc2
     c                   eval      b1grn2 = rkkgr2
     c                   eval      b1scr3 = rkksc3
     c                   eval      b1grn3 = rkkgr3
     c                   eval      b1scr4 = rkksc4
     c                   eval      b1grn4 = rkkgr4
     c                   eval      b1scr5 = rkksc5
     c                   eval      b1grn5 = rkkgr5
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     ra33i1_key    setgt     ra33i1
     c                   readp     ra33i1
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     ra33i1
      *
     c                   if        %eof or
     c                             rkkfir <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   enddo
      *
     c                   if        %eof
     c     ra33i1_key    setll     ra33i1
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på leverandør
      *
     c     ra33i1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra33i1_type
      *
      * Key for les på valutapost
      *
     c     ra33ir_key    klist
     c                   kfld                    ra33ir_kuid
      *
      * Key for oppdatering av valutapost
      *
     c     ra33iu_key    klist
     c                   kfld                    ra33iu_kuid
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      ra33i1_type = '      '
     c     ra33i1_key    setll     ra33i1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
