     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : VV771R
      * Beskrivelse . . : Vare, oppdaterer varetype etter telleresultat
      *
      *                   Behandler lager- og skaffevarer i vareregisteret.
      *                   Varer som ligger i oppgitt batch settes til lager-
      *                   vare dersom de har antall <> 0.
      *                   De andre til skaffevare.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       151000 HKO  Nytt program
      *       110101 HKO  Gjør oppslag på LTEL istedenfor LBDT !
      *       130102 HKO  Kaller program for oppretting/sletting i lager
      *                   Lagt inn parameterbilde for kall av funksjonen
      * 800   090223 ask  Programmet er helt omskrevet og endringer er ikke merket
8.01  * 800   280223 ask  Fjernet test på lagerenhet på lager register
8.02  * 800   040124 ask  VVARLR ble åpnet for update og skapte interne låsinger i programmet
8.03  * 800   260325 ask  Lagt inn oppdatering av lager på samme måte som i vedlikehold av lager
      *                   NXS-20613
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvlagl2    if   e           k disk    rename(vlagpfr:vlagl2r)
8.03 fvlaglu    uf   e           k disk    rename(vlagpfr:vlaglur)
8.02 fvvarlr    if   e           k disk    rename(vvarpfr:vvarlrr)
     fvvarlu    uf   e           k disk    rename(vvarpfr:vvarlur)
     flwexpf    uf a e           k disk    rename(lexcpfr:lwexpfr)
8.03  *
8.03  * Definering av Local data area
8.03  *
8.03 d                uds
8.03 d l_user                911    920
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vvfirm)
     d p_batc          s             10
     d p_numm          s              6  0
     d p_lage          s              2  0
     d p_oppd          s              1
     d p_antl          s              2  0
     d p_list          s              1
8.03  *
8.03  * Datastruktur for lageroppdatering
8.03  *
8.03 d                 ds
6.nu d hlrec                        128
8.03 d  hlvare                       15    overlay(hlrec)
8.03 d  hllage                        2  0 overlay(hlrec:16)
8.03 d  hldato                         d   overlay(hlrec:18) datfmt(*iso)
8.03 d  hltime                         t   overlay(hlrec:28) timfmt(*iso)
8.03 d  hlotyp                        2    overlay(hlrec:36)
8.03 d  hlltyp                        2    overlay(hlrec:38)
8.03 d  hlanta                       11  3 overlay(hlrec:40)
8.03 d  hlkopr                        9  2 overlay(hlrec:51)
8.03 d  hlrefr                       20    overlay(hlrec:60)
8.03 d  hlsyst                        1    overlay(hlrec:80)
8.03 d  hlaltk                        2    overlay(hlrec:81)
8.03 d  hlrest                        1    overlay(hlrec:83)
8.03 d  hltype                        1    overlay(hlrec:84)
8.03 d  hlenhe                        3    overlay(hlrec:85)
8.03 d  hlinlr                        1    overlay(hlrec:88)
8.03 d  hllety                        1    overlay(hlrec:89)
8.03 d  hlonum                        8  0 overlay(hlrec:90)
8.03 d  hlolin                        4  0 overlay(hlrec:98)
      *
      * Definering av variable i nøkler
      *
     d vlagl2_lage     s                   like(vllage)
8.03 d vlaglu_lage     s                   like(vllage)
8.03 d vlaglu_vare     s                   like(vlvare)
     d vvarlr_vare     s                   like(vvvare)
     d vvarlu_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vvfirm)
     d w_vtyp_gml      s                   like(vvvtyp)
     d w_anta          s              4  0
8.03 d w_stat          s              1
    *
     c/Exec SQL
     c+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR,
     c+             commit=*none
     c/End-Exec
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      vlagl2_lage = p_lage
     c     vlagl2_key    setll     vlagl2r
      *
     c     vlagl2_key    reade     vlagl2r
     c                   dow       not %eof
      *
8.01 c*                  if        vlenhl = ' '
8.01 c*                  goto      les_neste
8.01 c*                  endif
      *
     c                   eval      w_vtyp_gml = vlvtyp
      *
      * Sjekk lager på varen
      *
     c                   if        vlbehl <> 0 or
     c                             vloord <> 0 or
     c                             vlbbes <> 0
     c                   goto      les_neste
     c                   endif
      *
      * Hent vareinfo
      *
     c                   eval      vvarlr_vare = vlvare
     c     vvarlr_key    chain     vvarlr
     c                   if        not %found(vvarlr)
     c                   goto      les_neste
     c                   endif
      *
      * Sjekker om vare finnes i tellebunt på denne batchen
      *
         vlvtyp = 'S';
         vvvtyp = 'S';
         w_anta = 0;

         exec sql
         select count(*)
            into :w_anta
          from  lbdtpf
            where lvfirm = :w_firm and
                  lvbatc = :p_batc and
                  lvlage = :p_lage and
                  lvvare = :vlvare;
         if w_anta > 0;
            vlvtyp = 'L';
            vvvtyp = 'L';
         endif;

      *
      *
      * Hvis oppdatering - p_oppd = '1'
      *
     c                   if        p_oppd = '1'
8.03 c                   exsr      oppd_lager
     c
      *
      * Oppdaterer vareregister også om bare et lager
      *
     c                   if        p_antl = 1
     c                   eval      vvarlu_vare = vlvare
     c     vvarlu_key    chain     vvarlu
     c                   if        %found(vvarlu)
     c                   update    vvarlur
     c                   endif
     c                   endif
     c                   endif
      *
     c                   exsr      skriv_liste
      *
     c     les_neste     tag
      *
     c     vlagl2_key    reade     vlagl2r
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
8.03  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.03  * Subrutine for å hente å oppdatere lager
8.03  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.03  *
8.03 c     oppd_lager    begsr
8.03  *
8.03  * må også sette alle hittil i år tall til 0, ellers vil
8.03  * i mange tilfeller betraktes som aktiv og vareregister
8.03  *
8.03 c                   eval      vlaglu_vare = vlvare
8.03 c                   eval      vlaglu_lage = p_lage
8.03 c     vlaglu_key    chain     vlaglur
8.03 c                   if        %found(vlaglu)
8.03 c                   eval      vlbbes = 0
8.03 c                   eval      vloord = 0
8.03 c                   eval      vlpakk = 0
8.03 c                   eval      vlinng = 0
8.03 c                   eval      vlutta = 0
8.03 c                   eval      vltell = 0
8.03 c                   eval      vlsalg = 0
8.03 c                   time                    vledat
8.03 c                   time                    vletim
8.03 c                   eval      vleusr = l_user
8.03 c                   update    vlaglur
8.03 c                   endif
8.03  *
8.03 c                   move      vlvare        hlvare
8.03 c                   move      p_lage        hllage
8.03 c                   move      w_vtyp_gml    hlenhe
8.03 c                   time                    hldato
8.03 c                   time                    hltime
8.03 c                   eval      hlotyp  = *blank
8.03 c                   eval      hlltyp  = *blank
8.03  *
8.03 c                   eval      hlanta = 0
8.03 c                   eval      hlkopr = 0
8.03  *
8.03 c                   eval      hlrefr = 'Vtyp endret L--> S '
8.03 c                   move      'L'           hlsyst
8.03 c                   eval      hlaltk = 'J '
8.03 c                   eval      hlrest = *blank
8.03 c                   eval      hlonum = 0
8.03 c                   eval      hlolin = 0
8.03  *
8.03  *
8.03 c                   eval      w_stat = *blank
8.03  *
8.03 c                   call      'VL001R'
8.03 c                   parm                    w_stat
8.03 c                   parm                    hlrec
8.03  *
8.03 c     end_oppd      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_liste   begsr
      *
     c                   if        vlvtyp <> w_vtyp_gml
     c                   if        vlvtyp = 'L'
     c                   eval      exclin = *blank
     c                   eval      EXCLIN =  %trim(vvvare)   + X'05' +
     c                                     %trim(vvtek1)     + X'05' +
     c                                     'Settes til L'    + X'05'
     c                   write     lwexpfr
     c                   eval      p_list = 'L'
     c                   else
     c                   eval      exclin = *blank
     c                   eval      EXCLIN =  %trim(vvvare)   + X'05' +
     c                                     %trim(vvtek1)     + X'05' +
     c                                     'Settes til S'    + X'05'
     c                   write     lwexpfr
     c                   eval      p_list = 'L'
     c                   endif
      *
     c                   endif
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_batc
     c                   parm                    p_numm
     c                   parm                    p_lage
     c                   parm                    p_oppd
     c                   parm                    p_antl
     c                   parm                    p_list
      *
8.03  * Key for les av lager
      *
     c     vlagl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl2_lage
8.03  *
8.03  * Key for oppdatering av lager
8.03  *
8.03 c     vlaglu_key    klist
8.03 c                   kfld                    w_firm
8.03 c                   kfld                    vlaglu_vare
8.03 c                   kfld                    vlaglu_lage
      *
      * Key for oppslag mot vare
      *
     c     vvarlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlr_vare
      *
      * Key for oppdatering av vare
      *
     c     vvarlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlu_vare
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr

