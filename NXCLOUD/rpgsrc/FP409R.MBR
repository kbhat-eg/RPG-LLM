     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FP409R
      * Beskrivelse . . : Spesialpris, sletter spesialpriser for
      *                   rabattkategori/kunde
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       031202 HKO  Nytt program
      *       020703 HKO  Lagt inn rabattkategori
      *       180904 HKO  Sletter også spesialpris notat
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffprilu    uf   e           k disk    rename(fpripfr:fprilur)
     ffpril3    uf   e           k disk    rename(fpripfr:fpril3r)
     ffprnlu    uf   e           k disk    rename(fprnpfr:fprnlur)
5.70 fra30l1    if   e           k disk    rename(ra30pfr:ra30l1r)
      *
      * Definering parametre
      *
     d p_firm          s                   like(fpfirm)
     d p_rkat          s                   like(fprkat)
     d p_kund          s                   like(fpkund)
      *
      * Definering av variabler i nøkler
      *
     d fprilu_prgr     s                   like(fpprgr)
     d fprilu_rkat     s                   like(fprkat)
     d fpril3_kund     s                   like(fpkund)
     d fprnlu_nkun     s                   like(fpnkun)
     d fprnlu_nkpr     s                   like(fpnkpr)
     d fprnlu_nvar     s                   like(fpnvar)
      *
      * Definering av variable
      *
     d w_firm          s                   like(fpfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Sletter spesialpriser på rabattkategori
      *
     c                   if        p_rkat <> 0
      * alle prisgrupper
     c     ra30l1_key    setll     ra30l1
     c     ra30l1_key    reade     ra30l1
     c                   dow       not %eof(ra30l1)
      *
     c                   eval      fprilu_prgr = redkod
     c                   eval      fprilu_rkat = p_rkat
     c     fprilu_key    setll     fprilu
     c     fprilu_key    reade     fprilur
     c                   dow       not %eof(fprilu)
      *
     c                   delete    fprilur
      *
     c     fprilu_key    reade     fprilur
     c                   enddo
      *
     c     ra30l1_key    reade     ra30l1
     c                   enddo
     c                   endif
      *
      * Sletter spesialpriser på kunde
      *
     c                   if        p_kund <> 0
     c                   eval      fpril3_kund = p_kund
     c     fpril3_key    setll     fpril3
     c     fpril3_key    reade     fpril3r
     c                   dow       not %eof
      *
     c                   if        fpkpro = 0
      *
     c                   delete    fpril3r
      *
     c                   eval      fprnlu_nkun = fpkund
     c                   eval      fprnlu_nkpr = fpkpro
     c                   eval      fprnlu_nvar = fpvare
     c     fprnlu_key    chain     fprnlur
     c                   if        %found
     c                   delete    fprnlur
     c                   endif
      *
     c                   endif
      *
     c     fpril3_key    reade     fpril3r
     c                   enddo
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametere
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_rkat
     c                   parm                    p_kund
      *
      * Key for oppdatering av spesialpris
      *
     c     fprilu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fprilu_prgr
     c                   kfld                    fprilu_rkat
      *
     c     fpril3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fpril3_kund
      *
      * Key for oppdatering av spesialpris notat
      *
     c     fprnlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fprnlu_nkun
     c                   kfld                    fprnlu_nkpr
     c                   kfld                    fprnlu_nvar
      *
      * Key for oppslag på prisgruppe
      *
     c     ra30l1_key    klist
     c                   kfld                    w_firm
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
