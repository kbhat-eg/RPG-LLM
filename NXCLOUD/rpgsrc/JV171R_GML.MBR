     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JV171R
      * Beskrivelse . . : Vare, spørring/kalkulasjon vare/pris - varelinje
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       050203 HKO  Nytt program
      *       280905 HKO  Leter først etter betingelse for skaffevare
      *                   Endret parameterliste ved kall av JV751R og
      *                   JP505R
      *       191007 bof  Bruker prisgivende lev. i hent av betingelse
5.61  * R5.61 180608 AMD  Lagt inn manglende dato i parameterliste
5.62  * R5.62 120109 bof  Initert ny dato med dags dato
5.69  * R5.69 080509 BSA  Salgspris blir ikke rekalkulert ved gitte
5.69  *                   rabatter. Når vi nullstiller salgspris
5.69  *                   initieres varen på nytt.
5.70  * prgr  170609 bof  Utvidet med prisgruppe
6.21  *  6.20 011014 bof  Legger ut dagens dato i fra-dato pga. det er prisen
6.30  * 6.30  221014 Bsa  Innfører F tast for visning av NOBB info. Erstatter
6.30  *                   koding i jacada.
6.31  * 6.30  010715 bof  Utvidet lev.varenr til 20
6.3x  *       170615 bof  Utvidet bet. med prisgr.
6.32  *       251115 nho  3 parametere manglet inn i JV751R
6.33  *       080316 bof  Justert 3 parameter, vises i bildet
6.34  *       140416 bsa  Justert videre med parametre. Disse passer ikke
6.34  *                   med JV751R.
6.35  *       180416 bof  Ryddet opp i parameter
6.36  *       180416 bof  Utvidet med frakt, da endres også kalkulasjon.
6.37  *       110418 nho  Endring vedr utregniner                      .
6.38  *       250518 nho  Feil vedr utregning av DG                    .
6.39  *       121118 bof  Endringer i forb. logistikk-vare
7.01  * K000  200921 bof  Nytt parm i JX500R, tar med dato
8.01  *PRG2   150622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffodtpf    if   e           k disk
      *
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fvltpl1    if   e           k disk    rename(vltppfr:vltpl1r)
6.39 fjvprl1    if   e           k disk    rename(jvprpfr:jvprl1r)
      *
     fjv171d    cf   e             workstn
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_kund          s              6  0
     d p_kpro          s              6  0
     d p_lety          s              1  0
     d p_vare          s                   like(jvvare)
6.30 d p_nobb          s              8
6.32 d p_pgbe          s              1
6.32 d p_pgfr          s              1
6.32 d p_oppd          s              1
      *
      * Definering av Local data area
      *
     d                uds
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d jvarl1_vare     s                   like(jvvare)
     d jlevl1_ldor     s                   like(jlldor)
     d vltpl1_lety     s                   like(vylety)
6.39 d jvprl1_vare     s                   like(jxvare)
      *
      * Definering av variable
      *
6.31 d w_lvar          s             20
     d w_gdat          s               d   datfmt(*iso)
     d w_eldo          s              6  0
     d w_ompr          s             12  6
     d w_omp1          s             12  6
     d w_omp2          s             12  6
     d w_omp3          s             12  6
     d w_omv2          s             12  6
     d w_omv3          s             12  6
     d w_fakt          s             12  6
     d w_vekt          s              9  0
     d w_volu          s              8  5
     d w_bvar          s             15
     d w_pvar          s             15
     d w_stat          s              1
     d w_sald          s             11  2
     d w_osts          s              1  0
5.61 d w_odat          s               d   datfmt(*iso)
8.01 d w_prgr          s              2
5.70 d w_prut          s              1
6.34 d w_oppd          s              1
7.01 d w_xdato         s               d   datfmt(*iso)
7.01 d w_dato          s               d   datfmt(*iso)
      *
     d s_dekn          s              5  2
     d s_ifak          s              8  6
     d s_kbel          s              9  2
     d s_kfak          s              8  6
     d s_sfak          s              8  6
6.36 d s_ffak          s              8  6
6.37 d fØrste          s              1  0
     d s_rab1          s              5  2
     d s_rab2          s              5  2
      *
     d w_firm          s                   like(fdfirm)
     d w_vare          s                   like(fdvare)
     d w_sapr          s                   like(fdsapr)
5.69 d w_opri          s                   like(fdsapr)
6.37 d w_opr1          s                   like(fdsapr)
6.37 d w_anta          s                   like(fdanta)
     d w_enh1          s                   like(fdenh1)
     d w_enh2          s                   like(fdenh1)
     d w_enh3          s                   like(fdenh1)
     d w_grpr          s                   like(fdkopr)
     d w_ntpr          s                   like(fdkopr)
     d w_inpr          s                   like(fdkopr)
     d w_kopr          s                   like(fdkopr)
     d w_kupr          s                   like(fdsapr)
     d w_udat          s                   like(fdedat)
6.39 d w_lv_nobb       s                   like(jvnobb)
6.39 d w_lv_lvar       s                   like(jvlvar)
6.39 d w_lv_modn       s                   like(jvmodn)
6.39 d w_logi          s              9  0
      *
     d s_vare          s                   like(jvvare)
     d s_ldor          s                   like(jvldor)
     d s_ogrp          s                   like(jvogrp)
     d s_hgrp          s                   like(jvhgrp)
     d s_ugrp          s                   like(jvugrp)
     d s_anta          s                   like(fdanta)
     d s_ant1          s                   like(fdanta)
     d s_ant2          s                   like(fdanta)
     d s_ant3          s                   like(fdanta)
     d s_enhe          s                   like(fdenh1)
     d s_sapr          s                   like(fdsapr)
6.37 d x1sapr          s             11  2
6.37 d x2sapr          s             11  2
6.37 d x3sapr          s             11  2
6.37 d x4sapr          s             11  2
     d s_kopr          s                   like(fdsapr)
     d s_ntpr          s                   like(fdsapr)
     d s_inpr          s                   like(fdsapr)
6.36 d s_inp2          s                   like(fdsapr)
     d s_lety          s                   like(fdlety)
      *
6.3q C/Exec SQL
6.3q C+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR
6.3q C/End-Exec
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter vareinformasjon
      *
     c                   eval      jvarl1_vare = c1vare
     c     jvarl1_key    chain     jvarl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
     c                   time                    w_udat
      *
6.39  * Setter leverandør lik vareeier
6.39  *
7.01 c                   eval      w_xdato = *loval
6.39 c                   eval      c1ldor = jvldor
6.39 c                   exsr      sjekk_pgiv
6.39 c                   if        w_anta > 1
6.39 c                   call      'JX500R'
6.39 c                   parm                    jvvare
6.39 c                   parm                    c1ldor
6.39 c                   parm                    c1lnav
6.39 c                   parm                    w_grpr
7.01 c                   parm                    w_xdato
6.39 c                   endif
6.39  * hvis logistikk-vare, hent nobbnr og riktig lev.varenr
6.39 c                   if        w_logi <> 0
6.39 c/exec sql
6.39 c+ select jvquno,
6.39 c+        jvllva,
6.39 c+        jvmodn
6.39 c+ into   :w_lv_nobb,
6.39 c+        :w_lv_lvar,
6.39 c+        :w_lv_modn
6.39 c+ from   jvklpf
6.39 c+ inner join jvlepf on
6.39 c+       jvlvnr = jvquno and
6.39 c+       jvlldo = :c1ldor
6.39 c+ inner join jvarpf on
6.39 c+       jvvare = jvquno
6.39 c+ where  jvqvnr = :p_vare and
6.39 c+        jvqldo = :c1ldor
6.39 c/end-exec
6.39 c                   endif
      *
      * Viser registeringbildet
      *
      *
     c                   exsr      xc1bld
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * C1 Behandle bilde for kalkulasjon av antall
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
5.61 c                   time                    w_odat
5.70 c                   eval      w_prut = *blank
      *
     c     c1taga        tag
      *
      * Kaller pris og vareinformasjon, og initierer skjermbildet
      *
7.01 c                   eval      w_dato = w_odat
7.01 c                   if        w_xdato <> *loval
7.01 c                   eval      w_dato = w_xdato
7.01 c                   endif
      *
     c                   call      'JV751R'
     c                   parm                    w_firm
     c                   parm                    jvvare
     c                   parm                    w_prgr
     c                   parm                    c1lety
5.61 c                   parm                    w_dato
      *
     c                   parm                    w_osts
      *
     c                   parm                    c1ldor
     c                   parm                    c1lnav
     c                   parm                    w_lvar
     c                   parm                    w_eldo
     c                   parm                    w_grpr
     c                   parm                    w_gdat
      *
     c                   parm                    w_enh1
     c                   parm                    w_enh2
     c                   parm                    w_enh3
     c                   parm                    w_omp1
     c                   parm                    w_omp2
     c                   parm                    w_omp3
     c                   parm                    w_omv2
     c                   parm                    w_omv3
      *
     c                   parm                    c1kpri
      *
     c                   parm                    c1bldo
     c                   parm                    c1bogr
     c                   parm                    c1bhgr
     c                   parm                    c1bugr
     c                   parm                    c1bmod
     c                   parm                    w_bvar
     c                   parm                    c1bvty
     c                   parm                    c1blet
     c                   parm                    c1bbet
     c                   parm                    w_ntpr
     c                   parm                    c1ifak
      *
     c                   parm                    c1pogr
     c                   parm                    c1phgr
     c                   parm                    c1pugr
     c                   parm                    c1pldo
     c                   parm                    c1pmod
     c                   parm                    w_pvar
     c                   parm                    c1pkpr
     c                   parm                    c1pvty
     c                   parm                    c1plet
     c                   parm                    c1kfak
     c                   parm                    c1kbel
     c                   parm                    c1sfak
      *
     c                   parm                    w_inpr
     c                   parm                    w_kopr
     c                   parm                    w_sapr
     c                   parm                    c1dekn
6.33 c                   parm                    c1prut
6.35 c                   parm                    c1pgbe
6.35 c                   parm                    c1pgfr
6.35 c                   parm                    c1ffak
6.35 c                   parm                    c1inp2
6.35  * parameter er nå i hht. jv751r, og brukes i skjermbildet.
6.35  *
6.34  * C1INP2 er brukt i skjermbildet, men ikke håndtert i JV751R
6.34  * Setter feltet fast til 0, (og må sikkert rettes i JV751R)
6.34  * for ikke få JIT Gui i jacada.
6.35 c*                  eval      c1inp2 = 0
      *
     c                   eval      *in83 = w_enh2 = *blank
     c                   eval      *in84 = w_enh3 = *blank
      *
     c                   eval      c1bvar = w_bvar
     c                   eval      c1pvar = w_pvar
      *
     c                   eval      c1gdat = 0
6.21 c*                  if        w_gdat <> *loval
6.21 c*    *dmy          move      w_gdat        c1gdat
6.21 c*                  endif
6.21 c     *dmy          move      w_odat        c1gdat
      *
     c                   eval      c1grpr = w_grpr
     c                   eval      c1ntpr = w_ntpr
     c                   eval      c1inpr = w_inpr
      *
     c                   if        w_lv_nobb <> 0
     c                   eval      c1nobb = w_lv_nobb
     c                   eval      c1modn = w_lv_modn
     c                   else
     c                   eval      c1nobb = jvnobb
     c                   eval      c1modn = jvmodn
     c                   endif
     c                   eval      c1enhe = w_enh1
     c                   eval      c1sapr = w_sapr
     c                   eval      c1kopr = w_kopr
     c                   eval      c1rab1 = 0
     c                   eval      c1rab2 = 0
     c                   eval      c1lety = p_lety
     c                   eval      c1tek1 = jvtek1
     c                   eval      c1tek2 = jvtek2
     c                   eval      c1enh1 = w_enh1
     c                   eval      c1enh2 = w_enh2
     c                   eval      c1enh3 = w_enh3
     c                   eval      c1ant1 = 0
     c                   eval      c1ant2 = 0
     c                   eval      c1ant3 = 0
5.69 c                   eval      w_opri = w_sapr
      *
      * Kaller subrutine for å vise antall i andre enheter
      *
     c                   exsr      kalkulator
      *
      * Tar vare på gamle verdier
      *
     c                   eval      s_ldor = c1ldor
      *
     c                   eval      s_ntpr = c1ntpr
     c                   eval      s_ifak = c1ifak
     c                   eval      s_kfak = c1kfak
     c                   eval      s_kbel = c1kbel
     c                   eval      s_inpr = c1inpr
6.36 c                   eval      s_inp2 = c1inp2
     c                   eval      s_sfak = c1sfak
6.36 c                   eval      s_ffak = c1ffak
     c                   eval      s_dekn = c1dekn
      *
     c                   eval      s_anta = c1anta
     c                   eval      s_ant1 = c1ant1
     c                   eval      s_ant2 = c1ant2
     c                   eval      s_ant3 = c1ant3
     c                   eval      s_enhe = c1enhe
     c                   eval      s_sapr = c1sapr
     c                   eval      s_kopr = c1kopr
     c                   eval      s_rab1 = c1rab1
     c                   eval      s_rab2 = c1rab2
     c                   eval      s_lety = c1lety
      *
      * Viser bildet
      *
     c     c1tagb        tag
6.37 c                   if        fØrste = 0
6.37 c                   eval      fØrste = 1
6.37 c                   eval      c1anta = 1
6.37 c                   endif
      *
6.37 c***                eval      c1rab1 = 0
6.37 c***                eval      c1rab2 = 0
6.37  *
6.37 c                   eval      w_anta = c1ant1
     c                   exfmt     c1bld
6.37  *
6.37 c                   if        c1rab1 <> 0 and
6.37 c                             c1sapr <> w_opri
6.37 c                   eval      c1sapr = w_opri
6.37 c                   endif
6.37  *
6.37 c                   if        c1ant1 = 0 and
6.37 c                             c1sapr = 0 and
6.37 c                             c1rab1 = 0 and
6.37 c                             c1rab2 = 0
6.37 c                   eval      c1sapr = w_opri
6.37 c                   goto      c1tagb
6.37 c                   endif
6.37  *
      * F3=Gå tilbake
      *
     c                   if        *inkc or *inkl
     c                   goto      end_c1bld
     c                   endif
      *
      * F8=Produktinformasjon
      *
     c                   if        *inkh = *on
     c                   call      'FO704C'
     c                   parm                    w_firm
     c                   parm                    jvvare
     c                   goto      c1tagb
     c                   endif
      *
      * F13=Velg prisgivende leverandør
      *
     c                   if        *inkm = *on
     c                   call      'JX500R'
     c                   parm                    jvvare
     c                   parm                    c1ldor
     c                   parm                    c1lnav
     c                   parm                    w_grpr
7.01 c                   parm                    w_xdato
     c                   if        c1ldor <> s_ldor
     c                   goto      c1taga
     c                   else
     c                   goto      c1tagb
     c                   endif
     c                   endif
      *
      * F14=Velg betingelse
      *
     c                   if        *inkn = *on
     c                   exsr      betingelse
     c                   goto      c1tagb
     c                   endif
      *
      * F15=Vis kunde-/kundeprosjekt-informasjon
      *
     c                   if        *inkp = *on
     c                   call      'FL510R'
     c                   parm                    w_firm
     c                   parm                    p_kund
     c                   parm                    p_kpro
     c                   goto      c1tagb
     c                   endif
      *
      * F16=Rabattmatrise for kunde/kundeprosjekt
      *
     c                   if        *inkq = *on
     c                   call      'FR510R'
     c                   parm                    w_firm
     c                   parm                    p_kund
     c                   parm                    p_kpro
     c                   goto      c1tagb
     c                   endif
      *
      * F17=Spesialpriser for kunde/kundeprosjekt
      *
     c                   if        *inkr = *on
     c                   call      'FP510R'
     c                   parm                    w_firm
     c                   parm                    p_kund
     c                   parm                    p_kpro
     c                   goto      c1tagb
     c                   endif
6.30  *
6.30  * F18=Viser NOBB informasjon
6.30  *
6.30 c                   if        *inks = *on
6.30 c                   exsr      vis_nobb
6.30 c                   goto      c1tagb
6.30 c                   endif
      *
      * Validering
      *
     c                   if        c1kopr <> s_kopr
     c                   if        c1kfak <> s_kfak or
     c                             c1kbel <> s_kbel
     c                   eval      *in31 = *on
     c                   goto      c1tagb
     c                   endif
     c                   endif
      *
     c                   if        c1sfak <> s_sfak and
     c                             c1dekn <> s_dekn
     c                   eval      *in32 = *on
     c                   goto      c1tagb
     c                   endif
      *
     c                   if        c1sapr <> s_sapr
     c                   if        c1sfak <> s_sfak or
     c                             c1dekn <> s_dekn
     c                   eval      *in33 = *on
     c                   goto      c1tagb
     c                   endif
     c                   endif
      *
     c                   if        w_enh1 <> *blank
     c                   if        c1enhe = *blank or
     c                             c1enhe <> w_enh1 and
     c                             c1enhe <> w_enh2 and
     c                             c1enhe <> w_enh3
     c                   eval      *in34 = *on
     c                   goto      c1tagb
     c                   endif
     c                   endif
      *
      * Sjekker og behandler antall
      *
     c                   if        c1ant1 = 0 and
     c                             c1ant2 = 0 and
     c                             c1ant3 = 0 and
     c                             c1anta = 0
     c                   eval      *in35 = *on
     c                   goto      c1tagb
     c                   endif
      *
     c                   select
     c                   when      c1ant1 <> s_ant1 and
     c                             c1ant1 <> 0
     c                   eval      s_ant1 = c1ant1
     c                   eval      c1anta = c1ant1
     c                   eval      c1enhe = c1enh1
     c                   when      c1ant2 <> s_ant2 and
     c                             c1ant2 <> 0
     c                   eval      s_ant2 = c1ant2
     c                   eval      c1anta = c1ant2
     c                   eval      c1enhe = c1enh2
     c                   when      c1ant3 <> s_ant3 and
     c                             c1ant3 <> 0
     c                   eval      s_ant3 = c1ant3
     c                   eval      c1anta = c1ant3
     c                   eval      c1enhe = c1enh3
     c                   endsl
      *
      * Sjekk kostpris
      *
     c                   if        c1kopr = 0 and
     c                             c1inpr = 0
     c                   eval      *in38 = *on
     c                   goto      c1tagb
     c                   endif
5.69  *
5.69  * Hvis salgsprisfeltet nulles ut, initierer vi alt på nytt
5.69  *
5.69 c**                 if        c1sapr = 0 and
5.69 c**                           w_opri <> 0
5.69 c**                 goto      c1taga
5.69 c**                 endif
      *
6.37  *
6.37 c                   eval      x3sapr = c1sapr * c1anta
6.37 c                   eval      x4sapr = x3sapr / w_opri
6.37 c                   eval      x2sapr = w_opri * x4sapr
6.37 c                   if        x3sapr = x2sapr and
6.37 c                             x3sapr < w_opri
6.37 c                   eval      c1sapr = c1sapr * c1anta
6.37 c                   goto      nypris
6.37 c                   endif
6.37  *
6.37 c                   if        x3sapr <> x2sapr
6.37 c                   eval      c1sapr = c1sapr * c1anta
6.37 c                   else
6.37 c                   eval      c1sapr = w_opri * c1anta
6.37 c                   endif
6.37  *
6.37 c     nypris        tag
6.37  *
6.37 c                   if        c1sapr = 0 and
6.37 c                             c1rab1 <> 0
6.37 c                   eval(h)   c1sapr = c1sapr - (c1sapr * c1rab1/100)
6.37 c                   eval(h)   c1sapr = c1sapr - (c1sapr * c1rab2/100)
6.37 c                   endif
      * Kaller subrutine for å vise antall i andre enheter
      *
     c                   if        w_enh2 <> *blank
     c                   exsr      kalkulator
     c                   eval      s_ant1 = c1ant1
     c                   eval      s_ant2 = c1ant2
     c                   eval      s_ant3 = c1ant3
     c                   endif
      *
      * Dersom enhet er endret, og pris ikke er overstyrt, kalkuleres
      * ny pris
      *
     c                   if        c1enhe <> s_enhe
     c                   exsr      enhet
     c                   goto      c1tagb
     c                   endif
      *
      * Kalkulerer nye priser eller faktorer ved endring av priser eller
      * faktorer
      * Bildet vises på nytt
      *
     c                   if        c1ntpr <> s_ntpr or
     c                             c1ifak <> s_ifak or
     c                             c1kfak <> s_kfak or
     c                             c1kbel <> s_kbel or
     c                             c1sfak <> s_sfak or
6.36 c                             c1ffak <> s_ffak or
     c                             c1dekn <> s_dekn or
     c                             c1sapr <> s_sapr or
     c                             c1kopr <> s_kopr or
     c                             c1rab1 <> s_rab1 or
     c                             c1rab2 <> s_rab2
     c                   exsr      kalk_priser
     c                   goto      c1tagb
     c                   endif
      *
      * Viser bildet på nytt dersom varen har flere enheter og antall er
      * endret
      *
     c                   if        w_enh2 <> *blank and
     c                             c1anta <> s_anta
     c                   eval      s_anta = c1anta
     c                   goto      c1tagb
     c                   endif
      *
      * Sjekk salgspris
      *
     c                   if        c1sapr <= 0.01
     c                   eval      *in36 = *on
     c                   goto      c1tagb
     c                   endif
      *
6.37  *
6.37 c                   if        c1sapr <> w_opri
6.37 c                   eval      c1sapr = c1sapr / w_anta
6.37 c                   if        c1sapr = w_opri
6.37 c                   eval      c1sapr = w_opri * c1anta
6.37 c                   else
6.37 c                   eval      c1sapr = c1sapr * c1anta
6.37 c                   endif
6.37 c                   endif
6.37 c*
     c                   eval      w_sapr = c1sapr
     c                   if        c1rab1 > 0
     c                   eval(h)   w_sapr = w_sapr - (w_sapr * c1rab1 / 100)
     c                   endif
     c                   if        c1rab2 > 0
     c                   eval(h)   w_sapr = w_sapr - (w_sapr * c1rab2 / 100)
     c                   endif
      *
6.37 c                   eval(h)   c1sapr = c1sapr - (c1sapr * c1rab1/100)
6.37 c                   eval(h)   c1sapr = c1sapr - (c1sapr * c1rab2 / 100)
6.37  *
     c                   goto      c1tagb
      *
     c     end_c1bld     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Kalkulator for omregning mellom enheter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kalkulator    begsr
      *
     c                   if        c1enhe <> *blank
      *
     c                   eval      c1ant1 = 0
     c                   eval      c1ant2 = 0
     c                   eval      c1ant3 = 0
      *
     c                   select
     c                   when      c1enhe = w_enh1
     c                   eval      c1ant1 = c1anta
     c                   if        w_omv2 <> 0
     c                   eval(h)   c1ant2 = c1anta / w_omv2
     c                   endif
     c                   if        w_omv3 <> 0
     c                   eval(h)   c1ant3 = c1anta / w_omv3
     c                   endif
     c                   when      c1enhe = w_enh2
     c                   eval(h)   c1ant1 = c1anta * w_omv2
     c                   eval      c1ant2 = c1anta
     c                   if        w_omv3 <> 0
     c                   eval(h)   c1ant3 = c1ant1 / w_omv3
     c                   endif
     c                   when      c1enhe = w_enh3
     c                   eval(h)   c1ant1 = c1anta * w_omv3
     c                   if        w_omv2 <> 0
     c                   eval(h)   c1ant2 = c1ant1 / w_omv2
     c                   endif
     c                   eval      c1ant3 = c1anta
     c                   endsl
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling ved endring av enhet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     enhet         begsr
      *
     c                   select
     c                   when      c1enhe = w_enh1
     c                   eval      c1grpr = w_grpr
     c                   eval      c1ntpr = w_ntpr
     c                   when      c1enhe = w_enh2
     c                   eval      c1grpr = w_grpr * w_omv2
     c                   eval      c1ntpr = w_ntpr * w_omv2
     c                   when      c1enhe = w_enh3
     c                   eval      c1grpr = w_grpr * w_omv3
     c                   eval      c1ntpr = w_ntpr * w_omv3
     c                   endsl
      *
     c                   if        c1sapr = 0 or
     c                             c1sapr = s_sapr
     c                   eval      c1rab1 = 0
     c                   eval      c1rab2 = 0
     c                   endif
      *
      * Kalkulerer ny salgspris
      *
     c                   exsr      kalk_priser
      *
     c                   eval      s_anta = c1anta
     c                   eval      s_enhe = c1enhe
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter ny betingelse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     betingelse    begsr
      *
     c                   call      'JP505R'
     c                   parm                    w_firm
     c                   parm                    c1ldor
6.3x c                   parm                    w_prgr
     c                   parm                    jvvare
     c                   parm                    c1bldo
     c                   parm                    c1bogr
     c                   parm                    c1bhgr
     c                   parm                    c1bugr
     c                   parm                    c1bmod
     c                   parm                    w_bvar
     c                   parm                    c1bvty
     c                   parm                    c1blet
     c                   parm                    c1bbet
     c                   parm                    w_ntpr
     c                   parm                    c1ifak
      *
     c                   eval      c1bvar = w_bvar
      *
     c                   select
     c                   when      c1enhe = w_enh1
     c                   eval      c1ntpr = w_ntpr
     c                   when      c1enhe = w_enh2
     c                   eval      c1ntpr = w_ntpr * w_omv2
     c                   when      c1enhe = w_enh3
     c                   eval      c1ntpr = w_ntpr * w_omv3
     c                   endsl
      *
      * Kalkulerer ny salgspris
      *
     c                   exsr      kalk_priser
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kalkulasjon salgspris og faktorer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kalk_priser   begsr
      *
      * Finner omregningsfaktor pris for enhet
      *
     c                   select
     c                   when      c1enhe = w_enh1
     c                   eval      w_ompr = w_omp1
     c                   when      c1enhe = w_enh2
     c                   eval(h)   w_ompr = w_omp2 / w_omv2
     c                   when      c1enhe = w_enh3
     c                   eval(h)   w_ompr = w_omp3 / w_omv3
     c                   endsl
      *
      * Nettopris/innkjøpspris-faktor
      *
     c                   if        c1ntpr <> 0
     c                   eval      c1inpr = c1ntpr
     c                   else
     c                   eval(h)   c1inpr = c1grpr * c1ifak
     c                   endif
      *
     c                   if        c1inpr = 0
     c                   eval      c1inpr = c1kopr
     c                   endif
6.36  *
6.36  * Endret fraktpåslag
6.36 c                   eval      c1inp2 = c1inpr
6.36 c                   if        c1inpr <> s_inpr or
6.36 c                             c1ifak <> s_ifak or
6.36 c                             c1ffak <> s_ffak
6.36 c                   eval(h)   c1inp2 = (c1inpr * c1ffak)
6.36 c                   endif
      *
      * Endret kostpris
      *
     c                   if        c1kopr <> 0 and
     c                             c1kopr <> s_kopr
6.36 c                   eval(h)   c1kfak = (c1kopr - c1kbel) / c1inp2
     c                   if        c1sfak = s_sfak and
     c                             c1dekn = s_dekn
     c                   eval(h)   c1sfak = c1sapr / (c1kopr * w_ompr)
     c                   endif
     c                   else
     c                   if        c1inpr <> s_inpr or
6.36 c                             c1inp2 <> s_inp2 or
     c                             c1kfak <> s_kfak or
     c                             c1kbel <> s_kbel
6.36 c                   eval(h)   c1kopr = (c1inp2 * c1kfak) + c1kbel
     c                   endif
     c                   endif
      *
      * Kalkulerer ny salgspris dersom grunnlaget for denne er endret
      *
     c                   if        c1ntpr <> s_ntpr or
     c                             c1ifak <> s_ifak or
     c                             c1kfak <> s_kfak or
     c                             c1kbel <> s_kbel or
     c                             c1kopr <> s_kopr
     c                   eval(h)   c1sapr = c1kopr * c1sfak * w_ompr
     c                   endif
      *
      * Kalkulerer faktor for fratrekk av rabatter
      *
     c                   eval      w_fakt = 1
      *
     c                   if        c1rab1 <> 0
     c                   eval      w_fakt = w_fakt - (c1rab1 / 100 * w_fakt)
     c                   endif
     c                   if        c1rab2 <> 0
     c                   eval      w_fakt = w_fakt - (c1rab2 / 100 * w_fakt)
     c                   endif
      *
      * Endring av salgsprisfaktor, dekningsgrad eller salgspris
      *
     c                   select
     c                   when      c1sfak <> s_sfak
     c                   eval(h)   c1sapr = c1kopr * c1sfak * w_ompr
     c                   when      c1dekn <> s_dekn
     c                   eval(h)   c1sapr = (100 * c1kopr) / (100 - c1dekn)
     c                   eval(h)   c1sapr = c1sapr / w_fakt
     c                   eval(h)   c1sfak = c1sapr / (c1kopr * w_ompr)
     c                   when      c1sapr = 0
     c                   eval(h)   c1sapr = c1kopr * c1sfak * w_ompr
     c                   when      c1sapr <> s_sapr
     c                   eval(h)   c1sfak = c1sapr / (c1kopr * w_ompr)
     c                   endsl
5.69  *
5.69  * Vi kalkulerer salgspris uansett. Hvis rabatt er gitt og så null
5.69  * må vi også rekalkulere.
6.37 c                   if        c1anta = 1 and
6.37 c                             c1sapr = 0 and
6.37 c                             c1rab1 = 0 and
6.37 c                             c1rab2 = 0
6.37 c                   eval      c1sapr = w_opri
6.37 c                   endif
6.37  *
5.69 c                   eval(h)   c1sapr = c1sapr * w_fakt
      *
      * Kalkulerer ny dekningsgrad. DG presanteres som virkelig DG etter at
      * rabatter er trekt fra salgsprisen
      *
     c                   eval      c1dekn = 0
      *
6.38 c**                 eval(h)   w_kupr = c1sapr * w_fakt
6.38 c                   eval(h)   w_kupr = c1sapr * 1
      *
     c                   if        w_kupr <> 0
     c                   select
     c                   when      100 - ((c1kopr * 100) / w_kupr) < -999,99
     c                   eval      c1dekn = -999,99
     c                   when      100 - ((c1kopr * 100) / w_kupr) > 999,99
     c                   eval      c1dekn = 999,99
     c                   other
     c                   eval(h)   c1dekn = 100 - ((c1kopr * 100) / w_kupr)
     c                   endsl
     c                   endif
      *
6.37  * finne rabatt
6.37 c                   if        c1sapr <> w_opri
6.37 c                   eval      c1sapr = c1sapr / c1anta
6.37 c                   eval      x1sapr = c1sapr
6.37 c                   if        c1sapr = w_opri
6.37 c                   eval      c1sapr = w_opri * c1anta
6.37 c                   else
6.37 c                   eval      c1sapr = c1sapr * c1anta
6.37 c                   endif
6.37 c                   endif
6.37 c*
6.37 c** ??              eval      w_opr1 = w_opri * c1anta
6.37 c                   if        x1sapr <> w_opri
6.37 c                   if        c1rab1 = 0 and
6.37 c                             c1rab2 = 0 and
6.37 c                             c1sapr <> w_opr1
6.37 c                   eval(h)   c1rab1 = (w_opri - c1sapr) * 100 / w_opri
6.37 c                   endif
6.37 c                   endif
      * Tar vare på verdier i endrede felter
      *
     c                   eval      s_ntpr = c1ntpr
     c                   eval      s_ifak = c1ifak
     c                   eval      s_kfak = c1kfak
     c                   eval      s_kbel = c1kbel
     c                   eval      s_sfak = c1sfak
     c                   eval      s_inpr = c1inpr
6.36 c                   eval      s_inp2 = c1inp2
     c                   eval      s_dekn = c1dekn
     c                   eval      s_sapr = c1sapr
     c                   eval      s_kopr = c1kopr
     c                   eval      s_rab1 = c1rab1
     c                   eval      s_rab2 = c1rab2
      *
     c     end_sapr      endsr
      *
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30  * Vis NOBB informasjon for varen. Samme prinsipp som preview av utskrifter
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30  *
6.30 c     vis_nobb      begsr
6.30 c
6.30 c                   eval      p_nobb = %char(c1nobb)
6.30 c                   call      'AP633R'
6.30 c                   parm                    p_nobb
6.30  *
6.30 c                   endsr
      * Subrutine for å sjekke om varen har flere prisgivere
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.39 c     sjekk_pgiv    begsr
6.39  *
6.39 c                   eval      w_anta = 0
6.39  *
6.39 c                   eval      jvprl1_vare = c1vare
6.39 c     jvprl1_key    setll     jvprl1
6.39 c     jvprl1_key    reade     jvprl1
6.39 c                   dow       not %eof
6.39 c                   if        (jxtdat =  *loval) or
6.39 c                             (jxtdat <> *loval and
6.39 c                             jxtdat > w_udat)
6.39 c                   eval      w_anta = w_anta + 1
6.39 c                   endif
6.39 c     jvprl1_key    reade     jvprl1
6.39 c                   enddo
6.39  *
6.39 c                   endsr
6.39  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_kund
     c                   parm                    p_kpro
     c                   parm                    p_lety
     c                   parm                    p_vare
      *
      * Key for oppslag på vare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for oppslag på leveringstype
      *
     c     vltpl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltpl1_lety
6.39  *
6.39  * Key for les på vare-pris
6.39  *
6.39 c     jvprl1_key    klist
6.39 c                   kfld                    jvprl1_vare
6.39  *
      *
      *
      * Henter parametre
      *
     c                   eval      w_firm = p_firm
      *
6.39  * finner evt. logistikk-vare
6.39  *
6.39 c                   eval      w_logi = 0
6.39 c/exec sql
6.39 c+ select count(*)
6.39 i+ into   :w_logi
6.39 c+ from   jvkhpf
6.39 c+ where  jvkvnr = :p_vare and
6.39 c+        jvkfir = :w_firm
6.39 c/end-exec
5.70  *
5.70  * henter prisgruppe
5.70  *
5.70 c                   call      'VL711R   '
5.70 c                   parm                    w_prgr
      *
     c                   eval      c1vare = p_vare
      *
     c                   endsr
