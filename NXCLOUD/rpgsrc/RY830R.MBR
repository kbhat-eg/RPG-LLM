     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : RY830R
      * Beskrivelse . . : Sletter varer som er slettet i LVR
      *
      *                   NB! Betingelses-registeret blir ikke rørt
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       020398 HKO  Nytt program
      *       280605 HKO  Tilpasset nytt recordformat i påslagstabellen
      *       140905 HKO  Lagt inn sletting av vare-søktekst
      *       290208 bof  Utvidet forpakning med modulnr.
5.70  * PRGR  211108 bof  Utvidet med prisgruppe
      *
8.01  *PRG2   160622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
      *
     fjvdtl1    if   e           k disk    rename(jvdtpfr:jvdtl1r)
     fjvdtlu    uf   e           k disk    rename(jvdtpfr:jvdtlur)
     fjpfal6    if   e           k disk    rename(jpfapfr:jpfal6r)
     fjpfalu    uf   e           k disk    rename(jpfapfr:jpfalur)
     fjforl5    if   e           k disk    rename(jforpfr:jforl5r)
     fjforlu    uf   e           k disk    rename(jforpfr:jforlur)
     fjwprl1    if   e           k disk    rename(jwprpfr:jwprl1r)
     fjwprlu    uf   e           k disk    rename(jwprpfr:jwprlur)
     fjvpkl1    if   e           k disk    rename(jvpkpfr:jvpkl1r)
     fjvpklu    uf   e           k disk    rename(jvpkpfr:jvpklur)
     fjvprl1    if   e           k disk    rename(jvprpfr:jvprl1r)
     fjvprlu    uf   e           k disk    rename(jvprpfr:jvprlur)
     fjsokl1    if   e           k disk    rename(jsokpfr:jsokl1r)
     fjsoklu    uf   e           k disk    rename(jsokpfr:jsoklur)
     fjmatl1    if   e           k disk    rename(jmatpfr:jmatl1r)
     fjmatlu    uf   e           k disk    rename(jmatpfr:jmatlur)
      *
     fjvsoi1    if   e           k disk    rename(jvsost:jvsoi1r)
     fjvsoiu    uf   e           k disk    rename(jvsost:jvsoiur)
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variabler i nøkler
      *
     d jvarl1_vare     s                   like(jvvare)
      *
     d jvdtlu_dvar     s                   like(jvdvar)
5.70 d jpfalu_prgr     s                   like(jcprgr)
     d jpfalu_ogrp     s                   like(jcogrp)
     d jpfalu_hgrp     s                   like(jchgrp)
     d jpfalu_ugrp     s                   like(jcugrp)
     d jpfalu_ldor     s                   like(jcldor)
     d jpfalu_modn     s                   like(jcmodn)
     d jpfalu_vare     s                   like(jcvare)
     d jpfalu_kpri     s                   like(jckpri)
     d jpfalu_vtyp     s                   like(jcvtyp)
     d jpfalu_lety     s                   like(jclety)
     d jforlu_ogrp     s                   like(jzogrp)
     d jforlu_hgrp     s                   like(jzhgrp)
     d jforlu_ugrp     s                   like(jzugrp)
     d jforlu_ldor     s                   like(jzldor)
     d jforlu_modn     s                   like(jzmodn)
     d jforlu_vare     s                   like(jzvare)
     d jwprlu_vare     s                   like(jbvare)
     d jvpklu_vare     s                   like(jwvare)
     d jvprlu_vare     s                   like(jxvare)
     d jsoklu_vare     s                   like(jsvare)
     d jmatlu_var1     s                   like(jovar1)
     d jvsoiu_svar     s                   like(jvsvar)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Fjerner detaljer for slettet vare
      *
     c     jvdtl1_key    setll     jvdtl1
     c     jvdtl1_key    reade     jvdtl1
     c                   dow       not %eof
     c                   eval      jvarl1_vare = jvdvar
     c     jvarl1_key    chain     jvarl1
     c                   if        not %found
     c                   eval      jvdtlu_dvar = jvdvar
     c     jvdtlu_key    chain     jvdtlur
     c                   delete    jvdtlur
     c                   endif
     c     jvdtl1_key    reade     jvdtl1
     c                   enddo
      *
      * Fjerner påslag for slettet vare
      *
     c     jpfal6_key    setll     jpfal6
     c     jpfal6_key    reade     jpfal6
     c                   dow       not %eof
     c                   if        jcvare <> *blank
     c                   eval      jvarl1_vare = jcvare
     c     jvarl1_key    chain     jvarl1
     c                   if        not %found
5.70 c                   eval      jpfalu_prgr = jcprgr
     c                   eval      jpfalu_ogrp = jcogrp
     c                   eval      jpfalu_hgrp = jchgrp
     c                   eval      jpfalu_ugrp = jcugrp
     c                   eval      jpfalu_ldor = jcldor
     c                   eval      jpfalu_modn = jcmodn
     c                   eval      jpfalu_vare = jcvare
     c                   eval      jpfalu_kpri = jckpri
     c                   eval      jpfalu_vtyp = jcvtyp
     c                   eval      jpfalu_lety = jclety
     c     jpfalu_key    chain     jpfalur
     c                   delete    jpfalur
     c                   endif
     c                   endif
     c     jpfal6_key    reade     jpfal6
     c                   enddo
      *
      * Fjerner forpakning for slettet vare
      *
     c     jforl5_key    setll     jforl5
     c     jforl5_key    reade     jforl5
     c                   dow       not %eof
     c                   if        jzvare <> *blank
     c                   eval      jvarl1_vare = jzvare
     c     jvarl1_key    chain     jvarl1
     c                   if        not %found
     c                   eval      jforlu_ogrp = jzogrp
     c                   eval      jforlu_hgrp = jzhgrp
     c                   eval      jforlu_ugrp = jzugrp
     c                   eval      jforlu_ldor = jzldor
     c                   eval      jforlu_modn = jzmodn
     c                   eval      jforlu_vare = jzvare
     c     jforlu_key    chain     jforlur
     c                   delete    jforlur
     c                   endif
     c                   endif
     c     jforl5_key    reade     jforl5
     c                   enddo
      *
      * Fjerner pris-arbeidsregister for slettet vare
      *
     c     jwprl1_key    setll     jwprl1
     c     jwprl1_key    reade     jwprl1
     c                   dow       not %eof
     c                   eval      jvarl1_vare = jbvare
     c     jvarl1_key    chain     jvarl1
     c                   if        not %found
     c                   eval      jwprlu_vare = jbvare
     c     jwprlu_key    chain     jwprlur
     c                   delete    jwprlur
     c                   endif
     c     jwprl1_key    reade     jwprl1
     c                   enddo
      *
      * Fjerner lokalt registrert pakninger for slettet vare
      *
     c     *loval        setll     jvpkl1
     c                   read      jvpkl1
     c                   dow       not %eof
     c                   if        jwrsts = 1
     c                   eval      jvarl1_vare = jwvare
     c     jvarl1_key    chain     jvarl1
     c                   if        not %found
     c                   eval      jvpklu_vare = jwvare
     c     jvpklu_key    chain     jvpklur
     c                   delete    jvpklur
     c                   endif
     c                   endif
     c                   read      jvpkl1
     c                   enddo
      *
      * Fjerner lokalt registrerte priser for slettet vare
      *
     c     *loval        setll     jvprl1
     c                   read      jvprl1
     c                   dow       not %eof
     c                   if        jxrsts = 1
     c                   eval      jvarl1_vare = jxvare
     c     jvarl1_key    chain     jvarl1
     c                   if        not %found
     c                   eval      jvprlu_vare = jxvare
     c     jvprlu_key    chain     jvprlur
     c                   delete    jvprlur
     c                   endif
     c                   endif
     c                   read      jvprl1
     c                   enddo
      *
      * Fjerner lokalt registrerte søketekster for slettet vare
      *
     c                   read      jsokl1
     c                   dow       not %eof
     c                   if        jsokod = 'L'
     c                   eval      jvarl1_vare = jsvare
     c     jvarl1_key    chain     jvarl1
     c                   if        not %found
     c                   eval      jsoklu_vare = jsvare
     c     jsoklu_key    chain     jsoklur
     c                   delete    jsoklur
     c                   endif
     c                   endif
     c                   read      jsokl1
     c                   enddo
      *
      * Fjerner vare-søketekst for slettet vare
      *
     c     *loval        setll     jvsoi1
     c                   read      jvsoi1
     c                   dow       not %eof
     c                   eval      jvarl1_vare = jvsvar
     c     jvarl1_key    chain     jvarl1
     c                   if        not %found
     c                   eval      jvsoiu_svar = jvsvar
     c     jvsoiu_key    chain     jvsoiur
     c                   delete    jvsoiur
     c                   endif
     c                   read      jvsoi1
     c                   enddo
      *
      * Blanker slettede varer som matchet vare i matche-registeret
      *
     c     jmatl1_key    setll     jmatl1
     c     jmatl1_key    reade     jmatl1
     c                   dow       not %eof
      *
     c                   if        jovar2 <> *blank and
     c                             jovkod = 0
     c                   eval      jvarl1_vare = jovar2
     c     jvarl1_key    chain     jvarl1
     c                   if        not %found
     c                   eval      jmatlu_var1 = jovar1
     c     jmatlu_key    chain     jmatlur
     c                   eval      jovar2 = *blank
     c                   eval      joldo2 = 0
     c                   eval      jokode = 0
     c                   update    jmatlur
     c                   endif
     c                   endif
      *
     c     jmatl1_key    reade     jmatl1
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for les på vare-detalj
      *
     c     jvdtl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppdatering av vare-detalj
      *
     c     jvdtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jvdtlu_dvar
      *
      * Key for oppslag på vare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for les på påslagsfaktor
      *
     c     jpfal6_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppdatering av påslagsfaktor
      *
     c     jpfalu_key    klist
     c                   kfld                    w_firm
5.70 c                   kfld                    jpfalu_prgr
     c                   kfld                    jpfalu_ogrp
     c                   kfld                    jpfalu_hgrp
     c                   kfld                    jpfalu_ugrp
     c                   kfld                    jpfalu_ldor
     c                   kfld                    jpfalu_modn
     c                   kfld                    jpfalu_vare
     c                   kfld                    jpfalu_kpri
     c                   kfld                    jpfalu_vtyp
     c                   kfld                    jpfalu_lety
      *
      * Key for les på forpakning
      *
     c     jforl5_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppdatering av forpakning
      *
     c     jforlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jforlu_ogrp
     c                   kfld                    jforlu_hgrp
     c                   kfld                    jforlu_ugrp
     c                   kfld                    jforlu_ldor
     c                   kfld                    jforlu_modn
     c                   kfld                    jforlu_vare
      *
      * Key for les på midlertidig pris
      *
     c     jwprl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppdatering av midlertidig pris
      *
     c     jwprlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jwprlu_vare
      *
      * Key for oppdatering av vare-pakning
      *
     c     jvpklu_key    klist
     c                   kfld                    jvpklu_vare
      *
      * Key for oppdatering av vare-pris
      *
     c     jvprlu_key    klist
     c                   kfld                    jvprlu_vare
      *
      * Key for oppdatering av søketekst
      *
     c     jsoklu_key    klist
     c                   kfld                    jsoklu_vare
      *
      * Key for oppdatering av vare-søketekst
      *
     c     jvsoiu_key    klist
     c                   kfld                    jvsoiu_svar
      *
      * Key for les på matche-register
      *
     c     jmatl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppdatering av matche-register
      *
     c     jmatlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jmatlu_var1
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
