     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LO400R
      *  Beskrivelse . . : Sletting av bestillingen (hele)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      *  --------- ------  ------------------------------------------------
      *  6.10  170609 BSA  Oppdatert med sporingsinformasjon
      *  6.10  220909 ASK  Nye parametre til VL001R
6.11  *  6.10  110610 bof  Justert i utvidelse av parm VL001R
6.20  *  6.20  250713 bof  Slett logg når bestillings slettes
6.nu  *  6.30  050614 bof  endring i forb. nummerutvidelse
6.30  *  6.30  040615 bkv  Bestilling økt fra 6 til 8
6.3a  *  6.3a  180416 hko  Henter parameter for kommentar-2
      *
8.01  *PRG2    210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
8.02  *K000    270824 bsa  Logger linjer også ved sleting av bestillinger
8.03  *K000    280824 bsa  Slette også oppføring i tilleggsregister
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
     flohelu    uf   e           k disk    rename(lohepfr:lohelur)
6.20 flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flodtl1    if   e           k disk    rename(lodtpfr:lodtl1r)
     flodtlu    uf   e           k disk    rename(lodtpfr:lodtlur)
     flotxl1    if   e           k disk    rename(lotxpfr:lotxl1r)
     flotxlu    uf   e           k disk    rename(lotxpfr:lotxlur)
6.20 fllogl1    if   e           k disk    rename(llogpfr:llogl1r)
6.20 flloglu    uf   e           k disk    rename(llogpfr:lloglur)
     fldellu    uf a e           k disk    rename(ldelpfr:ldellur)
     ffodtlu    uf   e           k disk    rename(fodtpfr:fodtlur)
8.02 fldeslu    uf a e           k disk    rename(ldespfr:ldeslur)
8.03 flotii2    uf   e           k disk    rename(lotist:lotii2r)
      *
      * Definering av LDA
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Datastruktur for lageroppdatering
      *
     d                 ds
6.nu d hlrec                        128
     d  hlvare                       15    overlay(hlrec)
     d  hllage                        2  0 overlay(hlrec:16)
     d  hldato                         d   overlay(hlrec:18) datfmt(*iso)
     d  hltime                         t   overlay(hlrec:28) timfmt(*iso)
     d  hlotyp                        2    overlay(hlrec:36)
     d  hlltyp                        2    overlay(hlrec:38)
     d  hlanta                       11  3 overlay(hlrec:40)
     d  hlkopr                        9  2 overlay(hlrec:51)
     d  hlrefr                       20    overlay(hlrec:60)
     d  hlsyst                        1    overlay(hlrec:80)
     d  hlaltk                        2    overlay(hlrec:81)
     d  hlrest                        1    overlay(hlrec:83)
     d  hltype                        1    overlay(hlrec:84)
     d  hlenhe                        3    overlay(hlrec:85)
     d  hlinlr                        1    overlay(hlrec:88)
     d  hllety                        1    overlay(hlrec:89)
6.nu d  hlonum                        8  0 overlay(hlrec:90)
6.nu d  hlolin                        4  0 overlay(hlrec:98)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(lofirm)
     d p_numm          s                   like(lonumm)
     d p_suff          s                   like(losuff)
     d p_qmem          s              1
     d p_sald          s             11  2
     d p_type          s                   like(lytype)
     d p_kom1          s                   like(lykom1)
     d p_kom2          s                   like(lykom2)
     d p_deor          s              1
      *
      * Definering av variable i nøkler
      *
     d ldellu_aarr     s                   like(lyaarr)
     d ldellu_numm     s                   like(lynumm)
     d ldellu_suff     s                   like(lysuff)
     d lohelu_numm     s                   like(lonumm)
     d lohelu_suff     s                   like(losuff)
6.20 d lohel1_numm     s                   like(lonumm)
     d lodtl1_numm     s                   like(ldnumm)
     d lodtl1_suff     s                   like(ldsuff)
     d lodtl1_line     s                   like(ldline)
     d lodtlu_numm     s                   like(ldnumm)
     d lodtlu_suff     s                   like(ldsuff)
     d lodtlu_line     s                   like(ldline)
     d lotxl1_numm     s                   like(ltnumm)
     d lotxl1_suff     s                   like(ltsuff)
     d lotxlu_numm     s                   like(ltnumm)
     d lotxlu_suff     s                   like(ltsuff)
     d lotxlu_line     s                   like(ltline)
6.20 d llogl1_numm     s                   like(lmnumm)
6.20 d lloglu_numm     s                   like(lmnumm)
6.20 d lloglu_suff     s                   like(lmsuff)
6.20 d lloglu_kode     s                   like(lmkode)
6.20 d lloglu_date     s                   like(lmdate)
6.20 d lloglu_time     s                   like(lmtime)
     d fodtlu_numm     s                   like(fdnumm)
     d fodtlu_suff     s                   like(fdsuff)
     d fodtlu_line     s                   like(fdline)
8.02 d ldeslu_numm     s                   like(lrnumm)
8.02 d ldeslu_suff     s                   like(lrsuff)
8.02 d ldeslu_line     s                   like(lrline)
      *
      * Definering av variable
      *
6.nu d w_lrec          s            128
     d w_firm          s                   like(lofirm)
     d w_year          s              4  0
     d w_retu          s              1
     d w_dato          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
6.30 d w_numa          s              8
5.02 d w_lina          s              4
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sletting av ordre   ORDRE-HODE-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lohelu_key    chain     lohelur
     c                   if        %found
     c                   unlock    lohelu
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sletting av bestilling   BESTILLING-TEKST
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lotxl1_key    setll     lotxl1r
     c     lotxl1_key    reade     lotxl1r
      *
     c                   dow       not %eof
      *
 003 c                   eval      lotxlu_line = ltline
 003 c     lotxlu_key    chain     lotxlur
B004 c                   if        %found
 004 c                   delete    lotxlur
E004 c                   endif
 003  *
     c     lotxl1_key    reade     lotxl1r
 003 c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sletting av bestilling   BESTILLING-LINJE
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lodtl1_key    setll     lodtl1r
     c     lodtl1_key    reade     lodtl1r
      *
     c                   dow       not %eof
 003  *
 003 c                   eval      lodtlu_line = ldline
 003 c     lodtlu_key    chain     lodtlur
B004 c                   if        %found
 004 c                   delete    lodtlur
E004 c                   endif
8.02  *
8.02  * Legger linja ut i sletteregister
8.02  *
8.02 c                   exsr      del_line
8.02  *
 003  *  Sletter kobling til bestilling på ordrelinjer
 003  *
     c                   eval      fodtlu_numm = ldornu
     c                   eval      fodtlu_suff = ldorsu
     c                   eval      fodtlu_line = ldorli
     c     fodtlu_key    chain     fodtlur
     c                   if        %found
     c                   eval      fdbenr = 0
     c                   eval      fdbsuf = 0
     c                   eval      fdblin = 0
     c                   eval      fdantb = 0
6.10 c                   time                    fdedat
6.10 c                   time                    fdetim
6.10 c                   eval      fdeusr = l_user
      *
     c                   update    fodtlur
     c                   endif
 003  *
 003  *  Kaller opp subrutine for oppdatering av lager
 003  *
B004 c                   if        laalag <> 0  and
B005 c                             p_deor = *blank
 005 c                   mult      -1            ldanta
 005 c                   exsr      opplag
E005 c                   endif
 003  *
     c     lodtl1_key    reade     lodtl1r
 003 c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sletting av bestilling   BESTILLING-HODE
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lohelu_key    chain     lohelur
B001 c                   if        %found
 001 c                   delete    lohelur
E001 c                   endif
8.03  *
8.03 c     lohelu_key    chain     lotii2
8.03 c                   if        %found
8.03 c                   delete    lotii2r
8.03 c                   endif
      *
      *  Oppdaterer BESTILLING-SLETTET-REGISTER med ny bestilling
      *  Opplysninger fra slettet bestilling blir lagt over
      *
B001 c                   if        p_type <> *blank
 001 c     ldellu_key    chain     ldellur
B002 c                   if        not %found
 002 c                   eval      lyfirm = w_firm
 002 c                   eval      lyotyp = lootyp
 002 c                   eval      lybdat = lobdat
 002 c                   eval      lyinnk = loinnk
 002 c                   eval      lyvalk = lovalk
 002 c                   eval      lylele = lolele
 002 c                   eval      lyldor = loldor
 002 c                   eval      lylena = lolena
 002 c                   eval      lynavn = lonavn
 002 c                   eval      lyadr1 = loadr1
 002 c                   eval      lyadr2 = loadr2
 002 c                   eval      lysted = losted
 002 c                   eval      lyvref = lovref
 002 c                   eval      lydref = lodref
 002 c                   eval      lyrekv = lorekv
 002 c                   eval      lytoti = lototi
 002 c                   eval      lytotr = lototr
 002 c                   eval      lytotk = lototk
 002 c                   eval      lyornr = loornr
 002 c                   eval      lyorsu = loorsu
 002 c                   eval      lywsid =l_wsid
 002 c                   eval      lyuser = l_user
 002  *
 002  *  Legger inn dagens dato
 002  *
 002 c                   eval      lydate = w_dato
 002 c                   eval      lytime = w_time
 002  *
 002 c                   eval      lytype = p_type
 002 c                   eval      lykom1 = p_kom1
 002 c                   eval      lykom2 = p_kom2
 002  *
 002 c                   eval      lyaarr = ldellu_aarr
 002 c                   eval      lynumm = ldellu_numm
 002 c                   eval      lysuff = ldellu_suff
6.10 c                   time                    lydate
6.10 c                   time                    lytime
6.10 c                   eval      lyuser = l_user
6.10 c                   time                    lyedat
6.10 c                   time                    lyetim
6.10 c                   eval      lyeusr = l_user
 002 c                   write     ldellur
E002 c                   endif
E001 c                   endif
6.20  *
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  *  Sletting av logg knyttet til bestilling
6.20  *  sjekker først at det ikke ligger igjen best.hode med en eller annen
6.20  *  suffix.
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  *
6.20 c                   eval      lohel1_numm = p_numm
6.20 c     lohel1_key    chain     lohel1
6.20 c                   if        not %found(lohel1)
6.20  *
6.20 c     llogl1_key    setll     llogl1r
6.20 c     llogl1_key    reade     llogl1r
6.20  *
6.20 c                   dow       not %eof(llogl1)
6.20  *
6.20 c                   eval      lloglu_numm = lmnumm
6.20 c                   eval      lloglu_suff = lmsuff
6.20 c                   eval      lloglu_kode = lmkode
6.20 c                   eval      lloglu_date = lmdate
6.20 c                   eval      lloglu_time = lmtime
6.20 c     lloglu_key    chain     lloglu
6.20 c                   if        %found(lloglu)
6.20 c                   delete    lloglur
6.20 c                   endif
6.20  *
6.20 c     llogl1_key    reade     llogl1r
6.20 c                   enddo
6.20  *
6.20 c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Oppdatering av Lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     opplag        begsr
      *
      *  Legger over verdier i overførings-felter
      *
     c                   eval      hlvare = ldvare
     c                   eval      hllage = lolage
     c                   eval      hlenhe = ldenh1
     c                   move      *loval        hldato
     c                   move      *loval        hltime
     c                   eval      hlotyp = lootyp
     c                   eval      hlltyp = ldltyp
      *
     c                   z-add     ldanta        hlanta
     c                   z-add     ldkpny        hlkopr
     c                   eval      hlonum = p_numm
     c                   eval      hlolin = ldline
      *
     c                   eval      hlsyst = 'L'
     c                   eval      hlaltk = *blank
     c                   eval      hlrest = *blank
     c                   move      ldnumm        w_numa
     c                   move      ldline        w_lina
     c                   eval      hlrefr = 'B:' + w_numa +
     c                                      ' L:' + w_lina
     c                   move      ldlety        hllety
      *
      *  Kaller opp lageroppdaterings-program
      *
     c                   eval      w_lrec = hlrec
      *
     c                   call      'VL001R'
     c                   parm                    w_retu
     c                   parm                    w_lrec
      *
     c                   endsr
      *
8.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.02  *  Legger ut linjer på sletteregister
8.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.02  *
8.02 c     del_line      begsr
8.02  *
8.02 c                   eval      ldeslu_numm = ldnumm
8.02 c                   eval      ldeslu_suff = ldsuff
8.02 c                   eval      ldeslu_line = ldline
8.02  *
8.02 c     ldeslu_key    chain     ldeslu
8.02 c                   if        not %found
8.02 c                   eval      lrfirm = w_firm
8.02 c                   eval      lraarr = w_year
8.02 c                   eval      lrnumm = ldnumm
8.02 c                   eval      lrsuff = ldsuff
8.02 c                   eval      lrline = ldline
8.02 c                   eval      lrotyp = lootyp
8.02 c                   eval      lrltyp = ldltyp
8.02 c                   eval      lrlety = ldlety
8.02 c                   eval      lrvare = ldvare
8.02 c                   eval      lrenh1 = ldenh1
8.02 c                   eval      lrlage = ldlage
8.02 c                   eval      lrldor = ldldor
8.02 c                   eval      lrldat = ldldat
8.02 c                   eval      lravde = ldavde
8.02 c                   eval      lrkont = ldkont
8.02 c                   eval      lrspes = ldspes
8.02 c                   eval      lrakti = ldakti
8.02 c                   eval      lrlvar = ldlvar
8.02 c                   eval      lrnobb = ldnobb
8.02 c                   eval      lreann = ldeann
8.02 c                   eval      lrornu = ldornu
8.02 c                   eval      lrorsu = ldorsu
8.02 c                   eval      lrorli = ldorli
8.02 c                   eval      lrtek1 = ldtek1
8.02 c                   eval      lrtek2 = ldtek2
8.02 c                   eval      lranta = ldanta
8.02 c                   eval      lripny = ldipny
8.02 c                   eval      lrkpny = ldkpny
8.02 c                   eval      lrrab1 = ldrab1
8.02 c                   eval      lrrab2 = ldrab2
8.02 c                   eval      lrsumi = ldsumi
8.02 c                   eval      lrsumr = ldsumr
8.02 c                   eval      lrsumk = ldsumk
8.02 c                   eval      lromva = ldomva
8.02 c                   eval      lrlvre = ldlvre
8.02 c                   eval      lrogrp = ldogrp
8.02 c                   eval      lrhgrp = ldhgrp
8.02 c                   eval      lrugrp = ldugrp
8.02 c                   eval      lrkplj = ldkplj
8.02 c                   eval      lrvari = ldvari
8.02  *
8.02 c                   eval      lrslda = w_dato
8.02 c                   eval      lrslti = w_time
8.02 c                   eval      lrspgm = 'LO400R'
8.02 c                   eval      lrslws = l_wsid
8.02 c                   eval      lrslus = l_user
8.02  *
8.02 c                   eval      lrodat = ldodat
8.02 c                   eval      lrotim = ldotim
8.02 c                   eval      lrousr = ldousr
8.02 c                   eval      lredat = ldedat
8.02 c                   eval      lretim = ldetim
8.02 c                   eval      lreusr = ldeusr
8.02  *
8.02 c                   write     ldeslur
8.02 c                   endif
8.02  *
8.02 c                   endsr
8.02  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Tar inn parameter fra forrige program
      *
     c     *entry        plist
     c                   parm                    p_deor
     c                   parm                    p_firm
     c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_type
     c                   parm                    p_kom1
6.3a c                   parm                    p_kom2
      *
      *  Tar inn parameter fra forrige program
      *     c                   parm                    p_kom2
      *
      *  Key for les av LAGER-KODE
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      *  Key for oppdatering av BESTILLING-HODE
      *
     c     lohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelu_numm
     c                   kfld                    lohelu_suff
6.20  *
6.20  *  Key for les av BESTILLING-HODE
6.20  *
6.20 c     lohel1_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    lohel1_numm
      *
      *  Key for les av BESTILLING-LINJE
      *
     c     lodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtl1_numm
     c                   kfld                    lodtl1_suff
      *
      *  Key for oppdatering av BESTILLING-LINJE
      *
     c     lodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlu_numm
     c                   kfld                    lodtlu_suff
     c                   kfld                    lodtlu_line
      *
      *  Key for les av BESTILLING-TEKST
      *
     c     lotxl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lotxl1_numm
     c                   kfld                    lotxl1_suff
      *
      *  Key for oppdatering av BESTILLING-TEKST
      *
     c     lotxlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lotxlu_numm
     c                   kfld                    lotxlu_suff
     c                   kfld                    lotxlu_line
      *
      *  Key for file : BESTILLING-SLETTET-REGISTER
      *
     c     ldellu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ldellu_aarr
     c                   kfld                    ldellu_numm
     c                   kfld                    ldellu_suff
6.20  *
6.20  *  Key for les av Logg-linjer
6.20  *
6.20 c     llogl1_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    llogl1_numm
6.20  *
6.20  *  Key for oppdatering av Logg-linjer
6.20  *
6.20 c     lloglu_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    lloglu_numm
6.20 c                   kfld                    lloglu_suff
6.20 c                   kfld                    lloglu_kode
6.20 c                   kfld                    lloglu_date
6.20 c                   kfld                    lloglu_time
      *
      * Key for oppdatering av Ordre-linje-register
      *
     c     fodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtlu_numm
     c                   kfld                    fodtlu_suff
     c                   kfld                    fodtlu_line
8.02  *
8.02  * Key for oppdatering av Bestilling-linje-register
8.02  *
8.02 c     ldeslu_key    klist
8.02 c                   kfld                    w_firm
8.02 c                   kfld                    w_year
8.02 c                   kfld                    ldeslu_numm
8.02 c                   kfld                    ldeslu_suff
8.02 c                   kfld                    ldeslu_line
      *
      *  Legger inn type for oppdatering av lager  R=Registrering
      *
     c                   eval      hltype = 'R'
      *
      *  Legger verdier i tid/datofelter
      *
     c                   time                    w_dato
     c                   time                    w_time
     c                   extrct    w_dato:*y     w_year
      *
      *  Legger inn verdier i nøklene
      *
     c                   eval      w_firm = p_firm
     c                   eval      lohelu_numm = p_numm
     c                   eval      lohelu_suff = p_suff
      *
     c                   eval      lodtl1_numm = p_numm
     c                   eval      lodtl1_suff = p_suff
      *
     c                   eval      lodtlu_numm = p_numm
     c                   eval      lodtlu_suff = p_suff
      *
     c                   eval      lotxl1_numm = p_numm
     c                   eval      lotxl1_suff = p_suff
      *
6.20 c                   eval      llogl1_numm = p_numm
      *
     c                   eval      ldellu_aarr = w_year
     c                   eval      ldellu_numm = p_numm
     c                   eval      ldellu_suff = p_suff
      *
      *  Hent opplysninger fra LAGER-KODE-REGISTER
      *
     c     lstsl1_key    chain     lstsl1r
      *
     c                   endsr
