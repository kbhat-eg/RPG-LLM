     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASKORT
      * Program . . . . : RF812R
      * Beskrivelse . . : GE Capital (XL Bygg/byggeriet) - transaksjoner
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       221009 BSA  Nytt program
6.10  *       020412 bsa  Tilpasset layout for GE Capital
6.11  *       030712 bsa  Feil i linjeteller og firmainfo
6.12  *       080812 bsa  Skal legge ut butikknummer med foranstilte
6.12  *                   nuller.
6.13  *       090812 bsa  Sjekker mot valgt "kortlager", hvis det er forksjellig
6.13  *                   fra 0.
6.14  *       090812 bsa  KID nummer skal ikke legges ut.
6.15  *       100812 bsa  Skal ikke legge ut rene tekstlinjer.
6.16  *       100812 bsa  Endrer metoden for sette om det er funnet poster.
6.17  *       290812 bsa  Sjekker ikke lagersted på linjenivå. Reverserer
6.17  *                   delvis 6.13.
6.18  *       040912 bsa  Kan ikke legge ut negative tall i fila. Skaper
6.18  *                   ugyldige tegn.
6.1A  *       040912 bsa  Fortsetter fix på negative beløp.
6.20  *       111012 bsa  Skal ikke legge ut blake tekstlinjer.
6.21  *       061114 bsa  Ny fillayout til GE Moneybank (GE 6.10-2014).
6.22  *       251114 bsa  Justeringer på ny fillayout.
6.25  *       240516 bsa  Nullstiller variabler ved tekstlinjer
6.nu  *       230614 bof  Endring ihht. nummerutvidelse
6.30  *       021015 bsa  Håndterer 100 % rabatt
6.31  *       021015 bsa  Sjekker at det ikke er 0 i antall
6.32  *       171215 bsa  Flytter sjekk mot fakturabeløp
6.33  *       040517 bkv  Hoppe over hvis FOFSUM er 0
6.34  *       230322 bof  Takle rabatt over 100 %, settes til 99,99
      *
8.01  * 8.xx  010322 bsa  Endret utlegg av varenummer. Splittes i lengde
8.01  * 8.xx              på varenummer og konstant.
8.02  * 8.xx  080822 bsa  Feil felt benyttes for å summere opp rabatt.
8.03  * 8.xx  070223 sst  Legger ut bruttopris i rikitg format
8.04  *PRGR   150224 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frukrl5    if   e           k disk    rename(rukrpfr:rukrl5r)
     fbokkl5    uf   e           k disk    rename(bokkpfr:bokkl5r)
     frukrlu    uf   e           k disk    rename(rukrpfr:rukrlur)
     fruksl1    if   e           k disk    rename(rukspfr:ruksl1r)
     fbcdtir    if   e           k disk    rename(bcdtst:bcdtirr)
     fbchei1    if   e           k disk    rename(bchest:bchei1r)
     fsodtlr    if   e           k disk    rename(sodtpfr:sodtlrr)
     fsohelr    if   e           k disk    rename(sohepfr:sohelrr)
6.21 fbohel1    if   e           k disk    rename(bohepfr:bohel1r)
6.21 fra09l1    if   e           k disk    rename(ra09pfr:ra09l1r)
     fgemonbpf  o    e             disk
     fxmllogpf  o    e             disk
      *
      * Definering av parametre
      *
     d                 ds
     d p_prec                       128
     d  p_ktyp                        2    overlay(p_prec:1)
     d* p_frda                        6  0 overlay(p_prec:3)
     d* p_tida                        6  0 overlay(p_prec:9)
     d  p_frda                         d   overlay(p_prec:3)
     d  p_tida                         d   overlay(p_prec:13)
     d* p_omkj                        1  0 overlay(p_prec:15)
     d  p_omkj                        1  0 overlay(p_prec:23)
     d  p_lage                        2  0 overlay(p_prec:24)
     d  p_ftxt                       30    overlay(p_prec:26)
      *
      * Definering av variable i nøkler
      *
     d rukrl5_ktyp     s                   like(ruktyp)
     d rukrlu_kkun     s                   like(rukkun)
     d rukrlu_kpro     s                   like(rukpro)
     d rukrlu_ktyp     s                   like(ruktyp)
     d rukrlu_ktsq     s                   like(ruktsq)
     d bokkl5_ktyp     s                   like(brktyp)
     d bokkl5_kund     s                   like(brkund)
     d bokkll_ktyp     s                   like(brktyp)
     d bokkll_kund     s                   like(brkund)
     d bokkll_odat     s                   like(brodat)
     d sohelr_aarr     s                   like(soaarr)
     d sohelr_binr     s                   like(sobinr)
     d sohelr_numm     s                   like(sonumm)
     d sohelr_suff     s                   like(sosuff)
     d sodtlr_aarr     s                   like(sdaarr)
     d sodtlr_binr     s                   like(sdbinr)
     d bcdtir_bong     s                   like(bibong)
     d bcdtll_bong     s                   like(bibong)
     d bcdtll_boli     s                   like(biboli)
     d bchei1_bong     s                   like(bhbong)
     d ruksl1_skty     s                   like(ruskty)
     d ruksl1_slag     s                   like(ruslag)
6.21 d bohel1_aarr     s                   like(boaarr)
6.21 d bohel1_wsid     s                   like(bowsid)
6.21 d bohel1_bong     s                   like(bobong)
6.21 d ra09l1_ikod     s                   like(raikod)
      *
      * Definering av outputrecord
      *
     d d_orec          s            400
      *
      * Definering av variable
      *
     d b_tran          s              1    inz(*off)
     d b_genf          s              1    inz(*off)
     d b_rapp          s              1    inz(*off)
     d b_bong          s              1    inz(*off)
     d b_null          s              1    inz(*off)
      *
     d w_firm          s              3  0
     d w_prec          s            128
     d p_post          s              1
      *
     d w_year          s              4  0
     d w_frda          s               d   datfmt(*iso)
     d w_tida          s               d   datfmt(*iso)
     d w_wdat          s             10
     d w_wtim          s             10
     d w_decx          s             20
     d w_kknr          s             20
     d w_pris          s             11  2
     d w_pria          s             14
     d w_lana          s             13
     d w_mvab          s             11  2
     d w_snet          s             11  2
     d w_anet          s             14
     d w_smva          s             11  2
     d w_amva          s             14
     d w_stot          s             11  2
     d w_atot          s             14
     d w_rabp          s              5  2
     d w_raba          s              8
     d w_ltot          s             13  2
     d w_ltoa          s             13
6.21 d w_mtot          s             13  2
6.21 d w_mtoa          s             13
6.21 d w_bpri          s             13  2
6.21 d w_bpra          s             13
6.21 d w_brab          s              4  2
6.21 d w_braa          s              4
6.21 d w_trab          s             13  2
     d w_lbel          s             13  2
     d w_lbea          s             13
     d w_nett          s             11  2
     d w_binr          s             15
6.11 d w_tell          s             10  0
     d w_kidn          s             18
     d w_raba1         s             13
     d w_raba2         s             13
     d w_vare          s             15
     d w_enhe          s              4
     d w_rab1          s             13  2
     d w_rab2          s             13  2
     d w_sumb          s             11  2
     d w_tek1          s            100
      *
     d w_posi          s              4  0
     d w_posu          s              4  0
     d w_anta          s             13  2
     d w_ants          s             13  2
     d w_sisb          s              6  0
     d w_ddat          s               d   datfmt(*iso)
     d w_str_x         s              1
     d w_retk          s              1
     d w_stxt          s             20
     d w_levd          s              8  0
      *
     d w_tmp1          s              1    dim(50)
     d w_tmp2          s              1    dim(50)
     d w_str_inn       s             50
     d w_str_ut        s             50
     d x               s              3  0
     d y               s              3  0
      *
      * Generert dato
     d                 ds
     d w_gdat                          d   datfmt(*iso)
     d  w_gyea                        4    overlay(w_gdat:1)
     d  w_gmnd                        2    overlay(w_gdat:6)
     d  w_gdag                        2    overlay(w_gdat:9)
      * Fakturadato
     d                 ds
     d w_fdat                          d   datfmt(*iso)
     d  w_fyea                        4    overlay(w_fdat:1)
     d  w_fmnd                        2    overlay(w_fdat:6)
     d  w_fdag                        2    overlay(w_fdat:9)
      * Leveringsdato
     d                 ds
     d w_ldat                          d
     d  w_ldaa                        4    overlay(w_ldat:1)
     d  w_ldam                        2    overlay(w_ldat:6)
     d  w_ldad                        2    overlay(w_ldat:9)
      *
     d                 ds
     d w_dato                         8  0
     d  w_data                        8    overlay(w_dato:1)
     d  w_daty                        4    overlay(w_dato:1)
     d  w_datm                        2    overlay(w_dato:5)
     d  w_datd                        2    overlay(w_dato:7)
      *
     d                 ds
     d l_lrec                       400
     d  l_kknr                       13    overlay(l_lrec:1)
     d  l_dato                        8  0 overlay(l_lrec:14)
6.nu d  l_orna                        8    overlay(l_lrec:22)
6.nu d  l_sufa                        2    overlay(l_lrec:30)
6.nu d  l_stxt                       20    overlay(l_lrec:32)
6.nu d  l_vare                       15    overlay(l_lrec:52)
6.nu d  l_tek1                       35    overlay(l_lrec:67)
6.nu d  l_tek2                       35    overlay(l_lrec:102)
6.nu d  l_lana                       14    overlay(l_lrec:137)
6.nu d  l_enho                        3    overlay(l_lrec:151)
6.nu d  l_pria                       14    overlay(l_lrec:154)
6.nu d  l_raba                        8    overlay(l_lrec:168)
6.nu d  l_ltoa                       14    overlay(l_lrec:176)
      *
      *  Konstanter
      *
     d c_apos          c                   x'7D'
     d c_digits        c                   ' 0123456789'
     d c_pluss         c                   '+'
     d c_minus         c                   '-'
      *
      * String konstanter
      *
     d c_ta04          c                   '    '
     d c_ta06          c                   '      '
     d c_ta12          c                   '            '
     d c_ta24          c                   '                        '
      *
      * Feltnavn - meldingsspesifikke
      *
      * Header record - Filspec
      *
      * Fast verdi
     d h0_f01          c                   'EK'
      * Fast verdi (I=import)
     d h0_f02          c                   'I'
      * Fast verdi (H=header)
     d h0_f03          c                   'H'
      * Dato for generering av fil
     d h0_f04          s              8
      * Kjede identifikasjon
6.11 d h0_f05          c                   'BM        '
      * Filler (blanke)
     d h0_f06          s            278
      *
      * Substruktur #1 - KID spesifikasjon (Heading nivå)
      *
      * Fast verdi
     d s1_f01          c                   'EK'
      * Fast verdi (I=import)
     d s1_f02          c                   'I'
      * Fast verdi (K=kid informasjon)
     d s1_f03          c                   'K'
      * Bilagsdato (fakturadato)
     d s1_f04          s              8
      * GE Butikknummer
6.21 d s1_f05          s              7
6.21  * Filler (blanke)
6.21 d s1_f05x         c                   '    '
      * Identifikasjon (kortnummer til kunde)
     d s1_f06          s             20
      * Fakturabeløp
     d s1_f07          s             14
      * Kampanje (valgfritt)
6.21 d s1_f08          c                   '   '
      * Autorisasjonsnummer (valgfritt)
     d s1_f09          c                   '0000'
      * Referansenummer (faktura/bong nummer)
     d s1_f10          s             15
6.21  * Ordrenummer 16 pos, evnt fakturanummer på nytt)
     d s1_f11          s             25
6.21  * Totalt mva beløp
6.21 d s1_f13          s             14
      * Filler (blanke)
6.21 d s1_f12          s            182
      *
      * Substruktur #2 - Varelinje spesifikasjon
      *
      * Fast verdi
     d s2_f01          c                   'EK'
      * Fast verdi (I=import)
     d s2_f02          c                   'I'
      * Fast verdi (D=detaljer om kjøpet)
     d s2_f03          c                   'D'
      * Bilagsdato
     d s2_f04          s              8
      * GE Butikknummer
6.21 d s2_f05          s              7
6.21  * Rabattprosent
6.21 d s2_f05x         s              4
      * Identifikasjon (feks kortnummer. 5 siste posisjoner)
     d s2_f06          s             20
      * Fakturabeløp
     d s2_f07          s             14
      * Kampanje (valgfritt)
6.21 d s2_f08          c                   '   '
      * Autorisasjonsnummer (valgfritt)
     d s2_f09          c                   '0000'
      * Referansenummer (Faktura/bongnummer)
     d s2_f10          s             15
      * Varetekst
     d s2_f11          s             35
      * Fakturert kvantum
     d s2_f12          s             14
      * Enhet (ref. NOBB standard)
     d s2_f13          s              3
      * Rabattbeløp 1
6.21 d s2_f14          c                   '0000000000000+'
6.21  * Enhetspris
     d s2_f15          s             14
      * Rabattbeløp 3
6.21 d s2_f16          c                   '0000000000000+'
      * Beløp ink. mva (belastet kunde)
     d s2_f17          s             14
      * Leveringsdato (kjøpsdato)
     d s2_f18          s              8
      * Selger (navn på selger)
     d s2_f19          s             30
      * Beløp eks. mva
6.21 d s2_f20          c                   '0000000000000+'
      * Mva
6.21 d s2_f21          c                   '0000000000000+'
      * Varenummer(NOBB , vestrestilt)
8.01 d*s2_f22          s             35
8.01 d s2_f22          s             15
8.01 d s2_f220         c                   '                    '
      * Filler
6.21 d s2_f23          c                   '            '
      *
      * Substruktur #3 - Trailer record
      *
      * Fast verdi
     d s3_f01          c                   'EK'
      * Fast verdi (I=import)
     d s3_f02          c                   'I'
      * Fast verdi (T=trailer record)
     d s3_f03          c                   'T'
      * Generert dato
     d s3_f04          s              8
      * Antall records på fila
     d s3_f05          s             10
      * Filler (blanke)
     d s3_f06          s            278
      *
      * LDA område
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Skriver initielle headinginformasjon
      *
     c                   exsr      skriv_init
      *
     c                   if        b_genf = *on
      *
      * Leser kunde/kort register og produserer vare spesifikasjoner
      *
     c                   exsr      les_kort
      *
      * Skriver avsluttende info
      *
     c                   exsr      avsl_melding
      *
     c                   endif
      *
      * Avslutt program
      *
6.16  * Sjekk om det er funnet poster. Teller er større enn 2
6.16 c                   if        w_tell > 2
6.16 c                   eval      p_post = '1'
6.16 c                   endif
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å lese kunde/kort koblinger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_kort      begsr
      *
     c     rukrl5_key    setll     rukrl5
     c     rukrl5_key    reade     rukrl5
      *
     c                   dow       not %eof(rukrl5)
      * Vi benytter fradato fra siste kjøretidspunkt, hvis ikke fradato
      * er angitt
     c                   if        w_frda = *loval
     c                   eval      w_frda = rukdat
     c                   endif
     c                   move      rukknr        w_kknr
      *
      * Les fra BOKKL5 for å finne poster som skal være med i rapporten
      *
     c                   eval      bokkl5_ktyp = ruktyp
     c                   eval      bokkl5_kund = rukkun
     c                   eval      bokkll_ktyp = ruktyp
     c                   eval      bokkll_kund = rukkun
     c                   eval      bokkll_odat = w_frda
     c     bokkll_key    setll     bokkl5
     c     bokkl5_key    reade     bokkl5
     c                   dow       not %eof
      * Sjekk mot til dato
     c                   if        brodat > w_tida
     c                   goto      les_neste
     c                   endif
      * Skal posten legges ut på nytt, må det være omkjøring
     c                   if        brofda <> *loval and
     c                             p_omkj = 0
     c                   goto      les_neste
     c                   endif
      * Skal posten legges ut, kan den ikke være behandlet tidligere
     c                   if        brofda = *loval and
     c                             p_omkj = 1
     c                   goto      les_neste
     c                   endif
      * Hvis fakturanummer utfylt, les fra historisk fakturaregister eller fra bongregister
6.21 c                   eval      w_binr = *blanks
     c                   eval      b_bong = *off
     c                   if        brbinr <> 0
6.16 c**                 eval      p_post = '1'
     c                   movel     brbinr        w_binr
     c                   exsr      les_hfakt
     c                   elseif    brbong <> *blank
6.16 c**                 eval      p_post = '1'
     c                   movel     brbong        w_binr
     c                   exsr      les_bong
     c                   endif
      *
      * Merk BOKK postering med at den er lest
      *
6.13 c                   if        b_rapp = *on
     c                   if        p_omkj = 0
     c                   time                    brofda
     c                   time                    bredat
     c                   time                    brofti
     c                   time                    bretim
     c                   eval      breusr = l_user
     c                   update    bokkl5r
     c                   endif
6.13 c                   endif
      *
     c     les_neste     tag
      *
     c     bokkl5_key    reade     bokkl5
     c                   enddo
      *
     c     rukrl5_key    reade     rukrl5
     c                   enddo
      *
     c     end_kort      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for les av poster fra historisk fakturaregister *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     les_hfakt     begsr
      *
     c                   eval      b_rapp = *off
     c                   eval      b_bong = *off
      * Sjekk fra hvilket år statistikkposten tilhører
     c                   eval      sodtlr_aarr = %subdt(brodat:*years)
     c                   eval      sodtlr_binr = brbinr
     c     sodtlr_key    chain     sodtlr                             91
     c                   if        *in91 = *off
     c                   eval      b_rapp = *on
     c                   else
     c                   eval      sodtlr_aarr = %subdt(brodat:*years)-1
     c                   eval      sodtlr_binr = brbinr
     c     sodtlr_key    chain     sodtlr                             91
     c                   if        *in91 = *off
     c                   eval      b_rapp = *on
     c                   endif
     c                   endif
      * Hvis det finnes linjer, henter vi info fra hodet
     c                   if        b_rapp = *on
      * Hent informasjon fra fakturahodet. MÅ finne posten med fakturasummen
     c                   eval      sohelr_aarr = sdaarr
     c                   eval      sohelr_binr = brbinr
     c                   eval      sohelr_numm = 0
     c                   eval      sohelr_suff = 0
     c     sohelr_key    setll     sohelr
     c     sohell_key    reade     sohelr
     c                   dow       not %eof(sohelr)
6.32 c**                 if        sofsum <> 0
6.13 c                   if        p_lage <> 0 and
6.13 c                             p_lage <> solage
6.13 c                   eval      b_rapp = *off
6.13 c                   goto      les_nyhead
6.13 c                   endif
6.33 c                   if        sofsum = 0
6.33 c                   goto      les_nyhead
6.33 c                   endif
      * Skriv informasjon om fakturanr/fakturadato og utsalgssted
     c     *iso          move      sofkda        w_dato                         Fakturadato
     c                   if        soldat <> *loval
     c     *iso          move      soldat        w_levd                         Leveringsdato
     c                   else
     c     *iso          move      sobdat        w_levd                         Leveringsdato
     c                   endif
     c                   movel     sobinr        w_binr                         Fakturanummer
6.14 c**                 movel     sokidr        w_kidn                         KID nummer
6.14 c                   eval      w_kidn = *blanks                             KID nummer
6.21 c                   movel     sonumm        w_kidn                         ordrenummer
     c                   eval      w_ltot = sofsum                              Fakturatotal
6.21 c                   eval      w_mtot = sofsum - somvgp                     MVA total
6.21 c                   if        somkod = 1
6.21 c                   eval      w_mtot = 0
6.21 c                   endif
      * Selger
6.21 c                   eval      w_stxt = *blanks
6.21 c                   eval      ra09l1_ikod = soselg
6.21 c     ra09l1_key    chain     ra09l1
6.21 c                   if        %found
6.21 c                   eval      w_stxt = raitxt
6.21 c                   endif
     c                   exsr      skriv_fhspec
      *
     c     sohell_key    reade     sohelr
     c                   enddo
      *
      * Les fra SODTPF for å finne fakturalinjene
      *
     c     sodtlr_key    setll     sodtlr
     c     sodtlr_key    reade     sodtlr
      *
     c                   dow       not %eof(sodtlr)
6.13 c**6.17             if        p_lage <> 0 and
6.13 c**6.17                       p_lage <> sdlage
6.13 c**6.17             goto      les_nyline
6.13 c**6.17             endif
      * Skal ikke legge ut tekstlinjer
6.15 c                   if        sdltyp = 'TX'
6.15 c                   goto      les_nyline
6.15 c                   endif
      * Skriv varelinje (flytt over verdier først)
     c                   eval      w_vare = sdvare
     c                   eval      w_anta = sdanta
     c                   eval      w_sumb = sdsalg
     c                   eval      w_rab1 = sdrkr1
     c                   eval      w_rab2 = sdrkr2
     c                   eval      w_tek1 = *blanks
     c                   movel     sdtek1        w_tek1
     c                   eval      w_enhe = sdenh1
6.20  * Skal ikke legge ut blanke tekstlinjer
6.20 c                   if        %trim(w_tek1) <> *blank
     c                   exsr      skriv_flspec
     c                   exsr      skriv_logg
6.20 c                   endif
      *
6.13 c     les_nyline    tag
      *
     c     sodtlr_key    reade     sodtlr
     c                   enddo
      * Vi har lagt ut hele fakturaen med linjer
     c                   endif
      *
6.13 c     les_nyhead    tag
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for les av poster fra bongregister                  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     les_bong      begsr
      *
6.13 c**                 eval      b_tran = *off
6.13 c                   eval      b_rapp = *off
     c                   eval      b_bong = *on
      *
      * Les fra BCDTPF for å finne poster som skal være med i rapporten
      *
     c                   eval      bcdtir_bong = brbong
     c                   eval      bcdtll_bong = brbong
     c                   eval      bcdtll_boli = 0
     c     bcdtll_key    setll     bcdtir
     c     bcdtir_key    reade     bcdtir
     c                   dow       not %eof(bcdtir)
      *
6.13 c                   if        p_lage <> 0 and
6.13 c                             p_lage <> bilage
6.13 c                   goto      les_nybline
6.13 c                   endif
      *
6.13 c**                 if        b_tran = *off
6.13 c                   if        b_rapp = *off
6.13 c**                 eval      b_tran = *on
6.13 c                   eval      b_rapp = *on
     c
      * Les bonghode for informasjon
     c                   eval      bchei1_bong = brbong
     c     bchei1_key    chain     bchei1
      * Skriv informasjon om fakturanr/fakturadato og utsalgssted
     c     *iso          move      bhodat        w_dato                         Fakturadato
     c     *iso          move      bhodat        w_levd                         Leveringsdato
     c                   movel     brbong        w_binr                         Bongnummer
6.21 c                   movel     brbong        w_kidn                         Bongnummer
     c                   eval      w_ltot = bhmotb                              Fakturatotal
6.21  * Slå opp på BOHE  for å finne mva info
6.21 c                   eval      w_mtot = 0
6.21 c                   eval      bohel1_aarr = %subdt(bhodat:*years)
6.21 c                   movel     bhkass        bohel1_wsid
6.21 c                   eval      bohel1_bong = bhbong
6.21 c     bohel1_key    chain     bohel1
6.21 c                   if        %found
6.21 c                   eval      w_mtot = bototb - botots                     MVA total
6.21 c                   if        bomkod = 1
6.21 c                   eval      w_mtot = 0
6.21 c                   endif
6.21 c                   endif
      * Selger
6.21 c                   eval      w_stxt = *blanks
6.21 c                   eval      ra09l1_ikod = bhselg
6.21 c     ra09l1_key    chain     ra09l1
6.21 c                   if        %found
6.21 c                   eval      w_stxt = raitxt
6.21 c                   endif
     c                   exsr      skriv_fhspec
     c                   endif
      *
      * Skriv varelinje (flytt over verdier først)
      *
     c                   eval      w_anta = bianta
     c                   eval      w_sumb = bissim
8.02 c**                 eval      w_rab1 = bisrim
8.02 c                   eval      w_rab1 = birbe1+birbi2+bibrb1+bibrb2+biorbe
8.02 c                   if        w_rab1 <> 0
8.02 c                   eval      w_rab1 = bisrim
8.02 c                   endif
     c                   eval      w_rab2 = 0
     c                   eval      w_vare = bivare
     c                   eval      w_tek1 = *blanks
     c                   movel     bitek1        w_tek1
     c                   eval      w_enhe = bienhe
6.20  * Skal ikke legge ut blanke tekstlinjer
6.20 c                   if        %trim(w_tek1) <> *blank
     c                   exsr      skriv_flspec
     c                   exsr      skriv_logg
6.20 c                   endif
      *
6.13 c     les_nybline   tag
      *
      * Les neste post bongdetaljer
      *
     c     bcdtir_key    reade     bcdtir
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for skriving av fysisk record - m/konv. av bokstaver
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     write_outp    begsr
      *
     c                   eval      gemonb = d_orec
     c*    'Æ':X'46'     xlate     gemonb        gemonb
     c*    'æ':X'A0'     xlate     gemonb        gemonb
     c*    'Ø':X'77'     xlate     gemonb        gemonb
     c*    'ø':X'90'     xlate     gemonb        gemonb
     c*    'Å':X'2A'     xlate     gemonb        gemonb
     c*    'å':X'EF'     xlate     gemonb        gemonb
      *
     c                   write     gemonbpfr
      *
     c                   eval      d_orec = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for gen. av fakturaheading - fakturahistorikk
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     skriv_fhspec  begsr
      *
6.21 c                   eval      w_ltoa = *blanks
6.21 c                   eval      w_mtoa = *blanks
      *
      * Fakturaheadder
      *
     c     *iso          move      w_dato        w_fdat                         Fakturadato
     c                   eval      s1_f04 = w_fyea + w_fmnd + w_fdag
6.12 c                   movel     ruskid        s1_f05                         GE butikknr
6.12 c*                  eval      w_str_inn = *blanks
6.12 c*                  movel     ruskid        w_str_inn
6.12 c*                  exsr      rmv_char
6.12 c*                  eval      s1_f05 = %trim(w_str_ut)
      *
     c**                 move      brkort        s1_f06                         Kortnummer
     c                   eval      w_str_inn = *blanks
     c                   movel     brkort        w_str_inn
     c                   exsr      rmv_char
     c                   eval      s1_f06 = %trim(w_str_ut)
      *
6.1A c**                 move      w_ltot        w_ltoa
6.1A c**                 movel     w_ltoa        s1_f07                         Fakturabeløp
     c                   if        w_ltot < 0
     c                   move      c_minus       s1_f07                         Fakturabeløp
6.18 c                   eval      w_ltot = w_ltot * -1                         Fakturabeløp
6.1A c                   move      w_ltot        w_ltoa                         Fakturabeløp
6.1A c                   movel     w_ltoa        s1_f07                         Fakturabeløp
6.1A c                   move      c_minus       s1_f07                         Fakturabeløp
     c                   else
6.1A c                   move      w_ltot        w_ltoa                         Fakturabeløp
6.1A c                   movel     w_ltoa        s1_f07                         Fakturabeløp
     c                   move      c_pluss       s1_f07                         Fakturabeløp
     c                   endif
      *
6.21 c                   if        w_mtot < 0
6.21 c                   move      c_minus       s1_f13                         Mva total
6.21 c                   eval      w_mtot = w_mtot * -1
6.21 c                   move      w_mtot        w_mtoa
6.21 c                   movel     w_mtoa        s1_f13
6.21 c                   move      c_minus       s1_f13
6.21 c                   else
6.21 c                   move      w_mtot        w_mtoa
6.21 c                   movel     w_mtoa        s1_f13
6.21 c                   move      c_pluss       s1_f13
6.21 c                   endif
      *
     c                   movel     w_binr        s1_f10                         Fakturanr
     c                   movel     w_kidn        s1_f11                         Ordrenummer
     c                   eval      s1_f12 = *blanks                             Filler
     c                   eval      d_orec = s1_f01 + s1_f02 + s1_f03 +
     c                                      s1_f04 + s1_f05 + s1_f05x +
     c                                      s1_f06 + s1_f07 + s1_f08 +
     c                                      s1_f09 + s1_f10 + s1_f11 +
     c                                      s1_f13 + s1_f12
     c                   exsr      write_outp
      *
     c                   eval      w_tell = w_tell + 1
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for gen. av linjeinformasjon fakturahistorikk
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     skriv_flspec  begsr
      *
      * Fakturalinjer
      *
      * Nullstiller verdier som legge ut på xml
     c                   eval      w_lana = *blanks
     c                   eval      w_lbea = *blanks
     c                   eval      w_raba1 = *blanks
     c                   eval      w_raba2 = *blanks
6.21 c                   eval      w_bpra = *blanks
6.21 c                   eval      w_braa = *blanks
      * Hvis tekstlinje, kalkuleres ingenting
     c                   if        w_vare <> *blank
      * Antall
     c                   if        w_anta < 0
6.18 c                   eval      w_anta = w_anta * -1
6.1A c                   move      w_anta        w_lana
6.1A c                   movel     w_lana        s2_f12
     c                   move      c_minus       s2_f12
     c                   else
6.1A c                   move      w_anta        w_lana
6.1A c                   movel     w_lana        s2_f12
     c                   move      c_pluss       s2_f12
     c                   endif
6.21  * Rabatt 1 (erstattes av konstant)
6.21 c*                  if        w_rab1 < 0
6.21 c*                  eval      w_rab1 = w_rab1 * -1
6.21 c*                  move      w_rab1        w_raba1
6.21 c*                  movel     w_raba1       s2_f14
6.21 c*                  move      c_minus       s2_f14
6.21 c*                  else
6.21 c*                  move      w_rab1        w_raba1
6.21 c*                  movel     w_raba1       s2_f14
6.21 c*                  move      c_pluss       s2_f14
6.21 c*                  endif
6.21  * Rabatt 2
6.21 c*                  if        w_rab2 < 0
6.21 c*                  eval      w_rab2 = w_rab2 * -1
6.21 c*                  move      w_rab2        w_raba2
6.21 c*                  movel     w_raba2       s2_f15
6.21 c*                  move      c_minus       s2_f15
6.21 c*                  else
6.21 c*                  move      w_rab2        w_raba2
6.21 c*                  movel     w_raba2       s2_f15
6.21 c*                  move      c_pluss       s2_f15
6.21 c*                  endif
6.21  * Rabatt 3
6.21 c*                  eval      s2_f16  = '0000000000000+'
      *
6.21  * Beregner rabattprosent
6.21 c                   eval      s2_f05x = *blanks
6.21 c                   eval      w_trab = w_rab1 + w_rab2
6.21 c                   if        w_trab <> 0
6.30 c                   if        w_trab <> w_sumb
6.34 c                   if        ((w_trab / w_sumb) * 100) > 99,99
6.34 c                   eval      w_brab = 99.99
6.34 c                   else
6.21 c                   eval(h)   w_brab = (w_trab / w_sumb) * 100
6.34 c                   endif
6.30 c                   else
6.30 c                   eval      w_brab = 99.99
6.30 c                   endif
6.21 c                   move      w_brab        w_braa
6.21 c                   move      w_braa        s2_f05x
6.21 c                   endif
6.31  * Unngår 0 i antall
6.31 c                   if        w_anta = 0
6.31 c                   eval      w_anta = 1
6.31 c                   endif
      *Beregner linjetotaler
6.21 c                   if        b_bong = *off
     c                   eval(h)   w_lbel = (w_sumb - w_rab1 - w_rab2) * 1.25
6.21 c                   eval(h)   w_sumb = w_sumb * 1.25
6.22  * Beregner bruttopris
6.22 c                   eval(h)   w_bpri = w_sumb/w_anta
6.21 c                   else
6.22  * Beregner bruttopris
6.22 c                   eval(h)   w_bpri = w_sumb/w_anta
     c                   eval(h)   w_lbel = w_sumb - w_rab1 - w_rab2
6.21 c                   eval(h)   w_sumb = w_sumb - w_rab1 - w_rab2
6.21 c                   endif
      *
6.21 c                   if        w_bpri < 0
8.03 c                   eval      w_bpri = w_bpri * -1
6.21 c                   move      w_bpri        w_bpra
6.21 c                   movel     w_bpra        s2_f15
6.21 c                   move      c_minus       s2_f15
6.21 c                   else
6.21 c                   move      w_bpri        w_bpra
6.21 c                   movel     w_bpra        s2_f15
6.21 c                   move      c_pluss       s2_f15
6.21 c                   endif
      * Beregner nettobeløp inkl mva
     c                   if        w_lbel < 0
6.18 c                   eval      w_lbel = w_lbel * -1
6.1A c                   move      w_lbel        w_lbea
6.1A c                   movel     w_lbea        s2_f17
     c                   move      c_minus       s2_f17
     c                   else
6.1A c                   move      w_lbel        w_lbea
6.1A c                   movel     w_lbea        s2_f17
     c                   move      c_pluss       s2_f17
     c                   endif
6.25  * Hvis tekstlinje - nullstilles variabler
6.25 c                   else
6.25 c                   eval      s2_f12 = '0000000000000+'
6.25 c                   eval      s2_f13 = *blanks
6.25 c                   eval      s2_f05x = '0000'
6.25 c                   eval      s2_f15 = '0000000000000+'
6.25 c                   eval      s2_f17 = '0000000000000+'
     c                   endif
      *
     c     *iso          move      w_dato        w_fdat                         Fakturadato
     c                   eval      s2_f04 = w_fyea + w_fmnd + w_fdag
     c**                 eval      s2_f05 = ruskid                              GE butikknr
     c                   eval      s2_f05 = s1_f05                              GE butikknr
     c**                 move      brkort        s2_f06                         Kortnummer
     c                   eval      s2_f06 = s1_f06                              Kortnummer
     c                   eval      s2_f07 = s1_f07                              Fakturabeløp
6.21 c                   eval      s2_f10 = *blanks
     c                   eval      s2_f10 = w_binr                              Fakturanr
     c                   movel     w_tek1        s2_f11                         Varetekst
     c                   eval      s2_f13 = w_enhe                              Enhet
     c     *iso          move      w_levd        w_ldat                         Leveringsdato
     c                   eval      s2_f18 = w_ldaa + w_ldam + w_ldad            Leveringsdato
6.21 c**                 eval      s2_f19 = p_ftxt                              Utsalgssted
6.21 c**                 eval      s2_f20 = '0000000000000+'                    Beløp u/mva
6.21 c**                 eval      s2_f21 = '0000000000000+'                    MVA beløp
6.21 c**                 eval      s2_f22 = *blanks                             Prosjektnummer
6.21 c**                 eval      s2_f23 = *blanks                             Filler
6.21 c                   movel     w_stxt        s2_f19                         Selgernavn
6.21 c                   eval      s2_f22 = w_vare                              Artikkelnummer
     c                   eval      d_orec = s2_f01 + s2_f02 + s2_f03 +
6.21 c                                      s2_f04 + s2_f05 + s2_f05x +
6.21 c                                      s2_f06 + s2_f07 + s2_f08 +
6.21 c                                      s2_f09 +
     c                                      s2_f10 + s2_f11 + s2_f12 +
     c                                      s2_f13 + s2_f14 + s2_f15 +
     c                                      s2_f16 + s2_f17 + s2_f18 +
     c                                      s2_f19 + s2_f20 + s2_f21 +
8.01 c                                      s2_f22 + s2_f220 + s2_f23
     c                   exsr      write_outp
      *
     c                   eval      w_tell = w_tell + 1
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for gen. av initielle meldingsdata   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     skriv_init    begsr
      *
     c                   eval      w_tell = 0
     c                   eval      b_genf = *on
      * Hent inn infomasjon om kort, og at det finnes
     c                   eval      ruksl1_skty = %int(p_ktyp)
     c                   eval      ruksl1_slag = p_lage
     c     ruksl1_key    chain     ruksl1
     c                   if        not %found
     c                   eval      b_genf = *off
     c                   goto      avsl_init
     c                   endif
      *
      * Header
      *
     c                   eval      w_gdat = w_ddat                              Dagens dato
     c                   eval      h0_f04 = w_gyea + w_gmnd + w_gdag
     c                   eval      h0_f06 = *blanks                             Filler
     c                   eval      d_orec = h0_f01 + h0_f02 + h0_f03 +
     c                                      h0_f04 + h0_f05 + h0_f06
     c                   exsr      write_outp
      *
     c                   eval      w_tell = w_tell + 1
      *
     c     avsl_init     endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for gen. av avsluttende record
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avsl_melding  begsr
      *
     c                   eval      w_tell = w_tell + 1
      *
     c                   eval      ruksl1_skty = %int(p_ktyp)
     c                   eval      ruksl1_slag = p_lage
     c     ruksl1_key    chain     ruksl1
     c                   if        not %found
     c                   eval      b_genf = *off
     c                   goto      avsl_avsl
     c                   endif
      *
      * Trail record
      *
     c                   eval      s3_f04 = h0_f04                              Dagens dato
     c                   move      w_tell        s3_f05                         Antall records
     c                   eval      s3_f06 = *blanks                             Filler
     c                   eval      d_orec = s3_f01 + s3_f02 + s3_f03 +
     c                                      s3_f04 + s3_f05 + s3_f06
     c                   exsr      write_outp
      *
     c     avsl_avsl     endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for gen. av "logg" transaksjon       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_logg    begsr
      *
     c                   eval      l_lrec = *blanks
      *
     c                   eval      l_kknr = w_kknr
     c                   eval      l_dato = w_dato
     c                   eval      l_orna = w_binr
     c                   eval      l_sufa = '00'
     c                   eval      l_stxt = w_stxt
     c                   eval      l_vare = w_vare
     c                   eval      l_tek1 = w_tek1
     c                   eval      l_tek2 = *blanks
     c                   eval      l_lana = w_lana
     c                   eval      l_enho = w_enhe
     c                   eval      l_pria = *blanks
     c                   eval      l_raba = w_raba
     c                   eval      l_ltoa = w_ltoa
      *
     c                   eval      xmllog = l_lrec
     c                   write     xmllogpfr
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fjerne foranstilte 0'er og blanke
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     rmv_char      begsr
      * Fjerner alle blanke tegn.
     c                   eval      b_null = *on
     c                   eval      x = 1
     c                   eval      y = 1
     c                   eval      w_tmp1 = *blanks
     c                   eval      w_tmp2 = *blanks
     c                   eval      w_str_ut = *blanks
     c                   movea     w_str_inn     w_tmp1
      * Først nullstilles foranstilte 0'er
     c     x             doueq     50
     c                   if        w_tmp1(x) = '0' and
     c                             b_null = *on
     c                   move      ' '           w_tmp1(x)
     c                   else
     c                   eval      b_null = *off
     c                   endif
     c                   add       1             x
     c                   enddo
      * Deretter fjernes foranstilte blanke
     c                   eval      x = 1
     c                   eval      y = 1
     c     x             doueq     50
     c                   if        w_tmp1(x) <> *blank
     c                   move      w_tmp1(x)     w_tmp2(y)
     c                   add       1             y
     c                   endif
     c                   add       1             x
     c                   enddo
      * Legg over "trimmet" nøkkel
     c                   movea     w_tmp2        w_str_ut
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    w_prec
     c                   parm                    p_post
     c                   eval      p_prec = w_prec
      *
      * Key for posisjonering på korttype
      *
     c     rukrl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rukrl5_ktyp
      *
      * Key for posisjonering på korttype
      *
     c     ruksl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ruksl1_skty
     c                   kfld                    ruksl1_slag
      *
      * Key for les av historisk fakturaregister - hode
      *
     c     sohelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sohelr_aarr
     c                   kfld                    sohelr_binr
     c                   kfld                    sohelr_numm
     c                   kfld                    sohelr_suff
      *
      * Key for les av historisk fakturaregister - hode
      *
     c     sohell_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sohelr_aarr
     c                   kfld                    sohelr_binr
      *
      * Key for les av historisk fakturaregister - linjer
      *
     c     sodtlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sodtlr_aarr
     c                   kfld                    sodtlr_binr
      *
      * Key for les på bongregister
      *
     c     bcdtir_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bcdtir_bong
      *
      * Key for setll på bongregister
      *
     c     bcdtll_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bcdtll_bong
     c                   kfld                    bcdtll_boli
      *
      * Key for les på bongregister
      *
     c     bchei1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bchei1_bong
      *
      * Key for les av avsatte korttransaksjoner
      *
     c     bokkll_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bokkll_ktyp
     c                   kfld                    bokkll_kund
     c                   kfld                    bokkll_odat
      *
      * Key for les av avsatte korttransaksjoner
      *
     c     bokkl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bokkl5_ktyp
     c                   kfld                    bokkl5_kund
6.21  *
6.21  * Key for les av avsatte korttransaksjoner
6.21  *
6.21 c     bohel1_key    klist
6.21 c                   kfld                    w_firm
6.21 c                   kfld                    bohel1_aarr
6.21 c                   kfld                    bohel1_wsid
6.21 c                   kfld                    bohel1_bong
6.21  *
6.21  * Key for oppslag selgerkode
6.21  *
6.21 c     ra09l1_key    klist
6.21 c                   kfld                    w_firm
6.21 c                   kfld                    ra09l1_ikod
      *
      * Legger inn verdier fra parameter
      *
     c                   eval      w_firm = l_firm
     c                   move      p_ktyp        rukrl5_ktyp
     c**   *dmy          move      p_frda        w_frda
     c**   *dmy          move      p_tida        w_tida
     c                   eval      w_frda = p_frda
     c                   eval      w_tida = p_tida
     c                   time                    w_ddat
      *
     c                   endsr
