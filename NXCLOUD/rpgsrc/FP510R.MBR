     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FP510R
      * Beskrivelse . . : Spesialpris, vise for kunde/kundeprosjekt
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       040400 HKO  Nytt program
      *       180904 HKO  Lagt inn avtalenotat
5.70  * PRGR  131108 bof  Utvidet med prisgruppe
6.nu  * R6.30 030614 bsa  Programmet gjennomgått mtp nummerutvidelser.
7.xx  * 7.00  300316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
8.01  *PRG2   090622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffprilr    if   e           k disk    rename(fpripfr:fprilrr)
     ffpril1    if   e           k disk    rename(fpripfr:fpril1r)
     ffpril2    if   e           k disk    rename(fpripfr:fpril2r)
     ffpril3    if   e           k disk    rename(fpripfr:fpril3r)
     ffpril4    if   e           k disk    rename(fpripfr:fpril4r)
     ffpril5    if   e           k disk    rename(fpripfr:fpril5r)
     ffpril6    if   e           k disk    rename(fpripfr:fpril6r)
     ffpril7    if   e           k disk    rename(fpripfr:fpril7r)
     ffpril8    if   e           k disk    rename(fpripfr:fpril8r)
5.70 ffpril9    if   e           k disk    rename(fpripfr:fpril9r)
     fra06pf    if   e           k disk    rename(ra06pfr:ra06pfr)
     fra11pf    if   e           k disk    rename(ra11pfr:ra11pfr)
5.70 fra30lr    if   e           k disk    rename(ra30pfr:ra30lrr)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvenhl1    if   e           k disk    rename(venhpfr:venhl1r)
     fvltpl1    if   e           k disk    rename(vltppfr:vltpl1r)
      *
     ffp510d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av parametre
      *
     d p_firm          s                   like(fpfirm)
     d p_kund          s                   like(fpkund)
     d p_kpro          s                   like(fpkpro)
      *
      * Definering av variable i nøkler
      *
5.70 d fprilr_prgr     s                   like(fpprgr)
     d fprilr_rkat     s                   like(fprkat)
     d fprilr_kkat     s                   like(fpkkat)
     d fprilr_kund     s                   like(fpkund)
     d fprilr_kpro     s                   like(fpkpro)
     d fprilr_vare     s                   like(fpvare)
     d fprilr_enhe     s                   like(fpenhe)
     d fprilr_lety     s                   like(fplety)
5.70 d fpril1_prgr     s                   like(fpprgr)
     d fpril1_rkat     s                   like(fprkat)
     d fpril1_kkat     s                   like(fpkkat)
     d fpril1_kund     s                   like(fpkund)
     d fpril1_kpro     s                   like(fpkpro)
     d fpril1_vare     s                   like(fpvare)
     d fpril1_enhe     s                   like(fpenhe)
     d fpril1_lety     s                   like(fplety)
     d fpril2_kkat     s                   like(fpkkat)
     d fpril3_kund     s                   like(fpkund)
     d fpril3_kpro     s                   like(fpkpro)
     d fpril4_kpro     s                   like(fpkpro)
     d fpril5_vare     s                   like(fpvare)
     d fpril5_prgr     s                   like(fpprgr)
     d fpril5_enhe     s                   like(fpenhe)
     d fpril5_lety     s                   like(fplety)
     d fpril6_enhe     s                   like(fpenhe)
     d fpril6_lety     s                   like(fplety)
     d fpril7_lety     s                   like(fplety)
     d fpril8_numm     s                   like(fpnumm)
5.70 d fpril9_rkat     s                   like(fprkat)
     d ra06pf_fkod     s                   like(rafkod)
     d ra11pf_kkod     s                   like(rakkod)
5.70 d ra30lr_dkod     s                   like(redkod)
     d rkunl1_kund     s                   like(rkkund)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
     d vvarl1_vare     s                   like(vvvare)
     d venhl1_eenh     s                   like(vaeenh)
     d vltpl1_lety     s                   like(vylety)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
      *
     d b_inlr          s              1    inz(*off)
     d w_udat          s               d   datfmt(*iso)
      *
     d w_firm          s                   like(fpfirm)
5.70 d w_prgr          s                   like(fpprgr)
     d w_rkat          s                   like(fprkat)
     d w_kkat          s                   like(fpkkat)
     d w_kund          s                   like(fpkund)
     d w_kpro          s                   like(fpkpro)
     d w_rtxt          s                   like(raftxt)
     d w_ktxt          s                   like(raktxt)
5.70 d w_dtxt          s                   like(redtxt)
     d w_vare          s                   like(fpvare)
     d w_knav          s                   like(rknavn)
     d w_vtxt          s                   like(vvtek1)
     d w_etxt          s                   like(vaetxt)
     d w_ltxt          s                   like(vybesk)
     d w_pnav          s                   like(flnavn)
     d w_sapr          s                   like(fpsapr)
     d w_bupr          s                   like(fpsapr)
     d w_kopr          s                   like(fpkopr)
     d w_inpr          s                   like(fpkopr)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(10)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile                                  DSP SFL
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2tagb
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
5.70 c                   if        b2prgr <> *blank or
     c                             b2rkat <> 0 or
     c                             b2kkat <> 0 or
     c                             b2kund <> 0 or
     c                             b2kpro <> 0 or
     c                             b2vare <> *blank or
     c                             b2enhe <> *blank or
     c                             b2lety <> 0 or
     c                             b2numm <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'A'
     c                   eval      fpril2_kkat = b1kkat
     c     fpril2_key    setll     fpril2
     c                   when      w_seqe = 'K'
     c                   eval      fpril3_kund = b1kund
     c                   eval      fpril3_kpro = b1kpro
     c     fpril3_key    setll     fpril3
     c                   when      w_seqe = 'P'
     c                   eval      fpril4_kpro = b1kpro
     c     fpril4_key    setll     fpril4
     c                   when      w_seqe = 'V'
     c                   eval      fpril5_vare = b1vare
     c                   eval      fpril5_prgr = b1prgr
     c                   eval      fpril5_enhe = b1enhe
     c                   eval      fpril5_lety = b1lety
     c     fpril5_key    setll     fpril5
     c                   when      w_seqe = 'E'
     c                   eval      fpril6_enhe = b1enhe
     c                   eval      fpril6_lety = b1lety
     c     fpril6_key    setll     fpril6
     c                   when      w_seqe = 'L'
     c                   eval      fpril7_lety = b1lety
     c     fpril7_key    setll     fpril7
     c                   when      w_seqe = 'O'
     c                   eval      fpril8_numm = b1numm
     c     fpril8_key    setll     fpril8
5.70 c                   when      w_seqe = 'R'
5.70 c                   eval      fpril9_rkat = b1rkat
5.70 c     fpril9_key    setll     fpril9
     c                   other
5.70 c                   eval      fpril1_prgr = b1prgr
     c                   eval      fpril1_rkat = b1rkat
     c                   eval      fpril1_kkat = b1kkat
     c                   eval      fpril1_kund = b1kund
     c                   eval      fpril1_kpro = b1kpro
     c                   eval      fpril1_vare = b1vare
     c                   eval      fpril1_enhe = b1enhe
     c                   eval      fpril1_lety = b1lety
     c     fpril1_key    setll     fpril1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på rabattkategori
      *
     c                   if        b2rkat <> 0
     c                   eval      w_seqe = *blank
5.70 c                   eval      fpril1_prgr = b2prgr
     c                   eval      fpril1_rkat = b2rkat
     c                   eval      fpril1_kkat = b2kkat
     c                   eval      fpril1_kund = b2kund
     c                   eval      fpril1_kpro = b2kpro
     c                   eval      fpril1_vare = b2vare
     c                   eval      fpril1_enhe = b2enhe
     c                   eval      fpril1_lety = b2lety
     c     fpril1_key    setll     fpril1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på kundekategori
      *
     c                   if        b2kkat <> 0
     c                   eval      w_seqe = 'A'
     c                   eval      fpril2_kkat = b2kkat
     c     fpril2_key    setll     fpril2
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på kunde
      *
     c                   if        b2kund <> 0
     c                   eval      w_seqe = 'K'
     c                   eval      fpril3_kund = b2kund
     c                   eval      fpril3_kpro = b2kpro
     c     fpril3_key    setll     fpril3
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på kundeprosjekt
      *
     c                   if        b2kpro <> 0
     c                   eval      w_seqe = 'P'
     c                   eval      fpril4_kpro = b2kpro
     c     fpril4_key    setll     fpril4
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på vare
      *
     c                   if        b2vare <> *blank
     c                   eval      w_seqe = 'V'
     c                   eval      fpril5_vare = b2vare
     c                   eval      fpril5_prgr = b2prgr
     c                   eval      fpril5_enhe = b2enhe
     c                   eval      fpril5_lety = b2lety
     c     fpril5_key    setll     fpril5
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på enhet
      *
     c                   if        b2enhe <> *blank
     c                   eval      w_seqe = 'E'
     c                   eval      fpril6_enhe = b2enhe
     c                   eval      fpril6_lety = b2lety
     c     fpril6_key    setll     fpril6
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på leveringstype
      *
     c                   if        b2lety <> 0
     c                   eval      w_seqe = 'L'
     c                   eval      fpril7_lety = b2lety
     c     fpril7_key    setll     fpril7
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på ordrenummer
      *
     c                   if        b2numm <> 0
     c                   eval      w_seqe = 'O'
     c                   eval      fpril8_numm = b2numm
     c     fpril8_key    setll     fpril8
     c                   goto      end_pos
     c                   endif
5.70  *
5.70  * Posisjoner startverdi på rabattkategori
5.70  *
5.70 c                   if        b2prgr <> *blank
5.70 c                   eval      w_seqe = 'R'
5.70 c                   eval      fpril9_rkat = b2rkat
5.70 c     fpril9_key    setll     fpril9
5.70 c                   goto      end_pos
5.70 c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
5.70 c                   eval      b2prgr = *blank
     c                   eval      b2rkat = 0
     c                   eval      b2kkat = 0
     c                   eval      b2kund = 0
     c                   eval      b2kpro = 0
     c                   eval      b2vare = *blank
     c                   eval      b2enhe = *blank
     c                   eval      b2lety = 0
     c                   eval      b2numm = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Vis
      *
     c                   if        b1valg = 5
5.70 c                   eval      c2prgr = b1prgr
     c                   eval      c2rkat = b1rkat
     c                   eval      c2kkat = b1kkat
     c                   eval      c2kund = b1kund
     c                   eval      c2kpro = b1kpro
     c                   eval      c2vare = b1vare
     c                   eval      c2enhe = b1enhe
     c                   eval      c2lety = b1lety
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Avtalenotat
      *
     c                   if        b1valg = 7
     c                   eval      w_vare = b1vare
     c                   call      'FP150R'
     c                   parm                    w_firm
     c                   parm                    b1kund
     c                   parm                    b1kpro
     c                   parm                    w_vare
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * C2BLD Behandle bilde for ny - endring - vise
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
5.70 c                   eval      fprilr_prgr = c2prgr
     c                   eval      fprilr_rkat = c2rkat
     c                   eval      fprilr_kkat = c2kkat
     c                   eval      fprilr_kund = c2kund
     c                   eval      fprilr_kpro = c2kpro
     c                   eval      fprilr_vare = c2vare
     c                   eval      fprilr_enhe = c2enhe
     c                   eval      fprilr_lety = c2lety
     c     fprilr_key    chain     fprilr
      *
     c                   if        not %found
     c                   leavesr
     c                   endif
      *
     c                   eval      c2sapr = fpsapr
     c                   eval      c2rab1 = fprab1
     c                   eval      c2rab2 = fprab2
     c                   eval      c2kopr = fpkopr
     c                   eval      c2fdat = 0
     c                   eval      c2tdat = 0
     c                   eval      c2numm = fpnumm
     c                   eval      c2eusr = fpeusr
      *
     c                   if        fpfdat <> *loval
     c     *dmy          move      fpfdat        c2fdat
     c                   endif
      *
     c                   if        fptdat <> *loval
     c     *dmy          move      fptdat        c2tdat
     c                   endif
      *
     c     *dmy          move      fpedat        c2edat
      *
     c                   eval      c2dekn = 0
      *
      * Henter beskrivelse av nivå
      *
     c                   exsr      hent_info
      *
      * Kalkuler DG
      *
     c                   exsr      kalk_dg
      *
      * Behandler bildet
      *
     c                   exfmt     c2bld
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Fyll opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'A'
     c                   read      fpril2
     c                   when      w_seqe = 'K'
     c                   read      fpril3
     c                   when      w_seqe = 'P'
     c                   read      fpril4
     c                   when      w_seqe = 'V'
     c                   read      fpril5
     c                   when      w_seqe = 'E'
     c                   read      fpril6
     c                   when      w_seqe = 'L'
     c                   read      fpril7
     c                   when      w_seqe = 'O'
     c                   read      fpril8
5.70 c                   when      w_seqe = 'R'
5.70 c                   read      fpril9
     c                   other
     c                   read      fpril1
     c                   endsl
      *
     c                   if        %eof or
     c                             fpfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   if        w_kund <> 0
     c                   if        fprkat <> 0 and
     c                             fprkat <> w_rkat or
     c                             fpkkat <> 0 and
     c                             fpkkat <> w_kkat or
     c                             fpkund <> 0 and
     c                             fpkund <> w_kund
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        w_kpro <> 0 and
     c                             fpkpro <> 0 and
     c                             fpkpro <> w_kpro
     c                   iter
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'A'
     c                   eval      fpril2_kkat = fpkkat
     c                   when      w_seqe = 'K'
     c                   eval      fpril3_kund = fpkund
     c                   eval      fpril3_kpro = fpkpro
     c                   when      w_seqe = 'P'
     c                   eval      fpril4_kpro = fpkpro
     c                   when      w_seqe = 'V'
     c                   eval      fpril5_vare = fpvare
     c                   eval      fpril5_prgr = fpprgr
     c                   eval      fpril5_enhe = fpenhe
     c                   eval      fpril5_lety = fplety
     c                   when      w_seqe = 'E'
     c                   eval      fpril6_enhe = fpenhe
     c                   eval      fpril6_lety = fplety
     c                   when      w_seqe = 'L'
     c                   eval      fpril7_lety = fplety
     c                   when      w_seqe = 'O'
     c                   eval      fpril8_numm = fpnumm
5.70 c                   when      w_seqe = 'O'
5.70 c                   eval      fpril9_rkat = fprkat
     c                   other
5.70 c                   eval      fpril1_prgr = fpprgr
     c                   eval      fpril1_rkat = fprkat
     c                   eval      fpril1_kkat = fpkkat
     c                   eval      fpril1_kund = fpkund
     c                   eval      fpril1_kpro = fpkpro
     c                   eval      fpril1_vare = fpvare
     c                   eval      fpril1_enhe = fpenhe
     c                   eval      fpril1_lety = fplety
     c                   endsl
      *
5.70 c                   eval      b1prgr = fpprgr
     c                   eval      b1rkat = fprkat
     c                   eval      b1kkat = fpkkat
     c                   eval      b1kund = fpkund
     c                   eval      b1kpro = fpkpro
     c                   eval      b1vare = fpvare
     c                   eval      b1tek1 = *blank
     c                   eval      b1enhe = fpenhe
     c                   eval      b1lety = fplety
     c                   eval      b1numm = fpnumm
     c                   eval      b1sapr = fpsapr
     c                   eval      b1rab1 = fprab1
     c                   eval      b1kopr = fpkopr
      *
     c                   eval      vvarl1_vare = fpvare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   eval      b1tek1 = vvtek1
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'A'
     c     fpril2_key    setgt     fpril2
     c                   readp     fpril2
     c                   when      w_seqe = 'K'
     c     fpril3_key    setgt     fpril3
     c                   readp     fpril3
     c                   when      w_seqe = 'P'
     c     fpril4_key    setgt     fpril4
     c                   readp     fpril4
     c                   when      w_seqe = 'V'
     c     fpril5_key    setgt     fpril5
     c                   readp     fpril5
     c                   when      w_seqe = 'E'
     c     fpril6_key    setgt     fpril6
     c                   readp     fpril6
     c                   when      w_seqe = 'L'
     c     fpril7_key    setgt     fpril7
     c                   readp     fpril7
     c                   when      w_seqe = 'O'
     c     fpril8_key    setgt     fpril8
     c                   readp     fpril8
5.70 c                   when      w_seqe = 'R'
5.70 c     fpril9_key    setgt     fpril9
5.70 c                   readp     fpril9
     c                   other
     c     fpril1_key    setgt     fpril1
     c                   readp     fpril1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'A'
     c                   readp     fpril2
     c                   when      w_seqe = 'K'
     c                   readp     fpril3
     c                   when      w_seqe = 'P'
     c                   readp     fpril4
     c                   when      w_seqe = 'V'
     c                   readp     fpril5
     c                   when      w_seqe = 'E'
     c                   readp     fpril6
     c                   when      w_seqe = 'L'
     c                   readp     fpril7
     c                   when      w_seqe = 'O'
     c                   readp     fpril8
5.70 c                   when      w_seqe = 'R'
5.70 c                   readp     fpril9
     c                   other
     c                   readp     fpril1
     c                   endsl
      *
     c                   if        %eof or
     c                             fpfirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   if        w_kund <> 0
     c                   if        fprkat <> 0 and
     c                             fprkat <> w_rkat or
     c                             fpkkat <> 0 and
     c                             fpkkat <> w_kkat or
     c                             fpkund <> 0 and
     c                             fpkund <> w_kund
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        w_kpro <> 0 and
     c                             fpkpro <> 0 and
     c                             fpkpro <> w_kpro
     c                   iter
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'A'
     c                   eval      fpril2_kkat = fpkkat
     c                   when      w_seqe = 'K'
     c                   eval      fpril3_kund = fpkund
     c                   eval      fpril3_kpro = fpkpro
     c                   when      w_seqe = 'P'
     c                   eval      fpril4_kpro = fpkpro
     c                   when      w_seqe = 'V'
     c                   eval      fpril5_vare = fpvare
     c                   eval      fpril5_prgr = fpprgr
     c                   eval      fpril5_enhe = fpenhe
     c                   eval      fpril5_lety = fplety
     c                   when      w_seqe = 'E'
     c                   eval      fpril6_enhe = fpenhe
     c                   eval      fpril6_lety = fplety
     c                   when      w_seqe = 'L'
     c                   eval      fpril7_lety = fplety
     c                   when      w_seqe = 'O'
     c                   eval      fpril8_numm = fpnumm
5.70 c                   when      w_seqe = 'R'
5.70 c                   eval      fpril9_rkat = fprkat
     c                   other
5.70 c                   eval      fpril1_prgr = fpprgr
     c                   eval      fpril1_rkat = fprkat
     c                   eval      fpril1_kkat = fpkkat
     c                   eval      fpril1_kund = fpkund
     c                   eval      fpril1_kpro = fpkpro
     c                   eval      fpril1_vare = fpvare
     c                   eval      fpril1_enhe = fpenhe
     c                   eval      fpril1_lety = fplety
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'A'
     c     fpril2_key    setll     fpril2
     c                   when      w_seqe = 'K'
     c     fpril3_key    setll     fpril3
     c                   when      w_seqe = 'P'
     c     fpril4_key    setll     fpril4
     c                   when      w_seqe = 'V'
     c     fpril5_key    setll     fpril5
     c                   when      w_seqe = 'E'
     c     fpril6_key    setll     fpril6
     c                   when      w_seqe = 'L'
     c     fpril7_key    setll     fpril7
     c                   when      w_seqe = 'O'
     c     fpril8_key    setll     fpril8
5.70 c                   when      w_seqe = 'R'
5.70 c     fpril9_key    setll     fpril9
     c                   other
     c     fpril1_key    setll     fpril1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
5.70  *
5.70  * Prisgruppe
5.70  *
5.70 c                   if        w_afld = 'B2PRGR'
5.70 c                   call      'RA530R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    b2prgr
5.70 c                   parm                    w_dtxt
5.70 c                   leavesr
5.70 c                   endif
      *
      * Rabattkategori
      *
     c                   if        w_afld = 'B2RKAT'
     c                   call      'RA506R'
     c                   parm                    w_firm
     c                   parm                    b2rkat
     c                   parm                    w_rtxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Kundekategori
      *
     c                   if        w_afld = 'B2KKAT'
     c                   call      'RA511R'
     c                   parm                    w_firm
     c                   parm                    b2kkat
     c                   parm                    w_ktxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Kunde
      *
     c                   if        w_afld = 'B2KUND'
     c                   eval      b2kpro = 0
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    b2kund
     c                   parm                    w_knav
     c                   goto      end_spØrr
     c                   endif
      *
      * Kundeprosjekt
      *
     c                   if        w_afld = 'B2KPRO'
     c                   call      'FL500R'
     c                   parm                    w_firm
     c                   parm                    b2kund
     c                   parm                    b2kpro
     c                   parm                    w_knav
     c                   parm                    w_pnav
     c                   goto      end_spØrr
     c                   endif
      *
      * Varenummer
      *
     c                   if        w_afld = 'B2VARE'
     c                   eval      w_vare = b2vare
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_vtxt
     c                   eval      b2vare = w_vare
     c                   goto      end_spØrr
     c                   endif
      *
      * Enhet
      *
     c                   if        w_afld = 'B2ENHE'
     c                   if        b2vare <> *blank
     c                   eval      w_vare = b2vare
     c                   call      'VE550R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    b2enhe
     c                   parm                    w_etxt
     c                   else
     c                   call      'VA510R'
     c                   parm                    w_firm
     c                   parm                    b2enhe
     c                   parm                    w_etxt
     c                   endif
     c                   goto      end_spØrr
     c                   endif
      *
      * Leveringstype
      *
     c                   if        w_afld = 'B2LETY'
     c                   call      'VY500R'
     c                   parm                    w_firm
     c                   parm                    b2lety
     c                   parm                    w_ltxt
     c                   goto      end_spØrr
     c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter beskrivelse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_info     begsr
      *
     c                   eval      c2rtxt = *blank
     c                   eval      c2ktxt = *blank
     c                   eval      c2knav = *blank
     c                   eval      c2pnav = *blank
     c                   eval      c2vtxt = *blank
     c                   eval      c2etxt = *blank
     c                   eval      c2ltxt = *blank
     c                   eval      c2ptxt = *blank
5.70  *
5.70  * Prisgruppe
5.70  *
5.70 c                   if        c2prgr <> *blank
5.70 c                   eval      ra30lr_dkod = c2prgr
5.70 c     ra30lr_key    chain     ra30lr
5.70 c                   if        %found
5.70 c                   eval      c2ptxt = redtxt
5.70 c                   endif
5.70 c                   endif
      *
      * Rabattkategori
      *
     c                   if        c2rkat <> 0
     c                   eval      ra06pf_fkod = c2rkat
     c     ra06pf_key    chain     ra06pf                             91
     c                   if        *in91 = *off
     c                   eval      c2rtxt = raftxt
     c                   endif
     c                   endif
      *
      * Kundekategori
      *
     c                   if        c2kkat <> 0
     c                   eval      ra11pf_kkod = c2kkat
     c     ra11pf_key    chain     ra11pf                             91
     c                   if        *in91 = *off
     c                   eval      c2ktxt = raktxt
     c                   endif
     c                   endif
      *
      * Kunde
      *
     c                   if        c2kund <> 0
     c                   eval      rkunl1_kund = c2kund
     c     rkunl1_key    chain     rkunl1                             91
     c                   if        *in91 = *off
     c                   eval      c2knav = rknavn
     c                   endif
     c                   endif
      *
      * Prosjekt
      *
     c                   if        c2kpro <> 0
     c                   eval      fkprl1_kund = c2kund
     c                   eval      fkprl1_kpro = c2kpro
     c     fkprl1_key    chain     fkprl1                             91
     c                   if        *in91 = *off
     c                   eval      c2pnav = flnavn
     c                   endif
     c                   endif
      *
      * Varenummer
      *
     c                   if        c2vare <> *blank
     c                   eval      vvarl1_vare = c2vare
     c     vvarl1_key    chain     vvarl1                             91
     c                   if        *in91 = *off
     c                   eval      c2vtxt = vvtek1
     c                   endif
     c                   endif
      *
      * Enhet
      *
     c                   if        c2enhe <> *blank
     c                   eval      venhl1_eenh = c2enhe
     c     venhl1_key    chain     venhl1                             91
     c                   if        *in91 = *off
     c                   eval      c2etxt = vaetxt
     c                   endif
     c                   endif
      *
      * Leveringstype
      *
     c                   if        c2lety <> 0
     c                   eval      vltpl1_lety = c2lety
     c     vltpl1_key    chain     vltpl1                             91
     c                   if        *in91 = *off
     c                   eval      c2ltxt = vybesk
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Kalkuler DG for spesialpris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kalk_dg       begsr
      *
     c                   eval      c2dekn = 0
      *
      * Dersom salgspris eller kostpris ikke er utfylt, kalles program som
      * henter priser på vare
      *
     c                   if        c2sapr = 0 or
     c                             c2kopr = 0
      *
     c                   eval      w_vare = c2vare
     c                   eval      b_inlr = *off
      *
     c                   call      'VP701R'
     c                   parm                    w_firm
     c                   parm                    w_vare
5.70 c                   parm                    c2prgr
     c                   parm                    c2enhe
     c                   parm                    c2lety
     c                   parm                    w_udat
     c                   parm                    b_inlr
     c                   parm                    w_sapr
     c                   parm                    w_bupr
     c                   parm                    w_kopr
     c                   parm                    w_inpr
      *
     c                   endif
      *
      * Bestemmer salgspris og kostpris
      *
     c                   if        c2sapr <> 0
     c                   eval      w_sapr = c2sapr
     c                   endif
      *
     c                   if        c2kopr <> 0
     c                   eval      w_kopr = c2kopr
     c                   endif
      *
      * Finner salgspris etter rabatt
      *
     c                   if        c2rab1 <> 0
     c                   eval(h)   w_sapr = w_sapr - (w_sapr * c2rab1 / 100)
     c                   endif
      *
     c                   if        c2rab2 <> 0
     c                   eval(h)   w_sapr = w_sapr - (w_sapr * c2rab2 / 100)
     c                   endif
      *
      * Beregner DG
      *
     c                   if        w_sapr <> 0
     c                   select
     c                   when      (100 - (w_kopr * 100 / w_sapr)) < -999,99
     c                   eval      c2dekn = -999,99
     c                   when      (100 - (w_kopr * 100 / w_sapr)) > 999,99
     c                   eval      c2dekn = 999,99
     c                   other
     c                   eval(h)   c2dekn = 100 - (w_kopr * 100 / w_sapr)
     c                   endsl
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_kund
     c                   parm                    p_kpro
      *
      * Key for oppslag på spesialpriser
      *
     c     fprilr_key    klist
     c                   kfld                    w_firm
5.70 c                   kfld                    fprilr_prgr
     c                   kfld                    fprilr_rkat
     c                   kfld                    fprilr_kkat
     c                   kfld                    fprilr_kund
     c                   kfld                    fprilr_kpro
     c                   kfld                    fprilr_vare
     c                   kfld                    fprilr_enhe
     c                   kfld                    fprilr_lety
      *
      * Key for posisjonering på rabattkategori
      *
     c     fpril1_key    klist
     c                   kfld                    w_firm
5.70 c                   kfld                    fpril1_prgr
     c                   kfld                    fpril1_rkat
     c                   kfld                    fpril1_kkat
     c                   kfld                    fpril1_kund
     c                   kfld                    fpril1_kpro
     c                   kfld                    fpril1_vare
     c                   kfld                    fpril1_enhe
     c                   kfld                    fpril1_lety
      *
      * Key for posisjonering på kundekategori
      *
     c     fpril2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fpril2_kkat
      *
      * Key for posisjonering på kunde
      *
     c     fpril3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fpril3_kund
     c                   kfld                    fpril3_kpro
      *
      * Key for posisjonering på kundeprosjekt
      *
     c     fpril4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fpril4_kpro
      *
      * Key for posisjonering på varenummer
      *
     c     fpril5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fpril5_vare
     c                   kfld                    fpril5_prgr
     c                   kfld                    fpril5_enhe
     c                   kfld                    fpril5_lety
      *
      * Key for posisjonering på enhet
      *
     c     fpril6_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fpril6_enhe
     c                   kfld                    fpril6_lety
      *
      * Key for posisjonering på leveringstype
      *
     c     fpril7_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fpril7_lety
      *
      * Key for posisjonering på ordrenummer
      *
     c     fpril8_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fpril8_numm
5.70  *
5.70  * Key for posisjonering på rabattkategori
5.70  *
5.70 c     fpril9_key    klist
5.70 c                   kfld                    w_firm
5.70 c                   kfld                    fpril9_rkat
      *
      * Key for oppslag på rabattkategori
      *
     c     ra06pf_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra06pf_fkod
      *
      * Key for oppslag på kundekategori
      *
     c     ra11pf_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra11pf_kkod
5.70  *
5.70  * Key for oppslag på prisgruppe
5.70  *
5.70 c     ra30lr_key    klist
5.70 c                   kfld                    w_firm
5.70 c                   kfld                    ra30lr_dkod
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på enhet
      *
     c     venhl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    venhl1_eenh
      *
      * Key for oppslag på leveringstype
      *
     c     vltpl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltpl1_lety
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   eval      w_kund = p_kund
     c                   eval      w_kpro = p_kpro
     c                   eval      w_rkat = 0
     c                   eval      w_kkat = 0
      *
      * Henter overstyringer fra kunde
      *
     c                   if        w_kund <> 0
     c                   eval      rkunl1_kund = w_kund
     c     rkunl1_key    chain     rkunl1                             90
     c                   if        *in90 = *off
     c                   eval      w_rkat = rkrabk
     c                   eval      w_kkat = rkkatg
     c                   endif
     c                   endif
      *
      * Henter overstyringer fra kundeprosjekt
      *
     c                   if        w_kpro <> 0
      *
     c                   eval      fkprl1_kund = w_kund
     c                   eval      fkprl1_kpro = w_kpro
     c     fkprl1_key    chain     fkprl1                             90
     c                   if        *in90 = *off
      *
     c                   if        florka <> 0
     c                   eval      w_rkat = florka
     c                   endif
      *
     c                   if        flokun <> 0
     c                   eval      w_kund = flokun
     c                   eval      w_kpro = 0
     c                   endif
      *
     c                   if        flokpr <> 0
     c                   eval      w_kpro = flokpr
     c                   endif
      *
     c                   if        flokun <> 0
     c                   eval      rkunl1_kund = flokun
     c     rkunl1_key    chain     rkunl1                             91
     c                   if        *in91 = *off
     c                   eval      w_rkat = rkrabk
     c                   eval      w_kkat = rkkatg
     c                   endif
     c                   endif
      *
     c                   endif
     c                   endif
      *
      * Initierer skjermbilde
      *
     c                   eval      *in22 = *on
      *
     c                   eval      b2rka1 = w_rkat
     c                   eval      b2kka1 = w_kkat
     c                   eval      b2kun1 = w_kund
     c                   eval      b2kpr1 = w_kpro
      *
     c                   if        b2rka1 <> 0
     c                   eval      ra06pf_fkod = b2rka1
     c     ra06pf_key    chain     ra06pf                             91
     c                   if        *in91 = *off
     c                   eval      b2rtxt = raftxt
     c                   endif
     c                   endif
      *
     c                   if        b2kka1 <> 0
     c                   eval      ra11pf_kkod = b2kka1
     c     ra11pf_key    chain     ra11pf                             91
     c                   if        *in91 = *off
     c                   eval      b2ktxt = raktxt
     c                   endif
     c                   endif
      *
     c                   if        b2kun1 <> 0
     c                   eval      rkunl1_kund = b2kun1
     c     rkunl1_key    chain     rkunl1                             91
     c                   if        *in91 = *off
     c                   eval      b2knav = rknavn
     c                   endif
     c                   endif
      *
     c                   if        b2kpr1 <> 0
     c                   eval      fkprl1_kund = b2kun1
     c                   eval      fkprl1_kpro = b2kpr1
     c     fkprl1_key    chain     fkprl1                             91
     c                   if        *in91 = *off
     c                   eval      b2pnav = flnavn
     c                   endif
     c                   endif
      *
      * Henter dagens dato
      *
     c                   time                    w_udat
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c     fpril1_key    setll     fpril1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
