     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VG102R
      * Beskrivelse . . : Undergruppe, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       140199 HKO  Nytt program
      *       040400 HKO  Rettet feil med at 'sats for påslag' og varenummer
      *                   ikke ble oppdatert
      *       111000 KOB  Lagt inn vedlikehold av produktansvarlig
      *       301104 HKO  Lagt inn vedlikehold av prissammenligningsenheter
      * 6.10  290609 BSA  Oppdatert med sporingsinformasjon
6.20  * 6.20  150311 NHO  Vise undergr./transport i ordrereg
      *
7.xx  * 7.00  040216 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 7.00  040521 bsa  NXS-8528 utvidet med unspsc
      *
8.01  *K0000  141124 bsa  Justert etter utvidelse.
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fvugrl2    if   e           k disk    rename(vugrpfr:vugrl2r)
     fvugrlr    if   e           k disk    rename(vugrpfr:vugrlrr)
     fvugrlu    uf a e           k disk    rename(vugrpfr:vugrlur)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvkhvl1    if   e           k disk    rename(vkhvpfr:vkhvl1r)
     fvenhl1    if   e           k disk    rename(venhpfr:venhl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fra26l1    if   e           k disk    rename(ra26pfr:ra26l1r)
      *
     fvg102d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vgufir)
     d p_ogrp          s                   like(vguogr)
     d p_hgrp          s                   like(vguhgr)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d vugrl1_uugr     s                   like(vguugr)
     d vugrl2_utxt     s                   like(vgutxt)
     d vugrlr_uugr     s                   like(vguugr)
     d vugrlu_uugr     s                   like(vguugr)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vkhvl1_kkod     s                   like(vakkod)
     d venhl1_eenh     s                   like(vaeenh)
     d vvarl1_vare     s                   like(vvvare)
     d ra26l1_zsak     s                   like(razsak)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(vgufir)
     d w_ogrp          s                   like(vguogr)
     d w_hgrp          s                   like(vguhgr)
     d w_ktxt          s                   like(vaktxt)
     d w_ztxt          s                   like(raztxt)
     d w_etxt          s                   like(vaetxt)
     d w_vare          s                   like(vvvare)
     d w_vtxt          s                   like(vvtek1)
      *
     d s_uugr          s                   like(vguugr)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(10)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meleingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2uugr <> 0 or
     c                             b2utxt <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      vugrl2_utxt = b1utxt
     c     vugrl2_key    setll     vugrl2
     c                   other
     c                   eval      vugrl1_uugr = b1uugr
     c     vugrl1_key    setll     vugrl1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på undergruppe
      *
     c                   if        b2uugr <> 0
     c                   eval      w_seqe = ' '
     c                   eval      vugrl1_uugr = b2uugr
     c     vugrl1_key    setll     vugrl1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på beskrivelse
      *
     c                   if        b2utxt <> *blank
     c                   eval      w_seqe = 'B'
     c                   eval      vugrl2_utxt = b2utxt
     c     vugrl2_key    setll     vugrl2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2uugr = 0
     c                   eval      b2utxt = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1uugr = b1uugr
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1uugr = b1uugr
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1uugr = b1uugr
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1uugr = b1uugr
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1uogr = w_ogrp
     c                   eval      c1otxt = b2otxt
     c                   eval      c1uhgr = w_hgrp
     c                   eval      c1htxt = b2htxt
      *
     c                   eval      c1uugr = b2uugr
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
     c                   if        c1uugr = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      vugrlr_uugr = c1uugr
     c     vugrlr_key    chain     vugrlr                             90
     c                   if        *in90 = *off
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1uugr = 0
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      c1uugr = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      c1uogr = w_ogrp
     c                   eval      c1otxt = b2otxt
     c                   eval      c1uhgr = w_hgrp
     c                   eval      c1htxt = b2htxt
     c                   eval      c2ktxt = *blank
     c                   eval      c2ztxt = *blank
      *
     c                   eval      vugrlr_uugr = c1uugr
     c     vugrlr_key    chain     vugrlr                             90
      *
     c                   if        *in90 = *off
     c                   eval      c2utxt = vgutxt
     c                   eval      c2ukhv = vgukhv
     c                   eval      c2upra = vgupra
     c                   eval      c2uens = vguens
     c                   eval      c2uenl = vguenl
     c                   eval      c2uenp = vguenp
     c                   eval      c2uep1 = vguep1
     c                   eval      c2uep2 = vguep2
     c                   eval      c2udek = vgudek
     c                   eval      c2upra = vgupra
     c                   eval      c2uakt = vguakt
     c                   eval      c2uavr = vguavr
     c                   eval      c2ulag = vgulag
7.01 c                   eval      c2ufri = vgufri
6.20 c                   eval      c2utra = vgutra
     c                   eval      c2usts = vgusts
     c                   eval      c2ueus = vgueus
     c                   movel     vguvar        c2uvar
     c     *dmy          move      vgueda        c2ueda
      *
     c                   if        vgokhv <> *blank
     c                   eval      vkhvl1_kkod = vgokhv
     c     vkhvl1_key    chain     vkhvl1                             91
     c                   if        *in91 = *off
     c                   eval      c2ktxt = vaktxt
     c                   endif
     c                   endif
      *
     c                   if        vgupra <> *blank
     c                   eval      ra26l1_zsak = vgupra
     c     ra26l1_key    chain     ra26l1                             91
     c                   if        *in91 = *off
     c                   eval      c2ztxt = raztxt
     c                   endif
     c                   endif
      *
     c                   else
     c                   eval      c2utxt = *blank
     c                   eval      c2ukhv = *blank
     c                   eval      c2ktxt = *blank
     c                   eval      c2upra = *blank
     c                   eval      c2ztxt = *blank
     c                   eval      c2uens = *blank
     c                   eval      c2uenl = *blank
     c                   eval      c2uenp = *blank
     c                   eval      c2uep1 = *blank
     c                   eval      c2uep2 = *blank
     c                   eval      c2udek = 0
     c                   eval      c2upra = *blank
     c                   eval      c2uakt = 0
     c                   eval      c2uavr = 0
     c                   eval      c2ulag = 0
6.20 c                   eval      c2utra = 0
     c                   eval      c2usts = 0
     c                   eval      c2uvar = 0
     c                   eval      c2ueus = *blank
     c                   eval      c2ueda = 0
     c                   eval      c2ueus = *blank
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1uugr = k1uugr
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd = *on
      *
     c                   if        w_afld = 'C2UKHV'
     c                   call      'VA520R'
     c                   parm                    w_firm
     c                   parm                    c2ukhv
     c                   parm                    w_ktxt
     c                   eval      c2ktxt = w_ktxt
     c                   goto      c2taga
     c                   endif
      *
     c                   if        w_afld = 'C2UPRA'
     c                   call      'RA526R'
     c                   parm                    w_firm
     c                   parm                    c2upra
     c                   parm                    w_ztxt
     c                   eval      c2ztxt = w_ztxt
     c                   goto      c2taga
     c                   endif
      *
      *
     c                   if        w_afld = 'C2UENS'
     c                   call      'VA510R'
     c                   parm                    w_firm
     c                   parm                    c2uens
     c                   parm                    w_etxt
     c                   goto      c2taga
     c                   endif
      *
     c                   if        w_afld = 'C2UENL'
     c                   call      'VA510R'
     c                   parm                    w_firm
     c                   parm                    c2uenl
     c                   parm                    w_etxt
     c                   goto      c2taga
     c                   endif
      *
     c                   if        w_afld = 'C2UENP'
     c                   call      'VA510R'
     c                   parm                    w_firm
     c                   parm                    c2uenp
     c                   parm                    w_etxt
     c                   goto      c2taga
     c                   endif
      *
     c                   if        w_afld = 'C2UEP1'
     c                   call      'VA510R'
     c                   parm                    w_firm
     c                   parm                    c2uep1
     c                   parm                    w_etxt
     c                   goto      c2taga
     c                   endif
      *
     c                   if        w_afld = 'C2UEP2'
     c                   call      'VA510R'
     c                   parm                    w_firm
     c                   parm                    c2uep2
     c                   parm                    w_etxt
     c                   goto      c2taga
     c                   endif
      *
     c                   if        w_afld = 'C2UVAR'
     c                   movel     c2uvar        w_vare
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_vtxt
     c                   movel     w_vare        c2uvar
     c                   goto      c2taga
     c                   endif
      *
     c                   endif
      *
      * Validering av input
      *
      *  beskrivelse
      *
     c                   if        c2utxt = *blank
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
      *  Kontohenvisning
      *
     c                   if        c2ukhv <> *blank
     c                   eval      vkhvl1_kkod = c2ukhv
     c     vkhvl1_key    chain     vkhvl1                             91
     c                   if        *in91 = *on
     c                   eval      *in32 = *on
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
      *  Produktansvarlig
      *
     c                   if        c2upra <> *blank
     c                   eval      ra26l1_zsak = c2upra
     c     ra26l1_key    chain     ra26l1                             91
     c                   if        *in91 = *on
     c                   eval      *in38 = *on
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
      *  Statistikkenhet
      *
     c                   if        c2uens <> *blank
     c                   eval      venhl1_eenh = c2uens
     c     venhl1_key    chain     venhl1                             91
     c                   if        *in91 = *on
     c                   eval      *in33 = *on
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
      *  Lagerenhet
      *
     c                   if        c2uenl <> *blank
     c                   eval      venhl1_eenh = c2uenl
     c     venhl1_key    chain     venhl1                             91
     c                   if        *in91 = *on
     c                   eval      *in34 = *on
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
      *  Prisbok-enhet
      *
     c                   if        c2uenp <> *blank
     c                   eval      venhl1_eenh = c2uenp
     c     venhl1_key    chain     venhl1                             91
     c                   if        *in91 = *on
     c                   eval      *in35 = *on
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
      *  Prissammenligningsenheter
      *
     c                   if        c2uep1 <> *blank
     c                   eval      venhl1_eenh = c2uep1
     c     venhl1_key    chain     venhl1
     c                   if        not %found
     c                   eval      *in36 = *on
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
     c                   if        c2uep2 <> *blank
     c                   eval      venhl1_eenh = c2uep2
     c     venhl1_key    chain     venhl1
     c                   if        not %found
     c                   eval      *in37 = *on
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
      *  Varenummer
      *
     c                   if        c2uvar <> 0
     c                   movel     c2uvar        vvarl1_vare
     c     vvarl1_key    chain     vvarl1                             91
     c                   if        *in91 = *on
     c                   eval      *in38 = *on
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
      *  dersom sats oppgis må også varenummer oppgis
      *
     c                   if        c2usts <> 0 and
     c                             c2uvar = 0
     c                   eval      *in39 = *on
     c                   goto      c2taga
     c                   endif
      *
      * Oppdatering av undergruppe
      *
     c                   clear                   vugrlur
      *
     c                   eval      vugrlu_uugr = c1uugr
     c     vugrlu_key    chain     vugrlur                            90
      *
     c                   eval      vgutxt = c2utxt
     c                   eval      vgukhv = c2ukhv
     c                   eval      vgupra = c2upra
     c                   eval      vguens = c2uens
     c                   eval      vguenl = c2uenl
     c                   eval      vguenp = c2uenp
     c                   eval      vguep1 = c2uep1
     c                   eval      vguep2 = c2uep2
     c                   eval      vgudek = c2udek
     c                   eval      vgupra = c2upra
     c                   eval      vguakt = c2uakt
     c                   eval      vguavr = c2uavr
     c                   eval      vgulag = c2ulag
7.01 c                   eval      vgufri = c2ufri
6.20 c                   eval      vgutra = c2utra
     c                   eval      vgusts = c2usts
      *
     c                   movel     c2uvar        vguvar
      *
     c                   time                    vgueda
     c                   time                    vgueti
     c                   eval      vgueus = l_user
      *
     c                   if        *in90 = *off
     c                   update    vugrlur
     c                   else
     c                   eval      vgufir = w_firm
     c                   eval      vguogr = w_ogrp
     c                   eval      vguhgr = w_hgrp
     c                   eval      vguugr = c1uugr
6.10 c                   time                    vguoda
6.10 c                   time                    vguoti
6.10 c                   eval      vguous = l_user
     c                   write     vugrlur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1utxt = vgutxt
     c                   eval      b1ukhv = vgukhv
     c                   eval      b1upra = vgupra
     c                   eval      b1uens = vguens
     c                   eval      b1uenl = vguenl
     c                   eval      b1uenp = vguenp
     c                   eval      b1udek = vgudek
     c                   eval      b1upra = vgupra
     c                   eval      b1uakt = vguakt
     c                   eval      b1uavr = vguavr
     c                   eval      b1ulag = vgulag
7.01 c                   eval      b1ufri = vgufri
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      d1uogr = w_ogrp
     c                   eval      d1uhgr = w_hgrp
      *
     c                   eval      vugrlr_uugr = d1uugr
     c     vugrlr_key    chain     vugrlr                             90
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter undergruppe
      *
     c                   eval      vugrlu_uugr = d1uugr
     c     vugrlu_key    chain     vugrlur                            90
     c                   delete    vugrlur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_uugr = k1uugr
      *
      * Fyller kopierings-bildet
      *
     c                   eval      vugrlr_uugr = k1uugr
     c     vugrlr_key    chain     vugrlr                             90
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1uugr = 0
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      vugrlr_uugr = k1uugr
     c     vugrlr_key    chain     vugrlr                             90
     c                   if        *in90 = *off
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1uugr = s_uugr
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      vugrl2
     c                   other
     c                   read      vugrl1
     c                   endsl
      *
     c                   if        %eof or
     c                             vgufir <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   if        vguogr <> w_ogrp
     c                   if        w_seqe = 'B'
     c                   iter
     c                   else
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
     c                   endif
      *
     c                   if        vguhgr <> w_hgrp
     c                   if        w_seqe = 'B'
     c                   iter
     c                   else
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      vugrl2_utxt = vgutxt
     c                   other
     c                   eval      vugrl1_uugr = vguugr
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1uugr = vguugr
     c                   eval      b1utxt = vgutxt
     c                   eval      b1ukhv = vgukhv
     c                   eval      b1upra = vgupra
     c                   eval      b1uens = vguens
     c                   eval      b1uenl = vguenl
     c                   eval      b1uenp = vguenp
     c                   eval      b1udek = vgudek
     c                   eval      b1upra = vgupra
     c                   eval      b1uakt = vguakt
     c                   eval      b1uavr = vguavr
     c                   eval      b1ulag = vgulag
7.01 c                   eval      b1ufri = vgufri
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'B'
     c     vugrl2_key    setgt     vugrl2
     c                   readp     vugrl2
     c                   other
     c     vugrl1_key    setgt     vugrl1
     c                   readp     vugrl1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   readp     vugrl2
     c                   other
     c                   readp     vugrl1
     c                   endsl
      *
     c                   if        %eof or
     c                             vgufir <> w_firm
     c                   leave
     c                   endif
      *
     c                   if        vguogr <> w_ogrp
     c                   if        w_seqe = 'B'
     c                   iter
     c                   else
     c                   leave
     c                   endif
     c                   endif
      *
     c                   if        vguhgr <> w_hgrp
     c                   if        w_seqe = 'B'
     c                   iter
     c                   else
     c                   leave
     c                   endif
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      vugrl2_utxt = vgutxt
     c                   other
     c                   eval      vugrl1_uugr = vguugr
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'B'
     c     vugrl2_key    setll     vugrl2
     c                   other
     c     vugrl1_key    setll     vugrl1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ogrp
     c                   parm                    p_hgrp
      *
      * Key for posisjonering på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ogrp
     c                   kfld                    w_hgrp
     c                   kfld                    vugrl1_uugr
      *
      * Key for posisjonering på beskrivelse
      *
     c     vugrl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl2_utxt
      *
      * Key for oppslag på undergruppe
      *
     c     vugrlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ogrp
     c                   kfld                    w_hgrp
     c                   kfld                    vugrlr_uugr
      *
      * Key for oppdatering av undergruppe
      *
     c     vugrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ogrp
     c                   kfld                    w_hgrp
     c                   kfld                    vugrlu_uugr
      *
      * Key for les på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for les på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på kontohenvisning
      *
     c     vkhvl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vkhvl1_kkod
      *
      * Key for oppslag på produktansvarlig
      *
     c     ra26l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra26l1_zsak
      *
      * Key for oppslag på enhet
      *
     c     venhl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    venhl1_eenh
      *
      * Key for les på vareregister
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *
      * Henter firma, overgruppe og hovedgruppe fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_ogrp = p_ogrp
     c                   eval      w_hgrp = p_hgrp
      *
      * Initierer skjermbilde
      *
     c                   eval      *in22 = *on
      *
     c                   eval      b2uogr = w_ogrp
     c                   eval      b2uhgr = w_hgrp
      *
     c                   eval      vogrl1_oogr = w_ogrp
     c     vogrl1_key    chain     vogrl1                             91
     c                   if        *in91 = *off
     c                   eval      b2otxt = vgotxt
     c                   endif
      *
     c                   eval      vhgrl1_hogr = w_ogrp
     c                   eval      vhgrl1_hhgr = w_hgrp
     c     vhgrl1_key    chain     vhgrl1                             91
     c                   if        *in91 = *off
     c                   eval      b2htxt = vghtxt
     c                   endif
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      vugrl1_uugr = 0
     c     vugrl1_key    setll     vugrl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
