     H/TITLE  Utskrift av Bestilling-status-liste
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * System  . . . . : ASLAGR                                        *
      * Program . . . . : LO642R                                        *
      * Beskrivelse . . : Utskrift av bestilling-status-liste           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * Spesialversjon. :                                               *
      * Tilpasset Lor . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
6.nu  * R6.30     120614  Gjennomgått mtp. nummerutvidelse         bof  *
      *                                                                 *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     Frlevl1    IF   E           K DISK    RENAME(RLEVPFR:rlevl1R)
      *
     FLSTSL1    IF   E           K DISK    RENAME(LSTSPFR:LSTSL1R)
     FLOHEL1    IF   E           K DISK    RENAME(LOHEPFR:LOHEL1R)
      *
     FLO642P    O    E             PRINTER
      *
     D                 DS
      *                                                      Parameter-inn
     D  LDPREC                 1     64
     D  LDVAL1                 1      1  0
      *
      * Hente inn firma fra local
      *
     D                UDS
     D  DSFIRM               944    946  0
     D  DSFNAV               951    980
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av formater                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Benyttede formater
      * - - - - - - - - - -
      *
      * A1HDR  - Heading A1 på rapport
      * D1DET  - Detail  D1 på rapport
      * T1TOT  - Total   T1 på rapport
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * A1HDR Behandle heading A1                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      * Skrive heading
      *
     C                   WRITE     A1HDR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * D1DET Behandle file                                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Lese file
      *
     C     HEDKEY        SETLL     LOHEL1
     C                   READ      LOHEL1                                 15
      *
     C     *IN15         DOWEQ     '0'
      *
      * Hent opplysninger fra LEVERANDØR-REGISTER
      *
     C                   MOVE      LOLDOR        LEVNUM
     C     LEVKEY        CHAIN     rlevl1R                            64
     C                   MOVE      RLNAVN        D1NAVN
     C   64              MOVE      *BLANK        D1NAVN
      *
     c                   eval      d1bdat = 0
     c                   eval      d1ldat = 0
      *
     c                   if        lobdat <> *loval
     c     *dmy          move      lobdat        d1bdat
     c                   endif
      *
     c                   if        loldat <> *loval
     c     *dmy          move      loldat        d1ldat
     c                   endif
      *
      * Behandle record
      *
     C                   ADD       1             T1POST
     C                   WRITE     D1DET                                30
      *
      * Er det overflow ?
      *
     C     *IN30         IFEQ      *ON
     C                   WRITE     A1HDR
     C                   ENDIF
      *
     C                   READ      LOHEL1                                 15
     C                   ENDDO
      *
     C     XEND01        TAG
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * T1TOT Behandle total T1                                         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      * Skrive total
      *
     C                   WRITE     T1TOT                                30
     C                   GOTO      XSLUTT
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     XSLUTT        TAG
      *
     C                   SETON                                        LR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     *INZSR        BEGSR
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
      *  Redefinering av felter
      *
      *  Redefinering av nøkler
      *
     C     *LIKE         DEFINE    LAAFIR        STSFIR
      *
     C     *LIKE         DEFINE    RLFIRM        LEVFIR
     C     *LIKE         DEFINE    RLLEVR        LEVNUM
      *
     C     *LIKE         DEFINE    LOFIRM        HEDFIR
     C     *LIKE         DEFINE    LONUMM        HEDNUM
     C     *LIKE         DEFINE    LOSUFF        HEDSUF
      *
      * Key for leverandør-register
      *
     C     LEVKEY        KLIST
     C                   KFLD                    LEVFIR
     C                   KFLD                    LEVNUM
      *
      * Key for statusregister
      *
     C     STSKEY        KLIST
     C                   KFLD                    STSFIR
      *
      * Key for BESTILLING-HODE-REGISTER
      *
     C     HEDKEY        KLIST
     C                   KFLD                    HEDFIR
     C                   KFLD                    HEDNUM
     C                   KFLD                    HEDSUF
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     *ENTRY        PLIST
     C                   PARM                    WWPARM           64
      *
     C                   MOVE      WWPARM        LDPREC
      *
     C                   MOVE      DSFNAV        A1FNAV
      * Legge inn key
      *
     C                   MOVE      DSFIRM        A1FIRM
     C                   MOVE      DSFIRM        LEVFIR
     C                   MOVE      DSFIRM        STSFIR
     C                   MOVE      DSFIRM        HEDFIR
      *
      * Hent opplysninger fra LAGER-STATUS-KODER
      *
     C     STSKEY        CHAIN     LSTSL1R                            69
      *
     C                   ENDSR
