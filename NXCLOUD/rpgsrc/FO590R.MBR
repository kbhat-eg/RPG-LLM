     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FO590R
      * Beskrivelse . . : Ordre-hode, styring av plukking fra kjørekontor
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       241005 HKO  Nytt program
      *       030206 HKO  Ny logikk
5.54  * R5.54 030407 AMD  Bygget om kjørekontorløsningen til å benytte
      *                   loggfilen i stedet for infokodene
5.54  * R5.54 030407 AMD  Legger ut en streng for info/ordrestatus/kode
5.55  * R5.55 241007 AMD  Lagt inn mulighet for å tilbakestille ordre
      *                   som er klargjort for plukking slik at den
      *                   blir borte igjen fra dette skjermbildet
5.60  * R5.60 311007 AMD  Lagt inn Filter for valg av lager. NB! Søker
      *                   ned på linjenivå og kan ta noe lenger tid.
5.61  * R5.61 281107 bof  Lagt inn søk på kundenavn og gjort den bredere
      *                   og lagt på leveringsmåte
5.62  * R5.62 070108 bof  Utvidet nøkkel med år
5.63  * R5.63 310108 bof  Bytter mellom info i bildet m. F11
5.64  * R5.64 280208 bof  Default lager i Filter
5.65  * R5.65 200608 bof  F11 har blitt F23
5.66  * R5.66 110708 Amd  Lagt inn mulighet for å se loggfilen
      * 6.10  230609 BSA  Oppdatert med sporingsinformasjon
6.nu  * R6.30 280514 bsa  Programmet gjennomgått mtp nummerutvidelser.
6.nu  *                   Utvidet 11.12.15
6.30  * 6.30  130116 bsa  Utvider med filter på ordretyper og forsendelsesmåte
6.31  * 6.30  060818 bsa  Sjekker om ordren allerede er plukket.
6.32  * 7.00  070818 bkv  F5 setter alltid loval for dato
6.33  * 7.00  140819 bof  Hvis plukket,så sjekk om høyeste kode = 2
7.xx  * 7.00  300316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 7.00  300120 bkv  Endret kall til FU706 pga hastighet
7.02  * K000  171220 bsa  Tilbakestill plukking, fjerner også koden for
7.02  * K000              at plukkliste er kjørt.
7.03  * K000  050613 bof  Legge inn kjørerutine ved klar for plukking
      *
7.fb  * 7.10  040117 bof  Hvis depositumskrav og ikke betalt.
      *
8.01  *K000   130122 bsa  Skrevet om til bredt skjermbilde.
8.02  *K000   130122 bsa  Hvis forskjell på varekunde og fakturakunde, blir
8.02  *K000               det feil i visning av kunde/prosjekt.
8.03  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffohelr    if   e           k disk    rename(fohepfr:fohelrr)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
5.61 ffohela    if   e           k disk    rename(fohepfr:fohelar)
     ffohelb    if   e           k disk    rename(fohepfr:fohelbr)
     ffoheln    if   e           k disk    rename(fohepfr:fohelnr)
     ffohelo    if   e           k disk    rename(fohepfr:fohelor)
5.60 ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fra09l1    if   e           k disk    rename(ra09pfr:ra09l1r)
7.03 fra25l1    if   e           k disk    rename(ra25pfr:ra25l1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     fvinfl1    if   e           k disk    rename(vinfpfr:vinfl1r)
5.64 fausrlr    if   e           k disk    rename(ausrpfr:ausrlrr)
5.55 ffloglu    uf a e           k disk    rename(flogpfr:floglur)
6.31 ffloglr    if   e           k disk    rename(flogpfr:floglrr)
6.31 f                                     prefix(xx:2)
     ffohelu    uf   e           k disk    rename(fohepfr:fohelur)
6.30 fra17l1    if   e           k disk    rename(ra17pfr:ra17l1r)
7.fb fsdepl1    if   e           k disk    rename(sdeppfr:sdepl1r)
      *
     ffo590d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
5.54  *
5.54  * Definering av datastruktur
5.54  *
5.54 d                 ds
5.54 d d_urec                        80
5.54 d  d_firm                        3  0 overlay(d_urec)
5.62 d  d_aarr                        4  0 overlay(d_urec:4)
6.nu d  d_numm                        8  0 overlay(d_urec:8)
6.nu d  d_suff                        2  0 overlay(d_urec:16)
6.nu d  d_kode                        1    overlay(d_urec:18)
6.nu d  d_date                         d   overlay(d_urec:19) datfmt(*iso)
6.nu d  d_time                         t   overlay(d_urec:29) timfmt(*iso)
6.nu d  d_user                       10    overlay(d_urec:37)
6.nu d  d_wsid                       10    overlay(d_urec:47)
      *
      * Definering av variabler i nøkler
      *
     d fohelr_numm     s                   like(fonumm)
     d fohelr_suff     s                   like(fosuff)
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
5.61 d fohela_kuna     s                   like(fokuna)
     d fohelb_kjor     s                   like(fokjor)
     d fohelb_kjnr     s                   like(fokjnr)
     d foheln_ldat     s                   like(foldat)
     d foheln_otyp     s                   like(footyp)
     d foheln_kjor     s                   like(fokjor)
     d foheln_kjnr     s                   like(fokjnr)
     d fohelo_otyp     s                   like(footyp)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
     d rkunl1_kund     s                   like(rkkund)
     d ra09l1_ikod     s                   like(raikod)
     d votyl1_otyp     s                   like(vaotyp)
     d vinfl1_ityp     s                   like(vaityp)
5.62 d floglu_aarr     s                   like(fuaarr)
5.55 d floglu_numm     s                   like(funumm)
5.55 d floglu_suff     s                   like(fusuff)
5.55 d floglu_kode     s                   like(fukode)
5.55 d floglu_date     s                   like(fudate)
5.55 d floglu_time     s                   like(futime)
     d fohelu_numm     s                   like(fonumm)
     d fohelu_suff     s                   like(fosuff)
5.60 d fodtl1_numm     s                   like(fdnumm)
5.60 d fodtl1_suff     s                   like(fdsuff)
5.64 d ausrlr_user     s                   like(abuser)
6.30 d ra17l1_qkod     s                   like(raqkod)
6.31 d floglr_aarr     s                   like(fuaarr)
6.31 d floglr_numm     s                   like(funumm)
6.31 d floglr_suff     s                   like(fusuff)
6.31 d floglr_kode     s                   like(fukode)
6.31 d floglr_date     s                   like(fudate)
7.03 d ra25l1_yrut     s                   like(rayrut)
7.fb d sdepl1_kund     s                   like(skkund)
7.fb d sdepl1_numm     s                   like(sknumm)
7.fb d sdepl1_tell     s                   like(sktell)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_subf          s              1    inz(*off)
     d b_oppt          s              1    inz(*off)
     d b_pluk          s              1    inz(*off)
     d b_avvi          s              1    inz(*off)
5.55 d b_avbr          s              1    inz(*off)
5.60 d b_funn          s              1    inz(*off)
6.30 d b_otyp          s              1    inz(*off)
6.30 d b_lmat          s              1    inz(*off)
7.fb d b_depo          s              1    inz(*off)
      *
     d w_retu          s              1
5.54 d w_kode          s              1
6.33 d w_kod2          s              1
5.54 d w_svar          s              1
5.54 d w_stat          s              1
5.55 d w_date          s               d   datfmt(*iso)
5.55 d w_time          s               t   timfmt(*iso)
5.55 d w_user          s             10
5.55 d w_wsid          s             10
5.61 d w_vekt          s              9  3
5.61 d w_volu          s              9  3
5.60 d w_jkod          s              2  0
5.60 d w_jtxt          s             20
5.62 d w_aarr          s                   like(fuaarr)
6.nu d w_numm          s                   like(fonumm)
5.66 d w_suff          s                   like(fosuff)
6.30 d w_otyp          s                   like(footyp)
6.30 d w_lmat          s                   like(folmat)
6.30 d w_qtxt          s             20
6.31 d w_dato          s               d   datfmt(*iso)
6.31 d w_meld          s             50
8.02 d w_kund          s                   like(fokund)
      *
     d w_firm          s                   like(fofirm)
      *
      * Definering av konstanter
      *
8.01 d c_sfil          s              2  0 inz(16)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
5.60  *
5.60  * F01=Filter
5.60  *
5.60 c                   if        *inka
5.60 c                   exsr      filter
5.60 c                   goto      b2tagb
5.60 c                   endif
      *
5.61  * F11=Skift mellom informasjon
5.61  *
5.65 c                   if        *inkx
5.61 c                   if        *in51 = *off
5.61 c                   eval      *in51 = *on
5.61 c                   else
5.61 c                   eval      *in51 = *off
5.61 c                   endif
5.61 c                   exsr      forny
5.61 c*                  goto      b2tagb
5.61 c                   goto      b2taga
5.61 c                   endif
      *
      * F13=Ordre som plukkes
      *
     c                   if        *inkm
     c                   call      'FO592R'
     c                   exsr      forny
     c                   goto      b2tagb
     c                   endif
      *
      * F14=Restede ordre til levering
      *
     c                   if        *inkn
     c                   call      'FO570R'
     c                   exsr      forny
     c                   goto      b2tagb
     c                   endif
      *
      * Posisjonering
      *
     c                   if        b2ldat <> 0 or
     c                             b2otyp <> *blank or
     c                             b2kjor <> 0 or
     c                             b2numm <> 0 or
5.61 c                             b2knav <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
5.61 c                   when      w_seqe = 'N'
5.61 c                   eval      fohela_kuna = b1knav
5.61 c     fohelo_key    setll     fohelo
     c                   when      w_seqe = 'T'
     c                   eval      fohelo_otyp = b1otyp
     c     fohelo_key    setll     fohelo
     c                   when      w_seqe = 'R'
     c                   eval      fohelb_kjor = b1kjor
     c                   eval      fohelb_kjnr = b1kjnr
     c     fohelb_key    setll     fohelb
     c                   when      w_seqe = 'O'
     c                   eval      fohel1_numm = b1numm
     c                   eval      fohel1_suff = b1suff
     c     fohel1_key    setll     fohel1
     c                   other
6.32 c*                  if        b1ldat <> 0
6.32 c*                  eval      foheln_ldat = %date(b1ldat:*dmy)
6.32 c*                  else
     c                   eval      foheln_ldat = *loval
6.32 c*                  endif
     c                   eval      foheln_otyp = b1otyp
     c                   eval      foheln_kjor = b1kjor
     c                   eval      foheln_kjnr = b1kjnr
     c     foheln_key    setll     foheln
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Leveringsdato
      *
     c                   if        b2ldat <> 0
     c                   eval      w_seqe = *blank
     c                   eval      foheln_ldat = *loval
     c     *dmy          test(d)                 b2ldat                 31
     c                   if        *in31 = *off
     c     *dmy          move      b2ldat        foheln_ldat
     c                   endif
     c                   eval      foheln_otyp = b2otyp
     c                   eval      foheln_kjor = b2kjor
     c                   eval      foheln_kjnr = 0
     c     foheln_key    setll     foheln
     c                   goto      end_pos
     c                   endif
      *
      * Ordretype
      *
     c                   if        b2otyp <> *blank
     c                   eval      w_seqe = 'T'
     c                   eval      fohelo_otyp = b2otyp
     c     fohelo_key    setll     fohelo
     c                   goto      end_pos
     c                   endif
      *
      * Kjørerute
      *
     c                   if        b2kjor <> 0
     c                   eval      w_seqe = 'R'
     c                   eval      fohelb_kjor = b2kjor
     c                   eval      fohelb_kjnr = 0
     c     fohelb_key    setll     fohelb
     c                   goto      end_pos
     c                   endif
      *
      * Ordrenr
      *
     c                   if        b2numm <> 0
     c                   eval      w_seqe = 'O'
     c                   eval      fohel1_numm = b2numm
     c                   eval      fohel1_suff = 0
     c     fohel1_key    setll     fohel1
     c                   goto      end_pos
     c                   endif
5.61  *
5.61  * Kundenavn - alfa
5.61  *
5.61 c                   if        b2knav <> *blank
5.61 c                   eval      w_seqe = 'N'
5.61 c                   eval      fohela_kuna = b2knav
5.61 c     fohela_key    setll     fohela
5.61 c                   goto      end_pos
5.61 c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2ldat = 0
     c                   eval      b2kjor = 0
     c                   eval      b2otyp = *blank
     c                   eval      b2numm = 0
5.61 c                   eval      b2knav = *Blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre ordreinformasjon
      *
     c                   if        b1valg = 2
     c                   call      'FO150R'
     c                   parm                    w_firm
6.nu c                   parm                    b1numm
     c                   parm                    b1suff
     c                   eval      b1valg = 0
     c                   eval      b_forn = *on
     c                   update    b1sfl
     c                   iter
     c                   endif
5.55  *
5.55  * Avbryt klar for plukking
5.55  *
5.55 c                   if        b1valg = 4
5.55 c                   exsr      avbr_klar
5.55 c                   eval      b1valg = 0
5.55 c                   update    b1sfl
5.55 c                   iter
5.55 c                   endif
      *
      * Vis ordre
      *
     c                   if        b1valg = 5
     c                   call      'FO505R'
     c                   parm                    w_firm
6.nu c                   parm                    b1numm
     c                   parm                    b1suff
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis kunde/kundeprosjekt
      *
     c                   if        b1valg = 6
8.01 c                   eval      w_kund = b1kund
8.01 c                   if        b1lkun <> 0 and
8.01 c                             b1kund <> 0 and
8.01 c                             b1kund <> b1lkun
8.01 c                   eval      w_kund = b1lkun
8.01 c                   endif
     c                   call      'FL510R'
     c                   parm                    w_firm
8.01 c**                 parm                    b1kund
8.01 c                   parm                    w_kund
     c                   parm                    b1kpro
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Behandle ordre
      *
     c                   if        b1valg = 7
     c                   eval      w_retu = *blank
     c                   call      'FO500R'
6.nu c                   parm                    b1numm
     c                   parm                    w_retu
     c                   eval      b1valg = 0
     c                   eval      b_forn = *on
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Hvem behandler ordre
      *
     c                   if        b1valg = 8
     c                   call      'FO710R'
     c                   parm                    w_firm
6.nu c                   parm                    b1numm
     c                   parm                    b1suff
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Send til plukking
      *
     c                   if        b1valg = 9
     c                   exsr      send_plukking
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
5.66  *
5.66  * Viser loggfilen med ordrehistorikk
5.66  *
5.66 c                   if        b1valg = 60
5.66 c                   eval      w_aarr = %subdt(b1bdat:*years)
5.66 c                   eval      w_numm = b1numm
5.66 c                   eval      w_suff = b1suff
5.66 c                   call      'FU500R'
5.66 c                   parm                    w_firm
5.66 c                   parm                    w_aarr
6.nu c                   parm                    w_numm
5.66 c                   parm                    w_suff
5.66 c                   eval      b1valg = 0
5.66  ***                exsr      xfarge
5.66 c                   update    b1sfl
5.66 c                   iter
5.66 c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14  = *on
     c                   write     b2ctl
     c                   eval      *in14  = *off
      *
     c                   eval      *in15  = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'T'
     c                   read      fohelo
     c                   when      w_seqe = 'R'
     c                   read      fohelb
     c                   when      w_seqe = 'O'
     c                   read      fohel1
5.61 c                   when      w_seqe = 'N'
5.61 c                   read      fohela
     c                   other
     c                   read      foheln
     c                   endsl
      *
     c                   if        %eof or
     c                             fofirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
7.01 c                   eval      w_kode = *blank                              settes i chk_subfile
     c                   exsr      chk_subfile
     c                   if        b_subf = *off
     c                   iter
     c                   endif
7.01 c                   eval      b1kod2 = w_kode                              settes i chk_subfile
      *
      * Fyll opp subfile
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'T'
     c                   eval      fohelo_otyp = footyp
     c                   when      w_seqe = 'R'
     c                   eval      fohelb_kjor = fokjor
     c                   eval      fohelb_kjnr = fokjnr
     c                   when      w_seqe = 'O'
     c                   eval      fohel1_numm = fonumm
     c                   eval      fohel1_suff = fosuff
5.61 c                   when      w_seqe = 'N'
5.61 c                   eval      fohela_kuna = fokuna
     c                   other
     c                   eval      foheln_ldat = foldat
     c                   eval      foheln_otyp = footyp
     c                   eval      foheln_kjor = fokjor
     c                   eval      foheln_kjnr = fokjnr
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1ldat = 0
     c                   eval      b1otyp = footyp
     c                   eval      b1kjor = fokjor
     c                   eval      b1kjnr = fokjnr
     c                   eval      b1numm = fonumm
     c                   eval      b1suff = fosuff
     c                   eval      b1kund = fokund
     c                   eval      b1kpro = fokpro
8.01 c*                  eval      b1knav = fokuna
8.01 c                   eval      b1lnav = foadr1
8.02 c                   eval      b1lkun = folkun
     c                   eval      b1adr1 = foadr1
     c                   eval      b1lmtx = folmtx
     c                   eval      b1vekt = 0
     c                   eval      b1volu = 0
     c                   eval      b1ityp = foityp
     c                   eval      b1kode = *blank
     c                   eval      b1lmat = folmat
5.62 c                   eval      b1bdat = fobdat
      *
     c                   if        foldat <> *loval
     c     *dmy          move      foldat        b1ldat
     c                   endif
      *
     c                   eval      b1knav = *blank
     c                   eval      b1inf1 = *blank
      *
     c                   if        *in51  = *off
     c                   eval      rkunl1_kund = fokund
     c     rkunl1_key    chain     rkunl1r
     c                   if        %found
     c                   movel     rknavn        b1knav
     c                   endif
     c                   movel     foinf1        b1inf1
     c                   else
8.01 c**                 movel     b1adr1        b1knav
     c                   movel     b1lmtx        b1inf1
     c                   endif
8.01  *
8.01  * Hvis varekunde, fakturakunde og prosjekt, legg ut varekunde
8.01  *
8.01 c                   if        fokund <> 0 and
8.01 c                             folkun <> 0 and
8.01 c                             fokund <> folkun and
8.01 c                             fokpro <> 0
8.01 c                   eval      rkunl1_kund = folkun
8.01 c     rkunl1_key    chain     rkunl1r
8.01 c                   if        %found
8.01 c                   movel     rknavn        b1knav
8.01 c                   endif
8.01 c                   endif
      *
7.fb c                   exsr      sjekk_depo
7.fb c                   if        b_depo = *on
7.fb c                   eval      *in45  = *on
7.fb c                   endif
      *
3.34 c                   eval      *in41  = *off
3.34 c                   eval      *in42  = *off
3.34 c                   eval      *in43  = *off
3.34 c                   eval      *in44  = *off
      *
     c                   select
3.34 c                   when      vaifrg = 'H'
3.34 c                   eval      *in41  = *on
3.34 c                   when      vaifrg = 'G'
3.34 c                   eval      *in42  = *on
3.34 c                   when      vaifrg = 'B'
3.34 c                   eval      *in43  = *on
3.34 c                   when      vaifrg = 'R'
3.34 c                   eval      *in44  = *on
3.34 c                   endsl
3.34  *
     c                   select
3.12 c                   when      fokode = 1
3.12 c                   eval      b1kode = '**'
3.12 c                   when      fokode = 2
3.12 c                   eval      b1kode = 'UT'
3.12 c                   when      fokode = 3
3.12 c                   eval      b1kode = 'OK'
3.12 c                   when      fokode = 5
3.12 c                   eval      b1kode = 'VE'
3.12 c                   when      fokode = 7
3.12 c                   eval      b1kode = 'BU'
3.12 c                   endsl
      *
5.61 c                   eval      w_vekt = 0
5.61 c                   eval      w_volu = 0
3.12  *
     c                   call      'FO780R'
     c                   parm                    w_firm
6.nu c                   parm                    fonumm
     c                   parm                    fosuff
5.61 c                   parm                    w_vekt
5.61 c                   parm                    w_volu
     c                   parm                    b_avvi
5.61 c                   eval      b1vekt = w_vekt
5.61 c                   eval      b1volu = w_volu
      *
     c                   eval      *in80 = b_avvi = *on
7.01  **
7.01  ** Henter inn høyeste ordrestatus
7.01  ** og legger over alt i en streng
7.01  **
7.01 c*                   eval      w_kode = *blank
7.01 c*                   eval      w_aarr = %subdt(b1bdat:*years)
7.01 c*                   call      'FU706R'
7.01 c*                   parm                    w_aarr
7.01 c*                   parm                    b1numm
7.01 c*                   parm                    b1suff
7.01 c*                   parm                    w_kode
7.01  **
7.01 c*                   eval      b1kod2 = w_kode
5.54 c                   eval      b1alle = b1ityp + b1kod2 + b1kode
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'T'
     c     fohelo_key    setgt     fohelo
     c                   readp     fohelo
     c                   when      w_seqe = 'R'
     c     fohelb_key    setgt     fohelb
     c                   readp     fohelb
     c                   when      w_seqe = 'O'
     c     fohel1_key    setgt     fohel1
     c                   readp     fohel1
5.61 c                   when      w_seqe = 'N'
5.61 c     fohela_key    setgt     fohela
5.61 c                   readp     fohela
     c                   other
     c     foheln_key    setgt     foheln
     c                   readp     foheln
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'T'
     c                   readp     fohelo
     c                   when      w_seqe = 'R'
     c                   readp     fohelb
     c                   when      w_seqe = 'O'
     c                   readp     fohel1
5.61 c                   when      w_seqe = 'N'
5.61 c                   readp     fohela
     c                   other
     c                   readp     foheln
     c                   endsl
      *
     c                   if        %eof or
     c                             fofirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   exsr      chk_subfile
     c                   if        b_subf = *off
     c                   iter
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'T'
     c                   eval      fohelo_otyp = footyp
     c                   when      w_seqe = 'R'
     c                   eval      fohelb_kjor = fokjor
     c                   eval      fohelb_kjnr = fokjnr
     c                   when      w_seqe = 'O'
     c                   eval      fohel1_numm = fonumm
     c                   eval      fohel1_suff = fosuff
5.61 c                   when      w_seqe = 'N'
5.61 c                   eval      fohela_kuna = fokuna
     c                   other
     c                   eval      foheln_ldat = foldat
     c                   eval      foheln_otyp = footyp
     c                   eval      foheln_kjor = fokjor
     c                   eval      foheln_kjnr = fokjnr
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'T'
     c     fohelo_key    setll     fohelo
     c                   when      w_seqe = 'R'
     c     fohelb_key    setll     fohelb
     c                   when      w_seqe = 'O'
     c     fohel1_key    setll     fohel1
5.61 c                   when      w_seqe = 'N'
5.61 c     fohela_key    setll     fohela
     c                   other
     c     foheln_key    setll     foheln
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sjekke om rad skal være med i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     chk_subfile   begsr
      *
     c                   eval      b_subf = *off
      *
      * Viser ikke ordre dersom det ikke kjøres med kjørekontorløsning
      *
     c                   if        faakjk = 0
     c                   leavesr
     c                   endif
      *
      * Viser kun ordre med behandlingskode 1
      *
     c                   eval      votyl1_otyp = footyp
     c     votyl1_key    chain     votyl1
     c                   if        not %found or
     c                             vaoakk <> 1
     c                   leavesr
     c                   endif
      *
      * Kun ordrer med ordre informasjonskode som er parametersatt med
      * kjørekontorløsning blir med på utlukket
      * Utelater ordrer som er sendt til plukking
      *
5.54  *>                 if        foityp = *blank or
5.54  *>                           foplda <> *loval
5.54  *>                 leavesr
5.54  *>                 endif
      *
5.54  *>                 eval      vinfl1_ityp = foityp
5.54  *>   vinfl1_key    chain     vinfl1
5.54  *>                 if        %found and
5.54  *>                           vaikjo = '1'
5.54  *>                 eval      b_subf = *on
5.54  *>                 leavesr
5.54  *>                 endif
      *
5.54  * Ny logikk lagt inn. Den ovenstående er foreløpig stjernet
5.54  * ut og skal slettes på et sendere tidspunkt. Lar det stå for
5.54  * litt dokumentasjon av endringene i programmet
5.54  *
5.54  *
5.54  * Skal bare vise ordre som er klar til plukking
5.54  *
5.54 c*                  eval      w_svar = *blank
5.54 c*                  eval      w_kode = '2'
5.54 c*                  call      'FU702R'
5.54 c*                  parm                    fonumm
5.54 c*                  parm                    fosuff
5.54 c*                  parm                    w_kode
5.54 c*                  parm                    w_svar
5.54  *
5.54 c*                  if        w_svar = 'J'
5.54 c*                  else
5.54 c*                  leavesr
5.54 c*                  endif
7.01  **
7.01  ** Henter inn høyeste ordrestatus og sjekker om
7.01  ** denne er lik 2 eller 3
7.01  **
7.01 c*                   eval      w_kode = *blank
7.01 c*                   eval      w_aarr = %subdt(fobdat:*years)
7.01 c*                   call      'FU706R'
7.01 c*                   parm                    w_aarr
7.01 c*                   parm                    fonumm
7.01 c*                   parm                    fosuff
7.01 c*                   parm                    w_kode
7.01  **
7.01 c*                   if        w_kode = '2' or
7.01 c*                             w_kode = '3'
7.01 c*                   else
7.01 c*                   leavesr
7.01 c*                   endif
5.54  *
5.60  *
5.60  * Dersom lager er valgt,
5.60  * sjekk om dette finnes på ordrelinje
5.60  *
5.60 c                   if        f1lage <> 0
5.60 c                   exsr      xsjekk_lag
5.60 c                   if        b_funn = ' '
5.60 c                   leavesr
5.60 c                   endif
5.60 c                   endif
6.30  *
6.30  * Sjekk mot ordretyper
6.30  *
6.30 c                   if        f1oty1 <> *blanks or
6.30 c                             f1oty2 <> *blanks or
6.30 c                             f1oty3 <> *blanks
6.30  *
6.30 c                   eval      b_otyp = *off
6.30 c                   if        f1oty1 <> *blanks and
6.30 c                             footyp = f1oty1
6.30 c                   eval      b_otyp = *on
6.30 c                   endif
6.30 c                   if        f1oty2 <> *blanks and
6.30 c                             footyp = f1oty2
6.30 c                   eval      b_otyp = *on
6.30 c                   endif
6.30 c                   if        f1oty3 <> *blanks and
6.30 c                             footyp = f1oty3
6.30 c                   eval      b_otyp = *on
6.30 c                   endif
6.30  *
6.30 c                   if        b_otyp = *off
6.30 c                   leavesr
6.30 c                   endif
6.30  *
6.30 c                   endif
6.30  *
6.30  * Vi må sjekke forsendelsesmåte
6.30  *
6.30 c                   if        f1lma1 <> 0 or
6.30 c                             f1lma2 <> 0 or
6.30 c                             f1lma3 <> 0
6.30  *
6.30 c                   eval      b_lmat = *off
6.30 c                   if        f1lma1 <> 0 and
6.30 c                             folmat = f1lma1
6.30 c                   eval      b_lmat = *on
6.30 c                   endif
6.30 c                   if        f1lma2 <> 0 and
6.30 c                             folmat = f1lma2
6.30 c                   eval      b_lmat = *on
6.30 c                   endif
6.30 c                   if        f1lma3 <> 0 and
6.30 c                             folmat = f1lma3
6.30 c                   eval      b_lmat = *on
6.30 c                   endif
6.30  *
6.30 c                   if        b_lmat = *off
6.30 c                   leavesr
6.30 c                   endif
6.30  *
6.30 c                   endif
      *
7.01  *
7.01  * Henter inn høyeste ordrestatus og sjekker om
7.01  * denne er lik 2 eller 3
7.011 *
7.01 c                   eval      w_kode = *blank
7.01 c                   eval      w_aarr = %subdt(fobdat:*years)
7.01 c                   call      'FU706R'
7.01 c                   parm                    w_aarr
7.01 c                   parm                    fonumm
7.01 c                   parm                    fosuff
7.01 c                   parm                    w_kode
7.01  *
7.01 c                   if        w_kode = '2' or
7.01 c                             w_kode = '3'
7.01 c                   else
7.01 c                   leavesr
7.01 c                   endif
      *
5.54 c                   eval      b_subf = *on
5.54  *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12  = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13  = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12  = *off
     c                   eval      *in13  = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
5.60  *
5.60  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.60  * F1 Behandle bilde for filter
5.60  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.60  *
5.60 c     filter        begsr
5.60  *
5.60  *  Send Alt
5.60  *
5.60 c     xf1tag        tag
5.60 c                   write     f1win
5.60 c                   exfmt     f1win
5.60  *
5.60  *  F12=Avslutt subrutinen
5.60  *
5.60 c                   if        *inkc or *inkl
5.60 c                   goto      xf1end
5.60 c                   endif
5.60  *
6.30  *  F04=Søk
5.60  *
5.60 c                   if        *inkd
6.30  *
6.30  * Lager
6.30  *
6.30 c                   if        w_afl1 = 'F1LAGE'
5.60 c                   call      'RA510R'
5.60 c                   parm                    w_firm
5.60 c                   parm                    w_jkod
5.60 c                   parm                    w_jtxt
5.60 c                   if        w_jkod <> 0
5.60 c                   eval      f1lage = w_jkod
5.60 c                   endif
5.60 c                   goto      xf1tag
5.60 c                   endif
6.30  *
6.30  *  Ordretyper
6.30  *
6.30 c                   if        w_afl1 = 'F1OTY1'
6.30 c                   call      'VA550R'
6.30 c                   parm                    w_firm
6.30 c                   parm                    w_otyp
6.30 c                   if        w_otyp <> *blanks
6.30 c                   eval      f1oty1 = w_otyp
6.30 c                   endif
6.30 c                   goto      xf1tag
6.30 c                   endif
6.30  *
6.30 c                   if        w_afl1 = 'F1OTY2'
6.30 c                   call      'VA550R'
6.30 c                   parm                    w_firm
6.30 c                   parm                    w_otyp
6.30 c                   if        w_otyp <> *blanks
6.30 c                   eval      f1oty2 = w_otyp
6.30 c                   endif
6.30 c                   goto      xf1tag
6.30 c                   endif
6.30  *
6.30 c                   if        w_afl1 = 'F1OTY3'
6.30 c                   call      'VA550R'
6.30 c                   parm                    w_firm
6.30 c                   parm                    w_otyp
6.30 c                   if        w_otyp <> *blanks
6.30 c                   eval      f1oty3 = w_otyp
6.30 c                   endif
6.30 c                   goto      xf1tag
6.30 c                   endif
6.30  *
6.30  *  Forsendelsesmåte
6.30  *
6.30 c                   if        w_afl1 = 'F1LMA1'
6.30 c                   call      'RA517R'
6.30 c                   parm                    w_firm
6.30 c                   parm                    w_lmat
6.30 c                   parm                    w_qtxt
6.30 c                   if        w_lmat <> 0
6.30 c                   eval      f1lma1 = w_lmat
6.30 c                   endif
6.30 c                   goto      xf1tag
6.30 c                   endif
6.30  *
6.30 c                   if        w_afl1 = 'F1LMA2'
6.30 c                   call      'RA517R'
6.30 c                   parm                    w_firm
6.30 c                   parm                    w_lmat
6.30 c                   parm                    w_qtxt
6.30 c                   if        w_lmat <> 0
6.30 c                   eval      f1lma2 = w_lmat
6.30 c                   endif
6.30 c                   goto      xf1tag
6.30 c                   endif
6.30  *
6.30 c                   if        w_afl1 = 'F1LMA3'
6.30 c                   call      'RA517R'
6.30 c                   parm                    w_firm
6.30 c                   parm                    w_lmat
6.30 c                   parm                    w_qtxt
6.30 c                   if        w_lmat <> 0
6.30 c                   eval      f1lma3 = w_lmat
6.30 c                   endif
6.30 c                   goto      xf1tag
6.30 c                   endif
6.30
6.30 c                   endif
6.30  *
6.30  * Sjekk ordretyper
6.30  *
6.30 c                   if        f1oty1 <> *blanks
6.30 c                   eval      votyl1_otyp = f1oty1
6.30 c     votyl1_key    chain     votyl1
6.30 c                   if        not %found
6.30 c                   eval      *in34 = *on
6.30 c                   goto      xf1tag
6.30 c                   endif
6.30 c                   endif
6.30  * Ordretype 2
6.30 c                   if        f1oty2 <> *blanks
6.30 c                   eval      votyl1_otyp = f1oty2
6.30 c     votyl1_key    chain     votyl1
6.30 c                   if        not %found
6.30 c                   eval      *in34 = *on
6.30 c                   goto      xf1tag
6.30 c                   endif
6.30 c                   endif
6.30  * Ordretype 3
6.30 c                   if        f1oty3 <> *blanks
6.30 c                   eval      votyl1_otyp = f1oty3
6.30 c     votyl1_key    chain     votyl1
6.30 c                   if        not %found
6.30 c                   eval      *in34 = *on
6.30 c                   goto      xf1tag
6.30 c                   endif
6.30 c                   endif
6.30  * Forsendelsesmåte 1
6.30 c                   if        f1lma1 <> 0
6.30 c                   eval      ra17l1_qkod = f1lma1
6.00 c     ra17l1_key    chain     ra17l1
6.30 c                   if        not %found
6.30 c                   eval      *in35 = *on
6.30 c                   goto      xf1tag
6.30 c                   endif
6.30 c                   endif
6.30  * Forsendelsesmåte 2
6.30 c                   if        f1lma2 <> 0
6.30 c                   eval      ra17l1_qkod = f1lma2
6.30 c     ra17l1_key    chain     ra17l1
6.30 c                   if        not %found
6.30 c                   eval      *in35 = *on
6.30 c                   goto      xf1tag
6.30 c                   endif
6.30 c                   endif
6.30  * Forsendelsesmåte 3
6.30 c                   if        f1lma3 <> 0
6.30 c                   eval      ra17l1_qkod = f1lma3
6.30 c     ra17l1_key    chain     ra17l1
6.30 c                   if        not %found
6.30 c                   eval      *in35 = *on
6.30 c                   goto      xf1tag
6.30 c                   endif
6.30 c                   endif
5.60  *
5.60  * Posisjonerer databasen, og fyll opp subfilen pånytt
5.60  *
5.60 c                   eval      w_seqe = *blank
5.60 c                   eval      foheln_ldat = *loval
5.60 c                   eval      foheln_otyp = *blank
5.60 c                   eval      foheln_kjor = 0
5.60 c                   eval      foheln_kjnr = 0
5.60 c     foheln_key    setll     foheln
5.60  *
5.60 c                   exsr      clr_subfile
5.60 c                   exsr      crt_subfile
5.60  *
5.60 c     xf1end        endsr
5.55  *
5.55  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.55  * Subrutine for å avbryte klar til plukking
5.55  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.55  *
5.55 c     avbr_klar     begsr
5.55  *
5.55  * Dersom ordre er opptatt kalles program som viser hvem som bruker
5.55  * orderen
5.55  *
5.55 c                   exsr      sjekk_ordre
5.55 c                   if        b_oppt = *on
5.55 c                   call      'FO710R'
5.55 c                   parm                    w_firm
6.nu c                   parm                    b1numm
5.55 c                   parm                    b1suff
5.55 c                   leavesr
5.55 c                   endif
5.55  *
5.55  * Håndterer skjermbilde
5.55  *
5.55 c                   exsr      xc4win
5.55 c                   if        b_avbr = *off
5.55 c                   leavesr
5.55 c                   endif
5.55  *
5.55 c                   eval      b_forn = *on
7.02  *
7.02  * Ny runde for å fjerne poster også for plukkliste kjørt.
7.02  *
7.02 c                   eval      w_aarr = %subdt(b1bdat:*years)
7.02 c                   eval      floglu_aarr = w_aarr
7.02 c                   eval      floglu_numm = b1numm
7.02 c                   eval      floglu_suff = b1suff
7.02 c                   eval      floglu_kode = '3'
7.02 c                   eval      floglu_date = *hival
7.02 c                   eval      floglu_time = *hival
7.02 c     floglu_key    setll     floglur
7.02 c                   readp     floglur
7.02 c                   if        %eof
7.02 c                   goto      end_pluk
7.02 c                   endif
7.02  *
7.02 c                   if        fuaarr = w_aarr and
7.02 c                             funumm = b1numm and
7.02 c                             fusuff = b1suff and
7.02 c                             fukode = '3'
7.02 c                   delete    floglur
7.02 c                   endif
7.02  *
7.02 c     end_pluk      tag
5.55  *
5.55  * Fjerner post som sier at ordren er klar til plukking
5.55  *
5.62 c                   eval      w_aarr = %subdt(b1bdat:*years)
5.62 c                   eval      floglu_aarr = w_aarr
5.55 c                   eval      floglu_numm = b1numm
5.55 c                   eval      floglu_suff = b1suff
5.55 c                   eval      floglu_kode = '2'
5.55 c                   eval      floglu_date = *hival
5.55 c                   eval      floglu_time = *hival
5.55 c     floglu_key    setll     floglur
5.55 c                   readp     floglur
5.55 c                   if        %eof
5.55 c                   leavesr
5.55 c                   endif
5.55  *
5.62 c                   if        fuaarr = w_aarr and
5.55 c                             funumm = b1numm and
5.55 c                             fusuff = b1suff and
5.55 c                             fukode = '2'
5.55 c                   delete    floglur
5.55 c                   endif
5.55  *
5.55 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å skrive plukkliste samt å bestemme hvem som skal
      * plkke ordren
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     send_plukking begsr
      *
      * Dersom ordre er opptatt kalles program som viser hvem som bruker
      * orderen
      *
     c                   exsr      sjekk_ordre
     c                   if        b_oppt = *on
     c                   call      'FO710R'
     c                   parm                    w_firm
6.nu c                   parm                    b1numm
     c                   parm                    b1suff
     c                   leavesr
     c                   endif
6.31  *
6.31  * Sjekk at ordren ikke allerede er sendt til plukking.
6.31  *
6.31 c                   exsr      sjk_ord_plk
6.31 c                   if        b_oppt = *on
6.31 c                   eval      w_meld = 'Ordren er allerede sendt plukking'
6.31 c                   call      'AA005R'
6.31 c                   parm                    w_meld
6.31 c                   leavesr
6.31 c                   endif
      *
      * Håndterer skjermbilde
      *
     c                   exsr      xc2win
     c                   if        b_pluk = *off
     c                   leavesr
     c                   endif
      *
      * Oppdaterer ordre med plukker-informasjon
      *
     c                   eval      fohelu_numm = b1numm
     c                   eval      fohelu_suff = b1suff
     c     fohelu_key    chain     fohelu
     c                   if        not %found
     c                   leavesr
     c                   endif
      *
      *>                 time                    foplda
      *>                 time                    foplti
     c                   eval      foplse = c2plse
      *
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = l_user
     c                   update    fohelur
      *
      * Skriver plukkliste
      *
     c                   eval      w_retu = *blank
      *
     c                   call      'FO600R'
     c                   parm                    w_retu
     c                   parm                    w_firm
6.nu c                   parm                    b1numm
     c                   parm                    b1suff
      *
      * Blanker plukker-informasjon fra ordre-hode dersom utskriften er
      * avbrutt
      *
     c                   if        w_retu <> *blank
     c                   eval      fohelu_numm = b1numm
     c                   eval      fohelu_suff = b1suff
     c     fohelu_key    chain     fohelu
5.54  *>                 eval      foplda = *loval
5.54  *>                 eval      foplti = *loval
     c                   eval      foplse = 0
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = l_user
     c                   update    fohelur
     c                   endif
5.54  *
5.54  * Oppdaterer loggfilen med "Sendt til plukking"
5.54  * dersom utskriften ikke er avbrudt
5.54  *
5.54 c                   if        w_retu = *blank
5.54 c                   eval      d_firm = l_firm
5.54 c                   eval      d_user = l_user
5.54 c                   eval      d_wsid = l_wsid
5.54 c                   eval      d_numm = b1numm
5.54 c                   eval      d_suff = b1suff
5.54 c                   eval      d_kode = '4'
5.54 c                   time                    d_date
5.54 c                   time                    d_time
     c                   eval      d_aarr = %subdt(b1bdat:*years)
5.54  *
5.54  * Kaller program for oppdatering
5.54  *
5.54 c                   call      'FU900R'
5.54 c                   parm                    w_stat
5.54 c                   parm                    d_urec
5.54 c                   endif
      *
     c                   eval      b_forn = *on
      *
     c                   endsr
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  * Sjekker om ordren allerede er til behandling
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  *
6.31 c     sjk_ord_plk   begsr
6.31  *
6.31 c                   eval      b_oppt = *off
6.31  *
6.31  * Sjekk om den alllerede er sendt til plukking.
6.31  *
6.31 c                   eval      floglr_aarr = %subdt(b1bdat:*years)
6.31 c                   eval      floglr_numm = b1numm
6.31 c                   eval      floglr_suff = b1suff
6.31 c                   eval      floglr_kode = '4'
6.31 c                   eval      floglr_date = w_dato
6.31 c     floglr_key    setll     floglr
6.31 c     floglr_key    reade     floglr
6.31 c                   dow       not %eof
6.31 c                   if        %found and
6.31 c                             xxkode = '4'
6.31 c                   eval      b_oppt = *on
6.31 c                   endif
6.31 c     floglr_key    reade     floglr
6.31 c                   enddo
6.33  *
6.33  * hvis feil er satt,så sjekk om siste kode = 2,daqer det lov
6.33 c                   eval      w_kod2 = *blank
6.33 c                   call      'FU706R'
6.33 c                   parm                    floglr_aarr
6.33 c                   parm                    b1numm
6.33 c                   parm                    b1suff
6.33 c                   parm                    w_kod2
6.33  *
6.33 c                   if        w_kod2 = '2'
6.31 c                   eval      b_oppt = *off
6.33 c                   endif
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker om ordren allerede er til behandling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_ordre   begsr
      *
     c                   eval      b_oppt = *off
      *
      * Sjekker om ordren er i bruk
      *
     c                   eval      fohelr_numm = b1numm
     c                   eval      fohelr_suff = b1suff
     c     fohelr_key    chain     fohelr
     c                   if        %found and
     c                             fokode <> 0
     c                   eval      b_oppt = *on
     c                   endif
      *
     c                   endsr
5.60  *
5.60  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.60  * Sjekker om lager finnes på ordrelinje
5.60  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.60  *
5.60 c     xsjekk_lag    begsr
5.60  *
5.60  * Sjekker om oppgitt lagernummer er representert på
5.60  * en av varelinjene på ordren
5.60  *
5.60 c                   eval      b_funn = ' '
5.60 c                   eval      fodtl1_numm = fonumm
5.60 c                   eval      fodtl1_suff = fosuff
5.60 c     fodtl1_key    setll     fodtl1
5.60 c     fodtl1_key    reade     fodtl1
5.60  *
5.60 c                   dow       not %eof
5.60 c                   if        fdlage = f1lage
5.60 c                   eval      b_funn = '1'
5.60 c                   leave
5.60 c                   endif
5.60 c     fodtl1_key    reade     fodtl1
5.60 c                   enddo
5.60  *
5.60 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * C2 Behandle bilde for tildeling av plukke-ansvar
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2win        begsr
      *
     c                   eval      b_pluk = *off
      *
     c                   eval      fohel1_numm = b1numm
     c                   eval      fohel1_suff = b1suff
     c     fohel1_key    chain     fohel1r
     c                   if        not %found
     c                   leavesr
     c                   endif
      *
     c                   eval      c2numm = fonumm
     c                   eval      c2suff = fosuff
     c                   eval      c2knav = fokuna
7.03 c                   eval      c2kjor = fokjor
     c                   eval      c2pnav = *blank
     c                   eval      c2plse = 0
     c                   eval      c2snav = *blank
7.03 c                   eval      c2kjna = *blank
      *
     c                   eval      rkunl1_kund = fokund
     c     rkunl1_key    chain     rkunl1r
     c                   if        %found
     c                   eval      c2knav = rknavn
     c                   endif
      *
     c                   if        fokpro <> 0
     c                   eval      fkprl1_kund = fokund
     c                   eval      fkprl1_kpro = fokpro
     c     fkprl1_key    chain     fkprl1r
     c                   if        %found
     c                   eval      c2pnav = flnavn
     c                   endif
     c                   endif
7.03  *
7.03  *  henter kjørerute
7.03  *
7.03 c                   if        fokjor <> 0
7.03 c                   eval      ra25l1_yrut = fokjor
7.03 c     ra25l1_key    chain     ra25l1r
7.03 c                   if        %found(ra25l1)
7.03 c                   movel     raytxt        c2kjna
7.03 c                   endif
7.03 c                   endif
      *
     c     c2taga        tag
      *
     c                   write     c2win
     c                   exfmt     c2win
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
      *
     c                   if        w_afld = 'C2PLSE'
     c                   call      'RA509R'
     c                   parm                    w_firm
     c                   parm                    c2plse
     c                   parm                    c2snav
     c                   endif
7.03 c**                 goto      c2taga
7.03 c**                 endif
      *
7.03 c                   if        w_afld = 'C2KJOR'
7.03 c                   call      'RA525R'
7.03 c                   parm                    w_firm
7.03 c                   parm                    c2kjor
7.03 c                   parm                    c2kjna
7.03 c                   endif
      *
7.03 c                   goto      c2taga
7.03 c                   endif
      *
      * Validering
      *
     c*                  if        c2plse = 0
     c*                  eval      *in31 = *on
     c*                  goto      c2taga
     c*                  endif
      *
     c                   if        c2plse <> 0
     c                   eval      ra09l1_ikod = c2plse
     c     ra09l1_key    chain     ra09l1
     c                   if        not %found
     c                   eval      *in32 = *on
     c                   goto      c2taga
     c                   endif
     c                   endif
7.03  *
7.03  * validerer kjørerute hvis fors.måte = 25
7.03  *
7.03 c                   if        folmat = 25 or
7.03 c                             c2kjor <> 0
7.03 c                   eval      ra25l1_yrut = c2kjor
7.03 c     ra25l1_key    chain     ra25l1
7.03 c                   if        not %found
7.03 c                   eval      *in33 = *on
7.03 c                   goto      c2taga
7.03 c                   endif
7.03 c                   endif
7.03  *
7.03  * oppdaterer ordre med kjørerute
7.03  *
7.03 c                   eval      fohelu_numm = b1numm
7.03 c                   eval      fohelu_suff = b1suff
7.03 c     fohelu_key    chain     fohelur
7.03 c                   if        %found(fohelu)
7.03 c                   eval      fokjor = c2kjor
7.03 c                   time                    foedat
7.03 c                   time                    foetim
7.03 c                   eval      foeusr = l_user
7.03 c                   update    fohelur
7.03 c                   endif
      *
     c                   eval      b_pluk = *on
      *
     c                   endsr
5.55  *
5.55  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.55  * C4 Behandle bilde for avbryting av plukking
5.55  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.55  *
5.55 c     xc4win        begsr
5.55  *
5.55 c                   eval      b_avbr = *off
5.55  *
5.55 c                   eval      fohel1_numm = b1numm
5.55 c                   eval      fohel1_suff = b1suff
5.55 c     fohel1_key    chain     fohel1r
5.55 c                   if        not %found
5.55 c                   leavesr
5.55 c                   endif
5.55  *
5.55 c                   eval      c4numm = fonumm
5.55 c                   eval      c4suff = fosuff
5.55 c                   eval      c4knav = fokuna
5.55 c                   eval      c4pnav = *blank
5.55 c                   eval      c4upda = 0
5.55 c                   eval      c4upti = 0
5.55 c                   eval      c4upus = *blank
5.55 c                   eval      c4upws = *blank
5.55  *
5.55 c                   eval      rkunl1_kund = fokund
5.55 c     rkunl1_key    chain     rkunl1r
5.55 c                   if        %found
5.55 c                   eval      c4knav = rknavn
5.55 c                   endif
5.55  *
5.55 c                   if        fokpro <> 0
5.55 c                   eval      fkprl1_kund = fokund
5.55 c                   eval      fkprl1_kpro = fokpro
5.55 c     fkprl1_key    chain     fkprl1r
5.55 c                   if        %found
5.55 c                   eval      c4pnav = flnavn
5.55 c                   endif
5.55 c                   endif
5.55  *
5.55 c                   eval      w_kode = '2'
5.62 c                   eval      w_aarr = %subdt(b1bdat:*years)
5.55 c                   call      'FU704R'
5.62 c                   parm                    w_aarr
6.nu c                   parm                    b1numm
5.55 c                   parm                    b1suff
5.55 c                   parm                    w_kode
5.55 c                   parm                    w_svar
5.55 c                   parm                    w_date
5.55 c                   parm                    w_time
5.55 c                   parm                    w_user
5.55 c                   parm                    w_wsid
5.55  *
5.55 c                   if        w_svar = 'J'
5.55  *
5.55 c                   if        w_date <> *loval
5.55 c     *dmy          move      w_date        c4upda
5.55 c                   endif
5.55 c                   if        w_time <> *loval
5.55 c     *hms          move      w_time        c4upti
5.55 c                   endif
5.55 c                   eval      c4upus = w_user
5.55 c                   eval      c4upws = w_wsid
5.55 c                   endif
5.55  *
5.55 c     c4taga        tag
5.55  *
5.55 c                   write     c4win
5.55 c                   exfmt     c4win
5.55  *
5.55  * Behandling av funksjonstaster
5.55  *
5.55 c                   if        *inkc or *inkl
5.55 c                   leavesr
5.55 c                   endif
5.55  *
5.55  * Spørring
5.55  *
5.55  *
5.55  * Validering
5.55  *
5.55 c                   eval      b_avbr = *on
5.55  *
5.55 c                   endsr
      *
7.fb  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.fb  * Subrutine for å sjekke om linje har ubetalt depositumskrav
7.fb  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.fb  *
7.fb c     sjekk_depo    begsr
7.fb c                   eval      b_depo = *off
7.fb c                   eval      *in45  = *off
7.fb c                   eval      sdepl1_numm = b1numm
7.fb c                   eval      sdepl1_kund = b1kund
7.fb c                   eval      sdepl1_tell = 0
7.fb c     sdepl1_key    chain     sdepl1r
7.fb c                   if        %found(sdepl1) and
7.fb c                             skdepo > skdepb
7.fb c                   eval      b_depo = *on
7.fb c                   endif
7.fb  *
7.fb c                   endsr
7.fb  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på ordre-hode
      *
     c     fohelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelr_numm
     c                   kfld                    fohelr_suff
      *
      * Key for posisjonering på ordrenr
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
5.61  *
5.61  * Key for posisjonering på kundenavn
5.61  *
5.61 c     fohela_key    klist
5.61 c                   kfld                    w_firm
5.61 c                   kfld                    fohela_kuna
      *
      * Key for posisjonering på kjørerute
      *
     c     fohelb_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelb_kjor
     c                   kfld                    fohelb_kjnr
      *
      * Key for posisjonering på leveringsdato
      *
     c     foheln_key    klist
     c                   kfld                    w_firm
     c                   kfld                    foheln_ldat
     c                   kfld                    foheln_otyp
     c                   kfld                    foheln_kjor
     c                   kfld                    foheln_kjnr
      *
      * Key for posisjonering på ordretype
      *
     c     fohelo_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelo_otyp
      *
      * Key for oppslag på ofak kode-register
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på kunde-prosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på selger
      *
     c     ra09l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra09l1_ikod
      *
      * Key for oppslag på ordretype
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for oppslag på ordre informasjonskode
      *
     c     vinfl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vinfl1_ityp
      *
      * Key for oppdatering av ordre-hode
      *
     c     fohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelu_numm
     c                   kfld                    fohelu_suff
5.60  *
5.60  * Key for ordrelinje-register
5.60  *
5.60 c     fodtl1_key    klist
5.60 c                   kfld                    w_firm
5.60 c                   kfld                    fodtl1_numm
5.60 c                   kfld                    fodtl1_suff
5.55  *
5.55  * Key for oppdatering av logg-filen
5.55  *
5.55 c     floglu_key    klist
5.55 c                   kfld                    w_firm
5.62 c                   kfld                    floglu_aarr
5.55 c                   kfld                    floglu_numm
5.55 c                   kfld                    floglu_suff
5.55 c                   kfld                    floglu_kode
5.55 c                   kfld                    floglu_date
5.55 c                   kfld                    floglu_time
6.31  *
6.31  * Key for les av logg-filen
6.31  *
6.31 c     floglr_key    klist
6.31 c                   kfld                    w_firm
6.31 c                   kfld                    floglr_aarr
6.31 c                   kfld                    floglr_numm
6.31 c                   kfld                    floglr_suff
6.31 c                   kfld                    floglr_kode
6.31 c                   kfld                    floglr_date
5.64  *
5.64  * Key for les av bruker
5.64  *
5.64 c     ausrlr_key    klist
5.64 c                   kfld                    ausrlr_user
6.30  *
6.30  * Key for oppslag på forsendelsesmåte
6.30  *
6.30 c     ra17l1_key    klist
6.30 c                   kfld                    w_firm
6.30 c                   kfld                    ra17l1_qkod
7.03  *
7.03  * Key for oppslag på kjørerute
7.03  *
7.03 c     ra25l1_key    klist
7.03 c                   kfld                    w_firm
7.03 c                   kfld                    ra25l1_yrut
7.fb  *
7.fb  * Key for oppslag på depositum-fil
7.fb  *
7.fb c     sdepl1_key    klist
7.fb c                   kfld                    w_firm
7.fb c                   kfld                    sdepl1_numm
7.fb c                   kfld                    sdepl1_kund
7.fb c                   kfld                    sdepl1_tell
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
6.31 c                   time                    w_dato
5.64  *
5.64  * hente lager fra bruker
5.64  *
5.64 c                   eval      ausrlr_user = l_user
5.64 c     ausrlr_key    chain     ausrlr
5.64 c                   if        %found(ausrlr)
5.64 c                   eval      f1lage = ablagr
5.64 c                   endif
5.64 c
      *
      * Hente inn ofak kode-register
      *
     c     fstsl1_key    chain     fstsl1r
      *
      * Initierer skjermbilde
      *
     c                   eval      *in22  = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      foheln_ldat = *loval
     c                   eval      foheln_otyp = *blank
     c                   eval      foheln_kjor = 0
     c                   eval      foheln_kjnr = 0
     c     foheln_key    setll     foheln
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
