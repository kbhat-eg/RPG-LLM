     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FP720R
      * Beskrivelse . . : Generere spesialpriser utfra tilbud
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       220399 AMD  Nytt program
      * R3.10 040200 AMD  Kvitterer ut med kundenavn og kundeprosjekt
      *                   Lagt på F8 for oppdatering av spes.priser
      *                   (Enter kunne bli litt for kjapp)
3.31  * R3.31 230501 AMD  Lagt inn mulighet for å legge på rab.kategori
3.31  *       130405 HKO  Lagt inn valg av alle enheter
5.41  *       190406 AMD  Flere enheter ble ikke lagt ut etter at salgs-
      *                   enhet ble forandret fra flagg til rekkefølge.
      *                   Dette er nå korrigert.
5.60  *       090108 bof  Kundeprosjekt utvidet med kjørerute
5.70  * PRGR  151108 bof  Utvidet med prisgruppe
      * 6.10  250609 BSA  Oppdatert med sporingsinformasjon
6.20  * R6.20 111013 BKV  Rettet feil ved write av spesialpris pga prisgr
6.30  * R6.20 260114 bof  Krav at må fylle ut fra /til dato
6.31  * R6.20 270214 bof  Hvis kundeprosjekt,skal tildato fylles ut.
6.nu  * R6.30 270514 bsa  Programmet gjennomgått mtp nummerutvidelser.
6.32  * R6.20 220814 bkv  Legger inn default dagens dato på fra (kom. kode)
6.33  * R6.30 230617 nho  Må hente enhet fra VA hvis den ikke finnes i
      *                   vvenpf
7.01  * R7.00 051119 bkv  Legger inn valgt infotype
7.00  * R7.00 060320 bkv  132 bilde
7.02  *       210820 bof  Feilretting ved å finne enheter fra VA.
8.01  *PRG2   100622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffprilu    uf a e           k disk    rename(fpripfr:fprilur)
     ffpril1    if   e           k disk    rename(fpripfr:fpril1r)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
3.31 fra06pf    if   e           k disk    rename(ra06pfr:ra06pfr)
     fvvenl2    if   e           k disk    rename(vvenpfr:vvenl2r)
6.33 fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
6.33 fjvpkl1    if   e           k disk    rename(jvpkpfr:jvpkl1r)
7.01 ffohelu    uf a e           k disk    rename(fohepfr:fohelur)
7.01 fvinfl1    if   e           k disk    rename(vinfpfr:vinfl1r)
      *
     ffp720d    cf   e             workstn
      *
      * Definering av array
      *
     d a_meld          s             50    dim(2) ctdata perrcd(1)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
7.00 d l_filg                931    933
     d l_firm                944    946  0
      *
      * Definering av parametre
      *
     d p_retu          s              1
6.nu d p_numm          s                   like(fonumm)
     d p_suff          s                   like(fosuff)
      *
      * Definering av variable i nøkler
      *
5.70 d fprilu_prgr     s                   like(fpprgr)
     d fprilu_rkat     s                   like(fprkat)
     d fprilu_kkat     s                   like(fpkkat)
     d fprilu_kund     s                   like(fpkund)
     d fprilu_kpro     s                   like(fpkpro)
     d fprilu_vare     s                   like(fpvare)
     d fprilu_enhe     s                   like(fpenhe)
     d fprilu_lety     s                   like(fplety)
5.70 d fpril1_prgr     s                   like(fpprgr)
     d fpril1_rkat     s                   like(fprkat)
     d fpril1_kkat     s                   like(fpkkat)
     d fpril1_kund     s                   like(fpkund)
     d fpril1_kpro     s                   like(fpkpro)
     d fpril1_vare     s                   like(fpvare)
     d fpril1_enhe     s                   like(fpenhe)
     d fpril1_lety     s                   like(fplety)
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d fodtl1_numm     s                   like(fdnumm)
     d fodtl1_suff     s                   like(fdsuff)
     d rkunl1_kund     s                   like(rkkund)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
3.31 d ra06pf_fkod     s                   like(rafkod)
     d vvenl2_vare     s                   like(vevare)
     d vvenl2_saen     s                   like(vesaen)
6.33 d jvarl1_vare     s                   like(jvvare)
6.33 d jvpkl1_vare     s                   like(jwvare)
6.33 d jvpkl1_ldor     s                   like(jwldor)
6.33 d jvpkl1_pakn     s                   like(jwpakn)
7.01 d vinfl1_ityp     s                   like(vaityp)
6.33 d b_pakn          s              1    inz(*off)
6.33 d w_dato          s               d   datfmt(*iso)
      *
      * Definering av variable
      *
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(fpfirm)
5.70 d w_prgr          s                   like(fpprgr)
     d w_rkat          s                   like(fprkat)
     d w_kund          s                   like(fpkund)
     d w_kpro          s                   like(fpkpro)
     d w_vare          s                   like(fpvare)
     d w_enhe          s                   like(fpenhe)
     d w_lety          s                   like(fplety)
     d w_knav          s                   like(rknavn)
     d w_pnav          s                   like(flnavn)
     d w_fdat          s                   like(fpfdat)
     d w_tdat          s                   like(fptdat)
     d w_udat          s               d   datfmt(*iso)
     d w_enh1          s                   like(veenhe)
     d w_enh2          s                   like(veenhe)
     d w_enh3          s                   like(veenhe)
     d w_omr1          s                   like(veomre)
     d w_omr2          s                   like(veomre)
     d w_omr3          s                   like(veomre)
     d w_sap1          s                   like(fpsapr)
     d w_sap2          s                   like(fpsapr)
     d w_sap3          s                   like(fpsapr)
     d w_kop1          s                   like(fpkopr)
     d w_kop2          s                   like(fpkopr)
     d w_kop3          s                   like(fpkopr)
      *
     d w_adr1          s                   like(fladr1)
     d w_adr2          s                   like(fladr2)
     d w_sted          s                   like(fosted)
     d w_kprs          s                   like(flkprs)
     d w_tlfa          s                   like(fltlfa)
     d w_tlfp          s                   like(fltlfp)
     d w_tlfm          s                   like(fltlfm)
     d w_mail          s                   like(flmail)
     d w_orka          s                   like(florka)
     d w_okun          s                   like(flokun)
     d w_okpr          s                   like(flokpr)
     d w_opda          s                   like(flopda)
     d w_proj          s                   like(flproj)
     d w_kjor          s                   like(flkjor)
     d w_kjnr          s                   like(flkjnr)
3.31 d w_rtxt          s                   like(raftxt)
7.01 d w_ityp          s                   like(foityp)
      *
     d w_mld1          s             50
     d w_mld2          s             50
     d w_svar          s              1
     d w_in12          s              1  0
      *
7.00   dcl-s co402_verdi1  char(1);
7.00   dcl-s co402_verdi2  char(2048);
7.00   DCL-PR CO402R EXTPGM('CO402R');
7.00     p_filgrp            char(3)     const;
7.00     p_firm              packed(3:0) const;
7.00     p_lager             packed(2:0) const;
7.00     p_nokkel            char(50)    const;
7.00     p_verdi1            char(1);
7.00     p_verdi2            char(2048);
7.00   END-PR;
7.01 d u_701           s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * a1win  Bilde for valg av ny kunde og binding av kostpris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   exsr      initier
      *
     c     a1taga        tag
      *
     c                   write     a1win
     c                   exfmt     a1win
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   when      *inkg
     c                   exsr      spesial
     c                   goto      a1taga
     c                   endsl
      *
      * Sjekker input
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      a1taga
     c                   endif
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker om det finnes spesialpriser fra før
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      * Dersom F8 er tatt, kjør videre med oppdatering
      * ellers gå tilbake til skjermbildet
      *
     c                   if        *inkh  = *off
     c                   goto      a1taga
     c                   endif
     c                   eval      w_kund =  fokund
     c                   eval      w_kpro =  fokpro
      *
      * Dersom kunde eller kundeprosjekt er
      * overstyrt, skal disse benyttes
      *
     c                   if        a1kund <> 0
     c                   eval      w_kund =  a1kund
     c                   endif
      *
     c                   if        a1kpro <> 0
     c                   eval      w_kpro =  a1kpro
     c                   endif
3.31  *
3.31  * Dersom rabatt-kategori er lagt på,
3.31  * skal denne benyttes.
3.31  * (Kan ikke kombineres med kunde/prosjekt)
3.31  *
3.31 c                   eval      w_rkat =  0
3.31 c                   if        a1rkat <> 0
3.31 c                   eval      w_rkat =  a1rkat
3.31 c                   eval      w_kund =  0
3.31 c                   eval      w_kpro =  0
3.31 c                   endif
      *
5.70 c                   eval      fpril1_prgr = w_prgr
3.31 c                   eval      fpril1_rkat = w_rkat
     c                   eval      fpril1_kkat = 0
     c                   eval      fpril1_kund = w_kund
     c                   eval      fpril1_kpro = w_kpro
      *
     c     fpril1_key    chain     fpril1r                            94
      *
      * Poster finnes fra før,
      * skal endringer/tillegg oppdateres ?
      *
     c                   if        *in94  = *off
     c                   eval      w_mld1 = a_meld(1)
     c                   eval      w_mld2 = a_meld(2)
     c                   eval      w_svar = 'N'
     c                   eval      w_in12 = 0
      *
     c                   call      'AA007R'
     c                   parm                    w_mld1
     c                   parm                    w_mld2
     c                   parm                    w_svar
     c                   parm                    w_in12
      *
      * F12 er tatt
      *
     c                   if        w_in12 = 1
     c                   goto      a1taga
     c                   endif
      *
     c                   if        w_svar = 'N'
     c                   goto      a1taga
     c                   endif
      *
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Leser ordrelinjer og legger over til spesialpriser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      fodtl1_numm = p_numm
     c                   eval      fodtl1_suff = p_suff
     c     fodtl1_key    setll     fodtl1
     c     fodtl1_key    reade     fodtl1                                 90
      *
     c                   dow       *in90 = *off
      *
     c                   exsr      oppdater
      *
     c     fodtl1_key    reade     fodtl1                                 90
      *
     c                   enddo
7.01 c                   if        u_701 = *on
7.01 c     fohel1_key    chain     fohelur
7.01 c                   if        %found(fohelu)
7.01 c                   if        fokode = 0 and foityp = *blank
7.01 c                   eval      foityp = w_ityp
7.01 c                   update    fohelur
7.01 c                   else
7.01 c                   unlock    fohelu
7.01 c                   endif
7.01 c                   endif
7.01 c                   endif                                                  u_701
      *
      *
     c                   eval      a1meld = 'Overført'
     c                   goto      a1taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av verdier i skjermbilde
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     initier       begsr
      *
     c***>               if        fotkod = 'U'
     c                   if        fotdat <> *loval
     c     *dmy          move      fotdat        a1tdat
     c                   endif
     c***>               endif
      *
     c                   eval      a2kund = fokund
     c                   eval      a2kpro = fokpro
      *
      * Hent inn kundenavn
      *
     c                   eval      rkunl1_kund = a2kund
     c     rkunl1_key    chain     rkunl1                             90
     c                   if        *in90  = *off
     c                   eval      a2knav = rknavn
     c                   endif
      *
      * Hent inn navn på kundeprosjekt
      *
     c                   if        a2kpro <> 0
     c                   eval      fkprl1_kund = a2kund
     c                   eval      fkprl1_kpro = a2kpro
     c     fkprl1_key    chain     fkprl1                             91
     c                   if        *in91  = *off
     c                   eval      a2pnav = flnavn
6.31 c                   if        fltdat <> *loval
6.31 c     *dmy          move      fltdat        a1tdat
6.31 c                   endif
     c                   endif
     c                   endif
      *
      * Legg inn forklarende informasjon
      *
     c                   eval      a1knav = ' Blank = samme'
     c                   eval      a1pnav = ' Blank = samme'
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Kunde
      *
     c                   if        w_afld = 'A1KUND'
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    a1kund
     c                   parm                    w_knav
     c                   goto      end_spØrr
     c                   endif
      *
      * Kundeprosjekt
      *
     c                   if        w_afld = 'A1KPRO'
     c                   call      'FL501R'
     c                   parm                    w_firm
     c                   parm                    a1kund
     c                   parm                    fobdat
     c                   parm                    a1kpro
     c                   parm                    w_pnav
     c                   parm                    w_adr1
     c                   parm                    w_adr2
     c                   parm                    w_sted
     c                   parm                    w_kprs
     c                   parm                    w_tlfa
     c                   parm                    w_tlfp
     c                   parm                    w_tlfm
     c                   parm                    w_mail
     c                   parm                    w_orka
     c                   parm                    w_okun
     c                   parm                    w_okpr
     c                   parm                    w_opda
     c                   parm                    w_proj
     c                   parm                    w_kjor
     c                   parm                    w_kjnr
     c                   goto      end_spØrr
     c                   endif
3.31  *
3.31  * Rabattkategori
3.31  *
3.31 c                   if        w_afld = 'A1RKAT'
3.31 c                   call      'RA506R'
3.31 c                   parm                    w_firm
3.31 c                   parm                    a1rkat
3.31 c                   parm                    w_rtxt
3.31 c                   goto      end_spØrr
3.31 c                   endif
      *
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å kalle opp program for spesialpriser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spesial       begsr
      *
      * Spesialpriser
      *
     c                   call      'FP100R'
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker inntastede verdier, og henter beskrivelser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
3.31 c                   eval      w_rtxt = *blank
3.31 c                   eval      a1rnav = *blank
3.31 c                   eval      a1knav = ' Blank = samme'
3.31 c                   eval      a1pnav = ' Blank = samme'
      *
      * Kunde
      *
     c                   if        a1kund <> 0
     c                   eval      rkunl1_kund = a1kund
     c     rkunl1_key    chain     rkunl1                             90
     c                   if        *in90  = *on
     c                   eval      *in31  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
      * Kundeprosjekt
      *
     c                   if        a1kpro <> 0
     c                   eval      fkprl1_kund = a1kund
     c                   eval      fkprl1_kpro = a1kpro
     c     fkprl1_key    chain     fkprl1                             91
     c                   if        *in91  = *on
     c                   eval      *in32  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
3.31  *
3.31  * Rabattkategori og kunde kan ikke oppgis samtidig
3.31  *
3.31 c                   if        a1kund <> 0
3.31 c                   if        a1rkat <> 0
3.31 c                   eval      *in33 = *on
3.31 c                   eval      b_feil = *on
3.31 c                   goto      end_sjekk
3.31 c                   endif
3.31 c                   endif
3.31  *
3.31  * Rabattkategori
3.31  *
3.31 c                   if        a1rkat <> 0
3.31 c                   eval      ra06pf_fkod = a1rkat
3.31 c     ra06pf_key    chain     ra06pf                             92
3.31 c                   if        *in92 = *on
3.31 c                   eval      *in34  = *on
3.31 c                   eval      b_feil = *on
3.31 c                   goto      end_sjekk
3.31 c                   endif
3.31 c                   eval      w_rtxt = raftxt
3.31 c                   endif
      *
      * Kontollere inntastet periode fra - til
6.32  *
6.32  * krav at den må fylles ut.
6.32 c*                  if        a1fdat =  0
6.32 c*                  eval      *in38  = *on
6.32 c*                  eval      b_feil = *on
6.32 c*                  goto      end_sjekk
6.32 c*                  endif
      *
     c                   if        a1fdat <> 0
     c     *dmy          test(d)                 a1fdat                 35
     c                   if        *in35  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
     c                   if        a1tdat <> 0
     c     *dmy          test(d)                 a1tdat                 36
     c                   if        *in36 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
     c                   if        a1tdat <> 0 and
     c                             a1fdat = 0
     c     *dmy          move      w_udat        a1fdat
     c                   endif
      *
     c                   if        a1fdat <> 0
     c     *dmy          move      a1fdat        w_fdat
     c                   if        a1tdat <> 0
     c     *dmy          move      a1tdat        w_tdat
     c                   else
     c                   eval      w_tdat = *loval
     c                   endif
     c                   if        w_fdat > w_tdat
     c                   eval      *in37 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
      * Hent inn kundenavn
      *
     c                   eval      rkunl1_kund = a1kund
     c     rkunl1_key    chain     rkunl1                             90
     c                   if        *in90  = *off
     c                   eval      a1knav = rknavn
     c                   endif
      *
      * Hent inn navn på kundeprosjekt
      *
     c                   if        a1kpro <> 0
     c                   eval      fkprl1_kund = a1kund
     c                   eval      fkprl1_kpro = a1kpro
     c     fkprl1_key    chain     fkprl1                             91
     c                   if        *in91  = *off
     c                   eval      a1pnav = flnavn
     c                   endif
     c                   endif
3.31  *
3.31  * Hent inn navn på rabattkategori
3.31  *
3.31 c                   if        a1rkat <> 0
3.31 c                   eval      ra06pf_fkod = a1rkat
3.31 c     ra06pf_key    chain     ra06pf                             92
3.31 c                   if        *in92  = *off
3.31 c                   eval      a1rnav = raftxt
3.31 c                   endif
3.31 c                   endif
      *
     c     end_sjekk     endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdatere spesial-priser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdater      begsr
      *
     c                   if        fdvare = *blank
     c                   leavesr
     c                   endif
      *
      * Nullstill record og legg inn nøkler
      *
     c                   clear                   fprilur
      *
5.70 c                   eval      fprilu_prgr = fdprgr
3.31 c                   eval      fprilu_rkat = w_rkat
     c                   eval      fprilu_kkat = 0
     c                   eval      fprilu_kund = w_kund
     c                   eval      fprilu_kpro = w_kpro
     c                   eval      fprilu_vare = fdvare
     c                   eval      fprilu_enhe = fdenh1
     c                   eval      fprilu_lety = fdlety
     c     fprilu_key    chain     fprilur
      *
     c                   eval      fpsapr = fdsapr
     c                   eval      fprab1 = fdrab1
     c                   eval      fprab2 = fdrab2
     c                   eval      fpkopr = 0
     c                   eval      fpnumm = fonumm
     c                   eval      fpeusr = l_user
      *
     c                   eval      fpfdat = *loval
     c                   eval      fptdat = *loval
      *
     c                   if        a1fdat <> 0
     c     *dmy          move      a1fdat        fpfdat
     c                   endif
      *
     c                   if        a1tdat <> 0
     c     *dmy          move      a1tdat        fptdat
     c                   endif
      *
      * Legg inn kostpris dersom den skal låses
      *
     c                   if        a1valg = '1'
     c                   eval      fpkopr = fdkopr
     c                   endif
      *
      * Legg inn bare rabatter dersom dette er valgt.
      * Pris nullstilles og rabatt overføres.
      * Dersom rabatt ikke er oppgitt, legges pris inn.
      *
     c                   if        a1raba = '1'
     c                   if        fdrab1 <> 0
     c                   eval      fpsapr =  0
     c                   endif
     c                   endif
      *
     c                   time                    fpetim
     c                   time                    fpedat
      *
     c                   if        %found(fprilu)
6.10 c                   time                    fpedat
6.10 c                   time                    fpetim
6.10 c                   eval      fpeusr = l_user
     c                   update    fprilur
     c                   else
     c                   eval      fpfirm = w_firm
6.20 c                   eval      fpprgr = fdprgr
3.31 c                   eval      fprkat = w_rkat
     c                   eval      fpkkat = 0
     c                   eval      fpkund = w_kund
     c                   eval      fpkpro = w_kpro
     c                   eval      fpvare = fdvare
     c                   eval      fpenhe = fdenh1
     c                   eval      fplety = fdlety
6.10 c                   time                    fpodat
6.10 c                   time                    fpotim
6.10 c                   eval      fpousr = l_user
6.10 c                   time                    fpedat
6.10 c                   time                    fpetim
6.10 c                   eval      fpeusr = l_user
     c                   write     fprilur
     c                   endif
      *
     c                   if        a1enhe <> '1'
     c                   leavesr
     c                   endif
      *
      * Oppdatering dersom alle enheter er valgt
      *
      * Finner tre salgsenheter på varen
      *
     c                   eval      w_enh1 = *blank
     c                   eval      w_enh2 = *blank
     c                   eval      w_enh3 = *blank
     c                   eval      w_omr1 = 0
     c                   eval      w_omr2 = 0
     c                   eval      w_omr3 = 0
      *
     c                   eval      vvenl2_vare = fdvare
     c                   eval      vvenl2_saen = 1
     c     vvenl2_key    setll     vvenl2
5.41 c     vvenl2_key2   reade     vvenl2
5.41 c                   dow       not %eof and
5.41 c                             vesaen > 0
      *
     c                   select
     c                   when      w_enh1 = *blank
     c                   eval      w_enh1 = veenhe
     c                   eval      w_omr1 = veomre
     c                   when      w_enh1 <> *blank and
     c                             w_enh2 = *blank
     c                   eval      w_enh2 = veenhe
     c                   eval      w_omr2 = veomre
     c                   when      w_enh1 <> *blank and
     c                             w_enh2 <> *blank and
     c                             w_enh3 = *blank
     c                   eval      w_enh3 = veenhe
     c                   eval      w_omr3 = veomre
     c                   leave
     c                   endsl
      *
5.41 c     vvenl2_key2   reade     vvenl2
     c                   enddo
      *
6.33  * Hent enhet fra skaffevare dersom den ikke finnes i VVENPF
6.33 c                   if        w_enh1 = *blank
6.33  *
6.33 c                   eval      jvarl1_vare = fdvare
6.33 c     jvarl1_key    chain     jvarl1
6.33 c                   if        %found
6.33 c                   eval      jvpkl1_vare = fdvare
6.33 c                   eval      jvpkl1_ldor = fdbldo
6.33 c     jvpkl1_key    setll     jvpkl1
7.02 c     jvpkl1_key    reade     jvpkl1
6.33 c                   dow       not %eof
6.33 c                   if        jwvare <> fdvare or
6.33 c                             jwldor <> fdbldo
6.33 c                   leave
6.33 c                   endif
6.33  *
6.33  * sjekker fra / tildato
6.33 c                   exsr      sjekk_pkdato
6.33 c                   if        b_pakn = *on
6.33 c                   select
6.33 c                   when      w_enh1 = *blank
6.33 c                   eval      w_enh1 = jwenhe
6.33 c                   eval      w_omr1 = jwofak
6.33 c                   when      w_enh2 = *blank and
6.33 c                             jwenhe <> w_enh1
6.33 c                   eval      w_enh2 = jwenhe
6.33 c                   eval      w_omr2 = jwofak
6.33 c                   when      w_enh3 = *blank and
6.33 c                             jwenhe <> w_enh1 and
6.33 c                             jwenhe <> w_enh2
6.33 c                   eval      w_enh3 = jwenhe
6.33 c                   eval      w_omr3 = jwofak
6.33 c                   leave
6.33 c                   endsl
6.33 c                   endif
7.02 c     jvpkl1_key    reade     jvpkl1
6.33 c                   enddo
6.33  *
6.33 c                   endif
6.33  *
6.33 c                   endif
      * Finner salgspris for alle salgsenheter
      *
     c                   eval      w_sap1 = 0
     c                   eval      w_sap2 = 0
     c                   eval      w_sap3 = 0
     c                   eval      w_kop1 = 0
     c                   eval      w_kop2 = 0
     c                   eval      w_kop3 = 0
      *
     c                   select
     c                   when      fdenh1 = w_enh1
     c                   eval      w_sap1 = fdsapr
     c                   eval      w_sap2 = w_sap1 * w_omr2 / w_omr1
     c                   eval      w_sap3 = w_sap1 * w_omr3 / w_omr1
     c                   eval      w_kop1 = fdkopr
     c                   eval      w_kop2 = w_kop1 * w_omr2 / w_omr1
     c                   eval      w_kop3 = w_kop1 * w_omr3 / w_omr1
     c                   when      fdenh1 = w_enh2
     c                   eval      w_sap2 = fdsapr
     c                   eval      w_sap1 = w_sap2 * w_omr1 / w_omr2
     c                   eval      w_sap3 = w_sap2 * w_omr3 / w_omr2
     c                   eval      w_kop2 = fdkopr
     c                   eval      w_kop1 = w_kop2 * w_omr1 / w_omr2
     c                   eval      w_kop3 = w_kop2 * w_omr3 / w_omr2
     c                   when      fdenh1 = w_enh3
     c                   eval      w_sap3 = fdsapr
     c                   eval      w_sap1 = w_sap3 * w_omr1 / w_omr3
     c                   eval      w_sap2 = w_sap3 * w_omr2 / w_omr3
     c                   eval      w_kop3 = fdkopr
     c                   eval      w_kop1 = w_kop3 * w_omr1 / w_omr3
     c                   eval      w_kop2 = w_kop3 * w_omr2 / w_omr3
     c                   endsl
      *
      * Oppdaterer alternative enheter
      *
      * Enhet-1
      *
     c                   if        w_enh1 <> fdenh1
      *
     c                   eval      fprilu_enhe = w_enh1
     c     fprilu_key    chain     fprilur
      *
     c                   eval      fpsapr = w_sap1
     c                   eval      fprab1 = fdrab1
     c                   eval      fprab2 = fdrab2
     c                   eval      fpkopr = 0
     c                   eval      fpnumm = fonumm
     c                   eval      fpeusr = l_user
      *
     c                   eval      fpfdat = *loval
     c                   eval      fptdat = *loval
      *
     c                   if        a1fdat <> 0
     c     *dmy          move      a1fdat        fpfdat
     c                   endif
      *
     c                   if        a1tdat <> 0
     c     *dmy          move      a1tdat        fptdat
     c                   endif
      *
      * Legg inn kostpris dersom den skal låses
      *
     c                   if        a1valg = '1'
     c                   eval      fpkopr = w_kop1
     c                   endif
      *
      * Legg inn bare rabatter dersom dette er valgt.
      * Pris nullstilles og rabatt overføres.
      * Dersom rabatt ikke er oppgitt, legges pris inn.
      *
     c                   if        a1raba = '1'
     c                   if        fdrab1 <> 0
     c                   eval      fpsapr =  0
     c                   endif
     c                   endif
      *
     c                   time                    fpetim
     c                   time                    fpedat
      *
     c                   if        %found(fprilu)
6.10 c                   time                    fpedat
6.10 c                   time                    fpetim
6.10 c                   eval      fpeusr = l_user
     c                   update    fprilur
     c                   else
     c                   eval      fpfirm = w_firm
6.20 c                   eval      fpprgr = fdprgr
3.31 c                   eval      fprkat = w_rkat
     c                   eval      fpkkat = 0
     c                   eval      fpkund = w_kund
     c                   eval      fpkpro = w_kpro
     c                   eval      fpvare = fdvare
     c                   eval      fpenhe = w_enh1
     c                   eval      fplety = fdlety
6.10 c                   time                    fpodat
6.10 c                   time                    fpotim
6.10 c                   eval      fpousr = l_user
6.10 c                   time                    fpedat
6.10 c                   time                    fpetim
6.10 c                   eval      fpeusr = l_user
     c                   write     fprilur
     c                   endif
      *
     c                   endif
      *
      * Enhet-2
      *
     c                   if        w_enh2 <> *blank and
     c                             w_enh2 <> fdenh1
      *
     c                   eval      fprilu_enhe = w_enh2
     c     fprilu_key    chain     fprilur
      *
     c                   eval      fpsapr = w_sap2
     c                   eval      fprab1 = fdrab1
     c                   eval      fprab2 = fdrab2
     c                   eval      fpkopr = 0
     c                   eval      fpnumm = fonumm
     c                   eval      fpeusr = l_user
      *
     c                   eval      fpfdat = *loval
     c                   eval      fptdat = *loval
      *
     c                   if        a1fdat <> 0
     c     *dmy          move      a1fdat        fpfdat
     c                   endif
      *
     c                   if        a1tdat <> 0
     c     *dmy          move      a1tdat        fptdat
     c                   endif
      *
      * Legg inn kostpris dersom den skal låses
      *
     c                   if        a1valg = '1'
     c                   eval      fpkopr = w_kop2
     c                   endif
      *
      * Legg inn bare rabatter dersom dette er valgt.
      * Pris nullstilles og rabatt overføres.
      * Dersom rabatt ikke er oppgitt, legges pris inn.
      *
     c                   if        a1raba = '1'
     c                   if        fdrab1 <> 0
     c                   eval      fpsapr =  0
     c                   endif
     c                   endif
      *
     c                   time                    fpetim
     c                   time                    fpedat
      *
     c                   if        %found(fprilu)
6.10 c                   time                    fpedat
6.10 c                   time                    fpetim
6.10 c                   eval      fpeusr = l_user
     c                   update    fprilur
     c                   else
     c                   eval      fpfirm = w_firm
6.20 c                   eval      fpprgr = fdprgr
3.31 c                   eval      fprkat = w_rkat
     c                   eval      fpkkat = 0
     c                   eval      fpkund = w_kund
     c                   eval      fpkpro = w_kpro
     c                   eval      fpvare = fdvare
     c                   eval      fpenhe = w_enh2
     c                   eval      fplety = fdlety
6.10 c                   time                    fpodat
6.10 c                   time                    fpotim
6.10 c                   eval      fpousr = l_user
6.10 c                   time                    fpedat
6.10 c                   time                    fpetim
6.10 c                   eval      fpeusr = l_user
     c                   write     fprilur
     c                   endif
      *
     c                   endif
      *
      * Enhet-3
      *
     c                   if        w_enh3 <> *blank and
     c                             w_enh3 <> fdenh1
      *
     c                   eval      fprilu_enhe = w_enh3
     c     fprilu_key    chain     fprilur
      *
     c                   eval      fpsapr = w_sap3
     c                   eval      fprab1 = fdrab1
     c                   eval      fprab2 = fdrab2
     c                   eval      fpkopr = 0
     c                   eval      fpnumm = fonumm
     c                   eval      fpeusr = l_user
      *
     c                   eval      fpfdat = *loval
     c                   eval      fptdat = *loval
      *
     c                   if        a1fdat <> 0
     c     *dmy          move      a1fdat        fpfdat
     c                   endif
      *
     c                   if        a1tdat <> 0
     c     *dmy          move      a1tdat        fptdat
     c                   endif
      *
      * Legg inn kostpris dersom den skal låses
      *
     c                   if        a1valg = '1'
     c                   eval      fpkopr = w_kop3
     c                   endif
      *
      * Legg inn bare rabatter dersom dette er valgt.
      * Pris nullstilles og rabatt overføres.
      * Dersom rabatt ikke er oppgitt, legges pris inn.
      *
     c                   if        a1raba = '1'
     c                   if        fdrab1 <> 0
     c                   eval      fpsapr =  0
     c                   endif
     c                   endif
      *
     c                   time                    fpetim
     c                   time                    fpedat
      *
     c                   if        %found(fprilu)
6.10 c                   time                    fpedat
6.10 c                   time                    fpetim
6.10 c                   eval      fpeusr = l_user
     c                   update    fprilur
     c                   else
     c                   eval      fpfirm = w_firm
6.20 c                   eval      fpprgr = fdprgr
3.31 c                   eval      fprkat = w_rkat
     c                   eval      fpkkat = 0
     c                   eval      fpkund = w_kund
     c                   eval      fpkpro = w_kpro
     c                   eval      fpvare = fdvare
     c                   eval      fpenhe = w_enh3
     c                   eval      fplety = fdlety
6.10 c                   time                    fpodat
6.10 c                   time                    fpotim
6.10 c                   eval      fpousr = l_user
6.10 c                   time                    fpedat
6.10 c                   time                    fpetim
6.10 c                   eval      fpeusr = l_user
     c                   write     fprilur
     c                   endif
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sletter inneliggende poster
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c*    slette        begsr
      *
      * Sletter spesial-priser
      *
     c*                  eval      fprilu_kund = fokund
     c*                  eval      fprilu_kpro = fokpro
     c*    fprilu_ke2    setll     fprilu
     c*    fprilu_ke2    reade     fprilu                                 90
      *
     c*                  dow       *in90 = *off
     c*                  delete    fprilu
     c*    fprilu_ke2    reade     fprilu                                 90
     c*                  enddo
      *
     c*                  endsr
      *
      *
6.33  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.33  * Subrutine for å sjekke om dato er innenfor fra /til dato
6.33  * hvis *loval i begge datoer = ok
6.33  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.33  *
6.33 c     sjekk_pkdato  begsr
6.33  *
6.33 c                   eval      b_pakn = *on
6.33 c                   time                    w_dato
6.33  *
6.33 c                   if        jwfdat = *loval and
6.33 c                             jwtdat = *loval
6.33 c                   leavesr
6.33 c                   endif
6.33  *
6.33 c                   if        jwfdat <= w_dato and
6.33 c                             jwtdat >  w_dato
6.33 c                   leavesr
6.33 c                   endif
6.33  *
6.33 c                   if        jwfdat = *loval  and
6.33 c                             jwtdat >  w_dato
6.33 c                   leavesr
6.33 c                   endif
6.33  *
6.33 c                   if        jwfdat <= w_dato and
6.33 c                             jwtdat =  *loval
6.33 c                   leavesr
6.33 c                   endif
6.33  *
6.33 c                   eval      b_pakn = *off
6.33  *
6.33 c                   endsr
6.33  *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppdatering av spesialpriser
      *
     c     fprilu_key    klist
     c                   kfld                    w_firm
5.70 c                   kfld                    fprilu_prgr
     c                   kfld                    fprilu_rkat
     c                   kfld                    fprilu_kkat
     c                   kfld                    fprilu_kund
     c                   kfld                    fprilu_kpro
     c                   kfld                    fprilu_vare
     c                   kfld                    fprilu_enhe
     c                   kfld                    fprilu_lety
      *
      * Key for sletting av spesialpriser
      *
     c     fprilu_ke2    klist
     c                   kfld                    w_firm
5.70 c                   kfld                    fprilu_prgr
     c                   kfld                    fprilu_rkat
     c                   kfld                    fprilu_kkat
     c                   kfld                    fprilu_kund
     c                   kfld                    fprilu_kpro
      *
      * Key for oppslag mot spesialpriser
      *
     c     fpril1_key    klist
     c                   kfld                    w_firm
5.70 c                   kfld                    fpril1_prgr
     c                   kfld                    fpril1_rkat
     c                   kfld                    fpril1_kkat
     c                   kfld                    fpril1_kund
     c                   kfld                    fpril1_kpro
      *
      * Key for lesing av ordrehode
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      * Key for lesing av ordrelinjer
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
3.31  *
3.31  * Key for oppslag på rabattkategori
3.31  *
3.31 c     ra06pf_key    klist
3.31 c                   kfld                    w_firm
3.31 c                   kfld                    ra06pf_fkod
      *
      * Key for les på vare-enhet
      *
     c     vvenl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
     c                   kfld                    vvenl2_saen
5.41  *
5.41  * Key2 for les på vare-enhet
5.41  *
5.41 c     vvenl2_key2   klist
5.41 c                   kfld                    w_firm
5.41 c                   kfld                    vvenl2_vare
      *
6.33  * Key for oppslag på vare (VA)
6.33  *
6.33 c     jvarl1_key    klist
6.33 c                   kfld                    jvarl1_vare
6.33  * Key for oppslag på vare-pakning (VA)
6.33  *
6.33 c     jvpkl1_key    klist
6.33 c                   kfld                    jvpkl1_vare
6.33 c                   kfld                    jvpkl1_ldor
7.02 c*                  kfld                    jvpkl1_pakn
7.01  *
7.01  * Key for oppslag på ordre-info-type
7.01  *
7.01 c     vinfl1_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    vinfl1_ityp
      *
      *
      *
      * Tar inn parameter fra forrige program
      *
     c     *entry        plist
     c                   parm                    p_retu
6.nu c                   parm                    p_numm
     c                   parm                    p_suff
      *
      * Henter dagens dato
      *
     c                   time                    w_udat
      *
      * Henter firmanummer fra LDA
      *
     c                   eval      w_firm = l_firm
7.01  *
7.01  * Endring 7.01
7.01  *
7.01   CallP CO402R(l_filg:w_firm:0:'FP720R_701':
7.01                    co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01     if (%trim(co402_verdi2) <> *blank);
7.01       w_ityp = %trim(co402_verdi2);
7.01       vinfl1_ityp = w_ityp;
7.01       chain vinfl1_key vinfl1r;
7.01       if %found(vinfl1);
7.01         u_701 = *on;
7.01       endif;
7.01     endif;
7.01   endif;
      *
      * Henter inn ordre-hode
      *
     c                   eval      fohel1_numm = p_numm
     c                   eval      fohel1_suff = p_suff
     c     fohel1_key    chain     fohel1r                            93
5.70  *
5.70 c                   call      'VL712R  '
5.70 c                   parm                    w_firm
5.70 c                   parm                    folage
5.70 c                   parm                    foavde
5.70 c                   parm                    w_prgr
      *
     c                   endsr
      *
**
Det finnes spesialpriser fra før på denne kunde/  001
prosjekt.  Skal nye / endrede poster oppdateres?  002
