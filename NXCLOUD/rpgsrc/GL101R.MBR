      /TITLE  GL101R Renummerering av linjenummer på utbetalingsforslag
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * System  . . . . : ASOKON                                        *
      * Program . . . . : GL101R                                        *
      * Beskrivelse . . : Renummerering av linjenummer på utbetalings-  *
      *                   forslag + oppdatering av forslagnummer og     *
      *                   forslagslinjenummer på transaksjonsfile       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign *
      * --------- ------  ----------------------------------------------*
      *           241094  Nytt program oktober-1994                RH   *
T5.00 * V5.00     151002  Endring i datofelt og key-felt.          HFL  *
T5.00 * V5.20     151003  Programmet er restrukturert              KOB  *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KC = F3  = Exit                                           *
      *       KE = F5  = Forny                                          *
      *       KL = F12 = Annuller                                       *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       10 = Roll                                                 *
      *       12 = Sfldsp                                               *
      *       13 = Sfldspctl                                            *
      *       14 = Sflclr                                               *
      *       15 = Sflend                                               *
      *       16 = Readc 16 on, no more records                         *
      *       30 = Overflow i rapport.                                  *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    31    = Brukes også til meldinger i rapport.                 *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     fgubfl1    if   e           k disk    rename(gabfpfr:gabfpf1)
     fgubfl3    uf   e           k disk    rename(gabfpfr:gabfpf3)
     frltrlu    uf   e           k disk    rename(rltrpfr:rltrlur)
      *
      /eject
      *
      * Datastruktur for å "pakke ut" parameterliste:
      *
     d                 ds
     d dslist                         6
     d  wpfirm                        3  0 overlay(dslist)
     d  wpbfnr                        3  0 overlay(dslist:4)
      *
     d wpbfli          s                   like(gabfli)
     d wplist          s              6
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Oppdatering av nytt linjenummer.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Posisjonerer på GUBFL1 (GUBFPF) med firma og forslagsnr
      *
     c     gubfl1_key    setll     gubfl1
     c     gubfl1_key    reade     gubfl1                                 60
      *
     c                   dow       *in60 = *off
      *
     c                   eval      wpbfli = wpbfli + 10
      *
      * Oppdaterer post på transaksjonsfile med nytt forslagsnr  og
      * forslagslinjenummer.
      *
     c     rltrlu_key    chain     rltrlu
     c                   if        %found
     c                   eval      robfnr = wpbfnr
     c                   eval      robfli = wpbfli
     c                   update    rltrlur
     c                   endif
      *
      * Oppdaterer post på betalingsforslag med nytt linjnummer
      *
     c     gubfl3_key    chain     gubfl3
     c                   if        %found
     c                   eval      gabfli = wpbfli
     c                   update    gabfpf3
     c                   endif
      *
      * Leser neste post.
      *
     c     gubfl1_key    reade     gubfl1                                 60
      *
     c                   enddo
      *
      /space 3
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Oppdatering av nytt linjenummer.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xlinje        begsr
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    wplist
      *
      * "Pakker ut" parameterliste:
      *
     c                   movel     wplist        dslist
      *
      * Initierer hjelpevariable.
      *
     c                   eval      wpbfli = 0
      *
      * Key til GUBFL3:
      *
     c     gubfl3_key    klist
     c                   kfld                    gafirm
     c                   kfld                    gabfnr
     c                   kfld                    galevr
E5.00c                   kfld                    gafdat
     c                   kfld                    gavalk
     c                   kfld                    gakids
     c                   kfld                    gabelØ
E5.00c                   kfld                    gabdat
E5.00c                   kfld                    gatist
      *
      * Key til RLTRLU (RLTRPF):
      *
     c     rltrlu_key    klist
     c                   kfld                    wpfirm
     c                   kfld                    galevr
E5.00c                   kfld                    gabdat
E5.00c                   kfld                    gatist
      *
      * Key til GUBFL1:
      *
     c     gubfl1_key    klist
     c                   kfld                    wpfirm
     c                   kfld                    wpbfnr
      *
     c                   endsr
