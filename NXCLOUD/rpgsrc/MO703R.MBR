     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : MO703R
      * Beskrivelse . . : Overvåking - danner grunndata for type 4
      *                   Sjekk om leveingsdato = bestillingsdato
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       201206 bø   Nytt program
6.30  *R6.30  120614 bof  Gjennomgått mtp. nummerutvidelse
6.31  * 6.30  030118 bof  Gen. lwo1pf,lwo2pf,lwo3pf,lwo4pf,lwo7pf (num=8)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     floheld    if   e           k disk    rename(lohepfr:loheldr)
     flodtl1    if   e           k disk    rename(lodtpfr:lodtl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvotylr    if   e           k disk    rename(votypfr:votylrr)
     fra09l1    if   e           k disk    rename(ra09pfr:ra09l1r)
     flwo4lu    uf a e           k disk    rename(lwo4pfr:lwo4lur)
      *
      *
     d loheld_bdat     s                   like(lobdat)
     d lodtl1_numm     s                   like(ldnumm)
     d lodtl1_suff     s                   like(ldsuff)
     d vvarl1_vare     s                   like(vvvare)
     d lwo4lu_odat     s                   like(mwodat)
     d lwo4lu_osys     s                   like(mwosys)
     d lwo4lu_oonr     s                   like(mwoonr)
     d lwo4lu_osuf     s                   like(mwosuf)
     d lwo4lu_olin     s                   like(mwolin)
     d lwo4lu_ovnr     s                   like(mwovnr)
     d lwo4lx_odat     s                   like(mwodat)
     d votylr_otyp     s                   like(vaotyp)
     d ra09l1_ikod     s                   like(raikod)
      *
     d w_firm          s                   like(vvfirm)
     d w_vare          s                   like(vvvare)
     d w_dato          s                   like(vvedat)
     d w_aar           s              4  0
     d p_dato          s                   like(vvedat)
     d w_usr           s                   like(louser)
     d w_oda           s                   like(lodate)
     d w_tim           s                   like(lotime)
     d w_lih           s                   like(mwolih)
     d w_slg           s                   like(loinnk)
     d w_sln           s             30
      *
     d b_endr          s              1    inz(*off)
      *
      * Local Data Area
      *
     d                uds
      *
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
      *
      * Definering av variable
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * sletter først for dato som det skal kjøres for.
      *
     c                   eval      lwo4lx_odat = w_dato
readec     lwo4lx_key    setll     lwo4lu
readec     lwo4lx_key    reade     lwo4lu
readec                   dow       not %eof(lwo4lu)
     c                   delete    lwo4lur
readec     lwo4lx_key    reade     lwo4lu
     c                   enddo
      *
     c                   eval      w_aar = %subdt(w_dato:*years)
      *
     c                   exsr      les_ordre
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     avslutt       tag
     c                   eval      *inlr = *on
      *
     c                   return
      *
      * --------------------------------------------------------------
      *  Leser ordre - logger ut varer med endret varetekst
      * --------------------------------------------------------------
      *
     c     les_ordre     begsr
      *
     c                   eval      loheld_bdat = w_dato
readec     loheld_key    setll     loheld
readec     loheld_key    reade     loheld
readec                   dow       not %eof(loheld)
      *
     c                   eval      votylr_otyp = lootyp
     c     votylr_key    chain     votylr
     c                   if        %found(votylr) and
     c                             vaoakk = 2
      * sjekk lev.dato på heading
     c                   if        loldat = lobdat
     c                   eval      lwo4lu_odat = lobdat
     c                   eval      lwo4lu_osys = 'L'
     c                   eval      lwo4lu_oonr = lonumm
     c                   eval      lwo4lu_osuf = losuff
     c                   eval      lwo4lu_olin = 0
     c                   eval      lwo4lu_ovnr = *blank
     c                   eval      w_usr       = louser
     c                   eval      w_oda       = lodate
     c                   eval      w_tim       = lotime
     c                   eval      w_slg       = loinnk
     c                   eval      w_lih       = 'H'
     c                   exsr      les_selg
     c                   exsr      skriv_logg
     c                   endif
     c
      *
     c                   eval      lodtl1_numm = lonumm
     c                   eval      lodtl1_suff = losuff
readec     lodtl1_key    setll     lodtl1
readec                   dow       not %eof(lodtl1)
     c
     c                   if        ldldat <> *loval and
     c                             ldldat = lobdat
     c                   eval      lwo4lu_odat = lobdat
     c                   eval      lwo4lu_osys = 'L'
     c                   eval      lwo4lu_oonr = ldnumm
     c                   eval      lwo4lu_osuf = ldsuff
     c                   eval      lwo4lu_olin = ldline
     c                   eval      lwo4lu_ovnr = ldvare
     c                   eval      w_usr       = louser
     c                   eval      w_oda       = lodate
     c                   eval      w_tim       = lotime
     c                   eval      w_lih       = 'D'
     c                   eval      vvarl1_vare = ldvare
     c     vvarl1_key    chain     vvarl1
     c                   exsr      skriv_logg
     c                   endif
      *
readec     lodtl1_key    reade     lodtl1
readec                   enddo
      *
     c                   endif
      *
readec     loheld_key    reade     loheld
readec                   enddo
      *
     c                   endsr
      *
      *
      * --------------------------------------------------------------
      *  Skriver logg ved feil-situasjon type 4
      * --------------------------------------------------------------
      *
     c     skriv_logg    begsr
      *
     c                   clear                   lwo4lur
     c     lwo4lu_key    chain     lwo4lu
     c                   eval      mwousr = w_usr
     c                   eval      mwooda = w_oda
     c                   eval      mwotim = w_tim
     c                   eval      mwolih = w_lih
     c                   eval      mwoslg = w_slg
     c                   eval      mwosln = w_sln
     c                   eval      mwovtx = vvtek1
     c                   eval      mwoogr = vvogrp
     c                   eval      mwohgr = vvhgrp
     c                   eval      mwougr = vvugrp
     c                   if        %found(lwo4lu)
     c                   update    lwo4lur
     c                   else
     c                   eval      mwofir = w_firm
     c                   eval      mwodat = lwo4lu_odat
     c                   eval      mwosys = lwo4lu_osys
     c                   eval      mwoonr = lwo4lu_oonr
     c                   eval      mwosuf = lwo4lu_osuf
     c                   eval      mwovnr = lwo4lu_ovnr
     c                   eval      mwolin = lwo4lu_olin
     c                   write     lwo4lur
     c                   endif
      *
     c                   endsr
      *
      * --------------------------------------------------------------
      *  leser selger
      * --------------------------------------------------------------
      *
     c     les_selg      begsr
      *
     c                   eval      ra09l1_ikod = w_slg
     c     ra09l1_key    chain     ra09l1r
     c                   if        %found(ra09l1)
     c                   eval      w_sln = raitxt
     c                   else
     c                   eval      w_sln = *blank
     c                   endif
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for les av vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *  Key for les av ordre
      *
     c     loheld_key    klist
     c                   kfld                    w_firm
     c                   kfld                    loheld_bdat
      *
      *  Key for les av ordre
      *
     c     lodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtl1_numm
     c                   kfld                    lodtl1_suff
      *
      * Key for oppdatering av overvåking  4
      *
     c     lwo4lu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lwo4lu_odat
     c                   kfld                    lwo4lu_osys
     c                   kfld                    lwo4lu_oonr
     c                   kfld                    lwo4lu_osuf
     c                   kfld                    lwo4lu_olin
     c                   kfld                    lwo4lu_ovnr
      *
      * Key for slett av overvåking på den dato man vil kjøre på.
      *
     c     lwo4lx_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lwo4lx_odat
      *
      *  Key for les av ordretype
      *
     c     votylr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votylr_otyp
      *
      * les av selger
      *
     c     ra09l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra09l1_ikod
      *
     c     *entry        plist
     c                   parm                    p_dato
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_dato = p_dato
      *
     c                   endsr
