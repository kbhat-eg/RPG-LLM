      /title  ASOFAK Konvertering, konvertering av enheter -salgs.stat.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FX762R
      * Beskrivelse . . : Konvertering, konvertering av enheter i
      *                   salgsstatistikk
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       240200 HKO  Nytt program
8.01  *PRG2   220622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner - COMP
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffenkl1    if   e           k disk    rename(fenkpfr:fenkl1r)
     fsstapf    uf   e           k disk    rename(sstapfr:sstapfr)
      *
      * Definering av variable i nøkler
      *
     d fenkl1_firm     s                   like(fefirm)
     d fenkl1_enhf     s                   like(feenhf)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Avbryter dersom det ikke finnes enheter i konverteringstabell
      *
     c                   read      fenkl1                                 90
     c                   if        *in90 = *on
     c                   goto      avslutt
     c                   endif
      *
      * Behandler salgsstatistikk
      *
     c                   read      sstapfr                                90
     c                   dow       *in90 = *off
      *
     c                   if        sfenhe <> *blank
     c                   eval      fenkl1_firm = sffirm
     c                   eval      fenkl1_enhf = sfenhe
     c     fenkl1_key    chain     fenkl1                             91
     c                   if        *in91 = *off
     c                   eval      sfenhe = feenht
     c                   endif
     c                   endif
      *
     c                   if        sfenho <> *blank
     c                   eval      fenkl1_firm = sffirm
     c                   eval      fenkl1_enhf = sfenho
     c     fenkl1_key    chain     fenkl1                             91
     c                   if        *in91 = *off
     c                   eval      sfenho = feenht
     c                   endif
     c                   endif
      *
     c                   update    sstapfr
      *
     c                   read      sstapfr                                90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på enhets-konvertering
      *
     c     fenkl1_key    klist
     c                   kfld                    fenkl1_firm
     c                   kfld                    fenkl1_enhf
      *
     c                   endsr
