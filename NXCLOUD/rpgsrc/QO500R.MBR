     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : QO500R
      * Beskrivelse . . : Sende SMS
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       310319 bsa  Nytt program
7.01  *       140519 bsa  Hentes fra ordre/bestilling. Deretter kunde/
7.01  *                   leverandør. Til slutt fra selger på ordre/bestilling.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
7.01 frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
7.01 frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fra09l1    if   e           k disk    rename(ra09pfr:ra09l1r)
     fausrl1    if   e           k disk    rename(ausrpfr:ausrl1r)
     fqo500d    cf   e             workstn
      *
      * Definering av variabler i nøkler
      *
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d lohel1_numm     s                   like(lonumm)
     d lohel1_suff     s                   like(losuff)
     d ra09l1_ikod     s                   like(raikod)
7.01 d rkunl1_kund     s                   like(rkkund)
7.01 d rlevl1_levr     s                   like(rllevr)
      *
      * Definering av parametere
      *
     d p_firm          s                   like(fofirm)
     d p_numm          s                   like(fonumm)
     d p_suff          s                   like(fosuff)
     d p_syst          s              1
      *
      * Definering av local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
     d b_text          s              1    inz(*off)
      *
     d w_tell          s              2  0
     d w_firm          s                   like(fofirm)
     d w_smsm          s            320
     d w_ftlf          s             11
     d w_fpsw          s             15
     d sms_avsender    s             11
     d w_dta           s             10
7.01 d w_mobi          s             15
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
7.01 c                   eval      w_mobi = *blanks
7.01  * O=Ordreoppslag  L=Bestilling
     c                   if        p_syst = 'O'
     c                   eval      fohel1_numm = p_numm
     c                   eval      fohel1_suff = p_suff
     c     fohel1_key    chain     fohel1
7.01 c                   if        %found
7.01  * Bruk mobilnummer fra ordren
7.01 c                   eval      w_mobi = fomobi
7.01 c                   if        w_mobi = *blanks
7.01  * Sjekk videre mot kunde
7.01 c                   eval      rkunl1_kund = fokund
7.01 c     rkunl1_key    chain     rkunl1
7.01 c                   if        %found
7.01 c                   eval      w_mobi = rkmobn
7.01  * Hent til slutt fra selger
7.01 c                   if        w_mobi = *blanks
7.01 c                   eval      ra09l1_ikod = foselg
7.01 c     ra09l1_key    chain     ra09l1
7.01 c                   if        %found
7.01 c                   eval      w_mobi = raimob
7.01 c                   endif
7.01 c                   endif
7.01 c                   endif
7.01 c                   endif
7.01 c                   endif
7.01  * Vi sjekker på bestilling
     c                   elseif    p_syst = 'L'
     c                   eval      lohel1_numm = p_numm
     c                   eval      lohel1_suff = p_suff
     c     lohel1_key    chain     lohel1
7.01 c                   if        %found
7.01  * Sjekk mot leverandør
7.01 c                   eval      rlevl1_levr = loldor
7.01 c     rlevl1_key    chain     rlevl1
7.01 c                   if        %found
7.01 c                   eval      w_mobi = rlmobn
7.01  * Hent til slutt fra innkjøper
7.01 c                   if        w_mobi = *blanks
7.01 c                   eval      ra09l1_ikod = loinnk
7.01 c     ra09l1_key    chain     ra09l1
7.01 c                   if        not %found
7.01 c                   eval      raimob = *blanks
7.01 c                   endif
7.01 c                   endif
7.01 c                   endif
     c                   endif
      *
     c                   endif
      *
7.01 c                   if        p_syst = 'O'
     c                   eval      w_dta  = 'LO802C_' + %trim(%char(folage))
7.01 c                   else
7.01 c                   eval      w_dta  = 'LO802C_' + %trim(%char(lolage))
7.01 c                   endif
     c                   call      'LO802C'
     c                   parm                    w_dta
     c                   parm                    e1txt1
     c                   parm                    e1txt2
     c                   parm                    e1txt6
     c                   parm                    e1txt7
     c                   parm                    sms_avsender
      * Skal ikke bruke tekst fra lager
     c                   eval      e1txt1 = *blank
     c                   eval      e1txt2 = *blank
     c                   eval      e1txt3 = *blank
     c                   eval      e1txt4 = *blank
     c                   eval      e1txt5 = *blank
     c                   eval      e1txt6 = *blank
     c                   eval      e1txt7 = *blank
     c                   eval      e1txt7 = 'Mvh ' + %trim(abnavn)
      *
7.01 c                   eval      e1mobi = w_mobi
      *
     c                   exfmt     e1win
      *
     c                   if        *inkc = *on or
     c                             *inkl = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      w_smsm = e1txt1
     c                   if        e1txt2 <> *blank
     c                   eval      w_smsm = %trim(w_smsm) +
     c                                      %trim(e1txt2)
     c                   endif
     c                   if        e1txt3 <> *blank
     c                   eval      w_smsm = %trim(w_smsm) + ' ' +
     c                                      %trim(e1txt3)
     c                   endif
     c                   if        e1txt4 <> *blank
     c                   eval      w_smsm = %trim(w_smsm) + ' ' +
     c                                      %trim(e1txt4)
     c                   endif
     c                   if        e1txt5 <> *blank
     c                   eval      w_smsm = %trim(w_smsm) + ' ' +
     c                                      %trim(e1txt5)
     c                   endif
     c                   if        e1txt6 <> *blank
     c                   eval      w_smsm = %trim(w_smsm) + ' ' +
     c                                      %trim(e1txt6)
     c                   endif
     c                   if        e1txt7 <> *blank
     c                   eval      w_smsm = %trim(w_smsm) + ' ' +
     c                                      %trim(e1txt7)
     c                   endif
      *
     c*    'Æ':X'40'     xlate     w_smsm        w_smsm
     c*    'æ':X'40'     xlate     w_smsm        w_smsm
     c*    'Ø':X'E0'     xlate     w_smsm        w_smsm
     c*    'ø':X'40'     xlate     w_smsm        w_smsm
     c*    'Å':X'9F'     xlate     w_smsm        w_smsm
     c*    'å':X'40'     xlate     w_smsm        w_smsm
      *
     c                   eval      w_ftlf = sms_avsender
     c                   eval      w_fpsw = *blank
      *
     c                   if        %trim(w_smsm) <> *blanks and
     c                             w_ftlf <> *blank and
     c                             e1mobi <> *blank
     c                   call      'AF725R '
     c                   parm                    w_ftlf
     c                   parm                    w_fpsw
     c                   parm                    e1mobi
     c                   parm                    w_smsm
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_syst
      *
      * Key les på ordre
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      * Key les på bestilling
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
     c                   kfld                    lohel1_suff
      *
      * Key for oppslag på selger
      *
     c     ra09l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra09l1_ikod
7.01  *
7.01  * Key for oppslag på kunde
7.01  *
7.01 c     rkunl1_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    rkunl1_kund
7.01  *
7.01  * Key for oppslag på leverandør
7.01  *
7.01 c     rlevl1_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    rlevl1_levr
      *
      * Henter firma, ordrenr og suffiks fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c     l_user        chain     ausrl1
      *
     c                   endsr
