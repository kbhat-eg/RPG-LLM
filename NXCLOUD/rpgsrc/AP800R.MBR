      /TITLE  Henting av utskriftsparametre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : AP800R
      * Beskrivelse . . : Henter utskriftsparametre i fra formularfile.
      *
      *                   Denne versjon av programmet baserer seg på
      *                   parametere inn, i stedet for at LDA må være
      *                   oppdatert på forhånd.
      *
      *                   p_ruti = Navn på rutine som skal kjøres
      *                   p_valg = Valg om overstyring av verdier.
      *                   p_tvbt = Valg om tvungen batch skal brukes
      *                   p_in03 = F03/F12 er valgt,  avslutt jobb.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *              RHO  Nytt program august-94
      * V1.08 250497 AMD  Lagt inn oppdatering av disse koder i LDA:
      *                   LDA:  881  881   l_bgir
      *                   LDA:  883  883   l_kidk
      *                   LDA:  884  884   l_mvak
      *                   LDA:  885  885   l_ocrk
      *       280198 JT   Henter standardverdier fra ASPSTD dersom
      *                   rutinen ikke ble funnet i rutine-register.
      *       030598 HKO  Lagt inn kode i LDA for tvungen valg av
      *                   batchkjøring.
      *                   LDA:  886  886   l_tvbt
      *       280999 AMD  Lagt inn kode i LDA for styring *IPDS/*SCS
      *                   0=*SCS  1=*IPDS
      *                   LDA:  890  890   l_ipds
      *       190299 AMD  Lagt inn kode som styrer printerfiler slik at
      *                   styrekarakterer kan settes opp fritt.
      *                   LDA:  760  779   l_hex1
      *                   LDA:  780  799   l_hex2
3.15  * R3.15 080900 AMD  Lagt inn kode som styrer printertype
3.15  *                   LDA:  891  891   l_type
5.01  * R5.01 280203 AMD  Lagt inn kode for null rotasjon
5.01  *                   LDA:  892  892   l_0rot
5.01  * R5.01 280203 AMD  Kode for tvungen batch er nå lagt inn i
5.01  *                   rutineregister og oppdaterer LDA herfra
5.21  * R5.21 290803 AMD  Kode for manuell overstyring av OUTQ. Dersom
      *                   denne er lik 1, skal OUTQ fra LDA benyttes.
5.22  * R5.22 260504 AMD  Nummerteller for member sprakk dersom den gikk
      *                   i taket.  Lagt inn automatisk rullering.
5.31  * R5.31 270204 AMD  Lagt inn koder for styring av arkivering
5.32  * R5.32 270804 AMD  Lagt inn koder for save etter utprint
5.40  * R5.40 250505 AMD  Lagt inn oppdatering av disse opplysninger :
5.40  *                   LDA:  670  671 0 Skuff inn
5.40  *                   LDA:  672  673 0 Skuff ut
5.40  *                   LDA:  674  680   Duplex
5.40  *                   LDA:  681  690   Print kvalitet
5.41  * R5.41 150605 AMD  Valg av firmaopplysninger også i printerstyring
5.41  *                   LDA:  750  750 0 Firmaopplysninger
      *
      *       270505 AMD  Programmet er skrevet om for ny versjon 5.40
      *                   Endrings-kommentarer er fjernet
      *
pdf   *       160410 BSA  Programmet er skrevet om for PDF versjon. Dvs viser
pdf   *                   andre parametre hvis xml og PDF print/arkivering
pdf   *                   benyttes.
pdf   *                   LDA:  585  594   Overstyrt utkø
pdf   *                   LDA:  595  635   Bactnummer
pdf   *                   LDA:  636  636 0 Arkiveres
pdf   *                   LDA:  636  636 0 Arkiveres
pdf   *                   LDA:  637  637 0 Skjermvisning
pdf   *                   LDA:  638  638 0 Liggende/stående utskrift
pdf   *                   LDA:  639  639 0 Eksport til excel
pdf   *                   LDA:  640  640 0 Sendes på mail
6.10  *       100910 BSA  Problemer med nummerteller for member når en kjører
6.10  *                   fra java klienten.
6.11  *       131010 BSA  Glipper endel verdier hvis den ikke kjøres online.
6.12  *       081110 BSA  Går seg "vill" på avdelingsnivå.
6.13  *       020211 BSA  Flytter sekvens for styring av utlagt printer
6.14  *       160311 BSA  Hvis *ARKIV er valgt som skriver skal den kun arkiveres
6.15  *  6.15 070411 AMD  Lagt inn lager og avdeling som begrep for
6.15  *                   printerstyring (først lager, så avdeling)
6.16  *       011111 bsa  Legger ut feil printer ved spool håndtering
6.17  *       301111 bsa  Henter inn gyldig skriver ved *job valg
6.20  *  6.20 100512 bsa  Mulighet for overstyring av arkivflagg. Gjelder kun
6.20  *                   hvis ikke arkiv=1 er valgt på rutina.
6.21  *  6.20 110113 bsa  Nullstiller lda verdier (utkø)
6.nu  *  6.30 170614 bof  Endring ihht. nummerutvidelse
6.30  *  6.30 140814 bsa  Lagt inn mulighet for excel eksport
6.31  *  6.30 220814 bsa  Hvis excel eller preview, skal den ikke henge
6.31  *                   seg opp i utkø.
6.32  *  6.30 090117 bsa  Hvis excel eller preview, overstyrer vi utkø til
6.32  *                   XDUMMY.
6.33  *  6.30 190117 bsa  Kan ikke overstyre hvis excel.
6.34  *  6.30 080617 bsa  Hvis excel er mulig men ikke valgt, skal det være spool
6.34  *                   utskrift. Problemer med styring av utkø.
7.xx  * 7.00  290116 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 7.00  100320 bsa  Må nullstill felter ved printerstyring
Q.01  * 7.00  030419 bsa  Tillater skjerm med 27x132 størrelse
7.02  * 7.00  240920 bsa  Endrer til DUMMY, da noen har fjernet XDUMMY fra alle
7.02  * 7.00              maskiner.
      *
8.01  * 8.xx  020522 bsa  Henter info, men overstyrer alt slik at ikke det
8.01  * 8.xx              skjer skjermdialog.
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     faforl1    if   e           k disk    rename(aforpfr:aforl1r)
pdf  fafopl1    if   e           k disk    rename(afoppfr:afopl1r)
pdf  fafprlr    if   e           k disk    rename(afprpfr:afprlrr)
pdf  fafpslr    if   e           k disk    rename(afpspfr:afpslrr)
pdf  fafapl1    if   e           k disk    rename(afappfr:afapl1r)
     fausrl1    if   e           k disk    rename(ausrpfr:ausrl1r)
     fawsdl1    if   e           k disk    rename(awsdpfr:awsdl1r)
     faenhl1    if   e           k disk    rename(aenhpfr:aenhl1r)
     fanumlu    uf a e           k disk    rename(anumpfr:anumlur)
      *
     d m               s             30    dim(2) ctdata perrcd(1)
      *
      * Definering av local data area
      *
     d lda            uds
pdf  d l_poff                584    584
pdf  d l_utqm                585    594
pdf  d l_bach                595    635
pdf  d l_arch                636    636  0
pdf  d l_skje                637    637  0
pdf  d l_land                638    638  0
pdf  d l_exlp                639    639  0
pdf  d l_mail                640    640  0
5.40 d l_skin                670    671  0
5.40 d l_skut                672    673  0
5.40 d l_dplx                674    680
5.40 d l_pkva                681    690
     d l_arki                741    741  0
     d l_etik                742    742  0
     d l_work                743    743  0
     d l_pdfa                744    744  0
     d l_save                745    748
5.41 d l_prfi                750    750  0
     d l_hex1                760    779
     d l_hex2                780    799
     d l_ruti                800    809
     d l_outq                810    819
     d l_form                820    829
     d l_acpi                830    833  0
     d l_hldq                834    837
     d l_akop                838    843  0
     d l_chgv                844    844
     d l_ovrk                845    845
     d l_alin                856    858  0
     d l_prty                859    859
     d l_oflw                860    862  0
     d l_btch                863    863
     d l_mber                864    868  0
     d l_prog                871    880
     d l_bgir                881    881
     d l_kidk                883    883
     d l_mvak                884    884
     d l_ocrk                885    885
     d l_tvbt                886    886
     d l_apos                887    889  0
     d l_ipds                890    890  0
     d l_type                891    891  0
     d l_0rot                892    892
     d l_outm                899    899
      *
     d l_wsid                901    910
     d l_user                911    920
pdf  d l_filg                931    933
     d l_firm                944    946  0
pdf  d l_avde                947    950  0
      *
      * Definering av parametere
      *
     d p_ruti          s             10
     d p_valg          s              1
     d p_tvbt          s              1
     d p_in03          s              1
     d p_btch          s              1
      *
      * Definering av variable i nøkler
      *
pdf  d afpslr_ufil     s                   like(l_filg)
pdf  d afpslr_uide     s                   like(afuide)
pdf  d afprlr_sfil     s                   like(l_filg)
pdf  d afprlr_sprt     s                   like(afsprt)
pdf  d afopl1_ruti     s                   like(p_ruti)
pdf  d afapl1_arut     s                   like(p_ruti)
pdf  d afapl1_afir     s                   like(afafir)
pdf  d afapl1_aavd     s                   like(afaavd)
pdf  d aforl1_ruti     s                   like(p_ruti)
     d ausrl1_user     s                   like(abuser)
     d awsdl1_wsid     s                   like(l_wsid)
     d aenhl1_prid     s                   like(aeprid)
     d anumlu_fast     s             10
     d anumlu_fell     s              6
     d anumlu_type     s              2
      *
      * Definering av variable
      *
     d w_firm          s                   like(anfirm)
     d w_outq          s             10
6.17 d p_iotq          s             10
6.17 d p_uotq          s             10
     d w_sjek          s              1
     d w_sats          s              1
     d w_alin          s              2  0
     d w_apos          s              3  0
      *
pdf  d b_pdf           s              1    inz(*off)
6.34 d b_exlp          s              1    inz(*off)
6.12 d b_avd           s              1    inz(*off)
pdf  d w_kode          s              1
pdf  d w_fell          s              6
pdf  d w_type          s              2
pdf  d w_fast          s             10
pdf  d w_ruti          s             10
6.nu d w_numm          s              8  0
pdf  d w_jnav          s             15
pdf  d w_jkey          s             41
pdf  d w_jtxt          s             35
pdf  d w_sprt          s             10
pdf  d w_ptxt          s             30
pdf   *
pdf   * PDF parametre
pdf   *
pdf  d d_pdss          ds
pdf  d  d_mail                        1  0
pdf  d  d_exlp                        1  0
pdf  d  d_arki                        1  0
pdf  d  d_skje                        1  0
pdf  d  d_land                        1  0
pdf  d  d_xmlm                       75
pdf  d  d_jasm                       75
pdf  d  d_logo                       75
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedlogikk
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Printfile skal i utgangspunktet
      * overrides i CL-programmet
      *
     c                   eval      l_ovrk = '1'
      *
      * MEN dersom post ikke finnes i rutineregister, og kode
      * for å endre formularparametre er "NEI", så skal override
      * av printfile ikke gjøres.
      *
     c                   eval      aforl1_ruti = p_ruti
     c     aforl1_key    chain     aforl1r
     c                   if        not %found and
     c                             l_chgv = *blank
     c                   eval      l_ovrk = *blank
     c                   endif
      *
      * Oppdaterer LDA med defaultverdier
      * i tilfelle post i rutineregister
      * ikke finnes
      * ------------
     c                   exsr      xdefault
      * ------------
      * Oppdaterer LDA med verdier fra
      * Rutineregister/
      * Skjerm/Brukerregister/
      * Enhetsregister
      * ------------
     c                   eval      aforl1_ruti = p_ruti
     c     aforl1_key    chain     aforl1r
     c                   if        %found
     c                   exsr      xupd_lda
     c                   endif
pdf   * ****************************************************************** **
pdf   * *********************************************************************
pdf   * Leser PDF tilleggsregister, og sender ut dette skjermbildet hvis    *
pdf   * informasjonen er aktivert. NB Denne sekvensen styrer all videre     *
pdf   * håndtering av PDF i alle utskriftprogrammer. DVS l_poff sier om     *
pdf   * vi skal "bry" oss om pdf eller ei !!!!!!!!!!!!!                     *
pdf   * Skjerm/Brukerregister/                                              *
pdf   * Enhetsregister                                                      *
pdf   * ****************************************************************** **
pdf   * ****************************************************************** **
pdf   *
pdf  c                   if        l_poff = '1'
6.12 c                   eval      b_pdf = *off
6.20 c                   eval      *in34 = *off
pdf   * Sjekk om det skal benyttes avvik på avdelingsnivå
pdf  c                   eval      afapl1_arut = p_ruti
pdf  c                   eval      afapl1_afir = l_firm
pdf  c                   eval      afapl1_aavd = l_avde
pdf  c     afapl1_key    chain     afapl1r
6.12 c                   if        %found
6.12 c                   eval      b_avd = *on
6.12 c                   if        afapdf = 1
8.01 c**                 eval      d_mail = afamai
8.01 c**                 eval      d_exlp = afaexl
pdf  c                   eval      d_arki = afaarc
6.20 c                   if        afaarc = 1
6.20 c                   eval      *in34 = *on
6.20 c                   endif
8.01 c**                 eval      d_skje = afaprv
pdf  c                   eval      d_land = afalan
pdf  c                   eval      d_xmlm = afaxlm
pdf  c                   eval      d_jasm = afajas
pdf  c                   eval      d_logo = afalog
pdf  c                   exsr      xupd_lda_pdf
pdf  c                   eval      b_pdf = *on
6.12 c                   else
6.12  * Den finnes, men er deaktivert.
6.12 c                   eval      b_pdf = *off
6.12  * Vi skrur av hele aktiveringa. Slipper full test i alle programmer.
6.12 c                   eval      l_poff= '0'
6.12  * Den finnes, men er deaktivert.
6.12 c                   eval      b_pdf = *off
6.12  * Vi skrur av hele aktiveringa. Slipper full test i alle programmer.
6.12 c                   eval      l_poff= '0'
6.12 c                   eval      d_mail = 0
6.12 c                   eval      d_exlp = 0
6.12 c                   eval      d_arki = 0
6.12 c                   eval      d_skje = 0
6.12 c                   eval      d_land = 0
6.12 c                   eval      d_xmlm = *blanks
6.12 c                   eval      d_jasm = *blanks
6.12 c                   eval      d_logo = *blanks
6.12 c                   endif
pdf  c                   endif
pdf   *
pdf   * Sjekker uansett rutina. Hvis avslått her, skal PDF deaktiveres.
pdf   *
pdf  c                   eval      afopl1_ruti = p_ruti
pdf  c     afopl1_key    chain     afopl1r
pdf  c                   if        %found
pdf   * Er ikke avdelingsinfo funnet, brukes verdier fra rutine.
pdf  c                   if        afppdf = 1 and
6.12 c                             b_avd  = *off
8.01 c**                 eval      d_mail = afpmai
8.01 c**                 eval      d_exlp = afpexl
pdf  c                   eval      d_arki = afparc
6.20 c                   if        afparc = 1
6.20 c                   eval      *in34 = *on
6.20 c                   endif
8.01 c**                 eval      d_skje = afpprv
pdf  c                   eval      d_land = afplan
pdf  c                   eval      d_xmlm = afpxlm
pdf  c                   eval      d_jasm = afpjas
pdf  c                   eval      d_logo = afplog
pdf  c                   exsr      xupd_lda_pdf
pdf  c                   eval      b_pdf = *on
pdf  c                   endif
pdf   * Er rutina disabled, nullstilles alle verdier
pdf  c                   if        afppdf = 0
pdf  c                   eval      b_pdf = *off
pdf   * Vi skrur av hele aktiveringa. Slipper full test i alle programmer.
pdf  c                   eval      l_poff= '0'
pdf  c                   eval      d_mail = 0
pdf  c                   eval      d_exlp = 0
pdf  c                   eval      d_arki = 0
pdf  c                   eval      d_skje = 0
pdf  c                   eval      d_land = 0
pdf  c                   eval      d_xmlm = *blanks
pdf  c                   eval      d_jasm = *blanks
pdf  c                   eval      d_logo = *blanks
pdf  c                   endif
6.12  * Er rutina aktiv, men avdelingsstyrt, skal vi deaktivere
6.12 c                   if        afppdf = 2 and
6.12 c                             b_avd = *off
6.12 c                   eval      b_pdf = *off
6.12  * Vi skrur av hele aktiveringa. Slipper full test i alle programmer.
6.12 c                   eval      l_poff= '0'
6.12 c                   eval      d_mail = 0
6.12 c                   eval      d_exlp = 0
6.12 c                   eval      d_arki = 0
6.12 c                   eval      d_skje = 0
6.12 c                   eval      d_land = 0
6.12 c                   eval      d_xmlm = *blanks
6.12 c                   eval      d_jasm = *blanks
6.12 c                   eval      d_logo = *blanks
6.12 c                   endif
pdf  c                   else
pdf  c                   eval      b_pdf = *off
pdf   * Vi skrur av hele aktiveringa. Slipper full test i alle programmer.
pdf  c                   eval      l_poff= '0'
pdf  c                   endif
pdf   * (l_poff=1)
pdf  c                   endif
      * ------------
      * Behandler skjermbilde dersom
      * kode er satt for at dette skal vises
      * d.v.s   l_chgv er '1'
      * ------------
      *
      * Sjekker først om jobben
      * kjøres i batch i så fall
      * skal ikke skjermbilde vises
      *
     c                   call      'AP700C'
     c                   parm                    w_sats
     c                   if        w_sats = '0'
     c                   eval      l_chgv = ' '
     c                   eval      p_in03 = '1'
     c                   endif
      *
6.11 c                   if        b_pdf = *on
6.11 c                   exsr      pdf_batchno
6.11 c                   endif
      *
     c                   if        l_chgv = '1'
pdf  c                   if        b_pdf = *on
pdf  c                   exsr      xskjerm_pdf
pdf  c                   else
     c                   exsr      xskjerm
     c                   endif
pdf  c                   else
pdf   * Lag jobbnummer
pdf  c                   if        l_bach = *blanks
pdf  c                   exsr      pdf_batchno
pdf  c                   endif
pdf  c                   endif
pdf   *
     c                   if        p_in03 = '1'
     c                   exsr      xslutt_ktr
6.10 c                   exsr      xmember
     c                   goto      xslutt
     c                   endif
      * ------------
      * Utfører slutt-kontroll
      * ------------
     c                   exsr      xslutt_ktr
      * ------------
      * Henter inn nytt nummer fra
      * member-teller
      * ------------
     c                   exsr      xmember
      * ------------
      * Avslutt program
      * ------------
      *
      * End of job
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xslutt        tag
      *
6.14 c                   if        b_pdf = *on and
6.14 c                             l_outq = '*ARKIV'
6.14 c                   eval      l_utqm = l_outq
6.14 c                   eval      l_outq = 'ASP       '
6.14 c                   endif
6.14  *
6.17 c                   if        b_pdf = *on and
6.17 c                             l_outq = '*JOB  '
6.17 c                   eval      p_iotq = l_outq
6.17 c                   call      'AP615C '
6.17 c                   parm                    p_iotq
6.17 c                   parm                    p_uotq
6.17 c                   eval      l_outq = p_uotq
6.17 c                   endif
6.17  *
     c                   out       *dtaara
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Legger inn defaultverdier dersom rutine ikke finnes:
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xdefault      begsr
      *
      * Oppslag på rutinenavn ASPSTD og bruk verdier herfra dersom
      * denne finnes:
     c                   eval      aforl1_ruti = 'ASPSTD    '
     c     aforl1_key    chain     aforl1r
     c                   if        %found
     c                   exsr      xupd_lda
      *
     c                   else
     c                   if        l_outm <> '1'
     c                   eval      l_outq = '*JOB      '
     c                   endif
     c                   eval      l_acpi = 10
     c                   eval      l_akop = 1
     c                   eval      l_hldq = '*NO '
     c                   eval      l_save = '*NO '
     c                   eval      l_form = '0001      '
     c                   eval      l_apos = 132
     c                   eval      l_alin = 48
     c                   eval      l_prty = '5'
     c                   eval      l_oflw = 0
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for overføring av felter i fra skjermbilde til param.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xupd_lda      begsr
      *
      * Legger inn rutinenavn
      *
     c                   eval      l_ruti = aforl1_ruti
      *
      * Oppdaterer kode som forteller
      * om skjermbildet skal vises
      *
Obs ! *
Obs ! * Midlertidig går forbi dersom
Obs ! * det er ASPSTD som kommer inn
Obs ! *
Obs !c                   if        l_ruti = 'ASPSTD'
Obs !c                   goto      xforbi
Obs !c                   endif
Obs ! *
8.01 c**                 if        afchgv = 1
8.01 c**                 eval      l_chgv = '1'
8.01 c**                 else
     c                   eval      l_chgv = ' '
8.01 c**                 endif
Obs ! *
Obs !c     xforbi        tag
Obs ! *
      *
     c                   if        l_outm <> '1'
     c                   eval      l_outq = afoutq
     c                   endif
      *
     c                   eval      l_form = afform
     c                   eval      l_acpi = afacpi
     c                   eval      l_akop = afakop
     c                   eval      l_apos = afapos
     c                   eval      l_alin = afalin
5.41 c                   eval      l_prfi = afprfi
      *
     c                   if        afprty = *blank
     c                   eval      afprty = '5'
     c                   endif
      *
     c                   eval      l_prty = afprty
     c                   eval      l_oflw = afoflw
     c                   eval      l_prog = afprog
     c                   eval      l_bgir = %editc(afbgir:'X')
     c                   eval      l_kidk = %editc(afkidk:'X')
     c                   eval      l_mvak = %editc(afmvak:'X')
     c                   eval      l_ocrk = %editc(afocrk:'X')
     c                   eval      l_ipds = afipds
     c                   eval      l_hex1 = afhex1
     c                   eval      l_hex2 = afhex2
     c                   eval      l_0rot = af0rot
      *
     c                   eval      l_tvbt = aftvbt
     c                   eval      l_btch = aftvbt
     c                   eval      p_btch = aftvbt
      *
5.40 c                   eval      l_skin = afskin
5.40 c                   eval      l_skut = afskut
5.40 c                   eval      l_dplx = afdplx
5.40 c                   eval      l_pkva = afpkva
      *
      * Legge utskrift på HOLD?
      *
     c                   eval      l_hldq = '*NO '
      *
     c                   if        afhldq = 1
     c                   eval      l_hldq = '*YES'
     c                   endif
      *
      * Ta vare på spool etter utskrift ?
      *
     c                   eval      l_save = '*NO '
      *
     c                   if        afsave = 1
     c                   eval      l_save = '*YES'
     c                   endif
      *
      * Overflow:
      *
      * Dersom overflow er oppgitt, og mindre enn sidelengde,
      * skal denne benyttes
      *
     c                   if        l_oflw > 0
     c                   if        l_oflw < l_alin
     c                   goto      xtag01
     c                   endif
     c                   endif
      *
      * Overflow = max sidelengde, når max sidelengde
      * er lik eller mindre enn 6 linjer
      *
     c                   if        l_alin <= 6
     c                   eval      l_oflw = l_alin
     c                   endif
      *
      * Overflow = max sidelengde - 3, når max sidelengde er
      * mer enn 6 linjer:
      *
     c                   if        l_alin >= 6
     c     l_alin        sub       3             l_oflw
     c                   endif
      *
     c     xtag01        tag
      *
      * Koder for Multisupport
      *
     c                   eval      l_arki = afarki
     c                   eval      l_etik = afetik
     c                   eval      l_work = afwork
     c                   eval      l_pdfa = afpdfa
      *
      * Ekstra styrings-informasjon
      *
5.40 c                   eval      l_skin = afskin
5.40 c                   eval      l_skut = afskut
5.40 c                   eval      l_dplx = afdplx
5.40 c                   eval      l_pkva = afpkva
      *
      * Henter opp informasjon fra printer-styring dersom
      * det er lagt inn noe her som skal overstyre rutine
      * Her behandles følgende prioritering :
      *
      * 1) Dersom styring er lagt på skjerm, benytt denne
      * 2) Dersom styring er lagt på bruker, benytt denne
      * 3) Dersom styring er lagt på gruppe, benytt denne
6.15  * 4) Dersom styring er lagt på lager, benytt denne
6.15  * 5) Dersom styring er lagt på avdeling, benytt denne
      *
     c                   eval      awsdl1_wsid = l_wsid
     c     awsdl1_key    chain     awsdl1r
      *
     c                   if        not %found
     c                   eval      awsdl1_wsid = l_user
     c     awsdl1_key    chain     awsdl1r
     c                   endif
      *
     c                   if        not %found
     c                   eval      awsdl1_wsid = abgrup
     c     awsdl1_key    chain     awsdl1r
     c                   endif
6.15  *
6.15 c                   if        not %found
7.01 c                   eval      awsdl1_wsid = *blanks
6.15 c                   movel     ablagr        awsdl1_wsid
6.15 c     awsdl1_key    chain     awsdl1r
6.15 c                   endif
6.15  *
6.15 c                   if        not %found
7.01 c                   eval      awsdl1_wsid = *blanks
6.15 c                   movel     absavd        awsdl1_wsid
6.15 c     awsdl1_key    chain     awsdl1r
6.15 c                   endif
      *
     c                   if        %found
      *
      * OUTQ skal legges inn så sant den ikke er
      * manuelt overstyrt. Da beholdes det som
      * allerede er lagt ut i LDA.
      *
     c                   if        l_outm <> '1'
     c                   eval      l_outq = awoutq
     c                   endif
      *
     c                   if        awform <> *blank
     c                   eval      l_form = awform
     c                   endif
      *
     c                   if        awakop <> 0
     c                   eval      l_akop = awakop
     c                   endif
5.40  *
5.40 c                   if        awskin <> 0
5.40 c                   eval      l_skin = awskin
5.40 c                   endif
5.40  *
5.40 c                   if        awskut <> 0
5.40 c                   eval      l_skut = awskut
5.40 c                   endif
5.40  *
5.40 c                   if        awdplx <> *blank
5.40 c                   eval      l_dplx = awdplx
5.40 c                   endif
5.40  *
5.40 c                   if        awpkva <> *blank
5.40 c                   eval      l_pkva = awpkva
5.40 c                   endif
5.41  *
5.41 c                   if        awprfi <> 0
5.41 c                   eval      l_prfi = awprfi
5.41 c                   endif
      *
     c                   endif
      *
      * Sjekker om det er lagt inn post for enheter,
      * dersom funnet, benytt verdier herfra.
      *
     c                   eval      aenhl1_prid = l_outq
     c     aenhl1_key    chain     aenhl1r
      *
      * Dersom OCR-styring er lagt på printer-enhet
      * hent verdier herfra.
      *
     c                   if        %found
     c                   eval      l_ocrk = %editc(aeocrk:'X')
     c                   eval      l_hex1 = aehex1
     c                   eval      l_hex2 = aehex2
     c                   eval      l_type = aetype
     c                   endif
      *
     c                   endsr
      *
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
pdf   * Subrutine for overføring av felter i fra skjermbilde til param.
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
pdf   *
pdf  c     xupd_lda_pdf  begsr
pdf   *
pdf   * Legger inn rutinenavn
pdf   *
pdf  c                   eval      l_ruti = p_ruti
pdf   *
pdf   *
pdf   * Midlertidig går forbi dersom
pdf   * det er ASPSTD som kommer inn
pdf   *
pdf  c                   if        l_ruti = 'ASPSTD'
pdf  c                   goto      xforbi_pdf
pdf  c                   endif
pdf   *
pdf  c                   if        afchgv = 1
pdf  c                   eval      l_chgv = '1'
pdf  c                   else
pdf  c                   eval      l_chgv = ' '
pdf  c                   endif
pdf   *
pdf  c     xforbi_pdf    tag
pdf   *
pdf  c                   eval      l_arch = d_arki
8.01 c**                 eval      l_skje = d_skje
8.01 c                   eval      l_skje = 0
pdf  c                   eval      l_land = d_land
8.01 c**                 eval      l_exlp = d_exlp
8.01 c                   eval      l_exlp = 0
8.01 c**                 eval      l_mail = d_mail
8.01 c                   eval      l_mail = 0
6.13  * Hvis utskrift er PDF aktivert, skal den legges til deafult
6.13  * utkø PDFPRINT. Vi må sikre oss at printen ikker er borte når
6.13  * java servicer tar tak i utskriften. Reell outq sendes med i xml
6.13  * for styring av print.
6.13 c                   if         %trim(d_xmlm) = *blank
6.13 c                   if         l_outq <> *blank
6.13 c                   eval       l_utqm = l_outq
6.13 c                   endif
6.16 c                   if         l_chgv <> '1'
6.13 c                   eval       l_outq = *blanks
6.13 c                   eval       l_outq = 'PDFPRINT'
6.16 c                   endif
6.13 c                   endif
pdf   *
pdf  c                   endsr
pdf   *
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
pdf   * Subrutine for håndtering av skjermbilde
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
pdf   *
pdf  c     xskjerm_pdf   begsr
pdf   *
6.17 c                   if        l_outq = '*JOB  '
6.17 c                   eval      p_iotq = l_outq
6.17 c                   call      'AP615C '
6.17 c                   parm                    p_iotq
6.17 c                   parm                    p_uotq
6.17 c                   eval      l_outq = p_uotq
6.17 c                   endif
pdf   *
pdf   * Hvis utskrift er PDF aktivert, skal den legges til deafult
pdf   * utkø PDFPRINT. Vi må sikre oss at printen ikker er borte når
pdf   * java servicer tar tak i utskriften. Reell outq sendes med i xml
pdf   * for styring av print.
pdf   *
6.13 c                   if         b_pdf = *on and
6.13 c                              %trim(d_xmlm) = *blank
6.13 c                   eval       l_outq = *blanks
6.13 c                   eval       l_outq = 'PDFPRINT'
6.13 c                   endif
pdf   *
6.11 c                   endsr
pdf   *
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
pdf   * Subrutine for etablering av batchnummer
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
pdf   *
pdf  c     pdf_batchno   begsr
pdf   *
pdf   * Vi starter med logging av jobb, og får tilbake unik jobbid.
pdf   * Først få jobbnummer
pdf   *
pdf  c                   eval      w_kode = '0'
pdf  c                   eval      w_firm = l_firm
pdf  c                   eval      w_fell = 'NXTKLI'
pdf  c                   eval      w_type = *blank
pdf  c                   eval      w_fast = *blank
pdf  c                   eval      w_numm = *zero
pdf   *
pdf  c                   call      'AS100R'
pdf  c                   parm                    w_kode
pdf  c                   parm                    w_firm
pdf  c                   parm                    w_fell
pdf  c                   parm                    w_type
pdf  c                   parm                    w_fast
pdf  c                   parm                    w_numm
pdf   *
pdf  c                   eval      w_jnav = 'PDF' + %char(w_numm)
pdf  c                   eval      w_jtxt = 'Utskrift av PDF'
pdf  c                   call      'AB700R'
pdf  c                   parm                    w_jnav
pdf  c                   parm                    w_jkey
pdf  c                   parm                    w_jtxt
pdf  c                   eval      l_bach = w_jkey
pdf   *
pdf  c     end_batchno   endsr
pdf   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for håndtering av skjermbilde
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xskjerm       begsr
      *
     c     end_skjerm    endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Utfører slutt-kontroll
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xslutt_ktr    begsr
      *
      * Antall CPI.  Det kontrolleres at den må inneholde en av
      * flg. kjente verdier:
      *
      *
     c                   if        l_acpi <> 5  and
     c                             l_acpi <> 10 and
     c                             l_acpi <> 12 and
     c                             l_acpi <> 15 and
     c                             l_acpi <> 18 and
     c                             l_acpi <> 20
     c                   eval      l_acpi = 10
     c                   endif
      *
      * Antall skriveposisjoner pr. linje
      *
     c                   if        l_apos < 6 or
     c                             l_apos > 200
     c                   eval      l_apos = 132
     c                   endif
      *
      * Antall linjer pr. side:
      *
     c                   if        l_alin < 3 or
     c                             l_alin > 255
     c                   eval      l_alin = 48
     c                   endif
      *
      * Formulartype.
      *  Kommentar :
      *  Formular ble satt til 0001
      *  på forrige versjon, nå *STD
      *
     c                   if        l_form = *blank
     c                   eval      l_form = '*STD      '
     c                   endif
      *
      * Utkø
      *
     c                   if        l_outq = *blank
     c                   eval      l_outq = '*JOB      '
     c                   endif
      *
      * Antall kopier.
      *
     c                   if        l_akop = 0
     c                   eval      l_akop = 1
     c                   endif
      *
      * Prioritet:
      *
     c                   if        l_prty = *blank
     c                   eval      L_prty = '5'
     c                   endif
      *
     c                   if        l_prty <> '1' and
     c                             l_prty <> '2' and
     c                             l_prty <> '3' and
     c                             l_prty <> '4' and
     c                             l_prty <> '5' and
     c                             l_prty <> '6' and
     c                             l_prty <> '7' and
     c                             l_prty <> '8' and
     c                             l_prty <> '9'
     c                   eval      l_prty = '5'
     c                   endif
5.40  *
5.40  * Kontroll av :
5.40  *  - Inn-skuff
5.40  *  - Ut-skuff
5.40  *  - Duplex
5.40  *  - Papirkvalitet
5.40  *
5.40 c                   if        l_skin = 0
5.40 c                   eval      L_skin = 1
5.40 c                   endif
5.40  *
5.40 c                   if        l_dplx = *blank
5.40 c                   eval      L_dplx = '*NO'
5.40 c                   endif
5.40  *
5.40 c                   if        l_pkva = *blank
5.40 c                   eval      L_pkva = '*DRAFT'
5.40 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter inn nytt nummer fra member-teller
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xmember       begsr
      *
      * Øker memberteller med 1:
      *
     c     anumlu_key    chain     anumlur
     c                   if        %found
     c                   eval      ansist = ansist + 1
      *
     c                   if        ansist > 99999
     c                   eval      ansist = 1
     c                   endif
      *
     c                   update    anumlur
     c                   else
     c                   eval      anfirm = w_firm
     c                   eval      anfell = 'MEMBER'
     c                   eval      antxt1 = m(1)
     c                   eval      antxt2 = m(2)
     c                   eval      anwrap = '1'
     c                   eval      anntom = 99999
     c                   eval      ansist = 1
     c                   write     anumlur
     c                   endif
     c                   eval      l_mber = ansist
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *dtaara       define    *lda          lda
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_ruti
     c                   parm                    p_valg
     c                   parm                    p_tvbt
     c                   parm                    p_in03
     c                   parm                    p_btch
      *
      * Key for bruker-register
      *
     c     ausrl1_key    klist
     c                   kfld                    ausrl1_user
pdf   *
pdf   * Key for formular-file.
pdf   *
pdf  c     afopl1_key    klist
pdf  c                   kfld                    afopl1_ruti
pdf   *
pdf   * Key for avvik
pdf   *
pdf  c     afapl1_key    klist
pdf  c                   kfld                    afapl1_arut
pdf  c                   kfld                    afapl1_afir
pdf  c                   kfld                    afapl1_aavd
pdf   *
pdf   * Key for gyldig skriver
pdf   *
pdf  c     afprlr_key    klist
pdf  c                   kfld                    afprlr_sfil
pdf  c                   kfld                    afprlr_sprt
pdf   *
pdf   * Key for propertiesfil
pdf   *
pdf  c     afpslr_key    klist
pdf  c                   kfld                    afpslr_ufil
pdf  c                   kfld                    afpslr_uide
      *
      * Key for formular-file.
      *
     c     aforl1_key    klist
     c                   kfld                    aforl1_ruti
      *
      * Key for skjermfile.
      *
     c     awsdl1_key    klist
     c                   kfld                    awsdl1_wsid
     c                   kfld                    aforl1_ruti
      *
      * Key for tellerfiler:
      *
     c     anumlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    anumlu_fell
     c                   kfld                    anumlu_type
     c                   kfld                    anumlu_fast
      *
      * Key for enhetsregister (skrivere)
      *
     c     aenhl1_key    klist
     c                   kfld                    aenhl1_prid
      *
      * Initierer nøkkel anumlu_key
      *
     c                   eval      anumlu_fell = 'MEMBER'
      *
      * Initiering av øvrige variable
      *
     c                   eval      w_firm = l_firm
pdf  c                   eval      afprlr_sfil = l_filg
pdf  c                   eval      afpslr_ufil = l_filg
pdf  c                   eval      afpslr_uide = 'STATUS    '
     c                   eval      w_sjek = *blank
     c                   eval      w_outq = *blank
     c                   eval      w_alin = 4
     c                   eval      w_apos = 32
     c                   eval      p_in03 = '0'
      *
      * Legger inn rutine-navnet
      * og skjermbildevalg
      * og 'tvungen batch'
      *
     c                   eval      aforl1_ruti = p_ruti
     c                   eval      l_chgv = p_valg
     c                   eval      l_tvbt = p_tvbt
      *
      * Henter inn bruker-informasjon
      *
     c                   eval      ausrl1_user = l_user
     c     ausrl1_key    chain     ausrl1r
     c                   if        not %found
     c                   eval      abgrup = *blank
     c                   endif
pdf   *
pdf   * Sjekk om PDF aktivering er på/av
pdf   *
pdf  c     afpslr_key    chain     afpslrr
pdf  c                   if        not %found
pdf  c                   eval      l_poff = '0'
pdf  c                   else
pdf  c                   movel     afudss        l_poff
pdf  c                   endif
      *
6.21 c                   eval      l_utqm = *blanks
      *
     c                   endsr
**
Felles member teller for firma
**** Økes med 1 i AP100R  ****
