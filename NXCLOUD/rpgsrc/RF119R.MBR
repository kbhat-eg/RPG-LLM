     h datedit(*dmy.) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RF119R                                              *
      * Beskrivelse . . : Legger på sporingsref på alle transer               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      * R6.10     251009  Nytt program                                   ASK  *
6.10  *                                                                       *
6.11  *                                                                       *
7.01  *K00        060720  lagt på return + ab705r + switchstyring        bof  *
      *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frjoul2    uf   e           k disk    rename(rjoupfr:rjoul2r)
     fraa4lu    uf   e           k disk    rename(raa4pfr:raa4lur)
     frktrl3    if   e           k disk    rename(rktrpfr:rktrl3r)
     frltrl3    if   e           k disk    rename(rltrpfr:rltrl3r)
     fdstrlu    o    e           k disk    rename(dstrpfr:dstrlur)
      *
      * Definering av parametre
      *
     d p_memb          s             10
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
7.01 d l_filg                931    933
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d raa4lu_firm     s                   like(ra4fir)
     d raa4lu_4aar     s                   like(ra4aar)
     d rjoul2_nivo     s                   like(rjnivo)
     d rjoul2_memb     s                   like(rjmemb)
     d rktrl3_biln     s                   like(rnbiln)
     d rltrl3_biln     s                   like(robiln)
      *
      * Definering av øvrige variable
      *
     d w_firm          s                   like(rjfirm)
     d w_spor          s                   like(rjspor)
     d w_stmp          s               z
7.01 d w_jkey          s             41
7.01 d w_jnav          s             15
7.01 d w_jsta          s              2  0
7.01 d w_jmld          s            200
      *
     d s_firm          s                   like(rjfirm)
     d s_raar          s                   like(rjraar)
     d s_rper          s                   like(rjrper)
     d s_bunt          s                   like(rjbunt)
     d s_bdat          s                   like(rjbdat)
      *
7.01   dcl-s co402_verdi1  char(1);
7.01   dcl-s co402_verdi2  char(2048);
7.01   DCL-PR CO402R EXTPGM('CO402R');
7.01     p_filgrp            char(3)     const;
7.01     p_firm              packed(3:0) const;
7.01     p_lager             packed(2:0) const;
7.01     p_nokkel            char(50)    const;
7.01     p_verdi1            char(1);
7.01     p_verdi2            char(2048);
7.01   END-PR;
7.01 d u_logg          s              1    inz(*off)                            logging ajltpf
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  HOVEDPROGRAM
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
7.01 c                   if        u_logg = *on
7.01 c                   eval      w_jsta = 10
7.01 c                   eval      w_jmld = 'Start RF119R        '
7.01 c                   eval      w_jnav = 'OKON_RF119R-'
7.01 c                   call      'AB700R'
7.01 c                   parm                    w_jnav
7.01 c                   parm                    w_jkey
7.01 c                   parm                    w_jmld
7.01 c                   eval      w_jsta = 10
7.01 c                   eval      w_jmld = 'Start RF119R       '
7.01 c                   call      'AB705R'
7.01 c                   parm                    w_jkey
7.01 c                   parm                    w_jsta
7.01 c                   parm                    w_jmld
7.01 c                   endif
7.01  *
      * Leser poster i fra transfile
      *
     c                   eval      rjoul2_nivo = *blank
     c                   eval      rjoul2_memb = p_memb
     c     rjoul2_key    setll     rjoul2
      *
     c     rjoul2_key    reade     rjoul2
     c                   dow       not %eof
      *
      * Sjekker om ny sporingsref skal hentes
      *
     c                   if        rjfirm <> s_firm or
     c                             rjraar <> s_raar or
     c                             rjrper <> s_rper or
     c                             rjbunt <> s_bunt or
     c                             rjbdat <> s_bdat
     c                   exsr      finn_sref
     c                   eval      s_firm = rjfirm
     c                   eval      s_raar = rjraar
     c                   eval      s_rper = rjrper
     c                   eval      s_bunt = rjbunt
     c                   eval      s_bdat = rjbdat
     c                   endif
      *
      * Oppdaterer transfile med sporingsref
      *
     c                   eval      rjspor = w_spor
     c                   update    rjoul2r
      *
      * Sjekk om det er avkrysset reskontropost - oppdaterer avkrysn. ref
      *
     c                   if        rjreid = 'K' and
     c                             rjbiln <> 0
     c                   exsr      krys_kun
     c                   endif
      *
     c                   if        rjreid = 'L' and
     c                             rjbiln <> 0
     c                   exsr      krys_lev
     c                   endif
      *
     c     rjoul2_key    reade     rjoul2
     c                   enddo
      *
      *  Avslutt program
      *
7.01 c                   if        u_logg = *on
7.01 c                   eval      w_jsta = 10
7.01 c                   eval      w_jmld = 'Slutt RF119R før LR'
7.01 c                   call      'AB705R'
7.01 c                   parm                    w_jkey
7.01 c                   parm                    w_jsta
7.01 c                   parm                    w_jmld
7.01 c                   endif
      *
     c                   eval      *inlr = *on
7.01 c                   if        u_logg = *on
7.01 c                   eval      w_jsta = 10
7.01 c                   eval      w_jmld = 'Slutt RF119R etter LR'
7.01 c                   call      'AB705R'
7.01 c                   parm                    w_jkey
7.01 c                   parm                    w_jsta
7.01 c                   parm                    w_jmld
7.01 c                   endif
      *
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av sist brukte sporingsref
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_sref     begsr
      *
     c                   eval      raa4lu_firm = rjfirm
     c                   eval      raa4lu_4aar = rjraar
     c     raa4lu_key    chain     raa4lu
      *
     c                   if        %found
     c                   eval      ra4spo = ra4spo + 1
     c                   eval      w_spor = ra4spo
     c                   update    raa4lur
     c                   else
     c                   eval      w_spor = 0
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering tabell for avkrysningsref kunder
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     krys_kun      begsr
      *
     c                   eval      rktrl3_biln = rjbiln
     c     rktrl3_key    chain     rktrl3
      *
     c                   if        %found and
     c                             rnkund = rjkont
      *
     c                   time                    w_stmp
      *
     c                   eval      dsfirm = w_firm
     c                   eval      dsspor = w_spor
     c                   eval      dsavkr = rnavkr
     c                   eval      dskont = rnkund
     c                   eval      dsbelo = rnbelØ
     c                   eval      dsslet = 0
     c                   eval      dstis1 = w_stmp
     c                   eval      dseusr = l_user
      *
     c                   write     dstrlur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering tabell for avkrysningsref leverandører
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     krys_lev      begsr
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_memb
      *
      * Key for transfile
      *
     c     rjoul2_key    klist
     c                   kfld                    rjoul2_nivo
     c                   kfld                    rjoul2_memb
      *
      * Key for sist brukte nr. - koderegister
      *
     c     raa4lu_key    klist
     c                   kfld                    raa4lu_firm
     c                   kfld                    raa4lu_4aar
      *
      * Key for kundeposteringer
      *
     c     rktrl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rktrl3_biln
      *
      * Key for leverandørposteringer
      *
     c     rltrl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rltrl3_biln
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
7.01  * Test om logging skal gjøres
7.01  *
7.01   CallP CO402R(l_filg:w_firm:0:'RF-PROG LOGGING':
7.01                    co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01     u_logg = *on;
7.01   endif;
      *
     c                   endsr
