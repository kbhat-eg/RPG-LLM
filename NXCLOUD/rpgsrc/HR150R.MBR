     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : HR150R
      * Beskrivelse . . : Radioterminal trans, plukking - reg. ordre
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       110906 BOF  Nytt program
5.60  * R5.60 070108 bof  Utvidet logg med år
6.20  *       031212 bof  Viser suffix, setter KP hvis status 9.
6.21  *       041212 bof  Hvis ordre taste direkte på det stest hvilk. suff
6.nu  *       230514 bof  Endringer vedr. utvidelse av nummerserie
8.01  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffohell    if   e           k disk    rename(fohepfr:fohellr)
     ffohelr    if   e           k disk    rename(fohepfr:fohelrr)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     flwpll2    if   e           k disk    rename(lwplpfr:lwpll2r)
     ffusrl1    if   e           k disk    rename(fusrpfr:fusrl1r)
6.21 ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
      *
     fhr150d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Local Data Area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
6.21 d l_lage               1001   1002  0
      *
      * Definering av parametre
      *
     d p_firm          s                   like(fofirm)
     d p_numm          s                   like(fonumm)
     d p_lpro          s              1
     d p_kode          s              1
      *
      * Definering av variabler i nøkler
      *
     d votyl1_otyp     s                   like(vaotyp)
     d fohelr_numm     s                   like(fonumm)
     d fohelr_suff     s                   like(fosuff)
     d lwpll2_numm     s                   like(mknumm)
     d lwpll2_suff     s                   like(mksuff)
     d fusrl1_user     s                   like(fbuser)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
6.21 d fodtl1_numm     s                   like(fdnumm)
      *
      * Definering av variable
      *
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_oppr          s              1    inz(*off)
      *
     d w_endr          s              1
     d w_udat          s               d   datfmt(*iso)
     d w_stat          s              1
     d w_knav          s             30
5.60 d w_aarr          s              4  0
      *
     d w_firm          s                   like(fofirm)
     d w_numm          s                   like(fonumm)
     d w_suff          s                   like(fosuff)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(3)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_srrn - Relativt recordnummer i subfile
      * w_spge - Sidestørrelse i subfile (page)
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * wvisrn - Vis den side som inneholder denne record.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C2BLD  - C2 Skjermbilde for registrering
      * D1BLD  - D1 Skjermbilde for slette (Delete)
      * E1BLD  - E1 Skjermbilde for avslutting av varemottak
      * F1BLD  - F1 Skjermbilde for melding om vare ikke funnet
      * G1BLD  - F1 Skjermbilde for melding om vare ikke på bestilling
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   eval      *in22 = *on
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   eval      *in22 = *on
     c                   goto      b2tagb
     c                   endsl
      *
      * F4=Spørring på ordre
      *
     c*                  if        *inkd
     c*                  eval      b2numm = 0
     c*                  eval      p_lpro = *blank
     c*                  call      'FO500R'
     c*                  parm                    b2numm
     c*                  parm                    p_lpro
     c*                  eval      *in22 = *on
     c*                  if        b2numm = 0
     c*                  goto      b2tagb
     c*                  endif
     c*                  endif
      *
      * sjekker ordrenr.
      *
     c                   if        b2numm <> 0
     c                   eval      w_numm = b2numm
6.21 c                   exsr      sjekk_suff
     c                   exsr      xf1bld
     c*                  if        b_feil = *off
     c*                  exsr      xg1bld
     c*                  endif
     c                   if        b_feil = *on
     c                   goto      b2tagb
     c                   else
     c                   eval      p_kode = *blank
5.60 c                   eval      w_aarr = %subdt(fobdat:*years)
     c                   call      'FU706R '
5.60 c                   parm                    w_aarr
     c                   parm                    fonumm
     c                   parm                    fosuff
     c                   parm                    p_kode
     c                   select
     c                   when      p_kode = '2'
     c                   eval      w_endr = *blank
     c                   when      p_kode = '4'
     c                   eval      w_endr = 'E'
     c                   endsl
     c                   call      'HR151R '
     c                   parm                    w_firm
     c                   parm                    w_numm
     c                   parm                    w_suff
     c                   parm                    w_endr
     c                   exsr      forny
     c                   goto      b2taga
     c                   endif
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   eval      b2numm = 0
      *
     c     fohell_key    setll     fohell
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
      * Beholder eksisterende posisjonering i subfilen
      *
     c                   if        wcurrn > 0
     c                   eval      wvisrn = wcurrn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      wvisrn = w_srrn
     c                   eval      *in22 = *off
      *
      * Velge post
      *
     c                   if        b1valg = 1
     c                   eval      w_endr = *blank
     c                   call      'HR151R  '
     c                   parm                    w_firm
     c                   parm                    b1numm
     c                   parm                    b1suff
     c                   parm                    w_endr
     c                   eval      b_forn = *on
     c                   eval      b1valg = 0
     c*                  update    b1sfl
     c                   iter
     c                   endif
      * se på ordre informasjon
     c                   if        b1valg = 5
     c                   eval      w_numm = b1numm
     c                   eval      w_suff = b1suff
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å se på ordreinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      fohelr_numm = w_numm
     c                   eval      fohelr_suff = w_suff
     c     fohelr_key    chain     fohelr
     c                   if        not %found(fohelr)
     c                   goto      end_c2bld
     c                   endif
      *blanker felter
     c                   eval      c2knav = *blank
     c                   eval      c2kpro = *blank
     c                   eval      c2kper = *blank
     c                   eval      c2kmob = *blank
     c                   eval      c2ktlf = *blank
     c                   eval      c2ityp = *blank
     c                   eval      c2ldat = 0
     c*                  eval      c2inf1 = *blank
      *
      * Initierer skjermbilde
      *
     c                   eval      c2numm = w_numm
     c                   eval      c2suff = w_suff
     c                   eval      c2kmob = fomobi
     c                   eval      c2ityp = foityp
     c                   eval      c2ldat = 0
     c*                  eval      c2inf1 = foinf1
      *
     c                   if        foldat <> *loval
     c     *dmy          move      foldat        c2ldat
     c                   endif
      *
     c                   call      'RS750R '
     c                   parm                    w_stat
     c                   parm                    fokund
     c                   parm                    w_knav
     c                   eval      c2knav = w_knav
      *
     c                   eval      c2kpro = *blank
     c                   eval      c2kper = *blank
     c                   if        fokpro <> 0
     c                   eval      fkprl1_kund = fokund
     c                   eval      fkprl1_kpro = fokpro
     c     fkprl1_key    chain     fkprl1
     c                   eval      c2kpro = flnavn
     c                   eval      c2kper = flkprs
     c                   eval      c2ktlf = fltlfa
     c                   endif
      *
     c*                  if        foityp <> *blank
     c*                  eval      vinfl1_ityp = foityp
     c*    vinfl1_key    chain     vinfl1
     c*                  if        %found
     c*                  eval      b1itxt = vaitxt
     c*                  endif
     c*                  endif
      *
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc
     c                   goto      end_c2bld
     c                   endif
      *
     c     end_c2bld     endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av ordre ikke funnet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xf1bld        begsr
      *
     c                   eval      *in31 = *off
     c                   eval      *in32 = *off
      *
      * Tester om ordrenr. er gyldig
      *
     c                   eval      fohelr_numm = w_numm
     c                   eval      fohelr_suff = w_suff
     c     fohelr_key    chain     fohelr
     c                   if        %found(fohelr)
      *** ordre funnet
      * sjekker om kjørekontorløsning og at den er klar til plukking
     c                   if        faakjk = 1
     c                   eval      p_kode = *blank
5.60 c                   eval      w_aarr = %subdt(fobdat:*years)
     c                   call      'FU706R '
5.60 c                   parm                    w_aarr
     c                   parm                    fonumm
     c                   parm                    fosuff
     c                   parm                    p_kode
     c                   if        p_kode <> '4'
     c                   eval      *in32 = *on
     c                   endif
     c                   else
      * ikke kjørekontorløsning,sjekker om ordre ikke er i bruk
     c                   if        fokode = 1
     c                   eval      *in32  = *on
     c                   endif
     c                   endif
     c                   if        *in32 = *off
      * sjekker behandlingskode
     c                   eval      votyl1_otyp = footyp
     c     votyl1_key    chain     votyl1r
     c                   if        vaoakk = 1 and
     c                             foityp = '   '
     c                   goto      end_f1bld
     c                   else
     c                   eval      *in32 = *on
     c                   endif
     c                   endif
      *
     c                   else
     c                   eval      *in31 = *on
     c                   endif
     c                   if        *in31 = *on or
     c                             *in32 = *on
     c                   eval      b_feil = *on
     c                   endif
      *
     c                   exfmt     f1bld
      *
     c                   if        *inkc
     c                   goto      end_f1bld
     c                   endif
      *
     c                   if        *inkd
     c                   call      'FO500R'
     c                   parm                    w_numm
     c                   parm                    p_lpro
     c                   endif
      *
      *
     c     end_f1bld     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av varer allerede plukket fra ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xg1bld        begsr
      *
      *
     c                   eval      lwpll2_numm = w_numm
     c                   eval      lwpll2_suff = w_suff
     c     lwpll2_key3   setll     lwpll2
     c     lwpll2_key3   reade     lwpll2
      *
     c                   if        not %found(lwpll2)
     c                   eval      b_feil = *off
     c                   goto      end_g1bld
     c                   endif
      *
     c                   exfmt     g1bld
      *
     c                   eval      b_feil = *on
      *
     c                   if        *inkc
     c                   goto      end_g1bld
     c                   endif
      *
     c     end_g1bld     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c     fohell_key    reade     fohell
      *
     c                   if        %eof
     c                   goto      end_crt
     c                   endif
      *
      * sjekker ordretype - kun behandlingkode 1
      *
     c                   eval      votyl1_otyp = footyp
     c     votyl1_key    chain     votyl1r
     c                   if        vaoakk <> 1
     c                   iter
     c                   endif
      * sjekker status for plukking - den må enten være
      * 1 = klar for plukk,2=under plukking, 3=ferdig plukket
      *
     c                   eval      p_kode = *blank
5.60 c                   eval      w_aarr = %subdt(fobdat:*years)
     c                   call      'FU706R '
5.60 c                   parm                    w_aarr
     c                   parm                    fonumm
     c                   parm                    fosuff
     c                   parm                    p_kode
     c                   if        p_kode <> '4'
     c                   iter
     c                   endif
      *
      * sjekker om ordre skal plukkes av en spes. selger
     c                   if        foplse <> 0 and
     c                             foplse <> fbselg
     c                   iter
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   if        forest = 1
6.nu c                   eval      b1rest = 'R.'
     c                   else
     c                   eval      b1rest = *blank
     c                   endif
     c                   eval      b1numm = fonumm
     c                   eval      b1suff = fosuff
     c                   eval      p_kode = *blank
5.60 c                   eval      w_aarr = %subdt(fobdat:*years)
     c                   call      'FU706R '
5.60 c                   parm                    w_aarr
     c                   parm                    fonumm
     c                   parm                    fosuff
     c                   parm                    p_kode
     c                   select
     c                   when      p_kode = '2'
     c                   eval      b1stat = 'KP'
     c                   when      p_kode = '4'
     c                   eval      b1stat = 'KP'
6.20 c                   when      p_kode = '9'
6.20 c                   eval      b1stat = 'KP'
     c                   endsl
      *
     c                   call      'RS750R '
     c                   parm                    w_stat
     c                   parm                    fokund
     c                   parm                    w_knav
     c                   eval      b1knav = w_knav
      *
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      wvisrn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  * Subrutine for å finne riktig suffix for ordre og lager på bruker
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  *
6.21 c     sjekk_suff    begsr
6.21  *
6.21 c                   eval      w_suff = 0
6.21 c                   eval      fodtl1_numm = b2numm
6.21 c     fodtl1_key    setll     fodtl1
6.21 c     fodtl1_key    reade     fodtl1
6.21 c                   dow       not %eof(fodtl1)
6.21 c                   if        fdlage = l_lage
6.21 c                   eval      w_suff = fdsuff
6.21 c                   leavesr
6.21 c                   endif
6.21 c     fodtl1_key    reade     fodtl1
6.21 c                   enddo
6.21 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for les på radioterminal-trans
      *
     c     fohell_key    klist
     c                   kfld                    w_firm
      *
      * Key for les på radioterminal-trans
      *
     c     fohelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelr_numm
     c                   kfld                    fohelr_suff
6.21  *
6.21  * Key for les på ordre detaljer
6.21  *
6.21 c     fodtl1_key    klist
6.21 c                   kfld                    w_firm
6.21 c                   kfld                    fodtl1_numm
      *
      * Key for oppslag på ordre-type
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for les på radioterminal-trans
      *
     c     lwpll2_key3   klist
     c                   kfld                    w_firm
     c                   kfld                    lwpll2_numm
     c                   kfld                    lwpll2_suff
      *
      * Key for oppslag på bruker
      *
     c     fusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fusrl1_user
      *
      * Key for oppslag på kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      * Key for les av status
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      *
      * Henter firma og timestamp fra parameter
      *
     c                   eval      w_firm = l_firm
      *
      * Henter status
      *
     c     fstsl1_key    chain     fstsl1
      *
      * Henter selgernr
      *
     c                   eval      fusrl1_user = l_user
     c     fusrl1_key    chain     fusrl1
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      *in22 = *on
      *
     c     fohell_key    setll     fohell
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
