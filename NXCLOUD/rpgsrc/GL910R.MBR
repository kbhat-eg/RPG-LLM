     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASOKON
      *  Program . . . . : GL910R
      *  Beskrivelse . . : Generere unike filnavn med dato og klokke
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      *  --------- ------  -------------------------------------------------
      *            140524  Nytt program                            bsa
      *
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      *  Definering av datastrukturer for filnavn
      *
     d                 ds
     d d_tmps                        26
     d  d_tmps3                       3  0 overlay(d_tmps:21)
      *
     d                 ds
     d d_fnav                        20
     d  d_fnav01                      1    overlay(d_fnav)     inz('D')
     d  d_fnav02                      6  0 overlay(d_fnav:2)
     d  d_fnav03                      2    overlay(d_fnav:8)   inz('.T')
     d  d_fnav04                      6  0 overlay(d_fnav:10)
     d  d_fnavx3                      3  0 overlay(d_fnav:16)
     d                 ds
      *
      *  Definering av variable i parameter
      *
     d p_finn          s             20
     d p_fnut          s             50
      *
      * Definering av øvrige variable
      *
     d w_tmps          s               z
     d w_dato          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_dato_6        s              6  0
     d w_time_6        s              6  0
     d w_posi          s              2  0
     d w_prex          s             20
     d w_sufx          s             20
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Henter timestamp og bygger filanvn
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Splitter filnavn basert på prefiks og suffiks.
      *
     c                   eval      w_posi = %scan('.':p_finn)
     c                   if        w_posi > 0
     c                   eval      w_prex = %subst(p_finn:1:w_posi-1)
     c                   eval      w_sufx = %subst(p_finn:w_posi+1)
     c                   endif
      *
     c                   time                    w_dato
     c                   time                    w_time
     c                   time                    w_tmps
      *
     c     *ymd          move      w_dato        w_dato_6
     c     *hms          move      w_time        w_time_6
     c                   move      w_tmps        d_tmps
     c                   eval      d_fnav02 = w_dato_6
     c                   eval      d_fnav04 = w_time_6
     c                   eval      d_fnavx3 = d_tmps3
     c                   eval      p_fnut = %trim(%trim(w_prex) + '_' +
     c                             %trim(d_fnav) + '.' + %trim(w_sufx))
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    p_finn
     c                   parm                    p_fnut
      *
     c                   endsr
