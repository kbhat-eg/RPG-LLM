     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : LJ100R
      * Beskrivelse . . : Vedlikehold av faste bestillingstekster
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *  6.10  170609 BSA  Oppdatert med sporingsinformasjon
7.xx  *  7.00  150316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flftxl1    if   e           k disk    rename(lftxpfr:lftxl1r)
     flftxl2    if   e           k disk    rename(lftxpfr:lftxl2r)
     flftxlr    if   e           k disk    rename(lftxpfr:lftxlrr)
     flftxlu    uf a e           k disk    rename(lftxpfr:lftxlur)
     flj100d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av array
      *
     d a_meld          s             11    dim(4) ctdata perrcd(1)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d lftxl1_numm     s                   like(ljnumm)
     d lftxl2_refx     s                   like(ljrefx)
     d lftxl2_numm     s                   like(ljnumm)
     d lftxlr_numm     s                   like(ljnumm)
     d lftxlu_numm     s                   like(ljnumm)
      *
      * Definering av variable
      *
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(ljfirm)
     d w_numm_gml      s                   like(ljnumm)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_srrn - Relativt recordnummer i subfile
      * w_spge - Sidestørrelse i subfile (page)
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * w_visr - Vis den side som inneholder denne record.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for Batchnummer
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   exsr      forny
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2numm <> *blank or
     c                             b2refx <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_seqe = ' '
     c     lftxl1_key    setll     lftxl1
     c                   endif
      *
     c                   if        w_seqe = 'A'
     c     lftxl2_key    setll     lftxl2
     c                   endif
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på nummer
      *
     c                   if        b2numm <> *blank
     c                   eval      w_seqe = ' '
     c                   eval      lftxl1_numm = b2numm
     c     lftxl1_key    setll     lftxl1
     c                   endif
      *
      * Posisjoner startverdi på tekstreferanse
      *
     c                   if        b2refx <> *blank
     c                   eval      w_seqe = 'A'
     c                   eval      lftxl2_refx = b2refx
     c                   eval      lftxl2_numm = *blank
     c     lftxl2_key    setll     lftxl2
     c                   endif
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2numm = *blank
     c                   eval      b2refx = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
      * Beholder eksisterende posisjonering i subfilen
      *
     c                   if        w_curn > 0
     c                   eval      w_visr = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      w_visr = w_srrn
      *
      * Endre post
      *
     c                   if        b1valg = 2
     c                   eval      c1numm = b1numm
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopiere post
      *
     c                   if        b1valg = 3
     c                   eval      k1numm = b1numm
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slette post
      *
     c                   if        b1valg = 4
     c                   eval      d1numm = b1numm
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vise post
      *
     c                   if        b1valg = 5
     c                   eval      c1numm = b1numm
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c     c1taga        tag
      *
     c                   eval      c1numm = *blank
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Validering
      *
     c                   if        c1numm = *blank
     c                   eval      *in31 = *on
     c                   goto      c1taga
     c                   endif
      *
     c                   eval      lftxlr_numm = c1numm
     c     lftxlr_key    chain     lftxlr
      *
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      c1taga
     c                   endif
      *
     c                   exsr      xc2bld
      *
     c     end_c1bld     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      lftxlr_numm = c1numm
     c     lftxlr_key    chain     lftxlr
      *
     c                   if        %found
     c                   eval      c2nyen = a_meld(2)
     c                   eval      c2refx = ljrefx
     c                   eval      c2sl01 = ljsl01
     c                   eval      c2sl02 = ljsl02
     c                   eval      c2sl03 = ljsl03
     c                   eval      c2sl04 = ljsl04
     c                   eval      c2sl05 = ljsl05
     c                   eval      c2sl06 = ljsl06
     c                   eval      c2sl07 = ljsl07
     c                   eval      c2sl08 = ljsl08
     c                   eval      c2sl09 = ljsl09
     c                   eval      c2sl10 = ljsl10
     c                   eval      c2sl11 = ljsl11
     c                   eval      c2sl12 = ljsl12
     c                   eval      c2sl13 = ljsl13
     c                   eval      c2sl14 = ljsl14
     c                   eval      c2sl15 = ljsl15
     c                   eval      c2sl16 = ljsl16
      *
     c                   else
     c                   eval      c2refx = *blank
     c                   eval      c2sl01 = *blank
     c                   eval      c2sl02 = *blank
     c                   eval      c2sl03 = *blank
     c                   eval      c2sl04 = *blank
     c                   eval      c2sl05 = *blank
     c                   eval      c2sl06 = *blank
     c                   eval      c2sl07 = *blank
     c                   eval      c2sl08 = *blank
     c                   eval      c2sl09 = *blank
     c                   eval      c2sl10 = *blank
     c                   eval      c2sl11 = *blank
     c                   eval      c2sl12 = *blank
     c                   eval      c2sl13 = *blank
     c                   eval      c2sl14 = *blank
     c                   eval      c2sl15 = *blank
     c                   eval      c2sl16 = *blank
      *
     c                   eval      c2nyen = a_meld(1)
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c2nyen = a_meld(1)
     c                   eval      c1numm = k2numm
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   eval      c2nyen = a_meld(3)
     c                   endif
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   lftxlur
      *
     c                   eval      lftxlu_numm = c1numm
     c     lftxlu_key    chain     lftxlur
      *
     c                   eval      ljrefx = c2refx
     c                   eval      ljsl01 = c2sl01
     c                   eval      ljsl02 = c2sl02
     c                   eval      ljsl03 = c2sl03
     c                   eval      ljsl04 = c2sl04
     c                   eval      ljsl05 = c2sl05
     c                   eval      ljsl06 = c2sl06
     c                   eval      ljsl07 = c2sl07
     c                   eval      ljsl08 = c2sl08
     c                   eval      ljsl09 = c2sl09
     c                   eval      ljsl10 = c2sl10
     c                   eval      ljsl11 = c2sl11
     c                   eval      ljsl12 = c2sl12
     c                   eval      ljsl13 = c2sl13
     c                   eval      ljsl14 = c2sl14
     c                   eval      ljsl15 = c2sl15
     c                   eval      ljsl16 = c2sl16
      *
     c                   if        %found
6.10 c                   time                    ljedat
6.10 c                   time                    ljetim
6.10 c                   eval      ljeusr = l_user
     c                   update    lftxlur
     c                   else
     c                   eval      ljfirm = w_firm
     c                   eval      ljnumm = c1numm
6.10 c                   time                    ljodat
6.10 c                   time                    ljotim
6.10 c                   eval      ljousr = l_user
6.10 c                   time                    ljedat
6.10 c                   time                    ljetim
6.10 c                   eval      ljeusr = l_user
     c                   write     lftxlur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1numm = c1numm
     c                   eval      b1refx = c2refx
     c                   endif
      *
     c                   if        b1valg = 1 or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      lftxlr_numm = d1numm
     c     lftxlr_key    chain     lftxlr
      *
     c                   eval      d1numm = ljnumm
     c                   eval      d1refx = ljrefx
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
      * Sletter rad
      *
     c                   eval      lftxlu_numm = d1numm
     c     lftxlu_key    chain     lftxlur
     c                   delete    lftxlur
     c                   eval      b_forn = *on
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      w_numm_gml = k1numm
      *
      * Fyller kopierings-bildet
      *
     c                   eval      lftxlr_numm = k1numm
     c     lftxlr_key    chain     lftxlr
      *
      *
     c                   eval      k1numm = ljnumm
     c                   eval      k1refx = ljrefx
     c                   eval      k2numm = *blank
      *
     c     k1tagb        tag
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k2numm = *blank
     c                   eval      *in31 = *on
     c                   goto      k1tagb
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      lftxlr_numm = k2numm
     c     lftxlr_key    chain     lftxlr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1tagb
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1numm = w_numm_gml
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   if        w_seqe = ' '
     c                   read      lftxl1                                 15
     c                   else
     c                   read      lftxl2                                 15
     c                   endif
      *
     c                   if        *in15
     c                   goto      end_crt
     c                   endif
      *
     c                   if        ljfirm <> w_firm
     c                   eval      *in15 = *on
     c                   goto      end_crt
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1numm = ljnumm
     c                   eval      b1refx = ljrefx
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_visr = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for les av TEKST med kode
      *
     c     lftxl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lftxl1_numm
      *
      * Key for les av TEKST med referanse
      *
     c     lftxl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lftxl2_refx
     c                   kfld                    lftxl2_numm
      *
      * Key for direkte les av TEKST
      *
     c     lftxlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lftxlr_numm
      *
      * Key for oppdatering av TEKST
      *
     c     lftxlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lftxlu_numm
      *
      * Henter firmanummer fra LDA
      *
     c                   eval      w_firm = l_firm
      *
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      lftxl1_numm = *blank
     c     lftxl1_key    setll     lftxl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
**
  Ny post                  001
  Endring                  002
  Vise                     003
* Slettet *                004
