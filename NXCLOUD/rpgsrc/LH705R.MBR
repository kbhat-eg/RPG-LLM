      /TITLE  ASLAGR: Bestillingsforslag
     h datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LH705R
      *  Beskrivelse . . : Bestillingsforslag - overføring fra håndterminal
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign
      * --------- ------  -------------------------------------------------
      *           141299  Nytt program                             ASK
6.nu  *           260514  Sjekket mht. nummerutvidelsen            bof
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
      *
     flh705d    cf   e             workstn
      *
      *  Parameter record
      *
     d                 ds
     d d_prec                        32
     d  d_otyp                        2    overlay(d_prec)
     d  d_ldor                        6  0 overlay(d_prec:3)
     d  d_lage                        2  0 overlay(d_prec:9)
     d  d_bdat                        6  0 overlay(d_prec:11)
     d  d_ldat                        6  0 overlay(d_prec:17)
     d  d_numm                        6  0 overlay(d_prec:23)
     d  d_avde                        4  0 overlay(d_prec:29)
      *
      * Utskrifts-parameter
      *
     d                 ds
     d d_urec                        13
     d  d_fbes                        6  0 overlay(d_urec)
     d  d_tbes                        6  0 overlay(d_urec:7)
     d  d_sort                        1  0 overlay(d_urec:13)
      *
      *  Local Data Area
      *
     d                uds
     d l_firm                944    946  0
     d l_fnav                951    980
     d l_lage               1001   1002  0
      *
      * Definering av variable i nøkler
      *
     d votyl1_otyp     s                   like(vaotyp)
     d rlevl1_levr     s                   like(rllevr)
      *
      * Definering av øvrige variable
      *
     d w_firm          s                   like(vaofir)
     d w_navn          s                   like(rlnavn)
     d w_kode          s              2  0
     d w_text          s             20
     d w_adrs          s             30
     d w_ponr          s              4  0
     d w_pste          s             30
     d w_avde          s              4  0
     d w_bdat          s               d   datfmt(*iso)
     d w_ldat          s               d   datfmt(*iso)
     d w_sort          s              1  0
     d w_valg          s              1
      *
     d b_feil          s              1    inz(*off)
     d w_feil          s              1    inz(*off)
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      *  Setter variable som skal vises i skjermbildet
      *
     c     a1tag         tag
     c                   eval      a1otyp = 'B '
     c                   eval      a1ldor = 0
     c                   eval      a1lage = l_lage
     c                   eval      a1bdat = 0
     c                   eval      a1ldat = 0
      *
     c     a1tagb        tag
     c                   exfmt     a1bld
      *
      *  F3=Gå tilbake  (Avslutt)
      *
     c                   if        *inkc = *on
     c                   goto      xslutt
     c                   endif
      *
      *  F4=Spørring
      *
     c                   if        *inkd = *on
     c                   if        wactre = 'A1BLD'
     c                   exsr      sp_input
     c                   goto      a1tagb
     c                   endif
     c                   endif
      *
      *  Sjekker input
      *
     c                   eval      b_feil = *off
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      a1tagb
     c                   endif
      *
      *  Kaller program for kopiering av ekstern fil
      *
     c                   call      'HB700C'
     c                   parm                    w_feil
     c                   parm                    w_valg
      *
     c                   if        w_feil = *on
     c                   goto      forbi
     c                   endif
      *
      *  Kaller program avhengig av valg
      *
     c                   eval      d_otyp = a1otyp
     c                   eval      d_ldor = a1ldor
     c                   eval      d_lage = a1lage
     c                   eval      d_avde = w_avde
     c                   eval      d_bdat = a1bdat
     c                   eval      d_ldat = a1ldat
     c                   eval      d_numm = 0
      *
     c                   if        w_valg = '1'
     c                   call      'HB701R'
     c                   parm                    d_prec
     c                   endif
      *
     c                   if        w_valg = '2'
     c                   call      'HB702R'
     c                   parm                    d_prec
     c                   endif
      *
     c                   if        w_valg = '3'
     c                   call      'HB703R'
     c                   parm                    d_prec
     c                   endif
      *
      *  Legger ut kvittering av resultat
      *
     c     forbi         tag
     c                   if        d_numm > 0
     c                   eval      *in38 = *on
     c                   else
     c                   eval      *in37 = *on
     c                   endif
      *
     c     a1tagc        tag
     c                   eval      a1numm = d_numm
     c                   exfmt     a1bld
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for spørring på input A1BLD
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sp_input      begsr
      *
      *  Spørring på ordretype
      *
     c                   if        wactfe = 'A1OTYP'
     c                   call      'VA550R'
     c                   parm                    w_firm
     c                   parm                    a1otyp
      *
     c                   goto      sp_end
     c                   endif
      *
      *  Spørring på leverandør
      *
     c                   if        wactfe = 'A1LDOR'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    a1ldor
     c                   parm                    a1letx
      *
     c                   goto      sp_end
     c                   endif
      *
      *  Spørring lager
      *
     c                   if        wactfe = 'A1LAGE'
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    a1lage
     c                   parm                    a1latx
     c                   goto      sp_end
     c                   endif
      *
     c     sp_end        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for sjekking av input A1BLD
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
      *  Sjekk   Finnes ordretype
      *
     c                   eval      votyl1_otyp = a1otyp
     c     votyl1_key    chain     votyl1r                            90
     c                   if        *in90 = *on
     c                   eval      *in31 = *on
     c                   eval      b_feil = *on
     c                   goto      sjekk_end
     c                   endif
      *
      *  Sjekk, Finnes leverandør
      *
     c                   if        a1ldor <> 0
     c                   eval      rlevl1_levr = a1ldor
     c     rlevl1_key    chain     rlevl1r                            90
     c                   if        *in90 = *on
     c                   eval      *in32 = *on
     c                   eval      b_feil = *on
     c                   goto      sjekk_end
     c                   endif
     c                   eval      a1letx = rlnavn
     c                   endif
      *
      *  Sjekk, Finnes lager
      *
     c                   call      'FÅ720R'
     c                   parm                    w_firm
     c                   parm                    a1lage
     c                   parm                    a1latx
     c                   parm                    w_adrs
     c                   parm                    w_ponr
     c                   parm                    w_pste
     c                   parm                    w_avde
     c                   parm                    b_feil
      *
     c                   if        b_feil = *on
     c                   eval      *in33 = *on
     c                   goto      sjekk_end
     c                   endif
      *
      *  Sjekk, Er dato lagt inn og er gyldig
      *
     c                   if        a1bdat <> 0
     c     *dmy          test(d)                 a1bdat                 34
     c                   if        *in34 = *on
     c                   eval      b_feil = *on
     c                   goto      sjekk_end
     c                   endif
     c                   endif
      *
     c                   if        a1ldat <> 0
     c     *dmy          test(d)                 a1ldat                 36
     c                   if        *in36 = *on
     c                   eval      b_feil = *on
     c                   goto      sjekk_end
     c                   endif
     c                   endif
      *
     c                   if        a1bdat <> 0 and
     c                             a1ldat <> 0
     c     *dmy          move      a1bdat        w_bdat
     c     *dmy          move      a1ldat        w_ldat
     c                   eval      *in35 = *off
     c                   if        w_ldat < w_bdat
     c                   eval      *in35 = *on
     c                   eval      b_feil = *on
     c                   goto      sjekk_end
     c                   endif
     c                   endif
      *
     c     sjekk_end     endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     *inzsr        begsr
      *
      *  Key for les av ORDRE-TYPE
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      *  Key for les av LEVERANDØR
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      *  Henter inn firmanummer fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
