     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LI166R
      * Beskrivelse . . : Viser utregning av EDI linjer
      *
      * Spesialversjon. : Mestergruppen
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       090916 bsa  Nytt program
7.01  *       251016 bsa  Omorganisering av felter i skjermbildet
7.02  *       310517 bsa  Legger ut detaljer for utregning pr linje
7.03  *       070617 bsa  Bruk beløp der de finnes, deretter prosent
      *
8.01  * 8.00  060921 bsa  Utvidet meldingsnummer til 8 siffer. Justert
8.01  *                   GTIN til 14 siffer.
8.02  * 8.00  261121 bsa  Takler å komme ra brede skjermbilder
8.03  *K0000  310322 BSA  Henter koden fra riktig sted.
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fledil1    if   e           k disk    rename(ledipfr:ledil1r)
     fledilr    if   e           k disk    rename(ledipfr:ledilrr)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
      *
     fli166d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local Data Arae (LDA)
      *
     d                uds
     d l_user                911    920
     d l_fnav                951    980
      *
      * Parameter-inn
      *
     d d_prec          ds
     d  d_firm                        3  0
     d  d_type                        4
8.01 d  d_mnum                        8  0
     d  d_mlin                        6  0
     d  d_biln                        8  0
     d  d_kode                        1
     d  d_ekod                        1
      *
      * FAKTURA Hode
      *
     d d_fakh1         ds
     d  d_fh_dtyp                     3
     d  d_fh_fanr                    35
     d  d_fh_fako                     3
     d  d_fh_ldat                      d   datfmt(*iso)
     d  d_fh_best                    20
     d  d_fh_lord                    20
     d  d_fh_lfak                    20
     d  d_fh_sean                    13  0
     d  d_fh_snav                    30
     d  d_fh_fean                    13  0
     d  d_fh_fnav                    30
     d  d_fh_fad1                    30
     d  d_fh_fad2                    30
     d  d_fh_fste                    30
     d  d_fh_forg                    20
     d  d_fh_kref                    20
     d  d_fh_lref                    20
     d  d_fh_valk                     3
     d  d_fh_ffda                      d   datfmt(*iso)
     d  d_fh_levb                     3
     d  d_fh_forf                      d   datfmt(*iso)
     d  d_fh_bean                    13  0
     d  d_fh_biln                     8  0
      *
      * Tillegg/fradragslinjer
      *
     d d_fakh2         ds
     d  d_fh_tsig                     3
     d  d_fh_tkod                     3
     d  d_fh_ttek                    30
     d  d_fh_tpro                     4  2
     d  d_fh_tbel                     8  2
     d  d_fh_apro                     4  2
      *
      * Fritekst/hodelinje
      *
     d d_fakh3         ds
     d  d_fh_ftxq                     3
     d  d_fh_ftxt                    70
      *
      * Faktura partsinfo./hode
      *
     d d_fakh5         ds
     d  d_fh_part                     3
     d  d_fh_pean                    13  0
     d  d_fh_pnav                    30
     d  d_fh_pad1                    30
     d  d_fh_pad2                    30
     d  d_fh_pste                    30
     d  d_fh_porg                    20
      *
      * Faktura transport/lev. - hode
      *
     d d_fakh6         ds
     d  d_fh_trmo                     3
     d  d_fh_trna                    35
     d  d_fh_lebe                     3
      *
      * FAKTURA Linje
      *
     d d_fakl          ds
     d  d_fl_llin                     6  0
8.01 d  d_fl_vean                    14  0
     d  d_fl_vnob                     8  0
     d  d_fl_vlev                    15
     d  d_fl_tek1                    35
     d  d_fl_tek2                    35
     d  d_fl_bkva                    15  3
     d  d_fl_benh                     3
     d  d_fl_lkva                    15  3
     d  d_fl_lenh                     3
     d  d_fl_fkva                    15  3
     d  d_fl_fenh                     3
     d  d_fl_bnet                    15  2
     d  d_fl_bbto                    15  2
     d  d_fl_prkv                     3
     d  d_fl_pris                    15  3
     d  d_fl_penh                     3
     d  d_fl_prpr                     9  0
     d  d_fl_bnum                    20
     d  d_fl_blin                     6  0
     d  d_fl_mvap                     4  2
     d  d_fl_sig1                     3
     d  d_fl_kod1                     3
     d  d_fl_bel1                     9  2
     d  d_fl_pro1                     5  2
     d  d_fl_sig2                     3
     d  d_fl_kod2                     3
     d  d_fl_bel2                     9  2
     d  d_fl_pro2                     5  2
     d  d_fl_sig3                     3
     d  d_fl_kod3                     3
     d  d_fl_bel3                     9  2
     d  d_fl_pro3                     5  2
     d  d_fl_sig4                     3
     d  d_fl_kod4                     3
     d  d_fl_bel4                     9  2
     d  d_fl_pro4                     5  2
     d  d_fl_sig5                     3
     d  d_fl_kod5                     3
     d  d_fl_bel5                     9  2
     d  d_fl_pro5                     5  2
     d  d_fl_sig6                     3
     d  d_fl_kod6                     3
     d  d_fl_bel6                     9  2
     d  d_fl_pro6                     5  2
     d  d_fl_vevr                    15
8.03 d  d_fl_mvak                     1
     d  d_fl_manu                     3
      *
      * Fakturatotallinjer
      *
     d d_fakt          ds
     d  d_ft_ftot                    15  2
     d  d_ft_teks                    15  2
     d  d_ft_tmva                    15  2
     d  d_ft_mvgr                    15  2
     d  d_ft_veks                    15  2
     d  d_ft_vink                    15  2
     d  d_ft_stil                    15  2
     d  d_ft_sfra                    15  2
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d ledilr_type     s                   like(litype)
     d ledilr_mnum     s                   like(limnum)
     d ledilr_mlin     s                   like(limlin)
     d ledil1_type     s                   like(litype)
     d ledil1_mnum     s                   like(limnum)
8.01 d ledil1_mlin     s                   like(limlin)
     d rlevl1_levr     s                   like(rllevr)
      *
      * Definering av variable i parametre
      *
     d p_parm          s             30
     d p_gtin          s             14  0
     d p_vare          s             15
     d p_genh          s              3
     d p_nobb          s              8  0
     d p_stat          s              1
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d b_forn          s              1    inz(*off)
     d b_vare          s              1    inz(*off)
      *
     d w_mnum          s                   like(limnum)
     d w_mlin          s                   like(limlin)
     d w_firm          s                   like(lifirm)
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_year          s              4  0
     d w_ldor          s                   like(lildor)
     d w_kund          s                   like(linumm)
     d w_biln          s              8  0
     d w_llin          s              6  0
     d w_ltot          s             15  2
     d w_pris          s             15  3
     d w_prib          s             15  3
     d w_pril          s             11  2
     d w_mvap          s              4  2
     d w_prig          s             15  3
     d w_frad          s             15  3
     d w_fral          s             11  2
     d w_ftot          s             15  2
     d w_totb          s             15  2
     d w_antl          s             11  3
     d w_netl          s             11  2
      *
     d w_mld1          s             50
     d w_mld2          s             50
     d w_svar          s              1
     d w_in12          s              1  0
     d w_aarr          s              4  0
     d w_leva          s              6
     d w_tell          s              1  0
7.02 d w_kalk          s             15  3
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(10)
      *
     d lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
     d digit           c                   '0123456789 '
     d null            c                   '0000000000000'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * D1 Behandle åpningsopplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     d1tag         tag
      *
     c                   exsr      gen_diff
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   eval      ledil1_mlin = b1mlin
     c                   else
     c                   eval      ledil1_mlin = 500000
     c                   endif
      *
     c     ledil1_key    setll     ledil1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * D1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     d1taga        tag
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   eval      *in22 = *on
      *
     c                   eval      b2fanr = d_fh_fanr
     c                   eval      b2totf = d_ft_teks
     c                   eval      b2levr = w_ldor
     c                   eval      b2lnav = d_fh_snav
     c                   eval      b2kund = w_kund
     c                   eval      b2knav = d_fh_fnav
      *
     c                   eval      b2over = *blank
     c                   eval      b2over = 'Varenr   Tekst' +
     c                                      '        ' +
     c                                      'Antall  Enh       Brutto       F' +
     c                                      'radrag          Sum  S'
      *
     c                   eval      b2difs = 0
     c                   eval(h)   b2difs = w_ltot - d_ft_teks
     c                   eval(h)   b2kals = w_ltot
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile                                  DSP SFL
      *
     c                   eval      *in22 = *off
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkh
     c                   exsr      vis_tillegg
     c                   goto      b2tagb
     c                   when      *inki
     c                   exsr      vis_total
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile                                  CRT SFL
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on                                  CRT SFL
     c                   goto      b2taga
     c                   endsl
      *
      * F3=Avslutt
      *
     c                   if        *inkc = *on
     c                   goto      avslutt
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      d1taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      ledil1_mlin = b1mlin
     c     ledil1_key    setll     ledil1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      w_virn = w_srrn
      *
      * Vise linje detaljert
      *
     c                   if        b1valg = 5
     c                   exsr      vise_linje
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
7.02  *
7.02  * Vise linje detaljert utreging
7.02  *
7.02 c                   if        b1valg = 6
7.02 c                   exsr      calk_linje
7.02 c                   eval      b1valg = 0
7.02 c                   update    b1sfl
7.02 c                   iter
7.02 c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil                     SFL END
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      ledil1
      *
     c                   if        %eof or                                      EOF
     c                             lifirm <> w_firm or
     c                             limnum <> d_mnum or
     c                             limlin < 500000 or
     c                             limlin > 549999
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      ledil1_mlin = limlin
     c                   eval      d_fakl = limeld
      *
     c**                 eval      b1valg = 0
     c                   eval      b1mlin = limlin
      *
      * Konkatinerer linje
      *
     c                   if        %trim(d_fl_vevr) <> *blanks and
     c                             %trim(d_fl_vevr) <> '000000000000000'
     c                   movel     d_fl_vevr     b1vare
     c                   else
     c                   movel     d_fl_vnob     b1vare
     c                   endif
      *
     c                   exsr      finn_pris
      *
     c                   if        d_fl_vevr = *blank and
     c                             d_fl_vnob = 0
     c                   eval      b1text = %trim(d_fl_tek1) + ' ' +
     c                                      %trim(d_fl_tek2)
     c                   else
     c                   eval      w_antl = d_fl_fkva
     c                   eval      w_netl = d_fl_bnet
7.03 c                   if        d_fl_bbto <> 0
7.03 c                   eval(h)   w_pril = d_fl_bbto
7.03 c                   else
     c                   eval(h)   w_pril = d_fl_pris * d_fl_fkva
7.03 c                   endif
     c                   eval(h)   w_fral = w_fral * d_fl_fkva
     c                   eval      b1text = b1vare + ' ' +
     c                             %subst(d_fl_tek1:1:6) + ' ' +
     c                             %editw(w_antl : '      0 ,   -') +
     c                                 ' ' + d_fl_fenh + ' ' +
     c                             %editw(w_pril : '       0 ,  -') + ' ' +
     c                             %editw(w_fral : '       0 ,  -') + ' ' +
     c                             %editw(w_netl : '       0 ,  -') +
     c                             listat
      *
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     ledil1_key    setgt     ledil1
     c                   readp     ledil1
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     ledil1
      *
     c                   if        %eof or                                      EOF
     c                             lifirm <> w_firm or
     c                             limnum <> d_mnum or
     c                             limlin < 500000 or
     c                             limlin > 549999
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   eval      ledil1_mlin = limlin
      *
     c                   enddo
      *
     c                   if        %eof
     c                   eval      ledil1_mlin = limlin
     c     ledil1_key    setll     ledil1
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å vise linjedetaljer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     vise_linje    begsr
      *
     c                   eval      ledil1_mlin = b1mlin
     c     ledil1_key    chain     ledil1r
     c                   if        not %found
     c                   goto      end_vise
     c                   endif
      *
     c                   eval      d_fakl = limeld
      *
     c                   if        d_fl_vevr = *blanks and
     c                             d_fl_vnob <> 0
     c                   movel     d_fl_vnob     d_fl_vevr
     c                   endif
      *
     c                   eval      c5vare = b1vare
     c                   eval      c5tek1 = d_fl_tek1
     c**                 eval      c1tek2 = d_fl_tek2
     c                   eval      c5enhe = d_fl_fenh
     c                   eval      c5anta = d_fl_fkva
     c                   eval      c5sapr = d_fl_pris
     c                   eval      c5penh = d_fl_penh
     c                   eval      c5prkv = d_fl_prkv
     c                   eval      c5btot = d_fl_bbto
     c                   eval      c5rab1 = d_fl_pro1
     c                   eval      c5sig1 = d_fl_sig1
     c                   eval      c5kod1 = d_fl_kod1
     c                   eval      c5rsu1 = d_fl_bel1
     c                   eval      c5rab2 = d_fl_pro2
     c                   eval      c5sig2 = d_fl_sig2
     c                   eval      c5kod2 = d_fl_kod2
     c                   eval      c5rsu2 = d_fl_bel2
     c                   eval      c5rab3 = d_fl_pro3
     c                   eval      c5sig3 = d_fl_sig3
     c                   eval      c5kod3 = d_fl_kod3
     c                   eval      c5rsu3 = d_fl_bel3
     c                   eval      c5lsum = d_fl_bnet
     c                   eval      c5mvap = w_mvap
     c                   eval      c5gtin = d_fl_vean
     c                   eval      c5genh = *blanks
7.02  *
7.02 c                   eval      c5stat = *blanks
7.02 c                   select
7.02 c                   when      listat = ' '
7.02 c                   eval      c5stat = 'Ok!         '
7.02 c                   when      listat = '1'
7.02 c                   eval      c5stat = 'Endret lev. '
7.02 c                   when      listat = '2'
7.02 c                   eval      c5stat = 'Avvist lev  '
7.02 c                   when      listat = '3'
7.02 c                   eval      c5stat = 'Ikke på fakt'
7.02 c                   when      listat = '4'
7.02 c                   eval      c5stat = 'Ikke på best'
7.02 c                   when      listat = '5'
7.02 c                   eval      c5stat = 'Avvik antall'
7.02 c                   when      listat = '6'
7.02 c                   eval      c5stat = 'Avvik enhet '
7.02 c                   when      listat = '7'
7.02 c                   eval      c5stat = 'Avvik pris  '
7.02 c                   when      listat = 'B'
7.02 c                   eval      c5stat = 'Beløpsavvik '
7.02 c                   endsl
      * Hent enhet fra GTIN nummer
     c                   if        d_fl_vean > 0
     c                   eval      p_gtin = d_fl_vean
     c                   eval      p_vare = *blanks
     c                   eval      p_genh = *blanks
     c                   eval      p_nobb = 0
     c                   eval      p_stat = *blanks
      *
     c                   call      'VE717R'
     c                   parm                    w_firm
     c                   parm                    p_gtin
     c                   parm                    p_vare
     c                   parm                    p_genh
     c                   parm                    p_nobb
     c                   parm                    p_stat
      *
     c                   eval      c5genh = p_genh
     c                   endif
      *
     c                   exfmt     c5win
      *
     c                   if        *inkc = *on
     c                   leavesr
     c                   endif
      *
     c     end_vise      endsr
      *
7.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.02  * Subrutine for å vise utregnet linje
7.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.02  *
7.02 c     calk_linje    begsr
7.02  *
7.02 c                   eval      ledil1_mlin = b1mlin
7.02 c     ledil1_key    chain     ledil1r
7.02 c                   if        not %found
7.02 c                   goto      end_calk
7.02 c                   endif
7.02  *
7.02 c                   eval      d_fakl = limeld
7.02  *
7.02 c                   if        d_fl_vevr = *blanks and
7.02 c                             d_fl_vnob <> 0
7.02 c                   movel     d_fl_vnob     d_fl_vevr
7.02 c                   endif
      *
7.02 c                   eval      c6vare = b1vare
7.02 c                   eval      c6tek1 = d_fl_tek1
7.02 c                   eval      c6enhe = d_fl_fenh
7.02 c                   eval      c6anta = d_fl_fkva
7.02 c                   eval      c6sapr = d_fl_pris
7.02 c                   eval      c6penh = d_fl_penh
7.02 c                   if        d_fl_prkv = 'AAB'
7.02 c                   eval      c6prkv = 'BRT'
7.02 c                   else
7.02 c                   eval      c6prkv = 'NET'
7.02 c                   endif
7.02 c                   eval(h)   c6ktot = c6anta * c6sapr
7.02 c                   eval(h)   w_kalk = c6anta * c6sapr
7.02 c                   eval      c6btot = d_fl_bbto
7.02 c                   eval      c6rab1 = d_fl_pro1
7.02 c                   eval      c6rsu1 = d_fl_bel1
7.02  * Beregne rabattbeløp 1 hvis ikke utfylt
7.02 c                   if        c6rsu1 = 0 and
7.02 c                             c6rab1 <> 0
7.02 c                   eval      c6rsu1 = (w_kalk * c6rab1)/100
7.02 c                   if        d_fl_sig1 = 'C  '
7.02 c                   eval      w_kalk = w_kalk + c6rsu1
7.02 c                   else
7.02 c                   eval      w_kalk = w_kalk - c6rsu1
7.02 c                   endif
7.02 c                   endif
7.02 c                   if        d_fl_sig1 = 'C  '
7.02 c                   eval      c6sig1 = '+'
7.02 c                   else
7.02 c                   eval      c6sig1 = '-'
7.02 c                   endif
7.02 c                   eval      c6rab2 = d_fl_pro2
7.02 c                   eval      c6rsu2 = d_fl_bel2
7.02  * Beregne rabattbeløp 2 hvis ikke utfylt
7.02 c                   if        c6rsu2 = 0 and
7.02 c                             c6rab2 <> 0
7.02 c                   eval      c6rsu2 = (w_kalk * c6rab2)/100
7.02 c                   if        d_fl_sig2 = 'C  '
7.02 c                   eval      w_kalk = w_kalk + c6rsu2
7.02 c                   else
7.02 c                   eval      w_kalk = w_kalk - c6rsu2
7.02 c                   endif
7.02 c                   endif
7.02 c                   if        d_fl_sig2 = 'C  '
7.02 c                   eval      c6sig2 = '+'
7.02 c                   else
7.02 c                   eval      c6sig2 = '-'
7.02 c                   endif
7.02 c                   eval      c6rab3 = d_fl_pro3
7.02 c                   eval      c6rsu3 = d_fl_bel3
7.02  * Beregne rabattbeløp 3 hvis ikke utfylt
7.02 c                   if        c6rsu3 = 0 and
7.02 c                             c6rab3 <> 0
7.02 c                   eval      c6rsu3 = (w_kalk * c6rab3)/100
7.02 c                   if        d_fl_sig3 = 'C  '
7.02 c                   eval      w_kalk = w_kalk + c6rsu3
7.02 c                   else
7.02 c                   eval      w_kalk = w_kalk - c6rsu3
7.02 c                   endif
7.02 c                   endif
7.02 c                   if        d_fl_sig3 = 'C  '
7.02 c                   eval      c6sig3 = '+'
7.02 c                   else
7.02 c                   eval      c6sig3 = '-'
7.02 c                   endif
7.02 c                   eval      c6lsum = d_fl_bnet
7.02  * Så kalkulerer vi netto basert på tall fra EDI meldinga
7.02 c                   eval      c6ksum = c6ktot
7.02 c                   if        d_fl_prkv = 'AAB'
7.02 c                   if        c6sig1 = '+'
7.02 c                   eval      c6ksum = c6ksum + c6rsu1
7.02 c                   else
7.02 c                   eval      c6ksum = c6ksum - c6rsu1
7.02 c                   endif
7.02 c                   if        c6sig2 = '+'
7.02 c                   eval      c6ksum = c6ksum + c6rsu2
7.02 c                   else
7.02 c                   eval      c6ksum = c6ksum - c6rsu2
7.02 c                   endif
7.02 c                   if        c6sig3 = '+'
7.02 c                   eval      c6ksum = c6ksum + c6rsu3
7.02 c                   else
7.02 c                   eval      c6ksum = c6ksum - c6rsu3
7.02 c                   endif
7.02 c                   endif
7.02  * Hent pris som kalkuleres for differanse
7.02 c                   exsr      finn_pris
7.02 c                   eval      c6nxpr = w_pris
7.02 c                   eval(h)   c6nxto = w_pris * c6anta
7.02 c                   eval      c6dif1 = c6nxto - c6lsum
7.02 c                   eval      c6dif2 = c6nxto - c6ksum
7.02  * Konverter status
7.02 c                   eval      c6stat = *blanks
7.02 c                   select
7.02 c                   when      listat = ' '
7.02 c                   eval      c6stat = 'Ok!         '
7.02 c                   when      listat = '1'
7.02 c                   eval      c6stat = 'Endret lev. '
7.02 c                   when      listat = '2'
7.02 c                   eval      c6stat = 'Avvist lev  '
7.02 c                   when      listat = '3'
7.02 c                   eval      c6stat = 'Ikke på fakt'
7.02 c                   when      listat = '4'
7.02 c                   eval      c6stat = 'Ikke på best'
7.02 c                   when      listat = '5'
7.02 c                   eval      c6stat = 'Avvik antall'
7.02 c                   when      listat = '6'
7.02 c                   eval      c6stat = 'Avvik enhet '
7.02 c                   when      listat = '7'
7.02 c                   eval      c6stat = 'Avvik pris  '
7.02 c                   when      listat = 'B'
7.02 c                   eval      c6stat = 'Beløpsavvik '
7.02 c                   endsl
7.02  *
     c                   exfmt     c6win
      *
7.02 c                   if        *inkc = *on
7.02 c                   leavesr
7.02 c                   endif
7.02  *
7.02 c     end_calk      endsr
7.02  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Regner ut linjebeløp mot total basert på beløpskontrol
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     gen_diff      begsr
      *
     c                   if        d_fh_dtyp = '381' and
     c                             d_ft_ftot < 0
     c**                 goto      upd_ktrl
     c                   endif
      *
     c                   eval      w_ftot = d_ft_ftot
     c                   eval      b2totm = d_ft_ftot
     c                   eval      b2difm = 0
     c                   eval      b2kalm = 0
     c                   eval      w_totb = 0
     c                   exsr      finn_lint
     c                   exsr      finn_tifr
     c**                 eval      w_diff = w_ftot - w_ltot
     c                   eval      b2kalm = w_totb
     c                   eval      b2difm = w_totb - b2totm
      *
     c     end_diff      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finne tillegg/fradrag på hodenivå
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_tifr     begsr
      *
     c                   eval      ledil1_type = 'FAKT'
     c                   eval      ledil1_mnum = d_mnum
     c                   eval      ledil1_mlin = 600001
     c     ledil1_key    setll     ledil1
     c                   read      ledil1
      *
     c                   dow       not %eof and
     c                             limlin < 650000
      *
     c                   eval      d_fakh2 = limeld
      *
      * Akkumuler tillegg/fradrag
      *
      *
     c                   if        d_fh_apro > 0
     c                   eval      w_mvap = d_fh_apro
     c                   else
     c                   if        rlmkod = 0
     c                   eval      w_mvap = 25.00
     c                   endif
     c                   endif
      *
     c                   eval      w_prib = d_fh_tbel +
     c                                      ((d_fh_tbel * w_mvap)/100)
      *
     c                   eval      w_pris = d_fh_tbel
     c**                                    ((d_fh_tbel * w_mvap)/100)
      * Gjør om til absoluttverdi
     c                   if        w_pris < 0
     c                   eval      w_pris = w_pris * -1
     c                   endif
     c                   if        w_prib < 0
     c                   eval      w_prib = w_prib * -1
     c                   endif
      *
     c                   if        d_fh_tsig = 'C'
     c                   eval      w_ltot = w_ltot + w_pris
     c                   else
     c                   eval      w_ltot = w_ltot - w_pris
     c                   endif
      *
     c                   if        d_fh_tsig = 'C'
     c                   eval      w_totb = w_totb + w_prib
     c                   else
     c                   eval      w_totb = w_totb - w_prib
     c                   endif
      *
     c                   read      ledil1
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finne sum netto varelinjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_lint     begsr
      *
     c                   eval      w_ltot = 0
      *
     c                   eval      ledil1_type = 'FAKT'
     c                   eval      ledil1_mnum = d_mnum
     c                   eval      ledil1_mlin = 500001
     c     ledil1_key    setll     ledil1
     c                   read      ledil1
      *
     c                   dow       not %eof and
     c                             limlin < 550000
      *
     c                   eval      d_fakl = limeld
      *
      * Sjekk om fritekstlinje
      *
     c                   if        d_fl_vnob = 0 and
     c                             d_fl_fenh = *blank and
     c                             d_fl_fkva = 0
     c                   goto      les_neste
     c                   endif
      *
      * Akkumuler netto linjebeløp
      *
     c                   exsr      finn_pris
     c                   eval      w_ltot = w_ltot + (w_pris * d_fl_fkva)
     c                   eval      w_totb = w_totb + (w_prib * d_fl_fkva)
      *
     c     les_neste     tag
     c                   read      ledil1
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å beregne nettopris for beløpskontroll
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_pris     begsr
      *
      * Nullstiller
      *
     c                   eval      w_prig = d_fl_pris
     c                   eval      w_fral = 0
     c                   eval      w_pril = 0
      *
      * Tillegg 1
      *
     c                   eval      w_frad = 0
7.03 c                   if        d_fl_bel1  <> 0
7.03 c                   eval(h)   w_frad = d_fl_bel1 / d_fl_fkva
7.03 c                   else
     c                   if        d_fl_pro1 <> 0
     c                   eval(h)   w_frad = (w_prig * d_fl_pro1 ) / 100
     c                   endif
     c                   endif
      *
     c                   if        d_fl_sig1 = 'A  '
     c                   eval      w_prig = w_prig - w_frad
     c                   eval      w_fral = w_fral - w_frad
     c                   else
     c                   eval      w_prig = w_prig + w_frad
     c                   eval      w_fral = w_fral + w_frad
     c                   endif
      *
      * Tillegg 2
      *
     c                   eval      w_frad = 0
7.03 c                   if        d_fl_bel2  <> 0
7.03 c                   eval(h)   w_frad = d_fl_bel2 / d_fl_fkva
7.03 c                   else
     c                   if        d_fl_pro2 <> 0
     c                   eval(h)   w_frad = (w_prig * d_fl_pro2 ) / 100
     c                   endif
     c                   endif
      *
     c                   if        d_fl_sig2 = 'A  '
     c                   eval      w_prig = w_prig - w_frad
     c                   eval      w_fral = w_fral - w_frad
     c                   else
     c                   eval      w_prig = w_prig + w_frad
     c                   eval      w_fral = w_fral + w_frad
     c                   endif
      *
      * Tillegg 3
      *
     c                   eval      w_frad = 0
7.03 c                   if        d_fl_bel3  <> 0
7.03 c                   eval(h)   w_frad = d_fl_bel3 / d_fl_fkva
7.03 c                   else
     c                   if        d_fl_pro3 <> 0
     c                   eval(h)   w_frad = (w_prig * d_fl_pro3 ) / 100
     c                   endif
     c                   endif
      *
     c                   if        d_fl_sig3 = 'A  '
     c                   eval      w_prig = w_prig - w_frad
     c                   eval      w_fral = w_fral - w_frad
     c                   else
     c                   eval      w_prig = w_prig + w_frad
     c                   eval      w_fral = w_fral + w_frad
     c                   endif
      *
      * Tillegg 4
      *
     c                   eval      w_frad = 0
7.03 c                   if        d_fl_bel4  <> 0
7.03 c                   eval(h)   w_frad = d_fl_bel4 / d_fl_fkva
7.03 c                   else
     c                   if        d_fl_pro4 <> 0
     c                   eval(h)   w_frad = (w_prig * d_fl_pro4 ) / 100
     c                   endif
     c                   endif
      *
     c                   if        d_fl_sig4 = 'A  '
     c                   eval      w_prig = w_prig - w_frad
     c                   eval      w_fral = w_fral - w_frad
     c                   else
     c                   eval      w_prig = w_prig + w_frad
     c                   eval      w_fral = w_fral + w_frad
     c                   endif
      *
      * Tillegg 5
      *
     c                   eval      w_frad = 0
7.03 c                   if        d_fl_bel5  <> 0
7.03 c                   eval(h)   w_frad = d_fl_bel5 / d_fl_fkva
7.03 c                   else
     c                   if        d_fl_pro5 <> 0
     c                   eval(h)   w_frad = (w_prig * d_fl_pro5 ) / 100
     c                   endif
     c                   endif
      *
     c                   if        d_fl_sig5 = 'A  '
     c                   eval      w_prig = w_prig - w_frad
     c                   eval      w_fral = w_fral - w_frad
     c                   else
     c                   eval      w_prig = w_prig + w_frad
     c                   eval      w_fral = w_fral + w_frad
     c                   endif
      *
      * Tillegg 6
      *
     c                   eval      w_frad = 0
7.03 c                   if        d_fl_bel6  <> 0
7.03 c                   eval(h)   w_frad = d_fl_bel6 / d_fl_fkva
7.03 c                   else
     c                   if        d_fl_pro6 <> 0
     c                   eval(h)   w_frad = (w_prig * d_fl_pro6 ) / 100
     c                   endif
     c                   endif
      *
     c                   if        d_fl_sig6 = 'A  '
     c                   eval      w_prig = w_prig - w_frad
     c                   eval      w_fral = w_fral - w_frad
     c                   else
     c                   eval      w_prig = w_prig + w_frad
     c                   eval      w_fral = w_fral + w_frad
     c                   endif
      *
      * Nobb pris?
      *
     c                   if        d_fl_mvap > 0
     c                   eval      w_mvap = d_fl_mvap
     c                   else
     c                   if        rlmkod = 0
     c                   eval      w_mvap = 25.00
     c                   endif
     c                   endif
      *
     c                   if        d_fl_prkv = 'AAA'
     c                   eval(h)   w_pris = d_fl_pris
     c**                                    (d_fl_pris * w_mvap) / 100
     c                   else
     c                   eval(h)   w_pris = w_prig
     c**                                    (w_prig * w_mvap) / 100
     c                   endif
      *
     c                   if        d_fl_prkv = 'AAA'
     c                   eval(h)   w_prib = d_fl_pris +
     c                                      (d_fl_pris * w_mvap) / 100
     c                   else
     c                   eval(h)   w_prib = w_prig +
     c                                      (w_prig * w_mvap) / 100
     c                   endif
      *
     c     end_pris      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å vise tillegg- /fradragsinfo
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     vis_tillegg   begsr
      *
     c                   eval      b8tek0 = *blank
     c                   eval      b8tek1 = *blank
     c                   eval      b8tek2 = *blank
     c                   eval      b8tek3 = *blank
     c                   eval      b8tek4 = *blank
     c                   eval      b8tek5 = *blank
     c                   eval      b8tek6 = *blank
     c                   eval      b8tek7 = *blank
     c                   eval      b8bel0 = 0
     c                   eval      b8bel1 = 0
     c                   eval      b8bel2 = 0
     c                   eval      b8bel3 = 0
     c                   eval      b8bel4 = 0
     c                   eval      b8bel5 = 0
     c                   eval      b8bel6 = 0
     c                   eval      b8bel7 = 0
      *
     c                   eval      ledilr_mlin = 600000
     c     ledilr_key    setll     ledilr
     c                   eval      w_tell = 0
     c                   read      ledilr
      *
     c                   dow       limlin < 650000 and
     c                             not %eof
      *
     c                   eval      d_fakh2 = limeld
     c                   eval      w_tell = w_tell + 1
     c                   if        d_fh_tsig = 'A  '
     c                   eval      d_fh_tbel = d_fh_tbel * -1
     c                   endif
      *
     c                   select
      *
     c                   when      w_tell = 1
     c                   eval      b8tek0 = d_fh_ttek
     c                   eval      b8bel0 = d_fh_tbel
      *
     c                   when      w_tell = 2
     c                   eval      b8tek1 = d_fh_ttek
     c                   eval      b8bel1 = d_fh_tbel
      *
     c                   when      w_tell = 3
     c                   eval      b8tek2 = d_fh_ttek
     c                   eval      b8bel2 = d_fh_tbel
      *
     c                   when      w_tell = 4
     c                   eval      b8tek3 = d_fh_ttek
     c                   eval      b8bel3 = d_fh_tbel
      *
     c                   when      w_tell = 5
     c                   eval      b8tek4 = d_fh_ttek
     c                   eval      b8bel4 = d_fh_tbel
      *
     c                   when      w_tell = 6
     c                   eval      b8tek5 = d_fh_ttek
     c                   eval      b8bel5 = d_fh_tbel
      *
     c                   when      w_tell = 7
     c                   eval      b8tek6 = d_fh_ttek
     c                   eval      b8bel6 = d_fh_tbel
      *
     c                   when      w_tell = 8
     c                   eval      b8tek7 = d_fh_ttek
     c                   eval      b8bel7 = d_fh_tbel
     c                   endsl
      *
     c                   read      ledilr
     c                   enddo
      *
     c                   exfmt     b8win
      *
     c     end_tillegg   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å vise beløpsinfo
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     vis_total     begsr
      *
     c                   eval      b9tek0 = 'Sum varelinjer (eks.til/fra)  '
     c                   eval      b9tek1 = 'Sum varelinjer (ink.til/fra)  '
     c                   eval      b9tek2 = 'Sum alle tillegg faktura      '
     c                   eval      b9tek3 = 'Sum alle fradrag faktura      '
     c                   eval      b9tek4 = 'Netto total (eks.mva.)        '
     c                   eval      b9tek5 = 'Mva grunnlag                  '
     c                   eval      b9tek6 = 'Total mva.                    '
     c                   eval      b9tek7 = 'Fakturatotal                  '
      *
     c                   eval      ledilr_mlin = 900001
     c     ledilr_key    chain     ledilr
     c                   if        not %found
     c                   eval      b9bel0 = 0
     c                   eval      b9bel1 = 0
     c                   eval      b9bel2 = 0
     c                   eval      b9bel3 = 0
     c                   eval      b9bel4 = 0
     c                   eval      b9bel5 = 0
     c                   eval      b9bel6 = 0
     c                   eval      b9bel7 = 0
     c                   else
     c                   eval      d_fakt = limeld
     c                   eval      b9bel0 = d_ft_veks
     c                   eval      b9bel1 = d_ft_vink
     c                   eval      b9bel2 = d_ft_stil
     c                   eval      b9bel3 = d_ft_sfra * -1
     c                   eval      b9bel4 = d_ft_teks
     c                   eval      b9bel5 = d_ft_mvgr
     c                   eval      b9bel6 = d_ft_tmva
     c                   eval      b9bel7 = d_ft_ftot
     c                   endif
      *
     c                   exfmt     b9win
      *
     c     end_total     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_parm
      *
      *  Key for les av MELDINGSLINJER
      *
     c     ledil1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ledil1_type
     c                   kfld                    ledil1_mnum
     c                   kfld                    ledil1_mlin
      *
      *  Key for les av MELDINGSLINJER
      *
     c     ledilr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ledilr_type
     c                   kfld                    ledilr_mnum
     c                   kfld                    ledilr_mlin
      *
      * Key for oppslag på RLEV
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Henter verdier fra parmametre
      *
     c                   eval      d_prec = p_parm
     c                   time                    w_date
     c                   time                    w_time
     c                   eval      w_year = %subdt(w_date:*years)
      *
     c                   eval      ledilr_type = d_type
     c                   eval      ledilr_mnum = d_mnum
     c                   eval      ledil1_type = d_type
     c                   eval      ledil1_mnum = d_mnum
      * Finner info fra EDI mneldinga
     c                   eval      ledilr_mlin = 100001
     c     ledilr_key    chain     ledilr
     c                   if        %found
     c                   eval      d_fakh1 = limeld
     c                   eval      w_ldor = lildor
     c                   eval      w_kund = likund
     c                   endif
      *
     c                   eval      ledilr_mlin = 900001
     c     ledilr_key    chain     ledilr
     c                   if        %found
     c                   eval      d_fakt = limeld
     c                   endif
      * Hent info fra leverandør
     c                   eval      rlevl1_levr = w_ldor
     c     rlevl1_key    chain     rlevl1
      *
     c                   endsr
