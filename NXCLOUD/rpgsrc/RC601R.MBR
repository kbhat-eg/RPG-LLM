      /TITLE  Utskrift av saker
     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RC601R                                        *
      * Beskrivelse . . : Utskrift av saker                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
7.00  * 7.00/sst  080519  Hente navn på inkassofirma fra RCA0PF         *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KA = F1  = Valg av firma                                  *
      *       KC = F3  = Exit                                           *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     frcnfl1    if   e           k disk    rename(rcnfpfr:rcnfl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
7.00 frca0l1    if   e           k disk    rename(rca0pfr:rca0l1r)
     frc601p    o    e             printer oflind(*in30)
      *
      * Definering av variable i nøkler
      *
     d rcnfl1_kund     s                   like(rkkund)
     d rkunl1_kund     s                   like(rkkund)
      *
      * Definering av Local data area
      *
     d                uds
      *
     d l_wsid                901    910
     d l_user                911    920
     d l_file                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d                 ds
     d w_dato                         6  0
     d  w_dag                         2  0 overlay(w_dato)
     d  w_mnd                         2  0 overlay(w_dato:3)
     d  w_aar                         2  0 overlay(w_dato:5)
      *
     d                 ds
     d w_dat8                         8  0
     d  w_aar8                        4  0 overlay(w_dat8)
     d  w_mnd8                        2  0 overlay(w_dat8:5)
     d  w_dag8                        2  0 overlay(w_dat8:7)
      *
     d                 ds
     d w_nref                        20
     d  w_file                        3    overlay(w_nref:5)
      *
     d w_type          s              1
     d w_edat          s               d   datfmt(*iso)
     d w_sdat          s               d   datfmt(*iso)
      *
     d w_fast          s              6  0
      *
     d w_tota          s              7  0
     d w_anta          s              5  0
     d w_firm          s                   like(rkfirm)
     d w_kund          s                   like(rkkund)
     d w_slett         s              1
      *
      * Les saksregister
      *
     c     rcnfl1_key    setll     rcnfl1                                                    .
     c     rcnfl1_key    reade     rcnfl1                                 90
      *
     c                   dow       *in90 = *off
      *
     c                   eval      w_nref = rcnref
      *
     c                   if        w_file <> l_file
     c                   goto      neste
     c                   endif
      *
     c                   eval      w_tota = w_tota + 1
      *
     c                   if        w_type = 'J' and
     c                             rcnsta = ' '
     c                   goto      neste
     c                   endif
      *
     c                   if        w_type = 'N' and
     c                             rcnsta = 'A'
     c                   goto      neste
     c                   endif
      *
     c                   eval      rkunl1_kund = rcnask
     c     rkunl1_key    chain     rkunl1                             60
      *
     c                   exsr      kontroll
      *
     c                   if        w_slett = ' '
      *
     c                   exsr      utskrift
      *
     c                   endif
      *
     c     neste         tag
      *
     c     rcnfl1_key    reade     rcnfl1                                 90
     c                   enddo
      *
     c     slutt         tag
      *
     c                   eval      *inlr = *on
     c                   eval      t1tota = w_tota
     c                   eval      t1anta = w_anta
      *
     c                   write     t1tot
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kontroll
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kontroll      begsr
      *
      * Tell opp antall saker
      *
     c                   eval      w_slett = ' '
      *
      * Sjekk dato for avslutning
      *
     c                   eval      w_dato = *zeros
      *
     c                   if        w_type = 'J' and
     c                             rcndat <> *loval
     c                   if        rcndat  > w_edat
     c                   eval      w_slett = 'X'
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     utskrift      begsr
      *
      * Tell antall kunder på lista
      *
     c                   eval      w_anta = w_anta + 1
      *
      * Sjekker om overflow
      *
     c                   if        *in30 = *on                                               .
     c                   write     a1hdr                                                     .
     c                   eval      *in30 = *off                                              .
     c                   endif                                                           .....
      *
     c                   write     d1det
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    w_type
     c                   parm                    w_edat
      *
      * Key for saksregister
      *
     c     rcnfl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for kunderegister
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for statuskoder
      *
     c     rca0l1_key    klist
     c                   kfld                    w_firm
      *
     c                   eval      w_firm = l_firm
      *
7.00 c     rca0l1_key    chain     rca0l1
7.00 c                   if        not %found(rca0l1)
7.00 c                             or  rc0inf = *blank
7.00 c                   eval      rc0inf = 'Ukjent'
7.00 c                   endif
7.00  *
      *
     c                   if        w_type = 'J'
     c                   eval      *in11 = *on
     c                   else
     c                   eval      *in11 = *off
     c                   endif
      *
      * Skriv heading på første side
      *
     c                   write     a1hdr
      *
     c                   endsr
      *
