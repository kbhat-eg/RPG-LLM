     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASSTAT
      * Program . . . . : SF561R
      * Beskrivelse . . : Salgsstatistikk, registrering fra ordre
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       141005 HKO  Nytt program
6.20  * 6.20  061212 bsa  Endret i skjermbildet pga jacada
6.21  * 6.20  011113 bof  Tester på antall før man dividerer
6.22  * 6.20  020714 bsa  Justeringer i skjermbildet pga JIT GUI i Jacada
6.NU  * R6.30 260514 bsa  Programmet gjennomgått mtp nummerutvidelser.
7.xx  * 7.00  110117 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 7.00  050419 bkv  Vasker vekk probl Newlook tegn
8.01  *PRG2   220622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsstal3    if   e           k disk    rename(sstapfr:sstal3r)
     fsstal4    if   e           k disk    rename(sstapfr:sstal4r)
     fsstal5    if   e           k disk    rename(sstapfr:sstal5r)
     fsstal6    if   e           k disk    rename(sstapfr:sstal6r)
     fsstal7    if   e           k disk    rename(sstapfr:sstal7r)
     fsstal8    if   e           k disk    rename(sstapfr:sstal8r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     fsohel1    if   e           k disk    rename(sohepfr:sohel1r)
     fbohel1    if   e           k disk    rename(bohepfr:bohel1r)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
      *
     fsf161d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_kode          s              1
     d p_firm          s                   like(sffirm)
     d p_paar          s                   like(sfpaar)
     d p_vkun          s                   like(sfvkun)
     d p_kpro          s                   like(sfkpro)
     d p_bida          s                   like(sfbida)
6.NU d p_numm          s                   like(fonumm)
     d p_suff          s                   like(fosuff)
     d p_line          s                   like(fdline)
     d p_ltyp          s                   like(fdltyp)
     d p_lety          s                   like(fdlety)
     d p_kost          s              1
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d sstal3_vare     s                   like(sfvare)
     d sstal3_kpro     s                   like(sfkpro)
     d sstal3_bida     s                   like(sfbida)
     d sstal4_tek1     s                   like(sftek1)
     d sstal5_kpro     s                   like(sfkpro)
     d sstal6_bida     s                   like(sfbida)
     d sstal7_binr     s                   like(sfbinr)
     d sstal8_ornr     s                   like(sfornr)
     d rkunl1_kund     s                   like(rkkund)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
     d sohel1_aarr     s                   like(soaarr)
     d sohel1_binr     s                   like(sobinr)
     d sohel1_numm     s                   like(sonumm)
     d bohel1_aarr     s                   like(boaarr)
     d bohel1_wsid     s                   like(bowsid)
5.30 d bohel1_bong     s                   like(bobong)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d w_tek1          s             35
     d w_dato          s               d   datfmt(*iso)
      *
     d w_firm          s                   like(sffirm)
     d w_paar          s                   like(sfpaar)
     d w_vkun          s                   like(sfvkun)
     d w_bida          s                   like(sfbida)
     d w_vare          s                   like(sfvare)
     d w_pnav          s                   like(flnavn)
     d w_adr1          s                   like(fladr1)
     d w_adr2          s                   like(fladr2)
     d w_sted          s                   like(fladr1)
     d w_kprs          s                   like(flkprs)
     d w_tlfa          s                   like(fltlfa)
     d w_tlfp          s                   like(fltlfp)
     d w_tlfm          s                   like(fltlfm)
     d w_mail          s                   like(flmail)
     d w_orka          s                   like(florka)
     d w_okun          s                   like(flokun)
     d w_okpr          s                   like(flokpr)
     d w_opda          s                   like(flopda)
     d w_proj          s                   like(flproj)
6.NU d w_numm          s                   like(fonumm)
     d w_suff          s                   like(fosuff)
     d w_line          s                   like(fdline)
     d w_anta          s                   like(fdanta)
     d w_tots          s                   like(fdsums)
     d w_totr          s                   like(fdsumr)
7.01 d w_len           s              3  0
      *
      *********
     d xxlety          s                   like(fdlety)
     d xxltyp          s                   like(fdltyp)
      *
      * Definering av konstanter
      *
7.xx d c_sfil          s              2  0 inz(12)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile                                  DSP SFL
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2tagb
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile                                  CRT SFL
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on                                  CRT SFL
     c                   goto      b2taga
     c                   endsl
      *
      * F2=Registrering av tekstlinjer
      *
     c                   if        *inkb = *on
     c                   exsr      tekstlinje
     c                   goto      b2tagb
     c                   endif
      *
      * Posisjonering
      *
     c                   if        b2vare <> 0 or
     c                             b2tek1 <> *blank or
     c                             b2kpro <> 0 or
     c                             b2bida <> 0 or
     c                             b2binr <> 0 or
     c                             b2ornr <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'T'
     c                   eval      sstal4_tek1 = b1tek1
     c     sstal4_key    setll     sstal4
     c                   when      w_seqe = 'A'
     c                   eval      sstal5_kpro = b1kpro
     c     sstal5_key    setll     sstal5
     c                   when      w_seqe = 'D'
     c                   eval      sstal6_bida = %date(b1bida:*dmy)
     c     sstal6_key    setll     sstal6
     c                   when      w_seqe = 'F'
     c                   eval      sstal7_binr = b1binr
     c     sstal7_key    setll     sstal7
     c                   when      w_seqe = 'O'
     c                   eval      sstal8_ornr = b1ornr
     c     sstal8_key    setll     sstal8
     c                   other
     c                   eval      sstal3_vare = b1vare
     c                   eval      sstal3_kpro = b1kpro
     c                   eval      sstal3_bida = %date(b1bida:*dmy)
     c     sstal3_key    setll     sstal3
     c                   endsl
      *
     c                   exsr      clr_subfile                                  TØM SFL
     c                   exsr      crt_subfile                                  CRT SFL
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Vare
      *
     c                   if        b2vare <> 0
     c                   eval      w_seqe = *blank
     c                   movel     b2vare        sstal3_vare
     c                   eval      sstal3_kpro = b2kpro
     c                   eval      sstal3_bida = *loval
     c                   if        b2bida <> 0
     c     *dmy          test(d)                 b2bida                 31
     c                   if        *in31 = *off
     c     *dmy          move      b2bida        sstal3_bida
     c                   endif
     c                   endif
     c     sstal3_key    setll     sstal3
     c                   goto      end_pos
     c                   endif
      *
      * Varetekst
      *
     c                   if        b2tek1 <> *blank
     c                   eval      w_seqe = 'T'
     c                   eval      sstal4_tek1 = b2tek1
     c     sstal4_key    setll     sstal4
     c                   goto      end_pos
     c                   endif
      *
      * Kundeprosjekt
      *
     c                   if        b2kpro <> 0
     c                   eval      w_seqe = 'A'
     c                   eval      sstal5_kpro = b2kpro
     c     sstal5_key    setll     sstal5
     c                   goto      end_pos
     c                   endif
      *
      * Fakturadato/bilagsdato
      *
     c                   if        b2bida <> 0
     c                   eval      w_seqe = 'D'
     c                   eval      sstal6_bida = *loval
     c     *dmy          test(d)                 b2bida                 31
     c                   if        *in31 = *off
     c     *dmy          move      b2bida        sstal6_bida
     c                   endif
     c     sstal6_key    setll     sstal6
     c                   goto      end_pos
     c                   endif
      *
      * Fakturanummer
      *
     c                   if        b2binr <> 0
     c                   eval      w_seqe = 'F'
     c                   eval      sstal7_binr = b2binr
     c     sstal7_key    setll     sstal7
     c                   goto      end_pos
      *
     c                   endif
      *
      * Ordrenummer
      *
     c                   if        b2ornr <> 0
     c                   eval      w_seqe = 'O'
     c                   eval      sstal8_ornr = b2ornr
     c     sstal8_key    setll     sstal8
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile                                  TØM SFL
     c                   exsr      crt_subfile                                  CRT SFL
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2vare = 0
     c                   eval      b2tek1 = *blank
     c                   eval      b2kpro = 0
     c                   eval      b2bida = 0
     c                   eval      b2binr = 0
     c                   eval      b2ornr = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Vise salgsdetaljer
      *
     c                   if        b1valg = 5
     c     *dmy          move      b1bida        w_bida
     c                   call      'SF510R'
     c                   parm                    w_firm
     c                   parm                    w_paar
     c                   parm                    b1binr
     c                   parm                    b1line
     c                   parm                    w_bida
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Skriv fakturakopi
      *
     c                   if        b1valg = 6
     c                   exsr      skriv
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vise faktura/bong
      *
     c                   if        b1valg = 7
     c                   exsr      vise
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Antall er utfylt, linje skal registreres
      *
     c                   if        b1anta <> 0
      *
     c                   move      '->'          b1tek1
     c                   movel     b1vare        w_vare
      *
     c                   eval      w_line = w_line + 10
     c                   eval      w_anta = b1anta
      *
     c                   call      'FD130R'
     c                   parm                    p_kode
     c                   parm                    w_firm
     c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    w_line
     c                   parm                    p_ltyp
     c                   parm                    p_lety
     c                   parm                    w_vare
     c                   parm                    w_anta
     c                   parm                    b1enhe
     c                   parm                    b1sapr
     c                   parm                    b1kopr
     c                   parm                    b1ldor
     c                   parm                    b1lvar
     c                   parm                    p_kost
      *
     c                   exsr      hent_total
      *
     c                   eval      b1anta = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil                     SFL END
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'T'
     c                   read      sstal4
     c                   when      w_seqe = 'A'
     c                   read      sstal5
     c                   when      w_seqe = 'D'
     c                   read      sstal6
     c                   when      w_seqe = 'F'
     c                   read      sstal7
     c                   when      w_seqe = 'O'
     c                   read      sstal8
     c                   other
     c                   read      sstal3
     c                   endsl
      *
     c                   if        %eof or                                      EOF
     c                             sffirm <> w_firm or
     c                             sfpaar <> w_paar or
     c                             sfvkun <> w_vkun
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   if        p_kpro <> 0 and
     c                             sfkpro <> p_kpro
     c                   iter
     c                   endif
      *
     c                   if        p_bida <> *loval and
     c                             sfbida <> p_bida
     c                   iter
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'T'
     c                   eval      sstal4_tek1 = sftek1
     c                   when      w_seqe = 'A'
     c                   eval      sstal5_kpro = sfkpro
     c                   when      w_seqe = 'D'
     c                   eval      sstal6_bida = sfbida
     c                   when      w_seqe = 'F'
     c                   eval      sstal7_binr = sfbinr
     c                   when      w_seqe = 'O'
     c                   eval      sstal8_ornr = sfornr
     c                   other
     c                   eval      sstal3_vare = sfvare
     c                   eval      sstal3_kpro = sfkpro
     c                   eval      sstal3_bida = sfbida
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1bong = sfbong
6.21 c                   if        sfanta <> 0
     c                   eval(h)   b1sapr = (sfsumb - sfrab1 - sfrab2 -
     c                                      sfrabo - sfbrk1 - sfbrk2) /
     c                                      sfanta
     c                   eval(h)   b1kopr = sfsumk / sfanta
6.21 c                   else
6.21 c                   eval      b1sapr = 0
6.21 c                   eval      b1kopr = 0
6.21 c                   endif
     c                   eval      b1ldor = sfldor
     c                   eval      b1lvar = sflvar
     c                   eval      b1line = sfline
     c                   eval      b1wsid = sfwsid
     c                   eval      b1vare = sfvare
     c                   eval      b1tek1 = sftek1
     c                   eval      b1kpro = sfkpro
     c                   eval      b1binr = sfbinr
     c                   eval      b1ornr = sfornr
     c                   eval      b1otyp = sfotyp
     c                   eval      b1enhe = sfenhe
     c                   eval      b1ants = sfanta
      *
     c                   eval      b1bida = 0
      *
     c                   if        sfbida <> *loval
     c     *dmy          move      sfbida        b1bida
     c                   endif
      *
7.01 c                   exsr      newlo_vask
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'T'
     c     sstal4_key    setgt     sstal4
     c                   readp     sstal4
     c                   when      w_seqe = 'A'
     c     sstal5_key    setgt     sstal5
     c                   readp     sstal5
     c                   when      w_seqe = 'D'
     c     sstal6_key    setgt     sstal6
     c                   readp     sstal6
     c                   when      w_seqe = 'F'
     c     sstal7_key    setgt     sstal7
     c                   readp     sstal7
     c                   when      w_seqe = 'O'
     c     sstal8_key    setgt     sstal8
     c                   readp     sstal8
     c                   other
     c     sstal3_key    setgt     sstal3
     c                   readp     sstal3
     c                   endsl
      *
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'T'
     c                   readp     sstal4
     c                   when      w_seqe = 'A'
     c                   readp     sstal5
     c                   when      w_seqe = 'D'
     c                   readp     sstal6
     c                   when      w_seqe = 'F'
     c                   readp     sstal7
     c                   when      w_seqe = 'O'
     c                   readp     sstal8
     c                   other
     c                   readp     sstal3
     c                   endsl
      *
     c                   if        %eof or                                      EOF
     c                             sffirm <> w_firm or
     c                             sfpaar <> w_paar or
     c                             sfvkun <> w_vkun
     c                   leave
     c                   endif
      *
     c                   if        p_kpro <> 0 and
     c                             sfkpro <> p_kpro
     c                   iter
     c                   endif
      *
     c                   if        p_bida <> *loval and
     c                             sfbida <> p_bida
     c                   iter
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'T'
     c                   eval      sstal4_tek1 = sftek1
     c                   when      w_seqe = 'A'
     c                   eval      sstal5_kpro = sfkpro
     c                   when      w_seqe = 'D'
     c                   eval      sstal6_bida = sfbida
     c                   when      w_seqe = 'F'
     c                   eval      sstal7_binr = sfbinr
     c                   when      w_seqe = 'O'
     c                   eval      sstal8_ornr = sfornr
     c                   other
     c                   eval      sstal3_vare = sfvare
     c                   eval      sstal3_kpro = sfkpro
     c                   eval      sstal3_bida = sfbida
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'T'
     c     sstal4_key    setll     sstal4
     c                   when      w_seqe = 'A'
     c     sstal5_key    setll     sstal5
     c                   when      w_seqe = 'D'
     c     sstal6_key    setll     sstal6
     c                   when      w_seqe = 'F'
     c     sstal7_key    setll     sstal7
     c                   when      w_seqe = 'O'
     c     sstal8_key    setll     sstal8
     c                   other
     c     sstal3_key    setll     sstal3
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av spørringer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Vare
      *
     c                   if        w_afld = 'B2VARE'
     c                   movel     b2vare        w_vare
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_tek1
     c                   movel     w_vare        b2vare
     c                   leavesr
     c                   endif
      *
      * Kundeprosjekt
      *
     c                   if        w_afld = 'B2KPRO'
      *
     c                   if        p_bida <> *loval
     c                   eval      w_dato = p_bida
     c                   else
     c                   time                    w_dato
     c                   endif
      *
     c                   CALL      'FL501R'
     c                   parm                    w_firm
     c                   parm                    b2vkun
     c                   parm                    w_dato
     c                   parm                    b2kpro
     c                   parm                    w_pnav
     c                   parm                    w_adr1
     c                   parm                    w_adr2
     c                   parm                    w_sted
     c                   parm                    w_kprs
     c                   parm                    w_tlfa
     c                   parm                    w_tlfp
     c                   parm                    w_tlfm
     c                   parm                    w_mail
     c                   parm                    w_orka
     c                   parm                    w_okun
     c                   parm                    w_okpr
     c                   parm                    w_opda
     c                   parm                    w_proj
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for registrering av tekstlinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     tekstlinje    begsr
      *
     c                   eval      w_line = w_line + 10
      *
     c                   call      'FD110R'
     c                   parm                    p_kode
     c                   parm                    w_firm
6.NU c                   parm                    w_numm
     c                   parm                    w_suff
     c                   parm                    w_line
     c                   parm                    faatty
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av ordretotal
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_total    begsr
      *
     c                   eval      w_tots = 0
     c                   eval      w_totr = 0
      *
     c     fodtl1_key    setll     fodtl1
     c     fodtl1_key    reade     fodtl1
     c                   dow       not %eof
     c                   eval      w_tots = w_tots + fdsums
     c                   eval      w_totr = w_totr + fdsumr
     c     fodtl1_key    reade     fodtl1
     c                   enddo
      *
     c                   eval      b2tots = w_tots - w_totr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å vise faktura eller butikk-bong
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     vise          begsr
      *
      * Dersom rad er faktura, kalles program for å vise faktura
      *
     c                   eval      sohel1_aarr = w_paar
     c                   eval      sohel1_binr = b1binr
     c                   eval      sohel1_numm = b1ornr
     c     sohel1_key    chain     sohel1
     c                   if        %found
      *
     c                   call      'SO510R'
     c                   parm                    w_firm
     c                   parm                    w_paar
     c                   parm                    p_vkun
     c                   parm                    b1binr
      *
     c                   leavesr
     c                   endif
      *
      * Dersom rad er bong/butikkfaktura, kalles program for å vise
      * bong/butikkfaktura
      *
     c                   eval      bohel1_aarr = w_paar
     c                   eval      bohel1_wsid = b1wsid
5.30 c                   eval      bohel1_bong = b1bong
     c     bohel1_key    chain     bohel1
     c                   if        %found
      *
     c                   call      'BO310R'
     c                   parm                    w_firm
     c                   parm                    w_paar
     c                   parm                    b1wsid
     c                   parm                    b1bong
      *
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å skrive fakturakopi
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv         begsr
      *
      * Dersom rad er faktura, kalles program for å skrive fakturakopi
5.31  * For faktura kjørt i butikk skal bongnummer benyttes i stedet for
5.31  * ordrenummer dersom salget er gjort fra gammelt butikksystem
      *
     c                   eval      sohel1_aarr = w_paar
     c                   eval      sohel1_binr = b1binr
     c                   eval      sohel1_numm = b1ornr
5.31  *
     c                   if        b1bong <> *blank and
     c                             %scan('-':b1bong) = 0
5.31 c                   movel     b1bong        sohel1_numm
     c                   endif
5.31  *
     c     sohel1_key    chain     sohel1
     c                   if        %found
      *
     c                   call      'FO681R'
     c                   parm                    w_firm
     c                   parm                    w_paar
     c                   parm                    b1binr
     c                   parm                    b1otyp
      *
     c                   leavesr
     c                   endif
      *
     c                   endsr
5.31  *
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  * Newlook vask av data
7.01  *  Fjerner tegn som gjør at Newlook feiltolker
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *
7.01
7.01   begsr newlo_vask;
7.01     w_len = %len(%trim(b1tek1));
7.01     if (w_len > 0);
7.01       b1tek1 = %ScanRpl('.':'':b1tek1:w_len); // newlook tåler ikke . på slutten
7.01     endif;
7.01     w_len = %len(%trim(b1tek1));
7.01     if (w_len > 0);
7.01       b1tek1 = %ScanRpl(':':'':b1tek1:w_len); // newlook tåler ikke : på slutten
7.01     endif;
7.01   endsr;
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_kode
     c                   parm                    p_firm
     c                   parm                    p_paar
     c                   parm                    p_vkun
     c                   parm                    p_kpro
     c                   parm                    p_bida
6.NU c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_line
     c                   parm                    p_ltyp
     c                   parm                    p_lety
     c                   parm                    p_kost
      *
      * Key for posisjonering på kundenummer
      *
     c     sstal3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_paar
     c                   kfld                    w_vkun
     c                   kfld                    sstal3_vare
     c                   kfld                    sstal3_kpro
     c                   kfld                    sstal3_bida
      *
      * Key for posisjonering på varetekst
      *
     c     sstal4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_paar
     c                   kfld                    w_vkun
     c                   kfld                    sstal4_tek1
      *
      * Key for posisjonering på kundeprosjekt
      *
     c     sstal5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_paar
     c                   kfld                    w_vkun
     c                   kfld                    sstal5_kpro
      *
      * Key for posisjonering på fakturadato
      *
     c     sstal6_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_paar
     c                   kfld                    w_vkun
     c                   kfld                    sstal6_bida
      *
      * Key for posisjonering på fakturanummer
      *
     c     sstal7_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_paar
     c                   kfld                    w_vkun
     c                   kfld                    sstal7_binr
      *
      * Key for posisjonering på ordrenummer
      *
     c     sstal8_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_paar
     c                   kfld                    w_vkun
     c                   kfld                    sstal8_ornr
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      * Key for oppslag på faktura-hode-register
      *
     c     sohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_paar
     c                   kfld                    sohel1_binr
     c                   kfld                    sohel1_numm
      *
      * Key for oppslag på kode/status (OFAK)
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på bong-hode-register
      *
     c     bohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_paar
     c                   kfld                    bohel1_wsid
5.30 c                   kfld                    bohel1_bong
      *
      * Key for oppslag på ordre-hode
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
      *
      * Key for les på ordre-linje
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
      *
      *
      * Henter parametre
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_paar = p_paar
     c                   eval      w_vkun = p_vkun
      *
6.NU c                   eval      w_numm = p_numm
     c                   eval      w_suff = p_suff
     c                   eval      w_line = p_line
      *
      * Hent opplysninger fra ofak koderegister
      *
     c     fstsl1_key    chain     fstsl1r
      *
      * Hent opplysninger fra ordre-hode
      *
     c     fohel1_key    chain     fohel1r
      *
     c                   eval      b2numm = fonumm
      *
      * Henter total sum for ordre
      *
     c                   exsr      hent_total
      *
      * Initierer skjermbilde
      *
     c                   eval      *in22 = *on
      *
     c                   eval      b2paar = w_paar
     c                   eval      b2vkun = p_vkun
     c                   eval      b2lad1 = p_kpro
      *
     c                   eval      rkunl1_kund = p_vkun
     c     rkunl1_key    chain     rkunl1
     c                   if        %found
     c                   eval      b2knav = rknavn
     c                   endif
      *
     c                   if        p_kpro <> 0
     c                   eval      fkprl1_kund = p_vkun
     c                   eval      fkprl1_kpro = p_kpro
     c     fkprl1_key    chain     fkprl1
     c                   if        %found
     c                   eval      b2pnav = flnavn
     c                   endif
     c                   endif
      *
     c                   if        p_bida <> *loval
     c     *dmy          move      p_bida        b2bid1
     c                   endif
      *
      * Fyller subfile
      *
     c                   eval      sstal3_vare = *blank
     c                   eval      sstal3_kpro = 0
     c                   eval      sstal3_bida = *loval
     c     sstal3_key    setll     sstal3
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
