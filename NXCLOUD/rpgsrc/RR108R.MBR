     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RR108R                                              *
      * Beskrivelse . . : Resultatrapport, vedlikehold fra-til begrep         *
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       150902 KOB  Nytt program
T5.00 * R5.00 250303 HFL  Timestampfeil rettet
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frrf8l2    if   e           k disk    rename(rrf8pfr:rrf8l2r)
     frrf8lu    uf a e           k disk    rename(rrf8pfr:rrf8lur)
     frrf8lr    if   e           k disk    rename(rrf8pfr:rrf8lrr)
     frrf5l1    if   e           k disk    rename(rrf5pfr:rrf5l1r)
      *
     frr108d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d rrf5l1_5rap     s                   like(rr5rap)
     d rrf5l1_5lin     s                   like(rr5lin)
      *
     d rrf8l2_8rap     s                   like(rr8rap)
     d rrf8l2_8lin     s                   like(rr8lin)
     d rrf8l2_8lØp     s                   like(rr8lØp)
      *
     d rrf8lr_8rap     s                   like(rr8rap)
     d rrf8lr_8lin     s                   like(rr8lin)
     d rrf8lr_8lØp     s                   like(rr8lØp)
      *
     d rrf8lu_8rap     s                   like(rr8rap)
     d rrf8lu_8lin     s                   like(rr8lin)
     d rrf8lu_8lØp     s                   like(rr8lØp)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
     d w_8kto          s                   like(rr8fkt)
     d w_8sps          s                   like(rr8fsp)
     d w_8avd          s                   like(rr8fav)
     d w_8lin          s                   like(rr8lin)
     d w_8lØp          s                   like(rr8lØp)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d p_gkod          s              4  0
     d p_gtxt          s             20
     d p_0rap          s                   like(rr8rap)
     d p_0txt          s             40
     d p_5lin          s                   like(rr5lin)
     d p_5txt          s             40
     d p_5kod          s                   like(rr5kod)
     d p_kont          s                   like(rr8fkt)
     d p_spes          s                   like(rr8fsp)
     d p_avde          s                   like(rr8fav)
     d p_text          s             30
     d p_valg          s                   like(b1valg)
     d p_stat          s              1
     d brukt           s              1
      *
     d w_firm          s                   like(rr8fir)
      *
     d s_8rap          s                   like(rr8rap)
     d s_8lin          s                   like(rr8lin)
     d s_8lØp          s                   like(rr8lØp)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(12)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste recordnummer på siden
      * w_curn - Current recordnummer på siden
      * w_virn - Siden som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b28lØp <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_sfrn <> 0
     c     w_sfrn        chain     b1sfl
     c                   endif
      *
     c                   eval      rrf8l2_8lØp = b18lØp
     c     rrf8l2_key    setll     rrf8l2
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Løpenummer
      *
     c                   if        b28lØp <> 0
     c                   eval      rrf8l2_8lØp = b28lØp
     c     rrf8l2_key    setll     rrf8l2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b28lØp = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c18lØp = b18lØp
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k18lØp = b18lØp
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d18lØp = b18lØp
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c18lØp = b18lØp
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c18lØp = b28lØp
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Spørring
      *
kob  c                   if        *inkd
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c18lØp = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Validering
      *
kob  c***                if        c15lin = 0
     c***                eval      *in31  = *on
     c***                goto      c1taga
     c***                endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      rrf8lr_8lØp = c18lØp
     c     rrf8lr_key    chain     rrf8lr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c18lØp = 0
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b28lØp = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      rrf8lr_8lØp = c18lØp
     c     rrf8lr_key    chain     rrf8lr
      *
     c                   if        %found
     c                   eval      c28fkt = rr8fkt                                           .
     c                   eval      c28fsp = rr8fsp                                           .
     c                   eval      c28fav = rr8fav                                           .
     c                   eval      c28tkt = rr8tkt                                           .
     c                   eval      c28tsp = rr8tsp                                           .
     c                   eval      c28tav = rr8tav                                           .
     c***                eval      c28klf = rr8klf                                           .
     c                   eval      c28lif = rr8lif                                           .
     c                   eval      c28mot = rr8mot                                           .
     c                   eval      c28pro = rr8pro                                           .
     c                   eval      c2eusr = rr8usr
T5.00c                   if        rr8dat = *loval
T5.00c                   move      *zeros        c2edat
T5.00c                   else
     c     *dmy          move      rr8dat        c2edat
T5.00c                   endif
     c                   else
     c                   eval      c28fkt = *zero                                            .
     c                   eval      c28fsp = *zero                                            .
     c                   eval      c28fav = *zero                                            .
     c                   eval      c28tkt = *zero                                            .
     c                   eval      c28tsp = *zero                                            .
     c                   eval      c28tav = *zero                                            .
     c***                move      wp5klf        c28klf                                      .
     c                   eval      c28lif = *zeros                                           .
     c                   eval      c28mot = *blank                                           .
     c                   eval      c28pro = *zero                                            .
     c                   eval      c2eusr = *blank
     c                   eval      c2edat = 0
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c18lØp = k18lØp
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *in34 = *on
      *
      * Call program for
      * fra-konto
      *
     c                   if        *inkd = *on                                           .....
      *
     c                   select
     c                   when      w_afld = 'C28FKT'                                         .
      *                                                                 .
     c                   call      'RH500R'                                      konto       .
T5.00c                   parm                    w_firm
     c                   parm                    p_kont
     c                   parm                    p_spes
     c                   parm                    p_avde
     c                   parm                    p_text
      *                                                                 .
     c                   if        p_kont <> *zero                                           .
     c                   eval      c28fkt = p_kont                                           .
     c                   eval      c28fsp = p_spes                                           .
     c                   eval      c28fav = p_avde                                           .
     c                   endif                                                               .
      *                                                                 .
     c                   move      *on           *in31                                       .
     c                   goto      c2taga                                                    .
      *
      * Call program for
      * fra-avdeling
      *
     c                   when      w_afld = 'C28FAV'                                         .
      *
     c                   call      'RA507R'                                                  .
     c                   parm                    w_firm
     c                   parm                    p_gkod
     c                   parm                    p_gtxt
      *
     c                   if        p_avde <> *zero                                           .
     c                   eval      c28fav = p_avde                                           .
     c                   endif                                                               .
      *
     c                   move      *on           *in36                                       .
     c                   goto      c2taga                                                    .
      *
      * Call program for
      * til-konto
      *
     c                   when      w_afld = 'C28TKT'                                         .
      *                                                                 .
     c                   call      'RH500R'                                      konto       .
T5.00c                   parm                    w_firm
     c                   parm                    p_kont
     c                   parm                    p_spes
     c                   parm                    p_avde
     c                   parm                    p_text
      *                                                                 .
      *                                                                 .
     c                   eval      c28tkt = p_kont                                           .
     c                   eval      c28tsp = p_spes                                           .
     c                   eval      c28tav = p_avde                                           .
      *                                                                 .
     c                   move      *on           *in32                                       .
     c                   goto      c2taga                                                    .
      *
      * Call program for
      * til-avdeling
      *
     c                   when      w_afld = 'C28TAV'                                         .
      *                                                                 .
     c                   call      'RA507R'                                                  .
     c                   parm                    w_firm
     c                   parm                    p_gkod
     c                   parm                    p_gtxt
      *
     c                   if        p_avde <> *zero                                           .
     c                   eval      c28fav = p_avde                                           .
     c                   endif                                                               .
      *                                                                 .
     c                   move      *on           *in37                                       .
     c                   goto      c2taga                                                    .
      *                                                                 .
     c                   endsl                                                           .....
      *                                                                 .
     c                   endif                                                           .....
      *
      * Fra-konto må være utfylt
      *
     c                   move      *off          *in80
     c                   move      *off          *in81
     c                   move      *off          *in82
     c                   move      *off          *in83
     c                   move      *off          *in84
     c                   move      *off          *in85
      *
     c                   if        c28fkt = *zero                                        .....
     c                   move      *on           *in31                          Pos.cur.   . .
     c                   move      *on           *in84                          Error      . .
     c                   goto      c2taga                                                  . .
     c                   endif                                                           .....
      *
     c                   if        c28fav <> *zero                                       .....
     c                             or c28fkt <> *zero                                        .
     c                             or c28fsp <> *zero                                        .
      *                                                                 .
     c                   move      c28fkt        p_kont                                      .
     c                   move      c28fsp        p_spes                                      .
     c                   move      c28fav        p_avde                                      .
      *
     c                   call      'RS740R'                                      kontoplan-  .
     c                   parm                    p_stat
     c                   parm                    p_kont
     c                   parm                    p_spes
     c                   parm                    p_avde
     c                   parm                    p_text
      *                                                                 .
      * Avdeling finnes ikke                                            .
      *                                                                 .
     c                   if        p_stat = 'A'                                          ... .
     c                   move      *on           *in31                          Pos.cur.   . .
     c                   move      *on           *in80                          Error      . .
     c                   goto      c2taga                                                  . .
     c                   endif                                                           ... .
      *                                                                 .
      * Konto finnes ikke                                               .
      * men godtas uansett                                              .
      *                                                                 .
     c                   if        p_stat = 'N'                                          ... .
     c                   endif                                                           ... .
      *                                                                 .
      * Hvis avdeling er utfylt                                         .
      * må konto også utfylles                                          .
      *                                                                 .
     c                   if        p_stat = 'M'                                          ... .
     c                   move      *on           *in31                          Pos.cur.   . .
     c                   move      *on           *in82                          Error      . .
     c                   goto      c2taga                                                  . .
     c                   endif                                                           ... .
      *                                                                 .
      * Hvis spesifikasjon er utfylt                                    .
      * må konto også utfylles                                          .
      *                                                                 .
     c                   if        p_stat = 'K'                                          ... .
     c                   move      *on           *in31                          Pos.cur.   . .
     c                   move      *on           *in83                          Error      . .
     c                   goto      c2taga                                                  . .
     c                   endif                                                           ... .
      *
      * Sjekk om fra-konto
      * er i bruk på denne rapport
      * tidligere
      *
     c                   move      c28fkt        w_8kto
     c                   move      c28fsp        w_8sps
     c                   move      c28fav        w_8avd
     c                   exsr      xkon01                                                .....
      *                                                                 .
     c                   endif                                                           .....
      *
      * Til-konto må være > Fra-konto
      *
     c                   if        c28tkt > *zero                                        .....
     c                   if        c28fkt > c28tkt                                         . .
     c                   move      *on           *in32                          Pos.cur.   . .
     c                   move      *on           *in84                          Error      . .
     c                   goto      c2taga                                                  . .
     c                   endif                                                             . .
     c                   endif                                                           .....
      *
     c                   if        c28tav <> *zero or                                    .....
     c                             c28tkt <> *zero or                                        .
     c                             c28tsp <> *zero                                           .
      *
     c                   move      c28tkt        p_kont                                      .
     c                   move      c28tsp        p_spes                                      .
     c                   move      c28tav        p_avde                                      .
      *
     c                   call      'RS740R'                                      kontoplan-  .
     c                   parm                    p_stat
     c                   parm                    p_kont
     c                   parm                    p_spes
     c                   parm                    p_avde
     c                   parm                    p_text
      *
      * Avdeling finnes ikke
      * men godtatt hvis 9999
      *                                                                 .
     c                   if        p_stat = 'A'                                          ... .
     c                   if        c28tav <> 9999                                          . .
     c                   move      *on           *in32                          Pos.cur.   . .
     c                   move      *on           *in80                          Error      . .
     c                   goto      c2taga                                                  . .
     c                   endif                                                             . .
     c                   endif                                                           ... .
      *                                                                 .
      * Konto finnes ikke                                               .
      * men godtas uansett                                              .
      *                                                                 .
     c                   if        p_stat = 'N'                                          ... .
     c                   endif                                                           ... .
      *                                                                 .
      * Hvis avdeling er utfylt                                         .
      * må konto også utfylles                                          .
      *                                                                 .
     c                   if        p_stat = 'M'                                          ... .
     c                   move      *on           *in32                          Pos.cur.   . .
     c                   move      *on           *in82                          Error      . .
     c                   goto      c2taga                                                  . .
     c                   endif                                                           ... .
      *                                                                 .
      * Hvis spesifikasjon er utfylt                                    .
      * må konto også utfylles                                          .
      *                                                                 .
     c                   if        p_stat = 'K'                                          ... .
     c                   move      *on           *in32                          Pos.cur.   . .
     c                   move      *on           *in83                          Error      . .
     c                   goto      c2taga                                                  . .
     c                   endif                                                           ... .
      *                                                                 .
     c                   endif                                                           .....
      *
      * Sjekk om til-konto
      * er i bruk på denne linje
      *
     c                   move      c28tkt        w_8kto
     c                   move      c28tsp        w_8sps
     c                   move      c28tav        w_8avd
     c                   exsr      xkon01                                                .....
      *
     c                   endif
      /eject
      *
      * Hent fra linje
      *
     c                   if        *in35 = *on
      *
      * Sjekk om linjenr
      * er > dette linjenr
      *
     c                   if        c28lif >= p_5lin                                      .....
     c                   move      *on           *in33                          Pos.cur.     .
     c                   move      *on           *in80                          Error        .
     c                   goto      c2taga                                                    .
     c                   endif                                                           .....
      *
      * Sjekk om linjenr
      * er utfylt
      *
     c                   if        c28lif = *zero                                        .....
     c                   move      *on           *in33                          Pos.cur.     .
     c                   move      *on           *in81                          Error        .
     c                   goto      c2taga                                                    .
     c                   endif                                                           .....
      *
      * Sjekk om linjenr
      * finnes i linjeregisteret
      *
     c                   eval      rrf5l1_5rap = p_0rap
     c                   eval      rrf5l1_5lin = c28lif
     c     rrf5l1_key    chain     rrf5l1
     c                   if        %found                                                .....
     c                   move      *on           *in33                          Pos.cur.     .
     c                   move      *on           *in82                          Error        .
     c                   goto      c2taga                                                    .
     c                   endif                                                           .....
      *
     c                   endif
      *
     c                   if        *inkd
     c                   goto      c2taga
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   rrf8lur
      *
     c                   eval      rrf8lu_8lØp = c18lØp
     c     rrf8lu_key    chain     rrf8lur
      *
     c                   eval      rr8fkt = c28fkt                                           .
     c                   eval      rr8fsp = c28fsp                                           .
     c                   eval      rr8fav = c28fav                                           .
     c                   eval      rr8tkt = c28tkt                                           .
     c                   eval      rr8tsp = c28tsp                                           .
     c                   eval      rr8tav = c28tav                                           .
     c***                eval      rr8klf = c28klf                                           .
     c                   eval      rr8lif = c28lif                                           .
     c                   eval      rr8mot = c28mot                                           .
     c                   eval      rr8pro = c28pro                                           .
      *
     c                   time                    rr8dat
     c                   time                    rr8tim
     c                   eval      rr8usr = l_user
      *
     c                   if        %found
     c                   update    rrf8lur
     c                   else
     c                   eval      rr8fir = w_firm
     c                   eval      rr8rap = p_0rap
     c                   eval      rr8lin = p_5lin
     c                   eval      rr8lØp = c18lØP
     c                   write     rrf8lur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b18lØp = rr8lØp                                           .
     c                   eval      b18fkt = rr8fkt                                           .
     c                   eval      b18fsp = rr8fsp                                           .
     c                   eval      b18fav = rr8fav                                           .
     c                   eval      b18tkt = rr8tkt                                           .
     c                   eval      b18tsp = rr8tsp                                           .
     c                   eval      b18tav = rr8tav                                           .
     c                   eval      b18klf = rr8klf                                           .
     c                   eval      b18lif = rr8lif                                           .
     c                   eval      b18mot = rr8mot                                           .
     c                   eval      b18pro = rr8pro                                           .
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      rrf8lr_8lØp = d18lØp
     c     rrf8lr_key    chain     rrf8lr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      rrf8lu_8lØp = d18lØp
     c     rrf8lu_key    chain     rrf8lur
      *
     c                   delete    rrf8lur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c***                eval      s_8rap = k18rap
     c***                eval      s_8lin = k18lin
     c                   eval      s_8lØp = k18lØp
      *
      * Fyller kopierings-bildet
      *
     c                   eval      rrf8lr_8lØp = k18lØp
     c     rrf8lr_key    chain     rrf8lr
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k18lØp = 0
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      rrf8lr_8lØp = k18lØp
     c     rrf8lr_key    chain     rrf8lr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c***                eval      c18rap = s_8rap
     c***                eval      c18lin = s_8lin
     c                   eval      c18lØp = s_8lØp
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      rrf8l2
      *
     c                   if        %eof or
     c                             rr8fir <> w_firm or
     c                             rr8rap <> p_0rap or
     c                             rr8lin <> p_5lin
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c***                if        rr8lin <> p_5lin
     c***                iter
     c***                endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      rrf8l2_8lØp = rr8lØp
      *
     c                   eval      b1valg = 0
     c                   eval      b18rap = rr8rap
     c                   eval      b18lØp = rr8lØp
     c                   eval      b18fkt = rr8fkt                                           .
     c                   eval      b18fsp = rr8fsp                                           .
     c                   eval      b18fav = rr8fav                                           .
     c                   eval      b18tkt = rr8tkt                                           .
     c                   eval      b18tsp = rr8tsp                                           .
     c                   eval      b18tav = rr8tav                                           .
     c                   eval      b18klf = rr8klf                                           .
     c                   eval      b18lif = rr8lif                                           .
     c                   eval      b18mot = rr8mot                                           .
     c                   eval      b18pro = rr8pro                                           .
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        %eof(rrf8l2)
     c     rrf8l2_key    setgt     rrf8l2
     c                   readp     rrf8l2
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     rrf8l2
      *
     c                   if        %eof or
     c                             rr8fir <> w_firm
     c                   leave
     c                   endif
      *
     c                   if        rr8rap <> p_0rap
     c                   iter
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   eval      rrf8l2_8lØp = rr8lØp
      *
     c                   enddo
      *
     c                   if        %eof(rrf8l2)
     c     rrf8l2_key    setll     rrf8l2
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Kontroll mot tidligere linjer                                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xkon01        begsr
      *
      * Kontroller om konto er spesifisert
      * på en annen linje i rapporten
      *
     c                   eval      brukt = *blank
      *
     c                   eval      w_8lin = *zero
     c                   eval      w_8lØp = *zero
     c     rrf8l2_key    setll     rrf8l2
      *
     c     kotag1        tag
      *
     c                   read      rrf8l2                                 15
     c     *in15         cabeq     *on           kend01                         EOF
      *
     c                   if        rr8fir <> w_firm
     c                   move      *on           *in15
     c                   endif
      *
     c                   if        rr8rap <> p_0rap
     c                   move      *on           *in15
     c                   endif
      *
     c     *in15         cabeq     *on           kend01                         AVSLUTT
      *
     c                   if        rr8lin = p_5lin
     c                   goto      kotag1
     c                   endif
      *
     c                   if        w_8kto >= rr8fkt                                      .....
     c                             and w_8kto <= rr8tkt                                      .
      *
     c                   if        w_8sps >= rr8fsp and                                ..... .
     c                             w_8sps <= rr8tsp                                        . .
     c                   eval      brukt = 'N'                                             . .
     c                   move      rr8lin        w_8lin                                    . .
     c                   goto      kend01                                                  . .
     c                   endif                                                         ..... .
      *
     c                   endif                                                           .....
      *
     c                   goto      kotag1
      *
     c     kend01        tag
      *
     c                   endsr
      *
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_0rap
     c                   parm                    p_0txt
     c                   parm                    p_5lin
     c                   parm                    p_5txt
     c                   parm                    p_5kod
     c                   parm                    p_valg
      *
      * Key for posisjonering på linje
      *
     c     rrf5l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rrf5l1_5rap
     c                   kfld                    rrf5l1_5lin
      *
      * Key for posisjonering på linje
      *
     c     rrf8l2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rrf8l2_8rap
     c                   kfld                    rrf8l2_8lin
     c                   kfld                    rrf8l2_8lØp
      *
      * Key for les på løpenummer
      *
     c     rrf8lr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rrf8lr_8rap
     c                   kfld                    rrf8lr_8lin
     c                   kfld                    rrf8lr_8lØp
      *
      * Key for oppdatering av løpenummer
      *
     c     rrf8lu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rrf8lu_8rap
     c                   kfld                    rrf8lu_8lin
     c                   kfld                    rrf8lu_8lØp
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
     c                   eval      b20rap = p_0rap
     c                   eval      b20txt = p_0txt
     c                   eval      b25lin = p_5lin
     c                   eval      b25txt = p_5txt
     c                   eval      c20rap = p_0rap
     c                   eval      c20txt = p_0txt
     c                   eval      c25lin = p_5lin
     c                   eval      c25txt = p_5txt
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      rrf8l2_8rap = p_0rap
     c                   eval      rrf8l2_8lin = p_5lin
     c                   eval      rrf8lu_8rap = p_0rap
     c                   eval      rrf8lu_8lin = p_5lin
     c                   eval      rrf8lr_8rap = p_0rap
     c                   eval      rrf8lr_8lin = p_5lin
     c     rrf8l2_key    setll     rrf8l2
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
