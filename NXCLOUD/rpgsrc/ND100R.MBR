     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : ND100R
      * Beskrivelse . . : Ekstern ordre-linje, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       081002 HKO  Nytt program
      * 6.10  250609 BSA  Oppdatert med sporingsinformasjon
6.nu  * 6.30  030614 bsa  Programmet gjennomgått mtp nummerutvidelser.
6.21  * 6.20  050716 HKO  Utvidet med lager ved kall av ND105R
6.30  * 6.30  020719 bsa  Justert oppslag på tabeller
6.31  * 6.30  040719 bsa  Nullstiller tekstfelt
      *
7.xx  * 7.xx  190318 bkv  Endringer reltart til ny GUI. NB Kan kun gjelde
7.01  * 7.00  250614 bsa  Legger ut GTIN nummer fra EDI bestilling
7.02  * 7.00  090719 bsa  Viser tekst fra EDI melding
7.02a *                   Feil i visning
7.03  * 7.00  110719 bsa  Endret sjekk mot gyldig vare
7.04  * 7.00  281019 bsa  Gir mulighet for å bytte varenummer
7.05  * 7.01  260319 ask  Lagt ut varetkest 2 fra eksternordre pga. Winner
7.06  * 7.02  100419 ask  Lagt inn ekstra parameter (ordretype) i kall til ND105R
7.06  *                   for å overstyre kontroll mot lagervare på WIN
7.07  * 7.00  030120 bsa  Sjekker om varen er overført.
      *
8.01  *K0000  020622 bsa  Legger ut leveringsdato på tekstlinjer for å slippe splitt
8.01  *K0000              NB Leser videre fram til neste varelinje. Dvs at alle
8.01  *K0000              tekstlinjer vil "høre til" varelinja over.
8.02  *K0000  090323 bsa  Blir feil ved endring av vare, og feilmeldinger rundt dette.
8.03  *K0000  100323 bsa  Skal regne om mellom enheter ved bytte av enhet. Bruker samme
8.03  *K0000              logikk som ved ordre/bestilling.
8.04  *K0000  020523 bsa  Når vi endrer enhet blir omregningen feil, om vi bytter fra-til
8.04  *K0000              andre enheter uten å lagre.
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fnodtl1    if   e           k disk    rename(nodtpfr:nodtl1r)
     fnodtl2    if   e           k disk    rename(nodtpfr:nodtl2r)
     fnodtl3    if   e           k disk    rename(nodtpfr:nodtl3r)
     fnodtlr    if   e           k disk    rename(nodtpfr:nodtlrr)
     fnohel1    if   e           k disk    rename(nohepfr:nohel1r)
     fnkeolr    if   e           k disk    rename(nkeopfr:nkeolrr)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
7.03 fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
7.03 fjvpklr    if   e           k disk    rename(jvpkpfr:jvpklrr)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     fvltpl1    if   e           k disk    rename(vltppfr:vltpl1r)
     fnodtlu    uf   e           k disk    rename(nodtpfr:nodtlur)
7.01 feddtl1    if   e           k disk    rename(eddtpfr:eddtl1r)
7.02 fedtxlr    if   e           k disk    rename(edtxpfr:edtxlrr)
7.07 feddtl3    if   e           k disk    rename(eddtpfr:eddtl3r)
8.03 fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
      *
     fnd100d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(ndfirm)
     d p_fsys          s                   like(ndfsys)
     d p_tell          s                   like(ndtell)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d nodtl1_line     s                   like(ndline)
     d nodtl2_vare     s                   like(ndvare)
     d nodtl3_tek1     s                   like(ndtek1)
     d nodtlr_line     s                   like(ndline)
     d vvarl1_vare     s                   like(vvvare)
     d vvenl1_vare     s                   like(vevare)
     d vvenl1_enhe     s                   like(veenhe)
     d vltpl1_lety     s                   like(vylety)
     d nodtlu_line     s                   like(ndline)
     d nkeolr_kode     s                   like(nkkode)
6.30 d eddtl1_ldor     s                   like(edldor)
6.30 d eddtl1_ldat     s                   like(edldat)
6.30 d eddtl1_line     s                   like(edline)
7.02 d edtxlr_ldor     s                   like(etldor)
7.02 d edtxlr_ldat     s                   like(etldat)
7.02 d edtxlr_line     s                   like(etline)
7.02 d edtxlr_type     s                   like(ettype)
7.02 d edtxlr_tlin     s                   like(ettlin)
7.03 d jvarl1_vare     s                   like(jvvare)
7.03 d jvpklr_vare     s                   like(jwvare)
7.07 d eddtl3_line     s                   like(edline)
8.03 d vlagl1_lage     s                   like(vllage)
8.03 d vlagl1_vare     s                   like(vlvare)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
7.03 d b_VAva          s              1    inz(*off)
7.03 d b_VAen          s              1    inz(*off)
8.03 d b_enhe          s              1    inz(*off)
      *
     d w_etxt          s             35
     d w_ldat          s               d   datfmt(*iso)
     d w_udat          s               d   datfmt(*iso)
      *
     d w_firm          s                   like(ndfirm)
     d w_fsys          s                   like(ndfsys)
     d w_tell          s                   like(ndtell)
     d w_vare          s                   like(ndvare)
7.01 d w_gtin          s                   like(edgtin)
7.01 d w_gvar          s                   like(vvvare)
7.01 d w_genh          s                   like(ndenhe)
7.01 d w_gsts          s              1
7.04 d w_varenr        s                   like(vvvare)
7.04 d w_vtxt          s                   like(vvtek1)
8.03 d w_levr          s              6  0
8.03 d w_antu          s             11  3
      *
     d s_line          s                   like(ndline)
8.03 d s_enhe          s                   like(ndenhe)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * C3BLD  - C3 Skjermbilde for å vise melding/tekstlinje
      * D1WIN  - D1 Vindu for slette (Delete)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
7.02 c                   when      *inkp
7.02 c                   exsr      vis_otext
7.02 c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * F13=Statuskoder
      *
     c                   if        *inkm
     c                   call      'ND110R'
     c                   goto      b2tagb
     c                   endif
      *
      * Posisjonering
      *
     c                   if        b2vare <> *blank or
     c                             b2tek1 <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'V'
     c                   eval      nodtl2_vare = b1vare
     c     nodtl2_key    setll     nodtl2
     c                   when      w_seqe = 'T'
     c                   eval      nodtl3_tek1 = b1tek1
     c     nodtl3_key    setll     nodtl3
     c                   other
     c                   eval      nodtl1_line = b1line
     c     nodtl1_key    setll     nodtl1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
     c                   eval      w_seqe = *blank
      *
      * Varenummer
      *
     c                   if        b2vare <> *blank
     c                   eval      w_seqe = 'V'
     c                   eval      nodtl2_vare = b2vare
     c     nodtl2_key    setll     nodtl2
     c                   goto      end_pos
     c                   endif
      *
      * Varetekst-1
      *
     c                   if        b2tek1 <> *blank
     c                   eval      w_seqe = 'T'
     c                   eval      nodtl3_tek1 = b2tek1
     c     nodtl3_key    setll     nodtl3
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2vare = *blank
     c                   eval      b2tek1 = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c2nref = b2nref
     c                   eval      c2line = b1line
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1nref = b2nref
     c                   eval      d1line = b1line
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c2nref = b2nref
     c                   eval      c2line = b1line
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
7.04  *
7.04  * Bytt varenummer
7.04  *
7.04 c                   if        b1valg = 8
7.04 c                   eval      c2nref = b2nref
7.04 c                   eval      c2line = b1line
7.04 c                   exsr      xc8bld
8.02 c                   if        *inkc = *off
7.04 c                   exsr      xc2bld
8.02 c                   endif
7.04 c                   eval      b1valg = 0
7.04 c                   update    b1sfl
7.04 c                   iter
7.04 c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      nodtlr_line = c2line
     c     nodtlr_key    chain     nodtlr
     c                   if        not %found
     c                   goto      end_c2bld
     c                   endif
      *
      * Kaller eget bilde ved melding/tekstlinje
      *
     c                   if        ndvare = *blank and
     c                             ndtek1 <> *blank
     c                   exsr      xc3bld
     c                   goto      end_c2bld
     c                   endif
7.01  *
7.01  * Hent eventuell info om GTIN fra EDI bestilling
7.01  *
7.01 c                   eval      c2gtin = 0
7.01 c                   eval      c2genh = *blanks
7.01 c                   eval      c2gnob = 0
7.01  *
6.30 c                   eval      eddtl1_ldor = 0
6.30 c                   eval      eddtl1_ldat = *loval
6.30 c                   eval      eddtl1_line = nodtlr_line
6.30 c**   nodtlr_key    chain     eddtl1
6.30 c     eddtl1_key    chain     eddtl1
7.01 c                   if        %found
7.01 c                   eval      c2gtin = edgtin
7.01 c                   eval      w_gtin = edgtin
7.01  * Finn enhet og nobbnummer basert på angitt GTIN
7.01 c                   call      'VE710R'
7.01 c                   parm                    w_firm
7.01 c                   parm                    w_gtin
7.01 c                   parm                    w_gvar
7.01 c                   parm                    w_genh
7.01 c                   parm                    w_gsts
7.01  *
7.01 c                   if        w_gsts = '1'
7.01 c                   eval      c2gnob = %int(w_gvar)
7.01 c                   eval      c2genh = w_genh
7.01 c                   endif
7.01 c                   endif
      *
      * Initierer bildet
7.02  *
7.02  * Finnes det noen linjer med fritekstlinjer tilknyttet ordrehodet
7.02  *
7.02 c                   eval      edtxlr_ldor = 0
7.02 c                   eval      edtxlr_ldat = *loval
7.02 c                   eval      edtxlr_line = c2line
7.02 c                   eval      edtxlr_type = 'L'
7.02 c                   eval      edtxlr_tlin = 0
7.02 c     edtxlr_key    setll     edtxlr
7.02 c     edtxlr_key2   reade     edtxlr
7.02 c                   dow       not %eof
7.02 c                   if        %trim(ettek1) <> *blanks
7.02 c                   eval      *in32 = *on
7.02 c                   endif
7.02 c     edtxlr_key2   reade     edtxlr
7.02 c                   enddo
      *
     c                   eval      c2tek1 = ndtek1
7.05 c                   eval      c2tek2 = ndtek2
     c                   eval      c2ntpr = ndntpr
     c                   eval      c2vare = ndvare
     c                   eval      c2anta = ndanta
     c                   eval      c2enhe = ndenhe
     c                   eval      c2lety = ndlety
     c                   eval      c2ldat = 0
     c                   eval      c2eusr = ndeusr
6.31 c                   eval      c2vtxt = *blanks
      *
     c     *dmy          move      ndedat        c2edat
      *
     c                   if        ndldat <> *loval
     c     *dmy          move      ndldat        c2ldat
     c                   endif
      *
     c                   eval      vvarl1_vare = c2vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   eval      c2vtxt = vvtek1
7.03 c                   else
7.03  *
7.03  * Sjekk også mot VA
7.03  *
7.03 c                   eval      jvarl1_vare = c2vare
7.03 c     jvarl1_key    chain     jvarl1
7.03 c                   if        %found
7.03 c                   eval      c2vtxt = jvtek1
7.03 c                   endif
     c                   endif
      *
     c                   eval      vltpl1_lety = c2lety
     c     vltpl1_key    chain     vltpl1
     c                   if        %found
     c                   eval      c2ttxt = vybesk
     c                   endif
8.03  *
8.03  * Sjekk om vi har gyldig enhet på linja fra før.
8.03  *
8.03 c                   eval      b_enhe = *off
8.03 c                   eval      vvenl1_vare = c2vare
8.03 c                   eval      vvenl1_enhe = c2enhe
8.03 c     vvenl1_key    chain     vvenl1
8.03 c                   if        %found
8.03 c                   eval      b_enhe = *on
8.03 c                   eval      s_enhe = c2enhe
8.03 c                   else
8.03 c                   eval      jvpklr_vare = c2vare
8.03 c     jvpklr_key    setll     jvpklr
8.03 c     jvpklr_key    reade     jvpklr
8.03 c                   dow       not %eof
8.03 c                   if        jwenhe  = c2enhe
8.03 c                   eval      b_enhe = *on
8.03 c                   eval      s_enhe = c2enhe
8.03 c                   endif
8.03 c     jvpklr_key    reade     jvpklr
8.03 c                   enddo
8.03 c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
8.02 c                   if        *inkc or *inkl
8.02 c                   leavesr
8.02 c                   endif
      *
7.02 c                   if        *inkp
7.02 c                   exsr      vis_ltext
7.02 c                   goto      c2taga
7.02 c                   endif
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * F4=Spørring
      *
     c                   if        *inkd
      *
     c                   if        w_afld = 'C2VARE'
     c                   eval      w_vare = c2vare
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    c2vtxt
     c                   eval      c2vare = w_vare
     c                   goto      c2taga
     c                   endif
      *
     c                   if        w_afld = 'C2ENHE'
     c                   eval      w_vare = c2vare
     c                   call      'VE550R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    c2enhe
     c                   parm                    w_etxt
8.03  *
8.03  * Regn om antallet ihht ny valgt enhet. NB Kun hvis vi har en gyldig enhet fra før.
8.03  *
8.03 c                   if        b_enhe = *on and
8.03 c                             s_enhe <> c2enhe
8.03 c                   exsr      omr_antall
8.03 c                   eval      s_enhe = c2enhe
8.03 c                   endif
     c                   goto      c2taga
     c                   endif
      *
     c                   if        w_afld = 'C2LETY'
     c                   call      'VY500R'
     c                   parm                    w_firm
     c                   parm                    c2lety
     c                   parm                    c2ttxt
     c                   goto      c2taga
     c                   endif
      *
     c                   goto      c2taga
      *
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2vare = *blank
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
7.03 c                   eval      b_VAva = *off
     c                   eval      vvarl1_vare = c2vare
     c     vvarl1_key    chain     vvarl1
7.03 c                   if        %found
     c                   eval      c2vtxt = vvtek1
7.03 c                   else
7.03  * Sjekk også vare mot VA
7.03 c                   eval      jvarl1_vare = c2vare
7.03 c     jvarl1_key    chain     jvarl1
7.03 c                   if        not %found
7.03 c                   eval      *in32 = *on
7.03 c                   goto      c2taga
7.03 c                   endif
7.03 c                   eval      c2vtxt = jvtek1
7.03 c                   eval      b_VAva = *on
7.03 c                   endif
      *
     c                   if        c2anta <= 0
     c                   eval      *in33 = *on
     c                   goto      c2taga
     c                   endif
      *
7.03 c                   if        b_VAva = *off
     c                   eval      vvenl1_vare = c2vare
     c                   eval      vvenl1_enhe = c2enhe
     c     vvenl1_key    chain     vvenl1
     c                   if        not %found
     c                   eval      *in34 = *on
     c                   goto      c2taga
8.03 c                   else
8.03 c                   eval      b_enhe = *on
     c                   endif
7.03 c                   else
7.03  * Sjekk mot VA
7.03 c                   eval      b_VAen = *off
7.03 c                   eval      jvpklr_vare = c2vare
7.03 c     jvpklr_key    setll     jvpklr
7.03 c     jvpklr_key    reade     jvpklr
7.03 c                   dow       not %eof
7.03 c                   if        jwenhe  = c2enhe
7.03 c                   eval      b_VAen = *on
8.03 c                   eval      b_enhe = *on
7.03 c                   endif
7.03 c     jvpklr_key    reade     jvpklr
7.03 c                   enddo
7.03  *
7.03 c                   if        b_VAen = *off
7.03 c                   eval      *in34 = *on
7.03 c                   goto      c2taga
7.03 c                   endif
7.03  *
7.03 c                   endif
7.03  *
8.03 c                   if        b_enhe = *on and
8.03 c                             s_enhe <> c2enhe
8.03 c                   exsr      omr_antall
8.03 c                   eval      s_enhe = c2enhe
8.03  * Vis bildet på nytt med omregnet kvantum.
8.03 c                   goto      c2taga
8.03 c                   endif
      *
     c                   if        c2lety <> 0
     c                   eval      vltpl1_lety = c2lety
     c     vltpl1_key    chain     vltpl1
     c                   if        not %found
     c                   eval      *in35 = *on
     c                   goto      c2taga
     c                   endif
     c                   eval      c2ttxt = vybesk
     c                   endif
      *
     c                   if        c2ldat <> 0
      *
     c     *dmy          test(d)                 c2ldat                 36
     c                   if        *in36 = *on
     c                   goto      c2taga
     c                   endif
      *
     c                   time                    w_udat
     c     *dmy          move      c2ldat        w_ldat
     c                   if        w_ldat < w_udat
     c                   eval      *in37 = *on
     c                   goto      c2taga
     c                   endif
      *
     c                   endif
      *
      * Oppdatering av ekstern ordre-linje
      *
     c                   clear                   nodtlur
      *
     c                   eval      nodtlu_line = c2line
     c     nodtlu_key    chain     nodtlur
      *
     c                   eval      ndvare = c2vare
     c                   eval      ndanta = c2anta
     c                   eval      ndenhe = c2enhe
     c                   eval      ndlety = c2lety
     c                   eval      ndldat = *loval
     c                   eval      ndeusr = l_user
      *
     c                   time                    ndedat
     c                   time                    ndetim
      *
     c                   if        c2ldat <> 0
     c     *dmy          move      c2ldat        ndldat
     c                   endif
      *
     c                   if        %found(nodtlu)
6.10 c                   time                    ndedat
6.10 c                   time                    ndetim
6.10 c                   eval      ndeusr = l_user
     c                   update    nodtlur
     c                   endif
8.01  *
8.01  *  Oppdatering av tilhørende tekstlinje(r) med leveringsdato
8.01  *
8.01 c                   eval      nodtlu_line = c2line + 1
8.01 c     nodtlu_key    setll     nodtlu
8.01 c     nodtlu_key2   reade     nodtlu
8.01 c                   dow       not %eof
8.01 c                   if        ndvare = *blanks
8.01 c     *dmy          move      c2ldat        ndldat
8.01 c                   update    nodtlur
8.01 c                   else
8.01 c                   unlock    nodtlu
8.01 c                   leave
8.01 c                   endif
8.01 c     nodtlu_key2   reade     nodtlu
8.01 c                   enddo
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1vare = ndvare
     c                   eval      b1anta = ndanta
     c                   eval      b1enhe = ndenhe
     c                   eval      b1lety = ndlety
     c                   eval      b1ldat = 0
     c                   if        ndldat <> *loval
     c     *dmy          move      ndldat        b1ldat
     c                   endif
     c                   exsr      hent_status
     c                   endif
      *
8.03 c                   eval      b_forn = *on
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for melding/tekstlinje (C3BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc3bld        begsr
      *
      * Initierer bildet
      *
     c                   eval      c3meld = ndtek1 + ndtek2
      *
     c     c3taga        tag
      *
     c                   exfmt     c3bld
      *
     c                   endsr
      *
7.04  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.04  * Subrutine for å bytte varenummer                   (S8WIN)
7.04  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.04  *
7.04 c     xc8bld        begsr
7.04  *
7.04 c                   eval      s8vare = b1vare
7.04 c                   eval      s8nyva = *blank
7.04 c     s8tagb        tag
7.04  *
7.04 c                   exfmt     s8win
7.04  *
7.04  * F3=Avslutt
7.04  *
7.04 c                   if        *inkc or *inkl
7.04 c                   eval      *inkc = *on
7.04 c                   leavesr
7.04 c                   endif
7.04  *
7.04 c                   if        *inkd
7.04 c                   call      'VV500R'
7.04 c                   parm                    w_firm
7.04 c                   parm                    w_varenr
7.04 c                   parm                    w_vtxt
7.04 c                   eval      s8nyva = w_varenr
7.04 c                   endif
7.04  *
7.04 c                   if        s8nyva = *blank
7.04 c                   goto      end_c8bld
7.04 c                   endif
7.04  *
7.04  * Oppdater NODT med byttet varenummer.
7.04  *
7.04 c                   eval      nodtlu_line = b1line
7.04 c     nodtlu_key    chain     nodtlur
7.04  *
7.04 c                   if        %found
7.04 c                   eval      ndvare = s8nyva
7.04 c                   eval      ndtek1 = w_vtxt
7.04 c                   eval      ndldat = *loval
7.04 c                   eval      ndeusr = l_user
7.04 c                   time                    ndedat
7.04 c                   time                    ndetim
7.04 c                   eval      ndeusr = l_user
7.04 c                   update    nodtlur
7.04 c                   endif
7.04  *
8.03 c                   eval      b_forn = *on
7.04  *
7.04 c     end_c8bld     endsr
7.04  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      nodtlr_line = d1line
     c     nodtlr_key    chain     nodtlr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter ekstern ordre-linje
      *
     c                   eval      nodtlu_line = d1line
     c     nodtlu_key    chain     nodtlur
     c                   delete    nodtlur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'V'
     c                   read      nodtl2
     c                   when      w_seqe = 'T'
     c                   read      nodtl3
     c                   other
     c                   read      nodtl1
     c                   endsl
      *
     c                   if        %eof or
     c                             ndfirm <> w_firm or
     c                             ndfsys <> w_fsys or
     c                             ndtell <> w_tell
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'V'
     c                   eval      nodtl2_vare = ndvare
     c                   when      w_seqe = 'T'
     c                   eval      nodtl3_tek1 = ndtek1
     c                   other
     c                   eval      nodtl1_line = ndline
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1line = ndline
     c                   eval      b1vare = ndvare
     c                   eval      b1tek1 = ndtek1
     c                   eval      b1anta = ndanta
     c                   eval      b1enhe = ndenhe
     c                   eval      b1ntpr = ndntpr
     c                   eval      b1lety = ndlety
     c                   eval      b1ldat = 0
     c                   eval      b1stat = 0
7.07 c                   eval      b1over = 0
7.07  * Ligger den på skygggetabell med ordre og linjenummer
7.07 c                   eval      eddtl3_line = ndline
7.07 c     eddtl3_key    chain     eddtl3
7.07 c                   if        %found
7.07 c                   if        edonum <> 0 and
7.07 c                             edolin <> 0
7.07 c                   eval      b1over = 1
7.07 c                   endif
7.07 c                   endif
      *
     c                   if        ndldat <> *loval
     c     *dmy          move      ndldat        b1ldat
     c                   endif
      *
     c                   exsr      hent_status
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'V'
     c     nodtl2_key    setgt     nodtl2
     c                   readp     nodtl2
     c                   when      w_seqe = 'T'
     c     nodtl3_key    setgt     nodtl3
     c                   readp     nodtl3
     c                   other
     c     nodtl1_key    setgt     nodtl1
     c                   readp     nodtl1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'V'
     c                   readp     nodtl2
     c                   when      w_seqe = 'T'
     c                   readp     nodtl3
     c                   other
     c                   readp     nodtl1
     c                   endsl
      *
     c                   if        %eof or
     c                             ndfirm <> w_firm or
     c                             ndfsys <> w_fsys or
     c                             ndtell <> w_tell
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'V'
     c                   eval      nodtl2_vare = ndvare
     c                   when      w_seqe = 'T'
     c                   eval      nodtl3_tek1 = ndtek1
     c                   other
     c                   eval      nodtl1_line = ndline
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'V'
     c     nodtl2_key    setll     nodtl2
     c                   when      w_seqe = 'T'
     c     nodtl3_key    setll     nodtl3
     c                   other
     c     nodtl1_key    setll     nodtl1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å hente statuskode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_status   begsr
      *
      * Henter feilstatus fra ekstern ordre-linje
      *
     c                   call      'ND105R'
     c                   parm                    w_firm
     c                   parm                    b1stat
6.21 c                   parm                    nolage
     c                   parm                    ndvare
     c                   parm                    ndtek1
     c                   parm                    ndanta
     c                   parm                    ndenhe
     c                   parm                    ndlety
     c                   parm                    ndldat
7.06 c                   parm                    nofsys
7.01  *
7.01  * TEMPORÆRT DISABLED
7.01  *
7.01 c                   if        1=2
7.01 c                   if        b1stat = 0
7.01  * Slå opp mot EDI bestilling - kopiregister
6.30 c                   eval      eddtl1_ldor = 0
6.30 c                   eval      eddtl1_ldat = *loval
6.30 c                   eval      eddtl1_line = ndline
6.30 c*                  eval      nodtlu_line = ndline
6.30 c*    nodtlu_key    chain     eddtl1r
6.30 c     eddtl1_key    chain     eddtl1r
7.01 c                   if        %found
7.01 c                   eval      w_gtin = edgtin
7.01 c                   if        w_gtin <> 0
7.01 c                   call      'VE717R'
7.01 c                   parm                    w_firm
7.01 c                   parm                    w_gtin
7.01 c                   parm                    w_gvar
7.01 c                   parm                    w_genh
7.01 c                   parm                    w_gsts
7.01  *
7.01 c                   if        w_gsts = '1'
7.01 c                   if        w_gvar <> ndvare
7.01 c                   eval      b1stat = 4
7.01 c                   endif
7.01 c                   if        w_genh <> ndenhe
7.01 c                   eval      b1stat = 4
7.01 c                   endif
7.01 c                   else
7.01 c                   eval      b1stat = 4
7.01 c                   endif
7.01  *
7.01 c                   else
7.01 c                   eval      b1stat = 4
7.01 c                   endif
7.01 c                   endif
      *
7.01 c                   endif
7.01 c                   endif
      *
     c                   endsr
      *
7.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.02  * Subrutine for å vise ordretekst
7.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.02  *
7.02 c     vis_otext     begsr
7.02  *
7.02 c                   eval      b15tx1 = *blank
7.02 c                   eval      b15tx2 = *blank
7.02 c                   eval      b15tx3 = *blank
7.02 c                   eval      b15tx4 = *blank
7.02 c                   eval      b15tx5 = *blank
7.02  *
7.02 c                   eval      edtxlr_ldor = 0
7.02 c                   eval      edtxlr_ldat = *loval
7.02 c                   eval      edtxlr_line = 0
7.02 c                   eval      edtxlr_type = 'H'
7.02 c                   eval      edtxlr_tlin = 0
7.02 c     edtxlr_key    setll     edtxlr
7.02 c     edtxlr_key2   reade     edtxlr
7.02 c                   dow       not %eof
7.02  *
7.02 c                   if        %trim(ettek1) <> *blanks
7.02  *
7.02 c                   if        %trim(b15tx1) = *blanks
7.02 c                   eval      b15tx1 = %trim(ettek1)
7.02ac                   goto      les_neste
7.02 c                   endif
7.02  *
7.02 c                   if        %trim(b15tx2) = *blanks
7.02 c                   eval      b15tx2 = %trim(ettek1)
7.02ac                   goto      les_neste
7.02 c                   endif
7.02  *
7.02 c                   if        %trim(b15tx3) = *blanks
7.02 c                   eval      b15tx3 = %trim(ettek1)
7.02ac                   goto      les_neste
7.02 c                   endif
7.02  *
7.02 c                   if        %trim(b15tx4) = *blanks
7.02 c                   eval      b15tx4 = %trim(ettek1)
7.02ac                   goto      les_neste
7.02 c                   endif
7.02  *
7.02 c                   if        %trim(b15tx5) = *blanks
7.02 c                   eval      b15tx5 = %trim(ettek1)
7.02ac                   goto      les_neste
7.02 c                   endif
7.02  *
7.02ac     les_neste     tag
7.02  *
7.02 c                   endif
7.02  *
7.02 c     edtxlr_key2   reade     edtxlr
7.02 c                   enddo
7.02  *
7.02 c                   exfmt     b15win
7.02  *
7.02 c     end_otext     endsr
7.02  *
7.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.02  * Subrutine for å vise tekst tilknyttet linje
7.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.02  *
7.02 c     vis_ltext     begsr
7.02  *
7.02 c                   eval      b15tx1 = *blank
7.02 c                   eval      b15tx2 = *blank
7.02 c                   eval      b15tx3 = *blank
7.02 c                   eval      b15tx4 = *blank
7.02 c                   eval      b15tx5 = *blank
7.02  *
7.02 c                   eval      edtxlr_ldor = 0
7.02 c                   eval      edtxlr_ldat = *loval
7.02 c                   eval      edtxlr_line = ndline
7.02 c                   eval      edtxlr_type = 'L'
7.02 c                   eval      edtxlr_tlin = 0
7.02 c     edtxlr_key    setll     edtxlr
7.02 c     edtxlr_key2   reade     edtxlr
7.02 c                   dow       not %eof
7.02  *
7.02 c                   if        %trim(ettek1) <> *blanks
7.02  *
7.02 c                   if        %trim(b15tx1) = *blanks
7.02 c                   eval      b15tx1 = %trim(ettek1)
7.02 c                   goto      neste
7.02 c                   endif
7.02 c                   if        %trim(b15tx2) = *blanks
7.02 c                   eval      b15tx2 = %trim(ettek1)
7.02 c                   goto      neste
7.02 c                   endif
7.02 c                   if        %trim(b15tx3) = *blanks
7.02 c                   eval      b15tx3 = %trim(ettek1)
7.02 c                   goto      neste
7.02 c                   endif
7.02 c                   if        %trim(b15tx4) = *blanks
7.02 c                   eval      b15tx4 = %trim(ettek1)
7.02 c                   goto      neste
7.02 c                   endif
7.02 c                   if        %trim(b15tx5) = *blanks
7.02 c                   eval      b15tx5 = %trim(ettek1)
7.02 c                   goto      neste
7.02 c                   endif
7.02  *
7.02 c     neste         tag
7.02  *
7.02 c                   endif
7.02  *
7.02 c     edtxlr_key2   reade     edtxlr
7.02 c                   enddo
7.02  *
7.02 c                   exfmt     b15win
7.02  *
7.02 c     end_ltext     endsr
7.02  *
8.03  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.03  * Omregning av antall ifht enhet
8.03  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.03  *
8.03 c     omr_antall    begsr
8.03  *
8.03 c                   eval      w_levr = 0
8.03 c                   eval      w_vare = c2vare
8.03  *
8.03 c                   eval      vlagl1_vare = ndvare
8.03 c                   eval      vlagl1_lage = nolage
8.03 c     vlagl1_key    chain     vlagl1
8.03 c                   if        %found and
8.03 c                             vlbldo <> 0
8.03 c                   eval      w_levr = vlbldo
8.03 c                   else
8.03 c                   eval      w_levr = vvldor
8.03 c                   endif
8.03  *
8.03 c                   eval      w_antu = 0
8.03  *
8.03 c                   call      'VA771R'
8.03 c                   parm                    w_firm
8.03 c                   parm                    w_levr
8.03 c                   parm                    w_vare
8.03 c                   parm                    s_enhe
8.04 c**                 parm                    ndanta
8.04 c                   parm                    c2anta
8.03 c                   parm                    c2enhe
8.03 c                   parm                    w_antu
8.03  *
8.03 c                   if        w_antu <> 0
8.03 c                   eval      c2anta = w_antu
8.03 c                   endif
8.03  *
8.03 c                   endsr
8.03  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_fsys
     c                   parm                    p_tell
      *
      * Key for posisjonering på linjenummer
      *
     c     nodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_fsys
     c                   kfld                    w_tell
     c                   kfld                    nodtl1_line
      *
      * Key for posisjonering på varenummer
      *
     c     nodtl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_fsys
     c                   kfld                    w_tell
     c                   kfld                    nodtl2_vare
      *
      * Key for posisjonering på varetekst
      *
     c     nodtl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_fsys
     c                   kfld                    w_tell
     c                   kfld                    nodtl3_tek1
      *
      * Key for les på ekstern ordre-linje
      *
     c     nodtlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_fsys
     c                   kfld                    w_tell
     c                   kfld                    nodtlr_line
      *
      * Key for oppslag på ekstern ordre-hode
      *
     c     nohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_fsys
     c                   kfld                    w_tell
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
     c                   kfld                    vvenl1_enhe
      *
      * Key for oppslag på leveringstype
      *
     c     vltpl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltpl1_lety
      *
      * Key for oppdatering av ekstern ordre-linje
      *
     c     nodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_fsys
     c                   kfld                    w_tell
     c                   kfld                    nodtlu_line
8.01  *
8.01  * Key for oppdatering av ekstern ordre-linje
8.01  *
8.01 c     nodtlu_key2   klist
8.01 c                   kfld                    w_firm
8.01 c                   kfld                    w_fsys
8.01 c                   kfld                    w_tell
      *
      * Key for henting av ekstern ordrekode
      *
     c     nkeolr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    nkeolr_kode
6.30  *
6.30  * Key for les på ekstern ordre-linje
6.30  *
6.30 c     eddtl1_key    klist
6.30 c                   kfld                    w_firm
6.30 c                   kfld                    w_fsys
6.30 c                   kfld                    w_tell
6.30 c                   kfld                    eddtl1_ldor
6.30 c                   kfld                    eddtl1_ldat
6.30 c                   kfld                    eddtl1_line
7.02  *
7.02  * Key for les på ekstern ordre-linje
7.02  *
7.02 c     edtxlr_key    klist
7.02 c                   kfld                    w_firm
7.02 c                   kfld                    w_fsys
7.02 c                   kfld                    w_tell
7.02 c                   kfld                    edtxlr_ldor
7.02 c                   kfld                    edtxlr_ldat
7.02 c                   kfld                    edtxlr_line
7.02 c                   kfld                    edtxlr_type
7.02 c                   kfld                    edtxlr_tlin
7.02  *
7.02  * Key for les på ekstern ordre-linje
7.02  *
7.02 c     edtxlr_key2   klist
7.02 c                   kfld                    w_firm
7.02 c                   kfld                    w_fsys
7.02 c                   kfld                    w_tell
7.02 c                   kfld                    edtxlr_ldor
7.02 c                   kfld                    edtxlr_ldat
7.02 c                   kfld                    edtxlr_line
7.02 c                   kfld                    edtxlr_type
7.03  *
7.03  * Key for oppslag på vareregister i VA
7.03  *
7.03 c     jvarl1_key    klist
7.03 c                   kfld                    jvarl1_vare
7.03  *
7.03  * Key for oppslag på vareregister i VA
7.03  *
7.03 c     jvpklr_key    klist
7.03 c                   kfld                    jvpklr_vare
7.07  *
7.07  * Key for les på ekstern ordre-linje
7.07  *
7.07 c     eddtl3_key    klist
7.07 c                   kfld                    w_firm
7.07 c                   kfld                    w_fsys
7.07 c                   kfld                    w_tell
7.07 c                   kfld                    eddtl3_line
8.03  *
8.03  * Key for oppslag på vare/lager
8.03  *
8.03 c     vlagl1_key    klist
8.03 c                   kfld                    w_firm
8.03 c                   kfld                    vlagl1_vare
8.03 c                   kfld                    vlagl1_lage
      *
      * Henter firma og referanse fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_fsys = p_fsys
     c                   eval      w_tell = p_tell
      *
      * Initierer skjermbildet
      *
     c                   eval      *in22 = *on
      *
     c                   eval      b2fsys = w_fsys
      *
     c                   eval      nkeolr_kode = w_fsys
     c     nkeolr_key    chain     nkeolr
      *
     c     nohel1_key    chain     nohel1
      *
     c                   eval      b2nref = nonref
     c                   eval      b2knav = noknav
7.02  *
7.02  * Finnes det noen linjer med fritekstlinjer tilknyttet ordrehodet
7.02  *
7.02 c                   eval      edtxlr_ldor = 0
7.02 c                   eval      edtxlr_ldat = *loval
7.02 c                   eval      edtxlr_line = 0
7.02 c                   eval      edtxlr_type = 'H'
7.02 c                   eval      edtxlr_tlin = 0
7.02 c     edtxlr_key    setll     edtxlr
7.02 c     edtxlr_key2   reade     edtxlr
7.02 c                   dow       not %eof
7.02 c                   if        %trim(ettek1) <> *blanks
7.02 c                   eval      *in32 = *on
7.02 c                   endif
7.02 c     edtxlr_key2   reade     edtxlr
7.02 c                   enddo
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      nodtl1_line = 0
     c     nodtl1_key    setll     nodtl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
