     h option(*nodebugio) datedit(*dmy)
     h decedit('0.')
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : RZ002R
      * Beskrivelse . . : Leser linjer fra BOVF til fil
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * ----- ------ ---- -------------------------------------------------
      *       050911 nho  Zirius
6.nu  * 6.30  230614 bof  sjekket mtp. nummerutvidelse
6.36  *       120521 mst  Henter avvikende momskode, hvis den finnes
6.37  *       120521 mst  Henter avvikende bilagskode, hvis den finnes
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     fbovfl1    ip   e           k disk
     fsohel1    if   e           k disk    rename(sohepfr:sohel1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
6.36 frb00l1    if   e           k disk    rename(rb00pfr:rb00l1r)
6.37 frb10l1    if   e           k disk    rename(rb10pfr:rb10l1r)
      *
      *
     frz02pf    o    e             disk
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
6.11  *
      * Datastruktur for forfallsdato
     d                 ds
nina d d_fdat                  1      6  0
     d  d_fa1                  1      1  0
     d  d_fag                  1      2  0
     d  d_fnd                  3      4  0
nina d  d_far                  5      6  0
      *
     d                 ds
     d d_fdati                 1     10
     d  d_fa                   1      4
     d  d_f1                   5      5
     d  d_fn                   6      7
     d  d_f2                   8      8
     d  d_fd                   9     10
     d*
     d                 ds
     d d_utdato                1      8
     d  d_utaa                 1      4
     d  d_utmn                 5      6
     d  d_utda                 7      8
      *
      * Datastruktur for bilagsdato
     d                 ds
     d d_date                  1      6  0
     d  d_da1                  1      1  0
     d  d_dag                  1      2  0
     d  d_mnd                  3      4  0
     d  d_aar                  5      6  0
      *
     d                 ds
     d d_dati                  1     10
     d  d_aa                   1      4
     d  d_a1                   5      5
     d  d_mn                   6      7
     d  d_a2                   8      8
     d  d_dd                   9     10
      *
6.11  * Datastruktur for postted
6.11 d                 ds
6.11 d d_pste                  1     30
6.11 d  d_ps1                  6     30
      *
      * Definering av recordbeskrivelse - Zirius
     d                 ds
     d d_inpu                       433
     d  d_bunknr               1      5  0
     d  d_År                   6      9  0
     d  d_per                 10     11  0
     d  d_bilnr               12     19  0
     d  d_bildto              20     27  0
     d  d_fordto              28     35  0
     d  d_debkto              36     43  0
     d  d_debrkto             44     55  0
     d  d_debmva              56     56
     d  d_debavd              57     66
     d  d_debprosj            67     76
     d  d_debdim1             77     86
     d  d_debdim2             87     96
     d  d_krekto              97    104  0
     d  d_krerkto            105    116  0
     d  d_kremva             117    117
     d  d_kreavd             118    127
     d  d_kreprosj           128    137
     d  d_kredim1            138    147
     d  d_kredim2            148    157
     d  d_postnr             158    167  0
     d  d_art                168    169  0
     d  d_tekst              170    189
     d  d_belØp              190    201  2
     d  d_mengde             202    212  2
     d  d_valbel             213    225  3
     d  d_valkod             226    228
     d  d_kidnr              229    253
     d  d_journr             254    258
     d  d_knavn              259    293
     d  d_kadr1              294    328
     d  d_kadr2              329    363
     d  d_kadr3              364    398
     d  d_kpostnr            399    406
     d  d_kposted            407    433
     d*
     d rkunl1_kund     s                   like(rkkund)
     d w_firm          s                   like(sofirm)
     d w_aarr          s                   like(soaarr)
     d w_kund          s                   like(sokund)
     d w_binr          s                   like(sobinr)
     d w_avde          s                   like(bfdavd)
     d w_ponr          s              4
     d w_grns          s             11  2
     d*
     d w_lnavn         s                   like(rknavn)
     d w_ladr1         s                   like(rkgate)
     d w_ladr2         s                   like(rkadr2)
     d w_lposn         s              4
     d w_lposs         s                   like(rksted)
6.36 d rb00l1_gkod     s                   like(r00kod)
6.36 d w_type          s             10
6.37 d rb10l1_gkod     s                   like(raakod)
      * Key for oppslag på faktura-hode-register
      *
     c     sohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_aarr
     c                   kfld                    w_binr
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
6.36  * Key for posisjonering på koder (moms)
6.36  *
6.36 c     rb00l1_key    klist
6.36 c                   kfld                    w_firm
6.36 c                   kfld                    w_type
6.36 c                   kfld                    rb00l1_gkod
6.37  * Key for posisjonering på bilagskoder
6.37  *
6.37 c     rb10l1_key    klist
6.37 c                   kfld                    w_firm
6.37 c                   kfld                    rb10l1_gkod
      *
     c* Blanker felt
     c                   eval      d_dati  = *blanks
     c                   eval      d_fdati = *blanks
     c                   eval      d_knavn = *blanks
     c                   eval      d_kadr1 = *blanks
     c                   eval      d_kadr2 = *blanks
     c                   eval      d_kpostnr = *blanks
     c                   eval      d_kposted = *blanks
     c* Buntnr
     c                   eval      d_bunknr = bfbunt
     c* Bilagsnr
     c                   z-add     bfbiln        d_bilnr
6.37  *
6.37  * Sjekker om bilagstype skal konverteres
6.37 c*                  eval      xxbilk = bfbilk
6.37 c                   eval      rb10l1_gkod = bfbilk
6.37 c     rb10l1_key    chain     rb10l1
6.37 c                   if        %found
6.37 c*                  eval      xxbilk = rankod
6.37 c                   eval      bfbilk = rankod
6.37 c                   endif
      *
     c*Bilagsdato
     c                   move      bfbdat        d_dati
     c                   move      d_aa          d_aar
     c                   move      d_mn          d_mnd
     c                   move      d_dd          d_dag
     c                   move      d_aa          d_utaa
     c                   move      d_mn          d_utmn
     c                   move      d_dd          d_utda
     c                   move      d_utdato      d_bildto
     c                   move      d_aa          d_År
     c                   move      d_mn          d_per
      *
     c* Forfallsdato
     c                   move      bffdat        d_dati
     c                   move      d_aa          d_aar
     c                   move      d_mn          d_mnd
     c                   move      d_dd          d_dag
     c                   move      d_aa          d_utaa
     c                   move      d_mn          d_utmn
     c                   move      d_dd          d_utda
     c                   move      d_utdato      d_fordto
      *
     c* Debet konto
     c                   eval      d_debkto = bfdkto
      *
      *
     c* Debet reskontro ??????????  + prosjekt
     c                   if        bfdkto <> *zero
     c                   z-add     bfkund        d_debrkto
     c                   if        bfproj <> *zero
     c                   movel     bfproj        d_debprosj
     c                   else
     c                   movel     *blanks       d_debprosj
     c                   endif
     c                   movel     *blanks       d_kreprosj
     c                   eval      d_krerkto = *zeros
     c                   endif
      *
      *
     c* Debet mva kode
     c                   eval      d_debmva = bfdmom
6.36  * Sjekker om momskode  skal konverteres
6.36 c                   eval      w_type      = 'MVAKODE'
6.36 c                   move      d_debmva      rb00l1_gkod
6.36 c     rb00l1_key    chain     rb00l1
6.36 c                   if        %found
6.36 c*                  movel     r00nko        d_debmva
6.36 c                   move      r00nko        d_debmva
6.36 c                   endif
      *
     c* Debet avdeling
     c                   if        bfdavd <> *zeros
     c                   movel     bfdavd        d_debavd
     c                   else
     c                   eval      d_debavd = *blanks
     c                   endif
      *
     c* Debet prosjekt  ??????????
      *
     c* Debet dim1      ??????????
      *
     c* Debet dim2      ??????????
      *
     c* Kredit konto
     c                   eval      d_krekto = bfckto
      *
     c* Kredit reskontro??????????+ prosjekt
     c                   if        bfckto <> *zero
     c                   z-add     bfkund        d_debrkto
     c                   if        bfproj <> *zero
     c                   movel     bfproj        d_debprosj
     c                   else
     c                   movel     *blanks       d_debprosj
     c                   endif
     c                   movel     *blanks       d_kreprosj
     c                   eval      d_krerkto = *zeros
     c                   endif
      *
      *
     c* Kredit mva.kode
     c                   eval      d_kremva = bfcmom
6.36  * Sjekker om momskode  skal konverteres
6.36 c                   eval      w_type      = 'MVAKODE'
6.36 c                   move      d_kremva      rb00l1_gkod
6.36 c     rb00l1_key    chain     rb00l1
6.36 c                   if        %found
6.36 c*                  movel     r00nko        d_kremva
6.36 c                   move      r00nko        d_kremva
6.36 c                   endif
      *
     c* Kredit avdeling
     c                   if        bfcavd <> *zeros
     c                   movel     bfcavd        d_kreavd
     c                   else
     c                   eval      d_kreavd = *blanks
     c                   endif
      *
     c* Kredit prosjekt ??????????
      *
     c* Kredit dim1     ??????????
      *
     c* Kredit dim2     ??????????
      *
     c* Postnr/Faktnr   ??????????
      *
     c* Art
     c                   eval      d_art = bfbilk
      *
     c* Tekst
     c                   move      bftext        d_tekst
      *
     c* Beløp
     c                   z-add     bfbelp        d_belØp
     c* Mengde
     c                   z-add     bfmngd        d_mengde
      *
     c* Valutabeløp
     c                   z-add     bfvalb        d_valbel
      *
     c* Valutakode
     c                   eval      d_valkod = bfvalk
      *
     c* Journalnr(ikke i bruk)
      *
      * Finner data fra faktura-hode og kundereg
     c                   exsr      control_init
     c* Kid
     c                   eval      d_kidnr = sokidd
      *
6.12 c*
6.12 c                   if        bfdkto > 9999
6.12 c                   eval      rkunl1_kund = bfdkto
6.12 c     rkunl1_key    chain     rkunl1                             90
     c                   if        *in90 = '0'
     c                   movel     rknavn        d_knavn
     c                   movel     rkgate        d_kadr1
     c                   movel     rkadr2        d_kadr2
     c                   movel     rkponr        d_kpostnr
     c                   movel     rksted        d_kposted
6.12 c                   endif
6.12 c                   else
6.12 c                   if        bfckto > 9999
6.12 c                   eval      rkunl1_kund = bfckto
6.12 c     rkunl1_key    chain     rkunl1                             90
     c                   if        *in90 = '0'
     c                   movel     rknavn        d_knavn
     c                   movel     rkgate        d_kadr1
     c                   movel     rkadr2        d_kadr2
     c                   movel     rkponr        d_kpostnr
     c                   movel     rksted        d_kposted
6.12 c                   endif
6.12 c                   endif
6.12 c                   endif
     c*
     c*
     c*
6.11 c*
     c* Flytter felter og skriver data
     c                   move      d_inpu        rzdata
      *
     c                   write     rz02pfr
      *
      *
     c     dd999         tag
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     control_init  begsr
      *
     c                   eval      w_firm = bffirm
     c                   eval      w_binr = bfbiln
     c                   move      d_aa          w_aarr
      * Finner siste faktura-hode for faktura
      *
     c     sohel1_key    setll     sohel1
     c     sohel1_key    reade     sohel1                                 90
     c                   if        *in90 = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   dow       *in90 = *off
     c     sohel1_key    reade     sohel1                                 90
     c                   enddo
      *
     c     avslutt       tag
      * Henter lev.adresse dersom fyllt ut
     c                   if        solkun  <> *zeros
     c                   eval      rkunl1_kund = solkun
     c     rkunl1_key    chain     rkunl1                             90
     c                   eval      w_lnavn     = rknavn
     c                   eval      w_ladr1     = rkgate
     c                   eval      w_ladr2     = rkadr2
     c                   move      rkponr        w_lposn
     c                   eval      w_lposs     = rksted
     c                   endif
      * Henter kundenavn
      *
     c                   eval      rkunl1_kund = bfkund
     c     rkunl1_key    chain     rkunl1                             90
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
