     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : JO750R
      * Beskrivelse . . : Matching, Reversering av oppdatering - parameter
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       010398 HKO  Nytt program
      *       090698 HKO  Kaller JO751R med varenummer med riktig lengde
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjmatl7    if   e           k disk    rename(jmatpfr:jmatl7r)
      *
     fjo750d    cf   e             workstn
      *
      * Definering av Local Data Area (LDA)
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variable i nøkkler
      *
     d jmatl7_var2     s                   like(jovar2)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(jofirm)
     d w_var2          s                   like(jovar2)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Behandler bildet
      *
     c     b1tag         tag
      *
     c                   exfmt     b1bld
      *
      * Behandler standard funksjonstaster
      *
     c                   if        *inkc = *on or
     c                             *inkl = *on
     c                   goto      avslutt
     c                   endif
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   eval      b_feil = *off
     c                   goto      b1tag
     c                   endif
      *
      * Kaller reverseringsprogram
      *
     c                   eval      w_var2 = b1var2
      *
     c                   call      'JO751R'
     c                   parm                    w_firm
     c                   parm                    w_var2
      *
     c                   eval      b1var2 = *blank
      *
     c                   goto      b1tag
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
     c                   if        b1var2 = *blank
     c                   eval      *in31 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c                   eval      jmatl7_var2 = b1var2
     c     jmatl7_key    chain     jmatl7                             90
     c                   if        *in90 = *on
     c                   eval      *in32 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c                   if        jovkod = 0
     c                   eval      *in33 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på matchet-vare
      *
     c     jmatl7_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jmatl7_var2
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
