     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LI304R
      *  Beskrivelse . . : Behandling av bestilling, resting og sort.mm
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      *  --------- ------  ------------------------------------------------
      * R5.21 200803 AMD  Lagt om til AP600R i stedet for AP100R
5.60  * R5.60 191107 AMD  Dette program oppdaterte memosaldo for kunde-
      *                   ordre ?  Dette er fjernet fra programmet
      *  6.10  180609 BSA  Oppdatert med sporingsinformasjon
      *  6.10  220909 ASK  Nye parametre til VL001R
pdf   *  6.10  250610 BSA  Fax skal ikke lenger ha eget program. Alt gjøres
pdf   *                    i LO920R.
6.11  *  6.11  010910 BSA  Må sjekke at PDF print er aktivert for bestillinger
6.nu  *  6.30  050614 bof  endring i forb. nummerutvidelse
6.31  *  6.30  050914 bof  Ta vare på loggkode på suffixer
6.32  *  6.30  040615 bkv  Bestilling økt fra 6 til 8
6.nu  *  6.30  210218 bsa  Endring i forb. nummerutvidelse
6.33  *  6.30  041218 ask  Oppdatering av gjennomsnittlig kostpris
      *
8.01  *  800   271022 bsa  Slette oppføring i tilleggsregister
8.01  *  800               Oppretter oppføring i tilleggsregister
8.01  *  800               Brukes ved utsendelse av EDI bestilling fra
8.01  *  800               ny EDI modul (NGN).
8.02  *  800   080223 bsa  Må hente kvantum fra plukkregister, uavhengig av
8.02  *  800               om det skal restes eller ei.
8.03  *  800   100223 bsa  Feil ooppdatering av lager.
8.04  *  800   080623 bsa  Ny logikk vedrørende resting av ordre.
8.04  *  800               Om det er restkode på leverandør - rest
8.04  *  800               Om det ikke er restkode på leverandør, og alle kolli er
8.04  *  800               scannet - ikke rest. Da kommer det ikke en pakkseddel
8.04  *  800               til, for leverandøren har ikke rest.
8.04  *  800               Ny logikk vedrørende resting av ordre.
8.04  *  800               Om det ikke er restordre på leverandør, men ikke alle
8.04  *  800               kolli er scannet, så skal ikke linjene ryddes vekk,
8.04  *  800               for da er ikke hele varemottaket gjort
8.04  *  800               (det er ikke rest, det er delvis mottatt forsendelse)
8.05  *K0000   280824 bsa  Ny metode for innhenting av suffiks. Skal ikke
8.05  *K0000               gjenbruke suffiks. Ref salgsordre.
8.06  *K0000   030924 bsa  Hvis nytt suffiks oppdater tilhørende salgsordre.
8.06  *K0000               NB Dette er langt fra vanntett, da det er utfordinger
8.06  *K0000               rundt delleveranser som ikke løses.
      *
      *
9.01  *  9.01  130921 ask  Midlertidig fjernet oppdatering av gj.sn.kostpris
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     FLPM2LU    IP   E           K DISK    RENAME(LPM2PFR:LPM2LUR)
     Frlevl1    IF   E           K DISK    RENAME(RLEVPFR:rlevl1R)
     FVOTYL1    IF   E           K DISK    RENAME(VOTYPFR:VOTYL1R)
     FFODTLU    UF   E           K DISK    RENAME(FODTPFR:FODTLUR)
     FLSTSL1    IF   E           K DISK    RENAME(LSTSPFR:LSTSL1R)
     FLOHELU    UF A E           K DISK    RENAME(LOHEPFR:LOHELUR)
     FLOHEL1    IF   E           K DISK    RENAME(LOHEPFR:LOHEL1R)
     FLODTLU    UF A E           K DISK    RENAME(LODTPFR:LODTLUR)
8.02 flodtlr    if   e           k disk    rename(lodtpfr:lodtlrr)
8.02 f                                     prefix(xx:2)
     FLODTL1    IF   E           K DISK    RENAME(LODTPFR:LODTL1R)
     FLOTXL1    IF   E           K DISK    RENAME(LOTXPFR:LOTXL1R)
     FLPLOL1    IF   E           K DISK    RENAME(LPLOPFR:LPLOL1R)
     FLPLOLU    UF   E           K DISK    RENAME(LPLOPFR:LPLOLUR)
     FLPLLLU    UF   E           K DISK    RENAME(LPLLPFR:LPLLLUR)
     FLPSOLU    UF A E           K DISK    RENAME(LPSOPFR:LPSOLUR)
8.01 flotii2    uf a e           k disk    rename(lotist:lotii2r)
8.04 flblii4    if   e           k disk    rename(lblist:lblii4r)
8.01 ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
      *
      *
8.01 d lotii2_tnum     s                   like(lotnum)
8.01 d lotii2_tsuf     s                   like(lotsuf)
8.01 d fohel1_numm     s                   like(fonumm)
8.01 d fohel1_suff     s                   like(fosuff)
8.04 d lblii4_lnum     s                   like(pblnum)
8.04 d lblii4_lsuf     s                   like(pblsuf)
8.04 d lblii4_llin     s                   like(pbllin)
8.04 d lblii4_laar     s                   like(pblaar)
      *
      * Definering av LDA
      *
     D LDA            UDS
     D  DSALT1               540    540  0
     D  DSALT2               541    541  0
     D  DSPERI               561    566  0
     D  DSDATO               567    576d   datfmt(*iso)
      *
     D  l_ruti               800    809
     D  l_outq               810    819
     D  l_acpi               830    833  0
     D  l_chgv               844    844
     D  l_prog               871    880
6.31 D  l_wsid               901    910
     d l_user                911    920
      *
      * Datastruktur for lageroppdatering
      *
     d                 ds
6.nu d hlrec                        128
     d  hlvare                       15    overlay(hlrec)
     d  hllage                        2  0 overlay(hlrec:16)
     d  hldato                         d   overlay(hlrec:18) datfmt(*iso)
     d  hltime                         t   overlay(hlrec:28) timfmt(*iso)
     d  hlotyp                        2    overlay(hlrec:36)
     d  hlltyp                        2    overlay(hlrec:38)
     d  hlanta                       11  3 overlay(hlrec:40)
     d  hlkopr                        9  2 overlay(hlrec:51)
     d  hlrefr                       20    overlay(hlrec:60)
     d  hlsyst                        1    overlay(hlrec:80)
     d  hlaltk                        2    overlay(hlrec:81)
     d  hlrest                        1    overlay(hlrec:83)
     d  hltype                        1    overlay(hlrec:84)
     d  hlenhe                        3    overlay(hlrec:85)
     d  hlinlr                        1    overlay(hlrec:88)
     d  hllety                        1    overlay(hlrec:89)
6.nu d  hlonum                        8  0 overlay(hlrec:90)
6.nu d  hlolin                        4  0 overlay(hlrec:98)
      *
     D                 DS
      *                                                      Sortering
     D  SORTER                 1     68
     D  SOR001                 1      8  0
     D  SOR002                 9     38
     D  SOR003                39     41
     D  SOR004                42     58
6.nu D  SOR005                59     66  0
     D  SOR006                67     68  0
6.31  *
6.31  * Parameter logg
6.31  *
6.31 d                 ds
6.31 d d_urec                        80
6.31 d  d_firu                        3  0 overlay(d_urec)
6.31 d  d_numm                        8  0 overlay(d_urec:4)
6.31 d  d_suff                        2  0 overlay(d_urec:12)
6.31 d  d_kodu                        1    overlay(d_urec:14)
6.31 d  d_date                         d   overlay(d_urec:15) datfmt(*iso)
6.31 d  d_time                         t   overlay(d_urec:25) timfmt(*iso)
6.31 d  d_user                       10    overlay(d_urec:33)
6.31 d  d_wsid                       10    overlay(d_urec:43)
      *
      * Definering av variable
      *
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
5.21 d w_in03          s              1
5.21 d w_tvbt          s              1
5.21 d w_btch          s              1
6.31 d w_logk          s              1
6.31 d w_urec          s             80
6.31 d w_stat          s              1
6.33 d w_disp          s              1
6.33 d w_tilg          s              1
6.33 d w_modu          s             10
8.05 d w_suff          s                   like(losuff)
      *
     d w_firm          s                   like(lk2fir)
     d w_oakk          s                   like(vaoakk)
6.32 d w_numa          s              8
5.02 d w_lina          s              4
8.04 d w_aarr          s              4  0
6.11 d p_pdf           s              1
6.11 d p_ruti          s             10
      *
6.11 d b_pdf           s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Beskrivelse av spesielle felter som er benyttet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  WWTEST  Benyttes til og sjekkke om det er linjer igjen
      *  (0,1)   på bestillingen etter at den er restet. Hvis det ikke
      *          er varelinjer igjen på bestilligen slettes heading-record
      *
      *
      *  Hent opplysninger fra LAGER-KODE-REGISTER
      *
     C                   MOVE      LK2FIR        STSFIR
     C     STSKEY        CHAIN     LSTSL1R                            90
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Utplukk av bestillinger til utskrift og oppdatering
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Legger inn verdier fra PLUKK-2-LINJE-REGISTER (Parametre)
      *  Dersom ett bestillingsnummer er valgt vil fra/til være like
      *
     C                   MOVE      LK2FIR        PLOFIR
     C                   MOVE      LK2ONR        PLONUM
     C                   MOVE      LK2SUF        PLOSUF
     C                   MOVE      LK2ONR        SLTONR
     C                   MOVE      LK2SUF        SLTSUF
      *
6.nu  *  Legger inn 99999999/99 som sluttverdi
      *  dersom sluttverdi ikke er utfylt
      *
     C     SLTONR        IFEQ      *ZERO
6.nu C                   MOVE      99999999      SLTONR
     C                   MOVE      99            SLTSUF
     C                   END
      *
      *  Setter start for lesing i UTPLUKK-PARAM-REGISTER
      *
     C     PLOKEY        SETLL     LPLOL1R
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Behandling av utplukk
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Leser record fra UTPLUKK-PARAM-REGISTER
      *
     C     XSTART        TAG
     C                   READ      LPLOL1R                                90
     C     *IN90         CABEQ     *ON           XSLUTT
      *
      *  Avslutter dersom bestillingsnummer lest er større en testverdi
      *
     C     LKPONR        CABGT     SLTONR        XSLUTT
      *
      *  Avslutter dersom bestillingsnummer og suffix er for stort
      *
     C     LKPONR        IFEQ      SLTONR
     C     LKPSUF        CABGT     SLTSUF        XSLUTT
     C                   END
      *
      *  Sjekk, ordretype plukket er lik ordretype fra parameter
      *
     C     LKPOTY        CABNE     LK2OTY        XSTART
      *
      *  Sjekk, Dersom bestillinger bare fra denne skjerm
      *
     C     LK2VG1        IFEQ      1
     C     LKPWSD        CABNE     LK2WSD        XSTART
     C                   END
      *
      *  Sjekk, Dersom bestillinger bare for denne bruker
      *
     C     LK2VG2        IFEQ      1
     C     LKPUSR        CABNE     LK2USR        XSTART
     C                   END
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Ett bestillingsnummer er funnet som skal plukkes ut
      *  Dette bestillingsnummer skal behandles helt ferdig før neste
      *  bestillingsnummer hentes fra UTPLUKK-PARAM-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Legger inn firmanummer i nøkklene
      *
     C                   MOVE      LKPFIR        LEVFIR
     C                   MOVE      LKPFIR        OTYFIR
     C                   MOVE      LKPFIR        OLIFIR
     C                   MOVE      LKPFIR        HEDFIR
     C                   MOVE      LKPFIR        LINFIR
     C                   MOVE      LKPFIR        TXTFIR
     C                   MOVE      LKPFIR        PLOFIR
     C                   MOVE      LKPFIR        PLLFIR
     C                   MOVE      LKPFIR        PSOFIR
      *
      *  Legger inn bestillingsnummer og suffix i nøkklene
      *
     C                   MOVE      LKPONR        HEDNUM
     C                   MOVE      LKPSUF        HEDSUF
      *
     C                   MOVE      LKPONR        LINNUM
     C                   MOVE      LKPSUF        LINSUF
     C                   MOVE      *ZERO         LINLIN
      *
     C                   MOVE      LKPONR        TXTNUM
     C                   MOVE      LKPSUF        TXTSUF
     C                   MOVE      *ZERO         TXTLIN
      *
     C                   MOVE      LKPONR        PLONUM
     C                   MOVE      LKPSUF        PLOSUF
      *
     C                   MOVE      LKPONR        PLLNUM
     C                   MOVE      LKPSUF        PLLSUF
     C                   MOVE      *ZERO         PLLLIN
      *
     C                   MOVE      LKPONR        PSONUM
     C                   MOVE      LKPSUF        PSOSUF
      *
      *  Legger inn ordretype og alternativ i PLUKK-S-LINJE-felter
      *
     C                   MOVEL     LKPOTY        LKSOTY
     C                   MOVEL     LKPALT        LKSALT
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Bygger opp standardverdier for denne utskriften
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Hent opplysninger fra ORDRETYPE-REGISTER
      *
     C                   MOVE      LK2OTY        OTYTYP
     C     OTYKEY        CHAIN     VOTYL1R                            90
     C                   MOVE      VAORUT        WPRUTI
     C                   MOVE      VAOAKK        WPAKKI
     c                   eval      w_oakk = vaoakk
      *
      *  Legg inn verdier for henting av utskrifts-opplysninger
      *
     C                   MOVE      *BLANK        l_chgv
     C                   MOVE      WPRUTI        l_ruti
6.31  *
6.31  *  Henter siste logg-kode
6.31  *
6.31 c                   call      'LM701R'
6.31 c                   parm                    HEDNUM
6.31 c                   parm                    HEDSUF
6.31 c                   parm                    w_logk
      *
      *  Oppdaterer LDA
      *
     C                   OUT       *DTAARA
      *
      *  Kaller opp program før utskrift
      *
5.21 c                   call      'AP600R'
5.21 c                   PARM                    l_ruti
5.21 c                   PARM                    l_chgv
5.21 c                   PARM                    w_tvbt
5.21 c                   PARM                    w_in03
5.21 c                   PARM                    w_btch
      *
      *  Henter inn igjen LDA
      *
     C                   IN        *DTAARA
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Behandling av de enkelte utskrifter/formularer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Legger inn verdi for at bestilling/bestillingslinjer er valgt
      *
     C                   MOVE      1             DSALT1
      *
      *  Dersom alternativ til standard for dokumenter
      *  legg inn riktig program i local
      *
      *
      *  Dersom outq er overstyrt legg den inn til bruk
      *
     C     LK2UTQ        IFNE      *BLANK
     C                   MOVE      LK2UTQ        l_outq
     C                   END
      *
      *  Oppdaterer LDA
      *
     C                   OUT       *DTAARA
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Arbeider med opplysninger i tilknyttning til denne bestilling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Hent opplysninger fra BESTILLING-HODE-REGISTER
      *
     C     HEDKEY        CHAIN     LOHEL1R                            90
     C                   MOVE      LOOTYP        WOOTYP
     C                   MOVE      LOLOPP        WOLOPP
      *
      *   Legger inn sorteringsbegrep fra bestillings-register
      *
     C                   MOVE      *BLANK        SORTER
     C                   MOVE      *ZERO         SOR001
     C                   MOVE      *ZERO         SOR005
     C                   MOVE      *ZERO         SOR006
      *
     C                   MOVE      LOLDOR        SOR001
     C                   MOVE      LONAVN        SOR002
     C                   MOVE      LKPONR        SOR005
     C                   MOVE      LKPSUF        SOR006
      *
      *  Oppsamling av flere bestillinger på samme blankett
      *
     C     VAOSAM        IFEQ      1
      *
     C                   MOVE      *ZERO         SOR005
     C                   MOVE      *ZERO         SOR006
      *
      *  Overstyring av samle flere på samme blankett
      *
     C     VAOAKK        IFEQ      3
      *
      *  Hent opplysninger fra LEVERANDØR-REGISTER
      *
     C                   MOVE      LOLDOR        LEVNUM
     C     LEVKEY        CHAIN     rlevl1                             90
      *
     C                   MOVE      LKPONR        SOR001
     C                   MOVE      LKPSUF        SOR002
      *
     C                   END
     C                   END
      *
      *  Legger inn forsjellig valuta på forsjellige formularer
      *
     C                   MOVE      LOVALK        SOR003
      *
      *  Legger over hele sorteringsbegrepet til register-feltet
      *
     C                   MOVEL     SORTER        LKSSRT
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Arbeider med henting av heading/linjer/tekster og resting
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      *  Hent opplysninger fra ORDRETYPE-REGISTER
      *
     C                   MOVE      LOOTYP        OTYTYP
     C     OTYKEY        CHAIN     VOTYL1R                            90
      *
      *  Hopp til plukk linjer dersom dette bare er utskrift
      *
     C     LOOTYP        CABEQ     LK2OTY        TAG200
      *
      *  Legger inn stanard suffix dersom ikke resting av linjer
      *
     C                   MOVE      HEDSUF        REDSUF
      *
      *  Nullstiller hjelpevariable for test på om restbestilling benyttes
      *
     C                   MOVE      *ZERO         WWTEST
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Behandling av resting    Danner nye linjer og heading
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.04  * Her kommer ny restelogikk inn.
8.04  * - Hvis det ligger igjen kolli på pakkseddelen som ikke er plukket,
8.04  *   skal det uansett restes.
8.04  *
8.04 c                   eval      lblii4_lnum = lkponr
8.04 c                   eval      lblii4_lsuf = 0
8.04 c                   eval      lblii4_llin = 0
8.04 c                   eval      lblii4_laar = w_aarr - 1
8.04 c     lblii4_key    setll     lblii4
8.04 c     lblii4_key2   reade     lblii4
8.04 c                   dow       not %eof
8.04 c                   if        pblsta = *blanks
8.04 c                   eval      lkpres = 1
8.04 c                   endif
8.04 c     lblii4_key2   reade     lblii4
8.04 c                   enddo
      *
      *  Sjekk, Resting av bestilling, utfør dette først
      *
     C     LKPRES        IFNE      *ZERO
      *
      *  Kaller opp subrutine for henting av neste suffix
      *
     C                   EXSR      HNTSUF
      *
      *  Kaller opp subrutiner for behandling av resting
      *
     C                   EXSR      OLREST
      *
      *  Danner ny bestillingsheading/sletter eventuellt gammel
      *
     C     HEDKEY        CHAIN     LOHEL1R                            90
      *
      *  Det finnes ikke linjer som er restet igjen så slett bestilling
      *
     C     WWTEST        IFEQ      *ZERO
     C     HEDKEY        CHAIN     LOHELUR                            90
8.01 c                   if        %found
     C                   DELETE    LOHELUR
8.01 c                   endif
8.01 c     hedkey        chain     lotii2
8.01 c                   if        %found
8.01 c                   delete    lotii2r
8.01 c                   endif
     C                   END
      *
      *  Danner ny bestilling med ny suffix
      *
     C                   CALL      'LO709R'
     C                   PARM                    HEDNUM
     C                   PARM                    REDSUF
      *
     C     REDKEY        CHAIN     LOHELUR                            90
     C     *IN90         IFEQ      *ON
     C                   MOVE      REDSUF        LOSUFF
     C                   MOVE      3             LOKODE
6.10 c                   time                    lodate
6.10 c                   time                    lotime
6.10 c                   eval      louser = l_user
6.10 c                   time                    loedat
6.10 c                   time                    loetim
6.10 c                   eval      loeusr = l_user
     C                   WRITE     LOHELUR
8.01  *
8.01  * Legg ut post på tilleggsregister
8.01  *
8.01 c                   eval      lotii2_tnum = lonumm
8.01 c                   eval      lotii2_tsuf = losuff
8.01 c     lotii2_key    chain     lotii2r
8.01 c                   if        not %found
8.01  *
8.01 c                   if        loornr <> 0
8.01 c                   eval      fohel1_numm = loornr
8.01 c                   eval      fohel1_suff = loorsu
8.01 c     fohel1_key    chain     fohel1
8.01 c                   if        %found(fohel1)
8.01 c                   unlock    lotii2
8.01 c                   if        folety = 1
8.01 c                   eval      lotko2 = 'B'
8.01 c                   else
8.01 c                   eval      lotko2 = 'L'
8.01 c                   endif
8.01 c                   else
8.01 c                   eval      lotko2 = 'L'
8.01 c                   endif
8.01 c                   else
8.01 c                   eval      lotko2 = 'L'
8.01 c                   endif
8.01  *
8.01 c                   eval      lotfir = w_firm
8.01 c                   eval      lotnum = lonumm
8.01 c                   eval      lotsuf = losuff
8.01  *
8.01 c                   time                    loteda
8.01 c                   time                    loteti
8.01 c                   eval      loteus = l_user
8.01  *
8.01 c                   write     lotii2r
8.01 c                   endif
     C                   END
      *
6.31  * legger på loggkode på suffix
6.31 c                   exsr      best_logg
8.02  *
8.02  * Det skal ikke restes, men må fortsatt forholde seg til antall plukket
8.02  *
8.02 c                   else
8.02 c                   exsr      rst_plukk
      *
     C                   END
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  SPESIAL FOR LAGER :  behandling av bestilling som ikke restes
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.02  * Er håndtert i ny subrutine - ref 8.02
8.02 c                   if        1=2
     C     LAALAG        IFNE      *ZERO
     C     WOLOPP        IFEQ      *ZERO
     C     LKPRES        IFEQ      *ZERO
     C                   EXSR      LAGLAG
     C                   END
     C                   END
     C                   END
8.02 c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Etterbehandling av "gammel" bestilling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Hopper til etterbehandling ny bestilling dersom ikke
      *  Heading finnes på forrige /fra) bestilling
      *
      *  Trekker ut hele beløpet fra bestilling dersom ikke restlinjer
      *
     C     WWTEST        IFEQ      *ZERO
      *
      *  Beregner justeringsbeløp
      *
5.60  ***> LOTOTR        SUB       LOTOTI        WPSALD
      *
      *  Kaller opp subrutine for oppdatering av memosaldo
      *
5.60  ***>               CALL      'FA922R'
5.60  ***>               PARM                    LK2FIR
5.60  ***>               PARM                    LOOTYP
5.60  ***>               PARM                    LOLDOR
5.60  ***>               PARM                    WPSALD
      *
     C                   GOTO      TAG100
     C                   END
      *
      *  Beregner nye summer for "GAMMEL" bestilling
      *
     C                   MOVE      *BLANK        WPENOR
     C                   CALL      'LO730R'
     C                   PARM                    WPENOR
     C                   PARM                    HEDFIR
     C                   PARM                    HEDNUM
     C                   PARM                    HEDSUF
      *
      *  Oppdaterer bestillingen slik at den kan endres etc.etc.
      *
     C     HEDKEY        CHAIN     LOHELUR                            90
      *
      *  Legger inn kode for at Beholdning er oppdatert
      *
     C     WPAKKI        IFEQ      2
     C     WPAKKI        OREQ      3
     C     WWTEST        IFEQ      *ZERO
     C                   Z-ADD     1             LOLOPP
     C                   ENDIF
     C                   ENDIF
      *
     C     *IN90         IFEQ      *OFF
     C                   MOVE      *ZERO         LOKODE
     C                   MOVE      *loval        LOKOTI
     C                   MOVE      *loval        LOKODA
     C                   MOVE      *BLANK        LOKOUS
     C                   MOVE      *BLANK        LOKOWS
6.10 c                   time                    loedat
6.10 c                   time                    loetim
6.10 c                   eval      loeusr = l_user
     C                   UPDATE    LOHELUR
     C                   END
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Etterbehandling av "ny" bestilling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     C     TAG100        TAG
      *
      *  Legger inn rett suffix etter plukking
      *
     C                   MOVE      REDSUF        LKPSUF
     C                   MOVE      REDSUF        HEDSUF
     C                   MOVE      REDSUF        LINSUF
     C                   MOVE      REDSUF        TXTSUF
     C                   MOVE      REDSUF        PSOSUF
      *
      *  Oppdaterer bestillingen med nye verdier fra utplukk
      *
     C     HEDKEY        CHAIN     LOHELUR                            90
      *
      *  Legger inn kode for at Beholdning er oppdatert
      *
     C     WPAKKI        IFEQ      2
     C     WPAKKI        OREQ      3
     C                   Z-ADD     1             LOLOPP
     C                   ENDIF
     C     *IN90         IFEQ      *OFF
     C                   MOVE      LKPOTY        LOOTYP
     C                   MOVE      LKPLAG        LOLAGE
     C                   MOVE      *ZERO         LOTOTI
     C                   MOVE      *ZERO         LOTOTR
     c                   if        w_oakk = 2
     c                   eval      lovida = w_date
     c                   endif
6.10 c                   time                    loedat
6.10 c                   time                    loetim
6.10 c                   eval      loeusr = l_user
     C                   UPDATE    LOHELUR
     C                   END
      *
      *  Beregner nye summer for "NY" bestilling
      *
     C                   MOVE      *BLANK        WPENOR
     C                   CALL      'LO730R'
     C                   PARM                    WPENOR
     C                   PARM                    HEDFIR
     C                   PARM                    HEDNUM
     C                   PARM                    REDSUF
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Etterbehandling av bestilling som skal skrives ut  (henting m.m
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Henter bestilling/-linjer fra valgt ordre til PLUKK-S-LINJE-REGISTER
      *
     C     TAG200        TAG
      *
     C                   EXSR      OHEAD
     C     LKPTXT        IFEQ      *ZERO
     C                   EXSR      FORAN
     C                   ENDIF
     C                   EXSR      LINJE
     C     LKPTXT        IFEQ      *ZERO
     C                   EXSR      ETTER
     C                   ENDIF
      *
      *  Sletter record fra UTPLUKK-PARAM-REGISTER
      *
     C     TAG300        TAG
      *
     C     PLOKEY        CHAIN     LPLOLUR                            90
     C     *IN90         IFEQ      *OFF
9.xx C                   DELETE    LPLOLUR
     C                   END
      *
6.33  *  Sjekker om glidende gjennomsnittlig kostpris skal oppdateres
6.33  *
6.33 c                   eval      w_modu = 'GJKOST'
6.33 c                   call      'AX700R'
6.33 c                   parm                    w_modu
6.33 c                   parm                    w_disp
6.33 c                   parm                    w_tilg
6.33  *
6.33 c                   if        w_tilg = '0'
9.01 c*                  call      'VG800R'
9.01 c*                  parm                    hednum
9.01 c*                  parm                    redsuf
6.33 c                   endif
6.33  *
     C                   GOTO      XSTART
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     C     XSLUTT        TAG
      *
     CLR                 RETURN
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *   Henter neste ledige suffix                                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     CSR   HNTSUF        BEGSR
8.05  *
8.05  * Hent høyeste brukte suffiks på ordrenummer
8.05  *
8.05 c                   eval      w_suff = 0
8.05 c                   call      'AS115R'
8.05 c                   parm                    hednum
8.05 c                   parm                    w_suff
      *
8.05 C*                  MOVE      49            REDSUF
8.05 C*    REDKEY        SETLL     LOHEL1R
      *
8.05 C*                  MOVE      *OFF          *IN90
8.05 C*                  READP     LOHEL1R                                90
      *
8.05 C*    LOSUFF        ADD       1             REDSUF
8.05 c                   eval      redsuf = w_suff
      *
     CSR                 ENDSR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Legger ut bestillings-linjer til resting
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Forklaring på hva som skjer i denne subrutine:
      *  ----------------------------------------------
      *
      *  I skjermbildet for utplukk finnes tre alternativer:
      *
      *    0 = Ingen spesifisering av linjer
      *    1 = Plukk bare linjer som oppgis
      *    2 = Plukk alle linjer, men unntak
      *        (Feltet for denne koden heter FKPRES)
      *
      *  Kode 0 kommer ikke innom denne rutine, bare kode 1 eller 2.
      *  Det settes lower limit på varelinjeregister (som inneholder varer
      *  og tekstlinjer) og det leses så lenge man befinner seg på samme
      *  firmanummer / ordrenummer / suffix.
      *  Samtidig gjøres det oppslag mot utplukksregister for å se om det
      *  skal gjøres endringer i forhold til opprinnelig varelinje.
      *
      *  Disse koder er tilgjengelige:
      *    1 = Plukkes
      *    3 = Kopieres                 (Kun tekstlinjer)
      *    4 = Holdes tilbake
      *    5 = Ingen resting av antall  (Kun varelinjer)
      *        (Feltet for denne koden heter FKLKOD)
      *
      *  Utplukks(linje)register inneholder derfor poster bare der hvor
      *  det er registrert enten et antall eller en kode for utplukk.
      *  Vær også oppmerksom på at selv om det ikke blir funnet en
      *  post i utplukksregisteret, kan det legges inn verdier i feltene
      *  FKLANT og FKLKOD
      *  F. eks. settes FKLKOD til 1 når hele ordren skal plukkes, men
      *  unntak (FKPRES=2)
      *
      *  Rutinen tar seg av restnotering, oppdatering av lager, kopiering/
      *  plukking av tekslinjer etc.  En ordre som kommer innom her vil
      *  alltid få et nytt suffix, mens resten blir liggende igjen på
      *  gammel ordre eller slettes dersom den plukkes tom.
      *
     CSR   OLREST        BEGSR
      *
     C                   MOVE      *ZERO         LINLIN
     C     LINKEY        SETLL     LODTL1R
      *
      *  Leser inn en record fra BESTILLING-LINJE-REGISTER
      *
     C     OLRES1        TAG
     C                   MOVE      *OFF          *IN90
     C                   READ      LODTL1R                                90
     C     *IN90         IFEQ      *OFF
     C     LDFIRM        IFEQ      LINFIR
     C     LDNUMM        IFEQ      LINNUM
     C     LDSUFF        IFEQ      LINSUF
      *
      *  Hent opplysninger fra UTPLUKK-LINJE-REGISTER (rest/Tekst-linje)
      *
     C                   Z-ADD     LDLINE        PLLLIN
     C     PLLKEY        CHAIN     LPLLLUR                            92
9.xx C**N92              DELETE    LPLLLUR
      *
      *  Dersom alle linjer skal plukkes, men unntak kan gjøres
      *  Legges hele antallet inn som antall plukket
      *  dersom linjen ikke finnes i BESTILLING-PLUKK-LINJE-REGISTER
      *
     C     *IN92         IFEQ      *ON
     C                   MOVE      *ZERO         LKLANT
     C                   MOVE      *BLANK        LKLKOD
      *
      *  Plukke alt, men unntak
      *
     C     LKPRES        IFEQ      2
     C                   Z-ADD     LDANTA        LKLANT
     C                   MOVE      '1'           LKLKOD
     C                   END
      *
     C                   END
      *
      *  Dersom det er oppgitt kode 1 for utplukk, og antall            .
      *  er lik 0 i plukkfilen, legges hele antallet over.              .
      *
     C     *IN92         IFEQ      *OFF
     C     LKLKOD        IFEQ      '1'
     C     LKLANT        IFEQ      *ZERO
     C                   Z-ADD     LDANTA        LKLANT
     C                   END
     C                   END
     C                   END
      *                                                                 .
      *  Dersom tekstlinjer, gå til eget avsnitt                        .
      *                                                                 .
     C     LDVARE        CABEQ     *BLANK        XTXLIN
      *                                                                 .
      *  Dersom antall levert=0 skal ikke ORDRE-LINJE-REGISTER oppdat.  .
      *                                                                 .
     C     LKLKOD        IFEQ      *BLANK
     C     LKLANT        IFEQ      *ZERO
     C                   MOVE      1             WWTEST
     C                   GOTO      OLRES1
     C                   END
     C                   END
      *                                                                 .
      *  Dersom kode 4 er oppgitt, skal varelinje vente,                .
      *  og ordrelinjeregister skal ikke oppdateres.                    .
      *                                                                 .
     C     LKLKOD        IFEQ      '4'
     C                   MOVE      1             WWTEST
     C                   GOTO      OLRES1
     C                   END
      *                                                                 .
      *  Danner ny linjer for RESTORDRE                                 .
      *  Legger ut nye linjer for "NY" ordre  ): NY SUFFIX              .
      *                                                                 .
     C                   Z-ADD     LDLINE        LINLIN
     C     RINKEY        CHAIN     LODTLUR                            94
     C     *IN94         IFEQ      *ON
     C                   MOVE      REDSUF        LDSUFF
     C                   Z-ADD     LKLANT        LDANTA
     C                   Z-ADD     LKPLAG        LDLAGE
     c                   eval      ldldat = w_date
6.10 c                   time                    ldodat
6.10 c                   time                    ldotim
6.10 c                   eval      ldousr = l_user
6.10 c                   time                    ldedat
6.10 c                   time                    ldetim
6.10 c                   eval      ldeusr = l_user
     C                   WRITE     LODTLUR
      *
8.06  * Oppdater tilhørende salgsordre
      *
8.06 c                   if        ldornu > 0
8.06 c                   eval      olinum = ldornu
8.06 c                   eval      olisuf = ldorsu
8.06 c                   eval      olilin = ldorli
8.06 c     olikey        chain     fodtlu
8.06 c                   if        %found
8.06 c                   eval      fdbsuf = ldsuff
8.06 c                   time                    fdedat
8.06 c                   time                    fdetim
8.06 c                   eval      fdeusr = l_user
8.06 c                   update    fodtlur
8.06 c                   endif
8.06 c                   endif
      *
      *  Kaller opp subrutine for oppdatering av lager
      *
     C     LAALAG        IFNE      *ZERO
     C     WOLOPP        IFEQ      *ZERO
     C                   MOVE      LKPOTY        LDOTYP
     C                   EXSR      OPPLAG
     C                   END
     C                   END
      *
     C                   END
      *
      *  Oppdaterer linjer for "GAMMEL" ordre
      *                                                                 .
     C     LINKEY        CHAIN     LODTLUR                            94
     C     *IN94         IFEQ      *OFF
      *                                                               . .
      *  Kaller opp subrutine for oppdatering av lager                . .
      *                                                               . .
     C     LAALAG        IFNE      *ZERO
     C     WOLOPP        IFEQ      *ZERO
     C                   MOVE      WOOTYP        LDOTYP
     C                   MULT      -1            LDANTA
     C                   EXSR      OPPLAG
     C                   MULT      -1            LDANTA
     C                   END
     C                   END
      *                                                               . .
      *  Beregn rest    (REST=BESTILT-LEVERT)                         . .
      *  Kode for og bevare tidligere linjeantall =0                  . .
      *                                                               . .
     C     VAOBOO        IFEQ      0
     C     LDANTA        SUB       LKLANT        WWREST
     C                   END
      *                                                               . .
      *  Det skal ikke restnoteres                                    . .
      *                                                               . .
     C     VAOBOO        IFEQ      1
     C                   Z-ADD     0             WWREST
     C                   END
      *                                                               . .
      *  Resten skal ikke beholdes, den skal slettes                  . .
      *                                                               . .
     C     LKLKOD        IFEQ      '5'
     C                   Z-ADD     0             WWREST
     C                   END
      *                                                               . .
      *  Behold alltid hele det opprinnelige antallet                 . .
      *                                                               . .
     C     VAOBOO        IFEQ      2
     C                   Z-ADD     LDANTA        WWREST
     C                   END
      *                                                               . .
      *  Behandler oppdatering av ordren utfra resultatet av restingen. .
      *                                                               . .
     C     WWREST        IFGT      0
     C                   MOVE      1             WWTEST
     C                   Z-ADD     WWREST        LDANTA
6.10 c                   time                    ldedat
6.10 c                   time                    ldetim
6.10 c                   eval      ldeusr = l_user
     C                   UPDATE    LODTLUR
     C                   ELSE
     C                   Z-ADD     0             LDANTA
     C                   DELETE    LODTLUR
     C                   END
      *                                                               . .
      *  Kaller opp subrutine for oppdatering av lager                . .
      *                                                               . .
     C     LAALAG        IFNE      *ZERO
     C     WOLOPP        IFEQ      *ZERO
     C                   MOVE      WOOTYP        LDOTYP
     C                   EXSR      OPPLAG
     C                   END
     C                   END
      *                                                               . .
     C                   END
      *                                                                 .
      *  Ferdig med behandling av varelinjer                            .
      *                                                                 .
     C                   GOTO      XNESTE
      *                                                                 .
      *  -----------------------------------                            .
      *  Behandling av tekstlinjer                                      .
      *                                                                 .
     C     XTXLIN        TAG
      *                                                                 .
      *  Gå ut med linjer som ikke skal behandles                       .
      *                                                                 .
     C     LKLKOD        IFEQ      *BLANK
     C                   MOVE      1             WWTEST
     C                   GOTO      XNESTE
     C                   END
      *                                                                 .
     C     LKLKOD        IFEQ      '4'
     C                   MOVE      1             WWTEST
     C                   GOTO      XNESTE
     C                   END
      *                                                                 .
      *  Legge ut tekstlinje på ny suffix                               .
      *                                                                 .
     C                   Z-ADD     LDLINE        LINLIN
     C     RINKEY        CHAIN     LODTLUR                            94
     C     *IN94         IFEQ      *ON
     C                   MOVE      REDSUF        LDSUFF
     C                   Z-ADD     0             LDANTA
6.10 c                   time                    ldodat
6.10 c                   time                    ldotim
6.10 c                   eval      ldousr = l_user
6.10 c                   time                    ldedat
6.10 c                   time                    ldetim
6.10 c                   eval      ldeusr = l_user
     C                   WRITE     LODTLUR
     C                   END
      *                                                                 .
      *  Dersom det er igjen linjer på "gammel ordre"                   .
      *  kan ikke ordrehode slettes (kopiering)                         .
      *                                                                 .
     C     LKLKOD        IFEQ      '3'
     C                   MOVE      1             WWTEST
     C                   GOTO      XNESTE
     C                   END
      *                                                                 .
      *  Slette eventuelle tekstlinjer fra "gammel ordre"               .
      *                                                                 .
     C     LINKEY        CHAIN     LODTLUR                            94
     C     *IN94         IFEQ      *OFF
     C                   DELETE    LODTLUR
     C                   END
      *                                                                 .
     C     XNESTE        TAG
      *                                                                 .
     C                   GOTO      OLRES1
     C                   END
     C                   END
     C                   END
     C                   END
      *
     CSR                 ENDSR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Oppdaterer PLUKK-S-LINJE-REGISTER med sorterings-felter m.m.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     CSR   OHEAD         BEGSR
      *
     C                   MOVE      *ZERO         PSOTLL
     C                   MOVE      *ZERO         PSOLIN
     C     PSOKEY        CHAIN     LPSOLUR                            92
      *
     C     *IN92         IFEQ      *OFF
     C                   UPDATE    LPSOLUR
     C                   ELSE
     C                   MOVE      LKPFIR        LKSFIR
     C                   MOVE      LKPONR        LKSONR
     C                   MOVE      LKPSUF        LKSSUF
     C                   MOVE      *ZERO         LKSLIN
     C                   MOVE      *ZERO         LKSTLL
     C                   MOVE      'H'           LKSKOD
     C                   WRITE     LPSOLUR
     C                   END
      *
      *
     CSR                 ENDSR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Henter ordre-linjer og legger ut til PLUKK-S-LINJE-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     CSR   LINJE         BEGSR
      *
     C                   MOVE      *ZERO         LINLIN
      *
     C     LINKEY        SETLL     LODTL1R
      *
     C     LINJE1        TAG
     C                   SETOFF                                           90
     C                   READ      LODTL1R                                90
     C     *IN90         IFEQ      *OFF
     C     LDFIRM        IFEQ      LINFIR
     C     LDNUMM        IFEQ      LINNUM
     C     LDSUFF        IFEQ      LINSUF
      *
      *  Kaller opp subrutine for oppdatering av lager
      *
     C     LAALAG        IFNE      *ZERO
     C     WOLOPP        IFEQ      *ZERO
     C     VAOAKK        IFEQ      3
     C     LOOTYP        IFEQ      LK2OTY
     C                   MOVE      WOOTYP        LDOTYP
     C                   EXSR      OPPLAG
     C                   END
     C                   END
     C                   END
     C                   END
      *
      *  Kaller opp subrutine for oppdatering av antall på ORDRE-LINJE-REGISTER
      *
     C                   EXSR      OPPORL
      *
     C                   Z-ADD     1             PSOTLL
     C                   Z-ADD     LDLINE        PSOLIN
     C     PSOKEY        CHAIN     LPSOLUR                            92
      *
     C     *IN92         IFEQ      *OFF
     C                   UPDATE    LPSOLUR
     C                   ELSE
     C                   Z-ADD     3             LKSTLL
     C                   MOVE      LDLINE        LKSLIN
     C                   MOVE      'V'           LKSKOD
     C                   WRITE     LPSOLUR
     C                   END
      *
     C                   GOTO      LINJE1
     C                   END
     C                   END
     C                   END
     C                   END
      *
     CSR                 ENDSR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Henter best.-tekst (FORAN) og legger ut til PLUKK-S-LINJE-REG.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     CSR   FORAN         BEGSR
      *
     C                   MOVE      *ZERO         TXTLIN
      *
     C     TXTKEY        SETLL     LOTXL1R
      *
     C     FORAN1        TAG
     C                   SETOFF                                           90
     C                   READ      LOTXL1R                                90
     C     *IN90         IFEQ      *OFF
     C     LTFIRM        IFEQ      TXTFIR
     C     LTNUMM        IFEQ      TXTNUM
     C     LTSUFF        IFEQ      TXTSUF
     C     LTLINE        IFLE      4999
      *
     C                   Z-ADD     2             PSOTLL
     C                   Z-ADD     LTLINE        PSOLIN
     C     PSOKEY        CHAIN     LPSOLUR                            92
      *
     C     *IN92         IFEQ      *OFF
     C                   UPDATE    LPSOLUR
     C                   ELSE
     C                   Z-ADD     2             LKSTLL
     C                   MOVE      LTLINE        LKSLIN
     C                   MOVE      'K'           LKSKOD
     C                   WRITE     LPSOLUR
     C                   END
      *
     C                   GOTO      FORAN1
     C                   END
     C                   END
     C                   END
     C                   END
     C                   END
      *
     CSR                 ENDSR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Henter best.-tekst (ETTER) og legger ut til PLUKK-S-LINJE-REG.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     CSR   ETTER         BEGSR
      *
     C                   MOVE      5000          TXTLIN
      *
     C     TXTKEY        SETLL     LOTXL1R
      *
     C     ETTER1        TAG
     C                   SETOFF                                           90
     C                   READ      LOTXL1R                                90
     C     *IN90         IFEQ      *OFF
     C     LTFIRM        IFEQ      TXTFIR
     C     LTNUMM        IFEQ      TXTNUM
     C     LTSUFF        IFEQ      TXTSUF
     C     LTLINE        IFLE      9999
      *
     C                   Z-ADD     4             PSOTLL
     C                   Z-ADD     LTLINE        PSOLIN
     C     PSOKEY        CHAIN     LPSOLUR                            92
      *
     C     *IN92         IFEQ      *OFF
     C                   UPDATE    LPSOLUR
     C                   ELSE
     C                   Z-ADD     4             LKSTLL
     C                   MOVE      LTLINE        LKSLIN
     C                   MOVE      'K'           LKSKOD
     C                   WRITE     LPSOLUR
     C                   END
      *
     C                   GOTO      ETTER1
     C                   END
     C                   END
     C                   END
     C                   END
     C                   END
      *
     CSR                 ENDSR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Leser igjenom bestillingslinjer for oppdatering av lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     CSR   LAGLAG        BEGSR
      *
     C                   MOVE      *ZERO         LINLIN
     C     LINKEY        SETLL     LODTL1R
      *
     C     LANJE1        TAG
     C                   SETOFF                                           90
     C                   READ      LODTL1R                                90
     C     *IN90         IFEQ      *OFF
     C     LDFIRM        IFEQ      LINFIR
     C     LDNUMM        IFEQ      LINNUM
     C     LDSUFF        IFEQ      LINSUF
      *
      *  Behandler fra antall på ordretype FRA
      *
     C                   MOVE      WOOTYP        LDOTYP
     C                   MULT      -1            LDANTA
     C                   EXSR      OPPLAG
     C                   MULT      -1            LDANTA
      *
      *  Behandler til antall på ordretype TIL
      *
     C                   MOVE      LKPOTY        LDOTYP
     C                   MOVE      LKPLAG        LDLAGE
     C                   EXSR      OPPLAG
      *
     C                   GOTO      LANJE1
     C                   END
     C                   END
     C                   END
     C                   END
      *
     CSR                 ENDSR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Oppdatering av antall på ORDRE-LINJE-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     CSR   OPPORL        BEGSR
      *
      *  Sjekk om ordrenummer finnes på recorden
      *
     C     LDORNU        IFNE      *ZERO
      *
     C                   MOVE      LDORNU        OLINUM
     C                   MOVE      LDORSU        OLISUF
     C                   MOVE      LDORLI        OLILIN
     C     OLIKEY        CHAIN     FODTLUR                            93
      *
      *  Oppdaterer ordren med antall innkommet på pakk/fakt.
      *
     C     WPAKKI        IFEQ      2
     C     WPAKKI        OREQ      3
     C                   Z-ADD     LDANTA        FDANTC
     C                   ENDIF
      *
     C     *IN93         IFEQ      *OFF
6.10 c                   time                    ldedat
6.10 c                   time                    ldetim
6.10 c                   eval      ldeusr = l_user
     C                   UPDATE    FODTLUR
     C                   ENDIF
      *
     C                   ENDIF
      *
     CSR                 ENDSR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Oppdatering av Lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     CSR   OPPLAG        BEGSR
      *
      *  Legger over verdier i overførings-felter
      *
     C                   MOVE      LDVARE        HLVARE
     C                   MOVE      LDLAGE        HLLAGE
     C                   MOVE      LDENH1        hlenhe
     C                   MOVE      *loval        HLDATO
     C                   MOVE      *loval        HLTIME
     C                   MOVE      LDOTYP        HLOTYP
     C                   MOVE      LDLTYP        HLLTYP
      *
     C                   Z-ADD     LDANTA        HLANTA
     C                   Z-ADD     LDKPNY        hlkopr
     c                   eval      hlonum = ldnumm
     c                   eval      hlolin = ldline
     C                   MOVE      'L'           HLSYST
     C                   MOVE      *BLANK        HLALTK
     C                   MOVE      *BLANK        HLREST
     c                   move      ldnumm        w_numa
     c                   move      ldline        w_lina
     c                   eval      hlrefr = 'B:' + w_numa +
     c                                      ' L:' + w_lina
     C                   MOVE      LDLETY        HLLETY
      *
      *  Kaller opp lageroppdaterings-program
      *
     C                   MOVE      HLREC         WLREC
      *
     C                   CALL      'VL001R'
     C                   PARM                    WV001R            1
6.nu C                   PARM                    WLREC           128
      *
     CSR                 ENDSR
      *
8.03  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.03  *  Oppdatering av Lager fra rst_plukk
8.03  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.03  *
8.03 c     x_opplag      BEGSR
8.03  *
8.03  *  Legger over verdier i overførings-felter
8.03  *
8.03 c                   move      xxvare        hlvare
8.03 c                   move      xxlage        hllage
8.03 c                   move      xxenh1        hlenhe
8.03 c                   move      *loval        hldato
8.03 c                   move      *loval        hltime
8.03 c                   move      xxotyp        hlotyp
8.03 c                   move      xxltyp        hlltyp
      *
8.03 c                   z-add     xxanta        hlanta
8.03 c                   z-add     xxkpny        hlkopr
8.03 c                   eval      hlonum = xxnumm
8.03 c                   eval      hlolin = xxline
8.03 c                   move      'L'           hlsyst
8.03 c                   move      *blank        hlaltk
8.03 c                   move      *blank        hlrest
8.03 c                   move      xxnumm        w_numa
8.03 c                   move      xxline        w_lina
8.03 c                   eval      hlrefr = 'B:' + w_numa +
8.03 c                                      ' L:' + w_lina
8.03 c                   move      xxlety        hllety
      *
      *  Kaller opp lageroppdaterings-program
      *
8.03 c                   move      hlrec         wlrec
      *
8.03 c                   call      'VL001R'
8.03 c                   parm                    wv001r            1
8.03 c                   parm                    wlrec           128
      *
8.03 c                   endsr
      *
6.11  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.11  *  Sjekk om PDF er aktivert
6.11  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.11 c     pdf_sjekk     begsr
6.11  *
6.11 c                   eval      b_pdf = *off
6.11 c                   eval      p_pdf ='0'
6.11 c                   eval      p_ruti= l_ruti
6.11  * Sjekk om PROA er aktivert
6.11 c                   call      'AP608R'
6.11 c                   parm                    p_ruti
6.11 c                   parm                    p_pdf
6.11  * p_pdf = '1' = PROA aktivert
6.11 c                   if        p_pdf = '1'
6.11 c                   eval      b_pdf = *on
6.11 c                   endif
6.11  *
6.11 csr                 endsr
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  *  Subrutine for å logge samme loggkode på suffix
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  *
6.31 c     best_logg     begsr
6.31  *
6.31 c                   eval      d_firu = w_firm
6.31 c                   eval      d_numm = hednum
6.31 c                   eval      d_suff = redsuf
6.31 c                   eval      d_kodu = w_logk
6.31 c                   eval      d_date = w_date
6.31 c                   eval      d_time = w_time
6.31 c                   eval      d_user = l_user
6.31 c                   eval      d_wsid = l_wsid
6.31 c                   eval      w_urec = d_urec
6.31  *
6.31 c                   call      'LM900R'
6.31 c                   parm                    w_stat
6.31 c                   parm                    w_urec
6.31  *
6.31 c     end_log       endsr
      *
8.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.02  *  Subrutine for å hente utplukk
8.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.02  *
8.02 c     rst_plukk     begsr
      *
8.02 c                   eval      linlin = *zeros
8.02 c     linkey        setll     lodtlrr
8.02 c     linkey2       reade     lodtlrr
8.02 c                   dow       not %eof
8.02  *  Dersom tekstlinjer, gå til eget avsnitt                        .
8.02  *                                                                 .
8.02 c                   if        xxvare <> *blanks
      *
      *  Hent opplysninger fra UTPLUKK-LINJE-REGISTER (rest/Tekst-linje)
      *
8.02 c                   eval      plllin = xxline
8.02 c     pllkey        chain     lplllur
8.02 c                   if        %found
8.03 c*                  delete    lplllur
8.02  *
8.02  *  Kaller opp subrutine for oppdatering av lager
8.02  *
8.02 c     laalag        ifne      *zero
8.02 c     wolopp        ifeq      *zero
8.03 c                   eval      xxotyp = wootyp
8.03 C                   mult      -1            xxanta
8.03 c                   exsr      x_opplag
8.02 c     lklant        ifne      *zero
8.03 c                   eval      xxanta = lklant
8.02 c                   endif
8.03 c                   eval      xxotyp = lkpoty
8.03 c**                 mult      -1            ldanta
8.03 c                   exsr      x_opplag
8.02 c                   endif
8.02 c                   endif
      *
      * Oppdater bestillingslinje med nytt kvantum
      *
8.02 c                   eval      linlin = xxline
8.02 c     linkey        chain     lodtlur
8.02 c                   if        %found
8.02 c                   eval      ldanta = xxanta
8.02 c                   time                    ldodat
8.02 c                   time                    ldotim
8.02 c                   eval      ldousr = l_user
8.02 c                   time                    ldedat
8.02 c                   time                    ldetim
8.02 c                   eval      ldeusr = l_user
8.02 c                   update    lodtlur
8.02 c                   endif
      *
8.03 c                   delete    lplllur
      *
8.02 c                   endif
      *
8.02 c                   endif
      *
8.02 c     linkey2       reade     lodtlrr
8.02 c                   enddo
      *                                                               . .
8.02 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     CSR   *INZSR        BEGSR
      *
      *  Definisjon av local data area for output
      *
     C     *DTAARA       DEFINE    *LDA          LDA
      *
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     C                   MOVE      *ZERO         WWTEST            1 0
     C                   MOVE      *ZERO         WPAKKI            1 0
      *
     C                   MOVE      *ZERO         WPSALD           11 2
      *
     C                   MOVE      *BLANK        WPENOR            1
     C                   MOVE      *BLANK        WPRUTI           10
      *
      *  Redefinering av felter
      *
     C     *LIKE         DEFINE    LDANTA        WWREST
     C     *LIKE         DEFINE    LOSUFF        REDSUF
     C     *LIKE         DEFINE    LOOTYP        WOOTYP
     C     *LIKE         DEFINE    LOLOPP        WOLOPP
      *
      *  Redefinering av nøkler
      *
     C     *LIKE         DEFINE    RLFIRM        LEVFIR
     C     *LIKE         DEFINE    RLLEVR        LEVNUM
      *
     C     *LIKE         DEFINE    VAOFIR        OTYFIR
     C     *LIKE         DEFINE    VAOTYP        OTYTYP
      *
     C     *LIKE         DEFINE    LAAFIR        STSFIR
      *
     C     *LIKE         DEFINE    LKPFIR        PLOFIR
     C     *LIKE         DEFINE    LKPONR        PLONUM
     C     *LIKE         DEFINE    LKPSUF        PLOSUF
     C     *LIKE         DEFINE    LKPONR        SLTONR
     C     *LIKE         DEFINE    LKPSUF        SLTSUF
      *
     C     *LIKE         DEFINE    LKSFIR        PSOFIR
     C     *LIKE         DEFINE    LKSONR        PSONUM
     C     *LIKE         DEFINE    LKSSUF        PSOSUF
     C     *LIKE         DEFINE    LKSTLL        PSOTLL
     C     *LIKE         DEFINE    LKSLIN        PSOLIN
      *
     C     *LIKE         DEFINE    FDFIRM        OLIFIR
     C     *LIKE         DEFINE    FDNUMM        OLINUM
     C     *LIKE         DEFINE    FDSUFF        OLISUF
     C     *LIKE         DEFINE    FDLINE        OLILIN
      *
     C     *LIKE         DEFINE    LOFIRM        HEDFIR
     C     *LIKE         DEFINE    LONUMM        HEDNUM
     C     *LIKE         DEFINE    LOSUFF        HEDSUF
      *
     C     *LIKE         DEFINE    LDFIRM        LINFIR
     C     *LIKE         DEFINE    LDNUMM        LINNUM
     C     *LIKE         DEFINE    LDSUFF        LINSUF
     C     *LIKE         DEFINE    LDLINE        LINLIN
      *
     C     *LIKE         DEFINE    LTFIRM        TXTFIR
     C     *LIKE         DEFINE    LTNUMM        TXTNUM
     C     *LIKE         DEFINE    LTSUFF        TXTSUF
     C     *LIKE         DEFINE    LTLINE        TXTLIN
      *
     C     *LIKE         DEFINE    LKLFIR        PLLFIR
     C     *LIKE         DEFINE    LKLONR        PLLNUM
     C     *LIKE         DEFINE    LKLSUF        PLLSUF
     C     *LIKE         DEFINE    LKLLIN        PLLLIN
      *
      *  Key for file : LEVERANDØR-REGISTER
      *
     C     LEVKEY        KLIST
     C                   KFLD                    LEVFIR
     C                   KFLD                    LEVNUM
      *
      *  Key for file : ORDRETYPE-REGISTER
      *
     C     OTYKEY        KLIST
     C                   KFLD                    OTYFIR
     C                   KFLD                    OTYTYP
      *
      *  Key for file : ORDRE-LINJE-REGISTER
      *
     C     OLIKEY        KLIST
     C                   KFLD                    OLIFIR
     C                   KFLD                    OLINUM
     C                   KFLD                    OLISUF
     C                   KFLD                    OLILIN
      *
      *  Key for file : LAGER-KODE-REGISTER
      *
     C     STSKEY        KLIST
     C                   KFLD                    STSFIR
      *
      *  Key for file : BESTILLING-HODE-REGISTER
      *
     C     HEDKEY        KLIST
     C                   KFLD                    HEDFIR
     C                   KFLD                    HEDNUM
     C                   KFLD                    HEDSUF
      *
      *  Key for file : BESTILLING-LINJE-REGISTER
      *
     C     LINKEY        KLIST
     C                   KFLD                    LINFIR
     C                   KFLD                    LINNUM
     C                   KFLD                    LINSUF
     C                   KFLD                    LINLIN
8.02  *
8.02  *  Key for file : BESTILLING-LINJE-REGISTER
8.02  *
8.02 c     linkey2       klist
8.02 c                   kfld                    linfir
8.02 c                   kfld                    linnum
8.02 c                   kfld                    linsuf
      *
      *  Key for file : BESTILLING-TEKST-REGISTER
      *
     C     TXTKEY        KLIST
     C                   KFLD                    TXTFIR
     C                   KFLD                    TXTNUM
     C                   KFLD                    TXTSUF
     C                   KFLD                    TXTLIN
      *
      *  Key for file : BESTILLING-HODE-REGISTER, for resting
      *
     C     REDKEY        KLIST
     C                   KFLD                    HEDFIR
     C                   KFLD                    HEDNUM
     C                   KFLD                    REDSUF
      *
      *  Key for file : BESTILLING-LINJE-REGISTER, for resting
      *
     C     RINKEY        KLIST
     C                   KFLD                    LINFIR
     C                   KFLD                    LINNUM
     C                   KFLD                    REDSUF
     C                   KFLD                    LINLIN
      *
      *  Key for file : PLUKK-S-LINJE-REGISTER
      *
     C     PSOKEY        KLIST
     C                   KFLD                    PSOFIR
     C                   KFLD                    PSONUM
     C                   KFLD                    PSOSUF
     C                   KFLD                    PSOTLL
     C                   KFLD                    PSOLIN
      *
      *  Key for file : UTPLUKK-PARAM-REGISTER
      *
     C     PLOKEY        KLIST
     C                   KFLD                    PLOFIR
     C                   KFLD                    PLONUM
     C                   KFLD                    PLOSUF
      *
      *  Key for file : UTPLUKK-LINJE-REGISTER
      *
     C     PLLKEY        KLIST
     C                   KFLD                    PLLFIR
     C                   KFLD                    PLLNUM
     C                   KFLD                    PLLSUF
     C                   KFLD                    PLLLIN
8.01  *
8.01  * Key for tilleggsregister bestilling - LOTIST
8.01  *
8.01 c     lotii2_key    klist
8.01 c                   kfld                    w_firm
8.01 c                   kfld                    lotii2_tnum
8.01 c                   kfld                    lotii2_tsuf
8.01  *
8.01  * Key for ordretabell
8.01  *
8.01 c     fohel1_key    klist
8.01 c                   kfld                    w_firm
8.01 c                   kfld                    fohel1_numm
8.01 c                   kfld                    fohel1_suff
8.04  *
8.04  *  Key for file : pakkseddel linjer
8.04  *
8.04 c     lblii4_key    klist
8.04 c                   kfld                    w_firm
8.04 c                   kfld                    lblii4_lnum
8.04 c                   kfld                    lblii4_lsuf
8.04 c                   kfld                    lblii4_llin
8.04 c                   kfld                    lblii4_laar
8.04  *
8.04  *  Key for file : pakkseddel linjer
8.04  *
8.04 c     lblii4_key2   klist
8.04 c                   kfld                    w_firm
8.04 c                   kfld                    lblii4_lnum
8.04 c                   kfld                    lblii4_lsuf
      *
      *  Blanker hjelpefelter som benyttes i Local Data Area
      *
     C                   MOVE      *ZERO         DSALT1
     C                   MOVE      *ZERO         DSALT2
     C                   MOVE      *ZERO         DSPERI
     C                   MOVE      *loval        DSDATO
     C                   MOVE      *BLANK        l_prog
      *
      *  Legger inn type for oppdatering av lager  R=Registrering
      *                                             =Utskrift/Utplukk
      *
     C                   MOVE      *BLANK        HLTYPE
      *
      *  Legger inn dato og klokkeslett (timestamp)
      *
     c                   time                    w_date
     c                   time                    w_time
8.04 c                   eval      w_aarr = %subdt(w_date:*years)
      *
     CSR                 ENDSR
