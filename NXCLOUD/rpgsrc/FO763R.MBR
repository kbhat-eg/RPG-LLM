     H/TITLE  ASOFAK: Oppdatering av memo-saldo i kunde-register
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO763R
      * Beskrivelse . . : Oppdatering av memo-saldo en kunde
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * V6.30 091116 sst  Nytt program
      *
9.01  *       170117 sst  Hente parameter meosaldo fra AFPSPF
8.01  * K000  231122 sst  Flyttet ant memo-dager og utv.kreditgrense til  UTVIDET_MEMOSALDO (nx)
8.02  * K000  030323 sst  Justert beregneing av memosaldo vedr negative ordretyper
8.03  * K000  080323 sst  Lat til ny endringsdato ved beregning av memosaldo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     Frkmelu    UF a E           K DISK    RENAME(rkmepfr:rkmelur)
      *
     ffohelc    if   e           k disk    rename(fohepfr:fohelcr)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
      *
     D                UDS
      *
     d l_user                911    920
9.01 d l_filg                931    933
     D  DSSFIR               944    946  0
      *
      * Definering av variable
      *
     d kunfir          s                   like(rkfirm)
     d kunnum          s                   like(rkkund)
     d hedfir          s                   like(fofirm)
     d hedkun          s                   like(fokund)
     d votyl1_firm     s                   like(vaofir)
     d votyl1_otyp     s                   like(vaotyp)
      *
     d p_fir           s              3
     d p_kun           s              6
      *
     d w_date          s               d   datfmt(*iso)
9.01  *w_utvgrns = utvidelse av memosaldo(Mestergruppen)
9.01 d w_utvgrns       s              2  0
9.01  *w_memodagr = antall dager ordre skal med i memosaldo
9.01 d w_memodagr      s              3  0
9.01 d w_3x            s              3
9.01 d w_firm          s              3  0
      *
      * Eksternt program
7.00   dcl-s co402_verdi1  char(1);
7.00   dcl-s co402_verdi2  char(2048);
7.00   DCL-PR CO402R EXTPGM('CO402R');
7.00     p_filgrp            char(3)     const;
7.00     p_firm              packed(3:0) const;
7.00     p_lager             packed(2:0) const;
7.00     p_nokkel            char(50)    const;
7.00     p_verdi1            char(1);
7.00     p_verdi2            char(2048);
7.00   END-PR;
8.01 d u_kmem          s              1    inz(*off)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Nullstiller "memo-saldo" i KUNDE-REGISTER                      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c                   move      p_fir         kunfir
     c                   move      p_kun         kunnum
     C     kunkey        chain     rkmelur
      *
     c                   eval      rkmems = 0
     c                   eval      rkmema = 0
      *
      *  Oppdaterer RKMEPF med ny "memo-saldo"                          *
      *
     c                   move      p_fir         hedfir
     c                   move      p_kun         hedkun
     c     hedkey        setll     fohelc
     c     hedkey        reade     fohelc
      *
      *
     c                   dow       not %eof(fohelc)
      *
     c                   if        foldat < w_date
     c                   eval      votyl1_otyp = footyp
     c     votyl1_key    chain     votyl1r                            90
     c                   if        vaoogr > 0
      *  Memosaldo
8.02 c                   if        vaokre <> '-'
8.02 c                   eval      rkmems = rkmems + fotots - fototr
     c                   eval      rkmema = rkmema + fobrra
8.02 c                   else
8.02 c                   eval      rkmems = rkmems - fotots + fototr
     c                   eval      rkmema = rkmema - fobrra
8.02 c                   endif
      *  Memosaldo  Almenning   ????  fobrra = bruksrabatt almenning
     c                   endif
     c                   endif
     c     hedkey        reade     fohelc
     c                   enddo
      *
     c                   if        %found(rkmelu)
8.03 c                   time                    rkmeda
8.03 c                   time                    rkmeti
8.03 c                   eval      rkmeus = l_user
     c                   update    rkmelur
     c                   else
     c                   move      p_fir         rkfirm
     c                   move      p_kun         rkkund
     c                   eval      rkkjal = *zero
     c                   time                    rkmeda
     c                   time                    rkmeti
     c                   time                    rkmoda
     c                   time                    rkmoti
     c                   eval      rkmeus = l_user
     c                   write     rkmelur
     c                   endif
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     csr   *inzsr        begsr
     c     *entry        plist
     c                   parm                    p_fir
     c                   parm                    p_kun
      *
      *  Key for file : KUNDE-memo-saldo
      *
     C     kunkey        klist
     C                   kfld                    kunfir
     C                   kfld                    kunnum
      *
      *  Key for file : ORDRE-HODE-REGISTER
      *
     c     hedkey        klist
     c                   kfld                    hedfir
     c                   kfld                    hedkun
      *
     c     votyl1_key    klist
     c                   kfld                    votyl1_firm
     c                   kfld                    votyl1_otyp
      *
     c                   eval      kunfir = dssfir
     c                   eval      hedfir = dssfir
     c                   eval      votyl1_firm = dssfir
     c                   eval      w_firm      = dssfir
9.01  *
8.01  *
8.01  * Utvidet memosaldo
8.01  *   Hent inn ant dager fram ordre skal med i memosaldo
8.01  *   Hent inn % utvidelse av kreditgrense
8.01  *
8.01 c                   eval      w_memodagr = 999
8.01 c                   eval      w_utvgrns  = 0
8.01  *
8.01   CallP CO402R(l_filg:w_firm:0:'UTVIDET_MEMOSALDO':
8.01                    co402_verdi1:co402_verdi2);
8.01   if co402_verdi1 = '1';
8.01     u_kmem = *on;
8.01     if  %subst(co402_verdi2:1:3) <> *blank;
8.01       w_memodagr = %dec(%subst(co402_verdi2:1:3):3:0);
8.01     endif;
8.01     if  %subst(co402_verdi2:133:2) <> *blank;
8.01       w_utvgrns = %dec(%subst(co402_verdi2:133:2):2:0);
8.01     endif;
8.01   endif;
     c                   move      udate         w_date
     c                   adddur    w_memodagr:*d w_date
      *
     csr                 endsr
