      /TITLE  ASOFAK: Skriver reoletiketter
     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FP220R
      * Beskrivelse . . : Skriver reoletiketter 210 x 60
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       051106 KOB  Nytt program
      * V5.50 030507 KOB  Utvidet lagerplass til 8 poisjoner
      * V5.60 020408 JAF  Ny BM etikett
      *                   Støtte for Datamax Ex2
      * V5.60 210109 KOB  Lagt inn støtte for Datamax EX-2 som egen skrivertype
6.30  * 6.30  280514 BKV  LVAR økt fra 15 til 20
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvaw1pf    if   f  512    64aidisk
      *
     fEtikett   o    f  132        Printer oflind(*inof)
     *
     *+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     *               :  LOCAL DATA AREA                                 +
     *+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     *
     D                uds
     *
     d l_hex1                760    779
     d l_hex2                780    799
     d l_type                891    891  0
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
     *
      * Definering av variable
      *
     d w_skty          s                   like(l_type)
     d w_hyle          s                   like(vahyle)
      *
     d w_best          s              7
     d w_dato          s              8
     d w_altv          s             15
     d w_lvar          s             15
     d w_lvar_u        s             21
      *
     d digits          c                   ' 0123456789'
      *
     d w_anta_1        s              1  0
     d w_anta_2        s              2  0
     d w_anta_3        s              3  0
      *
     ivaw1pf    ns  01
      *
     i                                  1    4 0vaanta
     i                                  5   19  vattxt
     i                                 20   25  vatvar
     i                                 26   32  vatpri
     i                                 38   45  vadato
     i                                 46   53 0vanobb
     i                                 59   66 0vavare
     i                                 74   86 0vaeann
     i                                 87  101  valvar
6.30 i                                107  141  vatek1
6.30 i                                142  176  vatek2
6.30 i                                177  179  vaenh1
6.30 i                                180  187  vapri1
6.30 i                                188  190  vaenh2
6.30 i                                191  198  vapri2
6.30 i                                199  203  vabesk
6.30 i                                204  208  vabpun
6.30 i                                209  213  vabmen
6.30 i                                214  214 0vahyle
6.30 i                                215  215 0valvko
6.30 i                                216  223  vaplas                          Lagerplass
6.30 i                                224  224 0vaetyp                          Etikettype
6.30 i                                225  227  vamini                          Minimumsbeholdn.(L)
6.30 i                                228  230  vamaks                          Maksimumbeholdn.(L)
6.30 i                                231  231  vavtyp                          Varetype
6.30 i                                232  234  vaenin                          Bestillingsenhet
6.30 i                                235  237  vaenps                          Sammenligningsenhet
6.30 i                                238  245  vapspr                          Pris sammenligning
      *
     i                                256  256  vadumy
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   read      vaw1pf                                 61
      *
     c                   dow       *in61 = *off
      *
      * Sjekk om riktig etikettype
      *
     c                   if        vaetyp <> 8
     c                   goto      neste
     c                   endif
      *
     c                   if        *in60 = *off
     c                   eval      w_hyle = 9
      *
      * Hent skriverinformasjon
      *
     c                   eval      w_skty = l_type
     c                   eval      w_dato = vadato
     c                   eval      *in60 = *on
     c                   endif
      *
      * Klargjør alternativt varenummer
      *
     c                   select
     c                   when      valvko = 1
     c                   movel     valvar        w_altv
     c                   when      valvko = 2
     c                   movel     vavare        w_altv
     c                   when      valvko = 3
     c                   movel     vanobb        w_altv
     c                   other
     c                   move      *blank        w_altv
     c                   endsl
      *
      * Klargjør bestilingsinformasjon
      *
     c                   eval      w_best = vabpun+'-'+vabmen
      *
      * Sjekk om lev.varenr. er nummerisk
      * og flytt til felt med riktig lengde
      *
     c     digits        check     valvar                                 90
      *
     c                   if        *in90 = *off
      *
     c                   eval      w_lvar = %trim(valvar)
     c                   else
     c                   move      *zeros        w_lvar
      *
     c                   endif
      *
      * Legg ut nytt program
      * hvis skifte av etikettype
      *
     c     vahyle        ifne      w_hyle
      *
      * Valg av strekkode
      *
     c                   eval      *in20 =*off
     c                   eval      *in21 =*off
     c                   eval      *in22 =*off
     c                   eval      *in23 =*off
      *
     c                   select
     c                   when      vahyle = 0
     c                   eval      *in20 =*on
     c                   when      vahyle = 1
     c                   eval      *in21 =*on
     c                   when      vahyle = 2
     c                   eval      *in22 =*on
     c                   when      vahyle = 3
     c                   eval      *in23 =*on
     c                   endsl
     c                   eval      w_hyle = vahyle
      *
     c                   endif
      *
      * Skrivertype Meto mi-4
      *
     c     w_skty        ifeq      4
     c                   except    Ædata4
     c                   endif
      *
      * Skrivertype Datamax Ex-2 (Bruker samme utskrift som  Meto mi-4)
      *
     c     w_skty        ifeq      6
     c                   eval      *in66 = *on
     c                   except    Ædata4
     c                   endif
      *
     c     neste         tag
      *
     c                   read      vaw1pf                                 61
     c                   enddo
      *
     c                   goto      avslutt
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c                   endsr
      *
      * Behandle Meto mi-4
      *                                                                ***
     oEtikett   e            Ædata4
     O                                              '~n'
      *
     o          e            Ædata4
     O                                              '~c0000'
      *
     O          e            Ædata4
     O                                              '~e'
      *
     O          e            Ædata4
     O                                              '~M2055'
      *
     O          e            Ædata4
     O                                              '~ySPM'
      *
     O          e            Ædata4
     O                                              '~Kf0040'
      *
      * Kode for Datamax Ex-2
      *
     O          e    66      Ædata4
     O                                              '~KcLW210'
      *
     O          e            Ædata4
     O                                              '~L'
      *
     O          e            Ædata4
     O                                              'z'
      *
     O          e            Ædata4
     O                                              'D11'
      *
     O          e            Ædata4
     O                       l_hex1
      *
     O          e            Ædata4
     O                       l_hex2
      *
      * Antall etiketter
      *
     O          e            Ædata4
     O                                              'Q'
     O                       vaanta
      *
      * Varetekst 1
      *
     O          e            Ædata4
     O                                           15 '4912A1800010065'
     O                       vatek1
      *
      * Varetekst 2
      *
     O          e            Ædata4
     O                                           15 '4912A1400010105'
     O                       vatek2
      *
      * Bestillingsinformasjon
      *
     O*         e            Ædata4
     O*                                          15 '4911A1200200150'
     O*                      w_best
      *
      * Bestillingsmengde - strekkode
      *
     O*         e            Ædata4
     O*                                          15 '4D2201901200080'
     O*                      vabmen
      *
      * Alternativt varenummer
      *
     O*         e            Ædata4
     O*                                          15 '4911A1201900150'
     O*                      w_altv
      *
      * Dato
      *
     O          e            Ædata4
     O                                           15 '4911A1003100205'
     O                       w_dato
      *
      * Lagerplass
      *
     O          e            Ædata4
     O                                           15 '1911A0800010120'
     O                       vaplas
      *
      * Priskode
      *
     O          e            Ædata4
     O                                           15 '4911A1007350025'
     O                       vabesk
      *
      * Pristekst
      *
     O          e            Ædata4
     O                                           15 '4912A1205300046'
     O                       vatpri
      *
      * Varenr.tekst
      *
     O          e            Ædata4
     O                                           15 '4911A1003100150'
     O                       vatvar
      *
      * Eget varenr.
      *
     O          e            Ædata4
     O                                           15 '4911A1003800150'
     O                       vavare
      *
      * "Bar-code" (Eget varenr.)
      *
     O          e    20      Ædata4
     O                                           15 '4d6305001700190'
     O                       vavare
      *
      * Eget varenr.
      *
     O          e    20      Ædata4
     O                                           15 '4911A1001900205'
     O                       vavare
      *
      * "Bar-code" (Leverandørens varenr.)
      *
     O          e    21      Ædata4
     O                                           15 '4d6305001700190'
     O                       w_lvar
      *
      * Leverandørens varenummer
      *
     O          e    21      Ædata4
     O                                           15 '4911A1001900205'
     O                       w_lvar
      *
      * "Bar-code" (EAN-nr.)
      *
     O          e    22      Ædata4
     O                                           15 '4F3303501400190'
     O                       vaeann
      *
      * "Bar-code" (Nobb-nr.)
      *
     O          e    23      Ædata4
     O                                           15 '4d6305001700190'
     O                       vanobb
      *
      * Nobb-nr.
      *
     O          e    23      Ædata4
     O                                           15 '4911A1001900205'
     O                       vanobb
      *
      * Enhet2
      *
     O          e            Ædata4
     O                                           15 '4912A1005300120'
     O                       vaenh2
      *
      * Pris2
      *
     O          e            Ædata4
     O                                           15 '442300806000115'
     O                       vapri2
      *
      * Enhet1
      *
     O          e            Ædata4
     O                                           15 '4912A1005300210'
     O                       vaenh1
      *
      * Pris1
      *
     O          e            Ædata4
     O                                           15 '442300806000203'
     O                       vapri1
      *
     O          e            Ædata4
     O                                              'E'
      *
