      /title  ASLAGR Bytting av leverandørnummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : VL791R
      * Beskrivelse . . : Bytting av leverandørnummer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       080500 HKO  Nytt program
5.60  *       090908 bof  Utvidet med noen filer
6.10  * 6.10  170609 BSA  Oppdatert med sporingsinformasjon
6.20  * 6.20  020413 bof  Utvidet med leverandør på vlagpf
6.nu  * R6.30 040614 bsa  Programmet gjennomgått mtp nummerutvidelser.
6.31  * R6.30 040914 bof  Hvis best.forslag parm finnes fra før,slettes gml.
      *
8.01  *       220621 bsa  Utvidet meldingsnummer fra 6-8 siffer
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flohelk    uf   e           k disk    rename(lohepfr:lohelkr)
     flodtlk    uf   e           k disk    rename(lodtpfr:lodtlkr)
     flfdtl1    if   e           k disk    rename(lfdtpfr:lfdtl1r)
     flfdtlu    uf   e           k disk    rename(lfdtpfr:lfdtlur)
     flldilu    uf   e           k disk    rename(lldipfr:lldilur)
     fldell1    if   e           k disk    rename(ldelpfr:ldell1r)
     fldellu    uf   e           k disk    rename(ldelpfr:ldellur)
     flstslu    uf   e           k disk    rename(lstspfr:lstslur)
     fledil1    if   e           k disk    rename(ledipfr:ledil1r)
     fledilu    uf   e           k disk    rename(ledipfr:ledilur)
     flfhel1    if   e           k disk    rename(lfhepfr:lfhel1r)
     flfhelu    uf   e           k disk    rename(lfhepfr:lfhelur)
     fmltil3    uf   e           k disk    rename(mltipfr:mltil3r)
     fmmatl4    uf   e           k disk    rename(mmatpfr:mmatl4r)
     fmppalu    uf   e           k disk    rename(mppapfr:mppalur)
     fmprol1    if   e           k disk    rename(mpropfr:mprol1r)
     fmprolu    uf   e           k disk    rename(mpropfr:mprolur)
     fmprwl1    if   e           k disk    rename(mpropfr:mprwl1r)
     fmprwlu    uf   e           k disk    rename(mpropfr:mprwlur)
     fmprul1    if   e           k disk    rename(mprupfr:mprul1r)
     fmprulu    uf   e           k disk    rename(mprupfr:mprulur)
6.20 fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
6.20 fvlaglu    uf   e           k disk    rename(vlagpfr:vlaglur)
      *
     fsihel1    if   e           k disk    rename(sihepfr:sihel1r)
     fsihelu    uf   e           k disk    rename(sihepfr:sihelur)
      *
6.10  *  Local Data Area
6.10  *
6.10 d                uds
6.10 d l_user                911    920
      *
      * Definering av parametere
      *
     d p_firm          s                   like(lofirm)
     d p_gldo          s                   like(loldor)
     d p_ldor          s                   like(loldor)
      *
      * Definering av variable i nøkler
      *
     d lfdtlu_numm     s                   like(lfnumm)
     d lfdtlu_line     s                   like(lfline)
     d ldellu_aarr     s                   like(lyaarr)
     d ldellu_numm     s                   like(lynumm)
     d ldellu_suff     s                   like(lysuff)
     d sihelu_aarr     s                   like(shaarr)
     d sihelu_binr     s                   like(shbinr)
     d sihelu_numm     s                   like(shnumm)
     d sihelu_suff     s                   like(shsuff)
      *
     d ledilu_type     s                   like(litype)
     d ledilu_mnum     s                   like(limnum)
     d ledilu_mlin     s                   like(limlin)
     d lfhelu_numm     s                   like(lhnumm)
     d mprolu_vare     s                   like(movare)
     d mprolu_lage     s                   like(molage)
     d mprwlu_vare     s                   like(movare)
     d mprwlu_lage     s                   like(molage)
     d mprulu_vare     s                   like(mrvare)
     d mprulu_lage     s                   like(mrlage)
     d mprulu_aar      s                   like(mraar)
     d mprulu_uke      s                   like(mruke)
6.20 d vlaglu_vare     s                   like(vlvare)
6.20 d vlaglu_lage     s                   like(vllage)
6.31 d mppalu_ldor     s                   like(mpldor)
      *
      * Definering av variable
      *
     d w_firm          s                   like(lofirm)
     d w_gldo          s                   like(loldor)
6.31  *
6.31 d b_mppa_ny       s              1    inz(*off)
6.31 d b_mppa_gm       s              1    inz(*off)
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Bestilling-hode
      *
     c     key02         setll     lohelk
     c     key02         reade     lohelkr                                90
     c                   dow       *in90 = *off
     c                   eval      loldor = p_ldor
6.10 c                   time                    loedat
6.10 c                   time                    loetim
6.10 c                   eval      loeusr = l_user
     c                   update    lohelkr
     c     key02         reade     lohelkr                                90
     c                   enddo
      *
      * Bestilling-linje
      *
     c     key02         setll     lodtlk
     c     key02         reade     lodtlkr                                90
     c                   dow       *in90 = *off
     c                   eval      ldldor = p_ldor
6.10 c                   time                    ldedat
6.10 c                   time                    ldetim
6.10 c                   eval      ldeusr = l_user
     c                   update    lodtlkr
     c     key02         reade     lodtlkr                                90
     c                   enddo
      *
      * Bestillingsforslag-linje
      *
     c     key01         setll     lfdtl1
     c     key01         reade     lfdtl1                                 90
     c                   dow       *in90 = *off
     c                   if        lfldor = p_gldo
     c                   eval      lfdtlu_numm = lfnumm
     c                   eval      lfdtlu_line = lfline
     c     lfdtlu_key    chain     lfdtlur                            91
     c                   if        *in91 = *off
     c                   eval      lfldor = p_ldor
6.10 c                   time                    lfedat
6.10 c                   time                    lfetim
6.10 c                   eval      lfeusr = l_user
     c                   update    lfdtlur
     c                   endif
     c                   endif
     c     key01         reade     lfdtl1                                 90
     c                   enddo
      *
      * Leverandør distibusjonsliste
      *
     c     key02         setll     lldilu
     c     key02         reade     lldilur                                90
     c                   dow       *in90 = *off
     c                   eval      lpldor = p_ldor
     c                   update    lldilur
     c     key02         reade     lldilur                                90
     c                   enddo
      *
      * Bestilling-slettet
      *
     c     key01         setll     ldell1
     c     key01         reade     ldell1                                 90
     c                   dow       *in90 = *off
     c                   if        lyldor = p_gldo
     c                   eval      ldellu_aarr = lyaarr
     c                   eval      ldellu_numm = lynumm
     c                   eval      ldellu_suff = lysuff
     c     ldellu_key    chain     ldellur                            91
     c                   if        *in91 = *off
     c                   eval      lyldor = p_ldor
6.10 c                   time                    lyedat
6.10 c                   time                    lyetim
6.10 c                   eval      lyeusr = l_user
     c                   update    ldellur
     c                   endif
     c                   endif
     c     key01         reade     ldell1                                 90
     c                   enddo
      *
      * Status
      *
     c     key01         chain     lstslur                            90
     c                   if        *in90 = *off
     c                   if        laalev = p_gldo
     c                   eval      laalev = p_ldor
     c                   endif
     c                   if        laalnr = p_gldo
     c                   eval      laalnr = p_ldor
     c                   endif
6.10 c                   time                    laaeda
6.10 c                   time                    laaeti
6.10 c                   eval      laaeus = l_user
     c                   update    lstslur
     c                   endif
      *
      * Inng.faktura-hode
      *
     c     key01         setll     sihel1
     c     key01         reade     sihel1                                 90
     c                   dow       *in90 = *off
     c                   if        shldor = p_gldo
     c                   eval      sihelu_aarr = shaarr
     c                   eval      sihelu_binr = shbinr
     c                   eval      sihelu_numm = shnumm
     c                   eval      sihelu_suff = shsuff
     c     sihelu_key    chain     sihelur                            91
     c                   if        *in91 = *off
     c                   eval      shldor = p_ldor
6.10 c                   time                    shedat
6.10 c                   time                    shetim
6.10 c                   eval      sheusr = l_user
     c                   update    sihelur
     c                   endif
     c                   endif
     c     key01         reade     sihel1                                 90
     c                   enddo
      *
      * EDI-retur meldinger samlefil
      *
     c     key01         setll     ledil1
     c     key01         reade     ledil1                                 90
     c                   dow       *in90 = *off
     c                   if        lildor = p_gldo
     c                   eval      ledilu_type = litype
     c                   eval      ledilu_mnum = limnum
     c                   eval      ledilu_mlin = limlin
     c     ledilu_key    chain     ledilur                            91
     c                   if        *in91 = *off
     c                   eval      lildor = p_ldor
6.10 c                   time                    liedat
6.10 c                   time                    lietim
6.10 c                   eval      lieusr = l_user
     c                   update    ledilur
     c                   endif
     c                   endif
     c     key01         reade     ledil1                                 90
     c                   enddo
      *
      * Bestillingsforslag
      *
     c     key01         setll     lfhel1
     c     key01         reade     lfhel1                                 90
     c                   dow       *in90 = *off
     c                   if        lhldor = p_gldo
     c                   eval      lfhelu_numm = lhnumm
     c     lfhelu_key    chain     lfhelur                            91
     c                   if        *in91 = *off
     c                   eval      lhldor = p_ldor
6.10 c                   time                    lhedat
6.10 c                   time                    lhetim
6.10 c                   eval      lheusr = l_user
     c                   update    lfhelur
     c                   endif
     c                   endif
     c     key01         reade     lfhel1                                 90
     c                   enddo
      *
      * Leveringstid
      *
     c     key02         setll     mltil3
     c     key02         reade     mltil3r                                90
     c                   dow       *in90 = *off
     c                   eval      mcldor = p_ldor
6.10 c                   time                    mcedat
6.10 c                   time                    mcetim
6.10 c                   eval      mceusr = l_user
     c                   update    mltil3r
     c     key02         reade     mltil3r                                90
     c                   enddo
      *
      * Matrise Prognose
      *
     c     key02         setll     mmatl4
     c     key02         reade     mmatl4r                                90
     c                   dow       *in90 = *off
     c                   eval      mmldor = p_ldor
6.10 c                   time                    mmedat
6.10 c                   time                    mmetim
6.10 c                   eval      mmeusr = l_user
     c                   update    mmatl4r
     c     key02         reade     mmatl4r                                90
     c                   enddo
      *
      * Parameter pr. lev. best.forslag
      *
6.31 c                   eval      b_mppa_ny = *off
6.31 c                   eval      b_mppa_gm = *off
6.31  *
6.31  * sjekker om det finnes parameter fra før på til-lev,
6.31  * sjekker ny
6.31 c                   eval      mppalu_ldor = p_ldor
6.31 c     mppalu_key    chain     mppalu
6.31 c                   if        %found(mppalu)
6.31 c                   eval      b_mppa_ny = *on
6.31 c                   endif
6.31  * sjekker gammel
6.31 c                   eval      mppalu_ldor = p_gldo
6.31 c     mppalu_key    chain     mppalu
6.31 c                   if        %found(mppalu)
6.31 c                   eval      b_mppa_gm = *on
6.31 c                   endif
6.31  * hvis begge finnes slette "gammel"
6.31 c                   if        b_mppa_ny = *on and
6.31 c                             b_mppa_gm = *on
6.31 c                   eval      mppalu_ldor = p_gldo
6.31 c     mppalu_key    setll     mppalu
6.31 c     mppalu_key    reade     mppalu
6.31 c                   dow       not %eof(mppalu)
6.31 c                   delete    mppalur
6.31 c     mppalu_key    reade     mppalu
6.31 c                   enddo
6.31 c                   endif
      *
     c     key02         setll     mppalu
     c     key02         reade     mppalur                                90
     c                   dow       *in90 = *off
     c                   eval      mpldor = p_ldor
6.10 c                   time                    mpedat
6.10 c                   time                    mpetim
6.10 c                   eval      mpeusr = l_user
     c                   update    mppalur
     c     key02         reade     mppalur                                90
     c                   enddo
      *
      * prognose
      *
     c     key01         setll     mprol1
     c     key01         reade     mprol1                                 90
     c                   dow       *in90 = *off
     c                   if        moldor = p_gldo
     c                   eval      mprolu_vare = movare
     c                   eval      mprolu_lage = molage
     c     mprolu_key    chain     mprolur                            91
     c                   if        *in91 = *off
     c                   eval      moldor = p_ldor
6.10 c                   time                    moedat
6.10 c                   time                    moetim
6.10 c                   eval      moeusr = l_user
     c                   update    mprolur
     c                   endif
     c                   endif
     c     key01         reade     mprol1                                 90
     c                   enddo
      *
      * prognose  arbeidsregister
      *
     c     key01         setll     mprwl1
     c     key01         reade     mprwl1                                 90
     c                   dow       *in90 = *off
     c                   if        moldor = p_gldo
     c                   eval      mprwlu_vare = movare
     c                   eval      mprwlu_lage = molage
     c     mprwlu_key    chain     mprwlu                             91
     c                   if        *in91 = *off
     c                   eval      moldor = p_ldor
6.10 c                   time                    moedat
6.10 c                   time                    moetim
6.10 c                   eval      moeusr = l_user
     c                   update    mprwlur
     c                   endif
     c                   endif
     c     key01         reade     mprwl1                                 90
     c                   enddo
      *
      * prognose pr. uke
      *
     c     key01         setll     mprul1
     c     key01         reade     mprul1                                 90
     c                   dow       *in90 = *off
     c                   if        mrldor = p_gldo
     c                   eval      mprulu_vare = mrvare
     c                   eval      mprulu_lage = mrlage
     c                   eval      mprulu_aar  = mraar
     c                   eval      mprulu_uke  = mruke
     c     mprulu_key    chain     mprulur                            91
     c                   if        *in91 = *off
     c                   eval      mrldor = p_ldor
6.10 c                   time                    mredat
6.10 c                   time                    mretim
6.10 c                   eval      mreusr = l_user
     c                   update    mprulur
     c                   endif
     c                   endif
     c     key01         reade     mprul1                                 90
     c                   enddo
6.20  *
6.20  * lagerregister
6.20  *
6.20 c     key01         setll     vlagl1
6.20 c     key01         reade     vlagl1                                 90
6.20 c                   dow       *in90 = *off
6.20 c                   if        vlbldo = p_gldo
6.20 c                   eval      vlaglu_vare = vlvare
6.20 c                   eval      vlaglu_lage = vllage
6.20 c     vlaglu_key    chain     vlaglur                            91
6.20 c                   if        *in91 = *off
6.20 c                   eval      vlbldo = p_ldor
6.20 c                   time                    vledat
6.20 c                   time                    vletim
6.20 c                   eval      vleusr = l_user
6.20 c                   update    vlaglur
6.20 c                   endif
6.20 c                   endif
6.20 c     key01         reade     vlagl1                                 90
6.20 c                   enddo
      *
      * Avslutt program
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_gldo
     c                   parm                    p_ldor
      *
      * Key for les på firma
      *
     c     key01         klist
     c                   kfld                    w_firm
      *
      * Key for les på firma og leverandør
      *
     c     key02         klist
     c                   kfld                    w_firm
     c                   kfld                    w_gldo
      *
      * Key for oppdatering av bestillingsforslag-linje
      *
     c     lfdtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lfdtlu_numm
     c                   kfld                    lfdtlu_line
      *
      * Key for oppdatering av bestilling-slettet
      *
     c     ldellu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ldellu_aarr
     c                   kfld                    ldellu_numm
     c                   kfld                    ldellu_suff
      *
      * Key for oppdatering av edi-retur samlefil
      *
     c     ledilu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ledilu_type
     c                   kfld                    ledilu_mnum
     c                   kfld                    ledilu_mlin
      *
      * Key for oppdatering av bestillings-forslagshode
      *
     c     lfhelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lfhelu_numm
      *
      * Key for prognose
      *
     c     mprolu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mprolu_vare
     c                   kfld                    mprolu_lage
      *
      * Key for prognose  arbeidsfil
      *
     c     mprwlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mprwlu_vare
     c                   kfld                    mprwlu_lage
      *
      * Key for prognose pr. uke
      *
     c     mprulu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mprulu_vare
     c                   kfld                    mprulu_lage
     c                   kfld                    mprulu_aar
     c                   kfld                    mprulu_uke
      *
      * Key for oppdatering av inng.faktura-hode
      *
     c     sihelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sihelu_aarr
     c                   kfld                    sihelu_binr
     c                   kfld                    sihelu_numm
     c                   kfld                    sihelu_suff
6.20  *
6.20  * Key for vare lager
6.20  *
6.20 c     vlaglu_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    vlaglu_vare
6.20 c                   kfld                    vlaglu_lage
6.31  *
6.31  * Key for best.parm
6.31  *
6.31 c     mppalu_key    klist
6.31 c                   kfld                    w_firm
6.31 c                   kfld                    mppalu_ldor
      *
      *
      * Henter firma og gammelt leverandørnummer fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_gldo = p_gldo
      *
     c                   endsr
