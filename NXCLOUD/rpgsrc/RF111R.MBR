      /TITLE  Avkryssingsrutine kunder/leverandører
     h datedit(*dmy.) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RF111R                                              *
      * Beskrivelse . . : saldering  kunder
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      *K00 sst 041220  NXS6236  Automatisk nullstilling av kundeposter fra Shop
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
610  frjoupf    if   e           k disk    rename(rjoupfr:rjoupfr)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
7.02 frktrl8    uf   e           k disk    rename(rktrpfr:rktrl8r)
7.02 frktrlc    uf   e           k disk    rename(rktrpfr:rktrlcr)
     fraa1l1    if   e           k disk    rename(raa1pfr:raa1l1r)
     frjoulu    uf a e           k disk    rename(rjoupfr:rjoulur)
      *-----------------------
      * Data struktur
      *-----------------------
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
7.01 d l_filg                931    933
      *
      * Definering av variable
      *
7.01  *p_adra = J betyr at poster krysses aut selv om beløp < rest.
7.01  *           når postene kommer fra 'TELE'   (bank/Adra)
7.01 d p_adra          s              1    inz(' ')
      *
     d w_firm          s                   like(rnfirm)
     d w_kont          s                   like(rnkund)
610  d w_nivo          s                   like(rjnivo)
610  d w_memb          s                   like(rjmemb)
610  d w_mem1          s                   like(rjmemb)
     d w_sort          s                   like(rjsort)
7.02 d w_saldo         s             15  2
7.02 d w_nkund         s                   like(rnkund)
      *
     d rktrl8_kund     s                   like(rnkund)
     d rktrl8_biln     s                   like(rnbiln)
      *
7.01  *
7.01 d u_s701          s              1    inz(*off)                            Avvik innbetaling
7.02 d u_Null_Bilag_S  s              1    inz(*off)                            Nulle bilag fra shop
7.01  *
7.01  * Definering av eksterne prg
7.01  *
7.01   dcl-s co402_verdi1  char(1);
7.01   dcl-s co402_verdi2  char(2048);
7.01   DCL-PR CO402R EXTPGM('CO402R');
7.01     p_filgrp            char(3)     const;
7.01     p_firm              packed(3:0) const;
7.01     p_lager             packed(2:0) const;
7.01     p_nokkel            char(50)    const;
7.01     p_verdi1            char(1);
7.01     p_verdi2            char(2048);
7.01   END-PR;
7.01  *
7.02  *
7.02  *  Nullstille restbeløp på posteringer på kunder fra SHOP
7.02 c                   if        %subst(w_mem1:4:4) = 'SHOP'
7.02 c                             and U_Null_Bilag_s = *on
7.02 c                   exsr      Null_Bilag
7.02 c                   endif
      *
     c                   eval      *inlr = *on
     c                   return
      *
      *
7.02  * __________________________________________________________________
7.02  *  Nullstilling av bilag som gpr i 0 fra shop
7.02  * __________________________________________________________________
7.02 c     Null_Bilag    begsr
7.02  *    Her skal man starte på RJOU på nytt
7.02 c                   move      *blank        w_nivo
7.02 c                   eval      w_memb = w_mem1
7.02 c                   eval      w_sort = *blank
7.02 c     rjoupf_key    setll     rjoupf
7.02 c                   read      rjoupf
7.02  *    Finn første bilagsnr med kundenr entetn debet eller kredit
7.02 c                   dou       %eof(rjoupf)
7.02 c                   eval      rktrl8_kund = *zero
7.02 c                   if        rjkont > ra1hkt
7.02 c                             and rjkont <= ra1hrs
7.02 c                   eval      rktrl8_kund = rjkont
7.02 c                   endif
7.02 c                   if        rktrl8_kund = w_nkund
7.02 c                             or rktrl8_kund = *zero
7.02 c                   goto      nxt_rjou
7.02 c                   endif
7.02 c                   eval      w_nkund = rktrl8_kund
7.02  *
7.02 c                   eval      w_saldo = *zero
7.02  *
7.02  *    Les alle rktr på bnr på dette bilagsnr, kunde og år.
7.02 c                   eval      rktrl8_biln = rjbiln
7.02 c     rktrl8_key    setll     rktrl8
7.02 c     rktrl8_key    reade     rktrl8
7.02 c                   dow       not %eof(rktrl8)
7.02 c                   if        rnraar = rjraar
7.02 c                   eval      w_saldo = w_saldo + rnbelØ
7.02 c                   endif
7.02 c     rktrl8_key    reade     rktrl8
7.02 c                   enddo
7.02 c
7.02  *    Les alle rktr på ref på dette bilagsnr, kunde og år
7.02 c     rktrl8_key    setll     rktrlc
7.02 c     rktrl8_key    reade     rktrlc
7.02 c                   dow       not %eof(rktrlc)
7.02 c                   if        rnraar = rjraar
7.02 c                   eval      w_saldo = w_saldo + rnbelØ
7.02 c                   endif
7.02 c     rktrl8_key    reade     rktrlc
7.02 c                   enddo
7.02  *    Dersom sum oppr.beløp nullstilles restbeløp på disse bilag og ref.
7.02 c                   if        w_saldo = 0
7.02  *
7.02  *    Les alle rktr på bnr på dette bilagsnr, kunde og år og nullstill restbeløp
7.02 c                   eval      rktrl8_biln = rjbiln
7.02 c     rktrl8_key    setll     rktrl8
7.02 c     rktrl8_key    reade     rktrl8
7.02 c                   dow       not %eof(rktrl8)
7.02 c                   if        rnraar = rjraar
7.02 c                   eval      rnrest = *zero
7.02 c                   time                    rnedat
7.02 c                   time                    rnetim
7.02 c                   update    rktrl8r
7.02 c                   endif
7.02 c     rktrl8_key    reade     rktrl8
7.02 c                   enddo
7.02 c
7.02  *    Les alle rktr på ref på dette bilagsnr, kunde og år
7.02 c     rktrl8_key    setll     rktrlc
7.02 c     rktrl8_key    reade     rktrlc
7.02 c                   dow       not %eof(rktrlc)
7.02 c                   if        rnraar = rjraar
7.02 c                   eval      rnrest = *zero
7.02 c                   time                    rnedat
7.02 c                   time                    rnetim
7.02 c                   update    rktrlcr
7.02 c                   endif
7.02 c     rktrl8_key    reade     rktrlc
7.02 c                   enddo
7.02  *
7.02 c                   endif
7.02  *
7.02 c     nxt_rjou      tag
7.02 c                   read      rjoupf
7.02 c                   enddo
7.02  *
7.02 c                   endsr
7.02  * __________________________________________________________________
7.02  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    w_mem1
      *
      * Hente inn status-records
      *
     c     raa1l1_key    chain     raa1l1r                            69
      *
      * Key for firma
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
     c     rktrl8_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rktrl8_kund
     c                   kfld                    rktrl8_biln
      *
      * Key for transaksjonsfile
      *
     c     rjoulu_key    klist
610  c                   kfld                    w_nivo
610  c                   kfld                    w_memb
     c                   kfld                    w_sort
      *
     c     rjoupf_key    klist
610  c                   kfld                    w_nivo
610  c                   kfld                    w_memb
     c                   kfld                    w_sort
      *
      * Key for statusrecord
      *
     c     raa1l1_key    klist
     c                   kfld                    w_firm
      *
     c     raa3l1_key    klist
     c                   kfld                    w_firm
      *
      * Key for faste konti
      *
     c     ra04l1_key    klist
     c                   kfld                    w_firm
7.01  *
7.01  * Sjekk mot avkrysing av poster med avvik
7.01  *
7.01   CallP CO402R(l_filg:w_firm:0:'RF107_701':
7.01   co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01     u_s701 = *on;
7.01   endif;
7.01  *
7.01  *  Aut kryssing av poster kun når det hentes fra Adra/Tele
7.01  *
7.01 c                   if        u_s701 = *on and
7.02 c                             (   %subst(w_mem1:4:4) = 'TELE'
7.02 c                              or %subst(w_mem1:4:4) = 'SHOP')
7.01 c                   eval      p_adra = 'J'
7.01 c                   endif
7.01  *
7.02  *
7.02  * Sjekk om nullstilling av bilag fra Shop
7.02  *
7.02   CallP CO402R(l_filg:w_firm:0:'NULL_BILAG_S':
7.02   co402_verdi1:co402_verdi2);
7.02   if co402_verdi1 = '1';
7.02     u_Null_Bilag_S = *on;
7.02   endif;
7.02  *
      *
     c                   endsr
      *
