     H/TITLE  Utskrift av faktura-journal
     H DECEDIT('0,') DATEDIT(*DMY.)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : FO674R
      * Beskrivelse . . : Utskrift av faktura-journal
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * 6.10  250609 BSA  Oppdatert med sporingsinformasjon
pdf   * R6.10 040610 BSA  Innført PDF utskrift. Innebærer primært å finne
pdf   *                   generert spool, og kall av AP620R med dette som
pdf   *                   parametre.
6.11  * R6.10 071010 BSA  Overfører bacthtype som en del parameterlista. Viser
6.11  *                   hva slags rutine som kjøres. Legges med i arkivet.
6.20  * R6.20 190917 BKV  Viser firma foretaksnummer
6.nu  * R6.30 040614 bsa  Programmet gjennomgått mtp nummerutvidelser.
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     FSOHEL1    IF   E           K DISK    RENAME(SOHEPFR:SOHEL1R)
     FSOHELU    UF   E           K DISK    RENAME(SOHEPFR:SOHELUR)
     Frkunl1    IF   E           K DISK    RENAME(RKUNPFR:rkunl1r)
     FAFORL1    IF   E           K DISK    RENAME(AFORPFR:AFORL1R)
      *
     FFO674P    O    E             PRINTER
pdf  f                                     usropn
      *
      * - - - - - - - - - - - - - -
      * Datastruktur parameter-inn
      * - - - - - - - - - - - - - -
      *
     D                 DS
6.nu D  DSPARM                 1     32
     D  DSAARR                 1      4  0
     D  DSOTYP                 5      6
6.nu D  DSFFNR                 7     14  0
6.nu D  DSTFNR                15     22  0
6.nu D  DSPROG                23     32
      *
      * - - - - - - - - - - - - - -
      * Henter inn data fra LDA
      * - - - - - - - - - - - - - -
      *
     D                UDS
pdf  d l_poff                584    584
pdf  d l_utqm                585    594
pdf  d l_bach                595    635
pdf  d l_arch                636    636  0
pdf  d l_skje                637    637  0
pdf  d l_land                638    638  0
pdf  d l_exlp                639    639  0
pdf  d l_mail                640    640  0
     D  DSRUTI               800    809
pdf  D l_outq                810    819
     D  DSALIN               856    858  0
     D  DSWSID               901    910
     D  DSUSER               911    920
     D  DSFIRM               944    946  0
     D  l_fnav               951    980
      *
pdf  d splfname2       s             10a
pdf  d jobname2        s             10a
pdf  d username2       s             10a
pdf  d jobnbr2         s              6a
pdf  d splfnbr2        s             10i 0
pdf  d splfname3       s             10a
pdf  d jobname3        s             10a
pdf  d username3       s             10a
pdf  d jobnbr3         s              6a
pdf  d splfnbr3        s             10i 0
      *
pdf  d qsprilsp        pr                  extpgm('QSPRILSP')
pdf  d rcvvar                     32767a   options(*varsize)
pdf  d rcvvarlen                     10i 0 const
pdf  d format                         8a   const
pdf  d errorcode                  32767a   options(*varsize)
pdf   *
pdf  d sprl0100        ds
pdf  d  bytesrtn                     10i 0
pdf  d  bytesavl                     10i 0
pdf  d  splfname                     10a
pdf  d  jobname                      10a
pdf  d  username                     10a
pdf  d  jobnbr                        6a
pdf  d  splfnbr                      10i 0
pdf  d  systemname                    8a
pdf  d  createdate                    7a
pdf  d                                1a
pdf  d  createtime                    6a
pdf   *
pdf  d errorcode       ds
pdf  d  bytesprov                    10i 0 inz(0)
pdf  d  byteavail                    10i 0 inz(0)
      *
6.11 d p_btyp          s            100
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av formater                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Benyttede formater
      * - - - - - - - - - -
      *
      * A1HDR  - Heading på rapporten
      * A1DET  - Linjer  på rapporten
      * T1TOT  - Totaler på rapporten
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Styre-rutine                                                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Initierings-rutine
      *
     C     FCYCLE        CASEQ     *ZERO         XSTRSR
     C                   ENDCS
      *
      * Behandle ordrehode
      *
     C     *IN61         DOWEQ     *OFF
     C     SOFIRM        CABNE     WWFIRM        XFERDI
     C     SOAARR        CABNE     DSAARR        XFERDI
      *
     C     DSFFNR        IFEQ      *ZERO
     C     DSTFNR        IFEQ      *ZERO
     C     SOJOUR        IFEQ      1
     C                   GOTO      XTAG02
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
      *
     C     DSFFNR        IFEQ      *ZERO
     C     DSTFNR        IFEQ      *ZERO
     C     SOJOUR        IFEQ      0
     C                   GOTO      XTAG01
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
      *
     C     SOBINR        CABGT     DSTFNR        XFERDI
      *
     C     XTAG01        TAG
      *
     C                   EXSR      XAJOUR
     C                   EXSR      XPRINT
      *
     C     XTAG02        TAG
      *
     C                   READ      SOHEL1                                 61
     C                   ENDDO
      *
     C     XFERDI        TAG
     C                   WRITE     T1TOT                                30
      *
      * Ferdig
      *
     C     XSLUTT        TAG
pdf   * Generer xml for videre utskrift
pdf  c                   if        l_poff = '1'
pdf  c                   exsr      spoolnbr
pdf  c                   exsr      xml_create
pdf  c                   close     fo674p
pdf  c                   exsr      pdf_preview
pdf   * Avslutt jobbiden
pdf  c                   call      'AB710R'
pdf  c                   parm                    l_bach
pdf  c                   endif
      *
     C                   SETON                                        LR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Henter/Oppdatering av NUMMER-REGISTER                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     HNTNUM        BEGSR
      *
     C                   MOVE      'FJOURN'      WPFELL
     C                   MOVE      *BLANK        WPTYPE
     C                   MOVE      *BLANK        WPFAST
      *
      *  Kaller opp subprogram for henting av nummer fra NUMMER-REGISTER
      *
     C                   CALL      'AS100R'
     C                   PARM                    WPKODE
     C                   PARM                    WWFIRM
     C                   PARM                    WPFELL
     C                   PARM                    WPTYPE
     C                   PARM                    WPFAST
6.nu C                   PARM                    WPNUMM
      *
     C                   ENDSR
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * XAJOUR Klargjør data for print                                  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XAJOUR        BEGSR
      *
     c                   eval      a1fkda = 0
     c                   eval      a1ffda = 0
      *
      * Snu fakturadato
      *
     c                   if        sofkda <> *loval
     c     *dmy          move      sofkda        a1fkda
     c                   endif
      *
      * Snu forfallsdato
      *
     c                   if        soffda <> *loval
     c     *dmy          move      soffda        a1ffda
     c                   endif
      *
      * Hente inn kunderegister
      *
     C                   MOVE      SOKUND        WWKUND
     C     KEY04         CHAIN     rkunl1r                            64
     C                   MOVE      RKNAVN        A1NAVN
      *
      * Øvrige felter til print
      *
     C                   MOVE      SOBINR        A1BINR
     C                   MOVE      SONUMM        A1NUMM
     C                   MOVE      SOSUFF        A1SUFF
     C                   MOVE      SOOTYP        A1OTYP
     C                   MOVE      SOKUND        A1KUND
     C                   MOVE      SONAVN        A2NAVN
      *
     C                   Z-ADD     SOFSUM        A1FSUM
     C                   ADD       SOFSUM        T1FSUM
      *
      * Oppdater historisk faktura-register dersom
      * fra - til - nummer ikke er oppgitt og posten
      * ikke har vært med på journal tidligere.
      *
     C     DSFFNR        IFEQ      *ZERO
     C     DSTFNR        IFEQ      *ZERO
     C     SOJOUR        IFEQ      *ZERO
     C                   MOVE      SOBINR        WWBINR
     C                   MOVE      SONUMM        WWNUMM
     C                   MOVE      SOSUFF        WWSUFF
     C     KEY01         CHAIN     SOHELUR                            61
     C     *IN61         IFEQ      *OFF
     C                   MOVE      1             SOJOUR
6.10 c                   time                    soedat
6.10 c                   time                    soetim
6.10 c                   eval      soeusr = dsuser
     C                   UPDATE    SOHELUR
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
      *
      *
     C                   ENDSR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * XPRINT Skriver fakturajournal                                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XPRINT        BEGSR
      *
      * Skrive fakturalinjer
      *
     C                   WRITE     A1DET                                30
      *
      * Er det overflow ?
      *
     C     *IN30         CASEQ     *ON           XOVRFL
     C                   ENDCS
      *
     C                   ENDSR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for overflow                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XOVRFL        BEGSR
      *
     C                   WRITE     A1HDR                                30
      *
     C                   ENDSR
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for oppstart                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XSTRSR        BEGSR
      *
     C                   Z-ADD     1             FCYCLE            1 0
      *
      * Legger inn firma i nøkkel
      *
     C                   MOVE      DSFIRM        WWFIRM
     C                   MOVE      DSFIRM        A1FIRM
      *
      * Legger inn år i nøkkel
      *
     C                   MOVE      DSAARR        WWAARR
      *
      * Klargjøre resterende
      * nøkkelbegrep
      *
     C                   MOVE      DSFFNR        WWBINR
     C                   MOVE      *ZERO         WWNUMM
     C                   MOVE      *ZERO         WWSUFF
      *
      * Hent neste ledige nummer fra nummer-register
      *
     C     DSFFNR        IFEQ      *ZERO
     C     DSTFNR        IFEQ      *ZERO
     C                   MOVE      *ZERO         WPNUMM
     C                   MOVE      '0'           WPKODE
     C                   EXSR      HNTNUM
     C                   MOVE      WPNUMM        A1LIST
     C                   ENDIF
     C                   ENDIF
      *
      * Les første post
      *
     C     KEY01         SETLL     SOHEL1
     C                   READ      SOHEL1                                 61
      *
      * Skriv første heading
      *
     C                   WRITE     A1HDR                                30
      *
     C                   ENDSR
      *
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Finner spool som er generert av jobben
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     spoolnbr      begsr
pdf   /Free
pdf
pdf            QSPRILSP( SPRL0100
pdf                    : %size(SPRL0100)
pdf                    : 'SPRL0100'
pdf                    : errorcode );
pdf   /End-free
pdf   *
pdf  c                   endsr
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Kaller eget program for generering av xml fil. Eget pgm         *
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     xml_create    begsr
pdf   *
pdf  c                   eval      splfname2 = *blanks
pdf  c                   eval      splfname3 = *blanks
pdf  c                   eval      jobname2 = *blanks
pdf  c                   eval      jobname3 = *blanks
pdf  c                   eval      username2 = *blanks
pdf  c                   eval      username3 = *blanks
pdf  c                   eval      jobnbr2 = *blanks
pdf  c                   eval      jobnbr3 = *blanks
pdf  c                   eval      splfnbr2 = *zeros
pdf  c                   eval      splfnbr3 = *zeros
6.11 c                   eval      p_btyp = 'FAKTURAJOURNAL'
pdf   *
pdf  c                   call      'AP620R'
pdf  c                   PARM                    splfname
pdf  c                   PARM                    jobname
pdf  c                   PARM                    username
pdf  c                   PARM                    jobnbr
pdf  c                   PARM                    splfnbr
pdf  c                   PARM                    splfname2
pdf  c                   PARM                    jobname2
pdf  c                   PARM                    username2
pdf  c                   PARM                    jobnbr2
pdf  c                   PARM                    splfnbr2
pdf  c                   PARM                    splfname3
pdf  c                   PARM                    jobname3
pdf  c                   PARM                    username3
pdf  c                   PARM                    jobnbr3
pdf  c                   PARM                    splfnbr3
pdf  c                   PARM                    l_bach
6.11 c                   PARM                    p_btyp
pdf   *
pdf  c                   endsr
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Subrutiner for XML/jasper integrasjon - Skjermvisning           *
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     pdf_preview   begsr
pdf  c                   if        l_skje = 1
pdf   * Vi kaller program for launch av PDF i browser
pdf  c                   call      'AP621R'
pdf  c                   parm                    l_bach
pdf   *
pdf  c                   endif
pdf   *
pdf  c                   endsr
pdf   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     *INZSR        BEGSR
      *
     C     *LIKE         DEFINE    SOKUND        WWKUND
     C     *LIKE         DEFINE    SOFIRM        WWFIRM
     C     *LIKE         DEFINE    SOAARR        WWAARR
     C     *LIKE         DEFINE    SOBINR        WWBINR
     C     *LIKE         DEFINE    SOWSID        WWWSID
     C     *LIKE         DEFINE    SONUMM        WWNUMM
     C     *LIKE         DEFINE    SOSUFF        WWSUFF
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     C                   MOVE      *BLANK        WWRUTI           10
     C                   MOVE      *BLANK        WPKODE            1
     C                   MOVE      *BLANK        WPFELL            6
     C                   MOVE      *BLANK        WPTYPE            2
     C                   MOVE      *BLANK        WPFAST           10
6.nu C                   MOVE      *ZERO         WPNUMM            8 0
      *
      * Key for Heading-file
      *
     C     KEY01         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWAARR
     C                   KFLD                    WWBINR
     C                   KFLD                    WWNUMM
     C                   KFLD                    WWSUFF
      *
      * Key for kunderegister
      *
     C     KEY04         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWKUND
      *
      * Key for formular-register
      *
     C     KEY10         KLIST
     C                   KFLD                    WWRUTI
      *
      * Hente inn formular-register
      *
     C                   MOVE      DSRUTI        WWRUTI
      *
     C     KEY10         CHAIN     AFORL1R                            69
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     *ENTRY        PLIST
6.nu C                   PARM                    WPPARM           32
      *
     C                   MOVE      *ZERO         DSAARR
     C                   MOVE      *BLANK        DSOTYP
     C                   MOVE      *ZERO         DSFFNR
     C                   MOVE      *ZERO         DSTFNR
     C                   MOVE      WPPARM        DSPARM
      *
      * Legger inn skjermid. og bruker i faste felter
      *
     C                   MOVE      DSUSER        A1USER
     C                   MOVE      DSWSID        A1WSID
     C                   MOVE      UDATE         A1DATE
     C                   TIME                    UTIME             6 0
     C                   MOVE      UTIME         A1TIME
     C                   MOVEL     l_fnav        A1FNAV
      *
pdf  c                   open      fo674p
      *
     C                   ENDSR
