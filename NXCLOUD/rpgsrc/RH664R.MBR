     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RH661R                                              *
      * Beskrivelse . . : Hovedbok - mva-oppgjør, utskrift                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
6.10  * NHO       170811  Nytt program                                        *
6.20  * bsa       061212  Tillegg for å berike metataggger                    *
6.21  * sst       220513  Rettet brudd logikk vedr Fordelingsskjema           *
6.22  * bof       270913  * bort kall til DEBUGCL                             *
6.23  * nho       080414  MVA-kode 0 i dette programmet være med på           *
6.23  *                   utskriften, 'Utskrift av poster med
6.23  *                   mva-kode = 0 hører til enkeltposter
6.24  * bsa       260216  Endret kategorisering for arkivering.
6.30  * R6.30 180816 bsa  Rokkert på rekkefølge av close printerfile
6.31  * R6.30 040719 bsa  Utvidet saldofelter
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *                                                                       *
      *  BRUK AV INDIKATORER                                                  *
      *                                                                       *
      *       KA = F1  = Valg av firma                                        *
      *       KC = F3  = Exit                                                 *
      *                                                                       *
      *       LR = Avslutt program                                            *
      *       30 = Protect av felter ved vise                                 *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer                   *
      *    60-69 = Fileindikatorer chain etc.                                 *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                           *
      *    80-89 = Arbeidsindikatorer ordinære                                *
      *    90-99 = Benyttes ved std. sub-rutiner                              *
      *                                                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     frhtrlb    if   e           k disk    rename(rhtrpfr:rhtrlbr)
     frhsal1    if   e           k disk    rename(rhsapfr:rhsal1r)
     frhtrlu    uf   e           k disk    rename(rhtrpfr:rhtrlur)
     frhovlr    if   e           k disk    rename(rhovpfr:rhovlrr)
     fra04l1    if   e           k disk    rename(ra04pfr:ra04l1r)
     fra05lr    if   e           k disk    rename(ra05pfr:ra05lrr)
     fraa4l1    uf   e           k disk    rename(raa4pfr:raa4l1r)
     frjoupf    o  a e           k disk    rename(rjoupfr:rjoupfr)
     frh664p    o    e             printer oflind(*in30)
pdf  f                                     usropn
      *
     d mk              s              1    dim(99)
     d bto             s             13  2 dim(99)
     d nto             s             13  2 dim(99)
     d knto            s             13  2 dim(99)
     d grl             s             13  2 dim(99)
     d imv             s             11  2 dim(99)
V5.40d imu             s             11  2 dim(99)
     d umv             s             11  2 dim(99)
6.31 d sal             s             15  2 dim(14)
      *
     d rhtrlu_raar     s                   like(rbraar)
     d rhtrlu_kont     s                   like(rbkont)
     d rhtrlu_spes     s                   like(rbspes)
     d rhtrlu_avde     s                   like(rbavde)
     d rhtrlu_rper     s                   like(rbrper)
     d rhtrlu_bdat     s                   like(rbbdat)
     d rhtrlu_tist     s                   like(rbtist)
      *
     d rhsal1_kont     s                   like(rikont)
     d rhsal1_spes     s                   like(rispes)
     d rhsal1_avde     s                   like(riavde)
     d rhsal1_raar     s                   like(riraar)
     d rhsal1_type     s                   like(ritype)
      *
     d rhovlr_kont     s                   like(rhkont)
     d rhovlr_spes     s                   like(rhspes)
     d rhovlr_avde     s                   like(rhavde)
      *
     d raa4l1_4aar     s                   like(ra4aar)
     d ra05lr_ekod     s                   like(raekod)
     d rhtrlb_raar     s                   like(rbraar)
      *
     d w_edat          s               d   datfmt(*iso)
610  d w_memb          s                   like(rjmemb)
     d w_kont          s                   like(rbkont)
      *
     d                 ds
     d w_dato                         6  0
     d  w_dag                         2  0 overlay(w_dato)
     d  w_mnd                         2  0 overlay(w_dato:3)
     d  w_aar                         2  0 overlay(w_dato:5)
      *
     d w_udat          s              6  0
     d w_msg           s             50
      *
      * Local data area
      *
     d                uds
     d rhpkda                300    305  0
     d rhprÅr                         4  0
     d rhpper                         2  0
     d rhpmva                         1  0
     d rhpmv0                         1
     d rhptst                         1
     d rhpost                         1
     d rhpsps                         1
      *
pdf  d l_poff                584    584
pdf  d l_bach                595    635
pdf  d l_skje                637    637  0
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d g_mvak          s                   like(rbmvak)
     d w_mvak          s                   like(rbmvak)
     d wpavde          s                   like(rbavde)
     d wpfirm          s                   like(rbfirm)
     d wpkont          s                   like(rbkont)
     d wpspes          s                   like(rbspes)
     d w_firm          s                   like(l_firm)
     d w_stat          s              1
     d w_akod          s              2  0
     d w_atxt          s             20
      *
     d amva            s              1
     d a01txt          s             20
     d brto            s             13  2
     d mvag            s             13  2
     d iavg            s             11  2
     d imva            s             11  2
      *
     d recnr1          s              5  0
     d tsal            s             13  2
     d umva            s             11  2
      *
     d oms01g          s             11  0
     d oms02g          s             11  0
     d oms03g          s             11  0
     d oms04g          s             11  0
     d oms04a          s             11  0
     d oms05g          s             11  0
     d oms05a          s             11  0
     d oms06g          s             11  0
     d oms06a          s             11  0
     d oms07g          s             11  0
     d oms07a          s             11  0
     d oms08a          s             11  0
     d oms09a          s             11  0
     d oms10a          s             11  0
     d oms11a          s             11  0
      *
     d wmva01g         s             11  2
     d wmva02g         s             11  2
     d wmva03g         s             11  2
     d wmva04g         s             11  2
     d wmva04a         s             11  2
     d wmva05g         s             11  2
     d wmva05a         s             11  2
     d wmva06g         s             11  2
     d wmva06a         s             11  2
     d wmva07g         s             11  2
     d wmva07a         s             11  2
     d wmva08a         s             11  2
     d wmva09a         s             11  2
     d wmva10a         s             11  2
     d wmva11a         s             11  2
      *
     d wptex1          s             30
     d wptex2          s             30
     d wsbel           s             13  2
     d wsfylk          s             11  2
     d wsimva          s             11  2
V5.40d wsimvu          s             11  2
     d wsksal          s             13  2
     d wsnett          s             13  2
     d wsumva          s             11  2
6.NU d ww4sgb          s              8  0
     d x               s              2  0
     d xmva            s                   like(raebmk)
6.12 d p_btyp          s            100
      *
pdf  d splfname2       s             10a
pdf  d jobname2        s             10a
pdf  d username2       s             10a
pdf  d jobnbr2         s              6a
pdf  d splfnbr2        s             10i 0
pdf  d splfname3       s             10a
pdf  d jobname3        s             10a
pdf  d username3       s             10a
pdf  d jobnbr3         s              6a
pdf  d splfnbr3        s             10i 0
6.20 d p_tagg0         s             20
6.20 d p_tagg0val      s             50
6.20 d p_tagg1         s             20
6.20 d p_tagg1val      s             50
6.20 d p_tagg2         s             20
6.20 d p_tagg2val      s             50
6.20 d p_tagg3         s             20
6.20 d p_tagg3val      s             50
6.20 d p_tagg4         s             20
6.20 d p_tagg4val      s             50
6.20 d p_tagg5         s             20
6.20 d p_tagg5val      s             50
6.20 d p_tagg6         s             20
6.20 d p_tagg6val      s             50
6.20 d p_tagg7         s             20
6.20 d p_tagg7val      s             50
6.20 d p_tagg8         s             20
6.20 d p_tagg8val      s             50
6.20 d p_tagg9         s             20
6.20 d p_tagg9val      s             50
6.20 d p_refn          s             50
6.20 d p_dspl          s            100
pdf   *
pdf  d qsprilsp        pr                  extpgm('QSPRILSP')
pdf  d rcvvar                     32767a   options(*varsize)
pdf  d rcvvarlen                     10i 0 const
pdf  d format                         8a   const
pdf  d errorcode                  32767a   options(*varsize)
pdf   *
pdf  d sprl0100        ds
pdf  d  bytesrtn                     10i 0
pdf  d  bytesavl                     10i 0
pdf  d  splfname                     10a
pdf  d  jobname                      10a
pdf  d  username                     10a
pdf  d  jobnbr                        6a
pdf  d  splfnbr                      10i 0
pdf  d  systemname                    8a
pdf  d  createdate                    7a
pdf  d                                1a
pdf  d  createtime                    6a
pdf   *
pdf  d errorcode       ds
pdf  d  bytesprov                    10i 0 inz(0)
pdf  d  byteavail                    10i 0 inz(0)
pdf   *
      /eject
      *
     irhsal1r
      * Saldobalanse
      *
     i              risa01                      sal(01)
     i              risa02                      sal(02)
     i              risa03                      sal(03)
     i              risa04                      sal(04)
     i              risa05                      sal(05)
     i              risa06                      sal(06)
     i              risa07                      sal(07)
     i              risa08                      sal(08)
     i              risa09                      sal(09)
     i              risa10                      sal(10)
     i              risa11                      sal(11)
     i              risa12                      sal(12)
     i              risa13                      sal(13)
     i              risa00                      sal(14)
      *
      /eject
     c     dummy         tag
      *
      * Sett starverdi
      *
     c                   eval      rhtrlb_raar = rhprÅr
     c     rhtrlb_key    setll     rhtrlb
     c     rhtrlb_key    reade     rhtrlb                                 60
      *
     c                   eval      *in88 = *off
     c                   dow       *in60 = *off
      *
     c                   eval      rhpsps = 'N'
      *
6.23 c**                 if        rhpmv0 = 'J'
     c                   if        rbmvak = ' '
     c                   eval      rbmvak = '0'
     c                   endif
6.23 c**                 goto      mva0
6.23 c**                 endif
      *
      * Lister ikke mvakode = 0
      *
6.23 c**                 if        rbmvak = ' ' or
6.23 c**                           rbmvak = '0'
6.23 c**                 goto      nyles
6.23 c**                 endif
      *
6.23 c**   mva0          tag
      *
      * Brudd på MVA-kode
      *
     c                   if        rbmvak <> g_mvak
     c                   exsr      brudd
     c                   move      rbmvak        g_mvak
     c                   endif
      *
      * Kun posteringer med riktig
      * periode som ikke har vært
      * behandlet før blir behandlet videre
      *
     c                   if        rbrper <= rhpper and
     c                             rbmmrk <> '1'
     c                   exsr      beh_post
     c                   endif
      *
     c     nyles         tag
     c     rhtrlb_key    reade     rhtrlb                                 60
     c                   enddo
      *
      /space 3
      *
     c     slutt         tag
      *
     c                   write     t0det
     c                   add       t0nto         t0tot
     c                   add       t0grl         t0to1
     c                   add       t0imv         t0to2
     c                   add       t0umv         t0to3
     c                   add       t0bto         t0to4
     c                   write     t2det
     c                   exsr      mvatot
pdf   * Generer xml for videre utskrift
pdf  c                   if        l_poff = '1'
pdf  c                   exsr      spoolnbr
6.30 c                   close     rh664p
6.30 c                   exsr      xml_create
pdf  c                   exsr      pdf_preview
pdf   * Avslutt jobbiden
pdf  c                   call      'AB710R'
pdf  c                   parm                    l_bach
pdf  c                   endif
      *
     c                   seton                                        lr
      *
      * Subrutine for brudd på konto
      *
      * Subrutine for brudd på konto
      *
     c     bruddk        begsr
      *
      *
      *
     c                   if        rbkont <> w_kont
     c                   write     t0det
     c                   eval      t0nto = *zeros
     c                   eval      t0grl = *zeros
     c                   eval      t0imv = *zeros
     c                   eval      t0umv = *zeros
     c                   eval      t0bto = *zeros
     c                   eval      w_kont = rbkont
     c                   endif
      *
     c                   endsr
      /eject
      *
      *
      /eject
      *
      * Subrutine for behandling av posteringer
      *
     c     beh_post      begsr
      *
      * Bilagsdato
      *
     c     *dmy          move      rbbdat        w_udat
      *
      * Nullstill beløpsfelt
      *
     c                   eval      brto = *zeros
     c                   eval      mvag = *zeros
     c                   eval      imva = *zeros
     c                   eval      umva = *zeros
      *
      * Beregn brutto-beløp
      *
     c                   eval      brto = rbmvag + rbmvab + rbinvb
      *
T5.30 * Koder uten grunnlag, f.eks. 0 og 4
      *
T5.30c                   if        brto = *zero
T5.30c                   eval      brto = rbneto
T5.30c                   endif
      *
     c                   eval      bto(x) = bto(x) + brto
T5.30c                   eval      nto(x) = nto(x) + rbneto
     c                   eval      knto(x) = knto(x) + rbneto
      *
      /eject
      *
      * Beregn mva-grunnlag
      *
     c                   eval      mvag = mvag + rbmvag
     c                   eval      grl(x) = grl(x) + rbmvag
      *
      * Inngående MVA (Høy sats)
      *
     c                   if        xmva = 1 or
     c                             xmva = 12
     c                   eval      imva = imva + rbmvab
     c                   eval      imv(x) = imv(x) + rbmvab
     c                   eval      wmva08a = wmva08a + rbmvab
     c                   endif
      *
      * Inngående MVA (Utgår ????)
      *
     c                   if        xmva = 2
     c                   eval      imva = imva + rbmvab
     c                   eval      imv(x) = imv(x) + rbmvab
     c                   eval      wmva08a = wmva08a + rbmvab
     c                   endif
      *
      * Inngående MVA (Middels sats)
      *
     c                   if        xmva = 11
     c                   eval      imva = imva + rbmvab
     c                   eval      imv(x) = imv(x) + rbmvab
     c                   eval      wmva09a = wmva09a + rbmvab
     c                   endif
      *
      * Inngående MVA (Lav sats)
      *
     c                   if        xmva = 21
     c                   eval      imva = imva + rbmvab
     c                   eval      imv(x) = imv(x) + rbmvab
     c                   eval      wmva10a = wmva10a + rbmvab
     c                   endif
      *
      * Inngående avgift på tjenester kjøpt fra utlandet
      *
     c                   if        xmva = 12
V5.40c                   eval      imu(x) = imu(x) + rbmvab
     c                   eval      wmva07a = wmva07a + rbmvab
     c                   eval      wmva07g = wmva07g + rbmvag
     c                   endif
      *
      * Utenfor MVA området
      *
     c                   if        xmva = 10
E5.50c                   eval      wmva01g = wmva01g - rbneto
     c                   endif
      *
      * Utgående MVA (Høy sats)
      *
     c                   if        xmva = 3
     c                   eval      umva = umva + rbmvab
     c                   eval      umv(x) = umv(x) + rbmvab
     c                   eval      wmva01g = wmva01g - rbmvag
     c                   eval      wmva02g = wmva02g - rbmvag
     c                   eval      wmva04g = wmva04g - rbmvag
     c                   eval      wmva04a = wmva04a - rbmvab
     c                   endif
      *
      * Utgående MVA (Middels sats)
      *
     c                   if        xmva = 13
     c                   eval      umva = umva + rbmvab
     c                   eval      umv(x) = umv(x) + rbmvab
     c                   eval      wmva01g = wmva01g - rbmvag
     c                   eval      wmva02g = wmva02g - rbmvag
     c                   eval      wmva05g = wmva05g - rbmvag
     c                   eval      wmva05a = wmva05a - rbmvab
     c                   endif
      *
      * Utgående MVA (Lav sats)
      *
     c                   if        xmva = 23
     c                   eval      umva = umva + rbmvab
     c                   eval      umv(x) = umv(x) + rbmvab
     c                   eval      wmva01g = wmva01g - rbmvag
     c                   eval      wmva02g = wmva02g - rbmvag
     c                   eval      wmva06g = wmva06g - rbmvag
     c                   eval      wmva06a = wmva06a - rbmvab
     c                   endif
      *
      * Avgiftsfritt
      *
     c                   if        xmva = 4
T5.30c                   eval      wmva01g = wmva01g - rbneto
T5.30c                   eval      wmva02g = wmva02g - rbneto
T5.30c                   eval      wmva03g = wmva03g + rbneto
     c                   endif
      *
      * Grunnlag utgående MVA  - høy sats
      *
     c                   if        xmva = 5
     c                   eval      wmva01g = wmva01g - rbmvag
     c                   eval      wmva02g = wmva02g - rbmvag
     c                   eval      wmva04g = wmva04g - rbmvag
     c                   endif
      *
      * Grunnlag utgående MVA  - middels sats
      *
     c                   if        xmva = 15
     c                   eval      wmva01g = wmva01g - rbmvag
     c                   eval      wmva02g = wmva02g - rbmvag
     c                   eval      wmva05g = wmva05g - rbmvag
     c                   endif
      *
      * Grunnlag utgående MVA  - lav sats
      *
     c                   if        xmva = 25
     c                   eval      wmva01g = wmva01g - rbmvag
     c                   eval      wmva02g = wmva02g - rbmvag
     c                   eval      wmva06g = wmva06g - rbmvag
     c                   endif
      /eject
      *
      * Direkte postering - høy sats
      *
     c                   if        xmva = 9
      *
      * Inngående MVA
      *
     c                   if        rbkont = radk02
     c                   eval      imva = imva + rbneto
     c                   eval      imv(x) = imv(x) + rbneto
     c                   eval      wmva08a = wmva08a + rbneto
     c                   endif
      *
      * Utgående MVA
      *
     c                   if        rbkont = radk01
     c                   eval      umva = umva + rbneto
     c                   eval      umv(x) = umv(x) + rbneto
     c                   eval      wmva04a = wmva04a - rbneto
     c                   endif
      *
     c                   endif
      *
      * Direkte postering - middels sats
      *
     c                   if        xmva = 19
      *
      * Inngående MVA
      *
     c                   if        rbkont = radk02
     c                   eval      imva = imva + rbneto
     c                   eval      imv(x) = imv(x) + rbneto
     c                   eval      wmva09a = wmva09a + rbneto
     c                   endif
      *
      * Utgående MVA
      *
     c                   if        rbkont = radk01
     c                   eval      umva = umva + rbneto
     c                   eval      umv(x) = umv(x) + rbneto
     c                   eval      wmva05a = wmva05a - rbneto
     c                   endif
      *
     c                   endif
      *
      * Direkte postering - lav sats
      *
     c                   if        xmva = 29
      *
      * Inngående MVA
      *
     c                   if        rbkont = radk02
     c                   eval      imva = imva + rbneto
     c                   eval      imv(x) = imv(x) + rbneto
     c                   eval      wmva10a = wmva10a + rbneto
     c                   endif
      *
      * Utgående MVA
      *
     c                   if        rbkont = radk01
     c                   eval      umva = umva + rbneto
     c                   eval      umv(x) = umv(x) + rbneto
     c                   eval      wmva06a = wmva06a - rbneto
     c                   endif
      *
     c                   endif
      /eject
      *
      * Skriv heading
      *
     c                   if        *in30 = *on
     c                   write     t1hdr
     c                   eval      *in30 = *off
     c                   endif
      *
      * Skriv postering
      *
     c                   if        rhpsps = 'N'
     c*                            xmva > *zero
      * Skriver heading første gang
     c                   if        fØrste = 0
     c                   add       1             fØrste            1 0
     c                   eval      w_mvak = rbmvak
     c                   eval      w_kont = rbkont
     c                   write     t1deh
     c                   endif
      *
      *
6.21 c                   if        rbkont = w_kont and
6.21 c                             rbmvak = w_mvak
     c                   add       rbneto        t0nto
     c                   add       rbmvag        t0grl
     c                   add       imva          t0imv
     c                   add       umva          t0umv
     c                   add       brto          t0bto
     c                   else
     c                   write     t0det
     c                   add       t0nto         t0tot
     c                   add       t0grl         t0to1
     c                   add       t0imv         t0to2
     c                   add       t0umv         t0to3
     c                   add       t0bto         t0to4
     c                   eval      t0nto = *zeros
     c                   eval      t0grl = *zeros
     c                   eval      t0imv = *zeros
     c                   eval      t0umv = *zeros
     c                   eval      t0bto = *zeros
     c                   eval      w_kont = rbkont
     c                   add       rbneto        t0nto
     c                   add       rbmvag        t0grl
     c                   add       imva          t0imv
     c                   add       umva          t0umv
     c                   add       brto          t0bto
     c                   endif
      *
      *
      * Skriver heading
     c                   if        fØrste = 1 and
     c                             rbmvak <> w_mvak
     c                   eval      w_mvak = rbmvak
     c                   write     t2det
     c                   eval      t0tot = *zeros
     c                   eval      t0tot = *zeros
     c                   eval      t0to1 = *zeros
     c                   eval      t0to2 = *zeros
     c                   eval      t0to3 = *zeros
     c                   eval      t0to4 = *zeros
     c                   write     t1deh
     c                   endif
      *
     c   88              write     d1det
     c                   endif
     c***                endif
      /SPACE 3
      *
      * Oppdater postering
      *
     c                   if        rhptst = 'N'
     c                   eval      rhtrlu_raar = rbraar
     c                   eval      rhtrlu_kont = rbkont
     c                   eval      rhtrlu_spes = rbspes
     c                   eval      rhtrlu_avde = rbavde
     c                   eval      rhtrlu_rper = rbrper
     c                   eval      rhtrlu_bdat = rbbdat
     c                   eval      rhtrlu_tist = rbtist
     c     rhtrlu_key    chain     rhtrlu                             62
      *
     c                   if        *in62 = *off
     c                   move      '1'           rbmmrk
     c                   move      rhpmva        rbmvap
6.11 c                   time                    rbedat
6.11 c                   time                    rbetim
6.11 c                   eval      rbeusr = l_user
     c                   update    rhtrlur
     c                   endif
      *
     c                   endif
      *
     c                   endsr
      *
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for behandling av brudd på mva-kode                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     brudd         begsr
      *
     c                   if        rbmvak = ' '
     c                   move      '0'           amva
     c                   else
     c                   move      rbmvak        amva
     c                   endif
      *
     c                   eval      x = x + 1
      *
     c                   move      amva          mk(x)
      *
      * Hent behandlingskode fra MVA-kode i koderegister
      *
     c                   move      amva          ra05lr_ekod
     c     ra05lr_key    chain     ra05lr                             61
      *
     c                   move      raebmk        xmva
     c                   eval      w_edat = raemda
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for utskrift av fordelingsskjema og mva-oppgave       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     mvatot        begsr
      *
     c                   write     t1hdr
      *
     c     1             do        98            x
      *
     c                   eval      wsbel = wsbel + bto(x)
     c                   eval      wsmvag = wsmvag + grl(x)
     c                   eval      wsimva = wsimva + imv(x)
     c                   eval      wsimvu = wsimvu + imu(x)
     c                   eval      wsumva = wsumva + umv(x)
     c                   eval      wsnett = wsnett + nto(x)
      *
     c                   if        nto(x) <> *zero or
     c                             grl(x) <> *zero or
     c                             bto(x) <> *zero or
     c                             imv(x) <> *zero or
V5.40c                             imu(x) <> *zero or
     c                             umv(x) <> *zero
     c                   eval      t1mk  = mk(x)
     c                   eval      t1bto = bto(x)
     c                   eval      t1grl = grl(x)
     c                   eval      t1imv = imv(x)
     c                   eval      t1umv = umv(x) + imu(x)
     c                   eval      t1nto = nto(x)
      *
     c                   write     t1det
     c                   endif
      *
     c                   enddo
      *
      /eject
      *
     c                   write     t1tot
      *
      * Hent saldo på fylkesskattesjefens konto
      *
     c                   move      l_firm        rifirm
     c                   move      radk04        rhsal1_kont
     c                   move      rads04        rhsal1_spes
     c                   move      rada04        rhsal1_avde
     c                   move      rhprÅr        rhsal1_raar
     c                   move      1             rhsal1_type
      *
     c     rhsal1_key    chain     rhsal1                             61
     c                   if        *in61 = *off
      *
      * Beregn saldo denne periode
      *
     c                   eval      tsal = sal(14)
      *
     c     1             do        rhpper        x
     c                   eval      tsal = tsal + sal(x)
     c                   enddo
      *
     c                   else
     c                   eval      tsal = *zero
     c                   endif
      *
      * Summer periodens beløp
      * til tidligere saldo på konto
      *
     c                   eval      wsfylk = wsumva + wsimva - wsimvu
      *
     c                   eval      wsksal = tsal - wsfylk
      *
      * Beløp avrundes til hele kroner
      *
     c                   eval(h)   oms01g = wmva01g
     c                   eval(h)   oms02g = wmva02g
      *
     c                   eval(h)   oms03g = 0 - wmva03g
      *
     c                   eval(h)   oms04a = wmva04a
     c                   eval(h)   oms04g = wmva04g
      *
     c                   eval(h)   oms05a = wmva05a
     c                   eval(h)   oms05g = wmva05g
      *
     c                   eval(h)   oms06a = wmva06a
     c                   eval(h)   oms06g = wmva06g
      *
     c                   eval(h)   oms07g = wmva07g
     c                   eval(h)   oms07a = wmva07a
      *
     c                   eval(h)   oms08a = wmva08a
      *
     c                   eval(h)   oms09a = wmva09a
      *
     c                   eval(h)   oms10a = wmva10a
      *
     c                   eval      oms11a = oms04a + oms05a + oms06a
     c                                    + oms07a - oms08a - oms10a
T5.30c                                    - oms09a
      *
     c                   if        oms11a > *zero
     c                   move      *on           *in80
     c                   else
     c                   move      *off          *in80
     c                   endif
      *
      * Hent Fylkesskattesjefens konto
      *
     c                   move      radk04        rhovlr_kont
     c                   move      rads04        rhovlr_spes
     c                   move      rada04        rhovlr_avde
      *
     c     rhovlr_key    chain     rhovlr                             61
      *
     c                   eval      t1tek1 = *blanks
     c                   move      '9'           rjmvak
      *
     c                   if        *in61  = *off
     c                   movel     rhtext        t1tek1
     c                   move      rhmvak        rjmvak
     c                   endif
      *
      * Skriv MVA-oppgave
      *
     c                   write     t1mva
      *
      * Legg ut posteringer
      *
     c                   exsr      mvapos
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for å legge ut posteringer                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     mvapos        begsr
      *
     c                   eval      rjreid = 'H'
     c                   eval      rjfirm  = w_firm
     c                   eval      rjmmrk  = '1'
     c                   eval      rjmvap  = rhpmva
     c     *dmy          move      rhpkda        rjbdat
     c                   eval      rjbiln = ww4sgb
     c                   eval      rjdbkr = ' '
     c                   eval      rjbilk = 99
     c                   eval      rjtext = a01txt
     c                   eval      rjfeik = '1'
     c                   eval      rjatte = ' '
     c                   eval      rjtype = 2
     c                   eval      rjrper = rhpper
     c                   move      rhprÅr        rjraar
      *
      * Overført inngående avgift
      *
     c                   if        wsimva <> *zero
     c                   eval      rjneto = wsimva * -1
     c                   eval      rhpost = 'J'
     c                   move      radk02        rjkont
     c                   move      rads02        rjspes
     c                   move      rada02        rjavde
     c                   move      recnr1        rjbunt
610  c                   eval      rjnivo = *blank
610  c                   eval      rjmemb = w_memb
610  c                   time                    rjedat
610  c                   time                    rjetim
610  c                   eval      rjeusr = l_user
     c                   write     rjoupfr
     c                   endif
      *
V5.40 * Overført utgående avgift utenlandske tjenester
V5.40 *
V5.40c                   if        wsimvu <> *zero
V5.40c***                eval      rjneto = wsimvu * -1
V5.41c                   eval      rjneto = wsimvu
V5.40c                   eval      rhpost = 'J'
V5.40c                   move      radk08        rjkont
V5.40c                   move      rads08        rjspes
V5.40c                   move      rada08        rjavde
V5.40c                   move      recnr1        rjbunt
V5.40c                   write     rjoupfr
610  c                   eval      rjnivo = *blank
610  c                   eval      rjmemb = w_memb
610  c                   time                    rjedat
610  c                   time                    rjetim
610  c                   eval      rjeusr = l_user
V5.40c                   endif
      *
      * Overført utgående avgift
      *
     c                   if        wsumva <> *zero
     c                   eval      rjneto = wsumva * -1
     c                   eval      rhpost = 'J'
     c                   move      radk01        rjkont
     c                   move      rads01        rjspes
     c                   move      rada01        rjavde
     c                   move      recnr1        rjbunt
610  c                   eval      rjnivo = *blank
610  c                   eval      rjmemb = w_memb
610  c                   time                    rjedat
610  c                   time                    rjetim
610  c                   eval      rjeusr = l_user
     c                   write     rjoupfr
     c                   endif
      *
      * Overført fylkesskattesjefen
      *
     c                   if        wsfylk <> *zero
     c                   eval      rjneto = wsfylk
     c                   eval      rhpost = 'J'
     c                   move      radk04        rjkont
     c                   move      rads04        rjspes
     c                   move      rada04        rjavde
     c                   move      recnr1        rjbunt
610  c                   eval      rjnivo = *blank
610  c                   eval      rjmemb = w_memb
610  c                   time                    rjedat
610  c                   time                    rjetim
610  c                   eval      rjeusr = l_user
     c                   write     rjoupfr
     c                   endif
      *
     c                   endsr
      *
      /eject
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Finner spool som er generert av jobben
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     spoolnbr      begsr
pdf   /Free
pdf
pdf            QSPRILSP( SPRL0100
pdf                    : %size(SPRL0100)
pdf                    : 'SPRL0100'
pdf                    : errorcode );
pdf   /End-free
pdf  c*
pdf   *
pdf  c                   endsr
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Kaller eget program for generering av xml fil. Eget pgm         *
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     xml_create    begsr
pdf   *
pdf  c                   eval      splfname2 = *blanks
pdf  c                   eval      splfname3 = *blanks
pdf  c                   eval      jobname2 = *blanks
pdf  c                   eval      jobname3 = *blanks
pdf  c                   eval      username2 = *blanks
pdf  c                   eval      username3 = *blanks
pdf  c                   eval      jobnbr2 = *blanks
pdf  c                   eval      jobnbr3 = *blanks
pdf  c                   eval      splfnbr2 = *zeros
pdf  c                   eval      splfnbr3 = *zeros
6.24 c**                 eval      p_btyp = 'MVA-OPPGJØR'
6.24 c                   eval      p_btyp = 'MVAOPPGAVE'
6.20 c                   if        *in81 = *off
6.20 c                   eval      p_dspl = 'MVA - oppgjør, kjørt dato: '+
6.20 c                                      %char(udate)+' Mva periode: '+
6.20 c                                      %char(rhpmva)
6.20 c                   else
6.20 c                   eval      p_dspl = 'Test MVA - oppgjør, kjørt dato: '+
6.20 c                                      %char(udate)+' Mva periode: '+
6.20 c                                      %char(rhpmva)
6.20 c                   endif
6.20 c                   eval      p_refn = 'MOP-' + %char(udate)
6.20 c                   eval      p_tagg0 = *blanks
6.20 c                   eval      p_tagg0val = *blanks
6.20 c                   eval      p_tagg1 = *blanks
6.20 c                   eval      p_tagg1val = *blanks
6.20 c                   eval      p_tagg2 = *blanks
6.20 c                   eval      p_tagg2val = *blanks
6.20 c                   eval      p_tagg3 = *blanks
6.20 c                   eval      p_tagg3val = *blanks
6.20 c                   eval      p_tagg4 = *blanks
6.20 c                   eval      p_tagg4val = *blanks
6.20 c                   eval      p_tagg5 = *blanks
6.20 c                   eval      p_tagg5val = *blanks
6.20 c                   eval      p_tagg6 = *blanks
6.20 c                   eval      p_tagg6val = *blanks
6.20 c                   eval      p_tagg7 = *blanks
6.20 c                   eval      p_tagg7val = *blanks
6.20 c                   eval      p_tagg8 = *blanks
6.20 c                   eval      p_tagg8val = *blanks
6.20 c                   eval      p_tagg9 = *blanks
6.20 c                   eval      p_tagg9val = *blanks
pdf   *
6.20 c**                 call      'AP620R'
6.20 c                   call      'AP627R'
pdf  c                   PARM                    splfname
pdf  c                   PARM                    jobname
pdf  c                   PARM                    username
pdf  c                   PARM                    jobnbr
pdf  c                   PARM                    splfnbr
pdf  c                   PARM                    splfname2
pdf  c                   PARM                    jobname2
pdf  c                   PARM                    username2
pdf  c                   PARM                    jobnbr2
pdf  c                   PARM                    splfnbr2
pdf  c                   PARM                    splfname3
pdf  c                   PARM                    jobname3
pdf  c                   PARM                    username3
pdf  c                   PARM                    jobnbr3
pdf  c                   PARM                    splfnbr3
6.20 c                   parm                    p_tagg0
6.20 c                   parm                    p_tagg0val
6.20 c                   parm                    p_tagg1
6.20 c                   parm                    p_tagg1val
6.20 c                   parm                    p_tagg2
6.20 c                   parm                    p_tagg2val
6.20 c                   parm                    p_tagg3
6.20 c                   parm                    p_tagg3val
6.20 c                   parm                    p_tagg4
6.20 c                   parm                    p_tagg4val
6.20 c                   parm                    p_tagg5
6.20 c                   parm                    p_tagg5val
6.20 c                   parm                    p_tagg6
6.20 c                   parm                    p_tagg6val
6.20 c                   parm                    p_tagg7
6.20 c                   parm                    p_tagg7val
6.20 c                   parm                    p_tagg8
6.20 c                   parm                    p_tagg8val
6.20 c                   parm                    p_tagg9
6.20 c                   parm                    p_tagg9val
pdf  c                   PARM                    l_bach
6.12 c                   PARM                    p_btyp
6.20 c                   PARM                    p_refn
6.20 c                   PARM                    p_dspl
pdf   *
pdf  c                   endsr
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Subrutiner for XML/jasper integrasjon - Skjermvisning           *
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     pdf_preview   begsr
pdf  c                   if        l_skje = 1
pdf   * Vi kaller program for launch av PDF i browser
pdf  c                   call      'AP621R'
pdf  c                   parm                    l_bach
pdf   *
pdf  c                   endif
pdf   *
pdf  c                   endsr
pdf   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
610  c                   parm                    w_memb
      *
     c                   eval      w_msg = 'Start RH664R'
6.22 c*                  call      'DEBUGCL'
6.22 c*                  parm                    w_msg

pdf  c                   open      rh664p
      * Definisjoner av arbeidsfelt
      *
     c                   eval      recnr1 = 1
     c                   eval      x = 0
      *
     c                   eval      wsbel = 0
     c                   eval      wsimva = 0
V5.40c                   eval      wsimvu = 0
     c                   eval      wsumva = 0
     c                   eval      wsnett = 0
     c                   eval      wmva01g = 0
     c                   eval      wmva02g = 0
     c                   eval      wmva03g = 0
     c                   eval      wmva04g = 0
     c                   eval      wmva04a = 0
     c                   eval      wmva05g = 0
     c                   eval      wmva05a = 0
     c                   eval      wmva06g = 0
     c                   eval      wmva07g = 0
     c                   eval      wmva07a = 0
     c                   eval      wmva08a = 0
     c                   eval      wmva09a = 0
     c                   eval      wmva10a = 0
     c                   eval      wmva11a = 0
     c                   eval      page = 0
      *
     c                   eval      mk = *blank
     c                   eval      rhpost = *blank
      *
      * Hent sist brukte bilagsnummer
      *
     c                   move      l_firm        w_firm
     c                   move      rhprÅr        raa4l1_4aar
     c     raa4l1_key    chain     raa4l1                             61
      *
     c                   if        *in61 = *off
     c                   eval      ww4sgb = ra4sgb + 1
     c                   eval      ra4sgb = ww4sgb
     c                   update    raa4l1r
     c                   endif
      *
      * Hent faste kontonr fra koderegister
      *
     c     ra04l1_key    chain     ra04l1                             61
      *
     c                   eval      a01txt = 'OVERFØRT'
      *
     c                   eval      x = 10
      *
T5.60c                   if        rhptst = 'J'
T5.60c                   eval      *in81  = *on
T5.60c                   endif
      *
      * Skriv heading
     c                   write     t1hdr
      *
      *
      *
      * Key for posteringsregister - MVA
      *
     c     rhtrlb_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhtrlb_raar
      *
      * Key for posteringsregister - oppdatering
      *
     c     rhtrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhtrlu_raar
     c                   kfld                    rhtrlu_kont
     c                   kfld                    rhtrlu_spes
     c                   kfld                    rhtrlu_avde
     c                   kfld                    rhtrlu_rper
     c                   kfld                    rhtrlu_bdat
     c                   kfld                    rhtrlu_tist
      *
      * Key for saldo-file
      *
     c     rhsal1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhsal1_kont
     c                   kfld                    rhsal1_spes
     c                   kfld                    rhsal1_avde
     c                   kfld                    rhsal1_raar
     c                   kfld                    rhsal1_type
      *
      * Key for kontoplan
      *
     c     rhovlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhovlr_kont
     c                   kfld                    rhovlr_spes
     c                   kfld                    rhovlr_avde
      *
      * Key for statuskode 4
      *
     c     raa4l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    raa4l1_4aar
      *
      * Key for faste konti
      *
     c     ra04l1_key    klist
     c                   kfld                    w_firm
      *
      * Key for mva-kode
      *
     c     ra05lr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra05lr_ekod
      *
     c                   endsr
      *
