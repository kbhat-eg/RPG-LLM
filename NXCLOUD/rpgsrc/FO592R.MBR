     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FO592R
      * Beskrivelse . . : Ordre-hode, ordrer som plukkes
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       251005 HKO  Nytt program
      *       040206 HKO  Ny logikk
      *       150206 HKO  Lagt inn valg for å merke ordre "Ferdig plukket"
5.54  * R5.54 020407 AMD  Bygget om kjørekontorløsningen til å benytte
      *                   loggfilen i stedet for infokodene
5.54  * R5.54 020407 AMD  Legger ut en streng for info/ordrestatus/kode
5.55  * R5.55 140907 AMD  Korrigering av 14 = Merk ferdigplukket + lar
5.55  *                   ordre ligge i skjermbilde etter ferdigplukk +
5.55  *                   nytt skjermbilde for filter
5.56  * R5.56 261007 AMD  Ved Avbryting av plukking ble kode 4 fjernet.
      *                   Program er endret slik at kode 6 også fjernes
      *                   dersom den finnes på ordren.
5.57  * R5.57 261007 AMD  Ved Avbryting av plukking ble kode 4 fjernet.
5.57  * R5.60 030107 bof  utvidet kundenavn, fjernet saksb.navn
5.60  * R5.60 070108 bof  Utvidet logg med år
5.61  * R5.61 310108 bof  Skifter mellom informasjon m. F11
5.62  * R5.62 280208 bof  F1 med lager og bruker lager blir lagt default
5.63  * R5.63 200608 bof  F11 har blitt F23
      * 6.10  230609 BSA  Oppdatert med sporingsinformasjon
6.nu  * R6.30 280514 bsa  Programmet gjennomgått mtp nummerutvidelser.
6.30  * 6.30  130116 BSA  Filtrerer på forsendelsesmåte
6.31  * 6.30  120416 bof  feilretting parm mot FU900r
6.32  * 6.30  050517 bsa  Lagt inn utskrift av kollietiketter
7.xx  * 7.00  310316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
7.fb  * 6.40  040117 bof  Hvis depositumskrav og ikke betalt.
      *
8.01  *K000   130122 bsa  Skrevet om til bredt skjermbilde.
8.02  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffohelr    if   e           k disk    rename(fohepfr:fohelrr)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffohelb    if   e           k disk    rename(fohepfr:fohelbr)
     ffoheln    if   e           k disk    rename(fohepfr:fohelnr)
     ffohelo    if   e           k disk    rename(fohepfr:fohelor)
     ffohelp    if   e           k disk    rename(fohepfr:fohelpr)
5.60 ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     ffusrl1    if   e           k disk    rename(fusrpfr:fusrl1r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fra09l1    if   e           k disk    rename(ra09pfr:ra09l1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     fvinfl1    if   e           k disk    rename(vinfpfr:vinfl1r)
5.62 fausrlr    if   e           k disk    rename(ausrpfr:ausrlrr)
5.54 ffloglu    uf a e           k disk    rename(flogpfr:floglur)
     ffohelu    uf   e           k disk    rename(fohepfr:fohelur)
6.30 fra17l1    if   e           k disk    rename(ra17pfr:ra17l1r)
7.fb fsdepl1    if   e           k disk    rename(sdeppfr:sdepl1r)
      *
     ffo592d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
5.54  *
5.54  * Definering av datastruktur
5.54  *
5.54 d                 ds
5.54 d d_urec                        80
5.54 d  d_firm                        3  0 overlay(d_urec)
5.60 d  d_aarr                        4  0 overlay(d_urec:4)
6.nu d  d_numm                        8  0 overlay(d_urec:8)
6.nu d  d_suff                        2  0 overlay(d_urec:16)
6.31 d  d_kodb                        1    overlay(d_urec:18)
6.nu d  d_date                         d   overlay(d_urec:19) datfmt(*iso)
6.nu d  d_time                         t   overlay(d_urec:29) timfmt(*iso)
6.nu d  d_user                       10    overlay(d_urec:37)
6.nu d  d_wsid                       10    overlay(d_urec:47)
      *
      * Definering av variabler i nøkler
      *
     d fohelr_numm     s                   like(fonumm)
     d fohelr_suff     s                   like(fosuff)
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d fohelb_kjor     s                   like(fokjor)
     d fohelb_kjnr     s                   like(fokjnr)
     d foheln_ldat     s                   like(foldat)
     d foheln_otyp     s                   like(footyp)
     d foheln_kjor     s                   like(fokjor)
     d foheln_kjnr     s                   like(fokjnr)
     d fohelo_otyp     s                   like(footyp)
     d fohelp_plse     s                   like(foplse)
     d fusrl1_user     s                   like(fbuser)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
     d rkunl1_kund     s                   like(rkkund)
     d ra09l1_ikod     s                   like(raikod)
     d votyl1_otyp     s                   like(vaotyp)
     d vinfl1_ityp     s                   like(vaityp)
5.54 d floglu_aarr     s                   like(fuaarr)
5.54 d floglu_numm     s                   like(funumm)
5.54 d floglu_suff     s                   like(fusuff)
5.54 d floglu_kode     s                   like(fukode)
5.54 d floglu_date     s                   like(fudate)
5.54 d floglu_time     s                   like(futime)
     d fohelu_numm     s                   like(fonumm)
     d fohelu_suff     s                   like(fosuff)
5.62 d ausrlr_user     s                   like(abuser)
5.62 d fodtl1_numm     s                   like(fdnumm)
5.62 d fodtl1_suff     s                   like(fdsuff)
6.30 d ra17l1_qkod     s                   like(raqkod)
7.fb d sdepl1_kund     s                   like(skkund)
7.fb d sdepl1_numm     s                   like(sknumm)
7.fb d sdepl1_tell     s                   like(sktell)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_subf          s              1    inz(*off)
     d b_oppt          s              1    inz(*off)
     d b_avbr          s              1    inz(*off)
5.62 d b_funn          s              1    inz(*off)
6.30 d b_lmat          s              1    inz(*off)
7.fb d b_depo          s              1    inz(*off)
      *
     d w_retu          s              1
     d w_plti_6        s              6  0
      *
     d w_firm          s                   like(fofirm)
     d w_snav          s                   like(raitxt)
6.nu d w_numm          s                   like(fonumm)
5.60 d w_aarr          s              4  0
5.54 d w_suff          s                   like(fosuff)
5.54 d w_svar          s              1
5.54 d w_kode          s              1
5.54 d w_stat          s              1
5.54 d w_date          s               d   datfmt(*iso)
5.54 d w_time          s               t   timfmt(*iso)
5.54 d w_user          s             10
5.54 d w_wsid          s             10
5.54 d w_kod2          s              1
6.30 d w_lmat          s                   like(folmat)
6.30 d w_qtxt          s             20
6.30 d w_jkod          s              2  0
6.30 d w_jtxt          s             20
5.54  *
5.54  * Definering av save-variable
5.54  *
5.54 d s_date          s               d   datfmt(*iso)
5.54 d s_time          s               t   timfmt(*iso)
      *
      * Definering av konstanter
      *
8.01 d c_sfil          s              2  0 inz(16)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
5.55 c                   when      *inka
5.55 c                   exsr      blaalt
5.55 c                   goto      b2tagb
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2tagb
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
8.01 c*                  when      *inkx
8.01 c*                  if        *in51 = *off
8.01 c*                  eval      *in51 = *on
8.01 c*                  else
8.01 c*                  eval      *in51 = *off
8.01 c*                  endif
8.01 c*                  exsr      forny
5.61 c*                  goto      b2tagb
8.01 c*                  goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2ldat <> 0 or
     c                             b2otyp <> *blank or
     c                             b2kjor <> 0 or
     c                             b2numm <> 0 or
     c                             b2plse <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
5.55  *
5.55  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.55  * F1 Behandle bilde for bla-alternativer
5.55  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.55  *
5.55 c     blaalt        begsr
5.55  *
5.55  *  Send Alt
5.55  *
5.55 c     xf1tag        tag
5.55 c                   write     f1win
5.55 c                   exfmt     f1win
5.55  *
5.55  *  F12=Avslutt subrutinen
5.55  *
5.55 c                   if        *inkc or *inkl
5.55 c                   goto      xf1end
5.55 c                   endif
6.30  *
6.30  * F4 - Spørring
6.30  *
6.30 c                   if        *inkd
6.30  *
6.30  * Lager
6.30  *
6.30 c                   if        w_afl1 = 'F1LAGE'
6.30 c                   call      'RA510R'
6.30 c                   parm                    w_firm
6.30 c                   parm                    w_jkod
6.30 c                   parm                    w_jtxt
6.30 c                   if        w_jkod <> 0
6.30 c                   eval      f1lage = w_jkod
6.30 c                   endif
6.30 c                   goto      xf1tag
6.30 c                   endif
6.30  *
6.30  *  Forsendelsesmåte
6.30  *
6.30 c                   if        w_afl1 = 'F1LMA1'
6.30 c                   call      'RA517R'
6.30 c                   parm                    w_firm
6.30 c                   parm                    w_lmat
6.30 c                   parm                    w_qtxt
6.30 c                   if        w_lmat <> 0
6.30 c                   eval      f1lma1 = w_lmat
6.30 c                   endif
6.30 c                   goto      xf1tag
6.30 c                   endif
6.30  *
6.30 c                   if        w_afl1 = 'F1LMA2'
6.30 c                   call      'RA517R'
6.30 c                   parm                    w_firm
6.30 c                   parm                    w_lmat
6.30 c                   parm                    w_qtxt
6.30 c                   if        w_lmat <> 0
6.30 c                   eval      f1lma2 = w_lmat
6.30 c                   endif
6.30 c                   goto      xf1tag
6.30 c                   endif
6.30  *
6.30 c                   if        w_afl1 = 'F1LMA3'
6.30 c                   call      'RA517R'
6.30 c                   parm                    w_firm
6.30 c                   parm                    w_lmat
6.30 c                   parm                    w_qtxt
6.30 c                   if        w_lmat <> 0
6.30 c                   eval      f1lma3 = w_lmat
6.30 c                   endif
6.30 c                   goto      xf1tag
6.30 c                   endif
6.30
6.30 c                   endif
6.30  *
6.30  * Sjekk forsendelsesmåte
6.30  *
6.30 c                   if        f1lma1 <> 0
6.30 c                   eval      ra17l1_qkod = f1lma1
6.30 c     ra17l1_key    chain     ra17l1
6.30 c                   if        not %found
6.30 c                   eval      *in35 = *on
6.30 c                   goto      xf1tag
6.30 c                   endif
6.30 c                   endif
6.30  * Forsendelsesmåte 2
6.30 c                   if        f1lma2 <> 0
6.30 c                   eval      ra17l1_qkod = f1lma2
6.30 c     ra17l1_key    chain     ra17l1
6.30 c                   if        not %found
6.30 c                   eval      *in35 = *on
6.30 c                   goto      xf1tag
6.30 c                   endif
6.30 c                   endif
6.30  * Forsendelsesmåte 3
6.30 c                   if        f1lma3 <> 0
6.30 c                   eval      ra17l1_qkod = f1lma3
6.30 c     ra17l1_key    chain     ra17l1
6.30 c                   if        not %found
6.30 c                   eval      *in35 = *on
6.30 c                   goto      xf1tag
6.30 c                   endif
6.30 c                   endif
5.55  *
5.55  * Posisjonerer databasen, og fyll opp subfilen pånytt
5.55  *
5.55 c                   eval      foheln_ldat = *loval
5.55 c                   eval      foheln_otyp = *blank
5.55 c                   eval      foheln_kjor = 0
5.55 c                   eval      foheln_kjnr = 0
5.55 c     foheln_key    setll     foheln
5.55  *
5.55 c                   exsr      clr_subfile
5.55 c                   exsr      crt_subfile
5.55  *
5.55 c     xf1end        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'T'
     c                   eval      fohelo_otyp = b1otyp
     c     fohelo_key    setll     fohelo
     c                   when      w_seqe = 'R'
     c                   eval      fohelb_kjor = b1kjor
     c                   eval      fohelb_kjnr = b1kjnr
     c     fohelb_key    setll     fohelb
     c                   when      w_seqe = 'O'
     c                   eval      fohel1_numm = b1numm
     c                   eval      fohel1_suff = b1suff
     c     fohel1_key    setll     fohel1
     c                   when      w_seqe = 'S'
     c                   eval      fohelp_plse = b1plse
     c     fohelp_key    setll     fohelp
     c                   other
     c                   if        b1ldat <> 0
     c                   eval      foheln_ldat = %date(b1ldat:*dmy)
     c                   else
     c                   eval      foheln_ldat = *loval
     c                   endif
     c                   eval      foheln_otyp = b1otyp
     c                   eval      foheln_kjor = b1kjor
     c                   eval      foheln_kjnr = b1kjnr
     c     foheln_key    setll     foheln
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Leveringsdato
      *
     c                   if        b2ldat <> 0
     c                   eval      w_seqe = *blank
     c                   eval      foheln_ldat = *loval
     c     *dmy          test(d)                 b2ldat                 31
     c                   if        *in31 = *off
     c     *dmy          move      b2ldat        foheln_ldat
     c                   endif
     c                   eval      foheln_otyp = b2otyp
     c                   eval      foheln_kjor = b2kjor
     c                   eval      foheln_kjnr = 0
     c     foheln_key    setll     foheln
     c                   goto      end_pos
     c                   endif
      *
      * Ordretype
      *
     c                   if        b2otyp <> *blank
     c                   eval      w_seqe = 'T'
     c                   eval      fohelo_otyp = b2otyp
     c     fohelo_key    setll     fohelo
     c                   goto      end_pos
     c                   endif
      *
      * Kjørerute
      *
     c                   if        b2kjor <> 0
     c                   eval      w_seqe = 'R'
     c                   eval      fohelb_kjor = b2kjor
     c                   eval      fohelb_kjnr = 0
     c     fohelb_key    setll     fohelb
     c                   goto      end_pos
     c                   endif
      *
      * Ordrenr
      *
     c                   if        b2numm <> 0
     c                   eval      w_seqe = 'O'
     c                   eval      fohel1_numm = b2numm
     c                   eval      fohel1_suff = 0
     c     fohel1_key    setll     fohel1
     c                   goto      end_pos
     c                   endif
      *
      * Plukkliste selgernr
      *
     c                   if        b2plse <> 0
     c                   eval      w_seqe = 'S'
     c                   eval      fohelp_plse = b2plse
     c     fohelp_key    setll     fohelp
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2ldat = 0
     c                   eval      b2kjor = 0
     c                   eval      b2otyp = *blank
     c                   eval      b2numm = 0
     c                   eval      b2plse = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre ordreinformasjon
      *
     c                   if        b1valg = 2
     c                   call      'FO150R'
     c                   parm                    w_firm
6.nu c                   parm                    b1numm
     c                   parm                    b1suff
     c                   eval      b1valg = 0
     c                   eval      b_forn = *on
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis ordre
      *
     c                   if        b1valg = 5
     c                   call      'FO505R'
     c                   parm                    w_firm
6.nu c                   parm                    b1numm
     c                   parm                    b1suff
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Skriv plukkliste
      *
     c                   if        b1valg = 6
     c                   exsr      utskrift
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Behandle ordre
      *
     c                   if        b1valg = 7
     c                   eval      w_retu = *blank
     c                   call      'FO500R'
6.nu c                   parm                    b1numm
     c                   parm                    w_retu
     c                   eval      b1valg = 0
     c                   eval      b_forn = *on
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Hvem behandler ordre
      *
     c                   if        b1valg = 8
     c                   call      'FO710R'
     c                   parm                    w_firm
6.nu c                   parm                    b1numm
     c                   parm                    b1suff
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Avbryt plukking
      *
     c                   if        b1valg = 9
     c                   exsr      avbr_plukking
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
6.32  *
6.32  * Skriv kollietiketter
6.32  *
6.32 c                   if        b1valg = 10
6.32 c                   call      'FP270R'
6.32 c                   parm                    b1numm
6.32 c                   parm                    b1suff
6.32  *
6.32 c                   eval      b1valg = 0
6.32 c                   eval      b_forn = *on
6.32 c                   update    b1sfl
6.32 c                   iter
6.32 c                   endif
      *
      * Merk ferdig plukket
      *
     c                   if        b1valg = 14
     c                   exsr      merk_plukking
     c                   eval      b1valg = 0
5.55 c                   eval      b_forn = *on
     c                   iter
     c                   endif
      *
      * Utplukk av ordre
      *
     c                   if        b1valg = 16
     c                   call      'FO736R'
     c                   parm                    w_retu
     c                   parm                    w_firm
6.nu c                   parm                    b1numm
     c                   parm                    b1suff
     c                   eval      b1valg = 0
     c                   eval      b_forn = *on
     c                   iter
     c                   endif
5.54  *
5.54  * Viser loggfilen med ordrehistorikk
5.54  *
5.54 c                   if        b1valg = 60
5.60 c                   eval      w_aarr = %subdt(b1bdat:*years)
5.54 c                   eval      w_numm = b1numm
5.54 c                   eval      w_suff = b1suff
5.54 c                   call      'FU500R'
5.54 c                   parm                    w_firm
5.60 c                   parm                    w_aarr
6.nu c                   parm                    w_numm
5.54 c                   parm                    w_suff
5.54 c                   eval      b1valg = 0
5.54  ***                exsr      xfarge
5.54 c                   update    b1sfl
5.54 c                   iter
5.54 c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14  = *on
     c                   write     b2ctl
     c                   eval      *in14  = *off
      *
     c                   eval      *in15  = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'T'
     c                   read      fohelo
     c                   when      w_seqe = 'R'
     c                   read      fohelb
     c                   when      w_seqe = 'O'
     c                   read      fohel1
     c                   when      w_seqe = 'S'
     c                   read      fohelp
     c                   other
     c                   read      foheln
     c                   endsl
      *
     c                   if        %eof or
     c                             fofirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   exsr      chk_subfile
     c                   if        b_subf = *off
     c                   iter
     c                   endif
      *
      * Fyll opp subfile
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'T'
     c                   eval      fohelo_otyp = footyp
     c                   when      w_seqe = 'R'
     c                   eval      fohelb_kjor = fokjor
     c                   eval      fohelb_kjnr = fokjnr
     c                   when      w_seqe = 'O'
     c                   eval      fohel1_numm = fonumm
     c                   eval      fohel1_suff = fosuff
     c                   when      w_seqe = 'S'
     c                   eval      fohelp_plse = foplse
     c                   other
     c                   eval      foheln_ldat = foldat
     c                   eval      foheln_otyp = footyp
     c                   eval      foheln_kjor = fokjor
     c                   eval      foheln_kjnr = fokjnr
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1ldat = 0
     c                   eval      b1otyp = footyp
     c                   eval      b1kjor = fokjor
     c                   eval      b1kjnr = fokjnr
     c                   eval      b1numm = fonumm
     c                   eval      b1suff = fosuff
     c                   eval      b1kund = fokund
     c                   eval      b1kpro = fokpro
     c                   eval      b1adr1 = foadr1
     c                   eval      b1knav = fokuna
8.01 c                   eval      b1lkun = folkun
     c                   eval      b1plse = foplse
     c                   eval      b1plda = 0
     c                   eval      b1plti = 0
     c                   eval      b1ityp = foityp
     c                   eval      b1kode = *blank
5.60 c                   eval      b1bdat = fobdat
      *
     c                   if        foldat <> *loval
     c     *dmy          move      foldat        b1ldat
     c                   endif
      *
     c                   if        s_date <> *loval
     c     *dmy          move      s_date        b1plda
     c                   endif
     c                   if        s_time <> *loval
     c     *hms          move      s_time        w_plti_6
     c                   movel     w_plti_6      b1plti
     c                   endif
      *
     c                   eval      b1knav = *blank
      *
8.01 c*                  if        *in51  = *off
     c                   eval      rkunl1_kund = fokund
     c     rkunl1_key    chain     rkunl1r
     c                   if        %found
     c                   eval      b1knav = rknavn
     c                   endif
8.01 c*                  else
8.01 c**                 movel     b1adr1        b1knav
8.01 c*                  endif
8.01 c                   movel     b1adr1        b1lnav
8.01  *
8.01  * Hvis varekunde, fakturakunde og prosjekt, legg ut varekunde
8.01  *
8.01 c                   if        fokund <> 0 and
8.01 c                             folkun <> 0 and
8.01 c                             fokund <> folkun and
8.01 c                             fokpro <> 0
8.01 c                   eval      rkunl1_kund = folkun
8.01 c     rkunl1_key    chain     rkunl1r
8.01 c                   if        %found
8.01 c                   movel     rknavn        b1knav
8.01 c                   endif
8.01 c                   endif
      *
     c*                  eval      ra09l1_ikod = foplse
     c*    ra09l1_key    chain     ra09l1
     c*                  if        %found
     c*                  eval      b1snav = raitxt
     c*                  endif
      *
7.fb c                   exsr      sjekk_depo
7.fb c                   if        b_depo = *on
7.fb c                   eval      *in45  = *on
7.fb c                   endif
      *
3.34 c                   eval      *in41  = *off
3.34 c                   eval      *in42  = *off
3.34 c                   eval      *in43  = *off
3.34 c                   eval      *in44  = *off
      *
     c                   select
3.34 c                   when      vaifrg = 'H'
3.34 c                   eval      *in41  = *on
3.34 c                   when      vaifrg = 'G'
3.34 c                   eval      *in42  = *on
3.34 c                   when      vaifrg = 'B'
3.34 c                   eval      *in43  = *on
3.34 c                   when      vaifrg = 'R'
3.34 c                   eval      *in44  = *on
3.34 c                   endsl
3.34  *
     c                   select
3.12 c                   when      fokode = 1
3.12 c                   eval      b1kode = '**'
3.12 c                   when      fokode = 2
3.12 c                   eval      b1kode = 'UT'
3.12 c                   when      fokode = 3
3.12 c                   eval      b1kode = 'OK'
3.12 c                   when      fokode = 5
3.12 c                   eval      b1kode = 'VE'
3.12 c                   when      fokode = 7
3.12 c                   eval      b1kode = 'BU'
3.12 c                   endsl
3.12  *
5.54  *
5.54  * Henter inn høyeste ordrestatus
5.54  * og legger over alt i en streng
5.54  *
5.54 c                   eval      w_kod2 = *blank
5.60 c                   eval      w_aarr = %subdt(b1bdat:*years)
5.54 c                   call      'FU706R'
5.60 c                   parm                    w_aarr
6.nu c                   parm                    b1numm
5.54 c                   parm                    b1suff
5.54 c                   parm                    w_kod2
5.54  *
5.54 c                   eval      b1kod2 = w_kod2
5.54 c                   eval      b1alle = b1ityp + b1kod2 + b1kode
5.54  *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'T'
     c     fohelo_key    setgt     fohelo
     c                   readp     fohelo
     c                   when      w_seqe = 'R'
     c     fohelb_key    setgt     fohelb
     c                   readp     fohelb
     c                   when      w_seqe = 'O'
     c     fohel1_key    setgt     fohel1
     c                   readp     fohel1
     c                   when      w_seqe = 'S'
     c     fohelp_key    setgt     fohelp
     c                   readp     fohelp
     c                   other
     c     foheln_key    setgt     foheln
     c                   readp     foheln
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'T'
     c                   readp     fohelo
     c                   when      w_seqe = 'R'
     c                   readp     fohelb
     c                   when      w_seqe = 'O'
     c                   readp     fohel1
     c                   when      w_seqe = 'S'
     c                   readp     fohelp
     c                   other
     c                   readp     foheln
     c                   endsl
      *
     c                   if        %eof or
     c                             fofirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   exsr      chk_subfile
     c                   if        b_subf = *off
     c                   iter
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'T'
     c                   eval      fohelo_otyp = footyp
     c                   when      w_seqe = 'R'
     c                   eval      fohelb_kjor = fokjor
     c                   eval      fohelb_kjnr = fokjnr
     c                   when      w_seqe = 'O'
     c                   eval      fohel1_numm = fonumm
     c                   eval      fohel1_suff = fosuff
     c                   when      w_seqe = 'S'
     c                   eval      fohelp_plse = foplse
     c                   other
     c                   eval      foheln_ldat = foldat
     c                   eval      foheln_otyp = footyp
     c                   eval      foheln_kjor = fokjor
     c                   eval      foheln_kjnr = fokjnr
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'T'
     c     fohelo_key    setll     fohelo
     c                   when      w_seqe = 'R'
     c     fohelb_key    setll     fohelb
     c                   when      w_seqe = 'O'
     c     fohel1_key    setll     fohel1
     c                   when      w_seqe = 'S'
     c     fohelp_key    setll     fohelp
     c                   other
     c     foheln_key    setll     foheln
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sjekke om rad skal være med i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     chk_subfile   begsr
      *
     c                   eval      b_subf = *off
      *
      * Viser ikke ordre dersom det ikke kjøres med kjørekontorløsning
      *
     c                   if        faakjk = 0
     c                   leavesr
     c                   endif
      *
      * Viser kun ordre med behandlingskode 1
      *
     c                   eval      votyl1_otyp = footyp
     c     votyl1_key    chain     votyl1
     c                   if        not %found or
     c                             vaoakk <> 1
     c                   leavesr
     c                   endif
5.62  *
5.62  * Sjekker lagerfilter
5.62  *
5.62 c                   if        f1lage <> 0
5.62 c                   exsr      xsjekk_lag
5.62 c                   if        b_funn = ' '
5.62 c                   leavesr
5.62 c                   endif
5.62 c                   endif
      *
5.54  *
5.54  * Skal bare vise ordre som er under plukking
5.54  * Henter inn høyeste ordrestatus og sjekker om
5.54  * denne er lik 4 eller 7
5.55  * Lar ferdigplukket ligge i skjembildet
5.54  *
5.57  * Henter siste kode.
5.54  *
5.54 c                   eval      w_kode = *blank
5.60 c                   eval      w_aarr = %subdt(fobdat:*years)
5.54 c                   call      'FU706R'
5.60 c                   parm                    w_aarr
6.nu c                   parm                    fonumm
5.54 c                   parm                    fosuff
5.54 c                   parm                    w_kode
5.54  *
5.54 c                   if        w_kode = '4' or
5.55 c                             w_kode = '6' or
5.54 c                             w_kode = '7'
5.54 c                   else
5.54 c                   leavesr
5.54 c                   endif
5.55  *
5.55 c                   if        w_kode = '4' and
5.55 c                             f1ko04 = 1
5.55 c                   leavesr
5.55 c                   endif
5.55  *
5.55 c                   if        w_kode = '6' and
5.55 c                             f1ko06 = 1
5.55 c                   leavesr
5.55 c                   endif
5.55  *
5.55 c                   if        w_kode = '7' and
5.55 c                             f1ko07 = 1
5.55 c                   leavesr
5.55 c                   endif
6.30  *
6.30  * Vi må sjekke forsendelsesmåte
6.30  *
6.30 c                   if        f1lma1 <> 0 or
6.30 c                             f1lma2 <> 0 or
6.30 c                             f1lma3 <> 0
6.30  *
6.30 c                   eval      b_lmat = *off
6.30 c                   if        f1lma1 <> 0 and
6.30 c                             folmat = f1lma1
6.30 c                   eval      b_lmat = *on
6.30 c                   endif
6.30 c                   if        f1lma2 <> 0 and
6.30 c                             folmat = f1lma2
6.30 c                   eval      b_lmat = *on
6.30 c                   endif
6.30 c                   if        f1lma3 <> 0 and
6.30 c                             folmat = f1lma3
6.30 c                   eval      b_lmat = *on
6.30 c                   endif
6.30  *
6.30 c                   if        b_lmat = *off
6.30 c                   leavesr
6.30 c                   endif
6.30  *
6.30 c                   endif
6.54  *
5.54  * Henter ut dato og kl.
5.54  *
5.54 c                   eval      s_date = *loval
5.54 c                   eval      s_time = *loval
5.54 c                   eval      w_kode = '2'
5.60 c                   eval      w_aarr = %subdt(fobdat:*years)
5.54 c                   call      'FU704R'
5.60 c                   parm                    w_aarr
6.nu c                   parm                    fonumm
5.54 c                   parm                    fosuff
5.54 c                   parm                    w_kode
5.54 c                   parm                    w_svar
5.54 c                   parm                    w_date
5.54 c                   parm                    w_time
5.54 c                   parm                    w_user
5.54 c                   parm                    w_wsid
5.54  *
5.54 c                   if        w_svar = 'J'
5.54  *
5.54 c                   if        w_date <> *loval
5.54 c                   eval      s_date = w_date
5.54 c                   endif
5.54 c                   if        w_time <> *loval
5.54 c                   eval      s_time = w_time
5.54 c                   endif
5.54 c                   endif
      *
     c                   eval      b_subf = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12  = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13  = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12  = *off
     c                   eval      *in13  = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
     c                   if        w_afld = 'B2PLSE'
     c                   call      'RA509R'
     c                   parm                    w_firm
     c                   parm                    b2plse
     c                   parm                    w_snav
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å skrive plukkliste
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     utskrift      begsr
      *
      * Dersom ordre er opptatt kalles program som viser hvem som bruker
      * orderen
      *
     c                   exsr      sjekk_ordre
     c                   if        b_oppt = *on
     c                   call      'FO710R'
     c                   parm                    w_firm
6.nu c                   parm                    b1numm
     c                   parm                    b1suff
     c                   leavesr
     c                   endif
      *
      * Skriver plukkliste
      *
     c                   eval      w_retu = *blank
      *
     c                   call      'FO600R'
     c                   parm                    w_retu
     c                   parm                    w_firm
6.nu c                   parm                    b1numm
     c                   parm                    b1suff
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å avbryte plukking
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avbr_plukking begsr
      *
      * Dersom ordre er opptatt kalles program som viser hvem som bruker
      * orderen
      *
     c                   exsr      sjekk_ordre
     c                   if        b_oppt = *on
     c                   call      'FO710R'
     c                   parm                    w_firm
6.nu c                   parm                    b1numm
     c                   parm                    b1suff
     c                   leavesr
     c                   endif
      *
      * Håndterer skjermbilde
      *
     c                   exsr      xc2win
     c                   if        b_avbr = *off
     c                   leavesr
     c                   endif
      *
      * Oppdaterer ordre med blanket plukker-informasjon
      *
     c                   eval      fohelu_numm = b1numm
     c                   eval      fohelu_suff = b1suff
     c     fohelu_key    chain     fohelu
     c                   if        not %found
     c                   leavesr
     c                   endif
      *
     c                   eval      foplse = 0
      *
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = l_user
     c                   update    fohelur
      *
     c                   eval      b_forn = *on
5.54  *
5.54  * Fjerner post som sier at ordren er under plukking
5.54  *
5.60 c                   eval      w_aarr = %subdt(b1bdat:*years)
5.54 c                   eval      floglu_aarr = w_aarr
5.54 c                   eval      floglu_numm = b1numm
5.54 c                   eval      floglu_suff = b1suff
5.54 c                   eval      floglu_kode = '4'
5.54 c                   eval      floglu_date = *hival
5.54 c                   eval      floglu_time = *hival
5.54 c     floglu_key    setll     floglur
5.54 c                   readp     floglur
5.54 c                   if        %eof
5.54 c                   goto      avbr_next
5.54 c                   endif
5.54  *
5.54 c                   if        funumm = b1numm and
5.54 c                             fusuff = b1suff and
5.54 c                             fukode = '4'
5.54 c                   delete    floglur
5.54 c                   endif
5.56  *
5.56  * Fjerner også post som sier at ordre er ferdig plukket
5.56  *
5.56 c     avbr_next     tag
5.60 c                   eval      w_aarr = %subdt(b1bdat:*years)
5.56 c                   eval      floglu_aarr = w_aarr
5.56 c                   eval      floglu_numm = b1numm
5.56 c                   eval      floglu_suff = b1suff
5.56 c                   eval      floglu_kode = '6'
5.56 c                   eval      floglu_date = *hival
5.56 c                   eval      floglu_time = *hival
5.56 c     floglu_key    setll     floglur
5.56 c                   readp     floglur
5.56 c                   if        %eof
5.56 c                   leavesr
5.56 c                   endif
5.56  *
5.56 c                   if        funumm = b1numm and
5.56 c                             fusuff = b1suff and
5.56 c                             fukode = '6'
5.56 c                   delete    floglur
5.56 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å merke ordre "Ferdig plukket"
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     merk_plukking begsr
5.54  *
5.54  * Er ordre allerede merket "Ferdig plukket"
5.54  *
5.54 c                   eval      w_kode = '6'
5.60 c                   eval      w_aarr = %subdt(b1bdat:*years)
5.54  *
5.54 c                   call      'FU702R'
5.60 c                   parm                    w_aarr
6.nu c                   parm                    b1numm
5.54 c                   parm                    b1suff
5.54 c                   parm                    w_kode
5.54 c                   parm                    w_svar
5.54  *
5.54 c                   if        w_svar = 'J'
5.54 c                   leavesr
5.54 c                   endif
5.54  *
5.54  *
5.54  * Oppdaterer loggfilen med "Ferdig plukket"
5.54  *
5.54 c                   eval      d_firm = l_firm
5.54 c                   eval      d_user = l_user
5.54 c                   eval      d_wsid = l_wsid
5.60 c                   eval      d_aarr = %subdt(b1bdat:*years)
5.54 c                   eval      d_numm = b1numm
5.54 c                   eval      d_suff = b1suff
5.54 c                   eval      d_kodb = '6'
5.54 c                   time                    d_date
5.54 c                   time                    d_time
5.54  *
5.54  * Kaller program for oppdatering
5.54  *
5.54 c                   call      'FU900R'
5.54 c                   parm                    w_stat
5.54 c                   parm                    d_urec
5.54  *
      *
      * Oppdaterer ordre med ordre informasjonskode
      *
     c***>               eval      fohelu_numm = b1numm
     c***>               eval      fohelu_suff = b1suff
     c***> fohelu_key    chain     fohelu
     c***>               if        not %found
     c***>               leavesr
     c***>               endif
      *
     c***>               eval      foityp = vaityp
      *
     c***>               update    fohelur
      *
     c***>               eval      b_forn = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker om ordren allerede er til behandling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_ordre   begsr
      *
     c                   eval      b_oppt = *off
      *
      * Sjekker om ordren er i bruk
      *
     c                   eval      fohelr_numm = b1numm
     c                   eval      fohelr_suff = b1suff
     c     fohelr_key    chain     fohelr
     c                   if        %found and
     c                             fokode <> 0
     c                   eval      b_oppt = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * C2 Behandle bilde for avbryting av plukking
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2win        begsr
      *
     c                   eval      b_avbr = *off
      *
     c                   eval      fohel1_numm = b1numm
     c                   eval      fohel1_suff = b1suff
     c     fohel1_key    chain     fohel1r
     c                   if        not %found
     c                   leavesr
     c                   endif
      *
     c                   eval      c2numm = fonumm
     c                   eval      c2suff = fosuff
     c                   eval      c2knav = fokuna
     c                   eval      c2pnav = *blank
5.54 c                   eval      c2upda = 0
5.54 c                   eval      c2upti = 0
5.54 c                   eval      c2upus = *blank
5.54 c                   eval      c2upws = *blank
      *
     c                   eval      rkunl1_kund = fokund
     c     rkunl1_key    chain     rkunl1r
     c                   if        %found
     c                   eval      c2knav = rknavn
     c                   endif
      *
     c                   if        fokpro <> 0
     c                   eval      fkprl1_kund = fokund
     c                   eval      fkprl1_kpro = fokpro
     c     fkprl1_key    chain     fkprl1r
     c                   if        %found
     c                   eval      c2pnav = flnavn
     c                   endif
     c                   endif
5.54  *
5.54 c                   eval      w_kode = '4'
5.60 c                   eval      w_aarr = %subdt(b1bdat:*years)
5.54 c                   call      'FU704R'
5.60 c                   parm                    w_aarr
6.nu c                   parm                    b1numm
5.54 c                   parm                    b1suff
5.54 c                   parm                    w_kode
5.54 c                   parm                    w_svar
5.54 c                   parm                    w_date
5.54 c                   parm                    w_time
5.54 c                   parm                    w_user
5.54 c                   parm                    w_wsid
5.54  *
5.54 c                   if        w_svar = 'J'
5.54  *
5.54 c                   if        w_date <> *loval
5.54 c     *dmy          move      w_date        c2upda
5.54 c                   endif
5.54 c                   if        w_time <> *loval
5.54 c     *hms          move      w_time        c2upti
5.54 c                   endif
5.54 c                   eval      c2upus = w_user
5.54 c                   eval      c2upws = w_wsid
5.54 c                   endif
5.54  *
     c     c2taga        tag
      *
     c                   write     c2win
     c                   exfmt     c2win
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * Spørring
      *
      *
      * Validering
      *
     c                   eval      b_avbr = *on
      *
     c                   endsr
5.62  *
5.62  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.62  * Sjekker om lager finnes på ordrelinje
5.62  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.62  *
5.62 c     xsjekk_lag    begsr
5.62  *
5.62  * Sjekker om oppgitt lagernummer er representert på
5.62  * en av varelinjene på ordren
5.62  *
5.62 c                   eval      b_funn = ' '
5.62 c                   eval      fodtl1_numm = fonumm
5.62 c                   eval      fodtl1_suff = fosuff
5.62 c     fodtl1_key    setll     fodtl1
5.62 c     fodtl1_key    reade     fodtl1
5.62  *
5.62 c                   dow       not %eof
5.62 c                   if        fdlage = f1lage
5.62 c                   eval      b_funn = '1'
5.62 c                   leave
5.62 c                   endif
5.62 c     fodtl1_key    reade     fodtl1
5.62 c                   enddo
5.62  *
5.62 c                   endsr
      *
7.fb  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.fb  * Subrutine for å sjekke om linje har ubetalt depositumskrav
7.fb  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.fb  *
7.fb c     sjekk_depo    begsr
7.fb c                   eval      b_depo = *off
7.fb c                   eval      *in45  = *off
7.fb c                   eval      sdepl1_numm = b1numm
7.fb c                   eval      sdepl1_kund = b1kund
7.fb c                   eval      sdepl1_tell = 0
7.fb c     sdepl1_key    chain     sdepl1r
7.fb c                   if        %found(sdepl1) and
7.fb c                             skdepo > skdepb
7.fb c                   eval      b_depo = *on
7.fb c                   endif
7.fb  *
7.fb c                   endsr
7.fb  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på ordre-hode
      *
     c     fohelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelr_numm
     c                   kfld                    fohelr_suff
      *
      * Key for posisjonering på ordrenr
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      * Key for posisjonering på kjørerute
      *
     c     fohelb_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelb_kjor
     c                   kfld                    fohelb_kjnr
      *
      * Key for posisjonering på leveringsdato
      *
     c     foheln_key    klist
     c                   kfld                    w_firm
     c                   kfld                    foheln_ldat
     c                   kfld                    foheln_otyp
     c                   kfld                    foheln_kjor
     c                   kfld                    foheln_kjnr
      *
      * Key for posisjonering på ordretype
      *
     c     fohelo_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelo_otyp
      *
      * Key for posisjonering på plukkliste selgernr
      *
     c     fohelp_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelp_plse
      *
      * Key for oppslag på ofak kode-register
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på ofak-bruker
      *
     c     fusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fusrl1_user
      *
      * Key for oppslag på kunde-prosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag selger
      *
     c     ra09l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra09l1_ikod
      *
      * Key for oppslag på ordretype
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for oppslag på ordre informasjonskode
      *
     c     vinfl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vinfl1_ityp
      *
      * Key for oppdatering av ordre-hode
      *
     c     fohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelu_numm
     c                   kfld                    fohelu_suff
5.54  *
5.54  * Key for oppdatering av logg-filen
5.54  *
5.54 c     floglu_key    klist
5.54 c                   kfld                    w_firm
5.54 c                   kfld                    floglu_aarr
5.54 c                   kfld                    floglu_numm
5.54 c                   kfld                    floglu_suff
5.54 c                   kfld                    floglu_kode
5.54 c                   kfld                    floglu_date
5.54 c                   kfld                    floglu_time
5.55  *
5.55  * Key for les av bruker
5.62  *
5.62 c     ausrlr_key    klist
5.62 c                   kfld                    ausrlr_user
5.62  *
5.62  * Key for ordrelinje-register
5.62  *
5.62 c     fodtl1_key    klist
5.62 c                   kfld                    w_firm
5.62 c                   kfld                    fodtl1_numm
5.62 c                   kfld                    fodtl1_suff
6.30  *
6.30  * Key for oppslag på forsendelsesmåte
6.30  *
6.30 c     ra17l1_key    klist
6.30 c                   kfld                    w_firm
6.30 c                   kfld                    ra17l1_qkod
7.fb  *
7.fb  * Key for oppslag på depositum-fil
7.fb  *
7.fb c     sdepl1_key    klist
7.fb c                   kfld                    w_firm
7.fb c                   kfld                    sdepl1_numm
7.fb c                   kfld                    sdepl1_kund
7.fb c                   kfld                    sdepl1_tell
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
      * hente lager fra bruker
      *
     c                   eval      ausrlr_user = l_user
     c     ausrlr_key    chain     ausrlr
     c                   if        %found(ausrlr)
     c                   eval      f1lage = ablagr
     c                   endif
      *
      * Hente inn ofak kode-register
      *
     c     fstsl1_key    chain     fstsl1r
      *
      * Henter opplysninger fra bruker
      *
     c                   eval      fusrl1_user = l_user
     c     fusrl1_key    chain     fusrl1
      *
      * Initierer skjermbilde
      *
     c                   eval      *in22  = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      foheln_ldat = *loval
     c                   eval      foheln_otyp = *blank
     c                   eval      foheln_kjor = 0
     c                   eval      foheln_kjnr = 0
     c     foheln_key    setll     foheln
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
