     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : Nexstep
      * Program . . . . : VE120R
      * beskrivelse . . : Vedlikehold av ustillingsvarer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       100419 bsa  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvutsl1    if   e           k disk    rename(vutspfr:vutsl1r)
     fvutslr    if   e           k disk    rename(vutspfr:vutslrr)
     fvutslu    uf a e           k disk    rename(vutspfr:vutslur)
     fvenhl1    if   e           k disk    rename(venhpfr:venhl1r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
      *
     fve120d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d vutslr_tlag     s                   like(vutlag)
     d vutslr_tvar     s                   like(vutvar)
     d vutsl1_tlag     s                   like(vutlag)
     d vutsl1_tvar     s                   like(vutvar)
     d vutslu_tlag     s                   like(vutlag)
     d vutslu_tvar     s                   like(vutvar)
     d venhl1_enhe     s                   like(vaeenh)
     d vvenl1_vare     s                   like(vevare)
     d vvenl1_enhe     s                   like(veenhe)
     d vvarl1_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_kabc          s              1    inz(*off)
      *
     d w_firm          s                   like(vutfir)
     d w_udate         s               d   datfmt(*iso)
     d w_stat          s              1
     d w_disp          s              1
     d w_frad          s               d   datfmt(*iso)
     d w_tild          s               d   datfmt(*iso)
     d w_etxt          s                   like(vaetxt)
     d w_vare          s                   like(vevare)
     d w_gtin          s             14  0
     d w_ldor          s              6  0
      *
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2tlag <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      vutslr_tlag = b1tlag
     c                   eval      vutslr_tvar = b1tvar
     c     vutslr_key    setll     vutslr
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Lager
      *
     c                   if        b2tlag <> 0
     c                   eval      vutslr_tlag = b2tlag
     c                   eval      vutslr_tvar = b2tvar
     c     vutslr_key    setll     vutslr
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2tlag = 0
     c                   eval      b2tvar = *blanks
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   movel     b1tvar        c1tvar
     c                   eval      c1tlag = b1tlag
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1tlag = b1tlag
     c                   movel     b1tvar        d1tvar
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1tlag = b1tlag
     c                   movel     b1tvar        c1tvar
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1tlag = 0
     c                   eval      c1tvar = *blank
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * F4=Spørring
      *
     c                   if        *inkd  = *on
      *
     c                   if        w_afld = 'C1TVAR'
     c                   eval      w_vare = c1tvar
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    c1vtxt
     c                   movel     w_vare        c1tvar
     c                   goto      c1taga
     c                   endif
      *
     c                   if        w_afld = 'C1TLAG'
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    c1tlag
     c                   parm                    c1ltxt
     c                   goto      c1taga
     c                   endif
      *
     c                   endif
      *
     c                   if        c1tlag = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Validering
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      vutsl1_tlag = c1tlag
     c                   eval      vutsl1_tvar = c1tvar
     c     vutsl1_key    chain     vutsl1
     c                   if        %found
     c                   exsr      xc2msg
     c                   goto      c1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1tlag = 0
     c                   eval      c1tvar = *blank
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2tlag = 0
     c                   eval      b2tvar = *blanks
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2msg        begsr
      *
     c                   exfmt     c2msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      vutsl1_tlag = c1tlag
     c                   eval      vutsl1_tvar = c1tvar
     c     vutsl1_key    chain     vutsl1
      *
     c                   if        %found
     c                   eval      c2tenh = vutenh
     c                   eval      c2tgti = vutgti
     c                   eval      c2tpls = vutpls
     c                   if        vutoda <> *loval
     c     *dmy          move      vutoda        c2toda
     c                   else
     c                   eval      c2toda = 0
     c                   endif
     c                   if        vuteda <> *loval
     c     *dmy          move      vuteda        c2teda
     c                   else
     c                   eval      c2teda = 0
     c                   endif
     c                   eval      c2teus = vuteus
     c                   else
     c                   eval      c2tenh = *blank
     c                   eval      c2tpls = *blank
     c                   eval      c2tgti = *blank
     c                   eval      c2teda = 0
     c                   eval      c2toda = 0
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * F4=Spørring
      *
     c                   if        *inkd  = *on
      *
     c                   eval      w_vare = c1tvar
     c                   call      'VE550R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    c2tenh
     c                   parm                    w_etxt
     c                   goto      c2taga
      *
     c                   endif
      *
      * Validering av input
      *
      *
      * Enhet må fylles ut
      *
     c                   if        c2tenh = *blank
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
      * Enhet må finnes
      *
     c                   eval      venhl1_enhe = c2tenh
     c     venhl1_key    chain     venhl1
     c                   if        not %found
     c                   eval      *in33 = *on
     c                   goto      c2taga
     c                   endif
      *
      * Enhet må finnes på varen
      *
     c                   eval      vvenl1_vare = c1tvar
     c                   eval      vvenl1_enhe = c2tenh
     c     vvenl1_key    chain     vvenl1
     c                   if        not %found
     c                   eval      *in33 = *on
     c                   goto      c2taga
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   vutslur
      *
     c                   eval      vutslu_tlag = c1tlag
     c                   eval      vutslu_tvar = c1tvar
     c     vutslu_key    chain     vutslur
      *
     c                   eval      vutenh = c2tenh
     c                   eval      vutpls = c2tpls
      *
      * Hent GTIN nummer basert på vare og enhet
      *
     c**                 eval      vutgti = c2tgti
     c                   eval      w_vare = c1tvar
     c                   eval      w_gtin = 0
     c                   eval      w_ldor = 0
     c                   eval      w_stat = *blanks
     c                   call      'VE713R'
     c                   parm                    w_firm
     c                   parm                    w_ldor
     c                   parm                    w_vare
     c                   parm                    c2tenh
     c                   parm                    w_gtin
     c                   parm                    w_stat
      *
     c                   if        w_gtin = 0
     c                   eval      vutgti = *blanks
     c                   else
     c                   move      w_gtin        vutgti
     c                   endif
      *
     c                   time                    vuteda
     c                   time                    vuteti
     c                   eval      vuteus = l_user
      *
     c                   if        %found
     c                   update    vutslur
     c                   else
     c                   eval      vutfir = w_firm
     c                   eval      vutlag = c1tlag
     c                   eval      vutvar = c1tvar
     c                   time                    vutoda
     c                   time                    vutoti
     c                   eval      vutous = l_user
     c                   write     vutslur
     c                   eval      b_forn = *on
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1tlag = vutlag
     c                   eval      b1tvar = vutvar
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      vutsl1_tlag = d1tlag
     c                   eval      vutsl1_tvar = d1tvar
     c     vutsl1_key    chain     vutsl1
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      vutslu_tlag = d1tlag
     c                   eval      vutslu_tvar = d1tvar
     c     vutslu_key    chain     vutslur
      *
     c                   delete    vutslur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      vutslr
      *
     c                   if        %eof or
     c                             vutfir <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      vutslr_tlag = vutlag
     c                   eval      vutslr_tvar = vutvar
     c                   eval      vvarl1_vare = vutvar
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   eval      vvtek1 = *blanks
     c                   endif
      *
     c                   eval      b1valg = 0
     c                   eval      b1tlag = vutlag
     c                   eval      b1tvar = vutvar
     c                   eval      b1tek1 = vvtek1
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     vutslr_key    setgt     vutslr
     c                   readp     vutslr
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     vutslr
      *
     c                   if        %eof or
     c                             vutfir <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   eval      vutslr_tlag = vutlag
      *
     c                   enddo
      *
     c                   if        %eof
     c     vutslr_key    setll     vutslr
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for les på utstillingsvare
      *
     c     vutslr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vutslr_tlag
     c                   kfld                    vutslr_tvar
      *
      * Key for oppdatering av utstillingsvare
      *
     c     vutslu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vutslu_tlag
     c                   kfld                    vutslu_tvar
      *
      * Key for posisjonering på utstillingsvare
      *
     c     vutsl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vutsl1_tlag
     c                   kfld                    vutsl1_tvar
      *
      *  Key for les av enhet
      *
     c     venhl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    venhl1_enhe
      *
      *  Key for les av VARE/ENHET
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
     c                   kfld                    vvenl1_enhe
      *
      *  Key for les av vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
     c                   time                    w_udate
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      vutslr_tlag = 0
     c                   eval      vutslr_tvar = *blanks
     c     vutslr_key    setll     vutslr
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
