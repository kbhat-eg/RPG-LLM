     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LO405R
      *  Beskrivelse . . : Sletter bestillinger, Parameterinntasting
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     flo405d    cf   e             workstn
      *
     d                 ds
      *                                                      Parameter-inn
     d d_prec                        64
     d  d_bdat                        6  0 overlay(d_prec)
     d  d_uvar                        1  0 overlay(d_prec:7)
     d  d_ldor                        6  0 overlay(d_prec:8)
     d  d_otyp                        2    overlay(d_prec:14)
      *
      * Local Data Area
      *
     d                uds
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variable
      *
     d w_batc          s              1
     d w_bdat          s               d   datfmt(*iso)
     d w_firm          s              3  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Behandle bilde for registrering av parametre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     b1tag         tag
      *
      *  Initierer variable som skal vises i skjermbildet
      *
     c                   eval      b1bdat = 0
     c                   eval      b1uvar = 1
     c                   eval      b1ldor = 0
     c                   eval      b1otyp = *blank
      *
     c     b1taga        tag
      *
     c     b1tagb        tag
     c                   exfmt     b1bld
      *
      *  F3=Gå tilbake  (Avslutt)
      *
     c                   if        *inkc = *on
     c                   goto      end_pgm
     c                   endif
      *
      * F4=Spørring
      *
     c                   if        *inkd  = *on
      *
     c                   if        w_afld = 'B1LDOR'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    b1ldor
     c                   parm                    b1lnav
     c                   goto      b1tagb
     c                   endif
      *
     c                   if        w_afld = 'B1OTYP'
     c                   call      'VA550R'
     c                   parm                    w_firm
     c                   parm                    b1otyp
     c                   goto      b1tagb
     c                   endif
      *
     c                   endif
      *
     c                   if        b1bdat <> 0
     c     *dmy          test(d)                 b1bdat                 31
     c                   if        *in31  = *on
     c                   goto      b1tagb
     c                   endif
     c                   endif
      *
     c                   if        b1bdat = *zero
     c                   eval      *in32  = *on
     c                   goto      b1tagb
     c                   endif
      *
     c                   if        b1uvar = *zero
     c                   if        b1otyp = *blank
     c                   eval      *in33  = *on
     c                   goto      b1tagb
     c                   endif
     c                   endif
      *
      *  Be om bekreftelse på valgte slettinger
      *
     c                   eval      d1tek1 = *blank
     c                   eval      d1tek2 = *blank
     c                   eval      d1tek3 = *blank
     c                   eval      d1tek4 = *blank
      *
     c                   if        b1bdat <> *zero
     c     *dmy          move      b1bdat        w_bdat
     c                   endif
      *
     c                   if        b1uvar = 1
     c                   eval      d1tek1 = '- bestillinger uten varelinjer'
     c                   else
     c                   eval      d1tek1 = '- bestillinger med og uten ' +
     c                                      'varelinjer'
     c                   endif
      *
     c                   if        b1bdat <> *zero
     c                   eval      d1tek2 = '- bestillinger foretatt før ' +
     c                                      %char(b1bdat)
     c                   else
     c                   eval      d1tek2 = '- ingen datobegrensning'
     c                   endif
      *
     c                   if        b1otyp <> *blank
     c                   eval      d1tek3 = '- bestillinger med ordretype ' +
     c                                      %trim(b1otyp)
     c                   endif
      *
     c                   if        b1ldor <> *zero
     c                   eval      d1tek4 = '- bestillinger til lev.nr. ' +
     c                                      %char(b1ldor)
     c                   endif
      *
      *  Legger inntastede opplysninger i Hjelpe-felter for Parameter
      *
     c                   eval      d_uvar = b1uvar
     c                   eval      d_bdat = b1bdat
     c                   eval      d_otyp = b1otyp
     c                   eval      d_ldor = b1ldor
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc = *on or
     c                             *inkl = *on
     c                   goto      b1taga
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Kaller opp neste 'CL'-program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  F10=Legge jobb på jobbkø
      *
     c                   if        *inkj = *on
     c                   eval      w_batc = '1'
     c                   else
     c                   eval      w_batc = '0'
     c                   endif
      *
      *  Kaller opp subprogram
      *
     c                   call      'LO405C'
     c                   parm                    d_prec
     c                   parm                    w_batc
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c                   endsr
