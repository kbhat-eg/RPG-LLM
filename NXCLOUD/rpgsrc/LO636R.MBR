     H/TITLE  ASLAGR: Utskrift av kontroll-liste
     H DECEDIT('0,') DATEDIT(*DMY.)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * Program . . . . : LO636R                                        *
      * Beskrivelse . . : Utskrift av kontroll-liste                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
      *   R2.02   220497  Harmonisert mot Versjon 1.01 av ASOKON        *
5.61  *   R5.61   040209  Lister avvike på totalliste dersom <> enn     *
5.61  *                   inntastet beløp                               *
5.70  * PRGR      021208  Utvidet med prisgruppe                    BOF
      * R6.10     180609  Oppdatert med sporingsinformasjon         BSA
6.10  * R6.10 070909 BSA  Utskriften endret pga utvidelse av referansefelter.
6.11  * R6.10 020713 BSA  Avviksfeltet tåler bare avvik på 9999 kroner.
6.11  *                   Endret i PF til 6 pos, og sjekker mva koden,
6.11  *                   slik at vi ikke forutsetter at det skal være
6.11  *                   mva belastning uansett.
6.20  * R6.20 120112 BSA  Utvidet VP750R med kampanje                   *
6.21  * R6.20 040216 bof  Liten endring i print-filen                   *
6.nu  * r6.30 120614 bof  endret ihht nummerutidelse
6.31  * R6.20 010316 bof  Liten endring i print-filen                   *
6.32  * R6.30 020516 BKV  Bug fix i loop. Hoppet over read              *
6.33  * R6.30 031117 bsa  Tar høyde for mvakode=2                       *
8.01  *PRG2   160622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     FLPSOL1    IPE  E           K DISK    RENAME(LPSOPFR:LPSOL1R)
      *
     FVOTYL1    IF   E           K DISK    RENAME(VOTYPFR:VOTYL1R)
     FVLTYL1    IF   E           K DISK    RENAME(VLTYPFR:VLTYL1R)
      *
     FLSTSL1    IF   E           K DISK    RENAME(LSTSPFR:LSTSL1R)
     FLOHELU    UF   E           K DISK    RENAME(LOHEPFR:LOHELUR)
     FLOHEL1    IF   E           K DISK    RENAME(LOHEPFR:LOHEL1R)
     FLODTL1    IF   E           K DISK    RENAME(LODTPFR:LODTL1R)
     FLOTXL1    IF   E           K DISK    RENAME(LOTXPFR:LOTXL1R)
     FVVARL1    IF   E           K DISK    RENAME(VVARPFR:VVARL1R)
     Frlevl1    IF   E           K DISK    RENAME(RLEVPFR:rlevl1R)
     FAFIRL1    IF   E           K DISK    RENAME(AFIRPFR:AFIRL1R)
     FAFORL1    IF   E           K DISK    RENAME(AFORPFR:AFORL1R)
      *
     FLO636P    O    E             PRINTER
      *
     D                 DS
      *                                                      Parameter-inn
     D  DSPREC                 1     64
     D  DSOTYP                 1      2
6.nu D  DSFBES                 5     12  0
6.nu D  DSTBES                13     20  0
6.nu D  DSFLEV                21     26  0
6.nu D  DSTLEV                27     32  0
6.nu D  DSFDAT                33     42d   datfmt(*iso)
6.nu D  DSTDAT                43     52d   datfmt(*iso)
6.nu D  DSVAL1                53     53  0
6.nu D  DSVAL2                54     54  0
6.nu D  DSVAL3                55     55  0
6.nu D  DSVAL4                56     56  0
6.nu D  DSVAL5                57     57  0
6.nu D  DSVAL6                58     58  0
6.nu D  DSVAL7                59     59  0
6.nu D  dsvva1                60     64  2
      *
      * - - - - - - - - - - - - - -
      * Henter inn data fra LDA
      * - - - - - - - - - - - - - -
      *
     D                UDS
     D  DSRUTI               800    809
     D  DSALIN               856    858  0
      *
     D  DSWSID               901    910
     D  DSUSER               911    920
     D  DSFIRM               944    946  0
      *
     d w_enhe          s                   like(ldenh1)
     d w_kpny          s                   like(ldkpny)
     d w_ipny          s                   like(ldipny)
     d w_lety          s              1  0
     d w_stat          s              1
5.61 d w_ipny1         s                   like(ldipny)
8.01 d w_prgr          s              2
5.70 d w_avde          s              4  0
6.20 d w_kamp          s              5
6.21 d w_atxt          s             30
      *
5.61 d w_sumi1         s                   like(ldipny)
5.61 d w_sumi2         s                   like(ldipny)
5.61 d w_sumi3         s                   like(ldipny)
5.61 d w_vva2          s                   like(dsvva1)
     I/EJECT
     ILPSOL1R       01
     I                                          LKSSRT        L4
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av formater                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Benyttede formater
      * - - - - - - - - - -
      *
      * A1HDR  - Heading A1 på rapport
      * D1DET  - Detail  D1 på rapport, Varelinje
      * E1DET  - Detail  E1 på rapport, Tekstlinje
      * O1DET  - Detail  O1 på rapport, Overføring til/fra neste side
      * T1TOT  - Total   T1 på rapport
      *
      *
      * Forklaring på variabler
      * - - - - - - - - - - - -
      *
      * D1ANT0 - Antall med 0 desimaler
      * D1ANT1 - Antall med 1 desimaler
      * D1ANT2 - Antall med 2 desimaler
      * D1ANT3 - Antall med 3 desimaler
      * D1NPRI - Netto  pris
      * D1BPRI - Brutto pris
      * D1NBEL - Netto  beløp
      * D1KBEL - Kost   beløp
      * D1RABP - Rabatt i prosent (Sum prosent 1 og 2)
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Styre-rutine                                                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Initierings-rutine
      *
     C   01FCYCLE        CASEQ     *ZERO         XSTRSR
     C                   ENDCS
      *
      * Bestillingshode
      *
     C     LKSKOD        CABEQ     'H'           TAG01
      *
      * Varelinje
      *
     C     LKSKOD        CABEQ     'V'           TAG02
      *
      * Kommentar/
      * tekstlinje
      *
     C     LKSKOD        CABEQ     'K'           TAG03
      *
      * Ferdig
      *
     C                   GOTO      XSLUTT
      *
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * A1HDR Behandle heading A1                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     TAG01         TAG
      *
      * Null-stilling av
      * variabler
      *
     C                   Z-ADD     0             O1TRSP
     C                   Z-ADD     0             A1BDAT
     C                   Z-ADD     0             A1LDAT
6.21 C                   MOVE      *BLANK        a1ATXT
     C                   MOVE      *BLANK        DSCTXT
     C                   MOVE      *BLANK        A1VTXT
     C                   MOVE      *BLANK        A1LTXT
     C                   MOVE      *BLANK        A1STXT
      *
      * Skal kontroll-kode
      * slettes ?
      *
     C     DSVAL6        IFEQ      1
     C                   MOVE      LKSONR        WWNUMM
     C                   MOVE      LKSSUF        WWSUFF
     C     KEY01         CHAIN     LOHELUR                            61
     C     *IN61         IFEQ      *OFF
     C                   MOVE      *BLANK        LOBIND
6.10 c                   time                    loedat
6.10 c                   time                    loetim
6.10 c                   eval      loeusr = dsuser
     C                   UPDATE    LOHELUR
     C                   ENDIF
     C                   ENDIF
      *
      * Hent bestillings-heading
      *
     C                   MOVE      LKSONR        WWNUMM
     C                   MOVE      LKSSUF        WWSUFF
     C     KEY01         CHAIN     LOHEL1R                            61
      *
      * Snu dato
      *
     c                   if        lobdat <> *loval
     c     *dmy          move      lobdat        a1bdat
     c                   endif
      *
     c                   if        loldat <> *loval
     c     *dmy          move      loldat        a1ldat
     c                   endif
      *
      * Legg inn bestillingsnummer
      *
     C                   MOVE      LONUMM        A1NUMM
     C                   MOVEL     '/'           A1SUFF
     C                   MOVE      LOSUFF        A1SUFF
      *
      * Hent opplysninger fra LEVERANDØR-REGISTER
      *
     C                   MOVE      LOLDOR        WWLDOR
     C     KEY04         CHAIN     rlevl1R                            64
     C                   MOVE      LOLDOR        A1LDOR
     C                   MOVE      RLNAVN        A1NAVN
     C                   MOVE      RLGATE        A1GATE
     C                   MOVE      RLADR2        A1ADR2
     C                   MOVE      RLSTED        A1STED
      *
      * Finner hvilken mva-sats
      * som skal benyttes
      *
     c                   call      'FÅ705R'
     c                   parm                    wwfirm
     c                   parm                    lobdat
     c                   parm                    WPMVAS
     c                   parm                    WPMVAT
     c                   parm                    WPMVAF
      *
      *  Hent tekst for avdeling
      *
     c                   call      'RS707R'
     c                   parm                    w_stat
     c                   parm                    loavde
6.21 c                   parm                    w_atxt
      *
      *  Hent tekst for innkjøper
      *
     c                   call      'RS709R'
     c                   parm                    w_stat
     c                   parm                    loinnk
     c                   parm                    a1stxt
      *
      *  Hent tekst for lager
      *
     c                   call      'RS710R'
     c                   parm                    w_stat
     c                   parm                    lolage
     c                   parm                    a1ltxt
      *
      *  Sjekk/Hent tekst for valuta
      *
     c                   call      'RS716R'
     c                   parm                    w_stat
     c                   parm                    lovalk
     c                   parm                    a1vtxt
      *
      * Flytte øvrige felter
      *
     C                   MOVE      LONAVN        A2NAVN
     C                   MOVE      LOADR1        A2ADR1
     C                   MOVE      LOADR2        A2ADR2
     C                   MOVE      LOSTED        A2STED
     C                   MOVE      LOMKOD        A1MKOD
     C                   MOVE      LORABP        A1RABP
     C                   MOVE      LOAVDE        A1AVDE
     C                   MOVE      LOVALK        A1VALK
     C                   MOVE      LOLAGE        A1LAGE
     C                   MOVE      LOINNK        A1INNK
      *                    MOVE LOBETB    A1BETB
     C                   MOVE      LOVREF        A1VREF
     C                   MOVE      LODREF        A1DREF
     C                   MOVE      LOREKV        A1REKV
     C                   MOVE      LOBINR        A1BINR
      *
      * Skrive heading
      *
     C     LKSALT        IFNE      5
     C                   Z-ADD     0             WLTELL
     C                   WRITE     A1HDR
     C                   ENDIF
6.11  * Sjekk mva koden, og juster mva satsen
6.33 c                   if        lomkod = 1  or
6.33 c                             lomkod = 2
6.11 c                   eval      wpmvat = 1
6.11 c                   endif
      *
5.61  * Finn detaljlinjer for dette bestillingsnummer
5.61  * og beregn totalt avvik
5.61  *
5.61 c                   move      *blanks       d2slen
5.61 c                   z-sub     dsvva1        w_vva2
5.61 c                   eval      w_sumi1 = *zeros
5.61 c                   eval      w_sumi2 = *zeros
5.61 c                   eval      w_sumi3 = *zeros
5.61 c     lototi        sub       lototr        w_sumi1
5.61 c     w_sumi1       mult(h)   wpmvat        w_sumi2
5.61 c     lototf        sub       w_sumi2       w_sumi3
5.61 c                   if        w_sumi3 < w_vva2 or
5.61 c                             w_sumi3 > dsvva1
5.61 c                   eval      d2slen = w_sumi3
5.61 c                   else
5.61 c                   goto      xslutt
5.61 c                   end
5.61  *
5.61 c                   eval      *in78 = '1'
6.21 c                   movel     w_atxt        a1atxt
5.61  *
     C                   WRITE     A1HDR1                               30
5.61 c                   eval      *in78 = '0'
      *
     C     LKSALT        IFNE      5
     C                   WRITE     A1HDR2
     C                   END
      *
      * Er det overflow ?
      *
     C     LKSALT        IFEQ      5
     C     *IN30         CASEQ     *ON           XOVRFL
     C                   ENDCS
     C                   END
      *
     C                   GOTO      XSLUTT
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * D1DET Behandle detail D1                                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     TAG02         TAG
      *
      * Nullstilling av variabler
      *
     C                   MOVE      *ZERO         D1LINE
     C                   MOVE      *BLANK        D1VARE
     C                   MOVE      *BLANK        D1TEK1
     C                   MOVE      *BLANK        D1ENH1
     C                   MOVE      *BLANK        D1TINE
     C                   MOVE      *ZERO         D1ANT0
     C                   MOVE      *ZERO         D1ANT1
     C                   MOVE      *ZERO         D1ANT2
     C                   MOVE      *ZERO         D1ANT3
     C                   MOVE      *OFF          *IN44
     C                   MOVE      *OFF          *IN46
      *
     C                   MOVE      LKSONR        WWNUMM
     C                   MOVE      LKSSUF        WWSUFF
     C                   MOVE      LKSLIN        WWLINE
     C     KEY02         CHAIN     LODTL1R                            62
      *
      * Hent linje-type
      *
     C                   MOVE      LDLTYP        WWLTYP
     C     KEY08         CHAIN     VLTYL1R                            68
      *
      * Sjekker om tekstlinjer
      *
     C     VALKTX        CABEQ     1             TAG02A
      *
      * Oppslag mot vareregister
      *
     C                   MOVE      LDVARE        WWVARE
     C     KEY11         CHAIN     VVARL1R                            69
      *
      * Finn innkjøpspris fra register.
      *
     c                   eval      w_lety = 0
5.70 c                   exsr      hent_prisgr
6.20 c                   eval      w_kamp = lokamp
      *
     c                   call      'VP750R'
     c                   parm                    wwfirm
     c                   parm                    ldvare
5.70 c                   parm                    w_prgr
     c                   parm                    w_lety
     c                   parm                    lobdat
     c                   parm                    w_enhe
     c                   parm                    w_kpny
     c                   parm                    w_ipny
6.20 c                   parm                    w_kamp
      *
      * Test på antall desimaler
      *
     C     VVDESI        COMP      0                                      40
     C     VVDESI        COMP      1                                      41
     C     VVDESI        COMP      2                                      42
     C     VVDESI        COMP      3                                      43
     C   40              Z-ADD     LDANTA        D1ANT0
     C   41              Z-ADD     LDANTA        D1ANT1
     C   42              Z-ADD     LDANTA        D1ANT2
     C   43              Z-ADD     LDANTA        D1ANT3
      *
      * Varelinje
      *
     C                   MOVE      LDLINE        D1LINE
     C                   MOVE      LDVARE        D1VARE
     C                   MOVE      LDTEK1        D1TEK1
     C                   MOVE      LDENH1        D1ENH1
      *
      * Brutto pris
      *
     C                   Z-ADD     LDIPNY        D1BPRI
      *
      * Netto pris
      *
     C                   Z-ADD     LDANTA        W1ANTA
     C     W1ANTA        IFEQ      0
     C                   Z-ADD     1             W1ANTA
     C                   ENDIF
     C     LDSUMI        SUB       LDSUMR        W1NPRI
     C     W1NPRI        DIV       W1ANTA        D1NPRI
      *
      * Beregne snittrabatt
      *
     C     100           SUB       LDRAB1        W1RAB1
     C                   MULT      LDRAB2        W1RAB1
     C                   DIV       100           W1RAB1
     C                   ADD       LDRAB1        W1RAB1
     C                   Z-ADD     W1RAB1        D1RABP
      *
      * Sum Beløp
      *
     C                   Z-ADD     LDSUMK        D1KBEL
     C     LDSUMI        SUB       LDSUMR        D1NBEL
     C     D1BPRI        SUB       w_ipny        D1DIFF
      *
     C                   Z-ADD     0             WWDEKN
     C     D1NBEL        IFNE      0
     C     D1DIFF        DIV(H)    D1NBEL        WWDEKN
     C     WWDEKN        MULT(H)   100           D1GRAD
     C                   END
      *
      * Skal mva. beregnes ?
      * Instruksjoner er foreløpig
      * sperret da mva. legges til
      * bare på bunnlinjen.
      *
     C     AFMVAK        IFEQ      1
     C                   MOVE      *ON           *IN37
      *          D1NPRI    MULT WPMVAT    D1NPRI
      *          D1BPRI    MULT WPMVAT    D1BPRI
      *          D1NBEL    MULT WPMVAT    D1NBEL
     C                   ENDIF
      *
      * Akkumuler til overføringssum
      *
     C                   ADD       D1NBEL        O1TRSP
      *
      * Skriv varelinjer
      *
     C     TAG02A        TAG
     C     VALKTX        IFNE      1
     C     LKSALT        IFNE      5
     C                   ADD       1             WLTELL
     C                   WRITE     D1DET                                30
     C                   END
     C                   ELSE
     C                   MOVE      LDTEK1        D1TEK1
     C                   MOVE      LDTEK2        D1TEK2
     C                   MOVE      *ON           *IN44
     C     LKSALT        IFNE      5
     C                   ADD       1             WLTELL
     C                   WRITE     D2DET                                30
     C                   END
     C                   END
      *
      * Er det overflow ?
      *
     C     LKSALT        IFNE      5
     C     *IN30         CASEQ     *ON           XOVRFL
     C                   ENDCS
     C                   END
      *
     C                   GOTO      XSLUTT
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Behandling av tekstlinjer                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     TAG03         TAG
      *
     C                   MOVE      LKSONR        WWNUMM
     C                   MOVE      LKSSUF        WWSUFF
     C                   MOVE      LKSLIN        WWLINE
     C     KEY03         CHAIN     LOTXL1R                            63
      *
      * Gå ut dersom det
      * ikke er oppsamling,
      * og linje-nummer er
      * større enn 5000.
      * Da skal print fore-
      * gå tilslutt på L4.
      *
     C     VAOSAM        IFEQ      0
     C     LKSLIN        IFGE      5000
     C                   GOTO      XSLUTT
     C                   END
     C                   ENDIF
      *
     C                   MOVEL     LTTEXT        E1TEXT
     C     LKSALT        IFNE      5
     C                   ADD       1             WLTELL
     C                   WRITE     E1DET                                30
     C                   END
      *
      * Er det overflow ?
      *
     C     LKSALT        IFNE      5
     C     *IN30         CASEQ     *ON           XOVRFL
     C                   ENDCS
     C                   END
      *
     C     XSLUTT        TAG
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * T1TOT Behandle total T1                                         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Null-stille variabler
      *
     CL4                 Z-ADD     0             T1TOTB
     CL4                 Z-ADD     0             T1TMVA
     CL4                 Z-ADD     0             T1MVAS
      *
      * Netto beløp
      *
     CL4   LOTOTI        SUB       LOTOTR        T1TNET
     CL4                 Z-ADD     LOTOTK        T1KOST
      *
      * Skal mva. beregnes ?
      *
     CL4                 MOVE      *ON           *IN37
     CL4   T1TNET        MULT(H)   WPMVAT        T1TOTB
     CL4   T1TOTB        SUB       T1TNET        T1TMVA
     CL4                 Z-ADD     WPMVAS        T1MVAS
      *
      * Behandler direkte fakturasum
      *
     CL4                 Z-ADD     LOTOTF        T1FSUM
     CL4   T1FSUM        SUB       T1TOTB        T1DIFF
      *
     CL4                 MOVE      *OFF          *IN38
     CL4   T1FSUM        IFNE      *ZERO
     CL4                 MOVE      *ON           *IN38
     CL4                 ENDIF
      *
      * Finne ut om det er
      * plass til å skrive
      * avslutning uten å
      * skifte til ny side
      * først.
      *
      * Trekker ut heading
      *
     CL4   DSALIN        SUB       24            WLTEST
      *
      * Trekker ut antall
      * linjer skrevet
      *
     CL4                 SUB       WLTELL        WLTEST
      *
      * Trekker ut plassbehov
      * for avslutning
      *
     CL4                 SUB       10            WLTEST
      *
      * Skrive ny heading ?
      *
     CL4   WLTEST        IFLT      0
     CL4   LKSALT        IFNE      5
     CL4                 WRITE     A1HDR
     CL4                 WRITE     A1HDR1
     CL4                 WRITE     A1HDR2
     CL4                 Z-ADD     0             WLTELL
     CL4                 END
     CL4                 ENDIF
      *
      * Skrive totalsum
      *
     CL4   LKSALT        IFNE      5
     CL4   DSVAL7        IFEQ      *ZERO
     CL4                 WRITE     T1TOT                                30
     CL4                 ENDIF
     CL4                 ENDIF
      *
      * Skrive tekstlinjer
      * dersom det ikke er
      * oppsamling og linje
      * nummer er større enn
      * 5000.
      *
     CL4   VAOSAM        IFEQ      0
     CL4                 MOVE      LKSONR        WWNUMM
     CL4                 MOVE      LKSSUF        WWSUFF
     CL4                 Z-ADD     5000          WWLINE
     CL4   KEY03         SETLL     LOTXL1R
     CL4                 READ      LOTXL1R                                63
     CL4   *IN63         CABEQ     *ON           TAG99
      *
     CL4   WWNUMM        DOWEQ     LTNUMM
     CL4   WWSUFF        IFEQ      LTSUFF
      *
     CL4                 MOVEL     LTTEXT        E1TEXT
     CL4                 ADD       1             WLTELL
     CL4   LKSALT        IFNE      5
     CL4                 WRITE     E1DET                                30
     CL4                 ENDIF
      *
      * Er det overflow ?
      *
     CL4   *IN30         IFEQ      *ON
     CL4   LKSALT        IFNE      5
     CL4                 WRITE     A1HDR
     CL4                 WRITE     A1HDR1
     CL4                 WRITE     A1HDR2
     CL4                 Z-ADD     0             WLTELL
     CL4                 ENDIF
     CL4                 ENDIF
      *
6.32 CL4                 ENDIF
     CL4                 READ      LOTXL1R                                63
     CL4   *IN63         CABEQ     *ON           TAG99
6.32 C*                  ENDIF
     CL4                 ENDDO
     CL4                 ENDIF
      *
     CL4   TAG99         TAG
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for overflow                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XOVRFL        BEGSR
      *
     C     LKSALT        IFNE      5
     C                   MOVE      *ON           *IN45
     C                   WRITE     O1DET
     C                   END
      *
     C                   WRITE     A1HDR
      *
     C     LKSALT        IFNE      5
     C                   WRITE     A1HDR1
     C                   WRITE     A1HDR2
     C                   END
      *
     C     LKSALT        IFNE      5
     C                   MOVE      *OFF          *IN45
     C                   WRITE     O1DET
     C                   END
      *
     C                   Z-ADD     0             WLTELL
      *
     C                   ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for oppstart                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XSTRSR        BEGSR
      *
     C                   Z-ADD     1             FCYCLE            1 0
      *
      * Legger inn firma i nøkkel
      *
     C                   MOVE      LKSFIR        WWFIRM
     C                   MOVE      LKSFIR        A1FIRM
      *
      * Klargjøring dersom firma -
      * opplysninger skal printes ut
      *
     C     KEY06         CHAIN     AFIRL1R                            66
     C                   MOVE      AAFNAV        A1FNAV
      *
      * Hente Leverandør engangs-nummer
      *
     C     KEY09         CHAIN     LSTSL1R                            69
      *
     C     *IN69         IFEQ      *OFF
     C                   MOVE      LAALEV        W0LDOR
     C                   END
      *
      * Slår opp ordretype
      *
     C                   MOVE      LKSOTY        WWOTYP
     C     KEY07         CHAIN     VOTYL1R                            67
      *
     C                   MOVEL     VAOTXT        A1TXT1
     C                   MOVE      LKSOTY        A1OTYP
      *
      * Er ordretype negativ ?
      *
     C     VAOKRE        IFEQ      '-'
     C                   MOVE      *ON           *IN36
     C                   ENDIF
      *
      * Skrive heading
      *
     C     LKSALT        IFEQ      5
     C                   Z-ADD     0             WLTELL
     C                   WRITE     A1HDR
     C                   ENDIF
      *
     C                   ENDSR
      *
5.70  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.70  *  Subrutine for å hente prisgruppe
5.70  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.70 c     hent_prisgr   begsr
5.70  *
5.70 c                   eval      w_avde = 0
5.70 c                   call      'VL712R'
5.70 c                   parm                    wwfirm
5.70 c                   parm                    lolage
5.70 c                   parm                    w_avde
5.70 c                   parm                    w_prgr
5.70  *
5.70 c                   endsr
      *
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     *INZSR        BEGSR
      *
     C     *LIKE         DEFINE    LAALEV        W0LDOR
     C     *LIKE         DEFINE    VAOTYP        WWOTYP
     C     *LIKE         DEFINE    LOLDOR        WWLDOR
     C     *LIKE         DEFINE    LOFIRM        WWFIRM
     C     *LIKE         DEFINE    LOWSID        WWWSID
     C     *LIKE         DEFINE    LONUMM        WWNUMM
     C     *LIKE         DEFINE    LOSUFF        WWSUFF
     C     *LIKE         DEFINE    LDLINE        WWLINE
     C     *LIKE         DEFINE    LDLTYP        WWLTYP
     C     *LIKE         DEFINE    LDNUMM        SANUMM
     C     *LIKE         DEFINE    VVVARE        WWVARE
     C     *LIKE         DEFINE    LDANTA        W1ANTA
     C     *LIKE         DEFINE    LDIPNY        W1RAB1
     C     *LIKE         DEFINE    LDIPNY        W1NPRI
     C     *LIKE         DEFINE    LDIPNY        D1NPRI
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     C                   MOVE      *ZERO         WPMVAS            6 2
     C                   MOVE      *ZERO         WPMVAT            5 4
     C                   MOVE      *ZERO         WPMVAF           10 9
      *
     C                   MOVE      *ZERO         WPPDAT            8 0
     C                   MOVE      *ZERO         WPPOMR           10 6
      *
     C                   MOVE      *ZERO         WWDEKN           13 4
      *
     C                   MOVE      *BLANK        WWRUTI           10
     C                   MOVE      *ZERO         WLTELL            3 0
     C                   MOVE      *ZERO         WLTEST            3 0
      *
      * Key for Heading-file
      *
     C     KEY01         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWNUMM
     C                   KFLD                    WWSUFF
      *
      * Key for Detalj-file
      *
     C     KEY02         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWNUMM
     C                   KFLD                    WWSUFF
     C                   KFLD                    WWLINE
      *
      * Key for Tekst-linjer
      *
     C     KEY03         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWNUMM
     C                   KFLD                    WWSUFF
     C                   KFLD                    WWLINE
      *
      * Key for Leverandør-register
      *
     C     KEY04         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWLDOR
      *
      * Key for Firmaregister
      *
     C     KEY06         KLIST
     C                   KFLD                    WWFIRM
      *
      * Key for ordretyper
      *
     C     KEY07         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWOTYP
      *
      * Key for linjetyper
      *
     C     KEY08         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWLTYP
      *
      * Key for statusregister
      *
     C     KEY09         KLIST
     C                   KFLD                    WWFIRM
      *
      * Key for formular-register
      *
     C     KEY10         KLIST
     C                   KFLD                    WWRUTI
      *
      * Key for vare-register
      *
     C     KEY11         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWVARE
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     *ENTRY        PLIST
     C                   PARM                    WWPARM           64
      *
     C                   MOVE      WWPARM        DSPREC
      *
      * Legger inn arb.stasjon og bruker i faste felter
      *
     C                   MOVE      DSUSER        A1USER
     C                   MOVE      DSWSID        A1WSID
     C                   MOVE      UDATE         A1DATE
     C                   TIME                    UTIME             6 0
     C                   MOVE      UTIME         A1TIME
      *
      * Hente inn rutinenavn
      * og meldingstekst
      *
     C                   MOVE      DSRUTI        WWRUTI
     C     KEY10         CHAIN     AFORL1R                            69
     C  N69              MOVE      AFMLD1        T1MLD1
      *
     C                   ENDSR
