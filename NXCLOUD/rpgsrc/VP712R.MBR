     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VP712R  (Erstatter vp701r fra VA)
      * Beskrivelse . . : Vare-pris, henter priser for enhet II valgt lev.nr
      *
      *                   Input:  Firma, prisgr, vare, leverendør enhet,
      *                           leveringstype, dato og LR-flagg
      *                   Output: Salgspris, kostpris, innkjøpspris og
      *                           butikkpris
      *
      *                   LR-flagg må være lik '1' for at *inLR ikke skal
      *                   settes
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       120599 HKO  Nytt program
5.70  * PRGR  231008 BOF  Utvidet med prisgruppe
6.20  *       220311 BOF  Utvidet med prisgiver på vare-pris
6.22  *       100112 BOF  skal kun bruke pris på leverandør som kommer
6.23  *       180913 BOF  Fjerne at innk.pris blir satt til kostpr. hvis 0
6.24  *       080114 BOF  Hvis bytte av hovedlev.,bruk hlev. pris
6.30  *       200814 BOF  Utvidet med innkjøpspris2
      *
8.01  *PRG2   150622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvprl1    if   e           k disk    rename(vvprpfr:vvprl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
      *
      * Definering av pamametre
      *
     d p_firm          s                   like(vpfirm)
     d p_vare          s                   like(vpvare)
     d p_ldor          s                   like(vpldor)
5.70 d p_prgr          s                   like(vpprgr)
     d p_enhe          s                   like(vpenhe)
     d p_lety          s                   like(vplety)
     d p_dato          s                   like(vppdat)
     d p_sapr          s                   like(vpsapr)
     d p_bupr          s                   like(vpbupr)
     d p_kopr          s                   like(vpkopr)
     d p_inpr          s                   like(vpinpr)
6.30 d p_inp2          s                   like(vpinpr)
6.24 d p_hlev          s              1  0
      *
     d p_inlr          s              1
      *
      * Definering av variable i nøkler
      *
     d vvprl1_vare     s                   like(vpvare)
6.20 d vvprl1_ldor     s                   like(vpldor)
5.70 d vvprl1_prgr     s                   like(vpprgr)
     d vvprl1_enhe     s                   like(vpenhe)
     d vvprl1_lety     s                   like(vplety)
     d vvarl1_vare     s                   like(vvvare)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
      *
      * Definering av variable
      *
     d b_pris          s              1    inz(*off)
6.20 d b_inlr          s              1    inz(*off)
      *
     d w_firm          s                   like(vpfirm)
     d w_dato          s                   like(vppdat)
6.20 d w_ldor          s                   like(vvldor)
6.20 d w_lage          s              2  0
      *
6.20 d                uds
6.20 d l_lage               1001   1002  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
6.20 c                   eval      w_lage = l_lage
     c                   eval      w_ldor = p_ldor
      *
6.20 c                   eval      vvarl1_vare = p_vare
6.20 c     vvarl1_key    chain     vvarl1
      *
      * Initierer dato
      *
     c                   eval      w_dato = p_dato
      *
     c                   if        w_dato = *loval
     c                   time                    w_dato
     c                   endif
      *
      * Initierer output-parametre
      *
     c                   eval      p_sapr = 0
     c                   eval      p_bupr = 0
     c                   eval      p_kopr = 0
     c                   eval      p_inpr = 0
6.30 c                   eval      p_inp2 = 0
6.24  *
6.24 c                   if        p_hlev = 1
6.24  *  hovedleverandør
6.24 c                   eval      vvprl1_ldor = vvldor
6.24 c                   exsr      hent_pris
6.24 c                   else
      *
      * Henter pris for vare, leveringstype og enhet
      *
6.20  * 1. leverandør for lageret
6.20 c                   eval      vvprl1_ldor = w_ldor
6.20 c                   exsr      hent_pris
6.22 c*                  if        b_pris = *off  and
6.22 c*                            w_ldor <> vvldor
6.22  * 2. hovedleverandør
6.22 c*                  eval      vvprl1_ldor = vvldor
6.22 c*                  exsr      hent_pris
6.22 c*                  if        b_pris = *off
6.22  * siste mulighet...
6.22 c*                  eval      vvprl1_ldor = 0
6.22 c*                  exsr      hent_pris
6.22 c*                  endif
6.22 c*                  endif
6.24 c                   endif
      *
      *
      * Avbryter dersom pris ikke ble funnet
      *
     c                   if        b_pris = *off
     c                   goto      avslutt
     c                   endif
      *
      * Dersom kostpris er 0 hentes kostpris på bakgrunn av DG-% på vare
      * eller undergruppe
      *
     c                   if        p_kopr = 0
      *
     c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1                             90
     c                   if        *in90 = *on or
     c                             vvdekn = 0
     c                   eval      vugrl1_uogr = vvogrp
     c                   eval      vugrl1_uhgr = vvhgrp
     c                   eval      vugrl1_uugr = vvugrp
     c     vugrl1_key    chain     vugrl1r                            90
     c                   if        *in90 = *off
     c                   eval      vvdekn = vgudek
     c                   endif
     c                   endif
      *
     c                   eval(h)   p_kopr = p_sapr * vvdekn / 100
     c                   if        p_kopr <> 0
     c                   eval      p_kopr = p_sapr - p_kopr
     c                   endif
      *
     c                   endif
      *
      * Innkjøpspris settes lik kostpris dersom innkjøpspris er 0
      *
6.23 c*                  if        p_inpr = 0
6.23 c*                  eval      p_inpr = p_kopr
6.23 c*                  endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   if        p_inlr <> *on
     c                   eval      *inlr = *on
     c                   endif
      *
     c                   return
6.20  *--------------------------------------------------------------------------------
6.20  * Henter priser med evt. valgt prisgruppe, evt. uten
6.20  **** koden er flyttet for å få en mer fleksibel kode ***
6.20  *--------------------------------------------------------------------------------
6.20  *
6.20 c     hent_pris     begsr
6.20  *
6.20 c                   eval      b_pris = *off
6.20  *
6.20 c                   eval      vvprl1_vare = p_vare
6.20 c                   eval      vvprl1_prgr = p_prgr
6.20 c                   eval      vvprl1_enhe = p_enhe
6.20 c                   eval      vvprl1_lety = p_lety
6.20 c     vvprl1_key    setll     vvprl1
6.20 c     vvprl1_key    reade     vvprl1                                 90
6.20 c                   dow       *in90 = *off
6.20  *
6.20 c                   eval      b_pris = *on
6.20 c                   eval      p_sapr = vpsapr
6.20 c                   eval      p_bupr = vpbupr
6.20 c                   eval      p_kopr = vpkopr
6.20 c                   eval      p_inpr = vpinpr
6.30 c                   eval      p_inp2 = vpinp2
6.20  *
6.20 c                   if        w_dato >= vppdat
6.20 c                   leave
6.20 c                   endif
6.20  *
6.20 c     vvprl1_key    reade     vvprl1                                 90
6.20 c                   enddo
6.20  *
6.20  * Dersom pris ikke finnes for oppgitt leveringstype, hentes pris fra
6.20  * leveringstype 0
6.20  *
6.20 c                   if        b_pris = *off and
6.20 c                             p_lety <> 0
6.20  *
6.20 c                   eval      vvprl1_vare = p_vare
6.20 c                   eval      vvprl1_prgr = p_prgr
6.20 c                   eval      vvprl1_enhe = p_enhe
6.20 c                   eval      vvprl1_lety = 0
6.20 c     vvprl1_key    setll     vvprl1
6.20 c     vvprl1_key    reade     vvprl1                                 90
6.20 c                   dow       *in90 = *off
6.20  *
6.20 c                   eval      b_pris = *on
6.20 c                   eval      p_sapr = vpsapr
6.20 c                   eval      p_bupr = vpbupr
6.20 c                   eval      p_kopr = vpkopr
6.20 c                   eval      p_inpr = vpinpr
6.30 c                   eval      p_inp2 = vpinp2
6.20  *
6.20 c                   if        w_dato >= vppdat
6.20 c                   leave
6.20 c                   endif
6.20  *
6.20 c     vvprl1_key    reade     vvprl1                                 90
6.20 c                   enddo
6.20  *
6.20 c                   endif
6.20  *
6.20  * Dersom pris ikke finnes for oppgitt prisgruppe, hentes pris fra
6.20  * prisgruppe = *blank
6.20  *
6.20 c                   if        b_pris = *off and
6.20 c                             p_prgr <> *blank
6.20  *
6.20 c                   eval      vvprl1_vare = p_vare
6.20 c                   eval      vvprl1_prgr = *blank
6.20 c                   eval      vvprl1_enhe = p_enhe
6.20 c                   eval      vvprl1_lety = p_lety
6.20 c     vvprl1_key    setll     vvprl1
6.20 c     vvprl1_key    reade     vvprl1                                 90
6.20 c                   dow       *in90 = *off
6.20  *
6.20 c                   eval      b_pris = *on
6.20 c                   eval      p_sapr = vpsapr
6.20 c                   eval      p_bupr = vpbupr
6.20 c                   eval      p_kopr = vpkopr
6.20 c                   eval      p_inpr = vpinpr
6.30 c                   eval      p_inp2 = vpinp2
6.20  *
6.20 c                   if        w_dato >= vppdat
6.20 c                   leave
6.20 c                   endif
6.20  *
6.20 c     vvprl1_key    reade     vvprl1                                 90
6.20 c                   enddo
6.20  *
6.20  * Dersom pris ikke finnes for oppgitt leveringstype, hentes pris fra
6.20  * leveringstype 0
6.20  *
6.20 c                   if        b_pris = *off and
6.20 c                             p_lety <> 0
6.20  *
6.20 c                   eval      vvprl1_vare = p_vare
6.20 c                   eval      vvprl1_prgr = *blank
6.20 c                   eval      vvprl1_enhe = p_enhe
6.20 c                   eval      vvprl1_lety = 0
6.20 c     vvprl1_key    setll     vvprl1
6.20 c     vvprl1_key    reade     vvprl1                                 90
6.20 c                   dow       *in90 = *off
6.20  *
6.20 c                   eval      b_pris = *on
6.20 c                   eval      p_sapr = vpsapr
6.20 c                   eval      p_bupr = vpbupr
6.20 c                   eval      p_kopr = vpkopr
6.20 c                   eval      p_inpr = vpinpr
6.30 c                   eval      p_inp2 = vpinp2
6.20  *
6.20 c                   if        w_dato >= vppdat
6.20 c                   leave
6.20 c                   endif
6.20  *
6.20 c     vvprl1_key    reade     vvprl1                                 90
6.20 c                   enddo
6.20  *
6.20 c                   endif
6.20  *
6.20 c                   endif
6.20  *
6.20 c                   endsr
6.20  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_ldor
5.70 c                   parm                    p_prgr
     c                   parm                    p_enhe
     c                   parm                    p_lety
     c                   parm                    p_dato
     c                   parm                    p_inlr
     c                   parm                    p_sapr
     c                   parm                    p_bupr
     c                   parm                    p_kopr
     c                   parm                    p_inpr
6.30 c                   parm                    p_inp2
6.24 c                   parm                    p_hlev
      *
      * Key for les på vare-pris-register
      *
     c     vvprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvprl1_vare
6.20 c                   kfld                    vvprl1_ldor
5.70 c                   kfld                    vvprl1_prgr
     c                   kfld                    vvprl1_enhe
     c                   kfld                    vvprl1_lety
      *
      * Key for oppslag på vare-register
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på undergruppe-register
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
     c                   endsr
