     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FR196R
      * Beskrivelse . . : Rabattmatrise, kopiering av avtale
      *
      *                   Kalles gjennom CL som har overridet FRABLU,
      *                   FPRILU og FPRNLU til filgruppen det skal
      *                   kopieres til.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       031202 HKO  Nytt program
      *       040703 HKO  Lagt inn valg av rabattkategori
      *       200904 HKO  Lagt inn kopiering av spesialpris notat
5.70  * PRGR  211008 bof  Utvidet med prisgruppe
8.01  *PRG2   100622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Kopieres fra
      *
     ffrabl1    if   e           k disk    rename(frabpfr:frabl1r)
     ffrabl3    if   e           k disk    rename(frabpfr:frabl3r)
     ffpril1    if   e           k disk    rename(fpripfr:fpril1r)
     ffpril3    if   e           k disk    rename(fpripfr:fpril3r)
     ffprnl1    if   e           k disk    rename(fprnpfr:fprnl1r)
      *
      * Kopieres til
      *
     ffrablu    uf a e           k disk    rename(frabpfr:frablur)
     ffprilu    uf a e           k disk    rename(fpripfr:fprilur)
     ffprnlu    uf a e           k disk    rename(fprnpfr:fprnlur)
      *
      * Definering parametre
      *
     d p_firm          s                   like(frfirm)
     d p_rkaf          s                   like(frrkat)
     d p_kunf          s                   like(frkund)
     d p_rkat          s                   like(frrkat)
     d p_kunt          s                   like(frkund)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
      *
      * Definering av variabler i nøkler
      *
5.70 d frabl1_prgr     s                   like(frprgr)
     d frabl1_rkat     s                   like(frrkat)
     d frabl3_kund     s                   like(frkund)
     d frabl3_kpro     s                   like(frkpro)
5.70 d fpril1_prgr     s                   like(fpprgr)
     d fpril1_rkat     s                   like(fprkat)
     d fpril3_kund     s                   like(fpkund)
     d fpril3_kpro     s                   like(fpkpro)
     d fprnl1_nkun     s                   like(fpnkun)
     d fprnl1_nkpr     s                   like(fpnkpr)
      *
      * Definering av variable
      *
     d w_firm          s                   like(frfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Kopierer rabatter
      *
     c                   if        p_rkaf <> 0
5.70 c                   eval      frabl1_prgr = *blank
     c                   eval      frabl1_rkat = p_rkaf
     c     frabl1_key    setll     frabl1
     c     frabl1_key    reade     frabl1
     c                   dow       not %eof
      *
     c                   eval      frrkat = p_rkat
     c                   eval      frkund = p_kunt
     c                   eval      freusr = l_user
      *
     c                   time                    fredat
     c                   time                    fretim
      *
     c                   write     frablur
      *
     c     frabl1_key    reade     frabl1
     c                   enddo
     c                   endif
      *
     c                   if        p_kunf <> 0
     c                   eval      frabl3_kund = p_kunf
     c                   eval      frabl3_kpro = 0
     c     frabl3_key    setll     frabl3
     c     frabl3_key    reade     frabl3
     c                   dow       not %eof
      *
     c                   eval      frrkat = p_rkat
     c                   eval      frkund = p_kunt
     c                   eval      freusr = l_user
      *
     c                   time                    fredat
     c                   time                    fretim
      *
     c                   write     frablur
      *
     c     frabl3_key    reade     frabl3
     c                   enddo
     c                   endif
      *
      * Kopierer spesialpriser
      *
     c                   if        p_rkaf <> 0
5.70 c                   eval      fpril1_prgr = *blank
     c                   eval      fpril1_rkat = p_rkaf
     c     fpril1_key    setll     fpril1
     c     fpril1_key    reade     fpril1
     c                   dow       not %eof
      *
     c                   eval      fprkat = p_rkat
     c                   eval      fpkund = p_kunt
     c                   eval      fpeusr = l_user
      *
     c                   time                    fpedat
     c                   time                    fpetim
      *
     c                   write     fprilur
      *
     c     fpril1_key    reade     fpril1
     c                   enddo
     c                   endif
      *
     c                   if        p_kunf <> 0
      *
     c                   eval      fpril3_kund = p_kunf
     c                   eval      fpril3_kpro = 0
     c     fpril3_key    setll     fpril3
     c     fpril3_key    reade     fpril3
     c                   dow       not %eof
      *
     c                   eval      fprkat = p_rkat
     c                   eval      fpkund = p_kunt
     c                   eval      fpeusr = l_user
      *
     c                   time                    fpedat
     c                   time                    fpetim
      *
     c                   write     fprilur
      *
     c     fpril3_key    reade     fpril3
     c                   enddo
      *
     c                   eval      fprnl1_nkun = p_kunf
     c                   eval      fprnl1_nkpr = 0
     c     fprnl1_key    setll     fprnl1
     c     fprnl1_key    reade     fprnl1
     c                   dow       not %eof
      *
     c                   eval      fpnkun = p_kunt
     c                   eval      fpneus = l_user
      *
     c                   time                    fpnoda
     c                   time                    fpneda
     c                   time                    fpneti
      *
     c                   write     fprnlur
      *
     c     fprnl1_key    reade     fprnl1
     c                   enddo
      *
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametere
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_rkaf
     c                   parm                    p_kunf
     c                   parm                    p_rkat
     c                   parm                    p_kunt
      *
      * Key for les på rabattmatrise
      *
     c     frabl1_key    klist
     c                   kfld                    w_firm
5.70 c                   kfld                    frabl1_prgr
     c                   kfld                    frabl1_rkat
      *
     c     frabl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    frabl3_kund
     c                   kfld                    frabl3_kpro
      *
      * Key for les på spesialpris
      *
     c     fpril1_key    klist
     c                   kfld                    w_firm
5.70 c                   kfld                    fpril1_prgr
     c                   kfld                    fpril1_rkat
      *
     c     fpril3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fpril3_kund
     c                   kfld                    fpril3_kpro
      *
      * Key for les på spesialpris notat
      *
     c     fprnl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fprnl1_nkun
     c                   kfld                    fprnl1_nkpr
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
