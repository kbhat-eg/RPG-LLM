     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : MM102R
      * Beskrivelse . . : Progn.p.uke , vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       041206 BØ   Nytt program
5.50  *       310507 bø   Utvidet b.punkt, m.meng,mini til 3 desimaler
      * 6.10  180609 BSA  Oppdatert med sporingsinformasjon
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fmprul1    if   e           k disk    rename(mprupfr:mprul1r)
     fmprul2    if   e           k disk    rename(mprupfr:mprul2r)
     fmprulr    if   e           k disk    rename(mprupfr:mprulrr)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fmprulu    uf a e           k disk    rename(mprupfr:mprulur)
      *
     fmm102d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d mprul1_aar      s                   like(mraar )
     d mprul1_uke      s                   like(mruke )
     d mprulr_aar      s                   like(mraar )
     d mprulr_uke      s                   like(mruke )
     d mprulu_aar      s                   like(mraar )
     d mprulu_uke      s                   like(mruke )
     d vvarl1_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(mrfirm)
     d w_vare          s                   like(mrvare)
     d w_lage          s                   like(mrlage)
     d w_vsor          s                   like(mrvsor)
     d w_ogrp          s                   like(mrogrp)
     d w_hgrp          s                   like(mrhgrp)
     d w_ugrp          s                   like(mrugrp)
     d w_ldor          s                   like(mrldor)
     d w_vark          s                   like(mrvark)
     d w_crdo          s                   like(mrcrdo)
      *
     d p_vare          s                   like(mrvare)
     d p_lage          s                   like(mrlage)
     d p_aar           s              4  0
     d p_uke           s              2  0
     d p_vsor          s                   like(mrvsor)
     d p_ogrp          s                   like(mrogrp)
     d p_hgrp          s                   like(mrhgrp)
     d p_ugrp          s                   like(mrugrp)
     d p_ldor          s                   like(mrldor)
     d p_vark          s                   like(mrvark)
     d p_crdo          s                   like(mrcrdo)
      *
     d s_aar           s                   like(mraar )
     d s_uke           s                   like(mruke )
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2aar  <> 0 or
     c                             b2uke  <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      mprul1_aar  = b1aar
     c                   eval      mprul1_uke  = b1uke
     c     mprul1_key    setll     mprul1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Progn.p.uke
      *
     c                   if        b2aar  <> 0
     c                   eval      w_seqe = *blank
     c                   eval      mprul1_aar  = b2aar
     c                   eval      mprul1_uke  = b2uke
     c     mprul1_key    setll     mprul1
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2aar  = 0
     c                   eval      b2uke  = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1uke  = b1uke
     c                   eval      c1aar  = b1aar
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1aar  = b1aar
     c                   eval      k1uke  = b1uke
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1aar  = b1aar
     c                   eval      d1uke  = b1uke
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c*                  if        b1valg = 5
     c*                  eval      c1aar  = b1aar
     c*                  exsr      xc2bld
     c*                  eval      b1valg = 0
     c*                  update    b1sfl
     c*                  iter
     c*                  endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1aar  = b2aar
     c                   eval      c1uke  = b2uke
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1aar  = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Validering
      *
     c                   if        c1aar  < 2005 or
     c                             c1aar  > 2050
     c                   eval      *in31  = *on
     c                   goto      c1taga
     c                   endif
     c                   if        c1uke  < 1 or
     c                             c1uke  > 52
     c                   eval      *in32  = *on
     c                   goto      c1taga
     c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      mprulr_aar  = c1aar
     c                   eval      mprulr_uke  = c1uke
     c     mprulr_key    chain     mprulr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1aar  = 0
     c                   eval      c1uke  = 0
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2aar  = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      mprulr_aar  = c1aar
     c                   eval      mprulr_uke  = c1uke
     c     mprulr_key    chain     mprulr
      *
     c                   if        %found
     c                   eval      c2antp = mrantp
     c                   eval      c2antr = mrantr
     c                   eval      c2eusr = mruser
     c                   if        mrodat = *loval
     c                   eval      c2odat = 0
     c                   else
     c     *dmy          move      mrodat        c2odat
     c                   endif
     c                   if        mredat = *loval
     c                   eval      c2edat = 0
     c                   else
     c     *dmy          move      mredat        c2edat
     c                   endif
     c                   else
     c                   eval      c2antp = 0
     c                   eval      c2antr = 0
     c                   eval      c2eusr = *blank
     c                   eval      c2odat = 0
     c                   eval      c2edat = 0
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1aar  = k1aar
     c                   eval      c1uke  = k1uke
     c                   endif
      *
     c*                  if        b1valg = 5
     c*                  eval      *in30 = *on
     c*                  endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c2taga
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   mprulur
      *
     c                   eval      mprulu_aar  = c1aar
     c                   eval      mprulu_uke  = c1uke
     c     mprulu_key    chain     mprulur
      *
     c                   eval      mrantr = c2antr
     c                   eval      mrvsor = w_vsor
     c                   eval      mrogrp = w_ogrp
     c                   eval      mrhgrp = w_hgrp
     c                   eval      mrugrp = w_ugrp
     c                   eval      mrldor = w_ldor
     c                   eval      mrvark = w_vark
     c                   eval      mrcrdo = w_crdo
      *
     c                   time                    mredat
     c                   time                    mretim
     c                   eval      mruser = l_user
      *
     c                   if        %found
6.10 c                   time                    mredat
6.10 c                   time                    mretim
6.10 c                   eval      mreusr = l_user
     c                   update    mprulur
     c                   else
     c                   eval      mrfirm = w_firm
     c                   eval      mrvare = w_vare
     c                   eval      mrlage = w_lage
     c                   eval      mraar  = c1aar
     c                   eval      mruke  = c1uke
     c                   time                    mrodat
6.10 c                   time                    mrotim
6.10 c                   eval      mruser = l_user
6.10 c                   time                    mredat
6.10 c                   time                    mretim
6.10 c                   eval      mreusr = l_user
     c                   write     mprulur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1antr = mrantr
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      mprulr_aar  = d1aar
     c                   eval      mprulr_uke  = d1uke
     c     mprulr_key    chain     mprulr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      mprulu_aar  = d1aar
     c                   eval      mprulu_uke  = d1uke
     c     mprulu_key    chain     mprulur
      *
     c                   delete    mprulur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_aar  = k1aar
     c                   eval      s_uke  = k1uke
      *
      * Fyller kopierings-bildet
      *
     c                   eval      mprulr_aar  = k1aar
     c                   eval      mprulr_uke  = k1uke
     c     mprulr_key    chain     mprulr
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1aar  = 0
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
     c                   if        k1uke  = 0
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      mprulr_aar  = k1aar
     c                   eval      mprulr_uke  = k1uke
     c     mprulr_key    chain     mprulr
     c                   if        %found
     c                   eval      *in33 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1aar  = s_aar
     c                   eval      c1uke  = s_uke
     c                   exsr      xc2bld
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      mprul1
      *
     c                   if        %eof or
     c                             mrfirm <> w_firm or
     c                             mrvare <> w_vare or
     c                             mrlage <> w_lage
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      mprul1_aar  = mraar
     c                   eval      mprul1_uke  = mruke
      *
     c                   eval      b1valg = 0
     c                   eval      b1aar  = mraar
     c                   eval      b1uke  = mruke
     c                   eval      b1antp = mrantp
     c                   eval      b1antr = mrantr
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     mprul1_key    setgt     mprul1
     c                   readp     mprul1
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     mprul1
      *
     c                   if        %eof or
     c                             mrfirm <> w_firm or
     c                             mrvare <> w_vare or
     c                             mrlage <> w_lage
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   eval      mprul1_aar  = mraar
     c                   eval      mprul1_uke  = mruke
      *
     c                   enddo
      *
     c                   if        %eof
     c     mprul1_key    setll     mprul1
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for posisjonering på Progn.p.uke
      *
     c     mprul1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
     c                   kfld                    w_lage
     c                   kfld                    mprul1_aar
      *
      * Key for les på Progn.p.uke
      *
     c     mprulr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
     c                   kfld                    w_lage
     c                   kfld                    mprulr_aar
     c                   kfld                    mprulr_uke
      *
      * Key for oppdatering av Progn.p.uke
      *
     c     mprulu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
     c                   kfld                    w_lage
     c                   kfld                    mprulu_aar
     c                   kfld                    mprulu_uke
      *
      * Key for les av vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *
      *
     C     *ENTRY        PLIST
     C                   PARM                    p_vare
     C                   PARM                    p_lage
     c                   parm                    p_aar
     c                   parm                    p_uke
     c                   parm                    p_vsor
     c                   parm                    p_ogrp
     c                   parm                    p_hgrp
     c                   parm                    p_ugrp
     c                   parm                    p_ldor
     c                   parm                    p_vark
     c                   parm                    p_crdo
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_vare = p_vare
     c                   eval      w_lage = p_lage
     c                   eval      w_vsor = p_vsor
     c                   eval      w_ogrp = p_ogrp
     c                   eval      w_hgrp = p_hgrp
     c                   eval      w_ugrp = p_ugrp
     c                   eval      w_ldor = p_ldor
     c                   eval      w_vark = p_vark
     c                   eval      w_crdo = p_crdo
      *
     c                   eval      *in22 = *on
      *
     c                   movel     p_vare        vvarl1_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found(vvarl1)
     c                   movel     vvvare        b2vare
     c                   movel     vvtek1        b2navn
     c                   eval      b2enhs = vvenhs
     c                   else
     c                   eval      b2navn = 'Ukjent vare'
     c                   endif
     c                   eval      b2lage = w_lage
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      mprul1_aar  = p_aar
     c                   eval      mprul1_uke  = p_uke
     c     mprul1_key    setll     mprul1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
