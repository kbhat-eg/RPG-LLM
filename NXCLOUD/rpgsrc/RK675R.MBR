     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RK675R                                              *
      * Beskrivelse . . : Kundeposteringer - utskrift av reklamasjoner        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
T5.30 * V5.30 090704 HFL  Valg av bare åpne poster eller alle.                *
T5.30 *                   Skriver restbeløp.                                  *
T5.30 * V5.30 140904 HFL  Kan skrive kladdeblokk.
T5.40 * V5.40 150605 HFL  Leser RKLAL2 i stedet for RKLALU.
 7.01 * V6.10 270412 NHO  Kun samme bilagsnr skal skrive notat                *
      *                   Kun samme 'tist'
      *                   Leser RKLAL3 i stedenfor rklal3
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frktrl1    if   e           k disk    rename(rktrpfr:rktrl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
7.01 frklal3    if   e           k disk    rename(rklapfr:rklal3r)
     frk675p    o    e             printer oflind(*in30)
      *
     d                uds
T5.30d l_klad                301    301
     d  l_wsid               901    910
     d  l_user               911    920
     d  l_fgrp               931    933
     d  l_firm               944    946  0
     d  l_fnav               951    980
      *
     d w_firm          s              3  0
     d rkunl1_kund     s                   like(rnkund)
     d n               s              3  0
T5.30d w_type          s              1
T5.30d w_syst          s                   like(rxsyst)
T5.30d w_pros          s                   like(rxpros)
T5.30d w_akti          s                   like(rxakti)
T5.30d w_kont          s                   like(rxkont)
T5.30d s_kund          s                   like(rkkund)
 7.01d s_biln          s                   like(rxbiln)
 7.01d s_tist          s                   like(rxtist)
      *
     d                 ds
     d w6bdat                         6  0
     d  w6bdag                        2  0 overlay(w6bdat)
     d  w6bmnd                        2  0 overlay(w6bdat:3)
     d  w6baar                        2  0 overlay(w6bdat:5)
      *
     c     dummy         tag
      *
      * Les kundetransaksjoner
      *
     c     rktrl1_key    setll     rktrl1r                                60
     c     rktrl1_key    reade     rktrl1r                                60
      *
     c                   dow       *in60 = *off
      *
      * Sjekk om reklamasjon
      *
     c                   if        rnbilk <> 89 and
     c                             rnrekl = *blank
     c                   goto      neste
     c                   endif
      *
T5.30c                   if        w_type = 'J' and
T5.30c                             rnrest = *zeros
T5.30c                   goto      neste
T5.30c                   endif
      *
T5.30 * Ved brudd på kunde, sjekkes det om kladdebok skal skrives
      *
7.01 c**                 if        rnkund <> s_kund and
7.01 c**                           s_kund <> *zeros
7.01 c                   if        s_kund <> *zeros
T5.30c                   if        l_klad = 'J'
T5.30c                   exsr      kladde
T5.30c                   endif
T5.30c                   endif
      *
      * Sjekker om overflow
      *
     c                   if        *in30 = *on                                               .
     c                   write     a1hdr                                                     .
     c                   eval      *in30 = *off                                              .
     c                   endif                                                           .....
      *
     c     *dmy          move      rnbdat        d1bdat
      *
     c                   eval      d1belo = rnbelØ
T5.30c                   eval      d1rest = rnrest
      *
     c                   move      rnkund        rkunl1_kund
     c     rkunl1_key    chain     rkunl1r                              61
      *
     c                   write     d1det
T5.30c                   eval      s_kund = rnkund
7.01 c                   eval      s_biln = rnbiln
7.01 c                   eval      s_tist = rntist
      *
     c     neste         tag
      *
     c     rktrl1_key    reade     rktrl1r                                60
     c                   enddo
      *
     c     slutt         tag
T5.30 * Kladdeblokk på sist skrevne kunde?
      *
T5.30c                   if        l_klad = 'J'
T5.30c                   exsr      kladde
T5.30c                   endif
     c                   eval      *inlr = *on
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
T5.30 * Subrutine for kladdeblokk                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
T5.30c     kladde        begsr
T5.30 * Finnes notatblokk på kunden?
      *
T5.30c                   eval      w_syst = 'K'                                              .
T5.30c                   eval      w_pros = *zeros                                           .
T5.30c                   eval      w_akti = *blanks                                          .
T5.30c                   eval      w_kont = s_kund                                           .
7.01 c     keynot        setll     rklal3                                 60
      *
T5.30c                   if        *in60 = *on
      *
7.01 c     keynot        reade     rklal3                                 61
T5.30c                   dow       *in61 = *off
 7.01 * Sjekk bilagsnr
 7.01c                   if        rxbiln <> s_biln
 7.01c                   goto      iknot
 7.01c                   endif
 7.01 * Sjekk tist
 7.01c                   if        rxtist <> s_tist
 7.01c                   goto      iknot
 7.01c                   endif
T5.30c                   eval      wnotat = rxtext
      *
T5.30 * Sjekker om overflow
      *
T5.30c                   if        *in30 = *on                                               .
T5.30c                   write     a1hdr                                                     .
T5.30c                   eval      *in30 = *off                                              .
T5.30c                   endif                                                           .....
T5.30c                   write     notatb                                                    .
 7.01c     iknot         tag
7.01 c     keynot        reade     rklal3                                 61
T5.30c                   enddo                                                           .....
T5.30c                   endif                                                               .
T5.30c                   endsr
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
T5.30c     *entry        plist
T5.30c                   parm                    w_type
      *
T5.30c                   if        w_type = 'J'
T5.30c                   eval      wtekst = 'Bare åpne poster'
T5.30c                   else
T5.30c                   eval      wtekst = *blanks
T5.30c                   endif
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
     c     rktrl1_key    klist
     c                   kfld                    w_firm
      *
T5.30 * Key for notatblokk
      *
T5.30c     keynot        klist
T5.30c                   kfld                    w_firm
T5.30c                   kfld                    w_syst
T5.30c                   kfld                    W_pros
T5.30c                   kfld                    W_akti
T5.30c                   kfld                    W_kont
T5.30c*                  kfld                    w_spes
T5.30c*                  kfld                    w_avde
T5.30c*                  kfld                    w_linj
      *
     c                   move      l_firm        w_firm
      *
     c                   write     a1hdr                                                     .
      *
     c                   endsr
      *
