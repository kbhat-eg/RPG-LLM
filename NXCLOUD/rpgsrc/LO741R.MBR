     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : LO741R
      * Beskrivelse . . : Lister opp bestillingsnummer med feil ref NEB standard
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       230616 bsa  Nytt program
6.30  * 6.30  170918 bkv  Utvidet med trelast sortiment
7.xx  * K000  230620 bsa  Takler brede skjermbilder
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flodtlr    if   e           k disk    rename(lodtpfr:lodtlrr)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
6.30 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
     flo741d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av parametre
      *
     d p_numm          s                   like(ldnumm)
     d p_suff          s                   like(ldsuff)
      *
      * Definering av variabler i nøkler
      *
     d lohel1_numm     s                   like(lonumm)
     d lohel1_suff     s                   like(losuff)
'    d lodtlr_numm     s                   like(ldnumm)
     d lodtlr_suff     s                   like(ldsuff)
     d lodtlr_line     s                   like(ldline)
     d vvarl1_vare     s                   like(ldvare)
     d jvarl1_vare     s                   like(jvvare)
     d vvenl1_vare     s                   like(vevare)
     d vvenl1_enhe     s                   like(veenhe)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_eof           s              1    inz(*off)
     d b_eror          s              1    inz(*off)
     d b_inlr          s              1    inz(*off)
      *
     d w_firm          s                   like(ldfirm)
     d w_stat          s              1
     d w_vtyp          s              1
     d w_eann          s             14  0
     d w_east          s              1
     d w_udat          s               d   datfmt(*iso)
     d w_lenr          s                   like(vvldor)
6.30 d w_lv_nobb       s                   like(vvlnob)
6.30 d w_lv_modn       s                   like(vvlmod)
6.30 d w_lv_lldo       s                   like(vvlnle)
6.30 d w_lv_llva       s                   like(vvllva)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(10)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * w_curn - Current recordnummer på siden
      * w_virn - Vis den side som inneholder denne record.
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   if        b_eof = *on
     c                   goto      avslutt
     c                   endif
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
     c**                 if        b_valg_ok = *on
     c**                 goto      avslutt
     c**                 endif
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_sfrn <> 0
     c     w_sfrn        chain     b1sfl
     c                   endif
      *
     c                   eval      lodtlr_numm = p_numm
      *
     c     lodtlr_key    setll     lodtlr
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c**                 if        *in22 = *off
     c**                 eval      *in22 = *on
     c**                 else
     c**                 eval      *in22 = *off
     c**                 endif
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c**                 eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Velg
      *
     c                   if        b1valg = 5
      *
     c                   eval      c1numm = p_numm
     c                   eval      c1suff = P_suff
     c                   eval      c1vare = b1vare
     c                   eval      c1tek1 = b1tek1
     c                   eval      c1eann = b1eann
     c**                 eval      c1nobb = b1nobb
     c                   eval      c1vtyp = b1vtyp
     c                   if        b1stat = 'G'
     c                   eval      c1etxt = 'Feil i GTIN nummer'
     c                   elseif    b1stat = 'N'
     c                   eval      c1etxt = 'Feil i NOBB nummer'
     c                   elseif    b1stat = 'D'
     c                   eval      c1etxt = 'Vare er diversenummer'
     c                   elseif    b1stat = 'V'
     c                   eval      c1etxt = 'Vare finnes ikke i VA'
     c                   elseif    b1stat = 'E'
     c                   eval      c1etxt = 'Feil i innkjøpsenhet'
     c                   elseif    b1stat = 'T'
     c                   eval      c1etxt = 'Overstyrt varetekst'
     c                   endif
      *
     c                   exfmt     b1win
      *
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
      *
     c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c     lodtlr_key2   reade     lodtlr
      *
     c                   if        %eof
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      * Kjør samme kontroll som tilsier feil ihht NEB standard.
     c                   eval      w_stat = *blanks
     c                   exsr      sjk_line
      *
     c                   if        w_stat <> *blanks
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1line = ldline
     c                   eval      b1vare = ldvare
     c                   movel     ldtek1        b1tek1
     c                   eval      b1vtyp = w_vtyp
     c                   eval      b1stat = w_stat
      *
     c                   write     b1sfl
     c                   endif
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekk av linje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_line      begsr
      *
      * Sjekk om diversevare
      *
     c                   call      'VL710R'
     c                   parm                    w_firm
     c                   parm                    ldvare
     c                   parm                    ldlage
     c                   parm                    w_vtyp
     c                   parm                    w_udat
     c                   parm                    w_lenr
     c                   parm                    b_inlr
6.30 c                   parm                    w_lv_nobb
6.30 c                   parm                    w_lv_modn
6.30 c                   parm                    w_lv_lldo
6.30 c                   parm                    w_lv_llva
      *
     c                   if        w_vtyp = 'D'
     c                   eval      w_stat = 'D'
     c                   goto      end_line
     c                   endif
      *
      * Sjekk EAN-nummer
      *
     c                   exsr      sjekk_ean
     c                   if        b_eror = *on
     c                   eval      w_stat = 'G'
     c                   goto      end_line
     c                   endif
      *
      * Sjekk Nobb-nummer
      *
     c                   if        ldnobb = 0
     c                   eval      w_stat = 'N'
     c                   goto      end_line
     c                   endif
      *
      * Sjekk matching mot LVR
      *
     c                   exsr      sjekk_match
     c                   if        b_eror = *on
     c                   eval      w_stat = 'V'
     c                   goto      end_line
     c                   endif
      *
      * Sjekk innkjøpsenheter - ikke nødvendig. Test er god nok i VE713R.
      *
     c                   if        1=2
     c                   exsr      sjekk_enh
     c                   if        b_eror = *on
     c                   eval      w_stat = 'E'
     c                   goto      end_line
     c                   endif
     c                   endif
      *
      * Sjekk om overstyrt varetekst - for streng, da enkelte nobbnr skal
      * kunne overstyres på varetekst. (feks diversenr på dører/vinduer osv)
      *
     c                   if        1=2
     c                   exsr      sjekk_tekst
     c                   if        b_eror = *on
     c                   eval      w_stat = 'T'
     c                   goto      end_line
     c                   endif
     c                   endif
      *
     c     end_line      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å sjekke EAN-nummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_ean     begsr
      *
     c                   eval      b_eror = *off
     c                   eval      w_eann = 0
     c                   eval      w_east = *blank
      *
     c                   call      'VE713R'
     c                   parm                    w_firm
     c                   parm                    loldor
     c                   parm                    ldvare
     c                   parm                    ldenh1
     c                   parm                    w_eann
     c                   parm                    w_east
      *
     c                   if        w_east <> '1' or
     c                             w_eann = 0
     c                   eval      b_eror = *on
     c                   goto      end_ean
     c                   endif
      *
     c     end_ean       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å sjekke match mot LVR
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_match   begsr
      *
     c                   eval      b_eror = *off
     c                   eval      jvarl1_vare = ldvare
     c     jvarl1_key    chain     jvarl1
     c                   if        not %found
     c                   eval      b_eror = *on
     c                   endif
      *
     c     end_match     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å sjekke innkjøpsenheter mot LVR
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_enh     begsr
      *
     c                   eval      b_eror = *off
     c                   eval      vvenl1_vare = ldvare
     c                   eval      vvenl1_enhe = ldenh1
     c     vvenl1_key    chain     vvenl1
     c                   if        %found and
     c                             veinen > 0
     c                   eval      b_eror = *on
     c                   endif
      *
     c     end_enh       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å sjekke overstyrt varetekst
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_tekst   begsr
      *
     c                   eval      b_eror = *off
      *
     c                   eval      vvarl1_vare = ldvare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   goto      end_tekst
     c                   endif
      *
     c                   if        vvtek1 <> jvtek1
     c                   eval      b_eror = *on
     c                   endif
      *
     c     end_tekst     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_numm
     c                   parm                    p_suff
      *
      * Key for posisjonering på bestillingsnummer
      *
     c     lodtlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlr_numm
     c                   kfld                    lodtlr_suff
     c                   kfld                    lodtlr_line
      *
      * Key for posisjonering på bestillingsnummer
      *
     c     lodtlr_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlr_numm
     c                   kfld                    lodtlr_suff
      *
      * Key for posisjonering på bestillingsnummer
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
     c                   kfld                    lohel1_suff
      *
      * Key for posisjonering på vareregister
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for posisjonering på vareregister
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for les av vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
     c                   kfld                    vvenl1_enhe
      *
      * Henter parametre, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      lohel1_numm = p_numm
     c                   eval      lohel1_suff = p_suff
     c     lohel1_key    chain     lohel1
     c                   if        not %found
     c                   eval      b_eof = *on
     c                   endif
      *
     c                   if        b_eof = *off
     c                   eval      lodtlr_numm = p_numm
     c                   eval      lodtlr_suff = p_suff
     c                   eval      lodtlr_line = 0
      *
     c     lodtlr_key    setll     lodtlr
     c                   exsr      crt_subfile
     c                   endif
      *
     c                   endsr
