     h option(*nodebugio) datedit(*ymd)
      /title  ASOFAK Gen. temp. flatfil/EDI for Autofakt-faktura
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FO184R
      * Beskrivelse . . : Fakturabeh., gen. av temp. flatfil i EDI-format
      *                                input til gen. av Autofakt-faktura
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       080205 GRO  Nytt program
6.11  * 6.10  290609 bof  Utivdet eannr til 14 og brukt std. oppslag
6.NU  * R6.30 230514 bsa  Programmet gjennomgått mtp nummerutvidelser.
6.30  *       180515 BKV  Endret LVAR pga økt fra 15 til 20 tegn
      *
8.01  *PRG2   220622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner - COMP
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fsohel1    if   e           k disk    rename(sohepfr:sohel1r)
     fsohelr    if   e           k disk    rename(sohepfr:sohelrr)
     f                                     prefix(xx:2)
     fsodtl1    if   e           k disk    rename(sodtpfr:sodtl1r)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvltyl1    if   e           k disk    rename(vltypfr:vltyl1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     fra10l1    if   e           k disk    rename(ra10pfr:ra10l1r)
     fra18l1    if   e           k disk    rename(ra18pfr:ra18l1r)
     fraa8l1    if   e           k disk    rename(raa8pfr:raa8l1r)
     ffmftl1    if   e           k disk    rename(fmftpfr:fmftl1r)
     ffnufl2    if   e           k disk    rename(fnufpfr:fnufl2r)
     ffnuflu    uf a e           k disk    rename(fnufpfr:fnuflur)
     ffnaxpf    o  a e           k disk    rename(fnaxpfr:fnaxpfr)
     ffo184p    o    e             printer
      *
      * Definering av Local data area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      *  Definering av datastrukturer for melding ut
      *
      * FAKTURA
      *
     d                 ds
     d f_ed00                        80
     d  f_id00                        4    overlay(f_ed00)
     d  f_decs00                      1    overlay(f_ed00:5)
     d  f_send00                     13    overlay(f_ed00:6)
     d  f_mott00                     13    overlay(f_ed00:19)
     d  f_gdat00                      6    overlay(f_ed00:32)
     d  f_gtim00                      4    overlay(f_ed00:38)
     d  f_test00                      1    overlay(f_ed00:39)
      *
     d                 ds
     d f_ed01                        80
     d  f_id01                        4    overlay(f_ed01)
     d  f_mref01                     14    overlay(f_ed01:5)
     d  f_mtyp01                      6    overlay(f_ed01:19)
     d  f_mver01                      3    overlay(f_ed01:25)
     d  f_mrel01                      3    overlay(f_ed01:28)
     d  f_mkon01                      2    overlay(f_ed01:31)
     d  f_mfor01                      6    overlay(f_ed01:33)
     d  f_mkod01                      3    overlay(f_ed01:39)
     d  f_fanr01                     35    overlay(f_ed01:42)
     d  f_mfun01                      3    overlay(f_ed01:77)
      *
     d                 ds
     d f_ed02                        80
     d  f_id02                        4    overlay(f_ed02)
     d  f_kva102                      3    overlay(f_ed02:5)
     d  f_mdat02                      8    overlay(f_ed02:8)
     d  f_for102                      3    overlay(f_ed02:16)
     d  f_kva202                      3    overlay(f_ed02:19)
     d  f_levd02                      8    overlay(f_ed02:22)
     d  f_for202                      3    overlay(f_ed02:30)
      *
     d                 ds
     d f_ed03                        80
     d  f_id03                        4    overlay(f_ed03)
     d  f_ftxq03                      3    overlay(f_ed03:5)
     d  f_ftxt03                     70    overlay(f_ed03:8)
      *
     d                 ds
     d f_ed04                        80
     d  f_id04                        4    overlay(f_ed04)
     d  f_rkv104                      3    overlay(f_ed04:5)
     d  f_repa04                     35    overlay(f_ed04:8)
      *
     d                 ds
     d f_ed05                        80
     d  f_id05                        4    overlay(f_ed05)
     d  f_pkva05                      3    overlay(f_ed05:5)
     d  f_peana05                    13    overlay(f_ed05:8)
     d    f_pean05                   13  0 overlay(f_peana05:1)
     d  f_porg05                      3    overlay(f_ed05:21)
     d  f_pnav05                     30    overlay(f_ed05:24)
      *
     d                 ds
     d f_ed06
     d  f_id06                        4    overlay(f_ed06)
     d  f_pad106                     30    overlay(f_ed06:5)
     d  f_pad206                     30    overlay(f_ed06:35)
      *
     d                 ds
     d f_ed07                        80
     d  f_id07                        4    overlay(f_ed07)
     d  f_ppon07                      4    overlay(f_ed07:5)
     d  f_ppst07                     30    overlay(f_ed07:9)
     d  f_lkod07                      3    overlay(f_ed07:39)
      *
     d                 ds
     d f_ed08                        80
     d  f_id08                        4    overlay(f_ed08)
     d  f_rkva08                      3    overlay(f_ed08:5)
     d  f_rref08                     35    overlay(f_ed08:8)
      *
     d                 ds
     d f_ed09                        80
     d  f_id09                        4    overlay(f_ed09)
     d  f_kfun09                      3    overlay(f_ed09:5)
     d  f_kkon09                     35    overlay(f_ed09:8)
      *
     d                 ds
     d f_ed10                        80
     d  f_id10                        4    overlay(f_ed10)
     d  f_vadk10                      3    overlay(f_ed10:5)
     d  f_vako10                      3    overlay(f_ed10:8)
     d  f_vakv10                      3    overlay(f_ed10:11)
     d  f_fdko10                      3    overlay(f_ed10:14)
     d  f_fdkv10                      3    overlay(f_ed10:17)
     d  f_fdat10                      8    overlay(f_ed10:20)
     d  f_fdfo10                      3    overlay(f_ed10:28)
     d  f_ffko10                      3    overlay(f_ed10:31)
     d  f_ffkv10                      3    overlay(f_ed10:34)
     d  f_ffat10                      8    overlay(f_ed10:37)
     d  f_fffo10                      3    overlay(f_ed10:45)
      *
     d                 ds
     d f_ed11                        80
     d  f_id11                        4    overlay(f_ed11)
     d  f_trkv11                      3    overlay(f_ed11:5)
     d  f_trmo11                      3    overlay(f_ed11:8)
     d  f_trna11                     35    overlay(f_ed11:11)
     d  f_leko11                      3    overlay(f_ed11:46)
     d  f_lebe11                      3    overlay(f_ed11:49)
     d  f_lekv11                      3    overlay(f_ed11:52)
      *
     d                 ds
     d f_ed12                        80
     d  f_id12                        4    overlay(f_ed12)
     d  f_rgkv12                      3    overlay(f_ed12:5)
     d  f_rgko12                      3    overlay(f_ed12:8)
     d  f_rgbe12                     35    overlay(f_ed12:11)
     d  f_beko12                      3    overlay(f_ed12:46)
     d  f_belp12                     18    overlay(f_ed12:49)
     d    f_belh12                   15  0 overlay(f_belp12:1)
     d    f_beld12                    2  2 overlay(f_belp12:17)
     d  f_takv12                      3    overlay(f_ed12:67)
     d  f_tako12                      3    overlay(f_ed12:70)
     d  f_tapr12                      6    overlay(f_ed12:73)
     d    f_taph12                    3  0 overlay(f_tapr12:1)
     d    f_tapd12                    2  2 overlay(f_tapr12:5)
     d  f_tafr12                      2    overlay(f_ed12:79)
      *
     d                 ds
     d f_ed13                        80
     d  f_id13                        4    overlay(f_ed13)
     d  f_vlina13                     6    overlay(f_ed13:5)
     d    f_vlin13                    6  0 overlay(f_vlina13:1)
     d  f_veana13                    13    overlay(f_ed13:11)
     d    f_vean13                   13  0 overlay(f_veana13:1)
     d  f_eank13                      3    overlay(f_ed13:24)
     d  f_piq113                      3    overlay(f_ed13:27)
     d  f_nobba13                    15    overlay(f_ed13:30)
     d    f_nobb13                    8  0 overlay(f_nobba13:1)
     d    f_nobx13                    7    overlay(f_nobba13:9)
     d  f_noko13                      3    overlay(f_ed13:45)
     d  f_piq213                      3    overlay(f_ed13:48)
     d  f_lvar13                     15    overlay(f_ed13:51)
     d  f_lvko13                      3    overlay(f_ed13:65)
      *
     d                 ds
     d f_ed14                        80
     d  f_id14                        4    overlay(f_ed14)
     d  f_txko14                      3    overlay(f_ed14:5)
     d  f_vtx114                     35    overlay(f_ed14:8)
     d  f_vtx214                     35    overlay(f_ed14:43)
      *
     d                 ds
     d f_ed15                        80
     d  f_id15                        4    overlay(f_ed15)
     d  f_kvko15                      3    overlay(f_ed15:5)
     d  f_kvan15                     15    overlay(f_ed15:8)
     d    f_kvah15                   11  0 overlay(f_kvan15:1)
     d    f_kvad15                    3  3 overlay(f_kvan15:13)
     d  f_enhk15                      3    overlay(f_ed15:23)
      *!! - fortegn/antall
     d  f_kvas15                      1    overlay(f_ed15:26)
      *
     d                 ds
     d f_ed16                        80
     d  f_id16                        4    overlay(f_ed16)
     d  f_fxlq16                      3    overlay(f_ed16:5)
     d  f_ftxl16                     70    overlay(f_ed16:8)
      *
     d                 ds
     d f_ed17                        80
     d  f_id17                        4    overlay(f_ed17)
     d  f_bonu17                      3    overlay(f_ed17:5)
     d  f_bekf17                      3    overlay(f_ed17:8)
     d  f_befo17                     18    overlay(f_ed17:11)
     d    f_befh17                   15  0 overlay(f_befo17:1)
     d    f_befd17                    2  2 overlay(f_befo17:17)
     d  f_beke17                      3    overlay(f_ed17:29)
     d  f_beet17                     18    overlay(f_ed17:32)
     d    f_beeh17                   15  0 overlay(f_beet17:1)
     d    f_beed17                    2  2 overlay(f_beet17:17)
      *!! - fortegn/varelinjebel. før/etter rabatt
     d  f_befs17                      1    overlay(f_ed17:50)
     d  f_bees17                      1    overlay(f_ed17:51)
      *
     d                 ds
     d f_ed18                        80
     d  f_id18                        4    overlay(f_ed18)
     d  f_prkv18                      3    overlay(f_ed18:5)
     d  f_pris18                     15    overlay(f_ed18:8)
     d    f_prih18                   11  0 overlay(f_pris18:1)
     d    f_prid18                    3  3 overlay(f_pris18:13)
     d  f_enha18                      9    overlay(f_ed18:23)
     d    f_enhh18                    9  0 overlay(f_enha18:1)
     d  f_enhp18                      3    overlay(f_ed18:32)
      *!! - fortegn/pris
     d  f_prix18                      1    overlay(f_ed18:35)
      *
     d                 ds
     d f_ed19                        80
     d  f_id19                        4    overlay(f_ed19)
     d  f_rekv19                      3    overlay(f_ed19:5)
     d  f_refn19                     35    overlay(f_ed19:8)
     d  f_relia19                     6    overlay(f_ed19:43)
     d    f_reli19                    6  0 overlay(f_relia19:1)
      *
     d                 ds
     d f_ed20                        80
     d  f_id20                        4    overlay(f_ed20)
     d  f_mvak20                      3    overlay(f_ed20:5)
     d  f_mvty20                      3    overlay(f_ed20:8)
     d  f_mvgr20                     15    overlay(f_ed20:11)
     d  f_mvap20                      6    overlay(f_ed20:26)
     d    f_mvah20                    3  0 overlay(f_mvap20:1)
     d    f_mvad20                    2  2 overlay(f_mvap20:5)
     d  f_mvko20                      3    overlay(f_ed20:32)
      *
     d                 ds
     d f_ed21                        80
     d  f_id21                        4    overlay(f_ed21)
     d  f_sakv21                      3    overlay(f_ed21:5)
     d  f_saty21                      3    overlay(f_ed21:8)
     d  f_mvbe21                     35    overlay(f_ed21:11)
     d  f_sagr21                     15    overlay(f_ed21:46)
     d    f_sagh21                   12  0 overlay(f_sagr21:1)
     d    f_sagd21                    2  2 overlay(f_sagr21:14)
     d  f_sasa21                     15    overlay(f_ed21:61)
     d    f_sash21                   12  0 overlay(f_sasa21:1)
     d    f_sasd21                    2  2 overlay(f_sasa21:14)
     d  f_same21                      3    overlay(f_ed21:76)
      *
     d                 ds
     d f_ed22                        80
     d  f_id22                        4    overlay(f_ed22)
     d  f_sako22                      3    overlay(f_ed22:5)
     d  f_sabe22                     18    overlay(f_ed22:8)
     d    f_sabh22                   15  0 overlay(f_sabe22:1)
     d    f_sabd22                    2  2 overlay(f_sabe22:17)
      *
     d                 ds
     d f_ed23                        80
     d  f_id23                        4    overlay(f_ed23)
     d  f_ftkv23                      3    overlay(f_ed23:5)
     d  f_ftrf23                      3    overlay(f_ed23:8)
     d  f_ftty23                      3    overlay(f_ed23:11)
     d  f_fttx23                     35    overlay(f_ed23:14)
     d  f_ftmk23                     15    overlay(f_ed23:49)
     d    f_ftmh23                   11  0 overlay(f_ftmk23:1)
     d    f_ftmd23                    3  3 overlay(f_ftmk23:13)
     d  f_ftme23                      3    overlay(f_ed23:64)
      *
     d                 ds
     d f_ed24                        80
     d  f_id24                        4    overlay(f_ed24)
     d  f_ftpr24                      8    overlay(f_ed24:5)
     d    f_ftph24                    5  0 overlay(f_ftpr24:1)
     d    f_ftpd24                    2  2 overlay(f_ftpr24:7)
     d  f_ftbe24                     18    overlay(f_ed24:13)
     d    f_ftbh24                   15  0 overlay(f_ftbe24:1)
     d    f_ftbd24                    2  2 overlay(f_ftbe24:17)
     d  f_ftko24                      3    overlay(f_ed24:31)
     d  f_ftsa24                     15    overlay(f_ed24:34)
     d    f_ftsh24                   12  0 overlay(f_ftsa24:1)
     d    f_ftsd24                    2  2 overlay(f_ftsa24:14)
      *
     d                 ds
     d f_ed25                        80
     d  f_id25                        4    overlay(f_ed25)
     d  f_toty25                      3    overlay(f_ed25:5)
     d  f_tobe25                     18    overlay(f_ed25:8)
     d    f_tobh25                   15  0 overlay(f_tobe25:1)
     d    f_tobd25                    2  2 overlay(f_tobe25:17)
      *!! - fortegn/tot.beløp
     d  f_tobs25                      1    overlay(f_ed25:26)
      *
     d                 ds
     d f_ed26                        80
     d  f_id26                        4    overlay(f_ed26)
     d  f_avkv26                      3    overlay(f_ed26:5)
     d  f_avty26                      3    overlay(f_ed26:8)
     d  f_avgr26                     18    overlay(f_ed26:11)
     d    f_avgh26                   15  0 overlay(f_avgr26:1)
     d    f_avgd26                    2  2 overlay(f_avgr26:17)
     d  f_avsa26                      6    overlay(f_ed26:29)
     d    f_avsh26                    3  0 overlay(f_avsa26:1)
     d    f_avsd26                    2  2 overlay(f_avsa26:5)
     d  f_avbk26                      3    overlay(f_ed26:35)
     d  f_avbe26                     18    overlay(f_ed26:38)
     d    f_avbh26                   15  0 overlay(f_avbe26:1)
     d    f_avbd26                    2  2 overlay(f_avbe26:17)
      *!! - fortegn/tot.beløp
     d  f_avgs26                      1    overlay(f_ed26:56)
     d  f_avbs26                      1    overlay(f_ed26:57)
      *
     d                 ds
     d f_ed27                        80
     d  f_id27                        4    overlay(f_ed27)
     d  f_tokv27                      3    overlay(f_ed27:5)
     d  f_toty27                      3    overlay(f_ed27:8)
     d  f_totx27                     35    overlay(f_ed27:11)
     d  f_tobe27                     18    overlay(f_ed27:46)
     d    f_tobh27                   15  0 overlay(f_tobe27:1)
     d    f_tobd27                    2  2 overlay(f_tobe27:17)
      *
      * Definering av variabler i nøkler
      *
     d sohel1_aarr     s                   like(soaarr)
     d sohel1_binr     s                   like(sobinr)
     d sohelr_aarr     s                   like(soaarr)
     d sohelr_binr     s                   like(sobinr)
     d sodtl1_aarr     s                   like(sdaarr)
     d sodtl1_binr     s                   like(sdbinr)
     d sodtl1_numm     s                   like(sdnumm)
     d rkunl1_kund     s                   like(rkkund)
     d vvarl1_vare     s                   like(vvvare)
     d vltyl1_ltyp     s                   like(sdltyp)
     d votyl1_otyp     s                   like(vaotyp)
     d ra10l1_jkod     s                   like(rajkod)
     d ra18l1_rkod     s                   like(rarkod)
     d fmftl1_faty     s                   like(fmtfat)
     d fnufl2_ufat     s                   like(fnufat)
     d fnufl2_ukda     s                   like(fnukda)
     d fnuflu_aarr     s                   like(fnuaar)
     d fnuflu_binr     s                   like(fnubin)
      *
      * Definering av variable
      *
     d w_firm          s                   like(rkfirm)
     d w_raba          s              5  2
     d w_tnto          s             13  2
     d w_tmva          s             13  2
     d w_tmva_m        s             15  5
     d w_tmvg          s             13  2
     d w_mdat_iso      s               d   datfmt(*iso)
     d w_mdat_alf      s              8
     d w_mtim          s              6  0
     d w_mtim_alf      s              6
     d w_mref          s              6  0
     d w_ftnr          s              9
     d w_npri          s             13  2
     d w_tbto          s             13  2
     d w_vkun          s              6  0
     d w_anto          s              6  0
     d w_fsum          s                   like(sofsum)
     d w_kidd          s                   like(sokidd)
     d w_kidr          s                   like(sokidr)
6.11 d w_east          s              1
6.11 d w_eann          s             14  0
      *
     d s_fsum          s             11  2
     d s_salp          s             11  2
     d s_salh          s             11  2
     d s_salf          s             11  2
     d s_rk1p          s             11  2
     d s_rk2p          s             11  2
     d s_rkop          s             11  2
     d s_rk1h          s             11  2
     d s_rk2h          s             11  2
     d s_rkoh          s             11  2
     d s_rk1f          s             11  2
     d s_rk2f          s             11  2
     d s_rkof          s             11  2
      *
     d                 ds
     d w_kvan                        14  3
     d   w_kvanh                     11    overlay(w_kvan:1)
     d   w_kvand                      3    overlay(w_kvan:12)
     d                 ds
     d w_belo                        17  2
     d   w_beloh                     15    overlay(w_belo:1)
     d   w_belod                      2    overlay(w_belo:16)
     d                 ds
     d w_pros                         5  2
     d   w_prosh                      3    overlay(w_pros:1)
     d   w_prosd                      2    overlay(w_pros:4)
     d                 ds
     d w_pris                        14  3
     d   w_prish                     11    overlay(w_pris:1)
     d   w_prisd                      3    overlay(w_pris:12)
      *
     d b_init          s              1    inz(*off)
     d b_binr          s              1    inz(*off)
     d b_numm          s              1    inz(*off)
     d b_logf          s              1    inz(*off)
     d b_saml          s              1    inz(*off)
     d b_uskr          s              1    inz(*off)
     d b_hode          s              1    inz(*off)
      *
     d s_binr          s                   like(sobinr)
     d s_numm          s                   like(sonumm)
      *
      *  Konstanter
      *
     d c_digits        c                   '0123456789'
      *
      * Definering av parametre
      *
     d p_aarr          s                   like(soaarr)
     d p_faty          s                   like(sofaty)
6.nu d p_fnum          s                   like(sobinr)
6.nu d p_tnum          s                   like(sobinr)
     d p_omkj          s              1
     d p_rapp          s              1
     d p_anta          s              4  0
     d p_feil          s              1
     d p_vkun          s              6
     d p_filn          s             20
     d p_filk          s              1
      *
     d p_stat          s              1
     d p_ekod          s              1
     d p_etxt          s             30
     d p_dato          s               d   datfmt(*iso)
     d p_mvap          s              6  2
     d p_mvat          s              5  4
     d p_mvaf          s             10  9
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Leser firmadata ifm avsenderinformasjon
      *
     c     afir_key      chain     afirl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
      * Henter OFAK informasjon
      *
     c     fstsl1_key    chain     fstsl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
     c*                  eval      ra10l1_jkod = faahla
     c*    ra10l1_key    chain     ra10l1
     c*                  if        not %found
     c*                  goto      avslutt
     c*                  endif
      *
      * Hent statuskode 8
      *
     c     raa8l1_key    chain     raa8l1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
      * Henter fakturatype info
      *
     c                   eval      fmftl1_faty = p_faty
     c     fmftl1_key    chain     fmftl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      fnufl2_ufat = p_faty
     c                   eval      fnufl2_ukda = *loval
      *
     c     fnufl2_key    setll     fnufl2
     c                   read      fnufl2
      *
     c                   dow       not %eof(fnufl2)
      *
     c                   if        fnuaar = p_aarr and
     c                             fnufat = p_faty
     c                   if        fnukun = w_vkun
      *
     c                   eval      sohel1_aarr = p_aarr
     c                   eval      sohel1_binr = fnubin
      *
     c     sohel1_key    setll     sohel1
     c                   read      sohel1
      *
     c                   if        not %eof
     c                   if        sobinr = fnubin
     c                   eval      b_binr = *on
     c                   endif
     c                   endif
      *
     c                   if        b_binr = *on
     c                   exsr      sjk_saml
     c                   endif
      *
     c                   exsr      sjk_uf_log
      *
      * Iterasjon på fakturahode
      *
     c                   dow       b_binr = *on
      *
     c                   eval      sodtl1_aarr = soaarr
     c                   eval      sodtl1_binr = sobinr
     c                   eval      sodtl1_numm = sonumm
     c                   eval      w_tbto = w_tbto + sofsum
      *
     c                   if        b_logf = *on
     c     sodtl1_key    setll     sodtl1
     c     sodtl1_key    reade     sodtl1
      *
     c                   if        not %eof
     c                   eval      b_numm = *on
     c                   endif
      *
      * Iterasjon på fakturalinje
      *
     c                   dow       b_numm = *on
      *
     c                   if        b_init = *off
     c                   exsr      skriv_init
     c                   endif
      *
     c                   if        b_hode = *off
     c                   exsr      gen_faktura
     c                   endif
      *
     c                   eval      vltyl1_ltyp = sdltyp
     c     vltyl1_key    chain     vltyl1r
     c                   if        valktx <> 1
      *
     c                   eval      vvarl1_vare = sdvare
     c     vvarl1_key    chain     vvarl1
      *
     c                   exsr      skriv_vlin
     c                   endif
      *
     c     sodtl1_key    reade     sodtl1
     c                   if        %eof
     c                   eval      b_numm = *off
     c                   endif
      *
     c                   eval      s_numm = sdnumm
     c                   eval      s_binr = sdbinr
     c                   enddo
     c                   endif
      *
     c                   exsr      save_fsum
      *
     c                   read      sohel1
     c                   if        %eof or
     c                             sobinr <> fnubin
     c                   eval      b_binr = *off
     c                   endif
      *
     c                   if        s_binr <> sobinr or
     c                             b_binr = *off
     c                   if        s_binr <> 0
     c                   exsr      skriv_fslut
     c                   endif
     c                   endif
      *
     c                   enddo
      *
     c                   endif
     c                   endif
      *
     c                   read      fnufl2
     c                   enddo
      *
     c*                  eval      p_filn = 'autof' + %trim(w_mdat_alf) +
     c*                                     '.dat'
     c                   eval      p_filn = 'a' + %trim(w_mdat_alf)
     c
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for gen. av initielle meldingsdata
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_init    begsr
      *
      * Fakturafil startpost (ED00)
      *
     c                   clear                   f_ed00
      *
     c                   eval      f_id00 = 'ED00'
     c                   eval      f_decs00 = ','
     c                   move      ra8ean        f_send00
     c                   move      rkeank        f_mott00
      *
     c                   time                    w_mdat_iso
     c     *iso0         move      w_mdat_iso    w_mdat_alf
     c                   eval      f_gdat00 = %subst(w_mdat_alf:3:6)
     c                   time                    w_mtim
     c                   move      w_mtim        w_mtim_alf
     c                   eval      f_gtim00 = %subst(w_mtim_alf:1:4)
     c*                  eval      f_test00 = '1'
      *
     c                   if        fmttes = 1
     c                   eval      f_test00 = '1'
     c                   else
     c                   eval      f_test00 = *blank
     c                   endif
      *
     c                   eval      fnaxln = f_ed00
     c                   exsr      write_outp
      *
     c                   eval      b_init = *on
     c                   eval      w_mref = *zero
     c                   eval      p_filk = '1'
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for generering av faktura-element
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     gen_faktura   begsr
      *
      * Fakturahoderecord (ED01) - Meldingshode
      *
     c                   if        b_init = *off
     c                   leavesr
     c                   endif
      *
     c                   exsr      init_fakth
     c                   eval      w_mref = w_mref + 1
      *
     c                   eval      f_id01 = 'ED01'
     c                   movel     w_mref        f_mref01
     c                   eval      f_mtyp01 = 'INVOIC'
     c                   eval      f_mver01 = 'D  '
     c                   eval      f_mrel01 = '93A'
     c                   eval      f_mkon01 = 'UN'
     c                   eval      f_mfor01 = 'TBF10 '
      *
     c                   eval      votyl1_otyp = sootyp
     c     votyl1_key    chain     votyl1r
      *
      * Er ordretype negativ (kreditnota) eller blank
      *
     c                   if        vaokre = '-'
     c                   eval      f_mkod01 = '381'
     c                   else
     c                   eval      f_mkod01 = '380'
     c                   endif
     c                   movel     sobinr        f_fanr01
      *
     c                   eval      fnaxln = f_ed01
     c                   exsr      write_outp
      *
      * Fakturahoderecord (ED02) - Datoinformasjon
      *
     c                   eval      f_id02 = 'ED02'
     c                   eval      f_kva102 = '137'
     c                   eval      f_mdat02 = w_mdat_alf
     c                   eval      f_for102 = '102'
     c                   eval      f_kva202 = '35 '
     c     *iso0         move      soldat        f_levd02
     c                   eval      f_for202 = '102'
      *
     c                   eval      fnaxln = f_ed02
     c                   exsr      write_outp
      *
      * Fakturahoderecord (ED03) - Fritekstinformasjon
      *
     c                   eval      f_id03 = 'ED03'
      *
      * Fakturahoderecord (ED04) - Referanseinformasjon (n)
      *
     c                   eval      f_id04 = 'ED04'
      *
      * - Kjøpers best.nr.
      *
     c                   if        sorekv <> *blank
     c                   eval      f_rkv104 = 'ON '
     c                   eval      f_repa04 = *blank
     c                   eval      f_repa04 = %trim(sorekv)
     c                   eval      fnaxln = f_ed04
     c                   exsr      write_outp
     c*                  else
     c*                  if        soproj <> *zero
     c*                  eval      f_rkv104 = 'ON '
     c*                  eval      f_repa04 = *blank
     c*                  movel     soproj        f_repa04
     c*                  eval      fnaxln = f_ed04
     c*                  exsr      write_outp
     c*                  endif
     c                   endif
      *
      * - Selgers ordrenr.
      *
     c                   eval      f_rkv104 = 'VN '
     c                   eval      f_repa04 = *blank
     c                   movel     sonumm        f_repa04
     c                   eval      fnaxln = f_ed04
     c                   exsr      write_outp
      *
      * - Kunderef.nr.
      *
     c                   if        sorekv <> *blank
     c                   eval      f_rkv104 = 'CR '
     c                   eval      f_repa04 = *blank
     c                   eval      f_repa04 = %trim(sorekv)
     c                   eval      fnaxln = f_ed04
     c                   exsr      write_outp
     c                   endif
      *
      * - Prosjektnr.
      *
     c                   if        soproj <> *zero
     c                   eval      f_rkv104 = 'AEP'
     c                   eval      f_repa04 = *blank
     c                   movel     soproj        f_repa04
     c                   eval      fnaxln = f_ed04
     c                   exsr      write_outp
     c                   endif
      *
      * - Ref. til fakt. som skal krediteres
      *
     c                   if        f_mkod01 = '381'
     c                   if        sokref <> *zero
     c                   eval      f_rkv104 = 'IV '
     c                   eval      f_repa04 = *blank
     c                   movel     sokref        f_repa04
     c                   eval      fnaxln = f_ed04
     c                   exsr      write_outp
     c                   endif
     c                   endif
      *
      * - KID-nr.
      *
     c*                  if        sokidd <> *blank
     c                   if        w_kidd <> *blank
     c                   eval      f_rkv104 = 'PQ '
     c                   eval      f_repa04 = *blank
     c*                  eval      f_repa04 = %trim(sokidd)
     c                   eval      f_repa04 = %trim(w_kidd)
     c                   eval      fnaxln = f_ed04
     c                   exsr      write_outp
     c                   endif
      *
      * - Kundenr. element CT benyttes
      *
     c                   eval      f_rkv104 = 'CT '
     c                   eval      f_repa04 = *blank
     c                   movel     sokund        f_repa04
     c                   eval      fnaxln = f_ed04
     c                   exsr      write_outp
      *
      * Fakturahoderecord (ED05-ED09) - Partsinformasjon (n)
      *
     c                   eval      f_id05 = 'ED05'
     c                   eval      f_id06 = 'ED06'
     c                   eval      f_id07 = 'ED07'
     c                   eval      f_id08 = 'ED08'
     c                   eval      f_id09 = 'ED09'
      *
      * - Kjøperinfo.
      *
     c                   eval      rkunl1_kund = sokund
     c     rkunl1_key    chain     rkunl1
      *
      *   - Navn/kjøper
     c                   if        %found
     c                   eval      f_pkva05 = 'BY '
     c                   if        rkeank <> *zero
     c                   move      rkeank        f_pean05
     c                   eval      f_porg05 = '9  '
     c                   endif
     c                   eval      f_pnav05 = %trim(rknavn)
     c                   eval      fnaxln = f_ed05
     c                   exsr      write_outp
      *
      *   - Gateadresse/kjøper
     c                   eval      f_pad106 = %trim(rkgate)
     c                   if        rkadr2 <> *blank
     c                   eval      f_pad206 = %trim(rkadr2)
     c                   endif
     c                   eval      fnaxln = f_ed06
     c                   exsr      write_outp
      *
      *   - Postadresse/kjøper
     c                   if        rkponr <> *zero
     c                   move      rkponr        f_ppon07
     c                   else
     c                   eval      f_ppon07 = %subst(rksted:1:4)
     c                   endif
     c                   eval      f_ppst07 = %trim(%subst(rksted:5))
     c                   if        rkland <> *blank
     c                   eval      f_lkod07 = rkland
     c                   endif
     c                   eval      fnaxln = f_ed07
     c                   exsr      write_outp
      *
      *   - Referanse/kjøper
     c                   if        rkftnr <> *zero
     c                   eval      f_rkva08 = 'VA '
     c                   move      rkftnr        w_ftnr
     c                   eval      f_rref08 = 'NO' + w_ftnr + 'MVA'
     c                   eval      fnaxln = f_ed08
     c                   exsr      write_outp
     c                   endif
      *
      *   - Kontaktperson/kjøper
     c                   if        sodref <> *blank
     c                   eval      f_kfun09 = 'PD '
     c                   eval      f_kkon09 = %trim(sodref)
     c                   eval      fnaxln = f_ed09
     c                   exsr      write_outp
     c                   endif
     c                   endif
      *
      * - Selgerinfo.
      *
      *   - Navn/selger
     c                   eval      f_pkva05 = 'SU '
     c                   if        ra8ean <> *zero
     c                   move      ra8ean        f_pean05
     c                   eval      f_porg05 = '9  '
     c                   endif
     c                   eval      f_pnav05 = %trim(aafnav)
     c                   eval      fnaxln = f_ed05
     c                   exsr      write_outp
      *
      *   - Gateadresse/selger
     c                   eval      f_pad106 = %trim(aafad1)
     c                   if        aafad2 <> *blank
     c                   eval      f_pad206 = %trim(aafad2)
     c                   endif
     c                   eval      fnaxln = f_ed06
     c                   exsr      write_outp
      *
      *   - Postadresse/selger
     c                   eval      f_ppon07 = %subst(aafste:1:4)
     c                   eval      f_ppst07 = %trim(%subst(aafste:5))
     c                   eval      fnaxln = f_ed07
     c                   exsr      write_outp
      *
      *   - Referanse/selger
     c                   if        aafftn <> *zero
     c                   eval      f_rkva08 = 'VA '
     c                   move      aafftn        w_ftnr
     c                   eval      f_rref08 = 'NO' + w_ftnr + 'MVA'
     c                   eval      fnaxln = f_ed08
     c                   exsr      write_outp
     c                   endif
      *
      *   - Kontaktperson/selger
     c                   if        sovref <> *blank
     c                   eval      f_kfun09 = 'AD '
     c                   eval      f_kkon09 = %trim(sovref)
     c                   eval      fnaxln = f_ed09
     c                   exsr      write_outp
     c                   endif
      *
      * Fakturahoderecord (ED10) - Valuta/forfallsinfo.
      *
     c                   eval      f_id10 = 'ED10'
     c                   if        sovalk <> *blank
     c                   eval      f_vadk10 = '2  '
     c                   eval      f_vako10 = sovalk
     c                   eval      f_vadk10 = '4  '
     c                   endif
      *
     c                   if        sofkda <> *loval
     c                   eval      f_fdko10 = '3  '
     c                   eval      f_fdkv10 = '7  '
     c     *iso0         move      sofkda        f_fdat10
     c                   eval      f_fdfo10 = '102'
     c                   endif
      *
     c                   if        soffda <> *loval
     c                   eval      f_ffko10 = '3  '
     c                   eval      f_ffkv10 = '13 '
     c     *iso0         move      soffda        f_ffat10
     c                   eval      f_fffo10 = '102'
     c                   endif
      *
     c                   eval      fnaxln = f_ed10
     c                   exsr      write_outp
      *
      * Fakturahoderecord (ED11) - Transport/leveringsinfo.
      *
     c                   if        solbet <> *zero
     c                   eval      ra18l1_rkod = solbet
     c     ra18l1_key    chain     ra18l1r
     c                   if        %found
     c                   if        rartx1 <> *blank
     c                   eval      f_id11 = 'ED11'
     c                   eval      f_leko11 = '3  '
     c                   eval      f_lebe11 = %subst(rartx1:1:3)
     c                   eval      f_lekv11 = '106'
     c                   eval      fnaxln = f_ed11
     c                   exsr      write_outp
     c                   endif
     c                   endif
     c                   endif
      *
      * Fakturahoderecord (ED12) - Tillegg/fradrag
      *
     c                   eval      f_id12 = 'ED12'
      *
      * Oppdater rapportfelter
      *
     c                   eval      a1binr = sobinr
     c                   eval      a1numm = sonumm
     c                   move      sosuff        a1suff
     c                   eval      a1otyp = sootyp
     c                   if        sofkda <> *loval
     c     *dmy          move      sofkda        a1fkda
     c                   else
     c                   eval      a1fkda = *zero
     c                   endif
     c                   if        soffda <> *loval
     c     *dmy          move      soffda        a1ffda
     c                   else
     c                   eval      a1ffda = *zero
     c                   endif
     c                   eval      a1kund = sokund
     c                   eval      a1navn = rknavn
     c                   eval      a2navn = sonavn
      *
     c                   if        p_rapp = '1'
     c                   if        b_uskr = *off
     c                   exsr      skr_rapph
     c                   endif
     c                   endif
      *
     c                   eval      b_hode = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Initiering av faktura-hode records
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     init_fakth    begsr
      *
     c                   clear                   f_ed01
     c                   clear                   f_ed02
     c                   clear                   f_ed03
     c                   clear                   f_ed04
     c                   clear                   f_ed05
     c                   clear                   f_ed06
     c                   clear                   f_ed07
     c                   clear                   f_ed08
     c                   clear                   f_ed09
     c                   clear                   f_ed10
     c                   clear                   f_ed11
     c                   clear                   f_ed12
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Initiering av faktura-linje records
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     init_faktl    begsr
      *
     c                   clear                   f_ed13
     c                   clear                   f_ed14
     c                   clear                   f_ed15
     c                   clear                   f_ed16
     c                   clear                   f_ed17
     c                   clear                   f_ed18
     c                   clear                   f_ed19
     c                   clear                   f_ed20
     c                   clear                   f_ed21
     c                   clear                   f_ed22
     c                   clear                   f_ed23
     c                   clear                   f_ed24
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Initiering av faktura-slutt records
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     init_fakts    begsr
      *
     c                   clear                   f_ed25
     c                   clear                   f_ed26
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for generering av fakturalinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_vlin    begsr
      *
      * Fakturalinjerecord (ED13) - Vareidentifikasjon
      *
     c                   exsr      init_faktl
     c                   eval      f_id13 = 'ED13'
      *
     c                   eval      f_vlin13 = sdline
      *
6.11  *
6.11  * Kaller program for henting av ean-nr
6.11  *
6.11 c                   call      'VE711R'
6.11 c                   parm                    w_firm
6.11 c                   parm                    sdvare
6.11 c                   parm                    sdenh1
6.11 c                   parm                    w_eann
6.11 c                   parm                    w_east
6.11 c                   if        w_east = '1'
6.11 c                   if        w_eann <> *zero
     c                   eval      f_vean13 = w_eann
     c                   eval      f_eank13 = 'EN '
     c                   endif
     c                   endif
      *
     c                   if        vvnobb <> *zero
     c                   eval      f_piq113 = '5  '
     c                   eval      f_nobb13 = vvnobb
     c                   eval      f_noko13 = 'NO1'
     c                   endif
      *
     c                   if        vvlvar <> *blank
     c                   eval      f_piq213 = '1  '
6.30 c*                  eval      f_lvar13 = %trim(vvlvar)
6.30 c                   movel     vvlvar        f_lvar13
     c                   eval      f_lvko13 = 'SA '
     c                   else
     c                   eval      f_piq213 = '1  '
     c                   eval      f_lvar13 = %trim(sdvare)
     c                   eval      f_lvko13 = 'SA '
     c                   endif
      *
     c                   eval      fnaxln = f_ed13
     c                   exsr      write_outp
      *
      * Fakturalinjerecord (ED14) - Varelinjer/varetekst
      *
     c                   eval      f_id14 = 'ED14'
      *
     c                   eval      f_txko14 = 'F  '
     c                   eval      f_vtx114 = %trim(sdtek1)
     c                   eval      f_vtx214 = %trim(sdtek2)
      *
     c                   eval      fnaxln = f_ed14
     c                   exsr      write_outp
      *
      * Fakturalinjerecord (ED15) - Varelinjer/kvantumsinformasjon
      *
     c                   eval      f_id15 = 'ED15'
      *
      *   - Fakturert kvantum
     c                   eval      f_kvko15 = '47 '
      *
     c                   eval      w_kvan = %abs(sdanta)
     c*                  eval      w_kvan = sdanta
     c                   eval      f_kvan15 = w_kvanh + '.' + w_kvand
     c                   eval      f_enhk15 = sdenh1
      *
     c                   eval      f_kvas15 = *blank
     c                   if        sdanta < 0
     c                   eval      f_kvas15 = '-'
     c                   endif
      *
     c                   eval      fnaxln = f_ed15
     c                   exsr      write_outp
      *
      * Fakturalinjerecord (ED16) - Varelinjer/fritekst
      *
     c                   eval      f_id16 = 'ED16'
      *
      * Fakturalinjerecord (ED17) - Varelinjer/rabatt & gebyrer
      *
     c                   eval      f_id17 = 'ED17'
      *
      *   - Varelinjebiløp før og etter rab./geb.
     c                   eval(h)   w_raba = sdrab1 +
     c                                      ((100 - sdrab1) * sdrab2 / 100)
     c                   eval(h)   w_npri = sdsalg - (sdsalg * w_raba / 100)
      *
     c                   eval      f_bekf17 = '66 '
     c*                  eval      w_belo = sdsalg
     c                   eval      w_belo = %abs(sdsalg)
     c                   eval      f_befo17 = w_beloh + '.' + w_belod
     c                   eval      f_befs17 = *blank
     c                   if        sdsalg < 0
     c                   eval      f_befs17 = '-'
     c                   endif
      *
     c                   eval      f_beke17 = '203'
     c*                  eval      w_belo = w_npri
     c                   eval      w_belo = %abs(w_npri)
     c                   eval      f_beet17 = w_beloh + '.' + w_belod
     c                   eval      f_bees17 = *blank
     c                   if        w_npri < 0
     c                   eval      f_bees17 = '-'
     c                   endif
      *
     c                   eval      fnaxln = f_ed17
     c                   exsr      write_outp
      *
      * Fakturalinjerecord (ED18) - Varelinjer/priser
      *
     c                   eval      f_id18 = 'ED18'
      *
      *   - Grunnpris inkl. avg.
     c                   eval      f_prkv18 = 'AAB'
     c*                  eval      w_pris = sdsapr
     c                   eval      w_pris = %abs(sdsapr)
     c                   eval      f_pris18 = w_prish + '.' + w_prisd
     c                   eval      f_enhh18 = *zero
     c                   eval      f_enhp18 = sdenh1
     c                   eval      f_prix18 = *blank
     c                   if        sdsapr < 0
     c                   eval      f_prix18 = '-'
     c                   endif
      *
     c                   eval      fnaxln = f_ed18
     c                   exsr      write_outp
      *
      * Fakturalinjerecord (ED19) - Varelinjer/referanser
      *
     c                   eval      f_id19 = 'ED19'
      *
      * - Selgers ordrenr.
     c                   eval      f_rekv19 = 'VN '
     c                   movel     sonumm        f_refn19
     c                   eval      f_reli19 = sdline
     c                   eval      fnaxln = f_ed19
     c                   exsr      write_outp
      *
      * - Kjøpers best.nr.
     c                   if        soproj <> *zero
     c                   eval      f_rekv19 = 'ON '
     c                   move      soproj        f_refn19
     c                   eval      f_relia19 = *blank
     c                   eval      fnaxln = f_ed19
     c                   exsr      write_outp
     c                   else
     c                   if        sorekv <> *blank
     c                   eval      f_rekv19 = 'ON '
     c                   eval      f_refn19 = %trim(sorekv)
     c                   eval      f_relia19 = *blank
     c                   eval      fnaxln = f_ed19
     c                   exsr      write_outp
     c                   endif
     c                   endif
      *
      * Fakturalinjerecord (ED20) - Varelinjer/mva prosent
      *
     c                   eval      f_id20 = 'ED20'
      *
      * Kaller program i økonomisystemet / MVA-oppslag
      *
     c                   select
     c                   when      sdkmva = 1
     c                   eval      p_ekod = '4'
     c                   when      sdkmva = 2
     c                   eval      p_ekod = 'H'
     c                   other
     c                   eval      p_ekod = '3'
     c                   endsl
      *
     c                   eval      p_dato = sofkda
      *
     c                   call      'RS755R'
     c                   parm                    p_stat
     c                   parm                    p_ekod
     c                   parm                    p_etxt
     c                   parm                    p_dato
     c                   parm                    p_mvap
     c                   parm                    p_mvat
     c                   parm                    p_mvaf
      *
     c                   eval      f_mvak20 = '7  '
     c                   eval      f_mvty20 = 'VAT'
     c                   eval      f_mvty20 = 'VAT'
     c                   eval      w_pros = p_mvap
     c                   eval      f_mvap20 = w_prosh + '.' + w_prosd
     c                   eval      f_mvko20 = 'S  '
      *
     c                   eval      fnaxln = f_ed20
     c                   exsr      write_outp
      *
      * Fakturalinjerecord (ED21) - Varelinjer/avgiftsinformasjon
      *
     c                   eval      f_id21 = 'ED21'
      *
      * Fakturalinjerecord (ED22) - Varelinjer/avgiftsbeløp
      *
     c                   eval      f_id22 = 'ED22'
      *
      * Fakturalinjerecord (ED23) - Varelinjer/fradrag & tilleggskoder
      *
     c                   eval      f_id23 = 'ED23'
      *
      * Fakturalinjerecord (ED24) - Varelinjer/fradrag & tilleggsbeløp /
      *                             prosent
      *
     c                   eval      f_id24 = 'ED24'
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for gen. av avsluttende fakturadata
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_fslut   begsr
      *
      * Fakturatotalrecord (ED25)
      *
     c                   exsr      init_fakts
     c                   eval      f_id25 = 'ED25'
      *
      * - Fakturatotal (Sluttotal hele fakt. inkl MVA)
     c                   eval      f_toty25 = '86 '
     c*                  eval      w_belo = s_fsum
     c                   eval      w_belo = %abs(s_fsum)
     c                   eval      f_tobe25 = w_beloh + '.' + w_belod
     c                   eval      f_tobs25 = *blank
     c                   if        s_fsum < 0
     c                   eval      f_tobs25 = '-'
     c                   endif
      *
     c                   eval      fnaxln = f_ed25
     c                   exsr      write_outp
      *
      * - Sum MVA grunnlag
     c                   eval      f_toty25 = '125'
     c                   eval      w_tmvg = *zero
     c                   eval      w_tmvg = ((s_salp + s_salh) -
     c                                       (s_rk1p + s_rk2p + s_rkop +
     c                                        s_rk1h + s_rk2h + s_rkoh))
     c*                  eval      w_belo = w_tmvg
     c                   eval      w_belo = %abs(w_tmvg)
     c                   eval      f_tobe25 = w_beloh + '.' + w_belod
     c                   eval      f_tobs25 = *blank
     c                   if        w_tmvg < 0
     c                   eval      f_tobs25 = '-'
     c                   endif
      *
     c                   eval      fnaxln = f_ed25
     c                   exsr      write_outp
      *
      * - Totalt MVA beløp for hele fakturaen
     c                   eval      f_toty25 = '150'
     c                   eval      w_tmva = *zero
      *
     c                   if        p_mvap > *zero
     c                   eval      w_tmva_m = ((w_tmvg / 100) * p_mvap)
     c                   eval(h)   w_tmva = w_tmva_m
     c                   endif
     c*                  eval      w_belo = w_tmva
     c                   eval      w_belo = %abs(w_tmva)
     c                   eval      f_tobe25 = w_beloh + '.' + w_belod
     c                   eval      f_tobs25 = *blank
     c                   if        w_tmva < 0
     c                   eval      f_tobs25 = '-'
     c                   endif
      *
     c                   eval      fnaxln = f_ed25
     c                   exsr      write_outp
      *
      * - Sum alle varelinjer etter evt. tillegg/fradrag på linjenivå
     c                   eval      f_toty25 = '203'
     c                   eval      w_tnto = *zero
     c                   eval      w_tnto = w_tmvg + (s_salf -
     c                                      (s_rk1f + s_rk2f + s_rkof))
     c*                  eval      w_belo = w_tnto
     c                   eval      w_belo = %abs(w_tnto)
     c                   eval      f_tobe25 = w_beloh + '.' + w_belod
     c                   eval      f_tobs25 = *blank
     c                   if        w_tnto < 0
     c                   eval      f_tobs25 = '-'
     c                   endif
      *
     c                   eval      fnaxln = f_ed25
     c                   exsr      write_outp
      *
      * - Nettototal (eks. MVA)
     c                   eval      f_toty25 = 'NET'
     c*                  eval      w_belo = s_fsum - w_tmva
     c                   eval      w_belo = %abs(s_fsum - w_tmva)
     c                   eval      f_tobe25 = w_beloh + '.' + w_belod
     c                   eval      f_tobs25 = *blank
     c                   if        (s_fsum - w_tmva) < 0
     c                   eval      f_tobs25 = '-'
     c                   endif
      *
     c                   eval      fnaxln = f_ed25
     c                   exsr      write_outp
      *
      * Fakturatotalavgift (ED26)
      *
     c                   eval      f_id26 = 'ED26'
      *
     c                   eval      f_avkv26 = '7  '
     c                   eval      f_avty26 = 'VAT'
     c*                  eval      w_belo = w_tmvg
     c                   eval      w_belo = %abs(w_tmvg)
     c                   eval      f_avgr26 = w_beloh + '.' + w_belod
     c                   eval      f_avgs26 = *blank
     c                   if        w_tmvg < 0
     c                   eval      f_avgs26 = '-'
     c                   endif
      *
     c                   eval      w_pros = p_mvap
     c                   eval      f_avsa26 = w_prosh + '.' + w_prosd
      *
     c                   eval      f_avbk26 = '176'
     c*                  eval      w_belo = w_tmva
     c                   eval      w_belo = %abs(w_tmva)
     c                   eval      f_avbe26 = w_beloh + '.' + w_belod
     c                   eval      f_avbs26 = *blank
     c                   if        w_tmva < 0
     c                   eval      f_avbs26 = '-'
     c                   endif
      *
     c                   eval      fnaxln = f_ed26
     c                   exsr      write_outp
      *
     c                   if        p_rapp = '1'
     c                   exsr      skr_rappl
     c                   endif
      *
     c                   exsr      upd_uf_log
      *
     c                   eval      p_anta = p_anta + 1
     c                   eval      s_binr = *zero
     c                   eval      b_hode = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for skriving av fysisk record - m/konv. av bokstaver
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     write_outp    begsr
      *
     c*    'Æ':X'46'     xlate     fnaxln        fnaxln
     c*    'æ':X'A0'     xlate     fnaxln        fnaxln
     c*    'Ø':X'77'     xlate     fnaxln        fnaxln
     c*    'ø':X'90'     xlate     fnaxln        fnaxln
     c*    'Å':X'2A'     xlate     fnaxln        fnaxln
     c*    'å':X'EF'     xlate     fnaxln        fnaxln
      *
     c                   write     fnaxpfr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for mellomlagring av hodesummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     save_fsum     begsr
      *
     c                   eval      s_fsum = sofsum
     c                   eval      s_salp = sosalp
     c                   eval      s_salh = sosalh
     c                   eval      s_salf = sosalf
     c                   eval      s_rk1p = sork1p
     c                   eval      s_rk2p = sork2p
     c                   eval      s_rkop = sorkop
     c                   eval      s_rk1h = sork1h
     c                   eval      s_rk2h = sork2h
     c                   eval      s_rkoh = sorkoh
     c                   eval      s_rk1f = sork1f
     c                   eval      s_rk2f = sork2f
     c                   eval      s_rkof = sorkof
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kontroll av utgående fakturalogg
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_uf_log    begsr
      *
     c                   eval      b_logf = *off
      *
      * Sjekk omkjøring og kjøredato
      *
     c                   if        p_omkj = *blank
     c                   if        fnukda <> *loval
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        p_omkj = '1'
     c                   if        fnukda = *loval
     c                   leavesr
     c                   endif
     c                   if        p_fnum <= 1 or
6.nu c                             p_tnum = 99999999
     c                   leavesr
     c                   endif
     c                   if        sobinr < p_fnum or
     c                             sobinr > p_tnum
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   eval      b_logf = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdat. av utgående fakturalogg
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     upd_uf_log    begsr
      *
     c                   eval      fnuflu_aarr = p_aarr
     c                   eval      fnuflu_binr = s_binr
     c     fnuflu_key    chain     fnuflur
     c                   if        %found
     c                   time                    fnukda
     c                   time                    fnukti
     c                   eval      fnukws = l_wsid
     c                   eval      fnutel = fnutel + 1
     c                   time                    fnueda
     c                   time                    fnueti
     c                   eval      fnueus = l_user
     c                   update    fnuflur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekk om samlefaktura
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_saml      begsr
      *
     c                   eval      b_saml = *off
     c                   eval      w_anto = *zero
     c                   eval      w_fsum = *zero
     c                   eval      w_kidr = *zero
     c                   eval      w_kidd = *blank
      *
     c                   eval      sohelr_aarr = soaarr
     c                   eval      sohelr_binr = sobinr
      *
     c     sohelr_key    setll     sohelr
     c     sohelr_key    reade     sohelr
      *
     c                   dow       not %eof
      *
     c                   eval      w_anto = w_anto + 1
      *
     c                   eval      w_fsum = xxfsum
     c                   eval      w_kidr = xxkidr
     c                   eval      w_kidd = xxkidd
      *
     c     sohelr_key    reade     sohelr
     c                   enddo
      *
     c                   if        w_anto > 1
     c                   eval      b_saml = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for skriving av rapportheader v/ utskrift valgt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_rapph     begsr
      *
     c                   eval      a1firm = l_firm
     c                   eval      a1fnav = l_fnav
     c                   eval      a1wsid = l_wsid
     c                   time                    a1time
     c                   time                    a1date
     c                   eval      a1user = l_user
      *
     c                   eval      a1ftyp = fmtbes
      *
     c                   if        p_omkj = '1'
     c                   eval      a1omkj = 'Omkjøring'
     c                   else
     c                   eval      a1omkj = *blank
     c                   endif
      *
     c                   write     a1hdr                                30
      *
     c                   eval      b_uskr = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for skriving av rapportlinje v/ utskrift valgt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_rappl     begsr
      *
     c                   eval      a1fsum = s_fsum
      *
     c                   write     a1det                                30
      *
      * Er det overflow ?
      *
     c                   select
     c                   when      *in30 = *on
     c                   exsr      xovrfl
     c                   endsl
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for overflow                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xovrfl        begsr
      *
     c                   write     a1hdr                                30
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    p_aarr
     c                   parm                    p_faty
6.nu c                   parm                    p_fnum
6.nu c                   parm                    p_tnum
     c                   parm                    p_omkj
     c                   parm                    p_rapp
     c                   parm                    p_anta
     c                   parm                    p_feil
     c                   parm                    p_vkun
     c                   parm                    p_filn
     c                   parm                    p_filk
      *
     c                   eval      w_firm = l_firm
      *
     c                   if        %check(c_digits : p_vkun) > 0
     c                   eval      w_vkun = *zero
     c                   else
     c                   move      p_vkun        w_vkun
     c                   endif
      *
     c                   eval      p_filn = *blank
     c                   eval      p_filk = *blank
      *
     c                   eval      p_feil = *blank
      *
      * Key for les av kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på faktura-hode-register
      *
     c     sohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sohel1_aarr
     c                   kfld                    sohel1_binr
      *
      * Key for oppslag på faktura-hode-register (les ant. ordre)
      *
     c     sohelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sohelr_aarr
     c                   kfld                    sohelr_binr
      *
      * Key for les av fakturalinjer
      *
     c     sodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sodtl1_aarr
     c                   kfld                    sodtl1_binr
     c                   kfld                    sodtl1_numm
      *
      * Key for firma
      *
     c     afir_key      klist
     c                   kfld                    w_firm
      *
      * Key for linjetyper
      *
     c     vltyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltyl1_ltyp
      *
      * Key for ordretyper
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for oppslag på ofak-koderegister (statuskoder)
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for les på lager
      *
     c     ra10l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra10l1_jkod
      *
      * Key for kode
      *
     c     ra18l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra18l1_rkod
      *
      * Key for statuskode 8
      *
     c     raa8l1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på vareregister
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for les av fakturatype
      *
     c     fmftl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fmftl1_faty
      *
      * Key for oppslag av fakturalogg (type/dato)
      *
     c     fnufl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fnufl2_ufat
     c                   kfld                    fnufl2_ukda
      *
      * Key for oppdat. av fakturalogg
      *
     c     fnuflu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fnuflu_aarr
     c                   kfld                    fnuflu_binr
      *
     c                   endsr
      *
