     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON
      * Program . . . . : RK711R
      * Beskrivelse . . : Overføring til Elcon-format
      *
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       040805 AMD  Nytt program
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fraa9l1    if   e           k disk    rename(raa9pfr:raa9l1r)
     frefal1    if   e           k disk    rename(refapfr:refal1r)
     frekul1    if   e           k disk    rename(rekupfr:rekul1r)
     fref2pf    uf a e             disk
     frek2pf    uf a e             disk
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av variable
      *
     d w_sted          s             25
     d w_klie          s              4  0
      *
      * Definering av variabler i nøkler
      *
     d w_firm          s                   like(rnfirm)
      *
      * Datastruktur for fakturalinjer
      *
     d                 ds
     d d_frec_1                     256
     d  d_tkod_1                      1  0 overlay(d_frec_1:1)
     d  d_klie_1                      4  0 overlay(d_frec_1:2)
     d  d_kund_1                      9  0 overlay(d_frec_1:6)
     d  d_biln_1                      8  0 overlay(d_frec_1:15)
     d  d_bdat_1                      6  0 overlay(d_frec_1:23)
     d  d_forf_1                      6  0 overlay(d_frec_1:29)
     d  d_fbel_1                     11  2 overlay(d_frec_1:35)
     d  d_rabb_1                      5  0 overlay(d_frec_1:46)
     d  d_dum1_1                      8    overlay(d_frec_1:51)
     d  d_ftxt_1                     40    overlay(d_frec_1:59)
     d  d_selg_1                      4    overlay(d_frec_1:99)
     d  d_proj_1                      7    overlay(d_frec_1:103)
     d  d_valk_1                      3    overlay(d_frec_1:110)
     d  d_valb_1                     11  2 overlay(d_frec_1:113)
     d  d_dum2_1                      5    overlay(d_frec_1:124)
      *
      * Datastruktur for kundetabell
      *
     d                 ds
     d d_krec_2                     256
     d  d_kode_2                      1    overlay(d_krec_2:1)
     d  d_vers_2                      4  0 overlay(d_krec_2:2)
     d  d_klie_2                      4  0 overlay(d_krec_2:6)
     d  d_kund_2                      9  0 overlay(d_krec_2:10)
     d  d_ftak_2                     11  0 overlay(d_krec_2:19)
     d  d_nav1_2                     35    overlay(d_krec_2:30)
     d  d_nav2_2                     20    overlay(d_krec_2:65)
     d  d_adr1_2                     30    overlay(d_krec_2:85)
     d  d_ponr_2                      4  0 overlay(d_krec_2:115)
     d  d_sted_2                     23    overlay(d_krec_2:119)
     d  d_kper_2                     20    overlay(d_krec_2:142)
     d  d_tlfn_2                     12    overlay(d_krec_2:162)
     d  d_land_2                     20    overlay(d_krec_2:174)
     d  d_lana_2                      2    overlay(d_krec_2:194)
     d  d_lann_2                      3  0 overlay(d_krec_2:196)
     d  d_bgir_2                     11  0 overlay(d_krec_2:199)
     d  d_dise_2                      4  0 overlay(d_krec_2:210)
     d  d_nav3_2                     35    overlay(d_krec_2:214)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Lese Kunde-transaksjoner
      *
     c                   read      refal1
     c                   dow       not %eof
      *
      * Oppdater faktura-tabell
      *
     c                   exsr      xopd_fakt
     c                   read      refal1
      *
     c                   enddo
      *
      * Lese Kunde-register
      *
     c                   read      rekul1
     c                   dow       not %eof
      *
      * Oppdater kunde-tabell
      *
     c                   exsr      xopd_kund
     c                   read      rekul1
      *
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdatering av fil for faktura-poster
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xopd_fakt     begsr
      *
      * Fakturaposter
      *
      *
      * Faktura eller
      * kreditnota ?
      *
     c                   if        rnbelØ < 0
     c                   eval      d_tkod_1 = 9
     c                   else
     c                   eval      d_tkod_1 = 1
     c                   endif
      *
     c                   eval      d_klie_1 = w_klie
     c                   eval      d_kund_1 = rnkund
     c                   eval      d_biln_1 = rnbiln
     c                   eval      d_fbel_1 = rnbelØ
     c                   eval      d_rabb_1 = 0
     c                   eval      d_dum1_1 = *blank
     c                   eval      d_ftxt_1 = *blank
     c                   eval      d_selg_1 = *blank
     c                   eval      d_proj_1 = *blank
     c                   eval      d_valk_1 = *blank
     c                   eval      d_valb_1 = 0
     c                   eval      d_dum2_1 = *blank
      *
     c                   eval      d_bdat_1 = 0
     c                   eval      d_forf_1 = 0
     c                   if        rnbdat   <> *loval
     c     *ymd          move      rnbdat        d_bdat_1
     c                   endif
     c                   if        rnfdat   <> *loval
     c     *ymd          move      rnfdat        d_forf_1
     c                   endif
      *
     c                   eval      refrec   = d_frec_1
     c                   write     ref2pfr
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdatering av fil for kunde-tabell
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xopd_kund     begsr
      *
      * Kunde-tabell
      *
     c                   move      rksted        w_sted
      *
     c                   eval      d_kode_2 = 'K'
     c                   eval      d_vers_2 = 9409
     c                   eval      d_klie_2 = w_klie
     c                   eval      d_kund_2 = rkkund
     c                   eval      d_ftak_2 = rkftnr
     c                   eval      d_nav1_2 = rknavn
     c                   eval      d_nav2_2 = *blank
     c                   eval      d_adr1_2 = rkgate
     c                   eval      d_ponr_2 = rkponr
     c                   eval      d_sted_2 = w_sted
     c                   eval      d_kper_2 = *blank
     c                   eval      d_tlfn_2 = rktlfn
     c                   eval      d_land_2 = *blank
     c                   eval      d_lana_2 = *blank
     c                   eval      d_lann_2 = 0
     c                   eval      d_bgir_2 = 0
     c                   eval      d_dise_2 = 0
     c                   eval      d_nav3_2 = *blank
      *
      * Oppdater kunde-tabell Elcon
      *
     c                   eval      rekrec   = d_krec_2
     c                   write     rek2pfr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for statustabell factoring
      *
     c     raa9l1_key    klist
     c                   kfld                    w_firm
      *
      * Key for kundetransaksjoner
      *
     c     refal1_key    klist
     c                   kfld                    w_firm
      *
      * Key for kunderegister
      *
     c     rekul1_key    klist
     c                   kfld                    w_firm
      *
      * Legge inn firmanummer
      *
     c                   eval      w_firm = l_firm
      *
      * Hent inn klientnummer
      *
     c     raa9l1_key    chain     raa9l1r
     c                   if        %found
     c                   eval      w_klie = ra9kli
     c                   endif
      *
     c                   endsr
      *
