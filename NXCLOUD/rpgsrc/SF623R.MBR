      /TITLE  Utskrift av statistikk
     H datedit(*dmy.) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  System. . . . . : ASSTAT                                       *
      *  Program . . . . : SF623R                                       *
      *  Beskrivelse . . : Utskrift av statistikk                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  Spesialversjon. :                                              *
      *  Tilpasset for . :                                              *
      *                                                                 *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.*
      *  --------- ------  ---------------------------------------------*
      *  V5.30     080304  Henting av navn kundekategori var feil       *
      *  V5.30     080304  Henting av navn rabattkategori var feil      *
      *  V5.30     230604  Kundeprosjekttekst ble ikke hentet      KOB  *
      *  V5.30     200904  Fjernet rabattgruppe                    KOB  *
      *  V5.50     301006  Lagt inn selger kasse                   KOB  *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     Fskwwpf    ip   e           k disk
     Fskpml1    if   f  512    15aidisk    keyloc(1)
     Frkunl1    if   e           k disk
     Frlevl1    if   e           k disk
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     Fvvasl1    if   e           k disk    rename(vvaspfr:vvasl1r)
     Fvahgl1    if   e           k disk
     Fvaugl1    if   e           k disk
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     Fvotyl1    if   e           k disk
     Fra06pf    if   e           k disk
     Fra07pf    if   e           k disk
     Fra08pf    if   e           k disk
     Fra09l1    if   e           k disk    rename(ra09pfr:ra09l1r)
     Fra10pf    if   e           k disk
     Fra11pf    if   e           k disk
     Fra12pf    if   e           k disk
     Ffkprl1    if   e           k disk
     Fvltpl1    if   e           k disk
     Fsf623p    o    e             printer oflind(*in30)
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tabeller                                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     D rt              s             25    dim(60) ctdata perrcd(1)              RAPPORT TEKSTER
      *
     D w_uhgrp         s              5
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Data Strukturer                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     D                 ds
     D skpml1_key                    15
     D  w1firm                        3  0 overlay(skpml1_key:1)
     D  wwtype                       10    overlay(skpml1_key:4)
     D  wwkode                        2    overlay(skpml1_key:14)
      *
     D                 ds
     D beg                           15
     D  begnum                       15  0 overlay(beg)
     D   k6                           6  0 overlay(begnum)
      *
     D                 ds
     D wwxgrp                         7  0
     D  wwxogr                        2  0 overlay(wwxgrp:1)
     D  wwxhgr                        2  0 overlay(wwxgrp:3)
     D  wwxugr                        3  0 overlay(wwxgrp:5)
      *
     D                uds
     D lcwsid                901    910
     D lcuser                911    920
     D lcfirm                944    946  0
     D lcnavn                951    980
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Felt defineringer                                              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     D ano4            s                   like(ano7)
     D ano4u           s              8  0
     D ano5            s                   like(ano7)
     D ano5u           s                   like(ano4u)
     D ano6            s                   like(ano7)
     D ano6u           s                   like(ano4u)
     D ano7            s              8  0
     D ano7u           s                   like(ano4u)
     D begtek          s             35
     D brtt2           s             35
     D brtt3           s             35
     D bru2            s             11  2
     D bru4            s                   like(bru7)
     D bru5            s                   like(bru7)
     D bru6            s                   like(bru7)
     D bru7            s              9  0
     D gjo4            s              8  0
     D gjo5            s                   like(gjo4)
     D gjo6            s                   like(gjo4)
     D gjo7            s                   like(gjo4)
     D anl2            s              8  0
     D anl4            s                   like(anl7)
     D anl5            s                   like(anl7)
     D anl6            s                   like(anl7)
     D anl7            s              8  0
     D net4            s              9  0
     D net5            s                   like(net4)
     D net6            s                   like(net4)
     D net7            s                   like(net4)
     D gjl4            s              8  0
     D gjl5            s                   like(gjl4)
     D gjl6            s                   like(gjl4)
     D gjl7            s                   like(gjl4)
     D rap             s              2  0
     D rab2            s             11  2
     D rab4            s              8  0
     D rab5            s                   like(rab4)
     D rab6            s                   like(rab4)
     D rab7            s                   like(rab4)
     D rptek           s             30
      *
     D utime           s              6  0
     D wwfkod          s                   like(rafkod)
     D wwgkod          s                   like(ragkod)
     D wwhkod          s                   like(rahkod)
     D wwikod          s                   like(raikod)
     D wwjkod          s                   like(rajkod)
     D wwkkod          s                   like(rakkod)
     D wwkund          s                   like(rkkund)
     D wwlevr          s                   like(rllevr)
     D wwlkod          s                   like(ralkod)
     D wwotyp          s                   like(vaotyp)
     D wwoogr          s                   like(vgoogr)
     D wwhogr          s                   like(vghogr)
     D wwhhgr          s                   like(vghhgr)
     D wwuogr          s                   like(vguogr)
     D wwuhgr          s                   like(vguhgr)
     D wwuugr          s                   like(vguugr)
     D wwvare          s                   like(vvvare)
     D wwfirm          s                   like(spfirm)
     D wwkpro          s                   like(flkpro)
     D wwlety          s                   like(vylety)
     D w_timestamp     s             12  0
     D w_date          s              6  0
     D wwbinr          s                   like(sfbinr)
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Input                                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     Iskwwpfr       01
     I                                          sffirm        l7
     I                                          swsrt1        l6
     I                                          swsrt2        l5
     I                                          swsrt3        l4
      *
     Iskpml1    ns  01   14 c0   15 c0
     I                                  1    3 0spfirm
     I                                  4   13  sptype
     I                                 14   15 0spkode
     I                                 31   60  sptext
     I                                 61   62 0rp1
     I                                 63   64 0rp2
     I                                 65   66 0rp3
     I                                 67   67  spes1
     I                                 68   68  spes2
     I                                 69   69  spes3
     I                                 70   70  spes4
     I                                 71   71  spes5
     I                                 73   74 0spmnd
     I                                 75   78 0spaar
     I                                 79   79  spsmli
     I                                 80   80  spsps7
     I                                 81   81  spsss1
     I                                 82   82  spsss2
     I                                 83   83  spsss3
     I                                 84   84  spslet
      *
     I          ns  01   14 c4   15 c1
     I                                  1    3 0spfirm
     I                                  4   13  sptype
     I                                 14   15 0spkode
     I                                 31   60  sptext
     I                                 61   64 0spfaar
     I                                 65   66 0spfmnd
     I                                 67   70 0sptaar
     I                                 71   72 0sptmnd
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Calculationn                                                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
B001 C   l6              do                                                               .....
 001 C                   eval      bru6 = 0                                                   .
 001 C                   eval      rab6 = 0                                                   .
 001 C                   eval      ano6 = 0                                                   .
 001 C                   eval      anl6 = 0                                                   .
 001  *                                                                  .
 001 C                   move      swsrt1        beg                                          .
 001 C                   eval      rap = rp1                                                  .
 001 C                   exsr      raprut                                                     .
 001 C                   movel     rptek         begtek                                       .
 001  *                                                                  .
E001 C                   enddo                                                            .....
      /eject
      *
B001 C   l5              do                                                               .....
 001 C                   eval      bru5 = 0                                                   .
 001 C                   eval      rab5 = 0                                                   .
 001 C                   eval      ano5 = 0                                                   .
 001 C                   eval      anl5 = 0                                                   .
 001 C                   eval      brtt2 = *blank                                             .
 001  *                                                                  .
B002 C                   if        rp2 <> *zero                                         ..... .
 002 C                   move      swsrt2        beg                                        . .
 002 C                   eval      rap = rp2                                                . .
 002 C                   exsr      raprut                                                   . .
 002 C                   movel     rptek         brtt2                                      . .
E002 C                   endif                                                          ..... .
 001  *                                                                  .
E001 C                   enddo                                                            .....
      /eject
      *
B001 C   l4              do                                                               .....
 001 C                   eval      bru4 = 0                                                   .
 001 C                   eval      rab4 = 0                                                   .
 001 C                   eval      ano4 = 0                                                   .
 001 C                   eval      anl4 = 0                                                   .
 001  *                                                                  .
 001 C                   eval      bru2 = 0                                                   .
 001 C                   eval      rab2 = 0                                                   .
 001 C                   eval      anl2 = 0                                                   .
 001  *                                                                  .
 001 C                   eval      brtt3 = *blank                                             .
 001  *                                                                  .
B002 C                   if        rp3 <> *zero                                         ..... .
 002 C                   move      swsrt3        beg                                        . .
 002 C                   eval      rap = rp3                                                . .
 002 C                   exsr      raprut                                                   . .
 002 C                   movel     rptek         brtt3                                      . .
E002 C                   endif                                                          ..... .
 001  *                                                                  .
E001 C                   enddo                                                            .....
      /eject
      *
      * Summering av enkeltposter
      *
     C                   eval      bru2 = bru2 + sfsumb
     C                   eval      rab2 = rab2 + sfrab1 + sfrab2 + sfrabo
      *
     C                   if        sfbinr <> wwbinr
     C                   eval      ano4 = ano4 + 1
     C                   eval      wwbinr = sfbinr
     C                   endif
     C                   eval      anl2 = anl2 + 1
      *
     C     xslutt        tag
      *
      /eject
      *
      * Brudd på L4 (SRT3)
      *
     CL4                 exsr      brdl4
     CL4                 if        swsrt3 <> *blank
     C                             and spes3 = ' '
     CL4                 write     TOL41                                30
     Cl4 73              move      *on           *in30
     CL4                 exsr      overflow
     CL4                 endif
      *
      * BRUDD PÅ L5  (SRT2)
      *
     Cl5                 exsr      brdl5
     Cl5                 if        swsrt2 <> *blank
     C                             and spes2 = ' '
     Cl5                 write     TOL51                                30
     Cl5 72              move      *on           *in30
     Cl5                 exsr      overflow
     Cl5                 endif
      *
      * BRUDD PÅ L6  (SRT1)
      *
     Cl6                 exsr      brdl6
      *
     Cl6                 if        swsrt1 <> *blank
     C                             and swsrt2 <> *blank and spes1 = ' '
     Cl6                 write     TOL62A                               30
     Cl6 71              move      *on           *in30
     Cl6                 exsr      overflow
     cl6                 else
     Cl6                 write     TOL62b                               30
     Cl6 71              move      *on           *in30
     Cl6                 exsr      overflow
     cl6                 endif
      *
      * BRUDD PÅ L7  (FIRMA)
      *
     Cl7                 if        *in64 = *off
     Cl7                 exsr      brdl7
     Cl7                 write     TOL71                                30
     Cl7                 endif
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for behandling av rapport                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     raprut        begsr
      *
     C                   eval      rptek = *blank
      *
      * -----------------
      * Alt. hovedgruppe
      * -----------------
      *
B001 C                   if        rap = 11                                               .....
 001 C                   movel     beg           wwhhgr                                       .
 001 C     hgrkey        chain     vahgl1                             50                      .
B002 C                   if        *in50 = *off
 002 C                   movel     vfhtxt        rptek                                        .
E002 C                   endif
 001 C                   goto      endrut                                                     .
E001 C                   endif                                                            .....
     *
     * -----------------
     * Alt. undergruppe
     * -----------------
     *
B001 C                   if        rap = 12                                               .....
     C                   movel     beg           w_uhgrp
 001 C                   movel     w_uhgrp       wwuhgr
 001 C                   move      w_uhgrp       wwuugr
 001 C     ugrkey        chain     vaugl1                             50
B002 C                   if        *in50 = *off
 002 C                   movel     vfutxt        rptek
E002 C                   endif
 001 C                   goto      endrut
E001 C                   endif                                                            .....
     *
     * ------------------
     * Overgruppe
     * ------------------
     *
B001 C                   if        rap = 14                                               .....
 001 C                   movel     beg           wwxgrp                                       .
 001 C                   eval      wwoogr = wwxogr                                            .
 001 C     vogrl1_key    chain     vogrl1                             50                      .
B002 C                   if        *in50 = *off
 002 C                   movel     vgotxt        rptek                                        .
E002 C                   endif
 001 C                   goto      endrut                                                     .
E001 C                   endif                                                            .....
     *
     * ------------------
     * Hovedgruppe
     * ------------------
     *
B001 C                   if        rap = 15                                               .....
 001 C                   movel     beg           wwxgrp                                       .
 001 C                   eval      wwhogr = wwxogr                                            .
 001 C                   eval      wwhhgr = wwxhgr                                            .
 001 C     vhgrl1_key    chain     vhgrl1                             50                      .
B002 C                   if        *in50 = *off
 002 C                   movel     vghtxt        rptek                                        .
E002 C                   endif
 001 C                   goto      endrut                                                     .
E001 C                   endif                                                            .....
     *
     * ------------------
     * Undergruppe
     * ------------------
     *
B001 C                   if        rap = 16                                               .....
 001 C                   movel     beg           wwxgrp                                       .
 001 C                   eval      wwuogr = wwxogr                                            .
 001 C                   eval      wwuhgr = wwxhgr                                            .
 001 C                   eval      wwuugr = wwxugr                                            .
 001 C     vugrl1_key    chain     vugrl1                             50
B002 C                   if        *in50 = *off
 002 C                   movel     vgutxt        rptek
E002 C                   endif
 001 C                   goto      endrut
E001 C                   endif                                                            .....
     *
     * ---------------
     * Kundekategori
     * ---------------
     *
B001 C                   if        rap = 21                                               .....
 001 C                   movel     beg           wwkkod
 001 C     ra11pf_key    chain     ra11pf                             50
B002 C                   if        *in50 = *off
 002 C                   movel     raktxt        rptek
E002 C                   endif
 001 C                   goto      endrut
E001 C                   endif                                                            .....
     *
     * ---------------
     * Rabattkategori
     * ---------------
     *
B001 C                   if        rap = 22                                               .....
 001 C                   movel     beg           wwfkod
 001 C     ra06pf_key    chain     ra06pf                             50
B002 C                   if        *in50 = *off
 002 C                   movel     raftxt        rptek
E002 C                   endif
 001 C                   goto      endrut
E001 C                   endif                                                            .....
     *
     * ---------------
     * Distrikt
     * ---------------
     *
B001 C                   if        rap = 23                                               .....
 001 C                   movel     beg           wwhkod
 001 C     ra08pf_key    chain     ra08pf                             50
B002 C                   if        *in50 = *off
 002 C                   movel     rahtxt        rptek
E002 C                   endif
 001 C                   goto      endrut
E001 C                   endif                                                            .....
     *
     * ---------------
     * Selger ordre
     * ---------------
     *
B001 C                   if        rap = 24                                               .....
 001 C                   movel     beg           wwikod
 001 C     ra09l1_key    chain     ra09l1                             50
B002 C                   if        *in50 = *off
 002 C                   movel     raitxt        rptek
E002 C                   endif
 001 C                   goto      endrut
E001 C                   endif                                                            .....
     *
     * ---------------
     * Land
     * ---------------
     *
B001 C                   if        rap = 25                                               .....
 001 C                   move      beg           wwlkod
 001 C     ra12pf_key    chain     ra12pf                             50
B002 C                   if        *in50 = *off
 002 C                   movel     raltxt        rptek
E002 C                   endif
 001 C                   goto      endrut
E001 C                   endif                                                            .....
     *
     * ---------------
     * Selger kasse
     * ---------------
     *
B001 C                   if        rap = 27                                               .....
 001 C                   movel     beg           wwikod
 001 C     ra09l1_key    chain     ra09l1                             50
B002 C                   if        *in50 = *off
 002 C                   movel     raitxt        rptek
E002 C                   endif
 001 C                   goto      endrut
E001 C                   endif                                                            .....
     *
     * ---------------
     * Varenummer
     * ---------------
     *
B001 C                   if        rap = 31                                               .....
 001 C                   movel     beg           wwvare
 001 C     vvarl1_key    chain     vvarl1                             50
B002 C                   if        *in50 = *off
 002 C                   movel     vvtek1        rptek
E002 C                   endif
B002 C                   if        *in50 = *on
 001 C     vvarl1_key    chain     vvasl1                             50
B002 C                   if        *in50 = *off
 002 C                   movel     vvtek1        rptek
E002 C                   else
 002 C                   movel     sftek1        rptek
E002 C                   endif
E002 C                   endif
 001 C                   goto      endrut
E001 C                   endif                                                            .....
     *
     * ---------------
     * Kundenummer
     * ---------------
     *
B001 C                   if        rap = 32                                               .....
 001 C                   movel     k6            wwkund
 001 C     rkunl1_key    chain     rkunl1                             50
B002 C                   if        *in50 = *off
 002 C                   movel     rknavn        rptek
E002 C                   endif
 001 C                   goto      endrut
E001 C                   endif                                                            .....
     *
     * ---------------
     * Kundeprosjekt
     * ---------------
     *
B001 C                   if        rap = 33                                               .....
 001 C                   movel     k6            wwkpro
B001 C                   if        wwkpro <> *zeros                                       .....
 001 C                   move      sfvkun        wwkund
 001 C     fkprl1_key    chain     fkprl1                             50
B002 C                   if        *in50 = *off
 002 C                   movel     flnavn        rptek
E002 C                   endif
E002 C                   endif
 001 C                   goto      endrut
E001 C                   endif                                                            .....
     *
     * ---------------
     * Periode
     * ---------------
     *
B001 C                   if        rap = 41                                               .....
 001 C                   movel     rt(41)        rptek
E001 C                   endif                                                            .....
     *
     * ---------------
     * Fakturanr/
     * Kreditnotanr.
     * ---------------
     *
B001 C                   if        rap = 42                                               .....
 001 C                   movel     rt(42)        rptek
E001 C                   endif                                                            .....
     *
     * ---------------
     * Fakturadato
     * ---------------
     *
B001 C                   if        rap = 43                                               .....
 001 C                   movel     rt(43)        rptek
E001 C                   endif                                                            .....
     *
     * ---------------
     * Ordretype
     * ---------------
     *
B001 C                   if        rap = 51                                               .....
 001 C                   movel     beg           wwotyp
 001 C     votyl1_key    chain     votyl1                             50
B002 C                   if        *in50 = *off
 002 C                   movel     vaotxt        rptek
E002 C                   endif
 001 C                   goto      endrut
E001 C                   endif                                                            .....
     *
     * ---------------
     * Avdeling
     * ---------------
     *
B001 C                   if        rap = 52                                               .....
 001 C                   movel     beg           wwgkod
 001 C     ra07pf_key    chain     ra07pf                             50
B002 C                   if        *in50 = *off
 002 C                   movel     ragtxt        rptek
E002 C                   endif
 001 C                   goto      endrut
E001 C                   endif                                                            .....
     *
     * ---------------
     * Lager
     * ---------------
     *
B001 C                   if        rap = 53                                               .....
 001 C                   movel     beg           wwjkod
 001 C     ra10pf_key    chain     ra10pf                             50
B002 C                   if        *in50 = *off
 002 C                   movel     rajtxt        rptek
E002 C                   endif
 001 C                   goto      endrut
E001 C                   endif                                                            .....
     *
     * ---------------
     * 1. siffer
     * Kundekategori
     * ---------------
     *
B001 C                   if        rap = 26                                               .....
 001 C                   movel     rt(26)        rptek
 001 C                   movel     beg           wwkkod
 001 C     ra11pf_key    chain     ra11pf                             50
B002 C                   if        *in50 = *off
 002 C                   movel     raktxt        rptek
E002 C                   endif
 001 C                   goto      endrut
E001 C                   endif                                                            .....
     *
     * ---------------
     * Leveringstype
     * ---------------
     *
B001 C                   if        rap = 55                                               .....
 001 C                   movel     rt(55)        rptek
 001 C                   movel     beg           wwlety
 001 C     vltpl1_key    chain     vltpl1                             50
B002 C                   if        *in50 = *off
 002 C                   movel     vybesk        rptek
E002 C                   endif
 001 C                   goto      endrut
E001 C                   endif                                                            .....
     *
     * ---------------
     * Leverandørnummer
     * ---------------
     *
B001 C                   if        rap = 56                                               .....
 001 C                   movel     k6            wwlevr
 001 C     rlevl1_key    chain     rlevl1                             50
B002 C                   if        *in50 = *off
 002 C                   movel     rlnavn        rptek
E002 C                   endif
 001 C                   goto      endrut
E001 C                   endif                                                            .....
     *
     C     endrut        endsr
     *
     /EJECT
     * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     * Subrutine for brudd på L4 (SRT3)                                *
     * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     *
     C     brdl4         begsr
     *
     C                   eval(h)   bru4 = bru2
     C                   eval(h)   rab4 = rab2
     C                   eval(h)   anl4 = anl2
     *
     C                   eval      net4 = bru4 - rab4
     *
     c                   if        ano4  <> *zero
     C                   eval      gjo4 = net4 / ano4
     c                   endif
     c                   if        anl4  <> *zero
     C                   eval      gjl4 = net4 / anl4
     c                   endif
     *
     C                   eval      bru5 = bru5 + bru4
     C                   eval      rab5 = rab5 + rab4
     *
     C                   eval      ano5 = ano5 + ano4
     C                   eval      anl5 = anl5 + anl4
 001 C                   eval      ano4u = ano4
     *
     C                   endsr
     /EJECT
     * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     * Subrutine for brudd på L5 (SRT2)                                *
     * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     *
     C     brdl5         begsr
     *
     C                   eval      net5 = bru5 - rab5
     c                   if        ano5  <> *zero
     C                   eval      gjo5 = net5 / ano5
     c                   endif
     c                   if        anl5  <> *zero
     C                   eval      gjl5 = net5 / anl5
     c                   endif
     *
     C                   eval      bru6 = bru6 + bru5
     C                   eval      rab6 = rab6 + rab5
     *
     C                   eval      ano6 = ano6 + ano5
     C                   eval      anl6 = anl6 + anl5
 001 C                   eval      ano5u = ano5
     *
     C                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for brudd på L6 (SRT1)                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     brdl6         begsr
      *
     c                   eval      net6 = bru6 - rab6
     c                   if        ano6  <> *zero
     c                   eval      gjo6 = net6 / ano6
     c                   endif
     c                   if        anl6  <> *zero
     c                   eval      gjl6 = net6 / anl6
     c                   endif
      *
     c                   eval      bru7 = bru7 + bru6
     c                   eval      rab7 = rab7 + rab6
      *
     c                   eval      ano7 = ano7 + ano6
     c                   eval      anl7 = anl7 + anl6
     c                   eval      ano6u = ano6
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for brudd på L7 (Firma)                               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     brdl7         begsr
      *
     c                   eval      net7 = bru7 - rab7
     c                   if        ano7  <> *zero
     c                   eval      gjo7 = net7 / ano7
     c                   endif
     c                   if        anl7  <> *zero
     c                   eval      gjl7 = net7 / anl7
     c                   endif
      *
     c                   eval      ano7u = ano7
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for behandling av overflow                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     overflow      begsr
     c                   if        *in30 = *on
     c                   if        rp1 > 0
     c                   eval      h1srt1 = rt(rp1)
     c                   endif
     c                   if        rp2 > 0
     c                   eval      h1srt2 = rt(rp2)
     c                   endif
     c                   if        rp3 > 0
     c                   eval      h1srt3 = rt(rp3)
     c                   endif
 001 c                   write     HD1A
     c                   endif
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     *inzsr        begsr
      *
     c                   eval      begtek = *blank
     c                   eval      rptek = *blank
     c                   eval      brtt2 = *blank
     C                   eval      brtt3 = *blank
     C                   eval      rap = *zero
     *
     C                   eval      net4 = 0
     C                   eval      gjo4 = 0
     C                   eval      rab4 = 0
     C                   eval      gjl4 = 0
     C                   eval      ano4u = 0
     *
     C                   eval      bru2 = 0
     C                   eval      rab2 = 0
     C                   eval      anl2 = 0
     *
     C                   eval      ano7 = 0
     C                   eval      bru7 = 0
     C                   eval      rab7 = 0
     C                   eval      anl7 = 0
     *
     C                   eval      *in20 = *off
     C                   eval      *in96 = *off
     *
     * Behandle kriterier for valg
     *
     C     *entry        plist
     C                   parm                    dstype           10
     *
     C                   time                    w_timestamp
     C                   move      w_timestamp   w_date
      *
     C                   eval      skpml1_key = *blank
     *
     C                   move      lcfirm        w1firm
     C                   move      lcfirm        wwfirm
     C                   movel     dstype        wwtype
     C                   move      '00'          wwkode
     C     skpml1_key    chain     skpml1                             50
     *
     C                   eval      *in61 = spes1 <> ' '                          BEGREP 1
     C                   eval      *in62 = spes2 <> ' '                          BEGREP 2
     C                   eval      *in63 = spes3 <> ' '                          BEGREP 3
     C                   eval      *in64 = spes4 <> ' '                          FIRMA
     C                   eval      *in65 = spes5 <> ' '                          TOTALT
     *
     C                   eval      *in57 = rp2 <> 0
     C                   eval      *in58 = rp3 <> 0
     *
     C                   eval      *in70 = spsps7 <> ' '                         BEGREP 1
     C                   eval      *in71 = spsss1 <> ' '                         BEGREP 1
     C                   eval      *in72 = spsss2 <> ' '                         BEGREP 1
     C                   eval      *in73 = spsss3 <> ' '                         BEGREP 1
     *
     C                   time                    utime
     *
     C                   if        rp1 > 0
     C                   eval      h1srt1 = rt(rp1)
     C                   endif
     *
     C                   if        rp2 > 0
     C                   eval      h1srt2 = rt(rp2)
     C                   endif
     *
     C                   if        rp3 > 0
     C                   eval      h1srt3 = rt(rp3)
     C                   endif
     *
     C                   eval      h1type = sptype
     C                   eval      h1text = sptext
     *
     * Hent periode fra - til
     *
     C                   move      '41'          wwkode
     C     skpml1_key    chain     skpml1                             50
     *
     C                   eval      h1fmnd = spfmnd
     C                   eval      h1faar = spfaar
     C                   eval      h1tmnd = sptmnd
     C                   eval      h1taar = sptaar
     *
 001 C                   write     HD1A
     *
     * Key for vareregister
     *
     C     vvarl1_key    klist
     C                   kfld                    wwfirm
     C                   kfld                    wwvare
     *
     * Key for kunderegister
     *
     C     rkunl1_key    klist
     C                   kfld                    wwfirm
     C                   kfld                    wwkund
     *
     * Key for leverandørregister
     *
     C     rlevl1_key    klist
     C                   kfld                    wwfirm
     C                   kfld                    wwlevr
     *
     * Key for kundeprosjekt
     *
     C     fkprl1_key    klist
     C                   kfld                    wwfirm
     C                   kfld                    wwkund
     C                   kfld                    wwkpro
     *
     * Key for alt. hovedgruppe
     *
     C     hgrkey        klist
     C                   kfld                    wwfirm
     C                   kfld                    wwhhgr
     *
     * Key for alt. undergruppe
     *
     C     ugrkey        klist
     C                   kfld                    wwfirm
     C                   kfld                    wwuhgr
     C                   kfld                    wwuugr
     *
     * Key for overgruppe
     *
     C     vogrl1_key    klist
     C                   kfld                    wwfirm
     C                   kfld                    wwoogr
     *
     * Key for hovedgruppe
     *
     C     vhgrl1_key    klist
     C                   kfld                    wwfirm
     C                   kfld                    wwhogr
     C                   kfld                    wwhhgr
     *
     * Key for undergruppe
     *
     C     vugrl1_key    klist
     C                   kfld                    wwfirm
     C                   kfld                    wwuogr
     C                   kfld                    wwuhgr
     C                   kfld                    wwuugr
     *
     * Key for ORDRETYPE
     *
     C     votyl1_key    klist
     C                   kfld                    wwfirm
     C                   kfld                    wwotyp
     *
     * Key for RABATTKATEGORI
     *
     C     ra06pf_key    klist
     C                   kfld                    wwfirm
     C                   kfld                    wwfkod
     *
     * Key for AVDELING
     *
     C     ra07pf_key    klist
     C                   kfld                    wwfirm
     C                   kfld                    wwgkod
     *
     * Key for DISTRIKT
     *
     C     ra08pf_key    klist
     C                   kfld                    wwfirm
     C                   kfld                    wwhkod
     *
     * Key for SELGER
     *
     C     ra09l1_key    klist
     C                   kfld                    wwfirm
     C                   kfld                    wwikod
     *
     * Key for LAGER
     *
     C     ra10pf_key    klist
     C                   kfld                    wwfirm
     C                   kfld                    wwjkod
     *
     * Key for KUNDEKATEGORI
     *
     C     ra11pf_key    klist
     C                   kfld                    wwfirm
     C                   kfld                    wwkkod
     *
     * Key for LAND
     *
     C     ra12pf_key    klist
     C                   kfld                    wwfirm
     C                   kfld                    wwlkod
     *
     * Key for leveringstype
     *
     C     vltpl1_key    klist
     C                   kfld                    wwfirm
     C                   kfld                    wwlety
     *
     C                   endsr
     /EJECT
     *******************  O U T P U T  S E C T I O N  *******************
     *
     *
     **********************  T A B E L L E R  ***************************
**       RAPPORT  TEKSTER
 Ledig                      01
 Ledig                      02
 Ledig                      03
 Ledig                      04
 Ledig                      05
 Ledig                      06
 Ledig                      07
 Ledig                      08
 Ledig                      09
 Ledig                      10
 Alt. hovedgruppe           11
 Alt. undergruppe           12
 Rabattgrp.                 13
 Overgruppe                 14
 Hovedgruppe                15
 Undergruppe                16
 Ledig                      17
 Ledig                      18
 Ledig                      19
 Ledig                      20
 Kundekategori              21
 Rabattkateg.               22
 Distrikt                   23
 Selger ordre               24
 Land                       25
 Hgr.-kundekat.             26
 Selger kasse               27
 Ledig                      28
 Ledig                      29
 Ledig                      30
 Varenummer                 31
 Kunde                      32
 Kundeprosjekt              33
 Ledig                      34
 Ledig                      35
 Ledig                      36
 Ledig                      37
 Ledig                      38
 Ledig                      39
 Ledig                      40
 Periode                    41
 Faktnr/Krednr              42
 Fakturadato                43
 Ledig                      44
 Ledig                      45
 Ledig                      46
 Ledig                      47
 Ledig                      48
 Ledig                      49
 Ledig                      50
 Ordretype                  51
 Avdeling                   52
 Lager                      53
 Produktansvarlig           54
 Leveringstype              55
 Leverandør                 56
 Time                       57
 Ledig                      58
 Ledig                      59
 Ledig                      60
