      /title  ASOFAK Lev.vare, henter vareregister - Jernia
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FW704R
      * Beskrivelse . . : Danning av leverandør vareregister, Jernia
      *
      *                   Programmet takler kun nytt Jernia-format
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       060699 HKO  Nytt program
      *       290200 HKO  Fjernet avrunding ved uttrekk av mva
      *       190401 HKO  Hopper over rader uten endringskode
      *                   Unngår forsendelsesrad fra Jernia
      *       301101 HKO  Tilpasset til AS2-formatet.
      *       030502 HKO  Endret lengde på d_nobb til 8
      *       050602 HKO  Lagt inn oversetting av varetekst fra ansii
      *       211003 HKO  Tilpasset nytt Jernia-format
      * 6.10  230609 BSA  Oppdatert med sporingsinformasjon
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fflvwpf    if   e           k disk
     ffenkl1    if   e           k disk    rename(fenkpfr:fenkl1r)
      *
     fflvapf    o  a e             disk
      *
6.10  * Local data area
6.10  *
6.10 d                uds
6.10 d l_user                911    920
      *
      * Definering av datastruktur
      *
      *  Recordbeskrivelse - Jernia
      *
     d                 ds
     d d_inpu                       259
     d  d_ekod                        1    overlay(d_inpu)                      Statuskode
     d  d_vare                       15    overlay(d_inpu:2)                    Varenr
     d  d_tek1                       30    overlay(d_inpu:17)                   Varetekst
     d  d_enhe                        3    overlay(d_inpu:47)                   Enhet
     d  d_lvar                       20    overlay(d_inpu:50)                   Lev. varenummer
     d  d_ldor                        8    overlay(d_inpu:70)                   Leverandør
     d  d_vgr1                        2    overlay(d_inpu:78)                   Jernia produkttype
     d  d_vgr2                        4    overlay(d_inpu:80)                   NOBB hovegruppe
     d  d_vgr3                        7    overlay(d_inpu:84)                   NOBB varegruppe
     d**d_eann                       15  0 overlay(d_inpu:91)                   EAN-nr
     d  d_eann                       13  0 overlay(d_inpu:93)                   EAN-nr
     d  d_ean2                       15  0 overlay(d_inpu:106)                  EAN-nr 2
     d  d_ean3                       15  0 overlay(d_inpu:121)                  EAN-nr 3
     d  d_ean4                       15  0 overlay(d_inpu:136)                  EAN-nr 4
     d  d_nobb                        8    overlay(d_inpu:151)                  Nobbnr
     d  d_pend                        1    overlay(d_inpu:159)                  Prisendring-kode
     d  d_inpr                       10  2 overlay(d_inpu:160)                  Innkjøpspris
     d  d_saim                       10  2 overlay(d_inpu:170)                  Salgspris inkl.mva
     d  d_antp                       10  0 overlay(d_inpu:180)                  Ant. i detalj.pakn.
     d  d_akod                        1    overlay(d_inpu:190)                  Anbrekkskode
     d  d_sort                        4    overlay(d_inpu:191)                  Grunnsortiment
     d  d_evar                       15    overlay(d_inpu:195)                  Erstatnings varenr
     d  d_eean                       15  0 overlay(d_inpu:210)                  Erstatnings EAN-nr
     d  d_enob                        8    overlay(d_inpu:225)                  Erstatnings Nobbnr
     d  d_raba                        5  2 overlay(d_inpu:233)                  Spesialrabatt
     d  d_fdat                        6  0 overlay(d_inpu:238)                  Spesialrab. fra-dato
     d  d_tdat                        6  0 overlay(d_inpu:244)                  Spesialrab. til-dato
     d  d_crdo                       10    overlay(d_inpu:250)                  Fritt/Cross docking
      *
     d**d_dato                        6  0 overlay(d_inpu:66)                   Endringsdato
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_ldor          s              6  0
      *
      * Definering av variable i nøkler
      *
     d fenkl1_enhf     s                   like(feenhf)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_udat          s               d   datfmt(*iso)
     d w_mvap          s              6  2
     d w_mvat          s              5  4
     d w_mvaf          s             10  9
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser input-fil, og legger data ut på leverandør vareregister
      *
     c                   read      flvwpf                                 90
     c                   dow       *in90 = *off
      *
     c                   eval      d_inpu = fwirad
      *
     c                   if        d_ekod = *blank
     c                   goto      neste_vare
     c                   endif
      *
      * Oversetter varetekst fra ansii
      *
     c     X'46':'Æ'     xlate     d_tek1        d_tek1
     c     X'A0':'æ'     xlate     d_tek1        d_tek1
     c     X'77':'Ø'     xlate     d_tek1        d_tek1
     c     X'90':'ø'     xlate     d_tek1        d_tek1
     c     X'2A':'Å'     xlate     d_tek1        d_tek1
     c     X'EF':'å'     xlate     d_tek1        d_tek1
      *
      * Skriver til leverandør vareregister
      *
     c                   eval      fwfirm = p_firm
     c                   eval      fwldor = p_ldor
     c                   eval      fwekod = d_ekod
     c                   eval      fwtek1 = d_tek1
     c                   eval      fwtek2 = *blank
     c                   eval      fwlvar = d_vare
     c                   eval      fweann = d_eann
     c                   eval      fwnobb = 0
     c                   eval      fwenhe = d_enhe
     c                   eval      fwinpr = d_inpr
     c                   eval      fwdato = *loval
     c                   eval      fwvgrp = %trim(d_vgr3)
     c                   eval      fwbmen = d_antp
     c                   eval      fwcrdo = 0
     c                   eval      fwtvar = %trim('0' + %subst(d_vare:1:7))
     c                   eval      fwhkod = 1
      *
     c                   eval      fwline = fwline + 1
     c                   eval      fwsapr = d_saim * w_mvaf
      *
     c                   if        d_nobb <> *blank
     c                   eval      d_nobb = %trim(d_nobb)
     c                   movel     d_nobb        fwnobb
     c                   endif
      *
     c                   eval      fenkl1_enhf = d_enhe
     c     fenkl1_key    chain     fenkl1                             91
     c                   if        *in91 = *off
     c                   eval      fwenhe = feenht
     c                   endif
      *
     c***                if        d_dato <> 0
     c***  *ymd          move      d_dato        fwdato
     c***                endif
      *
     c                   if        d_crdo = 'CD'
     c                   eval      fwcrdo = 1
     c                   endif
      *
6.10 c                   time                    fwodat
6.10 c                   time                    fwotim
6.10 c                   eval      fwousr = l_user
6.10 c                   time                    fwedat
6.10 c                   time                    fwetim
6.10 c                   eval      fweusr = l_user
     c                   write     flvapfr
      *
     c     neste_vare    tag
      *
     c                   read      flvwpf                                 90
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ldor
      *
      * Key for oppslag på enhet-konvertering
      *
     c     fenkl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fenkl1_enhf
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
      * Henter dagens dato
      *
     c                   time                    w_udat
      *
      * Henter mva-faktor
      *
     c                   call      'FÅ705R'
     c                   parm                    w_firm
     c                   parm                    w_udat
     c                   parm                    w_mvap
     c                   parm                    w_mvat
     c                   parm                    w_mvaf
      *
     c                   endsr
