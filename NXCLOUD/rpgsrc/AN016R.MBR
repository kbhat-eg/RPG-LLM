     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : AN016R
      * Beskrivelse . . : Generering av nattrutine, systemer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fasysl1    if   e           k disk    rename(asyspfr:asysl1r)
     fqcllesrc  o  a f  096        disk
      *
      * Definering av tabeller
      *
     d rec             s             80    dim(20) ctdata perrcd(1)             Programlinjer
     d bxx             s              1    dim(10)                              Bibliotek
     d pxx             s              1    dim(8)                               Prosedyre
     d fra             s              1    dim(30)                              Hjelpe-array
     d til             s              1    dim(30)                              Hjelpe-array
      *
      * Definering av data-strukturer
      *
     d                 ds
     d d_dato                         6  0
     d  d_aaar                        2  0 overlay(d_dato)
     d  d_mmnd                        2  0 overlay(d_dato:3)
     d  d_ddag                        2  0 overlay(d_dato:5)
      *
     d                 ds
     d d_recc                        84
     d  d_flt1                       27    overlay(d_recc)
     d  d_flt2                       57    overlay(d_recc:28)
      *
     d                 ds
     d d_valg                       128
     d  d_time                        6  0 overlay(d_valg)
     d  d_dens                       11    overlay(d_valg:7)
     d  d_rorg                        1    overlay(d_valg:18)
     d  d_back                        1    overlay(d_valg:19)
     d  d_ssbs                        1    overlay(d_valg:20)
     d  d_prc1                        1    overlay(d_valg:21)
     d  d_prc2                        1    overlay(d_valg:22)
     d  d_genp                        1    overlay(d_valg:23)
     d  d_savf                        1    overlay(d_valg:24)
     d  d_smap                        1    overlay(d_valg:25)
     d  d_natt                        1    overlay(d_valg:26)
     d  d_strt                        1    overlay(d_valg:27)
     d  d_slut                        1    overlay(d_valg:28)
      *
     d  d_pro1                        8    overlay(d_valg:31)
     d  d_bib1                       10    overlay(d_valg:39)
     d  d_sys1                        2    overlay(d_valg:49)
     d  d_pro2                        8    overlay(d_valg:51)
     d  d_bib2                       10    overlay(d_valg:59)
     d  d_sys2                        2    overlay(d_valg:69)
      *
      * Definering av parametere
      *
     d p_valg          s            128
      *                                                                     -
      * Definering av variable i nøkler
      *                                                                     -
     d asysl1_syst     s                   like(assyst)
      *                                                                     -
      * Definering av variable
      *                                                                     -
     d n               s              2  0
     d y               s              2  0
     d z               s              2  0
     d w_dato          s              6  0
     d w_recc          s             84
     d w_sys1          s              2
     d w_tell          s              6  0
     d w_bib1          s                   like(assyst)
     d w_pro1          s                   like(asprcs)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Legg ut records til Cl-program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Legger ut første del av CL-programmet
      *
     c                   for       n = 1 to 7
     c                   add       100           w_tell
     c                   movel     rec(n)        w_recc
     c                   except    xrecut
     c                   endfor
      *
      * Danner linje for CALL / STRS36PRC
      *
     c                   eval      asysl1_syst = *blank
     c     asysl1_key    setll     asysl1
      *
     c     xover1        tag
     c                   read      asysl1                                 62
     c                   if        *in62 = *off
      *
     c                   if        asprca <> *blank
     c                   add       100           w_tell
     c                   eval      w_pro1 = asprca
     c                   eval      w_bib1 = assyst
     c                   eval      w_sys1 = 'AS'
     c                   exsr      xsubr
     c                   except    xrecut
     c                   for       n = 10 to 18
     c                   add       100           w_tell
     c                   movel     rec(n)        w_recc
     c                   except    xrecut
     c                   endfor
     c                   endif
      *
     c                   if        asprcs <> *blank
     c                   add       100           w_tell
     c                   eval      w_pro1 = asprcs
     c                   eval      w_bib1 = assyst
     c                   eval      w_sys1 = '36'
     c                   exsr      xsubr
     c                   except    xrecut
     c                   for       n = 10 to 18
     c                   add       100           w_tell
     c                   movel     rec(n)        w_recc
     c                   except    xrecut
     c                   endfor
     c                   endif
      *
     c                   goto      xover1
     c                   endif
      *
      * Legger ut siste del av CL-programmet
      *
     c                   for       n = 19 to 20
     c                   add       100           w_tell
     c                   movel     rec(n)        w_recc
     c                   except    xrecut
     c                   endfor
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      *inlr = *on
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for stokke på plass navn på bibliotek med ( ).
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xsubr         begsr
      *
     c                   if        w_sys1 = 'AS'
     c                   exsr      xas400
     c                   endif
      *
     c                   if        w_sys1 = '36'
     c                   exsr      xs36
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for AS/400 for å få på plass biblitek/prosedyrer-navn
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xas400        begsr
      *
     c                   eval      z = *zero
     c                   eval      fra = *blank
     c                   eval      til = *blank
     c                   movel     rec(08)       w_recc
     c                   eval      d_recc = w_recc
      *
      * Legger inn biblitek som program skal startes fra
      *
     c                   movea     w_bib1        bxx
      *
     c                   for       y = 1 to 10
     c                   if        bxx(y) <> *blank
     c                   add       1             z
     c                   eval      fra(z) = bxx(y)
     c                   endif
     c                   endfor
      *
     c                   add       1             z
     c                   eval      fra(z) = '/'
      *
      * Legger inn program som skal startes
      *
     c                   movea     w_pro1        pxx
      *
     c                   for       y = 1 to 8
     c                   if        pxx(y) <> *blank
     c                   add       1             z
     c                   eval      fra(z) = pxx(y)
     c                   endif
     c                   endfor
      *
     c                   add       1             z
     c                   eval      fra(z) = ')'
      *
     c                   movea     fra           d_flt2
     c                   eval      w_recc = d_recc
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for S/36   for å få på plass biblitek/prosedyrer-navn
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xs36          begsr
      *
     c                   eval      z = *zero
     c                   eval      fra = *blank
     c                   eval      til = *blank
     c                   movel     rec(09)       w_recc
     c                   eval      d_recc = w_recc
      *
      * Legger inn program som skal startes
      *
     c                   movea     w_pro1        pxx
      *
     c                   for       y = 1 to 8
     c                   if        pxx(y) <> *blank
     c                   add       1             z
     c                   eval      fra(z) = pxx(y)
     c                   endif
     c                   endfor
      *
     c                   add       1             z
     c                   eval      fra(z) = ')'
     c                   add       1             z
     c                   eval      fra(z) = ' '
     c                   add       1             z
     c                   eval      fra(z) = 'C'
     c                   add       1             z
     c                   eval      fra(z) = 'U'
     c                   add       1             z
     c                   eval      fra(z) = 'R'
     c                   add       1             z
     c                   eval      fra(z) = 'L'
     c                   add       1             z
     c                   eval      fra(z) = 'I'
     c                   add       1             z
     c                   eval      fra(z) = 'B'
     c                   add       1             z
     c                   eval      fra(z) = '('
      *
      * Legger inn biblitek som program skal startes fra
      *
     c                   movea     w_bib1        bxx
      *
     c                   for       y = 1 to 10
     c                   if        bxx(y) <> *blank
     c                   add       1             z
     c                   eval      fra(z) = bxx(y)
     c                   endif
     c                   endfor
      *
     c                   add       1             z
     c                   eval      fra(z) = ')'
      *
     c                   movea     fra           d_flt2
     c                   eval      w_recc = d_recc
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *
     c                   eval      w_recc = *blank
     c                   eval      w_tell = *zero
     c                   eval      w_dato = *zero
     c                   eval      n = *zero
     c                   eval      y = *zero
     c                   eval      z = *zero
     c                   eval      w_sys1 = *blank
      *
     c                   eval      d_aaar = uyear
     c                   eval      d_mmnd = umonth
     c                   eval      d_ddag = uday
     c                   eval      w_dato = d_dato
      *
      * Key for bibliotekslister
      *
     c     asysl1_key    klist
     c                   kfld                    asysl1_syst
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_valg
      *
     c                   eval      d_valg = p_valg
      *
     c                   endsr
      *
     oqcllesrc  eadd         xrecut
     o                       w_recc              96
     o                       w_tell               6
     o                       w_dato              12
**
             PGM                                                  /*1*/
/*                                                                  2*/
/* Definisjon av variabler                                          3*/
/*                                                                  4*/
             DCL        VAR(&KODE) TYPE(*DEC)  LEN(2 0)           /*5*/
             DCL        VAR(&BIBL) TYPE(*CHAR) LEN(10)            /*6*/
/*                                                                  7*/
             CALL      PGM(1234567890/12345678)                   /*8*/
             STRS36PRC PRC(12345678) CURLIB(1234567890)           /*9*/
/*                                                                 10*/
             MONMSG     MSGID(CPF0001 SYS5514 SYS5552) EXEC(DO)  /*11*/
/*                                                                 12*/
/* Gi melding dersom jobb ikke startes                             13*/
/*                                                                 14*/
             CHGVAR     VAR(&KODE)  VALUE(25)                    /*15*/
             CALL       PGM(AN050R) PARM(&KODE &BIBL)            /*16*/
             ENDDO                                               /*17*/
 /*                                                                18*/
             ENDPGM                                              /*19*/
 /*                                                                20*/
