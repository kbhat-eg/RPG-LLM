     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FO550R
      * Beskrivelse . . : Multispørreprogram ordre
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       140605 ASK  Nytt program
6.10  *       260310 bof  utvidet med å spørre på flere år i hist.faktura
6.11  *  6.11 211210 AMD  Endret fra bestillingsdato til slettet dato
6.11  *                   da år hentes ut for oppslag mot LDELPF
6.20  *       180511 bsa  Hvis ikke ordrenr finnes, spørres det som om det
6.20  *                   er lagerordre. Ref oppgave JIRA31.
6.21  *  6.20 130112 bsa  Kode for hvilket type nummer som overføres. '1' er
6.21  *                   ordrenummer, '2' er bestillingsnummer.
6.22  *  6.20 170112 bsa  Tar en sving innom EDI for å finne medling
6.23  *  6.20 170112 bsa  F11 gir overgang til EDI mottaksrutine
6.24  *  6.20 130312 bsa  Type som sier hva slags type nummer som
6.24  *                   kommer fra kallende program.
6.25  *  6.20 070512 bsa  Div. jacada justeringer i dspf
6.26  *  6.20 230712 ask  Nullstiller interne skjermvariable pga. feil
6.27  *  6.20 150414 bsa  Må skille mellom bestilling og ordrenummer
6.27  *                   når en leser.
6.28  *  6.20 050914 bkv  Test på om w_numm er 0
6.29  *  6.20 291014 bsa  Hvis bestilling finnes via Finn_innfakt, skal
6.29  *                   den ikke finnes på nytt i Lagr_innfakt.
6.2a  *  6.20 111114 bsa  Hvis bestilling finnes via Finn_bestill, skal
6.2a  *                   den ikke finnes på nytt i Lagr_bestill.
6.2b  *  6.20 261114 bsa  Finn evnt tilhørende bestillingsnummer basert
6.2b  *                   på ordrenummer
6.2c  *  6.20 260215 nho  Hvis bestilling finnes via Finn_beslett, skal
6.2c  *                   den ikke finnes på nytt i Lagr_beslett.
6.2c  *  6.20 280415 bsa  Utvider med ordresuffix
6.2d  *  6.20 300715 bsa  Må sjekke også tidligere år ved leting etter
6.2d  *                   ordre/bestillinger.
6.nu  * R6.30 270514 bsa  Programmet gjennomgått mtp nummerutvidelser.
6.2e  *  6.10 170915 nho  * ut test på suffix ref 6.2c
6.2e  *  6.10 170915 nho  * ut w_curn og w_virn ref 6.23
6.2f  *  6.20 091015 bsa  Må sjekke på suffix. Kun hvis via valgboks.
6.2g  *  6.20 211015 bsa  Legges over i feil variabel.
6.2h  *  6.30 080317 bkv  Fjerner 6.2e  dvs: * ut w_curn og w_virn ref 6.23
6.30  *  6.30 280617 bsa  Bongoppslag hånteres ikke, hvis ikke ordrenummer.
6.31  *  6.30 110219 bsa  Sliter med å finne riktig år ved overgang til nye år.
      *
7.01  *  K00  100221 ask  lagt inn korreksjon av sjermfeil ved overgang til EDI (6.26,6.2e,6.2h)
      *
8.01  *       220621 bsa  Utvidet meldingsnummer fra 6-8 siffer
8.02  *K0000  160517 bsa  Utvider med oppslag mot arkiv.
8.03  *K0000  250423 ask  Utvidet EDI nummerserie i kall til LI106R (EDI oppslag)
8.04  *K0000  120924 bsa  Endret oppslag mot arkiv
8.05  *K0000  161224 bsa  Samme logikk for lesing av sletta bestillingslinjer
8.05  *K0000              som for salgsordre.
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = avslutt
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffdell8    if   e           k disk    rename(fdelpfr:fdell8r)
     fsohel2    if   e           k disk    rename(sohepfr:sohel2r)
     fsohel3    if   e           k disk    rename(sohepfr:sohel3r)
     fbohel1    if   e           k disk    rename(bohepfr:bohel1r)
     fbohel3    if   e           k disk    rename(bohepfr:bohel3r)
6.30 fbodtlq    if   e           k disk    rename(bodtpfr:bodtlqr)
     fbodtl4    if   e           k disk    rename(bodtpfr:bodtl4r)
     fbodtl3    if   e           k disk    rename(bodtpfr:bodtl3r)
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flohel2    if   e           k disk    rename(lohepfr:lohel2r)
     flohel3    if   e           k disk    rename(lohepfr:lohel3r)
     flohelc    if   e           k disk    rename(lohepfr:lohelcr)
     fldell2    if   e           k disk    rename(ldelpfr:ldell2r)
     fldell3    if   e           k disk    rename(ldelpfr:ldell3r)
     fsihelk    if   e           k disk    rename(sihepfr:sihelkr)
     fsihel7    if   e           k disk    rename(sihepfr:sihel7r)
     fsihel8    if   e           k disk    rename(sihepfr:sihel8r)
     fsihel9    if   e           k disk    rename(sihepfr:sihel9r)
6.22 fledil6    if   e           k disk    rename(ledipfr:ledil6r)
8.02 fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     ffo550d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      *  Definering av datastrukturer
      *
      * Transaksjons-array
     d                 ds
6.nu d tr_info                       79    dim(500)
6.nu d  tr_numm                       8  0 overlay(tr_info:1)
6.nu d  tr_suff                       2  0 overlay(tr_info:9)
6.nu d  tr_otyp                       2    overlay(tr_info:11)
6.nu d  tr_refn                      12    overlay(tr_info:13)
6.nu d  tr_stat                      10    overlay(tr_info:25)
6.nu d  tr_user                      10    overlay(tr_info:35)
6.nu d  tr_dato                        d   datfmt(*iso)
6.nu d                                     overlay(tr_info:45)
6.nu d  tr_time                        t   timfmt(*iso)
6.nu d                                     overlay(tr_info:55)
6.nu d  tr_sort                      18    overlay(tr_info:45)
6.nu d  tr_btyp                       1    overlay(tr_info:63)
6.nu d  tr_kund                       6  0 overlay(tr_info:64)
6.nu d  tr_kass                      10    overlay(tr_info:70)
      *
6.22 d                 ds
8.03 d d_prec                        22
6.22 d  d_firm                        3  0 overlay(d_prec)
6.22 d  d_type                        4    overlay(d_prec:4)
8.03 d  d_mnum                        8  0 overlay(d_prec:8)
8.03 d  d_mlin                        6  0 overlay(d_prec:16)
8.03 d  d_kode                        1    overlay(d_prec:22)
      *
      * Definering av parametre
      *
6.nu d p_numm          s                   like(fonumm)
6.24 d p_ntyp          s              1
      *
      * Definering av Local data area
      *
6.24 dlda             uds
6.24 d l_vise                700    700
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d fohel1_numm     s                   like(fonumm)
     d fdell8_numm     s                   like(fynumm)
     d sohel2_binr     s                   like(sobinr)
     d sohel3_aarr     s                   like(soaarr)
     d sohel3_numm     s                   like(sonumm)
     d bohel1_aarr     s                   like(boaarr)
     d bohel1_wsid     s                   like(bowsid)
     d bohel1_bong     s                   like(bobong)
     d bohel3_bong     s                   like(bobong)
     d bodtl4_bong     s                   like(bobong)
     d bodtl3_aarr     s                   like(bdaarr)
     d bodtl3_ornr     s                   like(bdornr)
     d lohelc_numm     s                   like(loornr)
     d lohel1_numm     s                   like(loornr)
     d lohel2_sorn     s                   like(losorn)
     d lohel3_sfak     s                   like(losfak)
     d ldell2_numm     s                   like(lynumm)
     d ldell3_ornr     s                   like(lyornr)
     d sihel7_aarr     s                   like(shaarr)
     d sihel7_numm     s                   like(shornr)
     d sihel8_lonr     s                   like(shlonr)
     d sihel9_lfnr     s                   like(shlfnr)
     d sihelk_numm     s                   like(shnumm)
6.22 d ledil6_mnum     s                   like(limnum)
6.22 d ledil6_numm     s                   like(linumm)
6.22 d ledil6_suff     s                   like(lisuff)
6.30 d bodtlq_bong     s                   like(bobong)
6.30 d bodtlq_aarr     s                   like(bdaarr)
8.02 d votyl1_otyp     s                   like(vaotyp)
      *
      * Definering av variable
      *
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
     d ind             s              3  0
      *
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
6.29 d b_best          s              1    inz(*off)
6.2a d b_bes2          s              1    inz(*off)
6.2c d b_bes3          s              1    inz(*off)
6.2f d b_mult          s              1    inz(*off)
      *
     d w_firm          s                   like(fofirm)
6.nu d w_numm          s                   like(fonumm)
     d w_snum          s                   like(fonumm)
6.nu d w_best          s                   like(lonumm)
6.22 d w_mnum          s                   like(lomnum)
     d w_date          s               d   datfmt(*iso)
     d w_ddat          s               d   datfmt(*iso)
     d w_aarr          s              4  0
6.2b d w_baar          s              4  0
     d w_daar          s              4  0
     d w_faar          s              4  0
6.nu d w_binr          s              8  0
     d w_bong          s             12
     d w_sbon          s             12
     d w_tell          s              3  0
6.2b d w_anta          s              3  0
6.22 d w_prec          s             45
6.23 d w_type          s              4
6.2c d w_osuf          s                   like(fosuff)
8.02 d w_modu          s             10    inz('PROA    ')
8.02 d w_disp          s              1    inz('1')
8.02 d w_tilg          s              1
8.02 d w_dtyp          s             50
8.02 d w_refn          s             50
      *
6.nu d s_onum          s              8  0
6.2c d s_osuf          s                   like(fosuff)
6.nu d s_fakt          s              8  0
     d s_bong          s             12
6.nu d s_best          s              8  0
     d s_lonr          s             15
     d s_lfak          s             15
7.01 d s_curn          s                   like(w_curn)
7.01 d s_virn          s                   like(w_virn)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(13)
      *
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_srrn - Relativt recordnummer i subfile
      * w_spge - Sidestørrelse i subfile (page)
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * w_virn - Vis den side som inneholder denne record.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Setter startverdier
      *
     c                   eval      s_onum = 0
6.2c c                   eval      s_osuf = 0
     c                   eval      s_fakt = 0
     c                   eval      s_bong = *blank
     c                   eval      s_best = 0
     c                   eval      s_lonr = *blank
     c                   eval      s_lfak = *blank
      *
     c                   exsr      clr_subfile
6.24 c                   if        p_ntyp = '1'
     c                   eval      b2onum = p_numm
6.24 c                   else
6.24 c                   eval      b2onum = 0
6.24 c                   endif
     c                   eval      b2fakt = 0
     c                   eval      b2bong = *blank
6.24 c                   if        p_ntyp = '2'
6.24 c                   eval      b2best = p_numm
6.24 c                   else
     c                   eval      b2best = 0
6.24 c                   endif
     c                   eval      b2lonr = *blank
     c                   eval      b2lfak = *blank
     c                   write     b2cmd
     c                   exsr      nytt_sok
      *
      * Send startbilde
      *
     c     b2taga        tag
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_pgm
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2taga
6.20 c                   when      *inkj
6.20 c                   exsr      jmp_fakthis
6.20 c                   goto      b2taga
6.23 c                   when      *inkk
6.23 c                   exsr      jmp_EDI
6.23 c                   goto      b2taga
     c                   when      *in21
     c                   eval      *in22  = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Nytt søkebegrep?
      *
     c                   if        b2onum <> s_onum or
     c                             b2fakt <> s_fakt or
     c                             b2bong <> s_bong or
     c                             b2best <> s_best or
     c                             b2lonr <> s_lonr or
     c                             b2lfak <> s_lfak
     c                   exsr      nytt_sok
     c                   goto      b2taga
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   eval      *in22 = *off
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
8.02 c                   eval      *in31 = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c*                  eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile    ,
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Vise detaljinformasjon
      *
     c                   if        b1valg = 5
     c     *dmy          move      b1dato        w_date
     c                   eval      w_aarr = %subdt(w_date:*years)
      *
      * Vis ordre
      *
     c                   if        b1btyp = 'O'
     c                   call      'FO505R'
     c                   parm                    w_firm
6.nu c                   parm                    b1numm
     c                   parm                    b1suff
     c                   endif
      *
      * Vis slettet ordre
      *
     c                   if        b1btyp = 'S'
     c                   call      'FY510R'
     c                   parm                    w_firm
     c                   parm                    w_aarr
6.nu c                   parm                    b1numm
     c                   parm                    b1suff
     c                   endif
      *
      * Vis utg. faktura
      *
     c                   if        b1btyp = 'F'
     c                   movel     b1refn        w_binr
     c                   call      'SO510R'
     c                   parm                    w_firm
     c                   parm                    w_aarr
     c                   parm                    b1kund
6.nu c                   parm                    w_binr
     c                   endif
      *
      * Vis bong
      *
     c                   if        b1btyp = 'C'
     c                   movel     b1refn        w_bong
     c                   call      'BO310R'
     c                   parm                    w_firm
     c                   parm                    w_aarr
     c                   parm                    b1kass
     c                   parm                    w_bong
     c                   endif
      *
      * Vis bestilling
      *
     c                   if        b1btyp = 'B'
     c                   call      'LO505R'
     c                   parm                    w_firm
6.nu c                   parm                    b1numm
     c                   parm                    b1suff
     c                   endif
      *
      * Vis slettet bestilling
      *
     c                   if        b1btyp = 'D'
     c                   call      'LY510R'
     c                   parm                    w_firm
     c                   parm                    w_aarr
6.nu c                   parm                    b1numm
     c                   parm                    b1suff
     c                   endif
      *
      * Vis inng. faktura
      *
     c                   if        b1btyp = 'I'
     c                   movel     b1refn        w_binr
6.31  *
6.31 c                   eval      sihelk_numm = b1numm
6.31 c     sihelk_key    setll     sihelk
6.31 c     sihelk_key    reade     sihelkr
6.31 c                   dow       not %eof
6.31  *
6.31 c                   if        shbinr =  w_binr
6.31 c                   eval      w_aarr = shaarr
6.31 c                   endif
6.31  *
6.31 c     sihelk_key    reade     sihelkr
6.31 c                   enddo
6.31  *
     c                   call      'SH410R'
     c                   parm                    w_firm
     c                   parm                    w_aarr
     c                   parm                    b1kund
6.nu c                   parm                    w_binr
     c                   endif
6.22  *
6.22  * Vis EDI ordre
6.22  *
6.22 c                   if        b1btyp = 'E'
6.22 c                   eval      d_firm = w_firm
6.22 c                   movel     b1refn        d_type
6.22 c                   eval      d_mnum = b1numm
6.22 c                   eval      d_mlin = 100001
6.22 c                   eval      d_kode = *blank
6.22 c                   eval      w_prec = d_prec
6.22  *
6.22 c                   if        d_type = 'OBKR'
6.22 c                   call      'LI102R'
6.22 c                   parm                    w_prec
6.22 c                   endif
6.22 c                   if        d_type = 'FAKT'
6.22 c                   call      'LI106R'
6.22 c                   parm                    w_prec
6.22 c                   endif
6.22 c                   endif
6.22  *
6.22 c                   endif
8.02  *
8.02  * Slå opp i arkiv.
8.02  *
8.02 c                   if        b1valg = 9
8.02 c     *dmy          move      b1dato        w_date
8.02 c                   eval      w_aarr = %subdt(w_date:*years)
8.02  *
8.02  * Kontrollerer gyldig tilgang
8.02  *
8.02 c                   call      'AX700R'
8.02 c                   parm                    w_modu
8.02 c                   parm                    w_disp
8.02 c                   parm                    w_tilg
8.02  *
8.02 c                   if        w_tilg = '0'
8.02  *
8.02  * Hent hvilken kategori ordretype vi sjekker mot.
8.02  *
8.02 c                   eval      w_dtyp = *blanks
8.02 c                   eval      votyl1_otyp = b1otyp
8.02 c     votyl1_key    chain     votyl1
8.02 c                   if        not %found
8.02 c                   eval      vaoakk = 9
8.02 c                   endif
8.02  *
8.02  * Vis tilbud
8.02  *
8.02 c                   if        vaosys = 0 and
8.02 c                             vaoakk = 0
8.02 c                   eval      w_dtyp = 'TILBUD'
8.02 c                   eval      w_refn = *blank
8.04 c                   eval      w_refn = %char(w_aarr) +
8.04 c                             %trim(%char(b1numm)+'-'+%char(b1suff))
8.02  *
8.02 c                   call      'AP622C'
8.02 c                   parm                    w_dtyp
8.02 c                   parm                    w_refn
8.02 c                   endif
8.02  *
8.02  * Vis ordrebekreftelse
8.02  *
8.02 c                   if        vaosys = 0  and
8.02 c                             vaoakk = 1
8.02 c                   eval      w_dtyp = 'ORDREBEKREFTELSE'
8.02 c                   eval      w_refn = *blank
8.04 c                   eval      w_refn = %char(w_aarr) +
8.04 c                             %trim(%char(b1numm)+'-'+%char(b1suff))
8.02  *
8.02 c                   call      'AP622C'
8.02 c                   parm                    w_dtyp
8.02 c                   parm                    w_refn
8.02 c                   endif
8.02  *
8.02  * Vis pakkkseddel
8.02  *
8.02 c                   if        vaosys = 0  and
8.02 c                             vaoakk = 2
8.02 c                   eval      w_dtyp = 'PAKKSEDDEL'
8.02 c                   eval      w_refn = *blank
8.04 c                   eval      w_refn = %char(w_aarr) +
8.04 c                             %trim(%char(b1numm)+'-'+%char(b1suff))
8.02  *
8.02 c                   call      'AP622C'
8.02 c                   parm                    w_dtyp
8.02 c                   parm                    w_refn
8.02 c                   endif
8.02  *
8.02  * Vis utg. faktura
8.02  *
8.02 c                   if        vaosys = 0 and
8.02 c                             vaoakk = 3
8.02 c                   movel     b1refn        w_binr
8.02 c                   eval      w_dtyp = 'FAKTURA'
8.02 c                   eval      w_refn = *blank
8.02 c                   eval      w_refn = %char(w_aarr) + %trim(%char(w_binr))
8.02  *
8.02 c                   call      'AP622C'
8.02 c                   parm                    w_dtyp
8.02 c                   parm                    w_refn
8.02  *
8.02 c                   endif
8.02  *
8.02  * Vis bestilling
8.02  *
8.02 c                   if        vaosys = 1 and
8.02 c                             vaoakk = 1
8.02 c                   eval      w_dtyp = 'BESTILLING'
8.02 c                   eval      w_refn = *blank
8.04 c                   eval      w_refn = %char(w_aarr) +
8.04 c                             %trim(%char(b1numm)+'-'+%char(b1suff))
8.02  *
8.02 c                   call      'AP622C'
8.02 c                   parm                    w_dtyp
8.02 c                   parm                    w_refn
8.02 c                   endif
8.02  *
8.02  * Vis innkommende pakkseddel
8.02  *
8.02 c                   if        vaosys = 1  and
8.02 c                             vaoakk = 2
8.02 c                   eval      w_dtyp = 'INNK_PAKKSEDDEL'
8.02 c                   eval      w_refn = *blank
8.04 c                   eval      w_refn = %char(w_aarr) +
8.04 c                             %trim(%char(b1numm)+'-'+%char(b1suff))
8.02  *
8.02 c                   call      'AP622C'
8.02 c                   parm                    w_dtyp
8.02 c                   parm                    w_refn
8.02 c                   endif
8.02  *
8.02  * Vis inng. faktura
8.02  *
8.02 c                   if        vaosys = 1 and
8.02 c                             vaoakk = 3
8.02 c                   movel     b1refn        w_binr
8.02 c                   eval      w_dtyp = 'incominginvoice'
8.02 c                   eval      w_refn = *blank
8.02  *
8.02 c**                 eval      w_refn = %trim(%char(w_binr))
8.02 c                   eval      w_refn = %char(w_aarr) + %trim(%char(w_binr))
8.02  *
8.02 c                   call      'AP622C'
8.02 c                   parm                    w_dtyp
8.02 c                   parm                    w_refn
8.02 c                   endif
8.02  *
8.02  * Vis EDI ordre - har ingen link
8.02  *
8.02 c**                 if        b1btyp = 'E'
8.02 c**
8.02 c**                 endif
      *
8.02 c                   if        vaoakk > 3
8.02 c                   eval      *in31 = *on
8.02 c                   endif
8.02  *
8.02 c                   endif
8.02 c                   endif
      *
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
8.02 c**                 endif
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   exsr      null_tab
     c                   exsr      finn_aktive
     c                   exsr      finn_ordslett
     c                   exsr      finn_faktura
     c                   exsr      finn_bong
     c                   exsr      finn_bestill
     c                   exsr      finn_beslett
     c                   exsr      finn_innfakt
6.20 c                   exsr      lagr_bestill
6.20 c                   exsr      lagr_beslett
6.20 c                   exsr      lagr_innfakt
6.22 c                   exsr      finn_EDI
      *
     c                   exsr      sort_tab
      *
     c                   eval      ind = 1
     c                   eval      w_spge = w_spge + c_sfil
      *
     c                   dou       ind > 500
      *
     c                   if        tr_sort(ind) <> *blank
     c                   eval      w_srrn = w_srrn + 1
     c                   eval      b1numm = tr_numm(ind)
     c                   eval      b1suff = tr_suff(ind)
     c                   eval      b1otyp = tr_otyp(ind)
     c                   eval      b1refn = tr_refn(ind)
     c                   eval      b1stat = tr_stat(ind)
     c                   eval      b1user = tr_user(ind)
     c                   if        tr_dato(ind) > *loval
     c     *dmy          move      tr_dato(ind)  b1dato
     c                   else
     c                   eval      b1dato = 0
     c                   endif
     c                   if        tr_time(ind) > *loval
     c     *hms          move      tr_time(ind)  b1time
     c                   else
     c                   eval      b1time = 0
     c                   endif
     c                   eval      b1btyp = tr_btyp(ind)
     c                   eval      b1kund = tr_kund(ind)
     c                   eval      b1kass = tr_kass(ind)
      *
     c                   write     b1sfl
     c                   endif
      *
     c                   eval      ind = ind + 1
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   eval      *in15 = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for nullstilling av tabell
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     null_tab      begsr
      *
      * Nullstiller tabell
      *
     c                   movea     *blank        tr_info
     c                   eval      ind = 1
      *
     c                   dou       ind > 500
     c                   eval      tr_numm(ind) = 0
     c                   eval      tr_suff(ind) = 0
     c                   eval      tr_otyp(ind) = *blank
     c                   eval      tr_refn(ind) = *blank
     c                   eval      tr_stat(ind) = *blank
     c                   eval      tr_user(ind) = *blank
     c                   eval      tr_dato(ind) = *loval
     c                   eval      tr_time(ind) = *loval
     c                   eval      tr_sort(ind) = *blank
     c                   eval      tr_btyp(ind) = *blank
     c                   eval      tr_kund(ind) = 0
     c                   eval      tr_kass(ind) = *blank
     c                   eval      ind = ind + 1
     c                   enddo
     c                   eval      ind = 1
      *
     c     end_null      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for uthenting av aktive ordres
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_aktive   begsr
      *
6.28 c                   if        w_numm = 0
6.28 c                   leavesr
6.28 c                   endif
      *
      *  Legger inn startverdi for lesingen
      *
      *
     c                   eval      fohel1_numm = w_numm
     c     fohel1_key    setll     fohel1
     c     fohel1_key    reade     fohel1r
      *
     c                   dow       not %eof
     c                   eval      ind = ind + 1
     c                   eval      tr_btyp(ind) = 'O'
     c                   eval      tr_dato(ind) = fodate
     c                   eval      tr_time(ind) = fotime
     c                   eval      tr_numm(ind) = fonumm
     c                   eval      tr_suff(ind) = fosuff
     c                   eval      tr_otyp(ind) = footyp
     c                   eval      tr_stat(ind) = 'I ordre'
     c                   eval      tr_user(ind) = fouser
      *
     c     fohel1_key    reade     fohel1r
     c                   enddo
      *
     c     end_aktive    endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for uthenting av slettede ordres
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_ordslett begsr
      *
6.28 c                   if        w_numm = 0
6.28 c                   leavesr
6.28 c                   endif
      *
      *  Legger inn startverdi for lesingen
      *
     c                   eval      fdell8_numm = w_numm
     c     fdell8_key    setll     fdell8
     c     fdell8_key    reade     fdell8r
      *
     c                   dow       not %eof
     c                   if        fytype <> 'SHOP' and
     c                             fytype <> 'FAKT'
     c                   eval      ind = ind + 1
     c                   eval      tr_btyp(ind) = 'S'
     c                   eval      tr_dato(ind) = fydate
     c                   eval      tr_time(ind) = fytime
     c                   eval      tr_numm(ind) = fynumm
     c                   eval      tr_suff(ind) = fysuff
     c                   eval      tr_otyp(ind) = fyotyp
     c                   eval      tr_stat(ind) = 'Slettet'
     c                   eval      tr_user(ind) = fyuser
     c                   endif
      *
      *
     c     fdell8_key    reade     fdell8r
     c                   enddo
      *
     c     end_ordslett  endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for uthenting av fakturerte ordres
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_faktura  begsr
      *
6.28 c                   if        w_numm = 0
6.28 c                   leavesr
6.28 c                   endif
      *
      *  Legger inn startverdi for lesingen
      *
6.2b c                   if        w_baar <> 0
6.2b c                   eval      sohel3_aarr = w_baar
6.2b c                   eval      sohel3_numm = w_numm
6.2c c**   sohel3_key    chain     sohel3r
6.2c c     sohel3_key    setll     sohel3
6.2c c     sohel3_key    reade     sohel3r
6.2b  *
6.2c c**                 if        %found
6.2c c                   dow       not %eof
6.2f c                   if        b_mult = *on  and
6.2f c                             sosuff <> w_osuf
6.2f c                   goto      nxt_sohe
6.2f c                   endif
6.2b c                   eval      ind = ind + 1
6.2b c                   eval      tr_btyp(ind) = 'F'
6.2b c                   eval      tr_dato(ind) = sofkda
6.2b c                   eval      tr_time(ind) = *loval
6.2b c                   eval      tr_numm(ind) = sonumm
6.2b c                   eval      tr_suff(ind) = sosuff
6.2b c                   eval      tr_otyp(ind) = sootyp
6.2b c                   eval      tr_stat(ind) = 'Fakturert'
6.2b c                   eval      tr_user(ind) = souser
6.2b c                   eval      tr_kund(ind) = sokund
6.2b c                   movel     sobinr        tr_refn(ind)
6.2f c     nxt_sohe      tag
6.2c  *
6.2c c     sohel3_key    reade     sohel3r
6.2c c                   enddo
      *
6.2b c                   else
6.10 c                   eval      w_tell = 0
6.10 c                   dow       w_tell < 6
6.10 c                   eval      sohel3_aarr = w_daar - w_tell
      *
     c     finn_fak      tag
     c                   eval      sohel3_numm = w_numm
     c     sohel3_key    setll     sohel3
     c     sohel3_key    reade     sohel3r
      *
     c                   dow       not %eof
6.2f c                   if        b_mult = *on  and
6.2f c                             sosuff <> w_osuf
6.2f c                   goto      nxt_sohel3
6.2f c                   endif
     c                   eval      ind = ind + 1
     c                   eval      tr_btyp(ind) = 'F'
     c                   eval      tr_dato(ind) = sofkda
     c                   eval      tr_time(ind) = *loval
     c                   eval      tr_numm(ind) = sonumm
     c                   eval      tr_suff(ind) = sosuff
     c                   eval      tr_otyp(ind) = sootyp
     c                   eval      tr_stat(ind) = 'Fakturert'
     c                   eval      tr_user(ind) = souser
     c                   eval      tr_kund(ind) = sokund
     c                   movel     sobinr        tr_refn(ind)
6.2f c     nxt_sohel3    tag
      *
     c     sohel3_key    reade     sohel3r
     c                   enddo
      *
6.10 c                   eval      w_tell = w_tell + 1
6.10 c                   enddo
      *
6.2b c                   endif
      *
6.10 c*                  if        sohel3_aarr = w_daar
6.10 c*                  eval      sohel3_aarr = w_faar
6.10 c*                  goto      finn_fak
6.10 c*                  endif
      *
     c     end_faktura   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for uthenting av ordres kjørt via butikk
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_bong     begsr
      *
      *  Legger inn startverdi for lesingen
      *
     c                   eval      w_sbon = *blank
     c                   eval      bodtl3_aarr = w_daar
6.30 c                   eval      bodtlq_aarr = w_daar
      *
6.27 c                   if        w_numm > 0
      *
     c     bong_ifjor    tag
     c                   eval      bodtl3_ornr = w_numm
     c     bodtl3_key    setll     bodtl3
     c     bodtl3_key    reade     bodtl3r
      *
     c                   dow       not %eof
     c                   if        bdbong <> w_sbon
     c                   eval      bohel1_aarr = bdaarr
     c                   eval      bohel1_wsid = bdwsid
     c                   eval      bohel1_bong = bdbong
     c                   eval      w_sbon = bdbong
      *
     c     bohel1_key    chain     bohel1
     c                   if        %found
     c                   eval      ind = ind + 1
     c                   eval      tr_btyp(ind) = 'C'
     c                   eval      tr_dato(ind) = bodate
     c                   eval      tr_time(ind) = botime
     c                   eval      tr_numm(ind) = bdornr
     c                   eval      tr_suff(ind) = bdorsu
     c                   eval      tr_otyp(ind) = bootyp
     c                   eval      tr_stat(ind) = 'Butikk   '
     c                   eval      tr_user(ind) = bouser
     c                   eval      tr_refn(ind) = bobong
     c                   eval      tr_kass(ind) = bowsid
     c                   endif
     c                   endif
      *
     c     bodtl3_key    reade     bodtl3r
     c                   enddo
      *
     c                   if        bodtl3_aarr = w_daar
     c                   eval      bodtl3_aarr = w_faar
     c                   goto      bong_ifjor
     c                   endif
      *
6.27 c                   endif
      *
6.30 c                   if        w_bong <> *blanks
6.30  *
6.30 c     bong_ifjor2   tag
6.30 c                   eval      bodtlq_bong = w_bong
6.30 c     bodtlq_key    setll     bodtlq
6.30 c     bodtlq_key    reade     bodtlqr
6.30  *
6.30 c                   dow       not %eof
6.30 c                   if        bdbong <> w_sbon
6.30 c                   eval      bohel1_aarr = bdaarr
6.30 c                   eval      bohel1_wsid = bdwsid
6.30 c                   eval      bohel1_bong = bdbong
6.30 c                   eval      w_sbon = bdbong
6.30  *
6.30 c     bohel1_key    chain     bohel1
6.30 c                   if        %found
6.30 c                   eval      ind = ind + 1
6.30 c                   eval      tr_btyp(ind) = 'C'
6.30 c                   eval      tr_dato(ind) = bodate
6.30 c                   eval      tr_time(ind) = botime
6.30 c                   eval      tr_numm(ind) = bdornr
6.30 c                   eval      tr_suff(ind) = bdorsu
6.30 c                   eval      tr_otyp(ind) = bootyp
6.30 c                   eval      tr_stat(ind) = 'Butikk   '
6.30 c                   eval      tr_user(ind) = bouser
6.30 c                   eval      tr_refn(ind) = bobong
6.30 c                   eval      tr_kass(ind) = bowsid
6.30 c                   endif
6.30 c                   endif
6.30  *
6.30 c     bodtlq_key    reade     bodtlqr
6.30 c                   enddo
6.30  *
6.30 c                   if        bodtlq_aarr = w_daar
6.30 c                   eval      bodtlq_aarr = w_faar
6.30 c                   goto      bong_ifjor2
6.30 c                   endif
6.30  *
6.30 c                   endif
      *
     c     end_bong      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne bestilling koblet til ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_bestill  begsr
      *
      *  Legger inn startverdi for lesingen
      *
6.27 c                   if        w_numm > 0
      *
     c                   eval      lohelc_numm = w_numm
     c     lohelc_key    setll     lohelc
     c     lohelc_key    reade     lohelcr
      *
     c                   dow       not %eof
     c                   eval      ind = ind + 1
     c                   eval      tr_btyp(ind) = 'B'
     c                   eval      tr_dato(ind) = lodate
     c                   eval      tr_time(ind) = lotime
     c                   eval      tr_numm(ind) = lonumm
     c                   eval      tr_suff(ind) = losuff
     c                   eval      tr_otyp(ind) = lootyp
     c                   eval      tr_stat(ind) = 'Bestilt'
     c                   eval      tr_user(ind) = louser
6.2a c                   eval      b_bes2 = *on
      *
     c     lohelc_key    reade     lohelcr
     c                   enddo
      *
6.27 c                   endif
      *
     c     end_bestill   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne slettet bestilling koblet til ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_beslett  begsr
      *
      *  Legger inn startverdi for lesingen
      *
6.27 c                   if        w_numm > 0
      *
     c                   eval      ldell3_ornr = w_numm
     c     ldell3_key    setll     ldell3
     c     ldell3_key    reade     ldell3r
      *
     c                   dow       not %eof
8.05 c                   if        lytype <> 'FAKT'
     c                   eval      ind = ind + 1
     c                   eval      tr_btyp(ind) = 'D'
6.11 c                   eval      tr_dato(ind) = lydate
     c                   eval      tr_time(ind) = lytime
     c                   eval      tr_numm(ind) = lynumm
     c                   eval      tr_suff(ind) = lysuff
     c                   eval      tr_otyp(ind) = lyotyp
     c                   eval      tr_stat(ind) = 'Slettet'
     c                   eval      tr_user(ind) = lyuser
6.2c c                   eval      b_bes3 = *on
8.05 c                   endif
      *
     c     ldell3_key    reade     ldell3r
     c                   enddo
      *
6.27 c                   endif
      *
     c     end_beslett   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for uthenting av inng. fakt koblet til ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_innfakt  begsr
      *
      *  Legger inn startverdi for lesingen
      *
6.2d c                   eval      w_tell = 1
6.27 c                   if        w_numm > 0
      *
     c                   eval      sihel7_aarr = w_daar
      *
     c     finn_innf     tag
     c                   eval      sihel7_numm = w_numm
     c     sihel7_key    setll     sihel7
     c     sihel7_key    reade     sihel7r
      *
     c                   dow       not %eof
6.2f c                   if        b_mult = *on  and
6.2f c                             shorsu <> w_osuf
6.2f c                   goto      nxt_sihe
6.2f c                   endif
     c                   eval      ind = ind + 1
     c                   eval      tr_btyp(ind) = 'I'
     c                   eval      tr_dato(ind) = shfkda
     c                   eval      tr_time(ind) = *loval
     c                   eval      tr_numm(ind) = shnumm
     c                   eval      tr_suff(ind) = shsuff
     c                   eval      tr_otyp(ind) = shotyp
     c                   eval      tr_stat(ind) = 'Inng.fakt.'
     c                   eval      tr_user(ind) = shuser
     c                   movel     shbinr        tr_refn(ind)
     c                   eval      tr_kund(ind) = shldor
6.29 c                   eval      b_best = *on
6.2f c     nxt_sihe      tag
      *
     c     sihel7_key    reade     sihel7r
     c                   enddo
      *
6.2d c                   if        w_tell < 6
6.2d c                   eval      sihel7_aarr = w_daar - w_tell
6.2d c**                 eval      sihel7_aarr = w_faar
6.2d c                   eval      w_tell = w_tell + 1
     c                   goto      finn_innf
     c                   endif
      *
6.27 c                   endif
      *
     c     end_innfakt   endsr
      *
6.22  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.22  * Subrutine for å finne EDI informasjon
6.22  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.22  *
6.22 c     finn_EDI      begsr
      *
6.28 c                   if        w_mnum = 0
6.28 c                   leavesr
6.28 c                   endif
6.22  *
6.22  *  Legger inn startverdi for lesingen
6.22  *
6.22 c                   eval      ledil6_mnum = w_mnum
6.22 c     ledil6_key    setll     ledil6
6.22 c     ledil6_key    reade     ledil6r
6.22  *
6.22 c                   dow       not %eof
6.22 c                   if        limlin = 100001
6.22 c                   eval      ind = ind + 1
6.22 c                   eval      tr_btyp(ind) = 'E'
6.22 c                   eval      tr_dato(ind) = liodat
6.22 c                   eval      tr_time(ind) = liotim
6.22 c                   eval      tr_numm(ind) = w_mnum
6.22 c                   eval      tr_suff(ind) = 0
6.22 c                   eval      tr_otyp(ind) = 'ED'
6.22 c                   eval      tr_stat(ind) = 'EDI melding'
6.22 c                   movel     litype        tr_refn(ind)
6.22 c                   eval      tr_user(ind) = lieusr
6.22 c                   endif
6.22  *
6.22 c     ledil6_key    reade     ledil6r
6.22 c                   enddo
6.22  *
6.22 c                   endsr
6.22  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sortering av tabell
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sort_tab      begsr
      *
     c                   sorta     tr_sort
      *
     c     end_sort      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for håndtering av nytt søkebegrep
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     nytt_sok      begsr
      *
     c                   eval      w_numm = 0
     c                   eval      w_best = 0
6.22 c                   eval      w_mnum = 0
6.30 c                   eval      w_bong = *blanks
6.29 c                   eval      b_best = *off
6.2a c                   eval      b_bes2 = *off
6.2c c                   eval      b_bes3 = *off
6.2f c                   eval      b_mult = *off
      *
     c                   select
     c                   when      b2onum <> 0
     c                   eval      b2fakt = 0
     c                   eval      b2bong = *blank
     c                   eval      b2best = 0
     c                   eval      b2lonr = *blank
     c                   eval      b2lfak = *blank
     c                   eval      w_numm = b2onum
6.2b c                   exsr      via_ordr
     c                   when      b2fakt <> 0
     c                   eval      b2bong = *blank
     c                   eval      b2best = 0
     c                   eval      b2lonr = *blank
     c                   eval      b2lfak = *blank
     c                   eval      w_numm = b2onum
     c                   exsr      via_fakt
     c                   when      b2bong <> *blank
     c                   eval      b2best = 0
     c                   eval      b2lonr = *blank
     c                   eval      b2lfak = *blank
     c                   eval      w_numm = b2onum
     c                   exsr      via_bong
     c                   when      b2best <> 0
     c                   eval      b2lonr = *blank
     c                   eval      b2lfak = *blank
     c                   eval      w_numm = b2onum
6.27 c                   eval      w_best = b2best
     c                   exsr      via_best
     c                   when      b2lonr <> *blank
     c                   eval      b2lfak = *blank
     c                   exsr      via_levonr
     c                   when      b2lfak <> *blank
     c                   exsr      via_levfak
     c                   endsl
      *
      * Finn data
      *
     c                   exsr      clr_subfile
     c                   if        w_numm <> 0 or
6.30 c                             w_best <> 0 or
6.30 c                             w_bong <> *blanks
     c                   exsr      crt_subfile
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      s_onum = b2onum
     c                   eval      s_fakt = b2fakt
     c                   eval      s_bong = b2bong
     c                   eval      s_best = b2best
     c                   eval      s_lonr = b2lonr
     c                   eval      s_lfak = b2lfak
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne ordrenr via faktura
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     via_fakt      begsr
      *
     c                   eval      w_tell = 0
      *
     c                   eval      sohel2_binr = b2fakt
      *
     c     sohel2_key    setll     sohel2
     c     sohel2_key    reade     sohel2r
      *
     c                   dow       not %eof
      *
     c                   eval      w_numm = sonumm
     c                   eval      w_tell = w_tell + 1
     c     sohel2_key    reade     sohel2r
     c                   enddo
      *
      *  Er det flere forekomster - kjør eget valgprogram
      *
     c                   if        w_tell > 1
     c                   call      'FO551R'
     c                   parm                    w_firm
6.nu c                   parm                    b2fakt
6.nu c                   parm                    w_numm
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne ordrenr via bong
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     via_bong      begsr
      *
     c                   eval      w_tell = 0
     c                   eval      w_snum = 0
      *
     c                   eval      bodtl4_bong = b2bong
      *
     c     bodtl4_key    setll     bodtl4
     c     bodtl4_key    reade     bodtl4r
      *
     c                   dow       not %eof(bodtl4)
      *
     c                   if        bdornr <> 0 and
     c                             bdornr <> w_snum
     c                   eval      w_tell = w_tell + 1
     c                   eval      w_numm = bdornr
     c                   eval      w_snum = bdornr
6.30 c                   else
6.30  * Forsøker primært å gå via ordrenummer
6.30 c                   eval      w_bong = bdbong
6.30 c                   endif
6.30  *
     c     bodtl4_key    reade     bodtl4r
     c                   enddo
      *
      *  Er det flere forekomster - kjør eget valgprogram
      *
     c                   if        w_tell > 1
     c                   call      'FO552R'
     c                   parm                    w_firm
     c                   parm                    b2bong
6.nu c                   parm                    w_numm
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne ordrenr via bestilling/inng fakt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     via_best      begsr
      *
      * Via bestilling
      *
     c                   eval      lohel1_numm = b2best
     c     lohel1_key    setll     lohel1
     c     lohel1_key    reade     lohel1r
      *
     c                   dow       not %eof
      *
     c                   if        loornr <> 0
     c                   eval      w_numm = loornr
6.22  * EDI meldingsnr ?
6.22 c                   if        lomnum <> 0
6.22 c                   eval      w_mnum = lomnum
6.22 c                   endif
     c                   goto      end_via_best
     c                   endif
     c     lohel1_key    reade     lohel1r
     c                   enddo
      *
      * Via inng. faktura
      *
6.2b c                   eval      w_tell = 0
     c                   eval      sihelk_numm = b2best
     c     sihelk_key    setll     sihelk
     c     sihelk_key    reade     sihelkr
      *
     c                   dow       not %eof
      *
6.2b c                   eval      w_tell = w_tell + 1
     c                   if        shornr <> 0
     c                   eval      w_numm = shornr
6.22  * EDI meldingsnr ?
6.22 c                   if        shmnum <> 0
6.22 c                   eval      w_mnum = shmnum
6.22 c                   endif
6.2b c**                 goto      end_via_best
     c                   endif
     c     sihelk_key    reade     sihelkr
     c                   enddo
6.2b c                   if        w_tell = 1
6.2b c                   goto      end_via_best
6.2b c                   endif
6.2b  *
6.2b  *  Er det flere forekomster - kjør eget valgprogram
6.2b  *
6.2b c                   if        w_tell > 1
6.2b c                   call      'FO554R'
6.2b c                   parm                    w_firm
6.2b c                   parm                    b2best
6.2b c                   parm                    w_numm
6.2b c                   parm                    w_baar
6.2b c                   goto      end_via_best
6.2b c                   endif
      *
      * Via bestilling slettet
      *
     c                   eval      ldell2_numm = b2best
     c     ldell2_key    setll     ldell2
     c     ldell2_key    reade     ldell2r
      *
     c                   dow       not %eof
      *
     c                   if        lyornr <> 0
     c                   eval      w_numm = lyornr
     c                   goto      end_via_best
     c                   endif
     c     ldell2_key    reade     ldell2r
     c                   enddo
6.20  * Vi har ikke fått tilslag, så vi forutsetter lagerordre
6.20 c                   exsr      les_best
      *
     c     end_via_best  endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne ordrenr via leverandørens ordrenummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     via_levonr    begsr
      *
      * Via bestilling
      *
     c                   eval      lohel2_sorn = b2lonr
     c     lohel2_key    setll     lohel2
     c     lohel2_key    reade     lohel2r
      *
     c                   dow       not %eof
      *
     c                   if        loornr <> 0
     c                   eval      w_numm = loornr
     c                   goto      end_via_levo
     c                   endif
     c     lohel2_key    reade     lohel2r
     c                   enddo
      *
      * Via inng. faktura
      *
     c                   eval      sihel8_lonr = b2lonr
     c     sihel8_key    setll     sihel8
     c     sihel8_key    reade     sihel8r
      *
     c                   dow       not %eof
      *
     c                   if        shornr <> 0
     c                   eval      w_numm = shornr
     c                   goto      end_via_levo
     c                   endif
     c     sihel8_key    reade     sihel8r
     c                   enddo
6.20  * Vi har ikke fått tilslag, så vi forutsetter lagerordre
6.20 c                   exsr      les_levonr
      *
     c     end_via_levo  endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne ordrenr via leverandørens faktura
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     via_levfak    begsr
      *
      * Via bestilling
      *
     c                   eval      lohel3_sfak = b2lfak
     c     lohel3_key    setll     lohel3
     c     lohel3_key    reade     lohel3r
      *
     c                   dow       not %eof
      *
     c                   if        loornr <> 0
     c                   eval      w_numm = loornr
     c                   goto      end_via_lfak
     c                   endif
     c     lohel3_key    reade     lohel3r
     c                   enddo
      *
      * Via inng. faktura
      *
     c                   eval      sihel9_lfnr = b2lfak
     c     sihel9_key    setll     sihel9
     c     sihel9_key    reade     sihel9r
      *
     c                   dow       not %eof
      *
     c                   if        shornr <> 0
     c                   eval      w_numm = shornr
     c                   goto      end_via_lfak
     c                   endif
     c     sihel9_key    reade     sihel9r
     c                   enddo
6.20  * Vi har ikke fått tilslag, så vi forutsetter lagerordre
6.20 c                   exsr      les_levfak
      *
     c     end_via_lfak  endsr
      *
6.2b  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.2b  * Subrutine for å finne ordrenr via ordre
6.2b  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.2b  *
6.2b c     via_ordr      begsr
6.2b  *
6.2b c                   eval      fohel1_numm = w_numm
6.2b c     fohel1_key    setll     fohel1
6.2b c     fohel1_key    reade     fohel1r
6.2b  *
6.2b c                   dow       not %eof
6.2b  *
6.2b c                   if        fobenr <> 0
6.2b c                   eval      w_best = fobenr
6.2b c                   endif
6.2b c                   goto      end_via_ordr
6.2b  *
6.2b c     fohel1_key    reade     fohel1r
6.2b c                   enddo
6.2b  *
6.2b  * Sjekk gamle ordrenummer
6.2b  *
6.2b c                   eval      w_anta = 0
6.2b c                   eval      w_tell = 0
6.2b  *
6.2b c                   dow       w_tell < 6
6.2b c                   eval      sohel3_aarr = w_daar - w_tell
6.2b c                   eval      sohel3_numm = w_numm
6.2b c     sohel3_key    setll     sohel3
6.2b c     sohel3_key    reade     sohel3r
6.2b  *
6.2b c                   dow       not %eof
6.2b  *
6.2b c                   eval      w_anta = w_anta + 1
6.2b  *
6.2b c     sohel3_key    reade     sohel3r
6.2b c                   enddo
6.2b c                   eval      w_tell = w_tell + 1
6.2b c                   enddo
6.2b  *
6.2b  *  Er det flere forekomster - kjør eget valgprogram
6.2b  *
6.2b c                   if        w_anta > 1
6.2b c                   call      'FO553R'
6.2b c                   parm                    w_firm
6.2b c                   parm                    b2onum
6.2c c                   parm                    w_osuf
6.2b c                   parm                    w_best
6.2b c                   parm                    w_baar
6.2f  *
6.2f c                   eval      b_mult = *on
6.2b c                   endif
6.2b  *
6.2b c     end_via_ordr  endsr
6.2b  *
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  * Subrutine for å finne lagerbestilling
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  *
6.20 c     les_best      begsr
6.20  *
6.20  * Via bestilling
6.20  *
6.20 c                   eval      lohel1_numm = b2best
6.20 c     lohel1_key    setll     lohel1
6.20 c     lohel1_key    reade     lohel1r
6.20  *
6.20 c                   dow       not %eof
6.20  *
6.27 c**                 eval      w_numm = lonumm
6.27 c                   eval      w_numm = loornr
6.22  * EDI meldingsnr ?
6.22 c                   if        lomnum <> 0
6.22 c                   eval      w_mnum = lomnum
6.22 c                   endif
6.20 c                   goto      end_les_best
6.20 c     lohel1_key    reade     lohel1r
6.20 c                   enddo
6.20  *
6.20  * Via inng. faktura
6.20  *
6.20 c                   eval      sihelk_numm = b2best
6.20 c     sihelk_key    setll     sihelk
6.20 c     sihelk_key    reade     sihelkr
6.20  *
6.20 c                   dow       not %eof
6.20  *
6.27 c**                 eval      w_numm = shnumm
6.27 c                   eval      w_numm = shornr
6.22  * EDI meldingsnr ?
6.22 c                   if        shmnum <> 0
6.22 c                   eval      w_mnum = shmnum
6.22 c                   endif
6.20 c                   goto      end_les_best
6.20 c     sihelk_key    reade     sihelkr
6.20 c                   enddo
6.20  *
6.20  * Via bestilling slettet
6.20  *
6.20 c                   eval      ldell2_numm = b2best
6.20 c     ldell2_key    setll     ldell2
6.20 c     ldell2_key    reade     ldell2r
6.20  *
6.20 c                   dow       not %eof
6.20  *
6.20 c**                 if        lyornr <> 0
6.20 c                   eval      w_numm = lyornr
6.27 c**                 eval      w_numm = lynumm
6.20 c                   goto      end_les_best
6.20 c**                 endif
6.20 c     ldell2_key    reade     ldell2r
6.20 c                   enddo
6.20  *
6.20 c     end_les_best  endsr
6.20  *
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  * Subrutine for å finne lagerbestilling via leverandørens ordrenummer
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  *
6.20 c     les_levonr    begsr
6.20  *
6.20  * Via bestilling
6.20  *
6.20 c                   eval      lohel2_sorn = b2lonr
6.20 c     lohel2_key    setll     lohel2
6.20 c     lohel2_key    reade     lohel2r
6.20  *
6.20 c                   dow       not %eof
6.20  *
6.20 c**                 if        loornr <> 0
6.20 c**                 eval      w_numm = loornr
6.2g c*                  eval      w_numm = lonumm
6.2g c                   eval      w_best = lonumm
6.20 c                   goto      end_les_levo
6.20 c**                 endif
6.20 c     lohel2_key    reade     lohel2r
6.20 c                   enddo
6.20  *
6.20  * Via inng. faktura
6.20  *
6.20 c                   eval      sihel8_lonr = b2lonr
6.20 c     sihel8_key    setll     sihel8
6.20 c     sihel8_key    reade     sihel8r
6.20  *
6.20 c                   dow       not %eof
6.20  *
6.20 c**                 if        shornr <> 0
6.20 c**                 eval      w_numm = shornr
6.2g c*                  eval      w_numm = shnumm
6.2g c                   eval      w_best = shnumm
6.20 c                   goto      end_les_levo
6.20 c**                 endif
6.20 c     sihel8_key    reade     sihel8r
6.20 c                   enddo
6.20  *
6.20 c     end_les_levo  endsr
6.20  *
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  * Subrutine for å finne lagerordre via leverandørens faktura
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  *
6.20 c     les_levfak    begsr
6.20  *
6.20  * Via bestilling
6.20  *
6.20 c                   eval      lohel3_sfak = b2lfak
6.20 c     lohel3_key    setll     lohel3
6.20 c     lohel3_key    reade     lohel3r
6.20  *
6.20 c                   dow       not %eof
6.20  *
6.20 c**                 if        loornr <> 0
6.20 c**                 eval      w_numm = loornr
6.20 c                   eval      w_numm = lonumm
6.20 c                   goto      end_les_lfak
6.20 c**                 endif
6.20 c     lohel3_key    reade     lohel3r
6.20 c                   enddo
6.20  *
6.20  * Via inng. faktura
6.20  *
6.20 c                   eval      sihel9_lfnr = b2lfak
6.20 c     sihel9_key    setll     sihel9
6.20 c     sihel9_key    reade     sihel9r
6.20  *
6.20 c                   dow       not %eof
6.20  *
6.20 c**                 if        shornr <> 0
6.20 c**                 eval      w_numm = shornr
6.20 c                   eval      w_numm = shnumm
6.20 c                   goto      end_les_lfak
6.20 c**                 endif
6.20 c     sihel9_key    reade     sihel9r
6.20 c                   enddo
6.20  *
6.20 c     end_les_lfak  endsr
6.20  *
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  * Subrutine for å finne bestilling koblet til ordre
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  *
6.20 c     lagr_bestill  begsr
6.20  *
6.2a c                   if        b_bes2 = *off
6.20  *
6.20  *  Legger inn startverdi for lesingen
6.20  *
6.27 c**                 eval      lohel1_numm = w_numm
6.27 c                   eval      lohel1_numm = w_best
6.20 c     lohel1_key    setll     lohel1
6.20 c     lohel1_key    reade     lohel1r
6.20  *
6.20 c                   dow       not %eof
6.20 c                   eval      ind = ind + 1
6.20 c                   eval      tr_btyp(ind) = 'B'
6.20 c                   eval      tr_dato(ind) = lodate
6.20 c                   eval      tr_time(ind) = lotime
6.20 c                   eval      tr_numm(ind) = lonumm
6.20 c                   eval      tr_suff(ind) = losuff
6.20 c                   eval      tr_otyp(ind) = lootyp
6.20 c                   eval      tr_stat(ind) = 'Bestilt'
6.20 c                   eval      tr_user(ind) = louser
6.20  *
6.20 c     lohel1_key    reade     lohel1r
6.20 c                   enddo
6.20  *
6.2a c                   endif
6.20  *
6.20 c                   endsr
6.20  *
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  * Subrutine for å finne slettet bestilling koblet til ordre
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  *
6.20 c     lagr_beslett  begsr
6.20  *
6.20  *  Legger inn startverdi for lesingen
6.2c c                   if        b_bes3 = *off
6.20  *
6.27 c**                 eval      ldell2_numm = w_numm
6.27 c                   eval      ldell2_numm = w_best
6.20 c     ldell2_key    setll     ldell2
6.20 c     ldell2_key    reade     ldell2r
6.20  *
6.20 c                   dow       not %eof
6.20  *
8.05 c                   if        lytype <> 'FAKT'
6.20  *
6.20 c                   eval      ind = ind + 1
6.20 c                   eval      tr_btyp(ind) = 'D'
6.20 c                   eval      tr_dato(ind) = lydate
6.20 c                   eval      tr_time(ind) = lytime
6.20 c                   eval      tr_numm(ind) = lynumm
6.20 c                   eval      tr_suff(ind) = lysuff
6.20 c                   eval      tr_otyp(ind) = lyotyp
6.20 c                   eval      tr_stat(ind) = 'Slettet'
6.20 c                   eval      tr_user(ind) = lyuser
6.20  *
8.05 c                   endif
6.20  *
6.20 c     ldell2_key    reade     ldell2r
6.20 c                   enddo
6.20  *
6.2c c                   endif
6.20 c                   endsr
6.20  *
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  * Subrutine for uthenting av inng. fakt koblet til ordre
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  *
6.20 c     lagr_innfakt  begsr
6.20  *
6.20  *  Legger inn startverdi for lesingen
6.20  *
6.27 c**                 eval      sihelk_numm = w_numm
6.30 c                   if        b_best = *off and
6.30 c                             w_best > 0
6.27 c                   eval      sihelk_numm = w_best
6.20 c     sihelk_key    setll     sihelk
6.20 c     sihelk_key    reade     sihelkr
6.20  *
6.20 c                   dow       not %eof
6.2b  * Sjekk om det er valgt år (hvis dubletter)
6.2b c                   if        w_baar <> 0 and
6.2b c                             w_baar <> shaarr
6.2b c                   goto      ny_les
6.2b c                   endif
6.2b  *
6.20 c                   eval      ind = ind + 1
6.20 c                   eval      tr_btyp(ind) = 'I'
6.20 c                   eval      tr_dato(ind) = shfkda
6.20 c                   eval      tr_time(ind) = *loval
6.20 c                   eval      tr_numm(ind) = shnumm
6.20 c                   eval      tr_suff(ind) = shsuff
6.20 c                   eval      tr_otyp(ind) = shotyp
6.20 c                   eval      tr_stat(ind) = 'Inng.fakt.'
6.20 c                   eval      tr_user(ind) = shuser
6.20 c                   movel     shbinr        tr_refn(ind)
6.20 c                   eval      tr_kund(ind) = shldor
6.2b  *
6.2b c     ny_les        tag
6.2b  *
6.20 c     sihelk_key    reade     sihelkr
6.20 c                   enddo
6.20  *
6.29 c                   endif
6.20  *
6.20 c                   endsr
6.20  *
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  * Subrutine for overgang til inngående fakturahistorikk
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  *
6.20 c     jmp_fakthis   begsr
6.20  *
6.20 c                   call      'SH400R'
6.20  *
6.20 c                   endsr
6.23  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.23  * Subrutine for overgang til EDI mottaksrutiner
6.23  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.23  *
6.23 c     jmp_EDI       begsr
6.23  *
7.01 c                   eval      s_curn = w_curn
7.01 c                   eval      s_virn = w_virn
6.24 c                   eval      l_vise = '1'
6.23 c                   eval      w_type = *blank
6.24 c                   out       *dtaara
6.23 c                   call      'LI100R'
6.23 c                   parm                    w_type
6.26  *
7.01 c                   eval      w_curn = s_curn
7.01 c                   eval      w_virn = s_virn
6.23  *
6.23 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *dtaara       define    *lda          lda
      *
      * Parametre
      *
     c     *entry        plist
6.nu c                   parm                    p_numm
6.24 c                   parm                    p_ntyp
      *
      *  Key les av AKTIVE-ORDRES
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
      *
      *  Key les av SLETTEDE-ORDRES
      *
     c     fdell8_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fdell8_numm
      *
      *  Key les av FAKTURERTE-ORDRES via fakturanr
      *
     c     sohel2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sohel2_binr
      *
      *  Key les av FAKTURERTE-ORDRES via ordre
      *
     c     sohel3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sohel3_aarr
     c                   kfld                    sohel3_numm
      *
      * Key for oppslag på bongnummer
      *
     c     bohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bohel1_aarr
     c                   kfld                    bohel1_wsid
     c                   kfld                    bohel1_bong
      *
      * Key for posisjonering på bongnummer
      *
     c     bohel3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bohel3_bong
      *
      *  Key les av BUTIKKEKSPEDERTE-ORDRES - linje
      *
     c     bodtl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bodtl3_aarr
     c                   kfld                    bodtl3_ornr
6.30  *
6.30  *  Key les av BUTIKKEKSPEDERTE-ORDRES - linje
6.30  *
6.30 c     bodtlq_key    klist
6.30 c                   kfld                    w_firm
6.30 c                   kfld                    bodtlq_aarr
6.30 c                   kfld                    bodtlq_bong
      *
      *  Key les av BUTIKKEKSPEDERTE-ORDRES - linje
      *
     c     bodtl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bodtl4_bong
      *
      *  Key les av BESTILLING på bestnr
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
      *
      *  Key les av BESTILLING på lev. ordrenummer
      *
     c     lohel2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel2_sorn
      *
      *  Key les av BESTILLING på lev. fakturanummer
      *
     c     lohel3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel3_sfak
      *
      *  Key les av BESTILLING på ordre
      *
     c     lohelc_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelc_numm
      *
      *  Key les av SLETTEDE-BESTILLINGER
      *
     c     ldell2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ldell2_numm
      *
      *  Key les av SLETTEDE-BESTILLINGER på ordre
      *
     c     ldell3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ldell3_ornr
      *
      *  Key les av INNGÅENDE FAKTURA på bestilling
      *
     c     sihelk_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sihelk_numm
      *
      *  Key les av INNGÅENDE FAKTURA på ordre
      *
     c     sihel7_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sihel7_aarr
     c                   kfld                    sihel7_numm
      *
      *  Key les av INNGÅENDE FAKTURA på lev. ordrenummer
      *
     c     sihel8_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sihel8_lonr
      *
      *  Key les av INNGÅENDE FAKTURA på ordre
      *
     c     sihel9_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sihel9_lfnr
6.22  *
6.22  *  Key les av EDI meldinger
6.22  *
6.22 c     ledil6_key    klist
6.22 c                   kfld                    w_firm
6.22 c                   kfld                    ledil6_mnum
8.02  *
8.02  *  Key les av ordretype
8.02  *
8.02 c     votyl1_key    klist
8.02 c                   kfld                    w_firm
8.02 c                   kfld                    votyl1_otyp
      *
      * Setter inn startverdier
      *
     c                   eval      w_firm = l_firm
     c                   eval      *in22  = *on
      *
     c                   time                    w_ddat
     c                   eval      w_daar = %subdt(w_ddat:*years)
     c                   eval      w_faar = w_daar - 1
      *
     c                   endsr
      *
