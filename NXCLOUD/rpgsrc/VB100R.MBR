     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VB100R
      * Beskrivelse . . : Bruksrett-knytning, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       051203 HKO  Nytt program
      *       080605 HKO  Åpnet for å legge inn knytning mot VA-vare
7.xx  * 7.00  130516 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvbrkl1    if   e           k disk    rename(vbrkpfr:vbrkl1r)
     fvbrkl2    if   e           k disk    rename(vbrkpfr:vbrkl2r)
     fvbrkl3    if   e           k disk    rename(vbrkpfr:vbrkl3r)
     fvbrkl4    if   e           k disk    rename(vbrkpfr:vbrkl4r)
     fvbrkl5    if   e           k disk    rename(vbrkpfr:vbrkl5r)
     fvbrklr    if   e           k disk    rename(vbrkpfr:vbrklrr)
     fvbrklu    uf a e           k disk    rename(vbrkpfr:vbrklur)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvarlb    if   e           k disk    rename(vvarpfr:vvarlbr)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     frmbtl1    if   e           k disk    rename(rmbtpfr:rmbtl1r)
      *
     fvb100d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vbfirm)
     d p_alme          s                   like(vbalme)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d vbrkl1_ogrp     s                   like(vbogrp)
     d vbrkl1_hgrp     s                   like(vbhgrp)
     d vbrkl1_ugrp     s                   like(vbugrp)
     d vbrkl1_modn     s                   like(vbmodn)
     d vbrkl1_vare     s                   like(vbvare)
     d vbrkl2_hgrp     s                   like(vbhgrp)
     d vbrkl3_ugrp     s                   like(vbugrp)
     d vbrkl4_modn     s                   like(vbmodn)
     d vbrkl5_vare     s                   like(vbvare)
     d vbrklr_ogrp     s                   like(vbogrp)
     d vbrklr_hgrp     s                   like(vbhgrp)
     d vbrklr_ugrp     s                   like(vbugrp)
     d vbrklr_modn     s                   like(vbmodn)
     d vbrklr_vare     s                   like(vbvare)
     d vbrklu_ogrp     s                   like(vbogrp)
     d vbrklu_hgrp     s                   like(vbhgrp)
     d vbrklu_ugrp     s                   like(vbugrp)
     d vbrklu_modn     s                   like(vbmodn)
     d vbrklu_vare     s                   like(vbvare)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d vvarl1_vare     s                   like(vvvare)
     d vvarlb_nmod     s                   like(vvnmod)
     d jvarl1_vare     s                   like(jvvare)
     d rmbtl1_tbrt     s                   like(rmtbrt)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_mtx1          s             35
      *
     d w_firm          s                   like(vbfirm)
     d w_alme          s                   like(vbalme)
     d w_ogrp          s                   like(vbugrp)
     d w_hgrp          s                   like(vbhgrp)
     d w_ugrp          s                   like(vbugrp)
     d w_modn          s                   like(vbmodn)
     d w_vare          s                   like(vbvare)
     d w_otxt          s                   like(vgotxt)
     d w_htxt          s                   like(vghtxt)
     d w_utxt          s                   like(vgutxt)
     d w_tek1          s                   like(vvtek1)
      *
     d s_ogrp          s                   like(vbogrp)
     d s_hgrp          s                   like(vbhgrp)
     d s_ugrp          s                   like(vbugrp)
     d s_modn          s                   like(vbmodn)
     d s_vare          s                   like(vbvare)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2tagb
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2ogrp <> 0 or
     c                             b2hgrp <> 0 or
     c                             b2ugrp <> 0 or
     c                             b2modn <> 0 or
     c                             b2vare <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'H'
     c                   eval      vbrkl2_hgrp = b1hgrp
     c     vbrkl2_key    setll     vbrkl2
     c                   when      w_seqe = 'U'
     c                   eval      vbrkl3_ugrp = b1ugrp
     c     vbrkl3_key    setll     vbrkl3
     c                   when      w_seqe = 'M'
     c                   eval      vbrkl4_modn = b1modn
     c     vbrkl4_key    setll     vbrkl4
     c                   when      w_seqe = 'V'
     c                   eval      vbrkl5_vare = b1vare
     c     vbrkl5_key    setll     vbrkl5
     c                   other
     c                   eval      vbrkl1_ogrp = b1ogrp
     c                   eval      vbrkl1_hgrp = b1hgrp
     c                   eval      vbrkl1_ugrp = b1ugrp
     c                   eval      vbrkl1_modn = b1modn
     c                   eval      vbrkl1_vare = b1vare
     c     vbrkl1_key    setll     vbrkl1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Overgruppe
      *
     c                   if        b2ogrp <> 0
     c                   eval      w_seqe = *blank
     c                   eval      vbrkl1_ogrp = b2ogrp
     c                   eval      vbrkl1_hgrp = b2hgrp
     c                   eval      vbrkl1_ugrp = b2ugrp
     c                   eval      vbrkl1_modn = b2modn
     c                   eval      vbrkl1_vare = b2vare
     c     vbrkl1_key    setll     vbrkl1
     c                   goto      end_pos
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        b2hgrp <> 0
     c                   eval      w_seqe = 'H'
     c                   eval      vbrkl2_hgrp = b2hgrp
     c     vbrkl2_key    setll     vbrkl2
     c                   goto      end_pos
     c                   endif
      *
      * Undergruppe
      *
     c                   if        b2ugrp <> 0
     c                   eval      w_seqe = 'U'
     c                   eval      vbrkl3_ugrp = b2ugrp
     c     vbrkl3_key    setll     vbrkl3
     c                   goto      end_pos
     c                   endif
      *
      * Modul
      *
     c                   if        b2modn <> 0
     c                   eval      w_seqe = 'M'
     c                   eval      vbrkl4_modn = b2modn
     c     vbrkl4_key    setll     vbrkl4
     c                   goto      end_pos
     c                   endif
      *
      * Vare
      *
     c                   if        b2vare <> *blank
     c                   eval      w_seqe = 'V'
     c                   eval      vbrkl5_vare = b2vare
     c     vbrkl5_key    setll     vbrkl5
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2ogrp = 0
     c                   eval      b2hgrp = 0
     c                   eval      b2ugrp = 0
     c                   eval      b2modn = 0
     c                   eval      b2vare = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1ogrp = b1ogrp
     c                   eval      c1hgrp = b1hgrp
     c                   eval      c1ugrp = b1ugrp
     c                   eval      c1modn = b1modn
     c                   eval      c1vare = b1vare
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1ogrp = b1ogrp
     c                   eval      k1hgrp = b1hgrp
     c                   eval      k1ugrp = b1ugrp
     c                   eval      k1modn = b1modn
     c                   eval      k1vare = b1vare
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1ogrp = b1ogrp
     c                   eval      d1hgrp = b1hgrp
     c                   eval      d1ugrp = b1ugrp
     c                   eval      d1modn = b1modn
     c                   eval      d1vare = b1vare
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1ogrp = b1ogrp
     c                   eval      c1hgrp = b1hgrp
     c                   eval      c1ugrp = b1ugrp
     c                   eval      c1modn = b1modn
     c                   eval      c1vare = b1vare
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1ogrp = b2ogrp
     c                   eval      c1hgrp = b2hgrp
     c                   eval      c1ugrp = b2ugrp
     c                   eval      c1modn = b2modn
     c                   eval      c1vare = b2vare
      *
     c                   exsr      hent_info
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      c1taga
     c                   endsl
      *
     c                   if        c1ogrp = 0 and
     c                             c1hgrp = 0 and
     c                             c1ugrp = 0 and
     c                             c1modn = 0 and
     c                             c1vare = *blank
     c                   goto      end_c1bld
     c                   endif
      *
      * Sjekker input
      *
     c                   eval      w_ogrp = c1ogrp
     c                   eval      w_hgrp = c1hgrp
     c                   eval      w_ugrp = c1ugrp
     c                   eval      w_modn = c1modn
     c                   eval      w_vare = c1vare
     c                   exsr      sjekk_input
     c                   eval      c1ogrp = w_ogrp
     c                   eval      c1hgrp = w_hgrp
     c                   eval      c1ugrp = w_ugrp
     c                   eval      c1modn = w_modn
     c                   eval      c1vare = w_vare
     c                   if        b_feil = *on
     c                   goto      c1taga
     c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      vbrklr_ogrp = c1ogrp
     c                   eval      vbrklr_hgrp = c1hgrp
     c                   eval      vbrklr_ugrp = c1ugrp
     c                   eval      vbrklr_modn = c1modn
     c                   eval      vbrklr_vare = c1vare
     c     vbrklr_key    chain     vbrklr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1ogrp = 0
     c                   eval      c1otxt = *blank
     c                   eval      c1hgrp = 0
     c                   eval      c1htxt = *blank
     c                   eval      c1ugrp = 0
     c                   eval      c1utxt = *blank
     c                   eval      c1modn = 0
     c                   eval      c1mtx1 = *blank
     c                   eval      c1vare = *blank
     c                   eval      c1tek1 = *blank
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2ogrp = 0
     c                   eval      b2hgrp = 0
     c                   eval      b2ugrp = 0
     c                   eval      b2modn = 0
     c                   eval      b2vare = *blank
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      vbrklr_ogrp = c1ogrp
     c                   eval      vbrklr_hgrp = c1hgrp
     c                   eval      vbrklr_ugrp = c1ugrp
     c                   eval      vbrklr_modn = c1modn
     c                   eval      vbrklr_vare = c1vare
     c     vbrklr_key    chain     vbrklr
      *
     c                   if        %found
     c                   eval      c2brty = vbbrty
     c                   eval      c2eusr = vbeusr
     c     *dmy          move      vbodat        c2odat
     c     *dmy          move      vbedat        c2edat
     c                   else
     c                   eval      c2brty = 0
     c                   eval      c2eusr = *blank
     c                   eval      c2odat = 0
     c                   eval      c2edat = 0
     c                   endif
      *
      * Henter beskrivelse av nivå
      *
     c                   exsr      hent_info
      *
     c                   eval      c2ttxt = *blank
      *
     c                   eval      rmbtl1_tbrt = c2brty
     c     rmbtl1_key    chain     rmbtl1
     c                   if        %found
     c                   eval      c2ttxt = rmttxt
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1ogrp = k1ogrp
     c                   eval      c1hgrp = k1hgrp
     c                   eval      c1ugrp = k1ugrp
     c                   eval      c1modn = k1modn
     c                   eval      c1vare = k1vare
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   if        w_afld = 'C2BRTY'
     c                   call      'RM503R'
     c                   parm                    w_firm
     c                   parm                    w_alme
     c                   parm                    c2brty
     c                   parm                    c2ttxt
     c                   endif
     c                   goto      c2taga
     c                   endif
      *
      * Validering av input
      *
     c                   eval      rmbtl1_tbrt = c2brty
     c     rmbtl1_key    chain     rmbtl1
     c                   if        not %found
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
     c                   eval      c2ttxt = rmttxt
      *
      * Oppdatering
      *
     c                   clear                   vbrklur
      *
     c                   eval      vbrklu_ogrp = c1ogrp
     c                   eval      vbrklu_hgrp = c1hgrp
     c                   eval      vbrklu_ugrp = c1ugrp
     c                   eval      vbrklu_modn = c1modn
     c                   eval      vbrklu_vare = c1vare
     c     vbrklu_key    chain     vbrklur
      *
     c                   eval      vbbrty = c2brty
     c                   eval      vbeusr = l_user
      *
     c                   time                    vbedat
     c                   time                    vbetim
      *
     c                   if        %found
     c                   update    vbrklur
     c                   else
     c                   eval      vbfirm = w_firm
     c                   eval      vbalme = w_alme
     c                   eval      vbogrp = c1ogrp
     c                   eval      vbhgrp = c1hgrp
     c                   eval      vbugrp = c1ugrp
     c                   eval      vbmodn = c1modn
     c                   eval      vbvare = c1vare
     c                   time                    vbodat
     c                   write     vbrklur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1vare = vbvare
     c                   eval      b1brty = vbbrty
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      vbrklr_ogrp = d1ogrp
     c                   eval      vbrklr_hgrp = d1hgrp
     c                   eval      vbrklr_ugrp = d1ugrp
     c                   eval      vbrklr_modn = d1modn
     c                   eval      vbrklr_vare = d1vare
     c     vbrklr_key    chain     vbrklr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      vbrklu_ogrp = d1ogrp
     c                   eval      vbrklu_hgrp = d1hgrp
     c                   eval      vbrklu_ugrp = d1ugrp
     c                   eval      vbrklu_modn = d1modn
     c                   eval      vbrklu_vare = d1vare
     c     vbrklu_key    chain     vbrklur
      *
     c                   delete    vbrklur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_ogrp = k1ogrp
     c                   eval      s_hgrp = k1hgrp
     c                   eval      s_ugrp = k1ugrp
     c                   eval      s_modn = k1modn
     c                   eval      s_vare = k1vare
      *
      * Fyller kopierings-bildet
      *
     c                   eval      vbrklr_ogrp = k1ogrp
     c                   eval      vbrklr_hgrp = k1hgrp
     c                   eval      vbrklr_ugrp = k1ugrp
     c                   eval      vbrklr_modn = k1modn
     c                   eval      vbrklr_vare = k1vare
     c     vbrklr_key    chain     vbrklr
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd = *on
     c                   exsr      spØrring
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker input
      *
     c                   eval      w_ogrp = k1ogrp
     c                   eval      w_hgrp = k1hgrp
     c                   eval      w_ugrp = k1ugrp
     c                   eval      w_modn = k1modn
     c                   eval      w_vare = k1vare
     c                   exsr      sjekk_input
     c                   eval      k1ogrp = w_ogrp
     c                   eval      k1hgrp = w_hgrp
     c                   eval      k1ugrp = w_ugrp
     c                   eval      k1modn = w_modn
     c                   eval      k1vare = w_vare
     c                   if        b_feil = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      vbrklr_ogrp = k1ogrp
     c                   eval      vbrklr_hgrp = k1hgrp
     c                   eval      vbrklr_ugrp = k1ugrp
     c                   eval      vbrklr_modn = k1modn
     c                   eval      vbrklr_vare = k1vare
     c     vbrklr_key    chain     vbrklr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1ogrp = s_ogrp
     c                   eval      c1hgrp = s_hgrp
     c                   eval      c1ugrp = s_ugrp
     c                   eval      c1modn = s_modn
     c                   eval      c1vare = s_vare
      *
     c                   exsr      xc2bld
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'H'
     c                   read      vbrkl2
     c                   when      w_seqe = 'U'
     c                   read      vbrkl3
     c                   when      w_seqe = 'M'
     c                   read      vbrkl4
     c                   when      w_seqe = 'V'
     c                   read      vbrkl5
     c                   other
     c                   read      vbrkl1
     c                   endsl
      *
     c                   if        %eof
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   if        vbfirm <> w_firm or
     c                             vbalme <> w_alme
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'H'
     c                   eval      vbrkl2_hgrp = vbhgrp
     c                   when      w_seqe = 'U'
     c                   eval      vbrkl3_ugrp = vbugrp
     c                   when      w_seqe = 'M'
     c                   eval      vbrkl4_modn = vbmodn
     c                   when      w_seqe = 'V'
     c                   eval      vbrkl5_vare = vbvare
     c                   other
     c                   eval      vbrkl1_ogrp = vbogrp
     c                   eval      vbrkl1_hgrp = vbhgrp
     c                   eval      vbrkl1_ugrp = vbugrp
     c                   eval      vbrkl1_modn = vbmodn
     c                   eval      vbrkl1_vare = vbvare
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1ogrp = vbogrp
     c                   eval      b1hgrp = vbhgrp
     c                   eval      b1ugrp = vbugrp
     c                   eval      b1utxt = *blank
     c                   eval      b1modn = vbmodn
     c                   eval      b1vare = vbvare
     c                   eval      b1tek1 = *blank
     c                   eval      b1brty = vbbrty
      *
     c                   if        b1ogrp <> 0
     c                   eval      vogrl1_oogr = b1ogrp
     c     vogrl1_key    chain     vogrl1
     c                   if        %found
     c                   eval      b1utxt = vgotxt
     c                   endif
     c                   endif
      *
     c                   if        b1hgrp <> 0
     c                   eval      vhgrl1_hogr = b1ogrp
     c                   eval      vhgrl1_hhgr = b1hgrp
     c     vhgrl1_key    chain     vhgrl1
     c                   if        %found
     c                   eval      b1utxt = vghtxt
     c                   endif
     c                   endif
      *
     c                   if        b1ugrp <> 0
     c                   eval      vugrl1_uogr = b1ogrp
     c                   eval      vugrl1_uhgr = b1hgrp
     c                   eval      vugrl1_uugr = b1ugrp
     c     vugrl1_key    chain     vugrl1
     c                   if        %found
     c                   eval      b1utxt = vgutxt
     c                   endif
     c                   endif
      *
     c                   if        b1vare <> *blank
     c                   eval      vvarl1_vare = b1vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   eval      b1tek1 = vvtek1
     c                   else
     c                   eval      jvarl1_vare = b1vare
     c     jvarl1_key    chain     jvarl1
     c                   if        %found
     c                   eval      b1tek1 = jvtek1
     c                   endif
     c                   endif
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'H'
     c     vbrkl2_key    setgt     vbrkl2
     c                   readp     vbrkl2
     c                   when      w_seqe = 'U'
     c     vbrkl3_key    setgt     vbrkl3
     c                   readp     vbrkl3
     c                   when      w_seqe = 'M'
     c     vbrkl4_key    setgt     vbrkl4
     c                   readp     vbrkl4
     c                   when      w_seqe = 'V'
     c     vbrkl5_key    setgt     vbrkl5
     c                   readp     vbrkl5
     c                   other
     c     vbrkl1_key    setgt     vbrkl1
     c                   readp     vbrkl1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'H'
     c                   readp     vbrkl2
     c                   when      w_seqe = 'U'
     c                   readp     vbrkl3
     c                   when      w_seqe = 'M'
     c                   readp     vbrkl4
     c                   when      w_seqe = 'V'
     c                   readp     vbrkl5
     c                   other
     c                   readp     vbrkl1
     c                   endsl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   if        vbfirm <> w_firm or
     c                             vbalme <> w_alme
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'H'
     c                   eval      vbrkl2_hgrp = vbhgrp
     c                   when      w_seqe = 'U'
     c                   eval      vbrkl3_ugrp = vbugrp
     c                   when      w_seqe = 'M'
     c                   eval      vbrkl4_modn = vbmodn
     c                   when      w_seqe = 'V'
     c                   eval      vbrkl5_vare = vbvare
     c                   other
     c                   eval      vbrkl1_ogrp = vbogrp
     c                   eval      vbrkl1_hgrp = vbhgrp
     c                   eval      vbrkl1_ugrp = vbugrp
     c                   eval      vbrkl1_modn = vbmodn
     c                   eval      vbrkl1_vare = vbvare
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'H'
     c     vbrkl2_key    setll     vbrkl2
     c                   when      w_seqe = 'U'
     c     vbrkl3_key    setll     vbrkl3
     c                   when      w_seqe = 'M'
     c     vbrkl4_key    setll     vbrkl4
     c                   when      w_seqe = 'V'
     c     vbrkl5_key    setll     vbrkl5
     c                   other
     c     vbrkl1_key    setll     vbrkl1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Overgruppe
      *
     c                   if        w_afld = 'B2OGRP'
     c                   eval      b2hgrp = 0
     c                   eval      b2ugrp = 0
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    w_otxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1OGRP'
     c                   eval      c1hgrp = 0
     c                   eval      c1ugrp = 0
     c                   eval      c1otxt = *blank
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    c1ogrp
     c                   parm                    c1otxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1OGRP'
     c                   eval      k1hgrp = 0
     c                   eval      k1ugrp = 0
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    k1ogrp
     c                   parm                    w_otxt
     c                   leavesr
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        w_afld = 'B2HGRP'
     c                   eval      b2ugrp = 0
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    b2hgrp
     c                   parm                    w_htxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1HGRP'
     c                   eval      c1ugrp = 0
     c                   eval      c1htxt = *blank
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    c1ogrp
     c                   parm                    c1hgrp
     c                   parm                    c1htxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1HGRP'
     c                   eval      k1ugrp = 0
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    k1ogrp
     c                   parm                    k1hgrp
     c                   parm                    w_htxt
     c                   leavesr
     c                   endif
      *
      * Undergruppe
      *
     c                   if        w_afld = 'B2UGRP'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    b2hgrp
     c                   parm                    b2ugrp
     c                   parm                    w_utxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1UGRP'
     c                   eval      c1utxt = *blank
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    c1ogrp
     c                   parm                    c1hgrp
     c                   parm                    c1ugrp
     c                   parm                    c1utxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1UGRP'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    k1ogrp
     c                   parm                    k1hgrp
     c                   parm                    k1ugrp
     c                   parm                    w_utxt
     c                   leavesr
     c                   endif
      *
      * Modul
      *
     c                   if        w_afld = 'B2MODN'
     c                   call      'FO707C'
     c                   parm                    w_firm
     c                   parm                    b2modn
     c                   parm                    w_mtx1
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1MODN'
     c                   call      'FO707C'
     c                   parm                    w_firm
     c                   parm                    c1modn
     c                   parm                    c1mtx1
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1MODN'
     c                   call      'FO707C'
     c                   parm                    w_firm
     c                   parm                    k1modn
     c                   parm                    w_mtx1
     c                   leavesr
     c                   endif
      *
      * Varenummer
      *
     c                   if        w_afld = 'B2VARE'
     c                   eval      w_vare = b2vare
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_tek1
     c                   eval      b2vare = w_vare
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1VARE'
     c                   eval      w_vare = c1vare
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    c1tek1
     c                   eval      c1vare = w_vare
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1VARE'
     c                   eval      w_vare = k1vare
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_tek1
     c                   eval      k1vare = w_vare
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker inntastede verdier, og henter beskrivelser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
     c                   eval      w_otxt = *blank
     c                   eval      w_htxt = *blank
     c                   eval      w_utxt = *blank
     c                   eval      w_mtx1 = *blank
     c                   eval      w_tek1 = *blank
      *
      * Sjekk nivå
      *
     c                   if        w_ogrp = 0 and
     c                             w_modn = 0 and
     c                             w_vare = *blank
     c                   eval      *in31  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      * Vare
      *
     c                   if        w_vare <> *blank
     c                   eval      vvarl1_vare = w_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   eval      jvarl1_vare = w_vare
     c     jvarl1_key    chain     jvarl1
     c                   if        %found
     c                   eval      vvogrp = jvogrp
     c                   eval      vvhgrp = jvhgrp
     c                   eval      vvugrp = jvugrp
     c                   eval      vvnmod = jvmodn
     c                   eval      vvtek1 = jvtek1
     c                   else
     c                   eval      *in39 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
     c                   eval      w_ogrp = vvogrp
     c                   eval      w_hgrp = vvhgrp
     c                   eval      w_ugrp = vvugrp
     c                   eval      w_modn = vvnmod
     c                   eval      w_tek1 = vvtek1
     c                   endif
      *
      * Modul
      *
     c                   if        w_modn <> 0 and
     c                             w_vare = *blank
     c                   eval      vvarlb_nmod = w_modn
     c     vvarlb_key    chain     vvarlb                             38
     c                   if        *in38 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      w_ogrp = vvogrp
     c                   eval      w_hgrp = vvhgrp
     c                   eval      w_ugrp = vvugrp
     c                   call      'FO714C'
     c                   parm                    w_firm
     c                   parm                    w_modn
     c                   parm                    w_mtx1
     c                   endif
      *
      * Overgruppe
      *
     c                   if        w_ogrp <> 0 and
     c                             w_hgrp = 0 and
     c                             w_ugrp = 0
     c                   eval      vogrl1_oogr = w_ogrp
     c     vogrl1_key    chain     vogrl1                             35
     c                   if        *in35 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      w_otxt = vgotxt
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        w_hgrp <> 0 and
     c                             w_ugrp = 0
     c                   eval      vhgrl1_hogr = w_ogrp
     c                   eval      vhgrl1_hhgr = w_hgrp
     c     vhgrl1_key    chain     vhgrl1                             36
     c                   if        *in36 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      w_htxt = vghtxt
     c                   endif
      *
      * Undergruppe
      *
     c                   if        w_ugrp <> 0
     c                   eval      vugrl1_uogr = w_ogrp
     c                   eval      vugrl1_uhgr = w_hgrp
     c                   eval      vugrl1_uugr = w_ugrp
     c     vugrl1_key    chain     vugrl1                             37
     c                   if        *in37 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      w_utxt = vgutxt
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter beskrivelse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_info     begsr
      *
     c                   eval      c1otxt = *blank
     c                   eval      c1htxt = *blank
     c                   eval      c1utxt = *blank
     c                   eval      c1mtx1 = *blank
     c                   eval      c1tek1 = *blank
      *
      * Overgruppe
      *
     c                   if        c1ogrp <> 0
     c                   eval      vogrl1_oogr = c1ogrp
     c     vogrl1_key    chain     vogrl1
     c                   if        %found
     c                   eval      c1otxt = vgotxt
     c                   endif
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        c1hgrp <> 0
     c                   eval      vhgrl1_hogr = c1ogrp
     c                   eval      vhgrl1_hhgr = c1hgrp
     c     vhgrl1_key    chain     vhgrl1
     c                   if        %found
     c                   eval      c1htxt = vghtxt
     c                   endif
     c                   endif
      *
      * Undergruppe
      *
     c                   if        c1ugrp <> 0
     c                   eval      vugrl1_uogr = c1ogrp
     c                   eval      vugrl1_uhgr = c1hgrp
     c                   eval      vugrl1_uugr = c1ugrp
     c     vugrl1_key    chain     vugrl1
     c                   if        %found
     c                   eval      c1utxt = vgutxt
     c                   endif
     c                   endif
      *
      * Modul
      *
     c                   if        c1modn <> 0
     c                   call      'FO714C'
     c                   parm                    w_firm
     c                   parm                    c1modn
     c                   parm                    c1mtx1
     c                   endif
      *
      * Vare
      *
     c                   if        c1vare <> *blank
     c                   eval      vvarl1_vare = c1vare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   eval      jvarl1_vare = c1vare
     c     jvarl1_key    chain     jvarl1
     c                   if        %found
     c                   eval      c1tek1 = jvtek1
     c                   endif
     c                   else
     c                   eval      c1tek1 = vvtek1
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_alme
      *
      * Key for posisjonering på overgruppe
      *
     c     vbrkl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_alme
     c                   kfld                    vbrkl1_ogrp
     c                   kfld                    vbrkl1_hgrp
     c                   kfld                    vbrkl1_ugrp
     c                   kfld                    vbrkl1_modn
     c                   kfld                    vbrkl1_vare
      *
      * Key for posisjonering på hovedgruppe
      *
     c     vbrkl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_alme
     c                   kfld                    vbrkl2_hgrp
      *
      * Key for posisjonering på undergruppe
      *
     c     vbrkl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_alme
     c                   kfld                    vbrkl3_ugrp
      *
      * Key for posisjonering på modul
      *
     c     vbrkl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_alme
     c                   kfld                    vbrkl4_modn
      *
      * Key for posisjonering på vare
      *
     c     vbrkl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_alme
     c                   kfld                    vbrkl5_vare
      *
      * Key for oppslag på bruksrett-knytning
      *
     c     vbrklr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_alme
     c                   kfld                    vbrklr_ogrp
     c                   kfld                    vbrklr_hgrp
     c                   kfld                    vbrklr_ugrp
     c                   kfld                    vbrklr_modn
     c                   kfld                    vbrklr_vare
      *
      * Key for oppdatering av bruksrett-knytning
      *
     c     vbrklu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_alme
     c                   kfld                    vbrklu_ogrp
     c                   kfld                    vbrklu_hgrp
     c                   kfld                    vbrklu_ugrp
     c                   kfld                    vbrklu_modn
     c                   kfld                    vbrklu_vare
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
     c     vvarlb_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlb_nmod
      *
      * Key for oppslag på vare (VA)
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for oppslag på bruksrett-type
      *
     c     rmbtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_alme
     c                   kfld                    rmbtl1_tbrt
      *
      *
      * Henter verdier fra parameter, og initierer skjermbildet
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_alme = p_alme
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      vbrkl1_ogrp = 0
     c                   eval      vbrkl1_hgrp = 0
     c                   eval      vbrkl1_ugrp = 0
     c                   eval      vbrkl1_modn = 0
     c     vbrkl1_key    setll     vbrkl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
