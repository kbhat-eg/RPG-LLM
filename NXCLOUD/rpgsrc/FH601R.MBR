      /TITLE  ASOFAK: Kunde distr.liste, utskrift av navnetiketter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FH601R
      * Beskrivelse . . : Kunde distr.liste, utskrift av navnetiketter
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       281099 HKO  Nytt program
7.01  *k00  030620 BOF NXS-1195  kompilert pga tab.utvidelse
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffkdil3    if   e           k disk    rename(fkdipfr:fkdil3r)
     ffkdil4    if   e           k disk    rename(fkdipfr:fkdil4r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     fapospf    if   e           k disk    rename(apospfr:apospfr)
      *
     ffh601p    o    e             printer infds(d_infds)
      *
      * Array
      *
     d a_kund          s            100    dim(9) ctdata perrcd(1)               Array søketekst
     d a_navn          s            100    dim(9) ctdata perrcd(1)               Array søketekst
     d a_adr1          s            100    dim(9) ctdata perrcd(1)               Array søketekst
     d a_adr2          s            100    dim(9) ctdata perrcd(1)               Array søketekst
     d a_padr          s            100    dim(9) ctdata perrcd(1)               Array søketekst
      *
      * Definering av Local data area
      *
     d                uds
     d l_ruti                800    809
     d l_alin                856    858  0
     d l_wsid                901    910
     d l_user                911    920
      *
      * Definering av datastrukturer
      *
      * Datastruktur for parameterliste -inn
      *
     d                 ds
     d d_list                        16
     d  d_firm                        3  0 overlay(d_list)
     d  d_valg                        1    overlay(d_list:4)
     d  d_ante                        1  0 overlay(d_list:5)
     d  d_bred                        2  0 overlay(d_list:6)
     d  d_hoyd                        2  0 overlay(d_list:8)
      *
      * Informasjon fra printer-file
      *
     d d_infds         ds
     d  d_linnbr             367    368B 0
     d  d_pagnbr             369    372B 0
      *
      * Definering av parametre
      *
     d p_parm          s             16
      *
      * Definering av variabler i nøkler
      *
     d fkdil3_ftis     s                   like(fhftis)
     d fkdil4_btis     s                   like(fhbtis)
     d rkunl1_kund     s                   like(rkkund)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
     d apospf_ponr     s                   like(apponr)
      *
      * Definering av variable
      *
     d w_etik          s              1  0
     d w_line          s              2  0
     d w_kpro_alf      s              6
      *
     d w_firm          s                   like(fhfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Identifiserer siste prisfil- eller prisbok-forsendelse
      *
     c                   if        d_valg = 'F'
      *
     c     fkdil3_key1   setll     fkdil3
     c     fkdil3_key1   reade     fkdil3                                 90
     c                   if        *in90 or fhftis = *loval
     c                   goto      avslutt
     c                   endif
      *
     c                   else
      *
     c     fkdil4_key1   setll     fkdil4
     c     fkdil4_key1   reade     fkdil4                                 90
     c                   if        *in90 or fhbtis = *loval
     c                   goto      avslutt
     c                   endif
      *
     c                   endif
      *
      * Hode/sideskift
      *
     c                   write     a1hdr
      *
      * Leser kunder/lev.adresser i siste forsendelse
      *
     c                   if        d_valg = 'F'
      *
     c                   eval      fkdil3_ftis = fhftis
     c     fkdil3_key2   setll     fkdil3
     c     fkdil3_key2   reade     fkdil3                                 90
     c                   dow       *in90 = *off
     c                   exsr      behandling
     c     fkdil3_key2   reade     fkdil3                                 90
     c                   enddo
      *
     c                   else
      *
     c                   eval      fkdil4_btis = fhbtis
     c     fkdil4_key2   setll     fkdil4
     c     fkdil4_key2   reade     fkdil4                                 90
     c                   dow       *in90 = *off
     c                   exsr      behandling
     c     fkdil4_key2   reade     fkdil4                                 90
     c                   enddo
      *
     c                   endif
      *
      * Skriver detaljlinje
      *
     c                   exsr      detalj
      *
      * Avslutt
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av utskrift
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
      * Finner etikett-kolonne
      *
     c                   eval      w_etik = w_etik + 1
      *
      * Dersom alle etiketter i bredden er utfylt med navn og adresse,
      * skrives etikettraden
      *
     c                   if        w_etik > d_ante
     c                   exsr      detalj
     c                   eval      w_etik = 1
     c                   endif
      *
      * Fyller array med kunde/kundeprosjekt-informasjon
      *
     c                   movel     fhkund        a_kund(w_etik)
      *
     c                   if        fhkpro = 0
      *
     c                   eval      rkunl1_kund = fhkund
     c     rkunl1_key    chain     rkunl1                             91
     c                   if        *in91 = *off
     c                   eval      a_navn(w_etik) = rknavn
     c                   eval      a_adr1(w_etik) = rkgate
     c                   eval      a_padr(w_etik) = rksted
     c                   endif
      *
     c                   else
      *
     c                   move      fhkpro        w_kpro_alf
     c                   eval      a_kund(w_etik) = %trim(a_kund(w_etik)) +
     c                                              '/' + w_kpro_alf
      *
     c                   eval      fkprl1_kund = fhkund
     c                   eval      fkprl1_kpro = fhkpro
     c     fkprl1_key    chain     fkprl1                             91
     c                   if        *in91 = *off
     c                   eval      a_navn(w_etik) = flnavn
     c                   eval      a_adr1(w_etik) = fladr1
     c                   eval      a_adr2(w_etik) = fladr2
     c                   movel     flponr        a_padr(w_etik)
     c                   eval      apospf_ponr = flponr
     c     apospf_key    chain     apospf                             91
     c                   if        *in91 = *off
     c                   eval      a_padr(w_etik) = %trim(a_padr(w_etik)) +
     c                                              ' ' + apsted
     c                   endif
     c                   endif
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine utskrift av detaljlinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     detalj        begsr
      *
      * Leser array, og fyller etikettrad
      *
     c                   eval      d1kund = %subst(a_kund(1):1:d_bred) +
     c                                      %subst(a_kund(2):1:d_bred) +
     c                                      %subst(a_kund(3):1:d_bred) +
     c                                      %subst(a_kund(4):1:d_bred) +
     c                                      %subst(a_kund(5):1:d_bred) +
     c                                      %subst(a_kund(6):1:d_bred) +
     c                                      %subst(a_kund(7):1:d_bred) +
     c                                      %subst(a_kund(8):1:d_bred) +
     c                                      %subst(a_kund(9):1:d_bred)
      *
     c                   eval      d1navn = %subst(a_navn(1):1:d_bred) +
     c                                      %subst(a_navn(2):1:d_bred) +
     c                                      %subst(a_navn(3):1:d_bred) +
     c                                      %subst(a_navn(4):1:d_bred) +
     c                                      %subst(a_navn(5):1:d_bred) +
     c                                      %subst(a_navn(6):1:d_bred) +
     c                                      %subst(a_navn(7):1:d_bred) +
     c                                      %subst(a_navn(8):1:d_bred) +
     c                                      %subst(a_navn(9):1:d_bred)
      *
     c                   eval      d1adr1 = %subst(a_adr1(1):1:d_bred) +
     c                                      %subst(a_adr1(2):1:d_bred) +
     c                                      %subst(a_adr1(3):1:d_bred) +
     c                                      %subst(a_adr1(4):1:d_bred) +
     c                                      %subst(a_adr1(5):1:d_bred) +
     c                                      %subst(a_adr1(6):1:d_bred) +
     c                                      %subst(a_adr1(7):1:d_bred) +
     c                                      %subst(a_adr1(8):1:d_bred) +
     c                                      %subst(a_adr1(9):1:d_bred)
      *
     c                   eval      d1adr2 = %subst(a_adr2(1):1:d_bred) +
     c                                      %subst(a_adr2(2):1:d_bred) +
     c                                      %subst(a_adr2(3):1:d_bred) +
     c                                      %subst(a_adr2(4):1:d_bred) +
     c                                      %subst(a_adr2(5):1:d_bred) +
     c                                      %subst(a_adr2(6):1:d_bred) +
     c                                      %subst(a_adr2(7):1:d_bred) +
     c                                      %subst(a_adr2(8):1:d_bred) +
     c                                      %subst(a_adr2(9):1:d_bred)
      *
     c                   eval      d1padr = %subst(a_padr(1):1:d_bred) +
     c                                      %subst(a_padr(2):1:d_bred) +
     c                                      %subst(a_padr(3):1:d_bred) +
     c                                      %subst(a_padr(4):1:d_bred) +
     c                                      %subst(a_padr(5):1:d_bred) +
     c                                      %subst(a_padr(6):1:d_bred) +
     c                                      %subst(a_padr(7):1:d_bred) +
     c                                      %subst(a_padr(8):1:d_bred) +
     c                                      %subst(a_padr(9):1:d_bred)
      *
      * Skriver hode dersom det ikke er plass til ny etikettrad på siden.
      *
     c                   if        (d_linnbr + d_hoyd) > l_alin
     c                   write     a1hdr
     c                   endif
      *
      * Skriver etikettrad
      *
     c                   write     d1dtl                                30
      *
      * Skriver blanke linjer for å fylle opp etiketthøyden
      *
     c                   eval      w_line = 5
     c                   dow       w_line < d_hoyd
     c                   write     d2dtl                                30
     c                   eval      w_line = w_line + 1
     c                   enddo
      *
      * Tømmer array
      *
     c                   movea     *blank        a_kund(*)
     c                   movea     *blank        a_navn(*)
     c                   movea     *blank        a_adr1(*)
     c                   movea     *blank        a_adr2(*)
     c                   movea     *blank        a_padr(*)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_parm
      *
      * Key for les på distribusjonsregister - prisfile
      *
     c     fkdil3_key1   klist
     c                   kfld                    w_firm
      *
     c     fkdil3_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    fkdil3_ftis
      *
      * Key for les på distribusjonsregister - prisbok
      *
     c     fkdil4_key1   klist
     c                   kfld                    w_firm
      *
     c     fkdil4_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    fkdil4_btis
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      * Key for oppslag på postadresse-register
      *
     c     apospf_key    klist
     c                   kfld                    apospf_ponr
      *
      *
      * Splitter datastruktur i variable
      *
     c                   eval      d_list = p_parm
      *
     c                   eval      w_firm = d_firm
      *
     c                   endsr
