     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FP600R
      * Beskrivelse . . : Spesialpris, utskrift - parameter
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       120599 HKO  Nytt program
      *       180204 HKO  Utvidet med kundekategori
5.70  * PRGR  131108 bof  Utvidet med prisgruppe
7.xx  * 7.00  010416 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
8.01  *PRG2   100622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
5.70 fra30l1    if   e           k disk    rename(ra30pfr:ra30l1r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
      *
     ffp600d    cf   e             workstn
      *
      * Definering av LDA
      *
     d                uds
     d l_firm                944    946  0
      *
      * Datastruktur for parameterliste -ut
      *
     d                 ds
     d d_list                        64
     d  d_firm                        3  0 overlay(d_list: 1)
     d  d_rkaf                        3  0 overlay(d_list: 4)
     d  d_rkat                        3  0 overlay(d_list: 7)
     d  d_kkaf                        4  0 overlay(d_list:10)
     d  d_kkat                        4  0 overlay(d_list:14)
     d  d_kund                        6  0 overlay(d_list:18)
     d  d_kpro                        6  0 overlay(d_list:24)
     d  d_varf                       15    overlay(d_list:30)
     d  d_vart                       15    overlay(d_list:45)
8.01 d  d_prgr                        2    overlay(d_list:60)
     d  d_dumm                        1    overlay(d_list:64) inz('*')
      *
      * Definering av variable i nøkler
      *
     d rkunl1_kund     s                   like(rkkund)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
5.70 d ra30l1_dkod     s                   like(redkod)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
     d w_stmp          s               z
     d w_stmp_alf      s             26
     d w_memb          s             10
      *
     d w_vare          s             15
     d w_rtxt          s             30
     d w_ktxt          s             30
     d w_vtxt          s             35
      *
     d w_firm          s                   like(flfirm)
     d w_knav          s                   like(rknavn)
     d w_pnav          s                   like(flnavn)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Behandling av skjermbilde
      *
     c     b1taga        tag
      *
     c                   exfmt     b1bld
      *
      * F3=Avslutt
      *
     c                   if        *inkc or *inkl
     c                   goto      avslutt
     c                   endif
      *
      * F4=Spørring
      *
     c                   if        *inkd = *on
     c                   exsr      spØrring
     c                   goto      b1taga
     c                   endif
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1taga
     c                   endif
      *
     c                   exsr      flytt_par
      *
      * Kaller utskriftsprogram
      *
     c                   call      'FP600C'
     c                   parm                    d_list
     c                   parm                    w_memb
      *
     c                   goto      b1taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørringer (FD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
5.70  *
5.70  * Pris gruppe
5.70  *
5.70 c                   if        w_afld = 'B1PRGR'
5.70 c                   call      'RA530R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    b1prgr
5.70 c                   parm                    w_rtxt
5.70 c                   leavesr
5.70 c                   endif
      *
      * Rabattkategori
      *
     c                   if        w_afld = 'B1RKAF'
     c                   call      'RA506R'
     c                   parm                    w_firm
     c                   parm                    b1rkaf
     c                   parm                    w_rtxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'B1RKAT'
     c                   call      'RA506R'
     c                   parm                    w_firm
     c                   parm                    b1rkat
     c                   parm                    w_rtxt
     c                   leavesr
     c                   endif
      *
      * Kundekategori
      *
     c                   if        w_afld = 'B1KKAF'
     c                   call      'RA511R'
     c                   parm                    w_firm
     c                   parm                    b1kkaf
     c                   parm                    w_ktxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'B1KKAT'
     c                   call      'RA511R'
     c                   parm                    w_firm
     c                   parm                    b1kkat
     c                   parm                    w_ktxt
     c                   leavesr
     c                   endif
      *
      * Kunde
      *
     c                   if        w_afld = 'B1KUND'
     c                   eval      b1kpro = 0
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    b1kund
     c                   parm                    w_knav
     c                   leavesr
     c                   endif
      *
      * Kundeprosjekt
      *
     c                   if        w_afld = 'B1KPRO'
     c                   call      'FL500R'
     c                   parm                    w_firm
     c                   parm                    b1kund
     c                   parm                    b1kpro
     c                   parm                    w_knav
     c                   parm                    w_pnav
     c                   leavesr
     c                   endif
      *
      * Vare
      *
     c                   if        w_afld = 'B1VARF'
     c                   movel     b1varf        w_vare
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_vtxt
     c                   movel     w_vare        b1varf
     c                   leavesr
     c                   endif                                                           .....
      *
     c                   if        w_afld = 'B1VART'
     c                   movel     b1vart        w_vare
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_vtxt
     c                   movel     w_vare        b1vart
     c                   leavesr
     c                   endif                                                           .....
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
5.70  *
5.70  * Prisgruppe
5 70  *
5.70 c                   if        b1prgr <> *blank
5.70 c                   eval      ra30l1_dkod = b1prgr
5.70 c     ra30l1_key    chain     ra30l1
5.70 c                   if        not %found(ra30l1)
5.70 c                   eval      *in38 = *on
5.70 c                   eval      b_feil = *on
5.70 c                   leavesr
5.70 c                   endif
5.70 c                   endif
      *
      * Rabattkategori
      *
     c                   if        b1rkaf <> 0 and
     c                             b1rkat <> 0 and
     c                             b1rkaf > b1rkat
     c                   eval      *in31 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      * Kundekategori
      *
     c                   if        b1kkaf <> 0 and
     c                             b1kkat <> 0 and
     c                             b1kkaf > b1kkat
     c                   eval      *in32 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      * Rabattkategori / kundekategori / kunde
      *
     c                   if        b1kund <> 0
     c                   if        b1rkaf <> 0 or
     c                             b1rkat <> 0 or
     c                             b1kkaf <> 0 or
     c                             b1kkat <> 0
     c                   eval      *in33 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
      * Kunde
      *
     c                   if        b1kund <> 0
     c                   eval      rkunl1_kund = b1kund
     c     rkunl1_key    chain     rkunl1
     c                   if        not %found
     c                   eval      *in34 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
      * Kunde / kundeprosjekt
      *
     c                   if        b1kund = 0 and
     c                             b1kpro <> 0
     c                   eval      *in35 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   if        b1kpro <> 0
     c                   eval      fkprl1_kund = b1kund
     c                   eval      fkprl1_kpro = b1kpro
     c     fkprl1_key    chain     fkprl1
     c                   if        not %found
     c                   eval      *in36 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
      * Vare
      *
     c                   if        b1varf <> 0 and
     c                             b1vart <> 0 and
     c                             b1varf > b1vart
     c                   eval      *in37 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for flytting av felter til DS-parametre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     flytt_par     begsr
      *
     c                   eval      d_firm = w_firm
      *
     c                   eval      d_rkaf = b1rkaf
     c                   eval      d_kkaf = b1kkaf
     c                   eval      d_kund = b1kund
     c                   eval      d_kpro = b1kpro
      *
     c                   if        b1rkat = 0
     c                   eval      d_rkat = b1rkaf
     c                   else
     c                   eval      d_rkat = b1rkat
     c                   endif
      *
     c                   if        b1kkat = 0
     c                   eval      d_kkat = b1kkaf
     c                   else
     c                   eval      d_kkat = b1kkat
     c                   endif
      *
     c                   if        b1varf = 0
     c                   eval      d_varf = *blank
     c                   else
     c                   movel     b1varf        d_varf
     c                   endif
      *
     c                   if        b1vart = 0
     c                   eval      d_vart = d_varf
     c                   else
     c                   movel     b1vart        d_vart
     c                   endif
      *
      * Lager membernavn sammensatt av 'W' + timestamp
      *
     c                   time                    w_stmp
      *
     c                   move      w_stmp        w_stmp_alf
     c                   eval      w_memb = 'W' +
     c                                      %subst(w_stmp_alf:12:2) +
     c                                      %subst(w_stmp_alf:15:2) +
     c                                      %subst(w_stmp_alf:18:2) +
     c                                      %subst(w_stmp_alf:21:3)
      *
5.70 c                   eval      d_prgr = b1prgr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      * Key for oppslag på prisgruppe
      *
     c     ra30l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra30l1_dkod
      *
      *
      * Initiering av skjermbilde
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
