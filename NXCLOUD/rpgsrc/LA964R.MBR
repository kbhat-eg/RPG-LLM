     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LA964R
      *  Beskrivelse . . : Oppdatering av lager-register utfra lagerbok
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      *  6.10  260609 BSA   Oppdatert med sporingsinformasjon
6.11  *  6.10  160210 BSA   VHIS er utvidet med sekvensnummer
6.30  *        230318 NHO   Leser ikke siste post på forrige år
6.30  *                     dersom oppdater saldo pr 01.01
6.31  *        180119 NHO   La ut feil felt, skal bruk beholdning
      *  --------- ------  -------------------------------------------------
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.30 fvhisl1    if   e           k disk    rename(vhispfr:vhisl1r)
6.30 f                                     prefix(xx:2)
     fvhisl2    if   e           k disk    rename(vhispfr:vhisl2r)
     fvlaglu    uf   e           k disk    rename(vlagpfr:vlaglur)
      *
      * Parameter-inn
      *
     d                 ds
     d d_prec                        64
     d  d_val1                        1  0 overlay(d_prec)
     d  d_val2                        1  0 overlay(d_prec:2)
     d  d_val3                        1  0 overlay(d_prec:3)
     d  d_val4                        1  0 overlay(d_prec:4)
     d  d_dato                       10d   datfmt(*iso)
     d                                     overlay(d_prec:5)
      *
      * Local Data Area
      *
     d                uds
6.10 d l_user                911    920
     d l_firm                944    946  0
      *
      * Definere variable i nøkler
      *
     d vhisl2_dato     s                   like(vhdato)
     d vhisl2_lage     s                   like(vhlage)
     d vhisl2_time     s                   like(vhtime)
6.11 d vhisl2_sekv     s                   like(vhsekv)
     d vhisl2_vare     s                   like(vhvare)
6.30 d vhisl1_lage     s                   like(vhlage)
6.30 d vhisl1_vare     s                   like(vhvare)
6.30 d vhisl1_dato     s                   like(vhdato)
6.30 d vhisl1_time     s                   like(vhtime)
6.30 d vhisl1_sekv     s                   like(vhsekv)
     d vlaglu_lage     s                   like(vllage)
     d vlaglu_vare     s                   like(vlvare)
      *
      * Definere øvrige variable
      *
     d w_firm          s                   like(vhfirm)
     d w_parm          s             64
     d w_anta          s                   like(vhanta)
     d w_dato          s              6  0
     d w_time          s              6  0
6.30 d resdto          s              8  0
6.30 d resdt1          s              4  0
6.30 d resdt2          s              4  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Leser poster fra register for utplukks-test
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     vhisl2_key    setll     vhisl2r
      *
     c     les_lagbok    tag
      *
     c     nyles         tag
     c                   read      vhisl2r
      *
     c                   if        %eof or
     c                             vhfirm <> w_firm
     c                   goto      pgm_end
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Oppdaterer  LAGER-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      w_anta = 0
     c                   eval      vlaglu_vare = vhvare
     c                   eval      vlaglu_lage = vhlage
6.30 c*    vlaglu_key    chain     vlaglur
6.30 c     vlaglu_key    chain     vlaglur                            66
      *
      *
      *  Sjekk om telletrans - skal ikke oppdatere
      *
     c                   if        vhsyst = 'L' and
     c                             vhkode = 'T '
     c                   goto      les_lagbok
     c                   endif
      *
      *
     c                   eval      vlbeve = vlbeve + 1
      *
      *  Oppdaterer SALG    iår
      *
     c                   if        vhsyst = 'F'
     c                   if        vhtype = 'U'
     c                   eval      vlsalg = vlsalg + vhanta
     c                   eval      w_anta = w_anta + vhanta
     c                   else
     c                   eval      vlsalg = vlsalg - vhanta
     c                   eval      w_anta = w_anta - vhanta
     c                   endif
     c                   goto      oppd_lagbok
     c                   endif
      *
      *  Oppdaterer UTTAK iår
      *
     c                   if        vhsyst = 'L' and
     c                             vhkode = 'U '
     c                   if        vhtype = 'U'
     c                   eval      vlutta = vlutta + vhanta
     c                   eval      w_anta = w_anta + vhanta
     c                   else
     c                   eval      vlutta = vlutta - vhanta
     c                   eval      w_anta = w_anta - vhanta
     c                   endif
     c                   goto      oppd_lagbok
     c                   endif
      *
      *  Oppdaterer JUSTERING iår
      *
     c                   if        vhsyst = 'L' and
     c                             vhkode = 'J '
     c                   if        vhtype = 'U'
     c                   eval      vljust = vljust - vhanta
     c                   eval      w_anta = w_anta + vhanta
     c                   else
     c                   eval      vljust = vljust + vhanta
     c                   eval      w_anta = w_anta - vhanta
     c                   endif
     c                   goto      oppd_lagbok
     c                   endif
      *
      *  Oppdaterer INNGANG iår
      *
     c                   if        vhsyst = 'L'
     c                   if        vhtype = 'U'
     c                   eval      vlinng = vlinng - vhanta
     c                   eval      w_anta = w_anta + vhanta
     c                   else
     c                   eval      vlinng = vlinng + vhanta
     c                   eval      w_anta = w_anta - vhanta
     c                   endif
     c                   goto      oppd_lagbok
     c                   endif
      *
      *  Korrigere saldo pr. 01/01 felt? (Årsrutinen)
      *
     c     oppd_lagbok   tag
      *
     c                   if        d_val3 = 1
6.30 c**                 eval      vlb101 = vlb101 + w_anta
6.30  *  Sjekk om post er på forrige år
6.30 c                   movel     vhdato        resdto
6.30 c                   movel     resdto        resdt2
6.30 c                   eval      resdt1 = 2000
6.30 c                   move      uyear         resdt1
6.30 c                   if        resdt2 < resdt1
6.30 c                   goto      nyadd
6.30 c                   endif
6.30  *  Les siste post på forrige år
6.30 c                   eval      vhisl1_lage = vhlage
6.30 c                   eval      vhisl1_dato = vhdato
6.30 c                   eval      vhisl1_time = vhtime
6.30 c                   eval      vhisl1_sekv = vhsekv
6.30 c                   eval      vhisl1_vare = vhvare
6.30 c     vhisl1_key    setll     vhisl1r
6.30 c     nyles1        tag
6.30 c                   readp     vhisl1r
6.30 c                   movel     xxdato        resdto
6.30 c                   movel     resdto        resdt2
6.30 c                   eval      resdt1 = 2000
6.30 c                   move      uyear         resdt1
6.30 c                   if        resdt2 = resdt1
6.30 c                   goto      nyles1
6.30 c                   endif
6.30 c     nyadd         tag
6.30 c                   if        xxvare <> vhvare
6.30 c                   goto      etadd
6.30 c                   endif
      *
6.30 c***                eval      vlb101 = vlb101 + xxanta
6.30 c*** 6.31           eval      vlb101 = xxanta
6.31 c                   eval      vlb101 = xxbehl
6.30 c                   endif
6.30 c     etadd         tag
6.30  *
      *
     c                   if        *in66 = *off
6.10 c                   time                    vledat
6.10 c                   time                    vletim
6.10 c                   eval      vleusr = l_user
     c                   update    vlaglur
     c                   endif
      *
     c                   goto      les_lagbok
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     pgm_end       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for les av HISTORISK-LAGERBOK-REGISTER
      *
6.30  *  Key for oppslag på historisk lagerbok
6.30  *
6.30 c     vhisl1_key    klist
6.30 c                   kfld                    w_firm
6.30 c                   kfld                    vhisl1_lage
6.30 c                   kfld                    vhisl1_vare
6.30 c                   kfld                    vhisl1_dato
6.30 c                   kfld                    vhisl1_time
6.30 c                   kfld                    vhisl1_sekv
      *
     c     vhisl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhisl2_dato
     c                   kfld                    vhisl2_time
6.11 c                   kfld                    vhisl2_sekv
     c                   kfld                    vhisl2_lage
     c                   kfld                    vhisl2_vare
      *
      *  Key for oppdatering av LAGER-REGISTER
      *
     c     vlaglu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlaglu_vare
     c                   kfld                    vlaglu_lage
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    w_parm
     c                   eval      d_prec = w_parm
      *
      *  Legge inn startverdier
      *
     c                   eval      w_firm = l_firm
     c                   eval      vhisl2_dato = d_dato
     c                   eval      vhisl2_time = *loval
6.11 c                   eval      vhisl2_sekv = *zero
     c                   eval      vhisl2_vare = *blank
      *
     c                   endsr
