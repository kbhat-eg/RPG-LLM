     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASSTAT
      * Program . . . . : SF871R
      * Beskrivelse . . : Salgsstatistikk, samler grunnlag per-trans
      *
      *                   Leser på initielt årstall for å forhindre at
      *                   jobben skal ta for lang tid. Bør endres årlig.
      *
      *                   Programmet er en kopi av SF871R, men måtte lage
      *                   en versjon som ikke benytter SSTAPF/SFERAP siden
      *                   dette allerede er et felt som benyttes til andre
      *                   formål for Flisekompaniet og Hansmark.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       120721 hko  Nytt program (kopi av SF871R)
8.01  *       040821 hko  Utvidet med beskrivelse av overgruppe og
8.01  *                   hovedgruppe
8.01  *       051021 hko  Kopi av SF872R fra Flisekompaniet.
8.01  *                   Utvidet med kundeprosjekt og ordre.
8.01  *                   Sletter innleste ordre ved innlesing.
      *
8.02  *       021121 mst  Utelater koder i tekstfelt
8.01  *PRG2   220622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      * - - - - - - --- - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsstal1    if   e           k disk    rename(sstapfr:sstal1r)
      *
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
      *
     fsos1l1    if   e           k disk    rename(sos1pfr:sos1l1r)
     f                                     prefix(x:1)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     fra07l1    if   e           k disk    rename(ra07pfr:ra07l1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     fra11l1    if   e           k disk    rename(ra11pfr:ra11l1r)
     fra06l1    if   e           k disk    rename(ra06pfr:ra06l1r)
     fra09l1    if   e           k disk    rename(ra09pfr:ra09l1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
8.01 fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
8.01 fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     fjmodl1    if   e           k disk    rename(jmodpfr:jmodl1r)
      *
     fsos1l2    uf   e           k disk    rename(sos1pfr:sos1l2r)
     f                                     prefix(x:1)
     fsos1pf    uf a e           k disk    rename(sos1pfr:sos1pfr)
      *
      * Definering av parametre
      *
     d p_fgrp          s              3
     d p_firm          s              3
      *
8.01  * Definering av datastruktur for varegruppe
8.01  *
8.01 d                 ds
8.01 d d_vgrp                         7
8.01 d  d_ogrp                        2  0 overlay(d_vgrp:1)
8.01 d  d_hgrp                        2  0 overlay(d_vgrp:3)
8.01 d  d_ugrp                        3  0 overlay(d_vgrp:5)
      *
      * Definering av variable i nøkler
      *
     d sstal1_paar     s                   like(sfpaar)
6.14 d fodtl1_numm     s                   like(fdnumm)
6.14 d fodtl1_suff     s                   like(fdsuff)
     d sos1l1_fgrp     s                   like(xafgrp)
     d sos1l1_paar     s                   like(x1paar)
     d sos1l1_pmnd     s                   like(x1pmnd)
     d sos1l1_savd     s                   like(x1savd)
     d sos1l1_binr     s                   like(x1binr)
     d sos1l1_line     s                   like(x1line)
     d sos1l1_bida     s                   like(x1bida)
     d sos1l1_ornr     s                   like(x1ornr)
     d sos1l1_orsu     s                   like(x1orsu)
     d sos1l1_orli     s                   like(x1orli)
     d sos1l1_orda     s                   like(x1orda)
     d sos1l1_bong     s                   like(x1bong)
     d sos1l2_fgrp     s                   like(xafgrp)
     d sos1l2_otyp     s                   like(x1otyp)
     d ra07l1_gkod     s                   like(ragkod)
     d rkunl1_kund     s                   like(rkkund)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkund)
     d ra11l1_kkod     s                   like(rakkod)
     d ra06l1_fkod     s                   like(rafkod)
     d ra09l1_ikod     s                   like(raikod)
     d rlevl1_levr     s                   like(rllevr)
8.01 d vogrl1_oogr     s                   like(vgoogr)
8.01 d vhgrl1_hogr     s                   like(vghogr)
8.01 d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d vlagl1_vare     s                   like(vlvare)
     d vlagl1_lage     s                   like(vllage)
     d jmodl1_modn     s                   like(jumodn)
      *
      * Definering av variable
      *
     d b_inlr          s              1    inz(*off)
      *
     d w_firm          s                   like(sffirm)
      *
     d w_time          s               t   timfmt(*iso)
     d w_ftid          s               t   timfmt(*iso)
     d w_ttid          s               t   timfmt(*iso)
      *
     d w_bupr          s              9  2
     d w_latx          s             20
     d w_adrs          s             30
     d w_ponr          s              4  0
     d w_pste          s             30
      *
     d b_feil          s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   clear                   sos1pfr
      *
      * Henter firma-informasjon
      *
     c                   eval      aafgrp = p_fgrp
      *
     c     afirl1_key    chain     afirl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
      * Sletter innleste ordre (ordretype O, RO, P og PN)
      *
     c                   eval      sos1l2_fgrp = aafgrp
     c                   eval      sos1l2_otyp = 'O'
     c     sos1l2_key    setll     sos1l2
     c     sos1l2_key    reade     sos1l2r
     c                   dow       not %eof
     c                   delete    sos1l2r
     c     sos1l2_key    reade     sos1l2r
     c                   enddo
      *
     c                   eval      sos1l2_otyp = 'RO'
     c     sos1l2_key    setll     sos1l2
     c     sos1l2_key    reade     sos1l2r
     c                   dow       not %eof
     c                   delete    sos1l2r
     c     sos1l2_key    reade     sos1l2r
     c                   enddo
      *
     c                   eval      sos1l2_otyp = 'P'
     c     sos1l2_key    setll     sos1l2
     c     sos1l2_key    reade     sos1l2r
     c                   dow       not %eof
     c                   delete    sos1l2r
     c     sos1l2_key    reade     sos1l2r
     c                   enddo
      *
     c                   eval      sos1l2_otyp = 'PN'
     c     sos1l2_key    setll     sos1l2
     c     sos1l2_key    reade     sos1l2r
     c                   dow       not %eof
     c                   delete    sos1l2r
     c     sos1l2_key    reade     sos1l2r
     c                   enddo
      *
      * Leser salgsstatistikk
      *
     c                   eval      sstal1_paar = 2020
     c     sstal1_key    setll     sstal1
     c     sstal1_key2   reade     sstal1
     c                   dow       not %eof
      *
     c                   exsr      oppdat_stat
      *
     c     sstal1_key2   reade     sstal1
     c                   enddo
      *
      * Leser ordre
      *
     c     fohel1_key    setll     fohel1
     c     fohel1_key    reade     fohel1
     c                   dow       not %eof
      *
     c                   if        footyp = 'O' or
     c                             footyp = 'RO' or
     c                             footyp = 'P'  or
     c                             footyp = 'PN'
     c                   eval      fodtl1_numm = fonumm
     c                   eval      fodtl1_suff = fosuff
     c     fodtl1_key    setll     fodtl1
     c     fodtl1_key    reade     fodtl1
     c                   dow       not %eof
     c                   exsr      oppdat_ordre
     c     fodtl1_key    reade     fodtl1
     c                   enddo
     c                   endif
      *
     c     fohel1_key    reade     fohel1
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting av aggregert statistikkfil
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdat_stat   begsr
      *
      * Avbryter dersom statistikktrans er rapportert tidligere
      *
     c                   eval      sos1l1_fgrp = aafgrp
     c                   eval      sos1l1_paar = sfpaar
     c                   eval      sos1l1_pmnd = sfpmnd
     c                   eval      sos1l1_savd = sfsavd
     c                   eval      sos1l1_binr = sfbinr
     c                   eval      sos1l1_line = sfline
     c                   eval      sos1l1_bida = sfbida
     c                   eval      sos1l1_ornr = sfornr
     c                   eval      sos1l1_orsu = sforsu
     c                   eval      sos1l1_orli = sforli
     c                   eval      sos1l1_orda = sforda
     c                   eval      sos1l1_bong = sfbong
     c     sos1l1_key    chain     sos1l1
     c                   if        %found
     c                   leavesr
     c                   endif
      *
     c                   eval      ragtxt = *blank
      *
     c                   eval      ra07l1_gkod = sfsavd
     c     ra07l1_key    chain     ra07l1
      *
     c                   eval      rknavn = *blank
     c                   eval      rkftnr = 0
     c                   eval      rkslgr = 0
     c                   eval      s1kans = *blank
      *
     c                   eval      rkunl1_kund = sfvkun
     c     rkunl1_key    chain     rkunl1
     c                   if        %found and
     c                             rkslgr <> 0
     c                   eval      ra09l1_ikod = rkslgr
     c     ra09l1_key    chain     ra09l1
     c                   if        %found
     c                   eval      s1kans = raitxt
     c                   endif
     c                   endif
      *
     c                   eval      flnavn = *blank
      *
     c                   eval      fkprl1_kund = sfvkun
     c                   eval      fkprl1_kpro = sfkpro
     c     fkprl1_key    chain     fkprl1
      *
     c                   eval      raktxt = *blank
      *
     c                   eval      ra11l1_kkod = sfkkat
     c     ra11l1_key    chain     ra11l1
      *
     c                   eval      raftxt = *blank
      *
     c                   eval      ra06l1_fkod = sfrkat
     c     ra06l1_key    chain     ra06l1
      *
     c                   eval      raitxt = *blank
      *
     c                   eval      ra09l1_ikod = sfselg
     c     ra09l1_key    chain     ra09l1
      *
     c                   eval      rlnavn = *blank
      *
     c                   eval      rlevl1_levr = sfldor
     c     rlevl1_key    chain     rlevl1
      *
8.01 c***                eval      vgutxt = *blank
8.01 c                   eval      d_ogrp = sfogrp
8.01 c                   eval      d_hgrp = sfhgrp
8.01 c                   eval      d_ugrp = sfugrp
8.02 c*                  eval     s1otxt = %subst(d_vgrp:1:2) + ' -'
8.02 c*                  eval      s1htxt = %subst(d_vgrp:1:4) + ' -'
8.02 c*                  eval      s1utxt = d_vgrp + ' -'
      *
8.01 c                   eval      vogrl1_oogr = sfogrp
8.01 c     vogrl1_key    chain     vogrl1
8.01 c                   if        %found
8.01 c                   eval      s1otxt = %trim(s1otxt) + ' ' +
8.01 c                                      %trim(vgotxt)
8.01 c
8.01 c                   endif
      *
8.01 c                   eval      vhgrl1_hogr = sfogrp
8.01 c                   eval      vhgrl1_hhgr = sfhgrp
8.01 c     vhgrl1_key    chain     vhgrl1
8.01 c                   if        %found
8.01 c                   eval      s1htxt = %trim(s1htxt) + ' ' +
8.01 c                                      %trim(vghtxt)
8.01 c
8.01 c                   endif
      *
     c                   eval      vugrl1_uogr = sfogrp
     c                   eval      vugrl1_uhgr = sfhgrp
     c                   eval      vugrl1_uugr = sfugrp
     c     vugrl1_key    chain     vugrl1
8.01 c                   if        %found
8.01 c                   eval      s1utxt = %trim(s1utxt) + ' ' +
8.01 c                                      %trim(vgutxt)
8.01 c                   endif
      *
     c                   eval      jumte1 = *blank
      *
     c                   eval      jmodl1_modn = sfmodn
     c     jmodl1_key    chain     jmodl1
      *
     c                   eval      s1paar = sfpaar
     c                   eval      s1pmnd = sfpmnd
     c                   eval      s1savd = sfsavd
     c                   eval      s1vkun = sfvkun
     c                   eval      s1kpro = sfkpro
     c                   eval      s1kkat = sfkkat
     c                   eval      s1rkat = sfrkat
     c                   eval      s1selg = sfselg
      *
     c                   eval      s1binr = sfbinr
     c                   eval      s1line = sfline
     c                   eval      s1bida = sfbida
     c                   eval      s1otyp = sfotyp
     c                   eval      s1ornr = sfornr
     c                   eval      s1orsu = sforsu
     c                   eval      s1orli = sforli
     c                   eval      s1orda = sforda
     c                   eval      s1bong = sfbong
     c                   eval      s1benr = sfbenr
     c                   eval      s1bsuf = sfbsuf
     c                   eval      s1blin = sfblin
      *
     c                   eval      s1vare = sfvare
     c                   eval      s1nobb = sfnobb
     c                   eval      s1tek1 = sftek1
     c                   eval      s1tek2 = sftek2
     c                   eval      s1ogrp = sfogrp
     c                   eval      s1hgrp = sfhgrp
     c                   eval      s1ugrp = sfugrp
     c                   eval      s1vgrp = (sfogrp*100000)+(sfhgrp*1000)+sfugrp
     c                   eval      s1modn = sfmodn
     c                   eval      s1vtyp = sfvtyp
     c                   eval      s1kamp = sfkamp
     c                   eval      s1kpri = sfkpri
     c                   eval      s1lvre = sflvre
     c                   eval      s1ldor = sfldor
     c                   eval      s1lvar = sflvar
      *
     c                   eval      s1enhe = sfenhe
     c                   eval      s1anta = sfanta
     c                   eval      s1prgr = sfprgr
     c                   eval      s1lety = sflety
     c                   eval      s1sumb = sfsumb
     c                   eval      s1rab1 = sfrab1
     c                   eval      s1rab2 = sfrab2
     c                   eval      s1rabo = sfrabo
     c                   eval      s1sumn = sfsumb - sfrab1 - sfrab2 - sfrabo
     c                   eval      s1sumk = sfsumk
     c                   eval      s1dekb = s1sumn - sfsumk
      *
     c                   eval      s1dekg = 0
      *
     c                   if        s1sumn <> 0
     c                   select
     c                   when      (100 - (s1sumk * 100 / s1sumn)) < -999,99
     c                   eval      s1dekg = -999,99
     c                   when      (100 - (s1sumk * 100 / s1sumn)) > 999,99
     c                   eval      s1dekg = 999,99
     c                   other
     c                   eval(h)   s1dekg = 100 - (s1sumk * 100 / s1sumn)
     c                   endsl
     c                   endif
      *
      * Henter prisinformasjon i salgsøyeblikket
      *
     c                   eval      b_inlr = *on
     c                   time                    w_time
      *
     c                   call      'VP704R'
     c                   parm                    w_firm
     c                   parm                    s1ldor
     c                   parm                    s1vare
     c                   parm                    s1enhe
     c                   parm                    s1prgr
     c                   parm                    s1lety
     c                   parm                    s1orda
     c                   parm                    w_time
     c                   parm                    s1sapr
     c                   parm                    w_bupr
     c                   parm                    s1kopr
     c                   parm                    s1inpr
     c                   parm                    s1pdat
     c                   parm                    s1kfda
     c                   parm                    w_ftid
     c                   parm                    s1ktda
     c                   parm                    w_ttid
     c                   parm                    s1gkam
     c                   parm                    s1ksap
     c                   parm                    s1kkop
     c                   parm                    s1kinp
     c                   parm                    b_inlr
      *
     c                   if        s1kopr = 0 and
     c                             s1inpr <> 0
     c                   eval      s1kopr = s1inpr
     c                   endif
      *
     c                   if        s1kkop = 0 and
     c                             s1kinp <> 0
     c                   eval      s1kkop = s1kinp
     c                   endif
      *
     c                   write     sos1pfr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting av aggregert statistikkfil - fra ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdat_ordre  begsr
      *
     c                   if        fdvare = *blank
     c                   leavesr
     c                   endif
      *
6.14  * Dersom lager er overstyrt på linjenivå hentes avdeling fra linje
6.14  *
6.14 c                   eval      s1savd = foavde
6.14  *
6.14 c                   if        fdlage <> folage
6.14 c                   eval      b_feil = *off
6.14 c                   call      'FÅ720R'
6.14 c                   parm                    w_firm
6.14 c                   parm                    fdlage
6.14 c                   parm                    w_latx
6.14 c                   parm                    w_adrs
6.14 c                   parm                    w_ponr
6.14 c                   parm                    w_pste
6.14 c                   parm                    s1savd
6.14 c                   parm                    b_feil
6.14 c                   endif
      *
     c                   eval      ragtxt = *blank
      *
     c                   eval      ra07l1_gkod = s1savd
     c     ra07l1_key    chain     ra07l1
      *
     c                   eval      rknavn = *blank
     c                   eval      rkftnr = 0
     c                   eval      rkslgr = 0
     c                   eval      s1kans = *blank
      *
     c                   eval      rkunl1_kund = fokund
     c     rkunl1_key    chain     rkunl1
     c                   if        %found and
     c                             rkslgr <> 0
     c                   eval      ra09l1_ikod = rkslgr
     c     ra09l1_key    chain     ra09l1
     c                   if        %found
     c                   eval      s1kans = raitxt
     c                   endif
     c                   endif
      *
     c                   eval      flnavn = *blank
      *
     c                   eval      fkprl1_kund = fokund
     c                   eval      fkprl1_kpro = fokpro
     c     fkprl1_key    chain     fkprl1
      *
     c                   eval      raktxt = *blank
      *
     c                   eval      ra11l1_kkod = rkkatg
     c     ra11l1_key    chain     ra11l1
      *
     c                   eval      raftxt = *blank
      *
     c                   eval      ra06l1_fkod = forkat
     c     ra06l1_key    chain     ra06l1
      *
     c                   eval      raitxt = *blank
      *
     c                   eval      ra09l1_ikod = foselg
     c     ra09l1_key    chain     ra09l1
      *
     c                   eval      rlnavn = *blank
      *
     c                   eval      rlevl1_levr = fdldor
     c     rlevl1_key    chain     rlevl1
      *
8.01 c                   eval      d_ogrp = fdogrp
8.01 c                   eval      d_hgrp = fdhgrp
8.01 c                   eval      d_ugrp = fdugrp
8.02 c*                  eval      s1otxt = %subst(d_vgrp:1:2) + ' -'
8.02 c*                  eval      s1htxt = %subst(d_vgrp:1:4) + ' -'
8.02 c*                  eval      s1utxt = d_vgrp + ' -'
      *
8.01 c                   eval      vogrl1_oogr = fdogrp
8.01 c     vogrl1_key    chain     vogrl1
8.01 c                   if        %found
8.01 c                   eval      s1otxt = %trim(s1otxt) + ' ' +
8.01 c                                      %trim(vgotxt)
8.01 c
8.01 c                   endif
      *
8.01 c                   eval      vhgrl1_hogr = fdogrp
8.01 c                   eval      vhgrl1_hhgr = fdhgrp
8.01 c     vhgrl1_key    chain     vhgrl1
8.01 c                   if        %found
8.01 c                   eval      s1htxt = %trim(s1htxt) + ' ' +
8.01 c                                      %trim(vghtxt)
8.01 c
8.01 c                   endif
      *
     c                   eval      vugrl1_uogr = fdogrp
     c                   eval      vugrl1_uhgr = fdhgrp
     c                   eval      vugrl1_uugr = fdugrp
     c     vugrl1_key    chain     vugrl1
8.01 c                   if        %found
8.01 c                   eval      s1utxt = %trim(s1utxt) + ' ' +
8.01 c                                      %trim(vgutxt)
8.01 c                   endif
      *
     c                   eval      jumte1 = *blank
      *
     c***                eval      jmodl1_modn = fdmodn
     c***  jmodl1_key    chain     jmodl1
      *
     c                   eval      s1paar = 0
     c                   eval      s1pmnd = 0
     c***                eval      s1savd = sfsavd
     c                   eval      s1vkun = fokund
     c                   eval      s1kpro = fokpro
     c                   eval      s1kkat = rkkatg
     c                   eval      s1rkat = forkat
     c                   eval      s1selg = foselg
      *
     c                   eval      s1binr = 0
     c                   eval      s1line = 0
     c                   eval      s1bida = *loval
     c                   eval      s1otyp = footyp
     c                   eval      s1ornr = fonumm
     c                   eval      s1orsu = fosuff
     c                   eval      s1orli = foline
     c                   eval      s1orda = fobdat
     c                   eval      s1bong = *blank
     c                   eval      s1benr = fdbenr
     c                   eval      s1bsuf = fdbsuf
     c                   eval      s1blin = fdblin
      *
     c                   eval      s1vare = fdvare
     c                   eval      s1nobb = fdnobb
     c                   eval      s1tek1 = fdtek1
     c                   eval      s1tek2 = fdtek2
     c                   eval      s1ogrp = fdogrp
     c                   eval      s1hgrp = fdhgrp
     c                   eval      s1ugrp = fdugrp
     c                   eval      s1vgrp = (fdogrp*100000)+(fdhgrp*1000)+fdugrp
     c                   eval      s1modn = 0
     c                   eval      s1vtyp = *blank
     c                   eval      s1kamp = fdkamp
     c                   eval      s1kpri = fdkpri
     c                   eval      s1lvre = fdlvre
     c                   eval      s1ldor = fdldor
     c                   eval      s1lvar = fdlvar
      *
     c                   if        s1lvre = 1
     c                   eval      s1vtyp = 'S'
     c                   else
     c                   eval      vlagl1_vare = fdvare
     c                   eval      vlagl1_lage = fdlage
     c     vlagl1_key    chain     vlagl1
     c                   if        %found
     c                   eval      s1vtyp = vlvtyp
     c                   endif
     c                   endif
      *
     c                   eval      s1enhe = fdenh1
     c                   eval      s1anta = fdanta
     c                   eval      s1prgr = fdprgr
     c                   eval      s1lety = fdlety
     c                   eval      s1sumb = fdsums
     c                   eval      s1rab1 = fdsumr
     c                   eval      s1rab2 = 0
     c                   eval      s1rabo = 0
     c                   eval      s1sumn = fdsums - fdsumr
     c                   eval      s1sumk = fdsumk
     c                   eval      s1dekb = s1sumn - fdsumk
      *
     c                   eval      s1dekg = 0
      *
     c                   if        s1sumn <> 0
     c                   select
     c                   when      (100 - (s1sumk * 100 / s1sumn)) < -999,99
     c                   eval      s1dekg = -999,99
     c                   when      (100 - (s1sumk * 100 / s1sumn)) > 999,99
     c                   eval      s1dekg = 999,99
     c                   other
     c                   eval(h)   s1dekg = 100 - (s1sumk * 100 / s1sumn)
     c                   endsl
     c                   endif
      *
     c                   if        footyp = 'RO' or
     c                             footyp = 'PN'
     c                   eval      s1anta = s1anta * -1
     c                   eval      s1sumb = s1sumb * -1
     c                   eval      s1rab1 = s1rab1 * -1
     c                   eval      s1rab2 = s1rab2 * -1
     c                   eval      s1sumn = s1sumn * -1
     c                   eval      s1sumk = s1sumk * -1
     c                   eval      s1dekb = s1dekb * -1
     c                   endif
      *
      * Henter prisinformasjon i salgsøyeblikket
      *
     c                   eval      b_inlr = *on
     c                   time                    w_time
      *
     c                   call      'VP704R'
     c                   parm                    w_firm
     c                   parm                    s1ldor
     c                   parm                    s1vare
     c                   parm                    s1enhe
     c                   parm                    s1prgr
     c                   parm                    s1lety
     c                   parm                    s1orda
     c                   parm                    w_time
     c                   parm                    s1sapr
     c                   parm                    w_bupr
     c                   parm                    s1kopr
     c                   parm                    s1inpr
     c                   parm                    s1pdat
     c                   parm                    s1kfda
     c                   parm                    w_ftid
     c                   parm                    s1ktda
     c                   parm                    w_ttid
     c                   parm                    s1gkam
     c                   parm                    s1ksap
     c                   parm                    s1kkop
     c                   parm                    s1kinp
     c                   parm                    b_inlr
      *
     c                   if        s1kopr = 0 and
     c                             s1inpr <> 0
     c                   eval      s1kopr = s1inpr
     c                   endif
      *
     c                   if        s1kkop = 0 and
     c                             s1kinp <> 0
     c                   eval      s1kkop = s1kinp
     c                   endif
      *
     c                   write     sos1pfr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_fgrp
     c                   parm                    p_firm
      *
      * Key for les på salgsstatistikk
      *
     c     sstal1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sstal1_paar
      *
     c     sstal1_key2   klist
     c                   kfld                    w_firm
      *
      * Key for les på ordre-hode
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
      *
      * Key for les på ordre-linje
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
6.13 c                   kfld                    fodtl1_numm
6.13 c                   kfld                    fodtl1_suff
      *
      * Key for oppslag på innrapportert salgsstatistikk
      *
     c     sos1l1_key    klist
     c                   kfld                    sos1l1_fgrp
     c                   kfld                    sos1l1_paar
     c                   kfld                    sos1l1_pmnd
     c                   kfld                    sos1l1_savd
     c                   kfld                    sos1l1_binr
     c                   kfld                    sos1l1_line
     c                   kfld                    sos1l1_bida
     c                   kfld                    sos1l1_ornr
     c                   kfld                    sos1l1_orsu
     c                   kfld                    sos1l1_orli
     c                   kfld                    sos1l1_orda
     c                   kfld                    sos1l1_bong
      *
      * Key for oppslag på innrapportert salgsstatistikk
      *
     c     sos1l2_key    klist
     c                   kfld                    sos1l2_fgrp
     c                   kfld                    sos1l2_otyp
      *
      * Key for oppslag på firma
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på avdeling
      *
     c     ra07l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra07l1_gkod
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      * Key for oppslag på kundekategori
      *
     c     ra11l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra11l1_kkod
      *
      * Key for oppslag på rabattkategori
      *
     c     ra06l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra06l1_fkod
      *
      * Key for oppslag på selger
      *
     c     ra09l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra09l1_ikod
      *
      * Key oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
8.01  * Key oppslag på overgryppe
8.01  *
8.01 c     vogrl1_key    klist
8.01 c                   kfld                    w_firm
8.01 c                   kfld                    vogrl1_oogr
8.01  *
8.01  * Key oppslag på hovedgruppe
8.01  *
8.01 c     vhgrl1_key    klist
8.01 c                   kfld                    w_firm
8.01 c                   kfld                    vhgrl1_hogr
8.01 c                   kfld                    vhgrl1_hhgr
      *
      * Key oppslag på varegruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key oppslag på vare-lager
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl1_vare
     c                   kfld                    vlagl1_lage
      *
      * Key oppslag på modul
      *
     c     jmodl1_key    klist
     c                   kfld                    jmodl1_modn
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = %dec(p_firm:3:0)
      *
     c                   endsr
