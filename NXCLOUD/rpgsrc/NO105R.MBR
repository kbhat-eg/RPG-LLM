     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : NO105R
      * Beskrivelse . . : Ekstern ordre-hode, hent feilstatus
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       081002 HKO  Nytt program
5.60  *       210410 bof  Tester på om gyldig lager også ved lager 0
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvltpl1    if   e           k disk    rename(vltppfr:vltpl1r)
     fausrl1    if   e           k disk    rename(ausrpfr:ausrl1r)
     ffusrl1    if   e           k disk    rename(fusrpfr:fusrl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:fkunl1r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vyfirm)
     d p_stat          s              1  0
     d p_lage          s              2  0
     d p_user          s                   like(fbuser)
     d p_kund          s                   like(rkkund)
     d p_kpro          s                   like(flkpro)
     d p_lety          s                   like(vylety)
     d p_ldat          s               d   datfmt(*iso)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d vltpl1_lety     s                   like(vylety)
     d ausrl1_user     s                   like(abuser)
     d fusrl1_user     s                   like(fbuser)
     d rkunl1_kund     s                   like(rkkund)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
      *
      * Definering av variable
      *
     d w_stat          s              1
     d w_ltxt          s             20
     d w_ldat          s               d   datfmt(*iso)
     d w_udat          s               d   datfmt(*iso)
      *
     d w_firm          s                   like(vyfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      p_stat = 0
      *
      * Lager
      *
5.60 c*                  if        p_lage <> 0
     c                   call      'RS710R'
     c                   parm                    w_stat
     c                   parm                    p_lage
     c                   parm                    w_ltxt
     c                   if        w_stat <> *blank
     c                   eval      p_stat = 1
     c                   goto      avslutt
     c                   endif
5.60 c*                  endif
      *
      * Bruker-id
      *
     c                   if        p_user <> *blank
      *
     c                   eval      ausrl1_user = p_user
     c     ausrl1_key    chain     ausrl1
     c                   if        not %found
     c                   eval      p_stat = 2
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      fusrl1_user = p_user
     c     fusrl1_key    chain     fusrl1
     c                   if        not %found or
     c                             fbko10 = 0
     c                   eval      p_stat = 2
     c                   goto      avslutt
     c                   endif
      *
     c                   endif
      *
      * Kunde
      *
     c                   if        p_kund = 0
     c                   eval      p_stat = 3
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      rkunl1_kund = p_kund
     c     rkunl1_key    chain     rkunl1
     c                   if        not %found
     c                   eval      p_stat = 3
     c                   goto      avslutt
     c                   endif
      *
      * Kundeprosjekt
      *
     c                   if        p_kpro <> 0
     c                   eval      fkprl1_kund = p_kund
     c                   eval      fkprl1_kpro = p_kpro
     c     fkprl1_key    chain     fkprl1
     c                   if        not %found
     c                   eval      p_stat = 4
     c                   goto      avslutt
     c                   endif
     c                   endif
      *
      * Leveringstype
      *
     c                   if        p_lety <> 0
     c                   eval      vltpl1_lety = p_lety
     c     vltpl1_key    chain     vltpl1
     c                   if        not %found
     c                   eval      p_stat = 5
     c                   goto      avslutt
     c                   endif
     c                   endif
      *
      * Leveringsdato
      *
     c                   if        p_ldat <> *loval
     c                   time                    w_udat
     c                   if        p_ldat < w_udat
     c                   eval      p_stat = 6
     c                   goto      avslutt
     c                   endif
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_stat
     c                   parm                    p_lage
     c                   parm                    p_user
     c                   parm                    p_kund
     c                   parm                    p_kpro
     c                   parm                    p_lety
     c                   parm                    p_ldat
      *
      * Key for oppslag på leveringstype
      *
     c     vltpl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltpl1_lety
      *
      * Key for oppslag på bruker
      *
     c     ausrl1_key    klist
     c                   kfld                    ausrl1_user
      *
      * Key for oppslag på ofak-bruker
      *
     c     fusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fusrl1_user
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
