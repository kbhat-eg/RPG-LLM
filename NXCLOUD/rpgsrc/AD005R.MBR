     H/TITLE  Kontroll av gyldig ordrenr
     H
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * Program . . . . : AD005R                                        *
      * Beskrivelse . . : Kontroll av tilgang til rutien, valg, funksjon*
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
      *           061002  Nytt program                             SS.  *
9.01  *           180921  Justert vedr hente parameter tilbake     ss.  *
9.02  *           191004  Newlook liker ikke selvlukkende vindu    bk.  *
9.03  *           291019  justering av valg                             *
      *                                                                 *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *    60-61 = Fileindikatorer chain etc.                           *
      *                                                                 *
      *  BRUK AV RETURKODE                                              *
      *                                                                 *
      *  N = Ordrenr er ikke i registeret                               *
      *  A = Ordre er sluttmeldt                                        *
      *  F = Ordre er under sluttfakturering                            *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Rutine uderlagt tilgangskontroll
     fiadrl1    if   e           k disk
      * Tilagn pr rutine og tilganskode
     fiadtl1    if   e           k disk
      *
      *
      * Definering av variabler i nøkler
      *
     d iadrl1_frm      s                   like(iadfrm)
     d iadrl1_rut      s                   like(iadrut)
     d iadtl1_frm      s                   like(iadtfr)
     d iadtl1_kod      s                   like(iadtko)
     d iadtl1_rut      s                   like(iadtru)
      *
      * Definering av variabler i parameter
      *
     d p_tilgko        s                   like(iadtko)
     d p_rutine        s                   like(iadtru)
     d p_valgfu        s                   like(iadtv1)
      *  p_status fra callende progr betyr 'ikke send feilmedling'
     d p_status        s             10
      *
     d w_v             s              3
     d i               s              3  0
      *
     D p_feilm         s              3    inz('   ')
      *
     D w_ali           s              2  0
     D w_apo           s              2  0
     D w_m1            s             40
     D w_m2            s             40
     D w_m3            s             40
     D w_m4            s             40
     D w_secx          s              3
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *  Hvis ikke rutiona er definert har alle tilgang
      *
     c                   eval      iadrl1_frm = *zero
     c                   eval      iadrl1_rut = p_rutine
     c     iadrl1_key    chain     iadrl1
     c                   if        not %found(iadrl1)
     c                   goto      tilgok
     c                   endif
      *
      *  Hvis ikke bruker via tilgagnkode er definert har den ikke tilgang
      *
     c                   if        p_tilgko = *blank
     c                   goto      tilgav
     c                   endif
      *
      * Finn tilgang for denne Tilgangskode og Rutine
      *
     c                   eval      iadtl1_kod = p_tilgko
     c                   eval      iadtl1_rut = p_rutine
     c                   eval      i = 10
     c     next          tag
     c     iadtl1_key    chain     iadtl1
     c                   if        not %found(iadtl1) and
     c                             i > 2
     c                   eval      i = i - 1
     c                   eval      %subst(iadtl1_kod:i:2) = '* '
     c                   goto      next
     c                   endif
      *
      * Hvis ikke denne Tilgangskode er derfinert prøv med *all
      *
     c                   if        not %found(iadtl1)
     c                   eval      iadtl1_kod = '*ALL      '
     c     iadtl1_key    chain     iadtl1
      *    Hvis ikke noen tilganger er definert på denne rutine er det ok
     c                   if        not %found(iadtl1)
     c                   goto      tilgok
     c                   endif
     c                   endif
      *
      *  Tilgang ikke definert
      *
     c                   if        not %found(iadtl1)
     c                   goto      tilgav
     c                   endif
      *
      *  Ikke tilgang til rutine
      *
9.01 c                   if        iadtsp = 'J'
9.01 c                             or iadtsp = 'JA'
9.01 c                             or iadtsp = 'Ja'
     c                   goto      tilgav
     c                   endif
9.01  *
9.01  *  Dersom ikke sperret eller blank ta med kode tilbake
9.01  *
9.01 c                   eval      p_status = *blank
9.01 c                   if        iadtsp <> *blank
9.01 c                   eval      p_status = iadtsp
9.01 c                   goto      tilgok
9.01 c                   endif
      *
      * Rediger sjekk om valg er forespurt
      *
     c                   if        %subst(p_valgfu:1:1) = 'F'
     c                   goto      sjekkF
     c                   endif
      *  Valg i 1. og ev 2. pos.
9.03 c                   if        p_valgfu <> *blank
9.03 c                   dow       %subst(p_valgfu:3:1) = ' '
9.03 c                   eval      %subst(p_valgfu:2:2) = %subst(p_valgfu:1:2)
9.03 c                   eval      %subst(p_valgfu:1:1) = *blank
9.03 c                   enddo
9.03 c                   endif
      *
     c                   if        %subst(p_valgfu:2:1) >  '0' and
     c                             %subst(p_valgfu:2:1) <= '9'
     c                   eval      %subst(w_v:1:1) = %subst(p_valgfu:2:1)
     c                   if        %subst(p_valgfu:3:1) >= '0' and
     c                             %subst(p_valgfu:3:1) <= '9'
     c                   eval      %subst(w_v:2:1) = %subst(p_valgfu:3:1)
     c                   endif
     c                   endif
     c                   if        w_v = *blank
     c                   if        %subst(p_valgfu:3:1) >= '0' and
     c                             %subst(p_valgfu:3:1) <= '9'
     c                   eval      %subst(w_v:2:1) = %subst(p_valgfu:3:1)
     c                   endif
     c                   endif
     c                   if        %subst(w_v:2:1) = *blank
     c                   eval      %subst(w_v:2:1) = %subst(w_v:1:1)
     c                   eval      %subst(w_v:1:1) = '0'
     c                   endif
     c                   if        w_v <> *blank
     c                   goto      valgdef
     c                   endif
      *
      *  Valg i 2. og ev 3. pos.
     c                   if        %subst(p_valgfu:2:1) >  '0' and
     c                             %subst(p_valgfu:2:1) <= '9'
     c                   eval      %subst(w_v:1:1) = %subst(p_valgfu:2:1)
     c                   if        %subst(p_valgfu:3:1) >= '0' and
     c                             %subst(p_valgfu:3:1) <= '9'
     c                   eval      %subst(w_v:2:1) = %subst(p_valgfu:3:1)
     c                   endif
     c                   endif
     c                   if        w_v <> *blank
     c                   goto      valgdef
     c                   endif
      *
      *  Valg i 3. pos.
     c                   if        %subst(p_valgfu:3:1) >  '0' and
     c                             %subst(p_valgfu:3:1) <= '9'
     c                   eval      %subst(w_v:1:1) = %subst(p_valgfu:3:1)
     c                   endif
     c                   if        w_v <> *blank
     c                   goto      valgdef
     c                   endif
      *  Hvis valg ikke er definert er alt ok
     c                   goto      tilgok
      *
     c     valgdef       tag
      * Sjekk om valg er sperret
     c                   if        iadtv1 = w_v or
     c                             iadtv2 = w_v or
     c                             iadtv3 = w_v or
     c                             iadtv4 = w_v or
     c                             iadtv5 = w_v or
     c                             iadtv6 = w_v or
     c                             iadtv7 = w_v or
     c                             iadtv8 = w_v or
     c                             iadtv9 = w_v
     c                   goto      tilgav
     c                   endif
      *
     c     sjekkF        tag
      * Sjekk om Funksjonstast er sperret
     c                   if        %subst(p_valgfu:1:1) <> 'F'
     c                   goto      sjekkX
     c                   endif
     c                   if        iadtf1 = p_valgfu or
     c                             iadtf2 = p_valgfu or
     c                             iadtf3 = p_valgfu or
     c                             iadtf4 = p_valgfu or
     c                             iadtf5 = p_valgfu or
     c                             iadtf6 = p_valgfu or
     c                             iadtf7 = p_valgfu or
     c                             iadtf8 = p_valgfu or
     c                             iadtf9 = p_valgfu
     c                   goto      tilgav
     c                   endif
      *
     c     sjekkX        tag
      * Ev andre sjekker legges inn her
      *
     c     tilgok        tag
9.01 c                   if        p_status = *blank
     c                   eval      p_status = 'Ok'
9.01 c                   endif
     c                   goto      end
      *
      *    Tilgang er avvist
      *
     c     tilgav        tag
     c                   if        p_feilm = 'Ja '
     c                   eval      w_ali = 4
     c                   eval      w_apo = 30
     c                   eval      w_m1 = 'Bruker har ikke tilgang til '
     c                   eval      w_m2 = 'valgt rutine: '
     c                   eval      %subst(w_m2:15:10) = p_rutine
     c                   eval      %subst(w_m2:26:10) = p_valgfu
     c                   eval      w_m3 = 'Om du mener dette er feil,    '
     c                   eval      w_m4 = 'ta kontakt med din leder.     '
     c                   call      'AD009C'
     c                   parm                    w_ali
     c                   parm                    w_apo
     c                   parm                    w_m1
     c                   parm                    w_m2
     c                   parm                    w_m3
     c                   parm                    w_m4
      **  Laget AD006c fra AA006C for å få inn en delay på 5 sek.
9.02 c*                   eval      w_secx = '005'
9.02 c*                   call      'W0005C'
9.02 c*                   parm                    w_secx
     c                   endif
     c                   eval      p_status = 'Sperret'
     c                   goto      end
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     end           tag
      *
     c                   eval      p_valgfu = *blank
     c                   eval      *inlr    = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_tilgko
     c                   parm                    p_rutine
     c                   parm                    p_valgfu
     c                   parm                    p_status
      *
     c                   if        p_status = 'Uten '
     c                   eval      p_feilm  = 'Nei'
     c                   else
     c                   eval      p_feilm  = 'Ja '
     c                   endif
      *
     c                   eval      p_status = *blank
     c                   eval      iadrl1_frm = '000'
     c                   eval      iadtl1_frm = '000'
      *
      * Key for Rutiner
      *
     c     iadrl1_key    klist
     c                   kfld                    iadrl1_frm
     c                   kfld                    iadrl1_rut
      *
      * Key for Tillgang
      *
     c     iadtl1_key    klist
     c                   kfld                    iadtl1_frm
     c                   kfld                    iadtl1_kod
     c                   kfld                    iadtl1_rut
      *
     c                   endsr
