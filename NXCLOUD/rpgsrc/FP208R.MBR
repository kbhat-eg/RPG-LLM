      /TITLE  ASOFAK Automatisk utskrift av etiketter
     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FP208R
      * Beskrivelse . . : Automatisk utskrift av etiketter
      *
      *                   Rutinen leser vare-register og henter inn enheter,
      *                   salgspris og eventuelt tilbudspris på aktuelle
      *                   salgsenheter.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       160699 KOB  Nytt program
      *       010900 KOB  Lagt inn logikk for data fra lageregister
      *       090102 KOB  Lagt inn test for å unngå at etiketter skrives
      *       090102 KOB  på kampanjevarer
      *       051003 KOB  Korrigert utskrift av priskode og lagerplass
      *       071103 KOB  Lagt ut varekode
      *       151103 KOB  Lagt inn lager i etikettregister
      * V5.30 140304 KOB  Henter lager fra paramerliste
      * V5.30 180404 KOB  Lagt inn ny parameter vedr. kontroll mot lager
      * V5.30 021204 KOB  Lagt inn nye parameter i call til VP716R
      * V5.40 100306 KOB  Etiketttype ble hentet fra tidligere vare
      * V5.40 210406 KOB  Lagt inn behandling av prissammenligning
      * V5.40 210406 KOB  Lagt inn kontroll av mva
      * V5.50 030507 KOB  Utvidet lagerplass til 8 poisjoner
      * V5.50 030507 KOB  Lagerplass hentes bare fra lagerregister
5.70  * PRGR  231008 sst  Prisgruppe
      * 6.10  260609 BSA  Oppdatert med sporingsinformasjon
6.11  * 6.11  160909 BSA  Utvider utplukk med leverandør og overstyring av
6.11  *                   allerede utskrevne etiketter.
6.12  * 6.12  021009 BSA  Skriver ut eankode tilknyttet valgte enheter
6.13  * 6.13  251109 bof  Bruker std. oppslag for å finne gtin-nr
6.14  * 6.14  051109 bof  Fjernet oppslag på lagerparametere fra vare
6.15  * 6.10  150310 BSA  Lagrer sist brukte utskrevne pris. Hvis prisene er like,
6.15  *                   skrives ikke ny etikett. Sjekker pris mot en prishorisont.
6.16  * 6.10  160710 BSA  Sorterer på varegruppe/leverandør (valg 3)
6.17  * 6.10  121010 BSA  Feil lengde på kalkulering av GTIN nummer hvis dette
6.17  *                   skjer ut fra varenummer.
6.18  * 6.10  171110 BSA  Konstruerer ikke GTIN nummer hvis det ikke finnes
6.19  * 6.10  010411 BSA  Henter inn GTIN nummer på samme måte som ved manuell
6.19  *                   utkjøring av etiketter.
6.21  * R6.20  190511 bof  Utvidet vl710r med utg.dato og lev.nr
6.22  *  6.20  080312 bof  Utvidet parm. til vp716r
6.23  *  6.20  010703 BSA  Legg inn klokkeslett på kjøretidspunkt
6.24  *  6.20  050615 BKV  Sjekk om hentet kampanje er gylidg
6.30  *  6.30  280514 BKV  LVAR økt fra 15 til 20
6.31  * 6.30  170918 bkv  Utvidet med trelast sortiment
8.01  *PRG2   090622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     fvfaspf    if   e           k disk
     fvetil1    if   e           k disk    rename(vetipfr:vetil1r)
     fvetilu    uf a e           k disk    rename(vetipfr:vetilur)
     fvpkol1    if   e           k disk    rename(vpkopfr:vpkol1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
6.31 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
     fvaw1pf    o    f  512        disk
      *
      * Definering av variable i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vlagl1_vare     s                   like(vlvare)
     d vlagl1_lage     s                   like(vllage)
     d rlevl1_levr     s                   like(rllevr)
     d vpkol1_pkod     s                   like(vapkod)
     d vetil1_vare     s                   like(vmvare)
     d vetil1_lage     s                   like(vmlage)
      *
      * Definering av konstanter
      *
     d sp              c                   '"&\'
     d bl              c                   '   '
      *
      * Definering av variable
      *
     d p_lage          s                   like(vmlage)
     d p_alle          s              1
5.70 d p_vtyp          s                   like(vvvtyp)
6.12 d b_eann          s              1    inz(*off)
6.16 d b_gtin          s              1    inz(*off)
      *
     d w_firm          s                   like(vvfirm)
     d w_vare          s                   like(vvvare)
     d w_vare_s        s                   like(vvvare)
     d w_lvar          s                   like(vvlvar)
     d w_nobb          s                   like(vvnobb)
     d w_tek1          s                   like(vvtek1)
     d w_tek2          s                   like(vvtek2)
     d w_kpri          s                   like(vvkpri)
     d w_plas          s                   like(vlplas)
     d w_vtyp          s                   like(vvvtyp)
     d w_plas_s        s                   like(vlplas)
6.15 d w_phor          s                   like(vnphor)
6.15 d w_ldor          s                   like(vvldor)
      *
6.13 d w_eanx          s             14
6.13 d w_eann          s             14  0
6.13 d w_eava          s                   like(vvvare)
6.13 d w_eaen          s                   like(vvenhe)
6.13 d w_east          s              1
      *
     d w_hyle          s                   like(rlhyle)
     d w_lvko          s                   like(rllvko)
      *
     d w_ptxt          s                   like(vaptxt)
      *
     d w_sort          s                   like(vnsort)
     d w_vtxt          s                   like(vnvtxt)
      *
     d w_etyp          s                   like(vmetyp)
     d w_etpr          s                   like(vmetpr)
     d w_fen1          s                   like(vmfen1)
     d w_fen2          s                   like(vmfen2)
     d w_sdat          s                   like(vmsdat)
     d w_tdat          s                   like(vmtdat)
     d w_lage          s                   like(vmlage)
     d w_dato          s               d   datfmt(*iso)
     d w_edat          s               d   datfmt(*iso)
     *
8.01 d w_prgr          s              2
     d w_lety          s              1  0
     d w_pdat          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
6.23 d w_time2         s               t   timfmt(*iso)
     d w_sae1          s              3
     d w_sap1          s              9  2
     d w_kop1          s              9  2
     d w_inp1          s              9  2
     d w_bup1          s              9  2
     d w_sad1          s               d   datfmt(*iso)
     d w_bup2          s              9  2
     d w_sae2          s              3
     d w_sap2          s              9  2
     d w_omr2          s             12  6
     d w_sad2          s               d   datfmt(*iso)
     d w_bup3          s              9  2
     d w_sae3          s              3
     d w_sap3          s              9  2
     d w_omr3          s             12  6
     d w_sad3          s               d   datfmt(*iso)
     d w_tfda          s               d   datfmt(*iso)
     d w_tfti          s               t   timfmt(*iso)
     d w_ttda          s               d   datfmt(*iso)
     d w_ttti          s               t   timfmt(*iso)
     d w_kamp          s              5
     d w_tip1          s              9  2
     d w_tkp1          s              9  2
     d w_tip2          s              9  2
     d w_tip3          s              9  2
     *
     d w_bmen          s              4  1
     d w_bpun          s              4  1
     d w_mini          s              3  0
     d w_maks          s              3  0
     d w_anta          s              4  0
     d wpanta          s              2  0
     d w_tlbn          s              1  0
     d w_tlbg          s              1  0
     d wantdg          s              5  0
     d w_hnte          s              1  0
     d k               s              2  0
     d w_pri1          s              9  2
     d w_pri2          s              9  2
     d w_pri1_u        s              7  2
     d w_pri2_u        s              7  2
     d w_enh1          s              3
     d w_enh2          s              3
     d wpbel1          s             11  2
     D wpkode          s              1  0
     *
     d w_enin          s              3
     d w_enps          s              3
     d w_pspr          s              9  2
     d w_pspr_u        s              7  2
     *
     d w_mvap          s              6  2
     d w_mvat          s              5  4
     d w_mvaf          s             10  9
     *
     d wwvar8          s              8  0
6.17 d weannr          s             13  0
6.17 d weanna          s             13
     d weanop          s              1  0
     d weansf          s              1  0
     d wean00          s              3  0
     d wean01          s              3  0
     d wean02          s              3  0
      *
6.21 d w_bldo          s                   like(vvldor)
6.21 d w_udat          s               d   datfmt(*iso)
6.21 d b_inlr          s              1    inz(*off)
6.31 d w_lv_nobb       s                   like(vvlnob)
6.31 d w_lv_modn       s                   like(vvlmod)
6.31 d w_lv_lldo       s                   like(vvlnle)
6.31 d w_lv_llva       s                   like(vvllva)
     *
     d eana            s              1    dim(12)
     d ean             s              1  0 dim(12)
6.16  *
6.16  * Datastruktur
6.16  *
6.16 d                 ds
6.16 d d_vgrp                         7
6.16 d  d_ogrp                        2  0 overlay(d_vgrp:1)
6.16 d  d_hgrp                        2  0 overlay(d_vgrp:3)
6.16 d  d_ugrp                        3  0 overlay(d_vgrp:5)
     *
     *+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     *               :  LOCAL DATA AREA                                 +
     *+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     *
     d                uds
     d l_valg                111    111  0
     d l_etpr                112    112  0
     d l_ken1                113    113  0
     d l_ken2                114    114  0
     d l_dato                115    122  0
     d l_sort                123    123  0
     d l_vtyp                124    124
     d l_kamp                125    125
6.11 d l_ldor                126    131  0
6.11 d l_omkj                132    132
     *
     d l_type                891    891  0
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
     d l_lage               1001   1002  0
      *
     c     dummy         tag
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser vareregister
      *
     c     vvarl1_key    setll     vvarl1
     c     vvarl1_key    reade     vvarl1                                 90
      *
     c                   dow       *in90 = *off
     *
     * Sjekk om ikke etikett
     *
     c                   if        vvketi <> *zeros
     c                   goto      neste
     c                   endif
6.11  * Sjekk om varen tilhører valgt leverandør
6.11 c                   if        l_ldor <> *zeros and
6.11 c                             l_ldor <> vvldor
6.11 c                   goto      neste
6.11 c                   endif
5.70  *
5.70  * Kaller program for henting av varetype
5.70  *
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    vvvare
5.70 c                   parm                    w_lage
5.70 c                   parm                    p_vtyp
6.21 c                   parm                    w_udat
6.21 c                   parm                    w_bldo
6.21 c                   parm                    b_inlr
6.31 c                   parm                    w_lv_nobb
6.31 c                   parm                    w_lv_modn
6.31 c                   parm                    w_lv_lldo
6.31 c                   parm                    w_lv_llva
     *
     * Sjekk om varetype skal med
     *
     c                   if        l_vtyp <> ' ' and
5.70 c                             p_vtyp <> l_vtyp
     c                   goto      neste
     c                   endif
     *
     c                   if        l_vtyp = ' '
5.70 c                   if        p_vtyp <> 'L' and
5.70 c                             p_vtyp <> 'S'
     c                   goto      neste
     c                   endif
     c                   endif
      *
     c                   eval      w_vare = vvvare
      *
      * Leser lagerregister
      *
     c                   move      vvvare        vlagl1_vare
     c                   eval      vlagl1_lage = p_lage
     c     vlagl1_key    chain     vlagl1
      *
      * Kontollerer om varen finnes i lagerregisteret
      *
     c                   if        not%found and
     c                             p_alle = ' ' and
     c                             p_lage <> 0
     c                   goto      neste
     c                   endif
      *
     c                   exsr      hent_etikett
      *
 003 c                   move      vvkpri        w_kpri
      *
     c                   exsr      hent_pris
     *
B001 c                   if        l_kamp = *blank and
6.24 c                             w_kamp <> *blank and
6.24 c                             w_tlbn = 1
 001 c                   goto      neste
E001 c                   endif
6.16  * Hent inn GTIN nummer og sjekk om dette avviker fra forrige utskrevne
6.16  * Styres av b_gtin status
6.16 c                   exsr      sjk_gtin
6.16  *
     c                   exsr      sjekk_etikett
     *
6.16 c                   if        b_gtin = *off
B001 c                   if        w_hnte = 1
 001 c                   goto      neste
E001 c                   endif
6.16 c                   endif
      *
     c                   exsr      klar_pris
      *
6.16 c                   if        b_gtin = *off
6.15 c                   if        w_hnte = 1                                   Ikke etikett
6.15 c                   goto      neste
6.15 c                   endif
6.16 c                   endif
      *
     c                   exsr      klar_rest
      *
     c                   exsr      oppd_etikett
      *
     c                   except    skriv
     *
     c     neste         tag
      *
     c     vvarl1_key    reade     vvarl1                                 90
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av etikettinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_pris     begsr
      *
      * Kaller program for henting av pris-informasjon på tre enheter
      *
     c                   call      'VP716R'
     c                   parm                    w_firm
     c                   parm                    w_vare
5.70 c                   parm                    w_prgr
6.22 c                   parm                    w_lage
     c                   parm                    w_lety
     c                   parm                    w_dato
     c                   parm                    w_time
     c                   parm                    w_sae1
     c                   parm                    w_sap1
     c                   parm                    w_bup1
     c                   parm                    w_kop1
     c                   parm                    w_inp1
     c                   parm                    w_sad1
     c                   parm                    w_sae2
     c                   parm                    w_sap2
     c                   parm                    w_bup2
     c                   parm                    w_omr2
     c                   parm                    w_sad2
     c                   parm                    w_sae3
     c                   parm                    w_sap3
     c                   parm                    w_bup3
     c                   parm                    w_omr3
     c                   parm                    w_sad3
     c                   parm                    w_tfda
     c                   parm                    w_tfti
     c                   parm                    w_ttda
     c                   parm                    w_ttti
     c                   parm                    w_kamp
     c                   parm                    w_tip1
     c                   parm                    w_tkp1
     c                   parm                    w_tip2
     c                   parm                    w_tip3
     c                   parm                    w_enin
     c                   parm                    w_enps
     c                   parm                    w_pspr
      *
     c                   eval      w_tlbn = 0
     c                   eval      w_tlbg = 0
      *
     c                   eval      wantdg = *zeros
6.23  * Sjekk tidspunkt opp mot kjøretidspunkt
6.23 c                   time                    w_time2
      *
     c                   if        w_dato >= w_tfda and
     c                             w_dato <= w_ttda
     c                   if        w_dato <> w_tfda or
     c                             w_dato =  w_tfda and
6.23 c**                           w_time >= w_tfti
6.23 c                             w_time2 >= w_tfti
     c                   if        w_dato <> w_ttda or
     c                             w_dato =  w_ttda and
6.23 c**                           w_time <= w_ttti
6.23 c                             w_time2 <= w_ttti
      *
     c                   eval      w_tlbn = 1
      *
     c                   endif
     c                   endif
E001 c                   endif
 001 *
     C     w_dato        subdur    w_ttda        wantdg : *d
 001 *
B002 c                   if        wantdg = 1
     c                   eval      w_tlbg = 1
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av etikettinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_etikett  begsr
      *
     c                   eval      w_hnte = 0
     c                   eval      w_fen1 = 1
     c                   eval      w_fen2 = 2
     c                   eval      w_sdat = *loval
     c                   eval      w_tdat = *loval
     *
     *  Les etikettregisteret
     *
B002 c                   eval      vetil1_lage = w_lage
B002 c                   eval      vetil1_vare = w_vare
     c     vetil1_key    chain     vetil1
     *
B001 c                   if        %found
 001 *
 001 *  Sjekker riktig hylle-etikett-type
 001 *  for denne kjøring
 001 *
     c                   eval      w_etyp = vmetyp
     c                   eval      w_fen1 = vmfen1
     c                   eval      w_fen2 = vmfen2
     c                   eval      w_sdat = vmsdat
     c                   eval      w_tdat = vmtdat
 001 *
E002 c                   else
     c                   eval      w_etyp = vnetyp
E002 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     * Sjekk om etikett skrevet før
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     *
     c     sjekk_etikett begsr
 001 *
B003 c                   eval      w_hnte = 0
 001 *
B003 c                   eval      w_pdat = w_sad1
 001 *
B003 c                   select
 001 *
B004 c                   when      w_sad2 >= w_dato
B003 c                   eval      w_pdat =  w_sad2
 001 *
B004 c                   when      w_sad3 >= w_dato
B003 c                   eval      w_pdat =  w_sad3
 001 *
E003 c                   endsl
 002 *
 001 * Ikke tilbud nå og ikke i går
 001 *
B002 c                   if        w_tlbn = 0 and
 002 c                             w_tlbg = 0
 002 *
6.11  * Hvis omkjøring, skriver vi ut uansett
6.11 c                   if        l_omkj = *blank
B004 c                   if        w_pdat <=  w_sdat
 004 c                   eval      w_hnte = 1
E004 c                   endif
6.11 c                   endif
 002 *
E002 c                   endif
 001 *
 001 * Tilbud i går
 001 *
B002 c                   if        w_tlbg = 1 and
 002 c                             w_dato = w_sdat
 004 c                   eval      w_hnte = 1
E002 c                   endif
 001 *
 001 * Tilbud nå
 001 *
B002 c                   if        w_tlbn = 1 and
 002 c                             w_tfda <= w_tdat
 004 c                   eval      w_hnte = 1
E002 c                   endif
      *
     c                   endsr
     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     * Klargjør priser for etikett
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     *
     c     klar_pris     begsr
     *
6.15 c                   eval      w_hnte = 0                                        ..... . .
     *
     * Nullstill priser/enheter
     *
     C                   eval      w_pri1 = *zero
     C                   eval      w_enh1 = *blank
     C                   eval      w_pri2 = *zero
     C                   eval      w_enh2 = *blank
     *
     * Etikett uten pris ?
     *
     c                   if        l_etpr = 1
     c                   goto      klar_pris_end
     c                   endif
     *
     *  Legger inn riktig pris avhengig av enhet
     *
     C                   select
     *
B001 C                   when      w_fen1 = 3
B002 c                   if        w_bup3 > 0
 001 C                   eval      w_pri1 = w_bup3
E002 c                   else
 001 C                   eval      w_pri1 = w_sap3
E002 c                   endif
 001 C                   eval      w_enh1 = w_sae3
 001 C                   eval      w_edat = w_sad3
B002 c                   if        w_tlbn = 1 and
B002 c                             w_tip3 > 0
 004 c                   eval      w_kpri = 'T'
 004 c                   eval      w_pri1 = w_tip3
E002 c                   endif
     *
B001 C                   when      w_fen1 = 2
B002 c                   if        w_bup2 > 0
 001 C                   eval      w_pri1 = w_bup2
E002 c                   else
 001 C                   eval      w_pri1 = w_sap2
E002 c                   endif
 001 C                   eval      w_enh1 = w_sae2
 001 C                   eval      w_edat = w_sad2
B002 c                   if        w_tlbn = 1 and
B002 c                             w_tip2 > 0
 004 c                   eval      w_kpri = 'T'
 004 c                   eval      w_pri1 = w_tip2
E002 c                   endif
     *
B001 C                   when      w_fen1 = 1
B002 c                   if        w_bup1 > 0
 001 C                   eval      w_pri1 = w_bup1
E002 c                   else
 001 C                   eval      w_pri1 = w_sap1
E002 c                   endif
 001 C                   eval      w_enh1 = w_sae1
 001 C                   eval      w_edat = w_sad1
B002 c                   if        w_tlbn = 1 and
B002 c                             w_tip1 > 0
 004 c                   eval      w_kpri = 'T'
 004 c                   eval      w_pri1 = w_tip1
     C                   eval      w_edat = w_tfda
E002 c                   endif
     *
B001 C                   endsl
     *
B001 C                   select
     *
B001 C                   when      w_fen2 = 3
B002 c                   if        w_bup3 > 0
 001 C                   eval      w_pri2 = w_bup3
E002 c                   else
 001 C                   eval      w_pri2 = w_sap3
E002 c                   endif
 001 C                   eval      w_enh2 = w_sae3
B002 c                   if        w_tlbn = 1 and
B002 c                             w_tip3 > 0
 004 c                   eval      w_pri2 = w_tip3
E002 c                   endif
     *
B001 C                   when      w_fen2 = 2
B002 c                   if        w_bup2 > 0
 001 C                   eval      w_pri2 = w_bup2
E002 c                   else
 001 C                   eval      w_pri2 = w_sap2
E002 c                   endif
 001 C                   eval      w_enh2 = w_sae2
B002 c                   if        w_tlbn = 1 and
B002 c                             w_tip2 > 0
 004 c                   eval      w_pri2 = w_tip2
E002 c                   endif
     *
B001 C                   when      w_fen2 = 1
B002 c                   if        w_bup1 > 0
 001 C                   eval      w_pri2 = w_bup1
E002 c                   else
 001 C                   eval      w_pri2 = w_sap1
E002 c                   endif
 001 C                   eval      w_enh2 = w_sae1
B002 c                   if        w_tlbn = 1 and
B002 c                             w_tip1 > 0
 004 c                   eval      w_pri2 = w_tip1
E002 c                   endif
     *
B001 C                   endsl
      *
      * Hent mva-sats
      *
      * Ordinær sats
      *
     c                   if        vvkmva = 0
      *
     c                   call      'FÅ705R'
     c                   parm                    w_firm
     c                   parm                    w_dato
     c                   parm                    w_mvap
     c                   parm                    w_mvat
     c                   parm                    w_mvaf
     *
     c                   endif
      *
      * Avgiftsfritt
      *
     c                   if        vvkmva = 1
     C                   eval      w_mvat = 1
     c                   endif
      *
      * Halv mva
      *
     c                   if        vvkmva = 2
      *
     c                   call      'FÅ706R'
     c                   parm                    w_firm
     c                   parm                    w_dato
     c                   parm                    w_mvap
     c                   parm                    w_mvat
     c                   parm                    w_mvaf
     *
     c                   endif
     *
     *  Beregner pris med MVA
     *
     C                   eval(h)   w_pri1 = w_pri1 * w_mvat
     C                   eval(h)   w_pri2 = w_pri2 * w_mvat
     C                   eval(h)   w_pspr = w_pspr * w_mvat
     *
     *  Øreavrunding  pris 1
     *
     c                   move      4             wpkode
     c                   eval      wpbel1 = w_pri1
     c                   call      'FA910R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    wpkode
     c                   parm                    wpbel1
     *
B001 c                   if        wpbel1 > 99999
     c                   eval      w_pri1_u = 99999
E001 c                   else
     c                   eval      w_pri1_u = wpbel1
E001 c                   endif
     *
     *  Øreavrunding  pris 2
     *
     c                   eval      wpbel1 = w_pri2
     c                   call      'FA910R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    wpkode
     c                   parm                    wpbel1
     *
B001 c                   if        wpbel1 > 99999
     c                   eval      w_pri2_u = 99999
E001 c                   else
     c                   eval      w_pri2_u = wpbel1
E001 c                   endif
     *
     *  Øreavrunding  sammenligningspris
     *
     c                   eval      wpbel1 = w_pspr
     c                   call      'FA910R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    wpkode
     c                   parm                    wpbel1
     *
     c                   if        wpbel1 > 99999
     c                   eval      w_pspr_u = 99999
     c                   else
     c                   eval      w_pspr_u = wpbel1
     c                   endif
     *
     * Hvis pris1 er null
     *
 001 c                   eval      *in71  = *off
B001 c                   if        w_pri1_u <> *zeros
 001 c                   eval      *in71  = *on
E001 c                   endif
     *
     * Hvis pris2 er null
     *
 001 c                   eval      *in72  = *off
B001 c                   if        w_pri2_u <> *zeros
 001 c                   eval      *in72  = *on
E001 c                   endif
     *
     * Hvis sammenligningspris er null
     *
     c                   eval      *in73  = *off
     c                   if        w_pspr_u <> *zeros
     c                   eval      *in73  = *on
     c                   endif
      *
6.15 * Sjekk ny og gammel pris. Hvis ingen endring skrives ikke etikett
6.11  * Hvis omkjøring, skriver vi ut uansett
6.11 c                   if        l_omkj = *blank
6.15 c                   if        vmsapr = w_pri1_u                                     .....
6.15 c                   eval      w_hnte = 1                                            .....
6.15 c                   endif
6.11 c                   endif
      *
     c     klar_pris_end endsr
     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     * Oppdater etikett-register
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     *
     c     Oppd_etikett  begsr
     *
     c     vetil1_key    chain     vetilu
     *
6.10 c                   time                    vmetim
6.10 c                   time                    vmedat
6.10 c                   eval      vmeusr = l_user
     *
B001 c                   if        %found
     *
     c                   eval      vmsdat = w_dato
6.15 c                   eval      vmsapr = w_pri1_u
6.16 c                   eval      vmgtin = w_eann
     *
     c                   if        w_tlbn = 1
     c                   eval      vmtdat = w_tfda
     c                   endif
     *
     c                   update    vetilur
     *
     c                   else
     *
     c                   eval      vmfirm = w_firm
     c                   eval      vmlage = w_lage
     c                   eval      vmvare = w_vare
     c                   eval      vmenhe = w_enh1
     c                   eval      vmetyp = w_etyp
     c                   eval      vmetpr = w_etpr
     c                   eval      vmfen1 = w_fen1
     c                   eval      vmfen2 = w_fen2
     c                   eval      vmsdat = w_dato
     c                   eval      vmtdat = w_tdat
6.15 c                   eval      vmsapr = w_pri1_u
6.16 c                   eval      vmgtin = w_eann
6.10 c                   time                    vmotim
6.10 c                   time                    vmodat
6.10 c                   eval      vmousr = l_user
     *
     c                   write     vetilur
     *
E001 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     * Klargjør felter for etikett
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     *
     c     klar_rest     begsr
     *
6.31 c                   if        w_lv_nobb > 0
6.31 c                   move      w_lv_nobb     w_lvar
6.31 c                   move      w_lv_llva     w_nobb
6.31 c                   else
 003 c                   move      vvlvar        w_lvar
 003 c                   move      vvnobb        w_nobb
6.31 c                   endif
6.16 * Innhenting av GTIN nummer er flyttet.
6.16 * Henter EAN-kode fra EAN-register
6.16 *
6.16 c**                 exsr      hntean
6.16 *
6.16 * Legger eget varenummer i EAN-kode
6.16 * dersom EAN-kode ikke utfylt
6.16 *
6.16 c**                 if        w_eann = *zero
6.16 c**                 movel     vvvare        wwvar8
6.16 c**                 eval      weannr = wwvar8
6.16 c**                 exsr      eansjk
6.16 c**                 movel     weannr        w_eann
6.16 c**                 move      weansf        w_eann
6.16 c**                 endif
6.16 *
     *   Henter leverandøropplysninger
     *
     c                   eval      w_lvko = *zeros
B003 c                   eval      w_hyle = *zeros
     *
B001 c                   if        vvldor <> *zero
 001 *                                                                 .
 001 c                   move      vvldor        rlevl1_levr
 001 c     rlevl1_key    chain     rlevl1                             86
 001 *                                                                 .
B002 c                   if        *in86 = *off
     c                   eval      w_lvko = rllvko
B003 c                   eval      w_hyle = rlhyle
E001 c                   endif
 001 *                                                                 .
E001 c                   endif
     *
     *  Legger inn bestillingspunkt
     *
B001 c                   if        vlbpun <> *zero
     c                   if        vlbpun <= 999,9
     c                   eval      w_bpun = vlbpun
     c                   else
     c                   eval      w_bpun = 999,9
     c                   endif
     *
6.14 c                   endif
     *
     *  Legger inn bestillingsmengde
     *
B001 c                   if        vlbmen <> *zero
B001 c                   if        vlbmen <= 999
     c                   eval      w_bmen = vlbmen
     c                   else
     c                   eval      w_bmen = 999
     c                   endif
     *
6.14 c                   endif
     *
     *  Legger inn minimumsbeholdning
     *
B001 c                   if        vlmini <> *zero
B001 c                   if        vlmini <= 999
     c                   eval      w_mini = vlmini
     c                   else
     c                   eval      w_mini = 999
     c                   endif
     *
6.14 c                   endif
     *
     *  Legger inn maksimumsbeholdning
     *
B001 c                   if        vlmaks <> *zero
B001 c                   if        vlmaks <= 999
     c                   eval      w_maks = vlmaks
     c                   else
     c                   eval      w_maks = 999
     c                   endif
     *
6.14 c                   endif
     *
     *  Hent tekst for priskode
     *
 001 c                   move      *blanks       w_ptxt
     *
B001 c                   if        w_kpri <> ' '
     c                   eval      vpkol1_pkod = w_kpri
 001 c     vpkol1_key    chain     vpkol1r
B002 c                   if        %found
 002 c                   move      vaptxt        w_ptxt
E002 c                   endif
E001 c                   endif
     *
     * Kontroller tekstfelt for ugyldige karakterer
     *
 001 c                   eval      w_tek1 = *blank
B001 c                   if        vvtek1 <> *blank
     c     sp:bl         xlate     vvtek1        w_tek1
E001 c                   endif
     *
 001 c                   eval      w_tek2 = *blank
B001 c                   if        vvtek2 <> *blank
     c     sp:bl         xlate     vvtek2        w_tek2
E001 c                   endif
     *
     * Hent lagerplass fra lagerregister
     *
 001 c                   eval      w_plas = vlplas
     *
     *  Legg ut varetype
     *
5.70 c                   eval      w_vtyp = p_vtyp
     *
     * Sortering på varenummer/lagerplass
     *
B001 c                   if        w_sort = *zero
 001 c                   eval      w_plas_s = w_plas
 001 c                   eval      w_vare_s = w_vare
6.15 c                   endif
6.15 c                   if        w_sort = 1
 001 c                   eval      w_plas_s = *blank
 001 c                   eval      w_vare_s = w_vare
E001 c                   endif
6.15  * Sortering på leverandør og lagerplass/varenummer
6.15 c                   if        w_sort = 2
6.15 c                   movel     vvldor        w_plas_s
6.15 c                   if        w_plas <> *blank
6.15 c                   movel     w_plas        w_vare_s
6.15 c                   else
6.15 c                   eval      w_vare_s = w_vare
6.15 c                   endif
6.15 c                   endif
6.16  * Sortering på varegruppe og leverandør
6.16 c                   if        w_sort = 3
6.16 c                   eval      d_ogrp = vvogrp
6.16 c                   eval      d_hgrp = vvhgrp
6.16 c                   eval      d_ugrp = vvugrp
6.16 c                   movel     d_vgrp        w_plas_s
6.16 c                   movel     vvldor        w_vare_s
6.16 c                   endif
     *
     c                   endsr
      *
6.16  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.16 * Hvis det er avvikende gtin nummer skal ny etikett skrives ut.
6.16  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.16 *
6.16 c     sjk_gtin      begsr
6.16 *
6.16 c                   eval      b_gtin = *off
6.16 *
6.16 * Henter EAN-kode fra EAN-register
6.16 *
6.16 c                   exsr      hntean
6.16 *
6.16 * Legger eget varenummer i EAN-kode
6.16 * dersom EAN-kode ikke utfylt
6.16 *
6.18  * Skal ikke konstruere NOBBnr hvis det ikke finnes på VVEP eller VEAN.
6.18  * Legger inn dummy test, slik at GTIN nummer ikke kalkuleres
6.18 c                   if        1 = 2
6.16 c                   if        w_eann = *zero
6.16 c                   movel     vvvare        wwvar8
6.16 c                   eval      weannr = wwvar8
6.16 c                   exsr      eansjk
6.16 c                   movel     weannr        w_eann
6.16 c                   move      weansf        w_eann
6.16 c                   endif
6.18 c                   endif
6.16  * Sjekk om "funnet" gtin nummer tilsvarer forrige utskrevne. Hvis nytt nummer
6.16  * skal det skrives ut ny.
6.16 c                   if        w_eann <> vmgtin
6.16 c                   eval      b_gtin = *on
6.16 c                   endif
6.16  *
6.16 c                   endsr
     *
     *+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     *   Henter ean-nr
     *+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     *
     c     hntean        begsr
     *
     c                   eval      w_eann = *zeros
6.12 c                   eval      b_eann = *off
6.13  *
6.19 c                   call      'VE716R'
6.19 c                   parm                    w_firm
6.19 c                   parm                    w_vare
6.19 c                   parm                    w_eann
6.19 c                   parm                    b_eann
6.13  *
6.13 c**6.19             call      'VE714R'
6.13 c**6.19             parm                    w_firm
6.13 c**6.19             parm                    w_vare
6.13 c**6.19             parm                    w_sae1
6.13 c**6.19             parm                    w_eann
6.13 c**6.19             parm                    w_east
6.13 c**6.19             if        w_east = '1'
6.13 c**6.19             eval      b_eann = *on
6.13 c**6.19             else
6.13 c**6.19             eval      w_eann = 0
6.13 c**6.19             endif
6.13  * Match på enhet 2 ?
6.13 c**6.               if        b_eann = *off and
6.13 c**6.                         w_sae2 <> *blank
6.13  *
6.13 c**6.19             call      'VE714R'
6.13 c**6.19             parm                    w_firm
6.13 c**6.19             parm                    w_vare
6.13 c**6.19             parm                    w_sae1
6.13 c**6.19             parm                    w_eann
6.13 c**6.19             parm                    w_east
6.13 c**6.19             if        w_east = '1'
6.13 c**6.19             eval      b_eann = *on
6.13 c**6.19             else
6.13 c**6.19             eval      w_eann = 0
6.13 c**6.19             endif
6.13 c**6.19             endif
     *
     c                   endsr
      *
     *+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     *   Sjekk om siste siffer i EAN-koden er riktig (Mod10)            +
     *+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     *
     c     eansjk        begsr
     *
     c                   eval      wean00 = *zero
     c                   eval      wean02 = *zero
     c                   eval      weansf = *zero
     *
     c                   move      weannr        weanna
     c                   movea     weanna        eana
     c                   move      eana          ean
     *
     c                   eval      wean00 = ean(02) + ean(04)
     c                   eval      wean00 = wean00 + ean(06)
     c                   eval      wean00 = wean00 + ean(08)
     c                   eval      wean00 = wean00 + ean(10)
     c                   eval      wean00 = wean00 + ean(12)
     c                   eval      wean00 = wean00 * 3
     *
     c                   eval      wean00 = wean00 + ean(01)
     c                   eval      wean00 = wean00 + ean(03)
     c                   eval      wean00 = wean00 + ean(05)
     c                   eval      wean00 = wean00 + ean(07)
     c                   eval      wean00 = wean00 + ean(09)
     c                   eval      wean00 = wean00 + ean(11)
     *
     c                   move      wean00        wean02
     c                   move      '0'           wean02
     c                   eval      wean02 = wean02 + 10
     *
     c                   eval      wean01 = wean02 - wean00
     c                   move      wean01        weansf
     *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_lage
     c                   parm                    p_alle
      *
      * Key for les av vare-register
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på lagerregister
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl1_vare
     c                   kfld                    vlagl1_lage
      *
      * Key for oppslag på etikett-register
      *
     c     vetil1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vetil1_vare
     c                   kfld                    vetil1_lage
      *
      * Key for oppslag på leverandør-register
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for oppslag på fastregister-etiketter
      *
     c     vfaspf_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på priskoder
      *
     c     vpkol1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vpkol1_pkod
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_lage = p_lage
     *
     * Henter koder fra fast-register
     *
     c                   eval      l_type = 0
     c                   eval      w_sort = 0
     c                   eval      w_etyp = 0
     c                   eval      w_etpr = 1
6.15 c                   eval      w_phor = 0
     *
     c     vfaspf_key    chain     vfaspf                             91
B001 c                   if        *in91 = *off
     c                   eval      w_sort = vnsort
     c                   eval      w_etyp = vnetyp
     c                   eval      w_vtxt = vnvtxt
6.15 c                   eval      w_phor = vnphor
E001 c                   endif
      *
      * Initierer parametre
      *
     c                   eval      w_sae1 = *blank
     c                   eval      w_sap1 = 0
     c                   eval      w_kop1 = 0
     c                   eval      w_inp1 = 0
     c                   eval      w_bup1 = 0
     c                   eval      w_sae2 = *blank
     c                   eval      w_bup2 = 0
     c                   eval      w_sap2 = 0
     c                   eval      w_omr2 = 0
     c                   eval      w_sae3 = *blank
     c                   eval      w_bup3 = 0
     c                   eval      w_sap3 = 0
     c                   eval      w_omr3 = 0
     c                   eval      w_tfda = *loval
     c                   eval      w_tfti = *loval
     c                   eval      w_ttda = *loval
     c                   eval      w_ttti = *loval
     c                   eval      w_kamp = *blank
     c                   eval      w_tip1 = 0
     c                   eval      w_tkp1 = 0
     c                   eval      w_tip2 = 0
     c                   eval      w_tip3 = 0
      *
     c                   eval      w_lety = 0
     c                   eval      w_anta = 1
      *
     c                   time                    w_dato
6.15 c                   if        w_phor > 0
6.15 c     w_dato        adddur    w_phor : *d   w_dato
6.15 c                   endif
      *
     c                   call      'FÅ705R'
     c                   parm                    w_firm
     c                   parm                    w_dato
     c                   parm                    w_mvap
     c                   parm                    w_mvat
     c                   parm                    w_mvaf
      *
5.70  * Henter prisgruppe
5.70  *
5.70 c                   call      'VL711R   '
5.70 c                   parm                    w_prgr
      *
     c                   endsr
      *
     *+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     *                   O   U   T   P   U   T                          +
     *+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     *
     Ovaw1pf    e            skriv
     O                       w_anta               4
     O                       w_vtxt              19
     O                       w_dato              45
     O                       w_nobb              53
     O                       w_vare              73
     O                       w_eann              86
6.30 O                       w_lvar             106
6.30 O                       w_tek1             141
6.30 O                       w_tek2             176
6.30 O                       w_enh1             179
6.30 O               71      w_pri1_u           187 '   0 ,  '
6.30 O                       w_enh2             190
6.30 O               72      w_pri2_u           198 '   0 ,  '
6.30 O                       w_ptxt             203
6.30 o                       w_bpun             208
6.30 o                       w_bmen             213
6.30 o                       w_hyle             214
6.30 o                       w_lvko             215
6.30 o                       w_plas             223
6.30 o                       w_etyp             224
6.30 o                       w_mini             227 '   '
6.30 o                       w_maks             230 '   '
6.30 o                       w_vtyp             231
6.30 o                       w_enin             234
6.30 o                       w_enps             237
6.30 o               73      w_pspr_u           245 '   0 ,  '
      *
      * Sorteringsrekkefølge
      *
     o                       w_plas_s           457
     o                       w_vare_s           472
