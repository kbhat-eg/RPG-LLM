     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FO505R
      * Beskrivelse . . : Vise ordre
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       010699 AMD  Nytt program
      *       040400 HKO  Lagt inn funksjonstast for vising av spesialpriser
3.31  * R3.31 160801 AMD  Lagt inn nye spørreprogram mot lager.
5.21  * R5.21 261103 AMD  Overstyrer med mobilnummer fra ordrehode
5.21  *                   dersom oppgitt.
      *       161203 HKO  Tar hensyn til bruksrett-rabatt
      *       251005 HKO  Viser oppfølgingsdato og informasjonsfelter
      *       071105 HKO  Fjernet F15=Rabattmatrise og F16=Spesialpriser
      *       241105 HKO  Viser tilbud utgår-dato
6.10  * R6.10 250809 BSA  Utvidet ref. felter fra 20 til 30 posisjoner
6.nu  * R6.30 270514 bsa  Programmet gjennomgått mtp nummerutvidelser.
7.xx  * 7.00  310316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 7.00  170818 BKV  Suffix ble ikke satt
7.02  * 7.00  170119 BKV  Fjerner DG visning
7.03  * 7.00  170119 BKV  132 bilde
7.04  * 7.00  170119 BKV  Newlook vask
      *
7.fb  * R6.40 220318 bof  Utvidet med farger ihht. fors.betaling/depositum
      *
8.01 A*K0000  110122 bsa  Omrokkerer og legger ut varekunde
8.02  *PRG2   200622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
8.03 A*K0000  110625 bsa  Falgegr skaffevarer som blå.
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     fvpkol1    if   e           k disk    rename(vpkopfr:vpkol1r)
     fvltyl1    if   e           k disk    rename(vltypfr:vltyl1r)
7.xx ffusrl1    if   e           k disk    rename(fusrpfr:fusrl1r)
7.xx fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
7.fb fsdepl1    if   e           k disk    rename(sdeppfr:sdepl1r)
      *
     ffo505d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
7.xx d l_user                911    920
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av datastrukturer
      *
      *  Datastruktur for detaljlinje
      *
7.03 d*                 ds
7.03 d* d_text                        70
7.03 d*  d_vare                        8    overlay(d_text)
7.03 d*  d_tek1                       30    overlay(d_text:10)
7.03 d*  d_anta                       13    overlay(d_text:37)
7.03 d*  d_enh1                        3    overlay(d_text:50)
7.03 d*  d_raba                        6    overlay(d_text:53)
7.03 d*  d_salg                       13    overlay(d_text:58)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(fofirm)
6.nu d p_numm          s                   like(fonumm)
     d p_suff          s                   like(fosuff)
      *
      * Definering av variabler i nøkler
      *
     d rkunl1_kund     s                   like(rkkund)
     d vpkol1_pkod     s                   like(vapkod)
     d vltyl1_ltyp     s                   like(valtyp)
7.xx d fusrl1_user     s                   like(fbuser)
7.xx d vvarl1_vare     s                   like(vvvare)
7.fb d sdepl1_numm     s                   like(sknumm)
7.fb d sdepl1_kund     s                   like(skkund)
7.fb d sdepl1_tell     s                   like(sktell)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_dato          s               d   datfmt(*iso)
     d w_raba          s              5  2
     d w_csts          s              1
     d w_ctx2          s             20
      *
     d w_firm          s                   like(fofirm)
     d w_kund          s                   like(fokund)
6.nu d w_numm          s                   like(fonumm)
     d w_suff          s                   like(fosuff)
     d w_vare          s                   like(fdvare)
     d w_eror          s              1
      *
7.xx d w_lage          s              2  0
7.xx d w_vtyp          s                   like(vvvtyp)
7.xx d w_ldor          s                   like(vvldor)
7.xx d w_behl          s             11  3
7.xx d w_udat          s               d   datfmt(*iso)
7.xx d b_inlr          s              1    inz(*off)
7.xx d w_behl0         s              5  0
7.04 d w_len           s              3  0
7.04 d w_teller        s              3  0
      *
7.xx d b_kost          s              1    inz(*off)
      *
      * Definering av konstanter
      *
7.03 d c_sfil          s              3  0 inz(011)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Fyller control-bildet
      *
     c                   eval      *in70 = *off
     c                   exsr      control_init
      *
      * Fyller subfile
      *
     c     fodtl1_key    setll     fodtl1
     c                   exsr      crt_subfile                                  CRT SFL
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      dsp_subfile                                  DSP SFL
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
7.03 c                   when      *in10
7.03 c                   exsr      crt_subfile                                  CRT SFL
7.03 c                   goto      b2tagb
7.xx c*                  when      *in21
7.xx c*                  eval      *in22 = *on                                  CRT SFL
7.xx c*                  goto      b2taga
     c                   endsl
7.xx  *
7.xx  * F1=Vis skjermbilde-II (dekning + beregnet)
7.xx  *
7.xx c                   if        *inka = *on
7.xx c                   if        *in70 = *off
7.xx c                   eval      *in70 = *on
7.xx c                   else
7.xx c                   eval      *in70 = *off
7.xx c                   endif
7.xx c     fodtl1_key    setll     fodtl1
7.xx c                   exsr      clr_subfile
7.xx c                   exsr      crt_subfile                                  CRT SFL
7.xx c                   goto      b2tagb
7.xx c                   endif
      *
      * F9=Spørre på lagerbeholdning
      *
     c                   if        *inki = *on
3.31 c                   call      'LL500R'
3.31  *                  parm                    w_eror
3.31  *                  parm                    w_firm
3.31  *                  parm                    w_vare
     c                   goto      b2tagb
     c                   endif
      *
      * F14=Vis kunde-/kundeprosjekt-informasjon
      *
     c                   if        *inkn = *on
     c                   call      'FL510R'
     c                   parm                    w_firm
     c                   parm                    fokund
     c                   parm                    fokpro
     c                   goto      b2tagb
     c                   endif
      *
      * F21=Spørring på vare/pris
      *
     C                   if        *inkv = *on
     C                   call      'VV550R'
     C                   goto      b2tagb
     C                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å initierer/fylle opp control-bildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     control_init  begsr
      *
      * Finner ordre-hode
      *
     c     fohel1_key    setll     fohel1
     c     fohel1_key    reade     fohel1                                 90
     c                   if        *in90 = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   dow       *in90 = *off
     c     fohel1_key    reade     fohel1                                 90
     c                   enddo
      *
     c                   eval      b2kund = fokund
     c                   eval      b2navn = fonavn
     c                   eval      b2adr1 = foadr1
     c                   eval      b2sted = fosted
     c                   eval      b2numm = fonumm
7.01 c                   eval      b2suff = fosuff
     c                   eval      b2vref = fovref
     c                   eval      b2dref = fodref
     c                   eval      b2inf1 = foinf1
     c                   eval      b2inf2 = foinf2
      *
     c                   if        fobdat <> *loval
     c     *dmy          move      fobdat        b2bdat
     c                   endif
      *
     c                   if        foddat <> *loval
     c     *dmy          move      foddat        b2ddat
     c                   endif
      *
     c                   if        fotdat <> *loval
     c     *dmy          move      fotdat        b2tdat
     c                   endif
      *
     c                   if        foldat <> *loval
     c     *dmy          move      foldat        b2ldat
     c                   endif
      *
      * Henter kundenavn
      *
     c                   if        fokund <> 0
     c                   eval      rkunl1_kund = fokund
     c     rkunl1_key    chain     rkunl1                             90
     c                   if        *in90 = *off
     c                   eval      b2knav = rknavn
     c                   eval      b2kgat = rkgate
     c                   eval      b2kste = rksted
     c                   endif
     c                   endif
5.21  *
5.21  * Viser mobilnummer
5.21  *
5.21 c                   eval      b2motx = *blank
5.21 c                   eval      b2mobi = rkmobn
5.21  *
5.21 c                   if        fomobi <> *blank
5.21 c                   eval      b2mobi = fomobi
5.21 c                   endif
5.21  *
5.21 c                   if        b2mobi <> *blank
5.21 c                   eval      b2motx = 'Mob:'
5.21 c                   endif
8.01  *
8.01  * Henter varekunde navn
8.01  *
8.01 c                   eval      b3lkun = 0
8.01 c                   eval      b3kuna = *blanks
8.01 c                   eval      b3lgat = *blanks
8.01 c                   eval      b3lste = *blanks
8.01  *
8.01 c                   if        folkun <> 0
8.01 c                   eval      rkunl1_kund = folkun
8.01 c     rkunl1_key    chain     rkunl1
8.01 c                   if        %found
8.01 c                   eval      b3lkun = folkun
8.01 c                   eval      b3kuna = rknavn
8.01 c                   eval      b3lgat = rkgate
8.01 c                   eval      b3lste = rksted
8.01  *
8.01  * Viser mobilnummer
8.01  *
8.01 c                   eval      b3motx = *blank
8.01 c                   eval      b3motx = 'Mob:'
8.01 c                   eval      b3mobi = rkmobn
8.01 c                   endif
8.01 c                   endif
      *
      * Finner ordretotal
      *
     c                   eval      b2salg = (fotots - fototr)
7.fb  *
7.fb  * sjekker depositum   - setter på farge
7.fb c                   eval      *in45  = *off
7.fb c                   eval      *in46  = *off
7.fb c                   eval      sdepl1_numm = fonumm
7.fb c                   eval      sdepl1_kund = fokund
7.fb c                   eval      sdepl1_tell = 0
7.fb c     sdepl1_key    chain     sdepl1r
7.fb c                   if        %found(sdepl1)
7.fb c                   if        skdepo <> 0 and
7.fb c                             skdepo > skdepb
7.fb c                   eval      *in45  = *on
7.fb c                   endif
7.fb c                   if        skdepo <> 0 and
7.fb c                             skdepb >= skdepo
7.fb c                   eval      *in46 = *on
7.fb c                   endif
7.fb c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c     fodtl1_key    setll     fodtl1
      *
     c                   exsr      clr_subfile                                  TØM SFL
     c                   exsr      crt_subfile                                  CRT SFL
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Vise
      *
     c                   if        b1valg = 5
     c                   call      'FD505R'
     c                   parm                    w_firm
     c                   parm                    b1numm
     c                   parm                    b1suff
     c                   parm                    b1line
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil                     SFL END
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c     fodtl1_key    reade     fodtl1                                 15
      *
     c                   if        *in15                                        EOF
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1numm = fdnumm
     c                   eval      b1suff = fdsuff
     c                   eval      b1lety = fdlety
     c                   eval      b1line = fdline
7.xx c                   eval      b1tek1 = fdtek1
7.03 c                   eval      b1tek2 = fdtek2
7.03 c*                   eval      b1text = *blank
7.xx c                   eval      b1vare = fdvare
7.xx c                   eval      b1enhe = fdenh1
7.03 c                   eval      b1raba = *blank
7.xx c                   eval      b1kpri = *blank
7.xx c                   eval      b1sumn = fdsums - fdsumr
7.xx c                   eval      b1sumk = fdsumk
7.xx c                   eval      b1sumb = 0
7.xx c                   eval      b1grad = 0
7.03 c                   move      fdanta        b1anta
7.03 c*                   eval      b1anta = %editw(fdanta : '        ,   -')
7.03 c*                   eval      b1sapr = %editw(fdsapr : '         ,  -')
7.03 c                   move      fdsapr        b1sapr
     c                   eval      b1salg = 0
7.xx c                   eval      b1neto = ''
7.xx c                   eval      b1kost = ''
7.xx c                   eval      b1dekg = ''
      *
7.xx c                   if        b_kost  = *off or *in70 = *off
7.xx c                   eval      b1sumk = 0
7.xx c                   eval      b1grad = 0
7.xx c                   endif
      *
     c                   eval      w_raba = 0
      *
7.xx c                   eval      b1lbeh = ''
7.xx c                   if        fdvare <> *blank and
7.xx c                             fdlvre = 0
7.xx c                   eval      vvarl1_vare = fdvare
7.xx c     vvarl1_key    chain     vvarl1
7.xx c                   if        not %found
7.xx c                   eval      b1tek1 = %subst(b1tek1:1:8) +
7.xx c                                      ' ** SLETTET VARE **'
7.xx c                   else
7.xx  *
7.xx  * henter varetype og beholdning
7.xx  *
7.xx c                   eval      w_lage = fdlage
7.xx c                   call      'VL715R'
7.xx c                   parm                    w_firm
7.xx c                   parm                    vvvare
7.xx c                   parm                    w_lage
7.xx c                   parm                    w_vtyp
7.xx c                   parm                    w_udat
7.xx c                   parm                    w_ldor
7.xx c                   parm                    w_behl
7.xx c                   parm                    b_inlr
7.xx c                   if        %abs(w_behl) < 100000
7.xx c                   eval(h)   w_behl0 = w_behl
7.xx c                   else
7.xx c                   eval      w_behl0 = 99999
7.xx c                   endif
7.xx c                   eval      b1lbeh = %editw(w_behl0 : '      -')
7.xx c                   endif
7.xx c                   endif
      *
      *
      * Finner linjetype
      *
     c                   eval      vltyl1_ltyp = fdltyp
     c     vltyl1_key    chain     vltyl1                             92
      *
      * Tekstlinje
      *
     c                   if        valktx = 1
7.03 c*                   eval      b1text = b1tek1 + fdtek2
     c                   eval      b1lety = *zero
7.03 c*                   eval      b1tex2 = %trimr(b1tek1) + %trimr(fdtek2)
      *
      * Varelinje
      *
     c                   else
      *
     c                   if        fdrab2 = 0 and
     c                             fdbrr1 = 0 and
     c                             fdbrr2 = 0
     c                   eval      w_raba = fdrab1
     c                   else
     c                   if        fdsums <> 0
     c                   eval(h)   w_raba = fdsumr * 100 / fdsums
     c                   endif
     c                   endif
      *
7.xx c                   if        fdlass > 0
7.xx c                   movel     fdlass        b1kpri
7.xx c                   endif
7.xx c                   move      fdkpri        b1kpri
      *
     c                   eval(h)   fdsums = fdsums - (fdsums * w_raba / 100)
      *
7.03 c*                   eval      d_anta = %editc(fdanta : 'M')
7.03 c*                   eval      d_salg = %editc(fdsums : 'M')
7.03 c                   move      fdsums        b1salg
      *
7.03 c*                   eval      d_vare = fdvare
7.03 c*                   eval      d_tek1 = fdtek1
7.03 c*                   eval      d_enh1 = fdenh1
7.03 c*                   eval      d_raba = *blank
      *
     c                   if        w_raba <> 0
7.03 c*                   eval      d_raba = %editc(w_raba : '4')
7.03 c                   eval      b1raba = %editc(w_raba : '4')
     c                   endif
      *
     c                   if        w_raba = 0
     c                   eval      vpkol1_pkod = fdkpri
     c     vpkol1_key    chain     vpkol1                             91
     c                   if        *in91 = *off
7.03 c*                   move      vaptxt        d_raba
7.03 c                   move      vaptxt        b1raba
     c                   endif
     c                   endif
      *
7.03 c*                   eval      b1text = d_text
7.xx c                   if        fdtek2 = *blank
7.03 c*                   eval      b1tex2=%trimr(b1tek1)
7.xx c                   else
7.03 c*                   eval      b1tex2=%trimr(b1tek1)+' : '+%trimr(fdtek2)
7.xx c                   endif
7.xx  *
7.xx  * Beregner DG
7.xx  *
7.xx c                   if        b1sumn <> 0
7.xx c                   select
7.xx c                   when      (100 - (b1sumk * 100 / b1sumn)) < -999,99
7.xx c                   eval      b1grad = -999,99
7.xx c                   when      (100 - (b1sumk * 100 / b1sumn)) > 999,99
7.xx c                   eval      b1grad = 999,99
7.xx c                   other
7.xx c                   eval(h)   b1grad = 100 - (b1sumk * 100 / b1sumn)
7.xx c                   endsl
7.xx c                   endif
7.xx  *
7.xx  * Dersom bruker ikke skal ha tilgang til å se kostpris blir
7.xx  * informasjon om denne fjernet.
7.xx  *
7.xx c                   if        b_kost = *off or *in70 = *off
7.xx c                   eval      b1sumk = 0
7.xx c                   eval      b1grad = 0
7.xx c                   endif
7.xx  *
7.xx c                   eval      b1neto = %editw(b1sumn : '         ,  -')
7.xx c                   eval      b1kost = %editw(b1sumk : '         ,  -')
7.xx c                   eval      b1dekg = %editw(b1grad : '   ,  -')
7.xx  *
     c                   endif
      *
8.03 c                   eval      *in41 = *off
8.03 c                   if        fdlvre = 1
8.03 c                   eval      *in41 = *on
8.03 c                   endif
7.04  *
7.04  *  Vasker tegn som newlook ikke tåler
7.04  *
7.04 c                   exsr      newlo_vask
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
7.04  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.04  * Newlook vask av data
7.04  *  Fjerner tegn som gjør at Newlook feiltolker
7.04  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.04  *
7.04   begsr newlo_vask;
7.04     for w_teller = 1 to 25;
7.04       w_len = %len(%trim(b1tek1));
7.04       if (w_len > 0);
7.04       b1tek1 = %ScanRpl('.':'':b1tek1:w_len); // newlook tåler ikke . på slutten
7.04       endif;
7.04       w_len = %len(%trim(b1tek1));
7.04       if (w_len > 0);
7.04       b1tek1 = %ScanRpl(':':'':b1tek1:w_len); // newlook tåler ikke : på slutten
7.04       endif;
7.04       w_len = %len(%trim(b1tek2));
7.04       if (w_len > 0);
7.04       b1tek2 = %ScanRpl('.':'':b1tek2:w_len); // newlook tåler ikke . på slutten
7.04       endif;
7.04       w_len = %len(%trim(b1tek2));
7.04       if (w_len > 0);
7.04       b1tek2 = %ScanRpl(':':'':b1tek2:w_len); // newlook tåler ikke : på slutten
7.04       endif;
7.04     endfor ;
7.04   endsr;
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
6.nu c                   parm                    p_numm
     c                   parm                    p_suff
      *
      * Key for oppslag på ordre-hode-register
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for les på ordre-linje-register
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
      *
      * Key for oppslag på priskode-register
      *
     c     vpkol1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vpkol1_pkod
      *
      * Key for oppslag på linje-type-register
      *
     c     vltyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltyl1_ltyp
7.xx  *
7.xx  * Key for oppslag på vare
7.xx  *
7.xx c     vvarl1_key    klist
7.xx c                   kfld                    w_firm
7.xx c                   kfld                    vvarl1_vare
7.xx  *
7.xx  * Key for oppslag på bruker-registrer
7.xx  *
7.xx c     fusrl1_key    klist
7.xx c                   kfld                    w_firm
7.xx c                   kfld                    fusrl1_user
7.fb  *
7.fb  * Key for oppslag på depositum-fil
7.fb  *
7.fb c     sdepl1_key    klist
7.fb c                   kfld                    w_firm
7.fb c                   kfld                    sdepl1_numm
7.fb c                   kfld                    sdepl1_kund
7.fb c                   kfld                    sdepl1_tell
      *
      * Henter parameter-verdier
      *
     c                   eval      w_firm = p_firm
6.nu c                   eval      w_numm = p_numm
     c                   eval      w_suff = p_suff
      *
     c                   eval      *in22 = *on
7.xx  *
7.xx  * Henter opplysninger fra bruker
7.xx  *
7.xx c                   eval      fusrl1_user = l_user
7.xx c     fusrl1_key    chain     fusrl1
7.xx  *
7.xx  * Sett indikator som styrer kostpris i henhold til kode på bruker
7.xx  *
7.xx c                   if        fbko01 = 1
7.xx c                   eval      b_kost = *on
7.xx c                   else
7.xx c                   eval      b_kost = *off
7.xx c                   endif
      *
     c                   endsr
