     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VG560R
      * Beskrivelse . . : Undergruppe, legg til sortiment
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       100903 HKO  Nytt program
      * 6.10  260609 BSA  Oppdatert med sporingsinformasjon
6.20  * 6.20  051110 bof  Utvidet med lager på sortiment
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fvugrl3    if   e           k disk    rename(vugrpfr:vugrl3r)
     fvugrl4    if   e           k disk    rename(vugrpfr:vugrl4r)
     fvugrl2    if   e           k disk    rename(vugrpfr:vugrl2r)
     fvvarla    if   e           k disk    rename(vvarpfr:vvarlar)
     fvsdtl1    if   e           k disk    rename(vsdtpfr:vsdtl1r)
     fvsdtlu    uf a e           k disk    rename(vsdtpfr:vsdtlur)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
      *
     fvg560d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vgufir)
     d p_vsor          s                   like(vdvsor)
     d p_ldor          s                   like(vdldor)
6.20 d p_lage          s                   like(vdlage)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d vugrl3_uhgr     s                   like(vguhgr)
     d vugrl4_uugr     s                   like(vguugr)
     d vugrl2_utxt     s                   like(vgutxt)
     d vvarla_ldor     s                   like(vvldor)
     d vvarla_ogrp     s                   like(vvogrp)
     d vvarla_hgrp     s                   like(vvhgrp)
     d vvarla_ugrp     s                   like(vvugrp)
     d vsdtl1_vsor     s                   like(vdvsor)
6.20 d vsdtl1_lage     s                   like(vdlage)
     d vsdtl1_ldor     s                   like(vdldor)
     d vsdtl1_ogrp     s                   like(vdogrp)
     d vsdtl1_hgrp     s                   like(vdhgrp)
     d vsdtl1_ugrp     s                   like(vdugrp)
     d vsdtl1_modn     s                   like(vdmodn)
     d vsdtl1_vare     s                   like(vdvare)
     d rlevl1_levr     s                   like(rllevr)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
      *
     d w_firm          s                   like(vgufir)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(10)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2uogr <> 0 or
     c                             b2uhgr <> 0 or
     c                             b2uugr <> 0 or
     c                             b2utxt <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'H'
     c                   eval      vugrl3_uhgr = b1uhgr
     c     vugrl3_key    setll     vugrl3
     c                   when      w_seqe = 'U'
     c                   eval      vugrl4_uugr = b1uugr
     c     vugrl4_key    setll     vugrl4
     c                   when      w_seqe = 'B'
     c                   eval      vugrl2_utxt = b1utxt
     c     vugrl2_key    setll     vugrl2
     c                   other
     c                   eval      vugrl1_uogr = b1uogr
     c                   eval      vugrl1_uhgr = b1uhgr
     c                   eval      vugrl1_uugr = b1uugr
     c     vugrl1_key    setll     vugrl1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på overgruppe
      *
     c                   if        b2uogr <> 0
     c                   eval      w_seqe = ' '
     c                   eval      vugrl1_uogr = b2uogr
     c                   eval      vugrl1_uhgr = b2uhgr
     c                   eval      vugrl1_uugr = b2uugr
     c     vugrl1_key    setll     vugrl1
     c                   goto      end_pos
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        b2uhgr <> 0
     c                   eval      w_seqe = 'H'
     c                   eval      vugrl3_uhgr = b2uhgr
     c     vugrl3_key    setll     vugrl3
     c                   goto      end_pos
     c                   endif
      *
      * Undergruppe
      *
     c                   if        b2uugr <> 0
     c                   eval      w_seqe = 'U'
     c                   eval      vugrl4_uugr = b2uugr
     c     vugrl4_key    setll     vugrl4
     c                   goto      end_pos
     c                   endif
      *
      * Beskrivelse
      *
     c                   if        b2utxt <> *blank
     c                   eval      w_seqe = 'B'
     c                   eval      vugrl2_utxt = b2utxt
     c     vugrl2_key    setll     vugrl2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2uogr = 0
     c                   eval      b2uhgr = 0
     c                   eval      b2uugr = 0
     c                   eval      b2utxt = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Legg til sortiment
      *
     c                   if        b1valg = 1
     c                   exsr      opprett
     c                   move      '->'          b1utxt
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Moduler
      *
     c                   if        b1valg = 7
     c                   call      'VV560R'
     c                   parm                    p_firm
     c                   parm                    p_vsor
6.20 c                   parm                    p_lage
     c                   parm                    p_ldor
     c                   parm                    b1uogr
     c                   parm                    b1uhgr
     c                   parm                    b1uugr
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'H'
     c                   read      vugrl3
     c                   when      w_seqe = 'U'
     c                   read      vugrl4
     c                   when      w_seqe = 'B'
     c                   read      vugrl2
     c                   other
     c                   read      vugrl1
     c                   endsl
      *
     c                   if        %eof or
     c                             vgufir <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      vvarla_ldor = p_ldor
     c                   eval      vvarla_ogrp = vguogr
     c                   eval      vvarla_hgrp = vguhgr
     c                   eval      vvarla_ugrp = vguugr
     c     vvarla_key    chain     vvarla
     c                   if        not %found
     c                   iter
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'H'
     c                   eval      vugrl3_uhgr = vguhgr
     c                   when      w_seqe = 'U'
     c                   eval      vugrl4_uugr = vguugr
     c                   when      w_seqe = 'B'
     c                   eval      vugrl2_utxt = vgutxt
     c                   other
     c                   eval      vugrl1_uogr = vguogr
     c                   eval      vugrl1_uhgr = vguhgr
     c                   eval      vugrl1_uugr = vguugr
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1uogr = vguogr
     c                   eval      b1uhgr = vguhgr
     c                   eval      b1uugr = vguugr
     c                   eval      b1utxt = vgutxt
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'H'
     c     vugrl3_key    setgt     vugrl3
     c                   readp     vugrl3
     c                   when      w_seqe = 'U'
     c     vugrl4_key    setgt     vugrl4
     c                   readp     vugrl4
     c                   when      w_seqe = 'B'
     c     vugrl2_key    setgt     vugrl2
     c                   readp     vugrl2
     c                   other
     c     vugrl1_key    setgt     vugrl1
     c                   readp     vugrl1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'H'
     c                   readp     vugrl3
     c                   when      w_seqe = 'U'
     c                   readp     vugrl4
     c                   when      w_seqe = 'B'
     c                   readp     vugrl2
     c                   other
     c                   readp     vugrl1
     c                   endsl
      *
     c                   if        %eof or
     c                             vgufir <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'H'
     c                   eval      vugrl3_uhgr = vguhgr
     c                   when      w_seqe = 'U'
     c                   eval      vugrl4_uugr = vguugr
     c                   when      w_seqe = 'B'
     c                   eval      vugrl2_utxt = vgutxt
     c                   other
     c                   eval      vugrl1_uogr = vguogr
     c                   eval      vugrl1_uhgr = vguhgr
     c                   eval      vugrl1_uugr = vguugr
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'H'
     c     vugrl3_key    setll     vugrl3
     c                   when      w_seqe = 'U'
     c     vugrl4_key    setll     vugrl4
     c                   when      w_seqe = 'B'
     c     vugrl2_key    setll     vugrl2
     c                   other
     c     vugrl1_key    setll     vugrl1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å opprette i varesortimnet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     opprett       begsr
      *
     c                   eval      vsdtl1_vsor = p_vsor
6.20 c                   eval      vsdtl1_lage = p_lage
     c                   eval      vsdtl1_ldor = p_ldor
     c                   eval      vsdtl1_ogrp = b1uogr
     c                   eval      vsdtl1_hgrp = b1uhgr
     c                   eval      vsdtl1_ugrp = b1uugr
     c                   eval      vsdtl1_modn = 0
     c                   eval      vsdtl1_vare = *blank
     c     vsdtl1_key    chain     vsdtl1
     c                   if        %found
     c                   goto      end_opprett
     c                   endif
      *
      * Oppretter varesortiment-linje
      *
     c                   clear                   vsdtlur
      *
     c                   eval      vdvsor = p_vsor
6.20 c                   eval      vdlage = p_lage
     c                   eval      vdldor = p_ldor
     c                   eval      vdogrp = b1uogr
     c                   eval      vdhgrp = b1uhgr
     c                   eval      vdugrp = b1uugr
      *
     c                   time                    vdodat
6.10 c                   time                    vdotim
6.10 c                   eval      vdousr = l_user
     c                   time                    vdedat
     c                   time                    vdetim
     c                   eval      vdeusr = l_user
      *
     c                   write     vsdtlur
      *
     c     end_opprett   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vsor
6.20 c                   parm                    p_lage
     c                   parm                    p_ldor
      *
      * Key for posisjonering på overgruppe/hovedgruppe/undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for posisjonering på hovedgruppe
      *
     c     vugrl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl3_uhgr
      *
      * Key for posisjonering på undergruppe
      *
     c     vugrl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl4_uugr
      *
      * Key for posisjonering på beskrivelse
      *
     c     vugrl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl2_utxt
      *
      * Key for oppslag på vare
      *
     c     vvarla_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarla_ldor
     c                   kfld                    vvarla_ogrp
     c                   kfld                    vvarla_hgrp
     c                   kfld                    vvarla_ugrp
      *
      * Key for oppslag på varesortiment-linje
      *
     c     vsdtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vsdtl1_vsor
6.20 c                   kfld                    vsdtl1_lage
     c                   kfld                    vsdtl1_ldor
     c                   kfld                    vsdtl1_ogrp
     c                   kfld                    vsdtl1_hgrp
     c                   kfld                    vsdtl1_ugrp
     c                   kfld                    vsdtl1_modn
     c                   kfld                    vsdtl1_vare
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      *
      * Henter parametre, og initierer skjermbildet
      *
     c                   eval      w_firm = p_firm
      *
     c                   eval      *in22 = *on
      *
     c                   eval      rlevl1_levr = p_ldor
     c     rlevl1_key    chain     rlevl1
      *
     c                   eval      b2vsor = p_vsor
6.20 c                   eval      b2lage = p_lage
     c                   eval      b2ldor = p_ldor
     c                   eval      b2lnav = rlnavn
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      vugrl1_uogr = 0
     c                   eval      vugrl1_uhgr = 0
     c                   eval      vugrl1_uugr = 0
     c     vugrl1_key    setll     vugrl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
