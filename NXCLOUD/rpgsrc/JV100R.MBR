     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JV100R
      * Beskrivelse . . : Vare, vedlikehold
      *
      *                   Kaller programmet JV101R for å opprette og endre
      *                   vare (erstatter C2BLD)
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       230899 HKO  Nytt program
      *       141003 HKO  Utvidet parameter ved kall av JV400R
      *       140905 HKO  Lagt inn frisøk
      *                   Endret parameter ved kall av JP500R
      *       121105 HKO  Viser '** SLETTET **' i vareteksten på slettede
      *                   varer
6.11  *       200809 bof  Nye valg på spørring alt.varenr, avgifter og NRF
6.12  *       130810 bof  Tatt med JVSTAT i select/fetch for sikkh.skyld
os71  * os71  151210 bsa  Lagt inn korreksjoner på SQL pga. OS/400 feil
os71  *                   Det er også lagt inn endring på SQL,
os71  *                   som er nødvendig ved overgang til v6.1
6.30  *       080114 bof  BM Modul - nytt valg for Oppsalg /Mersalg
6.31  *       110114 bof  Overgang til Fritekst
6.32  *       130114 bof  Overgang til Består av
6.33  *       130114 bof  Nytt kall for pakning
6.34  *       140714 bof  Nytt valg for Vare-leverandør
6.35  *       211014 bsa  Justeringer i skjermbildet grunnet jacada + F tast
6.35  *                   for NOBB informasjon.
6.3q  *       260515 bof  Datoformat sql
6.3x  * 6.30  040615 bof  utvidet parm i forb. med prgr. på betingelse
6.36  *       300616 bof  Viser om varen er utgått (i følge JVLEPF)
6.37  *       301018 bkv  Farger på TLS
7.xx  * 7.00  030616 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
8.01  *PRG2   140622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
8.02  *PRG2   160624 BOF  Utvidet med valg 20 = Egenskaper
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjvsost    if   e           k disk    rename(jvsost:jvsostr)
      *
     fjvarlr    if   e           k disk    rename(jvarpfr:jvarlrr)
     fjvarla    if   e           k disk    rename(jvarpfr:jvarlar)
     fjvarlb    if   e           k disk    rename(jvarpfr:jvarlbr)
     fjvarlc    if   e           k disk    rename(jvarpfr:jvarlcr)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjvarl2    if   e           k disk    rename(jvarpfr:jvarl2r)
     fjvarl3    if   e           k disk    rename(jvarpfr:jvarl3r)
     fjvarl4    if   e           k disk    rename(jvarpfr:jvarl4r)
     fjsokl3    if   e           k disk    rename(jsokpfr:jsokl3r)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
6.30 famodl1    if   e           k disk    rename(amodpfr:amodl1r)
6.36 fjvlelr    if   e           k disk    rename(jvlepfr:jvlelrr)
      *
     fjv100d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variable i nøkler
      *
     d jvarlr_vare     s                   like(jvvare)
     d jvarla_ogrp     s                   like(jvogrp)
     d jvarlb_ogrp     s                   like(jvogrp)
     d jvarlb_hgrp     s                   like(jvhgrp)
     d jvarlc_ogrp     s                   like(jvogrp)
     d jvarlc_hgrp     s                   like(jvhgrp)
     d jvarlc_ugrp     s                   like(jvugrp)
     d jvarl1_vare     s                   like(jvvare)
     d jvarl2_tek1     s                   like(jvtek1)
     d jvarl3_nobb     s                   like(jvnobb)
     d jvarl4_lvar     s                   like(jvlvar)
     d jsokl3_vare     s                   like(jsvare)
     d jsokl3_stek     s                   like(jsstek)
     d jlevl1_ldor     s                   like(jlldor)
6.30 d amodl1_modu     s                   like(axmodu)
6.36 d jvlelr_lvnr     s                   like(jvlvnr)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              2
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_inlr          s              1    inz(*off)
6.36 d b_akti          s              1    inz(*off)
     d b_open_sok      s              1    inz(*off)
      *
     d w_firm          s              3  0
     d w_ste1          s             35
     d w_ste2          s             35
     d w_ste3          s             35
     d w_ste4          s             35
     d w_ste5          s             35
     d w_leng          s              2  0
     d w_otxt          s             35
     d w_htxt          s             35
     d w_utxt          s             35
     d w_valg          s              1  0
     d w_lety          s              1  0
8.01 d w_prgr          s              2
6.36 d w_udat          s                   like(jvluda)
      *
6.35 d p_nobb          s              8
      *
     d s_stek          s             35
     d s_ftek          s             35
      *
     d w_vare          s                   like(jvvare)
     d w_pgiv          s                   like(jvldor)
      *
     d s_vare          s                   like(jvvare)
     d s_ogrp          s                   like(jvogrp)
     d s_hgrp          s                   like(jvhgrp)
     d s_ugrp          s                   like(jvugrp)
      *
6.37 d w_undv          s              8  0
6.37 d w_logl          s             15
6.37 d w_jvqvnr        s                   like(jvvare)
6.37 d w_jvquno        s              8  0
      *
      * Konstanter
      *
7.xx d c_sfil          s              2  0 inz(9)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.3q C/Exec SQL
6.3q C+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR
6.3q C/End-Exec
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2taga
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
6.37 c                   when      *inkm
6.37 c                   exsr      xb13win
6.37 c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * F9=Gjenta søk
      *
     c                   if        *inki = *on
     c                   eval      *in22 = *on
     c                   eval      b2ogrp = s_ogrp
     c                   eval      b2hgrp = s_hgrp
     c                   eval      b2ugrp = s_ugrp
     c                   eval      b2stek = s_stek
     c                   eval      b2ftek = s_ftek
     c                   goto      b2tagb
     c                   endif
      *
      * Søking
      *
     c                   if        b2ogrp <> 0 or
     c                             b2hgrp <> 0 or
     c                             b2ugrp <> 0 or
     c                             b2stek <> *blank or
     c                             b2ftek <> *blank
     c                   exsr      sØk
     c                   goto      b2tagb
     c                   endif
      *
      * Posisjonering
      *
     c                   if        b2tek1 <> *blank or
     c                             b2vare <> 0 or
     c                             b2nobb <> 0 or
     c                             b2lvar <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'O '
     c     jvarla_key    setll     jvarla
     c                   when      w_seqe = 'H '
     c     jvarlb_key    setll     jvarlb
     c                   when      w_seqe = 'U '
     c     jvarlc_key    setll     jvarlc
     c                   when      w_seqe = 'S ' or
     c                             w_seqe = 'SO' or
     c                             w_seqe = 'SH' or
     c                             w_seqe = 'SU' or
     c                             w_seqe = 'F ' or
     c                             w_seqe = 'FO' or
     c                             w_seqe = 'FH' or
     c                             w_seqe = 'FU'
     c                   eval      b_open_sok = *on
     c                   when      w_seqe = 'V '
     c                   eval      jvarl1_vare = b1vare
     c     jvarl1_key    setll     jvarl1
     c                   when      w_seqe = 'N '
     c                   eval      jvarl3_nobb = b1nobb
     c     jvarl3_key    setll     jvarl3
     c                   when      w_seqe = 'L '
     c                   eval      jvarl4_lvar = b1lvar
     c     jvarl4_key    setll     jvarl4
     c                   other
     c                   eval      jvarl2_tek1 = b1tek1
     c     jvarl2_key    setll     jvarl2
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for søking i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sØk           begsr
      *
      * Validering av varegruppe
      *
     c                   if        b2ogrp = 0 and
     c                             b2hgrp <> 0
     c                   eval      *in31 = *on
     c                   leavesr
     c                   endif
      *
     c                   if        b2hgrp = 0 and
     c                             b2ugrp <> 0
     c                   eval      *in32 = *on
     c                   leavesr
     c                   endif
      *
      * Tar vare på søk
      *
     c                   eval      s_ogrp = b2ogrp
     c                   eval      s_hgrp = b2hgrp
     c                   eval      s_ugrp = b2ugrp
     c                   eval      s_stek = b2stek
     c                   eval      s_ftek = b2ftek
      *
      * Søk via søketekst
      *
     c                   if        b2stek <> *blank
      *
     c                   eval      b_open_sok = *on
      *
     c                   call      'AS702R'
     c                   parm                    b2stek
     c                   parm                    w_ste1
     c                   parm                    w_ste2
     c                   parm                    w_ste3
     c                   parm                    w_ste4
     c                   parm                    w_ste5
      *
     c                   select
     c                   when      b2ogrp = 0 and
     c                             b2hgrp = 0 and
     c                             b2ugrp = 0
     c                   eval      w_seqe = 'S '
     c                   when      b2ogrp <> 0 and
     c                             b2hgrp = 0 and
     c                             b2ugrp = 0
     c                   eval      w_seqe = 'SO'
     c                   when      b2ogrp <> 0 and
     c                             b2hgrp <> 0 and
     c                             b2ugrp = 0
     c                   eval      w_seqe = 'SH'
     c                   when      b2ogrp <> 0 and
     c                             b2hgrp <> 0 and
     c                             b2ugrp <> 0
     c                   eval      w_seqe = 'SU'
     c                   endsl
      *
     c                   goto      end_sØk
      *
     c                   endif
      *
      * Søk via fritekst
      *
     c                   if        b2ftek <> *blank
      *
     c                   eval      b_open_sok = *on
      *
     c                   call      'AS701R'
     c                   parm                    b2ftek
     c                   parm                    w_ste1
     c                   parm                    w_ste2
     c                   parm                    w_ste3
     c                   parm                    w_ste4
     c                   parm                    w_ste5
      *
     c                   select
     c                   when      b2ogrp = 0 and
     c                             b2hgrp = 0 and
     c                             b2ugrp = 0
     c                   eval      w_seqe = 'F '
     c                   when      b2ogrp <> 0 and
     c                             b2hgrp = 0 and
     c                             b2ugrp = 0
     c                   eval      w_seqe = 'FO'
     c                   when      b2ogrp <> 0 and
     c                             b2hgrp <> 0 and
     c                             b2ugrp = 0
     c                   eval      w_seqe = 'FH'
     c                   when      b2ogrp <> 0 and
     c                             b2hgrp <> 0 and
     c                             b2ugrp <> 0
     c                   eval      w_seqe = 'FU'
     c                   endsl
      *
     c                   goto      end_sØk
      *
     c                   endif
      *
      * Søk på overgruppe
      *
     c                   if        b2ogrp <> 0 and
     c                             b2hgrp =  0 and
     c                             b2ugrp =  0
     c                   eval      w_seqe = 'O '
     c                   eval      jvarla_ogrp = b2ogrp
     c     jvarla_key    setll     jvarla
     c                   goto      end_sØk
     c                   endif
      *
      * Søk på hovedgruppe
      *
     c                   if        b2ogrp <> 0 and
     c                             b2hgrp <> 0 and
     c                             b2ugrp =  0
     c                   eval      w_seqe = 'H '
     c                   eval      jvarlb_ogrp = b2ogrp
     c                   eval      jvarlb_hgrp = b2hgrp
     c     jvarlb_key    setll     jvarlb
     c                   goto      end_sØk
     c                   endif
      *
      * Søk på undergruppe
      *
     c                   if        b2ogrp <> 0 and
     c                             b2hgrp <> 0 and
     c                             b2ugrp <> 0
     c                   eval      w_seqe = 'U '
     c                   eval      jvarlc_ogrp = b2ogrp
     c                   eval      jvarlc_hgrp = b2hgrp
     c                   eval      jvarlc_ugrp = b2ugrp
     c     jvarlc_key    setll     jvarlc
     c                   goto      end_sØk
     c                   endif
      *
     c     end_sØk       tag
      *
      * Blanker subfile, og fyller opp i.h.t. søk
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker søkefelt
      *
     c                   eval      b2ogrp = 0
     c                   eval      b2hgrp = 0
     c                   eval      b2ugrp = 0
     c                   eval      b2stek = *blank
     c                   eval      b2ftek = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Varetekst
      *
     c                   if        b2tek1 <> *blank
     c                   eval      w_seqe = *blank
     c                   eval      jvarl2_tek1 = b2tek1
     c     jvarl2_key    setll     jvarl2
     c                   goto      end_pos
     c                   endif
      *
      * Varenummer
      *
     c                   if        b2vare <> 0
     c                   eval      w_seqe = 'V '
     c                   movel     b2vare        jvarl1_vare
     c     jvarl1_key    setll     jvarl1
     c                   goto      end_pos
     c                   endif
      *
      * Nobb-nummer
      *
     c                   if        b2nobb <> 0
     c                   eval      w_seqe = 'N '
     c                   eval      jvarl3_nobb = b2nobb
     c     jvarl3_key    setll     jvarl3
     c                   goto      end_pos
     c                   endif
      *
      * Lev. varenummer
      *
     c                   if        b2lvar <> *blank
     c                   eval      w_seqe = 'L '
     c                   eval      jvarl4_lvar = b2lvar
     c     jvarl4_key    setll     jvarl4
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2tek1 = *blank
     c                   eval      b2vare = 0
     c                   eval      b2nobb = 0
     c                   eval      b2lvar = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
     c                   movel     b1vare        w_vare
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   movel     b1vare        c1vare
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
6.37  * sjekk om logistikk-vare
6.37 c                   eval      w_jvqvnr = b1vare
6.37 c                   exsr      finn_logivar
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   movel     b1vare        k1vare
     c                   exsr      xk1win
     c                   eval      b1valg = 0
6.37  * sjekk om logistikk-vare
6.37 c                   eval      w_jvqvnr = b1vare
6.37 c                   exsr      finn_logivar
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1vare = b1vare
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   call      'JV510R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   eval      b1valg = 0
6.37  * sjekk om logistikk-vare
6.37 c                   eval      w_jvqvnr = b1vare
6.37 c                   exsr      finn_logivar
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vedlikehold av vare-pris
      *
     c                   if        b1valg = 7
     c                   call      'JX100R'
     c                   parm                    w_vare
     c                   eval      b1valg = 0
6.37  * sjekk om logistikk-vare
6.37 c                   eval      w_jvqvnr = b1vare
6.37 c                   exsr      finn_logivar
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vedlikehold av vare-pakning
      *
     c                   if        b1valg = 8
6.33 c                   call      'JW505R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   eval      b1valg = 0
6.37  * sjekk om logistikk-vare
6.37 c                   eval      w_jvqvnr = b1vare
6.37 c                   exsr      finn_logivar
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vedlikehold av søketekst
      *
     c                   if        b1valg = 9
     c                   call      'JS100R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   eval      b1valg = 0
6.37  * sjekk om logistikk-vare
6.37 c                   eval      w_jvqvnr = b1vare
6.37 c                   exsr      finn_logivar
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vedlikehold av betingelser
      *
     c                   if        b1valg = 10
     c                   eval      w_pgiv = 0
     c                   eval      w_lety = 0
6.3x c                   eval      w_prgr = *blank
     c                   call      'JP500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_pgiv
6.3x c                   parm                    w_prgr
     c                   parm                    w_lety
     c                   eval      b1valg = 0
6.37  * sjekk om logistikk-vare
6.37 c                   eval      w_jvqvnr = b1vare
6.37 c                   exsr      finn_logivar
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vise produktinformasjon
      *
     c                   if        b1valg = 11
     c                   call      'JU510R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   eval      b1valg = 0
6.37  * sjekk om logistikk-vare
6.37 c                   eval      w_jvqvnr = b1vare
6.37 c                   exsr      finn_logivar
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vise kampanjepriser
      *
     c                   if        b1valg = 12
     c                   call      'JM510R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    b1tek1
     c                   eval      b1valg = 0
6.37  * sjekk om logistikk-vare
6.37 c                   eval      w_jvqvnr = b1vare
6.37 c                   exsr      finn_logivar
     c                   update    b1sfl
     c                   iter
     c                   endif
6.12  *
6.12  * Spørr Alt. Varenr
6.12  *
6.12 c                   if        b1valg = 13
6.12 c                   eval      w_valg = 5
6.12 c                   call      'JV113R'
6.12 c                   parm                    b1nobb
6.12 c                   parm                    w_valg
6.12 c                   eval      b1valg = 0
6.37  * sjekk om logistikk-vare
6.37 c                   eval      w_jvqvnr = b1vare
6.37 c                   exsr      finn_logivar
6.12 c                   update    b1sfl
6.12 c                   iter
6.12 c                   endif
6.12  *
6.12  * Spørr Avgifter
6.12  *
6.12 c                   if        b1valg = 14
6.12 c                   eval      w_valg = 5
6.12 c                   call      'JV114R'
6.12 c                   parm                    b1nobb
6.12 c                   parm                    w_valg
6.12 c                   eval      b1valg = 0
6.37  * sjekk om logistikk-vare
6.37 c                   eval      w_jvqvnr = b1vare
6.37 c                   exsr      finn_logivar
6.12 c                   update    b1sfl
6.12 c                   iter
6.12 c                   endif
6.12  *
6.12  * Spørr NRF
6.12  *
6.12 c                   if        b1valg = 15
6.12 c                   eval      w_valg = 5
6.12 c                   call      'JV115R'
6.12 c                   parm                    b1nobb
6.12 c                   parm                    w_valg
6.12 c                   eval      b1valg = 0
6.37  * sjekk om logistikk-vare
6.37 c                   eval      w_jvqvnr = b1vare
6.37 c                   exsr      finn_logivar
6.12 c                   update    b1sfl
6.12 c                   iter
6.12 c                   endif
6.30  *
6.30  * Oppsalg/Mersalg    BM_MODUL
6.30  *
6.30 c                   if        b1valg = 16 and
6.30 c                             *in29  = *on
6.30 c                   eval      w_valg = 2
6.30 c                   call      'JV116R'
6.30 c                   parm                    b1nobb
6.30 c                   parm                    w_valg
6.30 c                   eval      b1valg = 0
6.37  * sjekk om logistikk-vare
6.37 c                   eval      w_jvqvnr = b1vare
6.37 c                   exsr      finn_logivar
6.30 c                   update    b1sfl
6.30 c                   iter
6.30 c                   endif
6.31  *
6.31  * Spørr på fritekst
6.31  *
6.31 c                   if        b1valg = 17
6.31 c                   eval      w_valg = 5
6.31 c                   call      'JV512R'
6.31 c                   parm                    w_firm
6.31 c                   parm                    b1vare
6.31 c                   eval      b1valg = 0
6.37  * sjekk om logistikk-vare
6.37 c                   eval      w_jvqvnr = b1vare
6.37 c                   exsr      finn_logivar
6.31 c                   update    b1sfl
6.31 c                   iter
6.31 c                   endif
6.32  *
6.32  * Spørr på Består av
6.32  *
6.32 c                   if        b1valg = 18
6.32 c                   call      'JV117R'
6.32 c                   parm                    w_vare
6.32 c                   eval      b1valg = 0
6.37  * sjekk om logistikk-vare
6.37 c                   eval      w_jvqvnr = b1vare
6.37 c                   exsr      finn_logivar
6.32 c                   update    b1sfl
6.32 c                   iter
6.32 c                   endif
6.32  *
6.32  * Spørr på Vare-levernandør
6.32  *
6.32 c                   if        b1valg = 19
6.32 c                   call      'JV118R'
6.32 c                   parm                    w_vare
6.32 c                   eval      b1valg = 0
6.37  * sjekk om logistikk-vare
6.37 c                   eval      w_jvqvnr = b1vare
6.37 c                   exsr      finn_logivar
6.32 c                   update    b1sfl
6.32 c                   iter
6.32 c                   endif
8.02  *
8.02  * Spørr Egenskaper
8.02  *
8.02 c                   if        b1valg = 20
8.02 c                   call      'JV190R'
8.02 c                   parm                    b1nobb
8.02 c                   eval      b1valg = 0
8.02  * sjekk om logistikk-vare
8.02 c                   eval      w_jvqvnr = b1vare
8.02 c                   exsr      finn_logivar
8.02 c                   update    b1sfl
8.02 c                   iter
8.02 c                   endif

      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1vare = b2vare
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Dersom varenummer er oppgitt kreves det av varen finnes
      * Sender opp informasjonsbilde dersom varen finnes fra før
      *
     c                   if        c1vare <> 0
     c                   movel     c1vare        jvarlr_vare
     c     jvarlr_key    chain     jvarlr
     c                   if        not %found
     c                   eval      *in34 = *on
     c                   goto      c1taga
     c                   else
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1vare = 0
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      c1vare = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_valg = b1valg
      *
     c                   movel     c1vare        w_vare
      *
     c                   call      'JV101R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_valg
      *
     c                   movel     w_vare        c1vare
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
      *
     c                   eval      jvarlr_vare = w_vare
     c     jvarlr_key    chain     jvarlr
     c                   if        %found
     c                   eval      b1vare = jvvare
     c                   eval      b1nobb = jvnobb
     c                   eval      b1tek1 = jvtek1
     c                   endif
     c                   eval      jlevl1_ldor = jvldor
     c     jlevl1_key    chain     jlevl1
     c                   if        %found
     c                   eval      b1navn = jlnavn
     c                   endif
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      jvarlr_vare = d1vare
     c     jvarlr_key    chain     jvarlr
      *
     c     d1tagb        tag
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
6.35  *
6.35  * F18=NOBB informasjon
6.35  *
6.35 c                   if        *inks = *on
6.35 c                   exsr      vis_nobb
6.35 c                   goto      d1tagb
6.35 c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Kaller program for sletting av vare m/relasjoner
      *
     c                   eval      w_vare = d1vare
      *
     c                   call      'JV400R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    b_inlr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Fyller kopierings-bildet
      *
     c                   movel     k1vare        jvarlr_vare
     c     jvarlr_key    chain     jvarlr
      *
     c     k1tagb        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   movel     k1vare        c1vare
     c                   exsr      xc2bld
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   eval      s_vare = *blank
      *
      * Initierer SQL for søk
      *
     c                   if        b_open_sok = *on
      *
     c                   select
     c                   when      w_seqe = 'SO'
     c/exec sql
os71 c+                  declare sok_so cursor for
os71 c+                  select s.jsvare,
os71 c+                         s.jsstek,
os71 c+                         v.jvvare,
os71 c+                         v.jvogrp,
os71 c+                         v.jvtek1,
os71 c+                         v.jvnobb,
os71 c+                         v.jvlvar,
6.12 c+                         v.jvstat,
os71 c+                         v.jvldor
os71 c+                  from   jsokpf as s inner join
os71 c+                         jvarpf as v on
os71 c+                         s.jsvare = v.jvvare
os71 c+                  where  v.jvogrp = :b2ogrp and
os71 c+                         s.jsstek like :w_ste1
os71 c+                  order by v.jvtek1, v.jvvare
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_so
     c/end-exec
     c/exec sql
     c+                  open sok_so
     c/end-exec
      *
     c                   when      w_seqe = 'SH'
     c/exec sql
     c+                  declare sok_sh cursor for
os71 c+                  select s.jsvare,
os71 c+                         s.jsstek,
os71 c+                         v.jvvare,
os71 c+                         v.jvogrp,
os71 c+                         v.jvhgrp,
os71 c+                         v.jvtek1,
os71 c+                         v.jvnobb,
os71 c+                         v.jvlvar,
6.12 c+                         v.jvstat,
os71 c+                         v.jvldor
os71 c+                  from   jsokpf as s inner join
os71 c+                         jvarpf as v on
os71 c+                         s.jsvare = v.jvvare
os71 c+                  where  v.jvogrp = :b2ogrp and
os71 c+                         v.jvhgrp = :b2hgrp and
os71 c+                         s.jsstek like :w_ste1
os71 c+                  order by v.jvtek1, v.jvvare
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_sh
     c/end-exec
     c/exec sql
     c+                  open sok_sh
     c/end-exec
      *
     c                   when      w_seqe = 'SU'
     c/exec sql
     c+                  declare sok_su cursor for
os71 c+                  select s.jsvare,
os71 c+                         s.jsstek,
os71 c+                         v.jvvare,
os71 c+                         v.jvogrp,
os71 c+                         v.jvhgrp,
os71 c+                         v.jvugrp,
os71 c+                         v.jvtek1,
os71 c+                         v.jvnobb,
os71 c+                         v.jvlvar,
6.12 c+                         v.jvstat,
os71 c+                         v.jvldor
os71 c+                  from   jsokpf as s inner join
os71 c+                         jvarpf as v on
os71 c+                         s.jsvare = v.jvvare
os71 c+                  where  v.jvogrp = :b2ogrp and
os71 c+                         v.jvhgrp = :b2hgrp and
os71 c+                         v.jvugrp = :b2ugrp and
os71 c+                         s.jsstek like :w_ste1
os71 c+                  order by v.jvtek1, v.jvvare
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_su
     c/end-exec
     c/exec sql
     c+                  open sok_su
     c/end-exec
      *
     c                   when      w_seqe = 'S '
     c/exec sql
     c+                  declare sok_s cursor for
os71 c+                  select s.jsvare,
os71 c+                         s.jsstek,
os71 c+                         v.jvvare,
os71 c+                         v.jvtek1,
os71 c+                         v.jvnobb,
os71 c+                         v.jvlvar,
6.12 c+                         v.jvstat,
os71 c+                         v.jvldor
os71 c+                  from   jsokpf as s inner join
os71 c+                         jvarpf as v on
os71 c+                         s.jsvare = v.jvvare
os71 c+                  where  s.jsstek like :w_ste1
os71 c+                  order by v.jvtek1, v.jvvare
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_s
     c/end-exec
     c/exec sql
     c+                  open sok_s
     c/end-exec
      *
     c                   when      w_seqe = 'FO'
     c/exec sql
     c+                  declare sok_fo cursor for
os71 c+                  select s.jvsvar,
os71 c+                         s.jvstek,
os71 c+                         v.jvvare,
os71 c+                         v.jvogrp,
os71 c+                         v.jvtek1,
os71 c+                         v.jvnobb,
os71 c+                         v.jvlvar,
6.12 c+                         v.jvstat,
os71 c+                         v.jvldor
os71 c+                  from   jvsost as s inner join
os71 c+                         jvarpf as v on
os71 c+                         s.jvsvar = v.jvvare
os71 c+                  where  v.jvogrp = :b2ogrp and
os71 c+                         s.jvstek like :w_ste1 and
os71 c+                         s.jvstek like :w_ste2 and
os71 c+                         s.jvstek like :w_ste3 and
os71 c+                         s.jvstek like :w_ste4 and
os71 c+                         s.jvstek like :w_ste5
os71 c+                  order by s.jvstek, s.jvsvar
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_fo
     c/end-exec
     c/exec sql
     c+                  open sok_fo
     c/end-exec
      *
     c                   when      w_seqe = 'FH'
     c/exec sql
     c+                  declare sok_fh cursor for
os71 c+                  select s.jvsvar,
os71 c+                         s.jvstek,
os71 c+                         v.jvvare,
os71 c+                         v.jvogrp,
os71 c+                         v.jvhgrp,
os71 c+                         v.jvtek1,
os71 c+                         v.jvnobb,
os71 c+                         v.jvlvar,
6.12 c+                         v.jvstat,
os71 c+                         v.jvldor
os71 c+                  from   jvsost as s inner join
os71 c+                         jvarpf as v on
os71 c+                         s.jvsvar = v.jvvare
os71 c+                  where  v.jvogrp = :b2ogrp and
os71 c+                         v.jvhgrp = :b2hgrp and
os71 c+                         s.jvstek like :w_ste1 and
os71 c+                         s.jvstek like :w_ste2 and
os71 c+                         s.jvstek like :w_ste3 and
os71 c+                         s.jvstek like :w_ste4 and
os71 c+                         s.jvstek like :w_ste5
os71 c+                  order by s.jvstek, s.jvsvar
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_fh
     c/end-exec
     c/exec sql
     c+                  open sok_fh
     c/end-exec
      *
     c                   when      w_seqe = 'FU'
     c/exec sql
     c+                  declare sok_fu cursor for
os71 c+                  select s.jvsvar,
os71 c+                         s.jvstek,
os71 c+                         v.jvvare,
os71 c+                         v.jvogrp,
os71 c+                         v.jvhgrp,
os71 c+                         v.jvugrp,
os71 c+                         v.jvtek1,
os71 c+                         v.jvnobb,
os71 c+                         v.jvlvar,
6.12 c+                         v.jvstat,
os71 c+                         v.jvldor
os71 c+                  from   jvsost as s inner join
os71 c+                         jvarpf as v on
os71 c+                         s.jvsvar = v.jvvare
os71 c+                  where  v.jvogrp = :b2ogrp and
os71 c+                         v.jvhgrp = :b2hgrp and
os71 c+                         v.jvugrp = :b2ugrp and
os71 c+                         s.jvstek like :w_ste1 and
os71 c+                         s.jvstek like :w_ste2 and
os71 c+                         s.jvstek like :w_ste3 and
os71 c+                         s.jvstek like :w_ste4 and
os71 c+                         s.jvstek like :w_ste5
os71 c+                  order by s.jvstek, s.jvsvar
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_fu
     c/end-exec
     c/exec sql
     c+                  open sok_fu
     c/end-exec
      *
     c                   when      w_seqe = 'F '
     c/exec sql
     c+                  declare sok_f cursor for
os71 c+                  select s.jvsvar,
os71 c+                         s.jvstek,
os71 c+                         v.jvvare,
os71 c+                         v.jvtek1,
os71 c+                         v.jvnobb,
os71 c+                         v.jvlvar,
6.12 c+                         v.jvstat,
os71 c+                         v.jvldor
os71 c+                  from   jvsost as s inner join
os71 c+                         jvarpf as v on
os71 c+                         s.jvsvar = v.jvvare
os71 c+                  where  s.jvstek like :w_ste1 and
os71 c+                         s.jvstek like :w_ste2 and
os71 c+                         s.jvstek like :w_ste3 and
os71 c+                         s.jvstek like :w_ste4 and
os71 c+                         s.jvstek like :w_ste5
os71 c+                  order by s.jvstek, s.jvsvar
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_f
     c/end-exec
     c/exec sql
     c+                  open sok_f
     c/end-exec
      *
     c                   endsl
      *
     c                   endif
      *
      * Fyller opp subfile
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'O '
     c     jvarla_key    reade     jvarla                                 15
     c                   when      w_seqe = 'H '
     c     jvarlb_key    reade     jvarlb                                 15
     c                   when      w_seqe = 'U '
     c     jvarlc_key    reade     jvarlc                                 15
     c                   when      w_seqe = 'SO'
     c/exec sql
     c+                  fetch next
     c+                  from  sok_so
     c+                  into  :jsvare,
     c+                        :jsstek,
     c+                        :jvvare,
     c+                        :jvogrp,
     c+                        :jvtek1,
     c+                        :jvnobb,
     c+                        :jvlvar,
6.12 c+                        :jvstat,
     c+                        :jvldor
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'SH'
     c/exec sql
     c+                  fetch next
     c+                  from  sok_sh
     c+                  into  :jsvare,
     c+                        :jsstek,
     c+                        :jvvare,
     c+                        :jvogrp,
     c+                        :jvhgrp,
     c+                        :jvtek1,
     c+                        :jvnobb,
     c+                        :jvlvar,
6.12 c+                        :jvstat,
     c+                        :jvldor
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'SU'
     c/exec sql
     c+                  fetch next
     c+                  from  sok_su
     c+                  into  :jsvare,
     c+                        :jsstek,
     c+                        :jvvare,
     c+                        :jvogrp,
     c+                        :jvhgrp,
     c+                        :jvugrp,
     c+                        :jvtek1,
     c+                        :jvnobb,
     c+                        :jvlvar,
6.12 c+                        :jvstat,
     c+                        :jvldor
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'S '
     c/exec sql
     c+                  fetch next
     c+                  from  sok_s
     c+                  into  :jsvare,
     c+                        :jsstek,
     c+                        :jvvare,
     c+                        :jvtek1,
     c+                        :jvnobb,
     c+                        :jvlvar,
6.12 c+                        :jvstat,
     c+                        :jvldor
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'FO'
     c/exec sql
     c+                  fetch next
     c+                  from  sok_fo
     c+                  into  :jvsvar,
     c+                        :jvstek,
     c+                        :jvvare,
     c+                        :jvogrp,
     c+                        :jvtek1,
     c+                        :jvnobb,
     c+                        :jvlvar,
6.12 c+                        :jvstat,
     c+                        :jvldor
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'FH'
     c/exec sql
     c+                  fetch next
     c+                  from  sok_fh
     c+                  into  :jvsvar,
     c+                        :jvstek,
     c+                        :jvvare,
     c+                        :jvogrp,
     c+                        :jvhgrp,
     c+                        :jvtek1,
     c+                        :jvnobb,
     c+                        :jvlvar,
6.12 c+                        :jvstat,
     c+                        :jvldor
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'FU'
     c/exec sql
     c+                  fetch next
     c+                  from  sok_fu
     c+                  into  :jvsvar,
     c+                        :jvstek,
     c+                        :jvvare,
     c+                        :jvogrp,
     c+                        :jvhgrp,
     c+                        :jvugrp,
     c+                        :jvtek1,
     c+                        :jvnobb,
     c+                        :jvlvar,
6.12 c+                        :jvstat,
     c+                        :jvldor
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'F '
     c/exec sql
     c+                  fetch next
     c+                  from  sok_f
     c+                  into  :jvsvar,
     c+                        :jvstek,
     c+                        :jvvare,
     c+                        :jvtek1,
     c+                        :jvnobb,
     c+                        :jvlvar,
6.12 c+                        :jvstat,
     c+                        :jvldor
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'V '
     c                   read      jvarl1                                 15
     c                   when      w_seqe = 'N '
     c                   read      jvarl3                                 15
     c                   when      w_seqe = 'L '
     c                   read      jvarl4                                 15
     c                   other
     c                   read      jvarl2                                 15
     c                   endsl
      *
     c                   if        *in15 = *on
     c                   leave
     c                   endif
      *
      * Behandling av søketekst
      *
     c                   if        w_seqe = 'SO' or
     c                             w_seqe = 'SH' or
     c                             w_seqe = 'SU' or
     c                             w_seqe = 'S '
      *
     c                   if        jsvare = s_vare
     c                   iter
     c                   endif
      *
     c                   if        w_ste2 <> *blank
     c                   eval      w_leng = %len(%trim(w_ste2))
     c                   eval      jsokl3_vare = jsvare
     c                   eval      jsokl3_stek = w_ste2
     c     jsokl3_key    setll     jsokl3
     c                   read      jsokl3
     c                   if        %eof or
     c                             jvvare <> jsvare or
     c                             w_ste2 <> %subst(jsstek:1:w_leng)
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        w_ste3 <> *blank
     c                   eval      w_leng = %len(%trim(w_ste3))
     c                   eval      jsokl3_vare = jsvare
     c                   eval      jsokl3_stek = w_ste3
     c     jsokl3_key    setll     jsokl3
     c                   read      jsokl3
     c                   if        %eof or
     c                             jvvare <> jsvare or
     c                             w_ste3 <> %subst(jsstek:1:w_leng)
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        w_ste4 <> *blank
     c                   eval      w_leng = %len(%trim(w_ste4))
     c                   eval      jsokl3_vare = jsvare
     c                   eval      jsokl3_stek = w_ste4
     c     jsokl3_key    setll     jsokl3
     c                   read      jsokl3
     c                   if        %eof or
     c                             jvvare <> jsvare or
     c                             w_ste4 <> %subst(jsstek:1:w_leng)
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        w_ste5 <> *blank
     c                   eval      w_leng = %len(%trim(w_ste5))
     c                   eval      jsokl3_vare = jsvare
     c                   eval      jsokl3_stek = w_ste5
     c     jsokl3_key    setll     jsokl3
     c                   read      jsokl3
     c                   if        %eof or
     c                             jvvare <> jsvare or
     c                             w_ste5 <> %subst(jsstek:1:w_leng)
     c                   iter
     c                   endif
     c                   endif
      *
     c                   eval      s_vare = jsvare
      *
     c                   endif
      *
      * Skriver til subfile
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = *blank
     c                   eval      jvarl2_tek1 = jvtek1
     c                   when      w_seqe = 'V '
     c                   eval      jvarl1_vare = jvvare
     c                   when      w_seqe = 'N '
     c                   eval      jvarl3_nobb = jvnobb
     c                   when      w_seqe = 'L '
     c                   eval      jvarl4_lvar = jvlvar
     c                   endsl
      *
     c                   eval      b1tek1 = jvtek1
     c                   eval      b1vare = jvvare
     c                   eval      b1nobb = jvnobb
     c                   eval      b1lvar = jvlvar
     c                   eval      b1navn = *blank
      *
     c                   if        jvstat = 4
     c                   eval      b1tek1 = %subst(jvtek1:1:21) +
     c                                      ' ** SLETTET **'
     c                   endif
6.36  *
6.36  * sjekk om vare er utgått hos alle prisgivere /vareeiere
6.36  *
6.36 c                   eval      b_akti = *off
6.36 c                   eval      jvlelr_lvnr = jvvare
6.36 c     jvlelr_key    setll     jvlelr
6.36 c     jvlelr_key    reade     jvlelr
6.36 c                   dow       not %eof(jvlelr)
6.36  * hvis minst 1 ikke utgått-> aktiv
6.36 c                   if        jvluda = *loval
6.36 c                   eval      b_akti = *on
6.36 c                   endif
6.36  * hvis utgått dato, men ikke passert i tid--> aktiv
6.36 c                   if        jvluda <> *loval and
6.36 c                             jvluda > w_udat
6.36 c                   eval      b_akti = *on
6.36 c                   endif
6.36 c     jvlelr_key    reade     jvlelr
6.36 c                   enddo
6.36 c                   if        b_akti = *off
6.36 c                   eval      b1tek1 = %subst(jvtek1:1:21) +
6.36 c                                      ' ** UTGÅTT  **'
6.36 c                   endif
      *
     c                   eval      jlevl1_ldor = jvldor
     c     jlevl1_key    chain     jlevl1
     c                   if        %found
     c                   eval      b1navn = jlnavn
     c                   endif
6.37  *
6.37  * sjekk om logistikk-vare
6.37 c                   eval      w_jvqvnr = jvvare
6.37 c                   exsr      finn_logivar
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   eval      b_open_sok = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        w_seqe = 'O ' or
     c                             w_seqe = 'H ' or
     c                             w_seqe = 'U ' or
     c                             w_seqe = 'S ' or
     c                             w_seqe = 'SO' or
     c                             w_seqe = 'SH' or
     c                             w_seqe = 'SU' or
     c                             w_seqe = 'F ' or
     c                             w_seqe = 'FO' or
     c                             w_seqe = 'FH' or
     c                             w_seqe = 'FU'
     c                   exsr      forny
     c                   leavesr
     c                   endif
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'V '
     c     jvarl1_key    setgt     jvarl1
     c                   readp     jvarl1
     c                   when      w_seqe = 'N '
     c     jvarl3_key    setgt     jvarl3
     c                   readp     jvarl3
     c                   when      w_seqe = 'L '
     c     jvarl4_key    setgt     jvarl4
     c                   readp     jvarl4
     c                   other
     c     jvarl2_key    setgt     jvarl2
     c                   readp     jvarl2
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'V '
     c                   readp     jvarl1
     c                   when      w_seqe = 'N '
     c                   readp     jvarl3
     c                   when      w_seqe = 'L '
     c                   readp     jvarl4
     c                   other
     c                   readp     jvarl2
     c                   endsl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'V '
     c                   eval      jvarl1_vare = jvvare
     c                   when      w_seqe = 'N '
     c                   eval      jvarl3_nobb = jvnobb
     c                   when      w_seqe = 'L '
     c                   eval      jvarl4_lvar = jvlvar
     c                   other
     c                   eval      jvarl2_tek1 = jvtek1
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'V '
     c     jvarl1_key    setll     jvarl1
     c                   when      w_seqe = 'N '
     c     jvarl3_key    setll     jvarl3
     c                   when      w_seqe = 'L '
     c     jvarl4_key    setll     jvarl4
     c                   other
     c     jvarl2_key    setll     jvarl2
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Overgruppe
      *
     c                   if        w_afld = 'B2OGRP'
     c                   eval      b2hgrp = 0
     c                   eval      b2ugrp = 0
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    w_otxt
     c                   leavesr
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        w_afld = 'B2HGRP'
     c                   eval      b2ugrp = 0
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    b2hgrp
     c                   parm                    w_htxt
     c                   leavesr
     c                   endif
      *
      * Undergruppe
      *
     c                   if        w_afld = 'B2UGRP'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    b2hgrp
     c                   parm                    b2ugrp
     c                   parm                    w_utxt
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
6.35  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.35  * Vis NOBB informasjon for varen. Samme prinsipp som preview av utskrifter
6.35  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.35  *
6.35 c     vis_nobb      begsr
6.35 c
6.35 c                   eval      p_nobb = %char(jvnobb)
6.35 c                   call      'AP633R'
6.35 c                   parm                    p_nobb
6.35  *
6.35 c                   endsr
6.37  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.37  * Finn info om logistikkvare for riktig prisgiver
6.37  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.37  *
6.37 c     finn_logivar  begsr
6.37  *
6.37 c                   eval      *in27 = *off
6.37 c                   eval      *in28 = *off
6.37 c
6.37 c                   if        %trim(w_jvqvnr) = ''
6.37 c                   leavesr
6.37 c                   endif
6.37 c
6.37 c                   eval      w_logl = ''
6.37 c                   eval      w_undv = 0
     c                   movel     w_jvqvnr      w_jvquno
6.37 c/exec sql
6.37 c+                  select jvquno, jvqvnr
6.37 c+                  into :w_undv, :w_logl
6.37 c+                  from jvklpf
6.37 c+                  where (jvquno = :w_jvquno or
6.37 c+                  jvqvnr = :w_jvqvnr)
6.37 c+                  and jvqfir = :w_firm
6.37 c+                  fetch first 1 rows only
6.37 c/end-exec
6.37 c                   if        w_logl = w_jvqvnr
6.37 c                   eval      *in28 = *on
6.37 c                   elseif    w_undv > 0
6.37 c                   eval      *in27 = *on
6.37 c                   endif
6.37 c
6.37 c                   endsr
6.37  *
6.37  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.37  * Subrutine for å vise info
6.37  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.37  *
6.37 c     xb13win       begsr
6.37  *
6.37 c                   exfmt     b13win
6.37  *
6.37 c     end_xb13win   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på vare
      *
     c     jvarlr_key    klist
     c                   kfld                    jvarlr_vare
      *
      * Key for søk på overgruppe
      *
     c     jvarla_key    klist
     c                   kfld                    jvarla_ogrp
      *
      * Key for søk på hovedgruppe
      *
     c     jvarlb_key    klist
     c                   kfld                    jvarlb_ogrp
     c                   kfld                    jvarlb_hgrp
      *
      * Key for søk på undergruppe
      *
     c     jvarlc_key    klist
     c                   kfld                    jvarlc_ogrp
     c                   kfld                    jvarlc_hgrp
     c                   kfld                    jvarlc_ugrp
      *
      * Key for posisjonering på varetekst
      *
     c     jvarl2_key    klist
     c                   kfld                    jvarl2_tek1
      *
      * Key for posisjonering på varenummer
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for posisjonering på Nobb-nummer
      *
     c     jvarl3_key    klist
     c                   kfld                    jvarl3_nobb
      *
      * Key for posisjonering på lev. varenummer
      *
     c     jvarl4_key    klist
     c                   kfld                    jvarl4_lvar
      *
      * Key for oppslag på søketekst
      *
     c     jsokl3_key    klist
     c                   kfld                    jsokl3_vare
     c                   kfld                    jsokl3_stek
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
6.30  *
6.30  * Key for les av moduler Nexstep
6.30  *
6.30 c     amodl1_key    klist
6.30 c                   kfld                    amodl1_modu
6.36  *
6.36  * Key for les på vare-leverandør
6.36  *
6.36 c     jvlelr_key    klist
6.36 c                   kfld                    jvlelr_lvnr
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
6.36 c                   time                    w_udat
6.30  *
6.30  * Sjekker om BM Modul
6.30  *
6.30 c                   eval      amodl1_modu = 'BM_MODUL'
6.20 c     amodl1_key    chain     amodl1
6.30 c                   if        %found(amodl1)
6.30 c                   eval      *in29  = *on
6.30 c                   endif
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      jvarl2_tek1 = *blank
     c     jvarl2_key    setll     jvarl2
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
