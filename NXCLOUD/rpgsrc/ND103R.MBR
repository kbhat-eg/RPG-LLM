     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : ND103R
      * Beskrivelse . . : Vise ordrelinjer etter referanse
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       130318 bsa  Nytt program
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fnohel1    if   e           k disk    rename(nohepfr:nohel1r)
     fnkeolr    if   e           k disk    rename(nkeopfr:nkeolrr)
     feddtlr    if   e           k disk    rename(eddtpfr:eddtlrr)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
      *
     fnd103d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(edfirm)
     d p_fsys          s                   like(edfsys)
     d p_tell          s                   like(edtell)
     d p_ldor          s                   like(edldor)
     d p_ldat          s                   like(edldat)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d nkeolr_kode     s                   like(nkkode)
     d eddtlr_ldat     s                   like(edldat)
     d eddtlr_line     s                   like(edline)
     d rlevl1_levr     s                   like(rllevr)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
     d b_pluk          s              1    inz(*off)
     d b_alle          s              1    inz(*off)
      *
     d w_ldat          s               d   datfmt(*iso)
      *
     d w_firm          s                   like(edfirm)
     d w_fsys          s                   like(edfsys)
     d w_tell          s                   like(edtell)
     d w_ldor          s                   like(edldor)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(13)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * C3BLD  - C3 Skjermbilde for å vise melding/tekstlinje
      * D1WIN  - D1 Vindu for slette (Delete)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2line <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      eddtlr_line = b1line
     c     eddtlr_key    setll     eddtlr
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
     c                   eval      w_seqe = *blank
      *
      * Varenummer
      *
     c                   if        b2line <> 0
     c                   eval      eddtlr_line = b2line
     c     eddtlr_key    setll     eddtlr
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2line = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
     c                   eval      b_pluk = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
     c**                 if        b1valg = 1
      * Så legger vi varer ut for videre plukking
     c**                 exsr      plukk_linje
     c**                 eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c**                 endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c     eddtlr_key2   reade     eddtlr
      *
     c                   if        %eof
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c**                 eval      b1valg = 0
     c                   eval      b1line = edline
     c                   eval      b1olin = edolin
     c                   eval      b1vare = edvare
     c                   eval      b1tek1 = edtek1
     c                   eval      b1anta = edanta
     c                   eval      b1enhe = edenhe
     c**                 eval      b1stat = 0
     c**                 eval      b1ldor = edldor
      *
     c                   if        edldat <> *loval
     c     *dmy          move      edldat        b1ldat
     c                   endif
      *
     c                   write     b1sfl
      *
     c     les_neste     tag
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     eddtlr_key    setgt     eddtlr
     c     eddtlr_key2   readpe    eddtlr
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c     eddtlr_key2   readpe    eddtlr
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   eval      eddtlr_line = edline
      *
     c                   enddo
      *
     c                   if        %eof
     c     eddtlr_key    setll     eddtlr
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_fsys
     c                   parm                    p_tell
     c                   parm                    p_ldor
     c                   parm                    p_ldat
      *
      * Key for oppslag på ekstern ordre-hode
      *
     c     nohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_fsys
     c                   kfld                    w_tell
      *
      * Key for henting av ekstern ordrekode
      *
     c     nkeolr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    nkeolr_kode
      *
      * Key for henting av leverandørtekst
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for legge ut ordrelinjer
      *
     c     eddtlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    p_fsys
     c                   kfld                    p_tell
     c                   kfld                    p_ldor
     c                   kfld                    eddtlr_ldat
     c                   kfld                    eddtlr_line
      *
      * Key for legge ut ordrelinjer
      *
     c     eddtlr_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    p_fsys
     c                   kfld                    p_tell
     c                   kfld                    p_ldor
     c                   kfld                    eddtlr_ldat
      *
      * Henter firma og referanse fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_fsys = p_fsys
     c                   eval      w_tell = p_tell
     c                   eval      w_ldor = 0
     c                   time                    w_ldat
      *
      * Initierer skjermbildet
      *
     c                   eval      *in22 = *on
      *
     c                   eval      nkeolr_kode = w_fsys
     c     nkeolr_key    chain     nkeolr
      *
     c     nohel1_key    chain     nohel1
      *
     c                   eval      b2nref = nonref
     c                   eval      b2knav = noknav
     c                   eval      b2kund = nokund
      *
     c                   eval      b2ldor = p_ldor
     c                   eval      b2lnav = *blanks
     c                   eval      rlevl1_levr = p_ldor
     c     rlevl1_key    chain     rlevl1
     c                   if        %found
     c                   eval      b2lnav = rlnavn
     c                   endif
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   if        p_ldat <> *loval
     c                   move      p_ldat        eddtlr_ldat
     c                   else
     c                   eval      eddtlr_ldat = *loval
     c                   endif
     c                   eval      eddtlr_line = 0
     c     eddtlr_key    setll     eddtlr
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
