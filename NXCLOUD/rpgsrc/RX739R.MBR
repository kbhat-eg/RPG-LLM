     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RX739R
      * Beskrivelse . . : Kontaktperson i RKUNPF/RLEVPF flyttes til RUKPF
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       240806 HFL  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     frukplu    uf a e           k disk    rename(rukppfr:rukplur)
      *
      * Definering av local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      *
      * Definering av variabler i nøkler
      *
     d rukplu_kund     s                   like(rukund)
     d rukplu_levr     s                   like(rulevr)
     d rukplu_kpnr     s                   like(rukpnr)
     d rkunl1_kund     s                   like(rkkund)
     d rlevl1_levr     s                   like(rllevr)
      *
      * Definering av variable
      *
     d w_ddat          s               d   datfmt(*iso)
     d w_retk          s              1
     d w_firm          s                   like(rufirm)
     d w_kund          s                   like(rukund)
     d w_levr          s                   like(rulevr)
     d w_kpnr          s                   like(rukpnr)
     d w_alfa          s                   like(rualfa)
     d w_fnav          s                   like(rufnav)
     d w_enav          s                   like(ruenav)
     d w_pers          s                   like(rkpers)
     d p               s              2  0
      *
      * Definering av konstanter
      *
     d Lo              c                   'abcdefghijklmnopqrstuvwxyzæøå'
     d Up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅ'
      *
      * Leser kundefil
      *
     c                   read      rkunl1
      *
     c                   dow       not %eof
     c                   if        rkpers <> *blank
     c                   eval      w_pers = rkpers
     c                   exsr      del_navn
     c                   exsr      oppd_k
     c                   endif
      *
     c                   read      rkunl1
     c                   enddo
      *
     c     slutt_k       tag
      *
      * Leser leverandørfil
      *
     c                   read      rlevl1
      *
     c                   dow       not %eof
     c                   if        rlpers <> *blank
     c                   eval      w_pers = rlpers
     c                   exsr      del_navn
     c                   exsr      oppd_l
     c                   endif
      *
     c                   read      rlevl1
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å dele navn i fornavn og etternavn (felles)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     del_navn      begsr
      * Leser til første blanke, det er fornavn. Resten er etternavn
     c     ' '           scan      w_pers        p
     c                   eval      w_fnav = %subst(w_pers:1:p)
     c                   eval      w_enav = %subst(w_pers:p+1)
     c                   eval      rufnav = %trim(w_fnav)
     c                   eval      ruenav = %trim(w_enav)
      *
      * Lager alfa-felt
      *
     c                   eval      w_alfa = %trimr(ruenav) + ' ' +
     c                                      %trim(rufnav)
      *
      * Fjerner blank dersom bare ett navn (fornavn)
      *
     c                   eval      rualfa = %triml(w_alfa)
      *
     c     Lo:Up         xlate     rualfa        rualfa
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for flytting fra kunde- til kontaktpersonregister
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_k        begsr
      *
     c                   eval      rupenr = rkpenr
     c                   eval      rukrol = *blank
     c                   eval      ruktlf = rktlfn
     c                   eval      rukfax = rktlfx
     c                   eval      ruptlf = *blank
     c                   eval      rupfax = *blank
     c                   eval      rumtlf = rkmobn
     c                   eval      rustat = *blank
     c                   eval      rumail = rkemal
     c                   eval      rukund = rkkund
     c                   eval      rulevr = *zero
     c                   eval      rueusr = l_user
     c                   time                    ruetim
     c                   time                    ruedat
      *
     c                   eval      rufirm = rkfirm
      *
      * Hent sist brukte kontaktpersonnummer
      *
     c                   eval      w_retk = ' '
     c                   eval      w_firm = rkfirm
     c                   eval      w_kpnr = *zero
     c                   call      'RS003R'
     c                   parm                    w_retk
     c                   parm                    w_firm
     c                   parm                    w_kpnr
      *
     c                   if        w_retk = *blank
     c                   eval      rukpnr = w_kpnr
     c                   else
     c                   goto      forbi_k
     c                   endif
      *
     c                   eval      rukplu_kund = rkkund
     c                   eval      rukplu_levr = *zero
     c                   eval      rukplu_kpnr = w_kpnr
     c     rukplu_key    chain     rukplur
      *
     c                   if        not %found(rukplu)
6.10 c                   eval      rueusr = l_user
6.10 c                   time                    ruetim
6.10 c                   time                    ruedat
6.10 c                   eval      ruousr = l_user
6.10 c                   time                    ruotim
6.10 c                   time                    ruodat
     c                   write     rukplur
     c                   endif
      *
     c     forbi_k       tag
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for flytting fra leverandør- til kontaktpersonregister
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_l        begsr
      *
     c                   eval      rupenr = *zero
     c                   eval      rukrol = *blank
     c                   eval      ruktlf = rltlfn
     c                   eval      rukfax = rltlfx
     c                   eval      ruptlf = *blank
     c                   eval      rupfax = *blank
     c                   eval      rumtlf = rlmobn
     c                   eval      rustat = *blank
     c                   eval      rumail = rlemal
     c                   eval      rukund = *zero
     c                   eval      rulevr = rllevr
     c                   eval      rueusr = l_user
     c                   time                    ruetim
     c                   time                    ruedat
      *
     c                   eval      rufirm = rlfirm
      *
      * Hent sist brukte kontaktpersonnummer
      *
     c                   eval      w_retk = ' '
     c                   eval      w_firm = rlfirm
     c                   eval      w_kpnr = *zero
     c                   call      'RS003R'
     c                   parm                    w_retk
     c                   parm                    w_firm
     c                   parm                    w_kpnr
      *
     c                   if        w_retk = *blank
     c                   eval      rukpnr = w_kpnr
     c                   else
     c                   goto      forbi_l
     c                   endif
      *
     c                   eval      rukplu_kund = *zero
     c                   eval      rukplu_levr = rllevr
     c                   eval      rukplu_kpnr = w_kpnr
     c     rukplu_key    chain     rukplur
      *
     c                   if        not %found(rukplu)
6.10 c                   eval      ruousr = l_user
6.10 c                   time                    ruotim
6.10 c                   time                    ruodat
6.10 c                   eval      rueusr = l_user
6.10 c                   time                    ruetim
6.10 c                   time                    ruedat
     c                   write     rukplur
     c                   endif
      *
     c     forbi_l       tag
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *
      * Key for oppdat. av kontaktperson-register
      *
     c     rukplu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rukplu_kund
     c                   kfld                    rukplu_levr
     c                   kfld                    rukplu_kpnr
      *
      * Henter verdier fra LDA og initierer skjermbildet
      *
     c                   endsr
