     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VA198R
      * Beskrivelse . . : Koblingstabell mva, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       100909 AMD  Nytt program
6.31  * 6.30  291116 bof  Utvidet med koder for eksport
7.xx  * 7.00  140316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvmval1    if   e           k disk    rename(vmvapfr:vmval1r)
     fvmval2    if   e           k disk    rename(vmvapfr:vmval2r)
     fvmvalr    if   e           k disk    rename(vmvapfr:vmvalrr)
     fvmvalu    uf a e           k disk    rename(vmvapfr:vmvalur)
      *
     fva195d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d vmval1_mkod     s                   like(vamkod)
     d vmval2_mtxt     s                   like(vamtxt)
     d vmvalr_mkod     s                   like(vamkod)
     d vmvalu_mkod     s                   like(vamkod)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(vamfir)
     d w_ekod          s                   like(vamkof)
     d w_tx30          s                   like(vamtxt)
     d w_stat          s              1
      *
     d s_mkod          s                   like(vamkod)
      *
      * Definering av konstanter
      *
7.xx d c_sfil          s              2  0 inz(13)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2mkod <> 0 or
     c                             b2mtxt <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      vmval2_mtxt = b1mtxt
     c     vmval2_key    setll     vmval2
     c                   other
     c                   eval      vmval1_mkod = b1mkod
     c     vmval1_key    setll     vmval1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Mva.-kode
      *
     c                   if        b2mkod <> 0
     c                   eval      w_seqe = *blank
     c                   eval      vmval1_mkod = b2mkod
     c     vmval1_key    setll     vmval1
     c                   goto      end_pos
     c                   endif
      *
      * Beskrivelse
      *
     c                   if        b2mtxt <> *blank
     c                   eval      w_seqe = 'B'
     c                   eval      vmval2_mtxt = b2mtxt
     c     vmval2_key    setll     vmval2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2mkod = 0
     c                   eval      b2mtxt = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1mkod = b1mkod
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1mkod = b1mkod
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1mkod = b1mkod
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1mkod = b1mkod
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1mkod = b2mkod
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
     c***                if        c1mkod = 0
     c***                goto      end_c1bld
     c***                endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      vmvalr_mkod = c1mkod
     c     vmvalr_key    chain     vmvalr                             90
     c                   if        *in90 = *off
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1mkod = 0
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2mkod = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
     c                   eval      *in35  = *off
     c                   eval      *in36  = *off
6.31 c                   eval      *in37  = *off
6.31 c                   eval      *in38  = *off
      *
     c                   eval      vmvalr_mkod = c1mkod
     c     vmvalr_key    chain     vmvalr                             90
      *
     c                   if        *in90 = *off
     c                   eval      c2mtxt = vamtxt
     c***                eval      c2musr = vamusr
     c                   eval      c2mkof = vamkof
     c                   eval      c2mkla = vamkla
6.31 c                   eval      c2mkuo = vamkuo
6.31 c                   eval      c2mkui = vamkui
     c                   eval      c2meus = vameus
     c     *dmy          move      vameda        c2meda
     c                   else
     c                   eval      c2mtxt = *blank
     c***                eval      c2musr = *blank
     c                   eval      c2mkof = *blank
     c                   eval      c2mkla = *blank
6.31 c                   eval      c2mkuo = *blank
6.31 c                   eval      c2mkui = *blank
     c                   eval      c2meus = *blank
     c                   eval      c2meda = 0
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1mkod = k1mkod
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
      * Call program for
      * MVA-kode
      *
     c                   if        *inkd = *on and
     c                             w_afld = 'C2MKOF'
     c                   call      'RA505R'
     c                   parm                    w_firm
     c                   parm                    w_ekod
     c                   parm                    w_tx30
      *
     c                   if        w_ekod <> *blank
     c                   eval      c2mkof = w_ekod
     c***                eval      c2etxt = w_tx30
     c                   endif
      *
     c                   move      *on           *in35
     c                   goto      c2taga
     c                   endif
      *
     c                   if        *inkd = *on and
     c                             w_afld = 'C2MKLA'
     c                   call      'RA505R'
     c                   parm                    w_firm
     c                   parm                    w_ekod
     c                   parm                    w_tx30
      *
     c                   if        w_ekod <> *blank
     c                   eval      c2mkla = w_ekod
     c***                eval      c2etxt = w_tx30
     c                   endif
      *
     c                   move      *on           *in36
     c                   goto      c2taga
     c                   endif
6.31  *
6.31 c                   if        *inkd = *on and
6.31 c                             w_afld = 'C2MKUO'
6.31 c                   call      'RA505R'
6.31 c                   parm                    w_firm
6.31 c                   parm                    w_ekod
6.31 c                   parm                    w_tx30
6.31  *
6.31 c                   if        w_ekod <> *blank
6.31 c                   eval      c2mkuo = w_ekod
6.31 c                   endif
6.31  *
6.31 c                   move      *on           *in37
6.31 c                   goto      c2taga
6.31 c                   endif
6.31  *
6.31 c                   if        *inkd = *on and
6.31 c                             w_afld = 'C2MKUI'
6.31 c                   call      'RA505R'
6.31 c                   parm                    w_firm
6.31 c                   parm                    w_ekod
6.31 c                   parm                    w_tx30
6.31  *
6.31 c                   if        w_ekod <> *blank
6.31 c                   eval      c2mkui = w_ekod
6.31 c                   endif
6.31  *
6.31 c                   move      *on           *in38
6.31 c                   goto      c2taga
6.31 c                   endif
6.31  *
      * Validering av input
      *
     c                   if        c2mtxt = *blank
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
     c                   exsr      kontroll
      *
     c                   if        *in81 = *on or
6.31 c                             *in82 = *on or
6.31 c                             *in83 = *on or
6.31 c                             *in84 = *on
     c                   goto      c2taga
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   vmvalur
      *
     c                   eval      vmvalu_mkod = c1mkod
     c     vmvalu_key    chain     vmvalur                            90
      *
     c                   eval      vamtxt = c2mtxt
     c***                eval      vamusr = c2musr
     c                   eval      vamkof = c2mkof
     c                   eval      vamkla = c2mkla
6.31 c                   eval      vamkuo = c2mkuo
6.31 c                   eval      vamkui = c2mkui
      *
     c                   time                    vameda
     c                   time                    vameti
     c                   eval      vameus = l_user
      *
     c                   if        *in90 = *off
     c                   update    vmvalur
     c                   else
     c                   eval      vamfir = w_firm
     c                   eval      vamkod = c1mkod
     c                   write     vmvalur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1mtxt = vamtxt
     c                   eval      b1mkof = vamkof
     c                   eval      b1mkla = vamkla
6.31 c                   eval      b1mkuo = vamkuo
6.31 c                   eval      b1mkui = vamkui
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      vmvalr_mkod = d1mkod
     c     vmvalr_key    chain     vmvalr                             90
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      vmvalu_mkod = d1mkod
     c     vmvalu_key    chain     vmvalur                            90
     c                   delete    vmvalur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_mkod = k1mkod
      *
      * Fyller kopierings-bildet
      *
     c                   eval      vmvalr_mkod = k1mkod
     c     vmvalr_key    chain     vmvalr                             90
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1mkod = 0
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      vmvalr_mkod = k1mkod
     c     vmvalr_key    chain     vmvalr                             90
     c                   if        *in90 = *off
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1mkod = s_mkod
     c                   exsr      xc2bld
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kontroll av inputfelt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kontroll      begsr
      *
      * Sjekke om mva-kode for Ofak er gyldig
      *
     c                   eval      w_ekod = c2mkof                                           .
     c                   call      'RS705R'                                      MVA-
     c                   parm                    w_stat
     c                   parm                    w_ekod
     c                   parm                    w_tx30
      *
     c                   if        w_stat = 'N'                                 Kode finnes ikke
     c                   eval      *in35 = *on                                  Pos.cur,     .
     c                   eval      *in81 = *on                                  Error        .
     c                   goto      kont_end                                                  .
     c                   endif                                                               .
      *
      * Sjekke om mva-kode for Lagr er gyldig
      *
     c                   eval      w_ekod = c2mkla                                           .
     c                   call      'RS705R'                                      MVA-
     c                   parm                    w_stat
     c                   parm                    w_ekod
     c                   parm                    w_tx30
      *
     c                   if        w_stat = 'N'                                 Kode finnes ikke
     c                   eval      *in36 = *on                                  Pos.cur,     .
     c                   eval      *in82 = *on                                  Error        .
     c                   goto      kont_end                                                  .
     c                   endif                                                               .
6.31  *
6.31  * Sjekke om mva-kode for Eksport Ofak gyldig
6.31  *
6.31 c                   if        c2mkuo <> *blank
6.31 c                   eval      w_ekod = c2mkuo                                           .
6.31 c                   call      'RS705R'                                      MVA-
6.31 c                   parm                    w_stat
6.31 c                   parm                    w_ekod
6.31 c                   parm                    w_tx30
6.31  *
6.31 c                   if        w_stat = 'N'                                 Kode finnes ikke
6.31 c                   eval      *in37 = *on                                  Pos.cur,     .
6.31 c                   eval      *in83 = *on                                  Error        .
6.31 c                   goto      kont_end                                                  .
6.31 c                   endif                                                               .
6.31 c                   endif                                                               .
6.31  *
6.31  * Sjekke om mva-kode for Eksport innkjøp gyldig
6.31  *
6.31 c                   if        c2mkui <> *blank
6.31 c                   eval      w_ekod = c2mkui                                           .
6.31 c                   call      'RS705R'                                      MVA-
6.31 c                   parm                    w_stat
6.31 c                   parm                    w_ekod
6.31 c                   parm                    w_tx30
6.31  *
6.31 c                   if        w_stat = 'N'                                 Kode finnes ikke
6.31 c                   eval      *in38 = *on                                  Pos.cur,     .
6.31 c                   eval      *in84 = *on                                  Error        .
6.31 c                   goto      kont_end                                                  .
6.31 c                   endif                                                               .
6.31 c                   endif                                                               .
      *
     c     kont_end      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      vmval2
     c                   other
     c                   read      vmval1
     c                   endsl
      *
     c                   if        %eof or
     c                             vamfir <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      vmval2_mtxt = vamtxt
     c                   other
     c                   eval      vmval1_mkod = vamkod
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1mkod = vamkod
     c                   eval      b1mkof = vamkof
     c                   eval      b1mkla = vamkla
6.31 c                   eval      b1mkuo = vamkuo
6.31 c                   eval      b1mkui = vamkui
     c                   eval      b1mtxt = vamtxt
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'B'
     c     vmval2_key    setgt     vmval2
     c                   readp     vmval2
     c                   other
     c     vmval1_key    setgt     vmval1
     c                   readp     vmval1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   readp     vmval2
     c                   other
     c                   readp     vmval1
     c                   endsl
      *
     c                   if        %eof or
     c                             vamfir <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      vmval2_mtxt = vamtxt
     c                   other
     c                   eval      vmval1_mkod = vamkod
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'B'
     c     vmval2_key    setll     vmval2
     c                   other
     c     vmval1_key    setll     vmval1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på mva.-kode
      *
     c     vmval1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vmval1_mkod
      *
      * Key for posisjonering på beskrivelse
      *
     c     vmval2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vmval2_mtxt
      *
      * Key for les på mva.-kode
      *
     c     vmvalr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vmvalr_mkod
      *
      * Key for oppdatering av mva.-kode
      *
     c     vmvalu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vmvalu_mkod
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
      * Initierer skjermbildet
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      vmval1_mkod = 0
     c     vmval1_key    setll     vmval1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
