     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FR603R
      * Beskrivelse . . : Rabattmatrise, utskrift av kryssreferanse - param.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       180204 HKO  Nytt program
7.xx  * 7.00  010416 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffr603d    cf   e             workstn
      *
      * Definering av LDA
      *
     d                uds
     d l_firm                944    946  0
      *
      * Datastruktur for parameterliste -ut
      *
     d                 ds
     d d_list                        16
     d  d_firm                        3  0 overlay(d_list: 1)
     d  d_rkaf                        3  0 overlay(d_list: 4)
     d  d_rkat                        3  0 overlay(d_list: 7)
     d  d_dumm                        1    overlay(d_list:16) inz('*')
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s              3  0
     d w_rtxt          s             30
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Behandling av skjermbilde
      *
     c     b1taga        tag
      *
     c                   exfmt     b1bld
      *
      * F3=Avslutt
      *
     c                   if        *inkc or *inkl
     c                   goto      avslutt
     c                   endif
      *
      * F4=Spørring
      *
     c                   if        *inkd = *on
     c                   exsr      spØrring
     c                   goto      b1taga
     c                   endif
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1taga
     c                   endif
      *
     c                   exsr      flytt_par
      *
      * Kaller utskriftsprogram
      *
     c                   call      'FR603C'
     c                   parm                    d_list
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørringer (FD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Rabattkategori
      *
     c                   if        w_afld = 'B1RKAF'
     c                   call      'RA506R'
     c                   parm                    w_firm
     c                   parm                    b1rkaf
     c                   parm                    w_rtxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'B1RKAT'
     c                   call      'RA506R'
     c                   parm                    w_firm
     c                   parm                    b1rkat
     c                   parm                    w_rtxt
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
      * Rabattkategori
      *
     c                   if        b1rkaf <> 0 and
     c                             b1rkat <> 0 and
     c                             b1rkaf > b1rkat
     c                   eval      *in31 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for flytting av felter til DS-parametre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     flytt_par     begsr
      *
     c                   eval      d_firm = w_firm
      *
     c                   eval      d_rkaf = b1rkaf
      *
     c                   if        b1rkat = 0
     c                   eval      d_rkat = b1rkaf
     c                   else
     c                   eval      d_rkat = b1rkat
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
