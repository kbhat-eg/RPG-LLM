     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JV755R
      * Beskrivelse . . : Fellesprogram for endring av vareinformasjon
      *                   i EVR.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       171097 HKO  Nytt program
      *       060798 HKO  Oppdaterer søketekster i EVR på bakgrunn av
      *                   varetekst og leverandør som overføres.
      *       090399 HKO  Avbryter oppdatering dersom vare er valgt
      *                   utelatt fra oppdatering i EVR
      *       120803 HKO  Endret rekkefølge for oppretting av EAN-nummer
      *                   Endrer timestamp på hoved EAN-nummer dersom dette
      *                   finnes fra før
      *       211003 HKO  Skriver Nobb utgår-dato og cross docking
      *                   informasjon til vare
      *       100605 HKO  Behandler EAN-nr fra prisgivende leverandør.
      *                   Overfører EAN-nr mindre enn 12 siffer
      *       111005 HKO  Oppdaterer PSE på EVR-vare dersom PSE er overstyrt
      *                   på enhet i VA
      *       230306 HKO  Oppdaterer med overstyrt varegruppe
      *       140907 BOF  Enhet blir oppdatert på EAN-reg.
5.60  *       181108 BOF  Justert oppdatering av ean-reg.
6.11  * 6.10  130709 bof  Utivdet eannr til 14 og brukt std. oppslag
6.12  *       030909 bof  Utvidet med sortimentskode
6.13  *       250909 bof  Endret fra jvpkpf til jvpppf for noe info
6.14  *       120510 bof  Lagt inn JXEANN igjen skal utgå når xml er på plass
6.15  *       051110 bof  Forhindrer at enh.blir lagt i VVEPPF hvis ikke
6.15  *                   den finnes på VVENPF
      *
6.16  *       101110      Fjernet oppdat. av GTIN pr. prisgiver her.
6.30  *       050414 bof  Utvidet nøkkel i vare-pris med leverandør
6.30  *                   Fjernet jvpppf
6.31  *       140314 bof  Lagt inn test på fra /til dato på pakning
6.32  * 6.30  270314 bof  test om varepris VA er utgått
6.33  * 6.30  140714 bof  Takle priser pr. dato
6.34  * 6.30  020816 bof  Feilretting vedr. test på til prisdato.
6.35  * 6.30  121216 bof  Fjerner oppdat. av nobb- utg.dato
6.36  *  6.30  081018 bof  Utvidet med trelast sortiment
6.37  *K00    250520 bof  Henter utgår dato fra vare-lev hvis nobb.lev utfylt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjvdtl1    if   e           k disk    rename(jvdtpfr:jvdtl1r)
     fvvarlu    uf   e           k disk    rename(vvarpfr:vvarlur)
6.16 f*venl1    if   e           k disk    rename(vvenpfr:vvenl1r)
6.13 fvvepl2    if   e           k disk    rename(vveppfr:vvepl2r)
6.16 f*veplu    uf a e           k disk    rename(vveppfr:vveplur)
     fjvprl1    if   e           k disk    rename(jvprpfr:jvprl1r)
     fjvpkl1    if   e           k disk    rename(jvpkpfr:jvpkl1r)
6.37 fjvlel1    if   e           k disk    rename(jvlepfr:jvlel1r)
     fveanl1    if   e           k disk    rename(veanpfr:veanl1r)
     fveanlu    uf a e           k disk    rename(veanpfr:veanlur)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fjsokl1    if   e           k disk    rename(jsokpfr:jsokl1r)
     fvsokl1    if   e           k disk    rename(vsokpfr:vsokl1r)
     fvsoklu    uf a e           k disk    rename(vsokpfr:vsoklur)
6.37 fvvlelu    uf   e           k disk    rename(vvlepfr:vvlelur)
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_vare          s                   like(jvvare)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
      *
      * Definering av variabler i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d jvarl1_vare     s                   like(jvvare)
     d jvdtl1_dvar     s                   like(jvdvar)
6.13 d vvepl2_pgti     s                   like(vepgti)
6.16 d*vveplu_pvar     s                   like(vepvar)
6.16 d*vveplu_penh     s                   like(vepenh)
6.16 d*vveplu_pldo     s                   like(vepldo)
     d vvarlu_vare     s                   like(vvvare)
     d jvprl1_vare     s                   like(jxvare)
     d jvprl1_ldor     s                   like(jxldor)
     d jvpkl1_vare     s                   like(jwvare)
6.30 d jvpkl1_ldor     s                   like(jwldor)
     d veanl1_eann     s                   like(vxeann)
     d veanlu_eann     s                   like(vxeann)
6.16 d*vvenl1_vare     s                   like(vevare)
6.16 d*vvenl1_enhe     s                   like(veenhe)
     d jlevl1_ldor     s                   like(jlldor)
     d jsokl1_vare     s                   like(jsvare)
     d vsokl1_vare     s                   like(vsvare)
     d vsokl1_stek     s                   like(vsstek)
     d vsoklu_vare     s                   like(vsvare)
6.37 d vvlelu_lvnr     s                   like(vvlvnr)
6.37 d jvlel1_lvnr     s                   like(jvlvnr)
6.37 d jvlel1_lldo     s                   like(jvlldo)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vxfirm)
     d w_crdo          s                   like(jxcrdo)
6.14 d w_eann          s                   like(jxeann)
5.60 d w_enhe          s                   like(jwenhe)
6.31 d w_udat          s                   like(jvudat)
6.36 d w_lv_vare       s                   like(vvvare)
6.37 d w_nuda          s                   like(vvnuda)
6.37 d w_lvar          s                   like(vvlvar)
      *
6.31 d b_pakn          s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.36 C/Exec SQL
6.36 C+  Set Option DatFmt=*ISO
6.36 C/End-Exec
      * Avbryter oppdateringen dersom vare ikke finnes i EVR, eller dersom
      * vare er valgt utelatt fra oppdatering
      *
     c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1r
     c                   if        not %found or
     c                             vvkjus = 1
     c                   goto      avslutt
     c                   endif
      *
      * Kaller subrutine for oppdatering
      *
     c                   exsr      oppdater
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av vareinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdater      begsr
      *
      * Henter EAN-nr og cross-docing informasjon fra vare-pris
      *
6.14 c                   eval      w_eann = 0
     c                   eval      w_crdo = 0
6.37 c                   eval      w_nuda = *loval
6.37 c                   eval      w_lvar = *Blank
      *
     c                   if        vvnlev <> 0
     c                   eval      jvprl1_vare = p_vare
     c                   eval      jvprl1_ldor = vvnlev
6.33 c     jvprl1_key    setll     jvprl1
6.33 c     jvprl1_key    reade     jvprl1
6.33 c                   dow       not %eof
6.33 c                   if        w_udat >= jxgdat
6.32 c                   if        (jxtdat =  *loval) or
6.32 c                             (jxtdat <> *loval and
6.34 c                             jxtdat >= w_udat)
6.33 c**** ikke ean her  eval      w_eann = jxeann
     c                   eval      w_crdo = jxcrdo
6.32 c                   endif
     c                   endif
6.33 c     jvprl1_key    reade     jvprl1
6.33 c                   enddo
6.37 c                   eval      jvlel1_lvnr = p_vare
6.37 c                   eval      jvlel1_lldo = vvnlev
6.37 c     jvlel1_key    chain     jvlel1
6.37 c                   if        %found(jvlel1)
6.37 c                   eval      w_nuda = jvluda
6.37 c                   eval      w_lvar = jvllva
6.37 c                   endif
6.37 c                   endif
      *
      * Vare
      *
6.36  * sjekker om logistikk-vare
6.36 c                   eval      w_lv_vare = *blank
6.36 c/exec sql
6.36 c+ select jvqvnr
6.36 c+ into   :w_lv_vare
6.36 c+ from   jvklpf
6.36 c+ where  jvqvnr = :p_vare and jvqfir = :w_firm
6.36 c+ fetch first 1 rows only
6.36 c/end-exec

     c                   eval      jvarl1_vare = p_vare
     c     jvarl1_key    chain     jvarl1
      *
     c                   eval      vvarlu_vare = p_vare
     c     vvarlu_key    chain     vvarlur
      *
6.36 c                   if        w_lv_vare = *blank
6.36  * hvis logistikkvare, så skal ikke varetekst oppdateres
     c                   eval      vvtek1 = jvtek1
     c                   eval      vvtek2 = jvtek2
6.36 c                   endif
     c                   eval      vvogrp = jvogrp
     c                   eval      vvhgrp = jvhgrp
     c                   eval      vvugrp = jvugrp
6.12 c                   eval      vvsort = jvsort
     c                   eval      vvnobb = jvnobb
     c                   eval      vvnmod = jvmodn
     c                   eval      vvvtyp = 'L'
     c                   eval      vvcrdo = w_crdo
     c                   eval      vvenps = *blank
6.37 c                   eval      vvnuda = w_nuda
6.37 c                   eval      vvlvar = w_lvar
     c                   eval      vveusr = l_user
      *
     c                   time                    vvedat
     c                   time                    vvetim
      *
6.36 c                   if        w_lv_vare = *blank
6.36  * hvis logistikkvare, så skal ikke varetekst oppdateres
      *
     c                   if        jvete1 <> *blank
     c                   eval      vvtek1 = jvete1
     c                   eval      vvtek2 = jvete2
     c                   endif
6.36 c                   endif
      *
     c                   if        jveogr <> 0 and
     c                             jvehgr <> 0 and
     c                             jveugr <> 0
     c                   eval      vvogrp = jveogr
     c                   eval      vvhgrp = jvehgr
     c                   eval      vvugrp = jveugr
     c                   endif
      *
     c                   eval      jvdtl1_dvar = p_vare
     c     jvdtl1_key    chain     jvdtl1
     c                   if        %found and
     c                             jvdsko = 1
     c                   eval      vvvtyp = 'S'
     c                   endif
      *
6.30 c                   if        vvnlev <> 0
     c                   eval      jvpkl1_vare = p_vare
6.30 c                   eval      jvpkl1_ldor = vvnlev
     c     jvpkl1_key    setll     jvpkl1
     c     jvpkl1_key    reade     jvpkl1
     c                   dow       not %eof
6.31  *
6.31  * sjekker fra / tildato
6.31 c                   exsr      sjekk_pkdato
6.31 c                   if        b_pakn = *on
     c                   if        jwpsen = 1
     c                   eval      vvenps = jwenhe
     c                   leave
     c                   endif
6.31 c                   endif
     c     jvpkl1_key    reade     jvpkl1
     c                   enddo
6.30 c                   endif
      *
     c                   update    vvarlur
      *
      * EAN-nummer
      *
      * Dersom vare i LVR har EAN-nummer på enhet eller vare som ikke
      * ligger på varen i EVR, opprettes EAN-nummer i EAN-registeret.
      *
6.30  * ** FJERNET GAMMEL KODE HER SOM VAR * UT.     6.16
      *
6.16  * Beholder oppdat av jvean1 her, i tilfelle noen bruker det.
      *
     c                   if        jvean1 <> 0
6.11  *
6.11  *sjekker om ean-nr allerede ligger på vare  enhet
6.13 c                   eval      vvepl2_pgti = jvean1
6.13 c     vvepl2_key    chain     vvepl2
6.13 c                   if        not %found(vvepl2)
6.11  *
5.60 c                   clear                   veanlur
     c                   eval      veanl1_eann = jvean1
     c     veanl1_key    chain     veanl1
     c                   if        not %found
     c                   eval      vxfirm = w_firm
     c                   eval      vxvare = p_vare
     c                   eval      vxeann = jvean1
5.60 c                   eval      vxenhe = *blank
     c                   eval      vxeusr = l_user
     c                   time                    vxtime
     c                   time                    vxetim
     c                   time                    vxedat
     c                   write     veanlur
     c                   else
5.60 c                   if        vxvare <> p_vare
     c                   eval      veanlu_eann = jvean1
     c     veanlu_key    chain     veanlur
5.60 c                   if        %found(veanlu)
5.60 c                   eval      vxvare = p_vare
5.60 c                   eval      vxenhe = *blank
     c                   time                    vxtime
5.60 c                   time                    vxedat
     c                   update    veanlur
5.60 c                   endif
5.60 c                   endif
     c                   endif
6.11 c                   endif
     c                   endif
      *
6.14 c                   if        w_eann <> 0
6.14  * Ean-nr fra varepris
6.14  * sjekker om ean-nr allerede ligger på vare  enhet
6.14  * hvis ikke så legges den i generelt ean-nr register
6.14  *
6.14 c                   eval      vvepl2_pgti = w_eann
6.14 c     vvepl2_key    chain     vvepl2
6.14 c                   if        not %found(vvepl2)
6.14  *
6.14 c                   clear                   veanlur
6.14 c                   eval      veanl1_eann = w_eann
6.14 c     veanl1_key    chain     veanl1
6.14 c                   if        not %found
6.14 c                   eval      vxfirm = w_firm
6.14 c                   eval      vxvare = jvvare
6.14 c                   eval      vxenhe = *blank
6.14 c                   eval      vxeann = w_eann
6.14 c                   eval      vxeusr = l_user
6.14 c                   time                    vxtime
6.14 c                   time                    vxedat
6.14 c                   time                    vxetim
6.14 c                   write     veanlur
6.14 c                   else
6.14 c                   if        vxvare <> jvvare
6.14 c                   eval      veanlu_eann = w_eann
6.14 c     veanlu_key    chain     veanlur
6.14 c                   if        %found(veanlu)
6.14 c                   eval      vxvare = jvvare
6.14 c                   eval      vxenhe = *blank
6.14 c                   time                    vxetim
6.14 c                   time                    vxedat
6.14 c                   update    veanlur
6.14 c                   endif
6.14 c                   endif
6.14 c                   endif
6.14  *
6.14 c                   endif
6.14  *
6.14 c                   endif
      *
      *
      * Henter leverandørinfo
      *
     c                   eval      jlevl1_ldor = jvldor
     c     jlevl1_key    chain     jlevl1
      *
      * Oppdaterer søketekster i EVR med modulstikkord og egne søketekster
      * Søketeksten blir ikke kopiert over dersom den allerede finnes i
      * EVR søketekstregister
      *
     c                   eval      jsokl1_vare = p_vare
     c     jsokl1_key    setll     jsokl1
     c     jsokl1_key    reade     jsokl1
     c                   dow       not %eof
     c                   if        jsokod = 'L' or
     c                             jsokod = 'M' and
     c                             jlkmod = 1
     c                   eval      vsokl1_vare = jsvare
     c                   eval      vsokl1_stek = jsstek
     c     vsokl1_key    chain     vsokl1
     c                   if        not %found
     c                   eval      vsfirm = w_firm
     c                   eval      vsvare = jsvare
     c                   eval      vsstek = jsstek
     c                   write     vsoklur
     c                   endif
     c                   endif
     c     jsokl1_key    reade     jsokl1
     c                   enddo
      *
6.37  * Oppdaterer evt. utgår dato på Vare-leverandør
6.37 c                   eval      vvlelu_lvnr = p_vare
6.37 c     vvlelu_key    setll     vvlelu
6.37 c     vvlelu_key    reade     vvlelu
6.37 c                   dow       not %eof(vvlelu)
6.37 c                   eval      jvlel1_lvnr = p_vare
6.37 c                   eval      jvlel1_lldo = vvnlev
6.37 c     jvlel1_key    chain     jvlel1
6.37 c                   if        %found(jvlel1)
6.37 c                   eval      vvluda = jvluda
6.37 c                   eval      vvllva = jvllva
6.37 c                   endif
6.37 c                   update    vvlelur
6.37 c     vvlelu_key    reade     vvlelu
6.37 c                   enddo
      *
      * Oppdaterer søketekst mot vare i EVR
      *
     c                   call      'VV750R'
     c                   parm                    w_firm
     c                   parm                    jvvare
      *
     c                   endsr
6.11  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.11  * Subrutine for å rydde ean-nr på veanpf som ligger på vvenpf
6.11  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.11  *
6.11 c     rydd_ean      begsr
6.11  *
6.13 c                   eval      veanlu_eann = vepgti
6.11 c     veanlu_key    chain     veanlu
6.11 c                   if        %found(veanlu)
6.11 c                   delete    veanlur
6.11 c                   endif
6.11  *
6.11 c                   endsr
      *
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  * Subrutine for å sjekke om dato er innenfor fra /til dato
6.31  * hvis *loval i begge datoer = ok
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  *
6.31 c     sjekk_pkdato  begsr
6.31  *
6.31 c                   eval      b_pakn = *on
6.31  *
6.31 c                   if        jwfdat = *loval and
6.31 c                             jwtdat = *loval
6.31 c                   leavesr
6.31 c                   endif
6.31  *
6.31 c                   if        jwfdat <= w_udat and
6.31 c                             jwtdat >  w_udat
6.31 c                   leavesr
6.31 c                   endif
6.31  *
6.31 c                   if        jwfdat = *loval  and
6.31 c                             jwtdat >  w_udat
6.31 c                   leavesr
6.31 c                   endif
6.31  *
6.31 c                   if        jwfdat <= w_udat and
6.31 c                             jwtdat =  *loval
6.31 c                   leavesr
6.31 c                   endif
6.31  *
6.31 c                   eval      b_pakn = *off
6.31  *
6.31 c                   endsr
6.31  *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
      *
      * Key for oppslag på EVR-vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på vare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for oppslag på vare-detalj
      *
     c     jvdtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jvdtl1_dvar
      *
      * Key for oppdatering av EVR-vare
      *
     c     vvarlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlu_vare
      *
      * Key for oppslag på vare-pris
      *
     c     jvprl1_key    klist
     c                   kfld                    jvprl1_vare
     c                   kfld                    jvprl1_ldor
      *
      * Key for oppslag på vare-pakning
      *
     c     jvpkl1_key    klist
     c                   kfld                    jvpkl1_vare
6.30 c                   kfld                    jvpkl1_ldor
      *
      * Key for oppslag på EAN
      *
     c     veanl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    veanl1_eann
      *
      * Key for oppdatering av EAN
      *
     c     veanlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    veanlu_eann
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for oppslag på søketekst
      *
     c     jsokl1_key    klist
     c                   kfld                    jsokl1_vare
      *
      * Key for oppslag på EVR-søketekst
      *
     c     vsokl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vsokl1_vare
     c                   kfld                    vsokl1_stek
6.11  *
6.11  * Key for oppslag på vare enhet  pr. eann
6.11  *
6.13 c     vvepl2_key    klist
6.13 c                   kfld                    w_firm
6.13 c                   kfld                    vvepl2_pgti
6.13  *
6.13  * Key for oppdatering av EVR-vare-enhet-prisgiver
6.13  *
6.16 c*    vveplu_key    klist
6.16 c*                  kfld                    w_firm
6.16 c*                  kfld                    vveplu_pvar
6.16 c*                  kfld                    vveplu_penh
6.16 c*                  kfld                    vveplu_pldo
6.15  *
6.15  * Key for les av vare-enhet
6.15  *
6.16 c*    vvenl1_key    klist
6.16 c*                  kfld                    w_firm
6.16 c*                  kfld                    vvenl1_vare
6.16 c*                  kfld                    vvenl1_enhe
6.37  *
6.37  * Key for oppdatering av vare-leverandør med utgår dato
6.37  *
6.37 c     vvlelu_key    klist
6.37 c                   kfld                    w_firm
6.37 c                   kfld                    vvlelu_lvnr
6.37  *
6.37  * Key for oppslag på vare-leverandør
6.37  *
6.37 c     jvlel1_key    klist
6.37 c                   kfld                    jvlel1_lvnr
6.37 c                   kfld                    jvlel1_lldo
6.37  *

      *
      * Henter firma fra paremter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
