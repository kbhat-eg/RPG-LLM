     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : AN014R
      * Beskrivelse . . : Generering av nattrutine, Reorganisering
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fqcllesrc  o  a f  096        disk
      *
      * Definering av tabeller
      *
     d rec             s             80    dim(41) ctdata perrcd(1)             Programlinjer
      *
      * Definering av datastrukturer
      *
     d                 ds
     d d_dato                         6  0
     d  d_aaar                        2  0 overlay(d_dato)
     d  d_mmnd                        2  0 overlay(d_dato:3)
     d  d_ddag                        2  0 overlay(d_dato:5)
      *                                                                     -
      * Definering av parametere
      *                                                                     -
     d p_valg          s            128
      *                                                                     -
      * Definering av variable
      *                                                                     -
     d n               s              2  0
     d w_dato          s              6  0
     d w_recc          s             84
     d w_tell          s              6  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Legg ut records til Cl-program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Legger ut første del av CL-programmet
      *
     c                   for       n = 1 to 38
     c                   add       100           w_tell
     c                   movel     rec(n)        w_recc
     c                   except    xrecut
     c                   endfor
      *
      * Legger ut siste del av CL-programmet
      *
     c                   for       n = 39 to 41
     c                   add       100           w_tell
     c                   movel     rec(n)        w_recc
     c                   except    xrecut
     c                   endfor
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      *inlr = *on
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c                   eval      w_recc = *blank
     c                   eval      w_tell = *zero
     c                   eval      w_dato = *zero
     c                   eval      n = *zero
      *
     c                   eval      d_aaar = uyear
     c                   eval      d_mmnd = umonth
     c                   eval      d_ddag = uday
     c                   eval      w_dato = d_dato
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_valg
      *
     c                   endsr
      *
     oqcllesrc  eadd         xrecut
     o                       w_recc              96
     o                       w_tell               6
     o                       w_dato              12
**
             PGM
             DCL        VAR(&SPES)   TYPE(*CHAR) LEN(10) +
                                     VALUE('AN020C')
             DCL        VAR(&TEXT)   TYPE(*CHAR) LEN(30) +
                                     VALUE('Reorgansieringen')
/*                                                                   */
/* Sjekk om program finnes fra før                                   */
/*                                                                   */
             CHKOBJ     OBJ(ASPGPLK/QCLLESRC) OBJTYPE(*FILE) +
                          MBR(&SPES) AUT(*ALL)
             MONMSG     MSGID(CPF9815) EXEC(GOTO CMDLBL(START))
             RMVM       FILE(ASPGPLK/QCLLESRC) MBR(&SPES)
/*                                                                   */
/* Bygge opp Cl-member                                               */
/*                                                                   */
 START:      ADDPFM     FILE(ASPGPLK/QCLLESRC) MBR(&SPES) +
                          TEXT(&TEXT) SRCTYPE(CLP)
             OVRDBF     FILE(QCLLESRC) TOFILE(ASPGPLK/QCLLESRC) +
                          MBR(&SPES)
/*                                                                   */
             CLRPFM    FILE(ADB/APGMPF)
             MONMSG     MSGID(CPF3142)
/*                                                                   */
             DSPOBJD   OBJ(*ALLUSR/*ALL) OBJTYPE(*FILE) OUTPUT(*OUTFILE) +
                       OUTFILE(ADB/APGMPF)
/*                                                                   */
/* Call til RPG-program                                              */
/*                                                                   */
             CHGCURLIB  CURLIB(ADB)
             CALL       PGM(AN020R)
             DLTOVR     FILE(QCLLESRC)
/*                                                                   */
/* Create program                                                    */
/*                                                                   */
             CRTCLPGM   PGM(ASPGPLK/&SPES) SRCFILE(ASPGPLK/QCLLESRC)   +
                          TEXT(&TEXT) OPTION(*NOSRC *NOXREF +
                          *NOSECLVL)
/*                                                                   */
             CALL       PGM(ASPGPLK/AN020C)
/*                                                                   */
             ENDPGM
