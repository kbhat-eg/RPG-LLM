      /TITLE  ASSHOP: Spørring/Registrering av varelinjer
     h datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASSHOP
      * Program . . . . : BORGVR
      * Beskrivelse . . : Spørring/Registrering av varelinjer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       140699 HKO  Nytt program
      *       271099 HKO  Butikkpris vises dersom denne er utfylt på varen,
      *                   og prisen er lavere enn eventuell tilbudspris
      *       070700 HKO  Skrevet om logikk for henting av pris ved spørring
      *                   på og endring av leveringstype
3.31  *       050401 AMD  Varetekst 2 kom ikke fram i vinduet for
      *                   varelinje-registrering.
      *       161003 HKO  Ekskluderer/inkluderer skaffe-varer
      *       251103 HKO  Viser ** UTGÅTT ** i vareteksten på utgåtte varer
      *       161203 HKO  Endret parameter ved kall til VP900R
      *       120804 HKO  Endret parameter ved kall til VP900R
      *       041104 HKO  Endret parameter ved kall til VP900R
5.70  * PRGR  121208 bof  Utvidet med prisgruppe /varetype
6.20  * 6.20  191110 bof  Utivdet parameter for VL710r
6.21  * 6.20  250511 bof  Hvis nobb utg.dato på varen,så vises *UTG.NOBB*
6.30  * 6.30  170918 bkv  Utvidet med trelast sortiment
8.01  *PRG2   080622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvsopf    if   e           k disk
     fvvarlr    if   e           k disk    rename(vvarpfr:vvarlrr)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvarl2    if   e           k disk    rename(vvarpfr:vvarl2r)
     fvvarl3    if   e           k disk    rename(vvarpfr:vvarl3r)
     fvvarl4    if   e           k disk    rename(vvarpfr:vvarl4r)
     fvvarl6    if   e           k disk    rename(vvarpfr:vvarl6r)
     fvvarl7    if   e           k disk    rename(vvarpfr:vvarl7r)
     fvvarl8    if   e           k disk    rename(vvarpfr:vvarl8r)
     fbwsdl1    if   e           k disk    rename(bwsdpfr:bwsdl1r)
     fbstsl1    if   e           k disk    rename(bstspfr:bstsl1r)
     ffusrl1    if   e           k disk    rename(fusrpfr:fusrl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     fbmhel1    if   e           k disk    rename(bmhepfr:bmhel1r)
     fbmdtlr    if   e           k disk    rename(bmdtpfr:bmdtlrr)
     f                                     prefix(bq:2)
     fbmdtlu    uf a e           k disk    rename(bmdtpfr:bmdtlur)
6.30 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
     fborgvd    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(bmfirm)
     d p_aarr          s                   like(bmaarr)
     d p_wsid          s                   like(bmwsid)
     d p_line          s                   like(bmline)
     d p_tots          s                   like(bntots)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
      *
      * Definering av datastrukturer
      *
      * Parametre for henting av pris -inn
      *
     d                 ds
     d hirec                        120
     d  hifirm                        3  0 overlay(hirec)
     d  hirkat                        3  0 overlay(hirec:4)
     d  hikund                        6  0 overlay(hirec:7)
     d  hikpro                        6  0 overlay(hirec:13)
     d  hivare                       15    overlay(hirec:19)
     d  hilety                        1  0 overlay(hirec:34)
     d  hienhe                        3    overlay(hirec:35)
     d  hidato                         d   overlay(hirec:38) datfmt(*iso)
     d  hitime                         t   overlay(hirec:48) timfmt(*iso)
     d  hinumm                        6  0 overlay(hirec:56)
     d  hikode                        1    overlay(hirec:62)
     d  hidekn                        5  2 overlay(hirec:63)
     d  hiavgf                        1  0 overlay(hirec:68)
     d  hipriv                        1  0 overlay(hirec:69)
     d  hitilb                        1    overlay(hirec:70)
     d  hiskaf                        1    overlay(hirec:71)
     d  hildor                        6  0 overlay(hirec:72)
     d  hisapr                        9  2 overlay(hirec:78)
     d  hikopr                        9  2 overlay(hirec:87)
8.01 d  hiprgr                        2    overlay(hirec:96)
     d  hiinlr                        1    overlay(hirec:120)
      *
      * Parametre for henting av pris -ut
      *
     d                 ds
     d horec                         80
     d  holety                        1  0 overlay(horec)
     d  hokpri                        1    overlay(horec:2)
     d  hooppr                        9  2 overlay(horec:3)
     d  hosapr                        9  2 overlay(horec:12)
     d  hokopr                        9  2 overlay(horec:21)
     d  horab1                        5  2 overlay(horec:30)
     d  horab2                        5  2 overlay(horec:35)
     d  hodekn                        5  2 overlay(horec:40)
     d  hopros                        5  2 overlay(horec:45)
     d  hokamp                        5    overlay(horec:50)
8.01 d  hoprgr                        2    overlay(horec:71)
      *
      * Definering av variabler i nøkler
      *
     d vvarlr_vare     s                   like(vvvare)
     d vvarl1_vare     s                   like(vvvare)
     d vvarl2_tek1     s                   like(vvtek1)
     d vvarl3_nobb     s                   like(vvnobb)
     d vvarl4_lvar     s                   like(vvlvar)
     d vvarl6_ogrp     s                   like(vvogrp)
     d vvarl7_ogrp     s                   like(vvogrp)
     d vvarl7_hgrp     s                   like(vvhgrp)
     d vvarl8_ogrp     s                   like(vvogrp)
     d vvarl8_hgrp     s                   like(vvhgrp)
     d vvarl8_ugrp     s                   like(vvugrp)
     d fusrl1_user     s                   like(fbuser)
     d rkunl1_kund     s                   like(rkkund)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
      *
      * Definering av variable
      *
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
     d w_spge          s              4  0
     d w_srrn          s              4  0
      *
     d w_seqe          s              2
     d b_feil          s              1    inz(*off)
     d b_open_sok      s              1    inz(*off)
     d b_vare          s              1    inz(*off)
6.20 d b_inlr          s              1    inz(*off)
      *
     d w_udat          s               d   datfmt(*iso)
6.20 d w_utda          s               d   datfmt(*iso)
6.20 d w_ldor          s                   like(vvldor)
6.30 d w_lv_nobb       s                   like(vvlnob)
6.30 d w_lv_modn       s                   like(vvlmod)
6.30 d w_lv_lldo       s                   like(vvlnle)
6.30 d w_lv_llva       s                   like(vvllva)
     d w_utim          s               t   timfmt(*iso)
     d w_mvap          s              6  2
     d w_mvat          s              5  4
     d w_mvaf          s             10  9
     d w_ste1          s             35
     d w_ste2          s             35
     d w_ste3          s             35
     d w_ste4          s             35
     d w_ste5          s             35
     d w_posi          s              3  0
     d w_otxt          s             35
     d w_htxt          s             35
     d w_utxt          s             35
     d w_ltxt          s             35
     d w_omr2          s             12  6
     d w_omr3          s             12  6
     d w_Ører          s              2  2
     d w_inkl          s             11  2
     d w_inkl_3        s             12  3
     d w_eksl          s             12  3
     d w_bbes          s             11  3
     d w_lety          s              1  0
     d w_stek_gml      s             35
8.01 d w_prgr          s              2
      *
     d w_firm          s                   like(bmfirm)
     d w_aarr          s                   like(bmaarr)
     d w_wsid          s                   like(bmwsid)
     d w_numm          s                   like(bnnumm)
     d w_line          s                   like(bmline)
     d w_vare          s                   like(bmvare)
     d w_lage          s                   like(bwlage)
     d w_suml          s                   like(bmsums)
     d w_rab1          s                   like(bmsums)
     d w_sumr          s                   like(bmsums)
     d w_sums          s                   like(bmsums)
     d w_tots          s                   like(bmsums)
     d w_enh1          s                   like(bmenh1)
     d w_sap1          s                   like(bmkopr)
     d w_bup1          s                   like(bmkopr)
     d w_enh2          s                   like(bmenh1)
     d w_sap2          s                   like(bmkopr)
     d w_bup2          s                   like(bmkopr)
     d w_enh3          s                   like(bmenh1)
     d w_sap3          s                   like(bmkopr)
     d w_bup3          s                   like(bmkopr)
     d w_bupr          s                   like(bmkopr)
     d w_tipr          s                   like(bmkopr)
     d w_kopr          s                   like(bmkopr)
      *
      * Nb! Det refereres imot bmkopr da bmsapr har 3 desimaler
      *
     d w_ogrp_gml      s                   like(vvogrp)
     d w_hgrp_gml      s                   like(vvhgrp)
     d w_ugrp_gml      s                   like(vvugrp)
     d w_anta_gml      s                   like(bmanta)
     d w_enh1_gml      s                   like(bmenh1)
     d w_sapr_gml      s                   like(bmsapr)
     d w_lety_gml      s                   like(bmlety)
      *
5.70 d w_vtyp          s                   like(vvvtyp)
      *
      * Konstanter
      *
     d c_sfil          s              2  0 inz(12)
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2 Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      dsp_subfile                                  DSP SFL
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2taga
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * F2=Registrering av tekstlinjer
      *
     c                   if        *inkb = *on
     c                   exsr      xc2win
     c                   goto      b2tagb
     c                   endif
      *
      * F9=Gjenta søk
      *
     c                   if        *inki = *on
     c                   eval      *in22 = *on
     c                   eval      b2ogrp = w_ogrp_gml
     c                   eval      b2hgrp = w_hgrp_gml
     c                   eval      b2ugrp = w_ugrp_gml
     c                   eval      b2stek = w_stek_gml
     c                   goto      b2tagb
     c                   endif
      *
      * F13=Varelinjene som registreres, skal alltid vises
      *
     c                   if        *inkm = *on
     c                   if        b_vare = *off
     c                   eval      b_vare = *on
     c                   else
     c                   eval      b_vare = *off
     c                   endif
     c                   goto      b2tagb
     c                   endif
      *
      * F14=Vis kunde-/kundeprosjekt-informasjon
      *
     c                   if        *inkn = *on
     c                   call      'FL510R'
     c                   parm                    w_firm
     c                   parm                    bnkund
     c                   parm                    bnkpro
     c                   goto      b2tagb
     c                   endif
      *
      * F15=Rabattmatrise for kunde/kundeprosjekt
      *
     c                   if        *inkp = *on
     c                   call      'FR510R'
     c                   parm                    w_firm
     c                   parm                    bnkund
     c                   parm                    bnkpro
     c                   goto      b2tagb
     c                   endif
      *
      * F21=Bytt mellom å utelate og ta med skaffevarer
      *
     c                   if        *inkv = *on
      *
     c                   if        *in83 = *off
     c                   eval      *in83 = *on
     c                   else
     c                   eval      *in83 = *off
     c                   endif
      *
     c                   exsr      forny                                        CRT SFL
     c                   goto      b2taga
      *
     c                   endif
      *
      * F22=Bytt mellom å vise salgspris inkl. mva og eksl. mva
      *
     c                   if        *inkw = *on
      *
     c                   if        *in84 = *off
     c                   eval      *in84 = *on
     c                   else
     c                   eval      *in84 = *off
     c                   endif
      *
     c                   exsr      forny                                        CRT SFL
     c                   goto      b2taga
      *
     c                   endif
      *
      * F23=Ny posisjonering (Varenr/Lev.varenr/Nobbnr)
      *
     c                   if        *inkx = *on
      *
     c                   if        *in80 = *on
     c                   eval      *in80 = *off
     c                   eval      *in81 = *on
     c                   exsr      forny                                        CRT SFL
     c                   goto      b2taga
     c                   endif
      *
     c                   if        *in81 = *on
     c                   eval      *in81 = *off
     c                   eval      *in82 = *on
     c                   exsr      forny                                        CRT SFL
     c                   goto      b2taga
     c                   endif
      *
     c                   if        *in82 = *on
     c                   eval      *in82 = *off
     c                   eval      *in80 = *on
     c                   exsr      forny                                        CRT SFL
     c                   goto      b2taga
     c                   endif
      *
     c                   endif
      *
      * Søking
      *
     c                   if        b2ogrp <> 0 or
     c                             b2hgrp <> 0 or
     c                             b2ugrp <> 0 or
     c                             b2stek <> *blank
     c                   exsr      sØk
     c                   goto      b2tagb
     c                   endif
      *
      * Posisjonering
      *
     c                   if        b2tek1 <> *blank or
     c                             b2vare <> 0 or
     c                             b2nobb <> 0 or
     c                             b2lvar <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
      * Plasserer cursor, og viser bildet på nytt
      *
     c                   eval      *in22 = *on
      *
     c                   goto      b2taga
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
      * Legger inn siste linje + total i returparametre
      *
     c                   eval      p_line = w_line
     c                   eval      p_tots = w_tots
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   select
     c                   when      w_seqe = 'O'
     c     vvarl6_key    setll     vvarl6
     c                   when      w_seqe = 'H'
     c     vvarl7_key    setll     vvarl7
     c                   when      w_seqe = 'U'
     c     vvarl8_key    setll     vvarl8
     c                   when      w_seqe = 'SO' or
     c                             w_seqe = 'SH' or
     c                             w_seqe = 'SU' or
     c                             w_seqe = 'S '
     c                   eval      b_open_sok = *on
     c                   when      w_seqe = 'V'
     c     vvarl1_key    setll     vvarl1
     c                   when      w_seqe = 'N'
     c     vvarl3_key    setll     vvarl3
     c                   when      w_seqe = 'L'
     c     vvarl4_key    setll     vvarl4
     c                   other
     c     vvarl2_key    setll     vvarl2
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for søking i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sØk           begsr
      *
      * Validering av varegruppe
      *
     c                   if        b2ogrp = 0 and
     c                             b2hgrp <> 0
     c                   eval      *in31 = *on
     c                   goto      end_sØk2
     c                   endif
      *
     c                   if        b2hgrp = 0 and
     c                             b2ugrp <> 0
     c                   eval      *in32 = *on
     c                   goto      end_sØk2
     c                   endif
      *
      * Tar vare på søk
      *
     c                   eval      w_ogrp_gml = b2ogrp
     c                   eval      w_hgrp_gml = b2hgrp
     c                   eval      w_ugrp_gml = b2ugrp
     c                   eval      w_stek_gml = b2stek
      *
      * Søk via søketekst
      *
     c                   if        b2stek <> *blank
      *
     c                   eval      b_open_sok = *on
      *
     c                   select
     c                   when      b2ogrp = 0 and
     c                             b2hgrp = 0 and
     c                             b2ugrp = 0
     c                   eval      w_seqe = 'S '
     c                   when      b2ogrp <> 0 and
     c                             b2hgrp = 0 and
     c                             b2ugrp = 0
     c                   eval      w_seqe = 'SO'
     c                   when      b2ogrp <> 0 and
     c                             b2hgrp <> 0 and
     c                             b2ugrp = 0
     c                   eval      w_seqe = 'SH'
     c                   when      b2ogrp <> 0 and
     c                             b2hgrp <> 0 and
     c                             b2ugrp <> 0
     c                   eval      w_seqe = 'SU'
     c                   endsl
      *
      * Utleder søketekster ut av søketekst
      *
     c                   call      'AS700R'
     c                   parm                    b2stek
     c                   parm                    w_ste1
     c                   parm                    w_ste2
     c                   parm                    w_ste3
     c                   parm                    w_ste4
     c                   parm                    w_ste5
      *
     c                   goto      end_sØk
      *
     c                   endif
      *
      * Søk på overgruppe
      *
     c                   if        b2ogrp <> 0 and
     c                             b2hgrp =  0 and
     c                             b2ugrp =  0
     c                   eval      w_seqe = 'O'
     c                   eval      vvarl6_ogrp = b2ogrp
     c     vvarl6_key    setll     vvarl6
     c                   goto      end_sØk
     c                   endif
      *
      * Søk på hovedgruppe
      *
     c                   if        b2ogrp <> 0 and
     c                             b2hgrp <> 0 and
     c                             b2ugrp =  0
     c                   eval      w_seqe = 'H'
     c                   eval      vvarl7_ogrp = b2ogrp
     c                   eval      vvarl7_hgrp = b2hgrp
     c     vvarl7_key    setll     vvarl7
     c                   goto      end_sØk
     c                   endif
      *
      * Søk på undergruppe
      *
     c                   if        b2ogrp <> 0 and
     c                             b2hgrp <> 0 and
     c                             b2ugrp <> 0
     c                   eval      w_seqe = 'U'
     c                   eval      vvarl8_ogrp = b2ogrp
     c                   eval      vvarl8_hgrp = b2hgrp
     c                   eval      vvarl8_ugrp = b2ugrp
     c     vvarl8_key    setll     vvarl8
     c                   goto      end_sØk
     c                   endif
      *
     c     end_sØk       tag
      *
      * Blanker subfile, og fyller opp i.h.t. søk
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker søkefelt
      *
     c                   eval      b2ogrp = 0
     c                   eval      b2hgrp = 0
     c                   eval      b2ugrp = 0
     c                   eval      b2stek = *blank
      *
     c     end_sØk2      endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjonerer på varetekst
      *
     c                   if        b2tek1 <> *blank
     c                   eval      w_seqe = *blank
     c                   eval      vvarl2_tek1 = b2tek1
     c     vvarl2_key    setll     vvarl2
     c                   goto      end_pos
     c                   endif
      *
      * Posisjonerer på varenummer
      *
     c                   if        b2vare <> 0
     c                   eval      w_seqe = 'V'
     c                   movel     b2vare        vvarl1_vare
     c     vvarl1_key    setll     vvarl1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjonerer på nobb-nummer
      *
     c                   if        b2nobb <> 0
     c                   eval      w_seqe = 'N'
     c                   eval      vvarl3_nobb = b2nobb
     c     vvarl3_key    setll     vvarl3
     c                   goto      end_pos
     c                   endif
      *
      * Posisjonerer på lev. varenummer
      *
     c                   if        b2lvar <> *blank
     c                   eval      w_seqe = 'L'
     c                   eval      vvarl4_lvar = b2lvar
     c     vvarl4_key    setll     vvarl4
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2tek1 = *blank
     c                   eval      b2vare = 0
     c                   eval      b2nobb = 0
     c                   eval      b2lvar = *blank
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
      * Beholder eksisterende posisjonering i subfilen
      *
     c                   if        wcurrn > 0
     c                   eval      wvisrn = wcurrn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      wvisrn = w_srrn
      *
      * Antall er utfylt, linje skal registreres
      *
     c                   if        b1anta <> 0
     c                   move      '->'          b1tek1
     c                   eval      c1vare = b1vare
     c                   exsr      registrer
     c                   eval      b1anta = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for registering av vare i ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     registrer     begsr
      *
     c                   eval      vvarlr_vare = c1vare
     c     vvarlr_key    chain     vvarlr                             90
      *
      * Kaller program for henting av pris-informasjon på tre enheter
      *
     c                   eval      w_lety = 0
      *
     c                   call      'VP715R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   parm                    w_lety
     c                   parm                    bnbdat
     c                   parm                    w_enh1
     c                   parm                    w_sap1
     c                   parm                    w_bup1
     c                   parm                    w_enh2
     c                   parm                    w_sap2
     c                   parm                    w_bup2
     c                   parm                    w_omr2
     c                   parm                    w_enh3
     c                   parm                    w_sap3
     c                   parm                    w_bup3
     c                   parm                    w_omr3
      *
      * Fyller opp butikk-linje
      *
     c                   eval      bmltyp = baalty
     c                   eval      bmanta = b1anta
     c                   eval      bmvare = vvvare
     c                   eval      bmtek1 = vvtek1
     c                   eval      bmtek2 = vvtek2
     c                   eval      bmenh1 = w_enh1
     c                   eval      bmlety = 0
      *
      * Henter inn priser og rabatter
      *
     c                   exsr      hent_pris
      *
     c                   eval      bmsapr = hosapr
     c                   eval      bmkopr = hokopr
     c                   eval      bmkpri = hokpri
     c                   eval      bmrab1 = horab1
     c                   eval      bmrab2 = horab2
     c                   eval      bmselk = 0
     c                   eval      bmkamp = hokamp
      * henter varetype
      *
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    vvvare
5.70 c                   parm                    bwlage
5.70 c                   parm                    w_vtyp
6.20 c                   parm                    w_utda
6.20 c                   parm                    w_ldor
6.20 c                   parm                    b_inlr
6.30 c                   parm                    w_lv_nobb
6.30 c                   parm                    w_lv_modn
6.30 c                   parm                    w_lv_lldo
6.30 c                   parm                    w_lv_llva
      *
      * Viser registeringbildet dersom
      * - 'vise vare' er valgt
      * - valgt på bruker
      * - vare er skaffevare, diverse-vare eller tjeneste
      * - pris = 0
      * - enhet-2 eller enhet-3 finnes på varen
      *
     c                   if        fbko05 <> 0 or
     c                             b_vare = *on or
5.70 c                             w_vtyp = 'S' or
5.70 c                             w_vtyp = 'D' or
5.70 c                             w_vtyp = 'T' or
     c                             bmsapr <= 0.02 or
     c                             w_enh1 <> *blank or
     c                             w_enh2 <> *blank
      *
     c                   exsr      xc1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_reg
     c                   endif
      *
     c                   endif
      *
      * Beregning av totaler og oppdatering av butikk-linje-register
      *
     c                   eval      w_sums = bmsapr * bmanta
     c                   eval      w_suml = w_sums
      *
      * Rabatt
      *
     c                   eval      w_rab1 = w_suml * bnrabp
     c                   eval      w_rab1 = w_rab1 / 100
     c                   eval      w_sumr = w_rab1
     c                   eval      w_suml = w_suml - w_rab1
      *
     c                   eval      w_rab1 = w_suml * bmrab1
     c                   eval      w_rab1 = w_rab1 / 100
     c                   eval      w_sumr = w_sumr + w_rab1
     c                   eval      w_suml = w_suml - w_rab1
      *
     c                   eval      w_rab1 = w_suml * bmrab2
     c                   eval      w_rab1 = w_rab1 / 100
     c                   eval      w_sumr = w_sumr + w_rab1
     c                   eval      w_suml = w_suml - w_rab1
      *
      * Kostpris
      *
     c                   if        bmkopr = 0
     c                   eval      bmkopr = bmsapr * hopros
     c                   endif
      *
      * Beregning av totaler pr.linje
      *
     c                   eval      w_sums = w_suml
      *
     c                   eval      w_tots = w_tots + w_sums
     c                   eval      bmsums = w_sums
      *
      * Legger inn totalen i skjermfelt for visning av totalsum
      *
     c                   exsr      beregn_total
      *
      * Sjekk om linje finnes fra før, i så fall legg til 10 på linjenummer,
      * og prøv på nytt (Dersom det er en tekstlinje som finnes fra før,
      * vil alle verdier i recorden bli borte. Derfor blir det gjort oppslag
      * mot en egen logisk fil med rename av felter)
      *
     c     c1tagn        tag
     c                   eval      w_line = w_line + 10
     c     bmdtl1_key    chain     bmdtlrr                            92
     c                   if        *in92 = *off
     c                   goto      c1tagn
     c                   endif
      *
      * Oppdaterer bong-linje-register
      *
     c     bmdtl1_key    chain     bmdtlur                            92
     c                   if        *in92 = *on
     c                   eval      bmfirm = w_firm
     c                   eval      bmaarr = w_aarr
     c                   eval      bmwsid = w_wsid
     c                   eval      bmnumm = w_numm
     c                   eval      bmline = w_line
     c                   write     bmdtlur
     c                   endif
      *
     c     end_reg       endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * C1 Behandle bilde for registrering av ny varelinje med antall
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1win        begsr
      *
      * Initierer skjermbildet
      *
     c                   eval      c1enh1 = bmenh1
     c                   eval      c1anta = bmanta
     c                   eval      c1sapr = bmsapr
     c                   eval      c1kopr = bmselk
     c                   eval      c1rab1 = bmrab1
     c                   eval      c1rab2 = bmrab2
     c                   eval      c1lety = bmlety
     c                   eval      c1tek1 = bmtek1
3.31 c                   eval      c1tek2 = bmtek2
     c                   eval      c1kpri = bmkpri
     c                   eval      c1enh4 = w_enh1
     c                   eval      c1enh5 = w_enh2
     c                   eval      c1enh6 = w_enh3
      *
      * Kaller subrutine for å vise antall i andre enheter
      *
     c                   if        w_enh2 <> *blank
     c                   exsr      kalkulator
     c                   endif
      *
      * Beregner salgspris inkl. mva
      *
     c                   eval      w_inkl = c1sapr
     c                   exsr      add_mva
     c                   eval      c1sapr = w_inkl
      *
      * Henter lagerinformasjon dersom vare er lagervare, og lager kjøres
      *
     c                   eval      *in86  = *off
     c                   eval      c1behl = 0
     c                   eval      c1pakk = 0
     c                   eval      c1disp = 0
      * henter varetype
      *
5.70 c                   call      'VL710R'
5.70 c                   parm                    p_firm
5.70 c                   parm                    c1vare
5.70 c                   parm                    bwlage
5.70 c                   parm                    w_vtyp
6.20 c                   parm                    w_utda
6.20 c                   parm                    w_ldor
6.20 c                   parm                    b_inlr
6.30 c                   parm                    w_lv_nobb
6.30 c                   parm                    w_lv_modn
6.30 c                   parm                    w_lv_lldo
6.30 c                   parm                    w_lv_llva
      *
     c                   if        baalag = 1 and
5.70 c                             w_vtyp = 'L'
     c                   call      'VL700R'
     c                   parm                    p_firm
     c                   parm                    c1vare
     c                   parm                    bwlage
     c                   parm                    c1enh1
     c                   parm                    c1behl
     c                   parm                    w_bbes
     c                   parm                    c1pakk
     c                   parm                    c1disp
     c                   eval      *in86 = *on
     c                   endif
      *
      * Tar vare på gamle verdier
      *
     c                   eval      w_anta_gml = c1anta
     c                   eval      w_enh1_gml = c1enh1
     c                   eval      w_sapr_gml = c1sapr
     c                   eval      w_lety_gml = c1lety
      *
      * Viser bildet
      *
     c     c1tagb        tag
      *
     c                   exfmt     c1win
      *
      * F2=Manuell beregning av kostpris
      *
     c                   if        *inkb = *on
      *
     c                   call      'FO524R'
     c                   parm                    w_kopr
      *
     c                   if        w_kopr <> 0
     c                   eval      c1kopr = w_kopr
     c                   endif
      *
     c                   goto      c1tagb
     c                   endif
      *
      * F3=Gå tilbake
      *
     c                   if        *inkc or *inkl
     c                   goto      end_c1win
     c                   endif
      *
      * F4=Spørring på leveringstype
      *
     c                   if        *inkd  = *on
     c                   if        wactfe = 'C1LETY'
     c                   call      'VY500R'
     c                   parm                    w_firm
     c                   parm                    c1lety
     c                   parm                    w_ltxt
     c                   goto      c1tagb
     c                   endif
     c                   endif
      *
      * F8=Produktinformasjon
      *
     c                   if        *inkh = *on
     c                   call      'FO704C'
     c                   parm                    w_firm
     c                   parm                    bmvare
     c                   goto      c1tagb
     c                   endif
      *
      * Sjekk enhet
      *
     c                   if        w_enh1 <> *blank
     c                   if        c1enh1 = *blank or
     c                             c1enh1 <> w_enh1 and
     c                             c1enh1 <> w_enh2 and
     c                             c1enh1 <> w_enh3
     c                   eval      *in31 = *on
     c                   goto      c1tagb
     c                   endif
     c                   endif
      *
      * Kaller subrutine for å vise antall i andre enheter
      *
     c                   if        w_enh2 <> *blank
     c                   exsr      kalkulator
     c                   endif
      *
      * Dersom enhet eller leveringstype er endret, og pris ikke er
      * overstyrt, kalkuleres ny pris og rabatt
      *
     c                   if        c1enh1 <> w_enh1_gml or
     c                             c1lety <> w_lety_gml
      *
     c                   eval      w_enh1_gml = c1enh1
     c                   eval      w_lety_gml = c1lety
      *
     c                   if        c1sapr = w_sapr_gml or
     c                             c1sapr = 0
      *
      * Henter priser og rabatter
      *
     c                   eval      bmenh1 = c1enh1
     c                   eval      bmlety = c1lety
     c                   exsr      hent_pris
      *
     c                   eval      w_inkl = hosapr
     c                   exsr      add_mva
     c                   eval      hosapr = w_inkl
      *
      * Oppdater inn ny pris bare dersom pris er forsjellig fra null
      *
     c                   if        hosapr <> 0
     c                   eval      c1sapr = hosapr
     c                   eval      bmkopr = hokopr
     c                   eval      c1kpri = hokpri
     c                   eval      c1rab1 = horab1
     c                   eval      c1rab2 = horab2
     c                   eval      w_sapr_gml = c1sapr
     c                   eval      w_anta_gml = c1anta
     c                   goto      c1tagb
     c                   endif
      *
     c                   endif
     c                   endif
      *
      * Viser bildet på nytt dersom varen har flere enheter, antall er
      * endret og bildet ikke allerede er vist
      *
     c                   if        w_enh2 <> *blank and
     c                             c1anta <> w_anta_gml
     c                   eval      w_anta_gml = c1anta
     c                   goto      c1tagb
     c                   endif
      *
      * Sjekk pris
      *
     c                   if        c1sapr <= 0.01
     c                   eval      *in32 = *on
     c                   goto      c1tagb
     c                   endif
      *
      * Sjekk tekst
      *
     c                   if        c1tek1 = *blank
     c                   eval      *in33 = *on
     c                   goto      c1tagb
     c                   endif
      *
      * Legger tilbake verdiene fra skjermbildet
      *
     c                   eval      bmanta = c1anta
     c                   eval      bmselk = c1kopr
     c                   eval      bmrab1 = c1rab1
     c                   eval      bmrab2 = c1rab2
     c                   eval      bmlety = c1lety
     c                   eval      bmkpri = c1kpri
     c                   eval      bmtek1 = c1tek1
3.31 c                   eval      bmtek2 = c1tek2
     c                   eval      bmenh1 = c1enh1
      *
     c                   eval      w_eksl = c1sapr
     c                   exsr      mva_sub
     c                   eval      bmsapr = w_eksl
      *
     c     end_c1win     endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * C2 Behandle bilde for registrering av ny tekstlinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2win        begsr
      *
      * Blanker felter som benyttes i rutinen
      *
     c                   eval      c2text = *blank
      *
      * Send alt
      *
     c                   write     c2win
     c                   exfmt     c2win
      *
      * F3=Gå tilbake
      *
     c                   if        *inkc or *inkl
     c                   goto      end_c2win
     c                   endif
      *
      * Flytter informasjon fra bildet til register-feltene
      *
     c                   movel     c2text        bmtek1
     c                   move      c2text        bmtek2
      *
     c                   eval      bmltyp = baatty
      *
      * Blanker felter som ikke er benyttet under tekstregistrering
      *
     c                   eval      bmkamp = *blank
     c                   eval      bmvare = *blank
     c                   eval      bmenh1 = *blank
     c                   eval      bmkpri = *blank
     c                   eval      bmlety = 0
     c                   eval      bmanta = 0
     c                   eval      bmsapr = 0
     c                   eval      bmkopr = 0
     c                   eval      bmselk = 0
      *
     c                   eval      bmsums = 0
      *
     c                   eval      w_line = w_line + 10
     c     bmdtl1_key    chain     bmdtlur                            92
     c                   if        *in92 = *on
     c                   eval      bmfirm = w_firm
     c                   eval      bmaarr = w_aarr
     c                   eval      bmwsid = w_wsid
     c                   eval      bmnumm = w_numm
     c                   eval      bmline = w_line
     c                   write     bmdtlur
     c                   endif
      *
     c     end_c2win     endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter opplysninger om varen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_pris     begsr
      *
     c                   eval      hifirm = w_firm
     c                   eval      hirkat = 0
      *
     c                   eval      hikund = bnkund
     c                   if        bnlkun <> 0
     c                   eval      hikund = bnlkun
     c                   endif
      *
     c                   eval      hikpro = bnkpro
     c                   eval      hivare = vvvare
     c                   eval      hienhe = bmenh1
     c                   eval      hilety = bmlety
     c                   eval      hidato = bnbdat
     c                   eval      hinumm = 0
     c                   eval      hikode = *blank
     c                   eval      hidekn = 0
     c                   eval      hiavgf = 0
     c                   eval      hipriv = 0
     c                   eval      hiskaf = *off
     c                   eval      hildor = 0
     c                   eval      hisapr = 0
     c                   eval      hikopr = 0
5.70 c                   eval      hiprgr = w_prgr
      *
     c                   time                    hitime
      *
     c                   call      'VP900R'
     c                   parm                    hirec
     c                   parm                    horec
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Beregning av beløp inkl. moms
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     add_mva       begsr
      *
     c                   if        baamva = 1 and
     c                             bnmkod = 0
     c                   eval(h)   w_inkl_3 = w_inkl * w_mvat
     c                   eval(h)   w_inkl   = w_inkl_3 * 1
     c                   endif
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Beregning av beløp eksl. moms
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     mva_sub       begsr
      *
     c                   if        baamva = 1 and
     c                             bnmkod = 0
     c                   eval(h)   w_eksl = w_eksl * w_mvaf
     c                   endif
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Beregning av totalsum for vising i skjermbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     beregn_total  begsr
      *
      * Beregner totalsum inkl. mva
      *
     c                   eval      w_inkl = w_tots
     c                   exsr      add_mva
     c                   eval      b2tots = w_inkl
      *
      * Avrunder totalsum
      *
     c                   move      b2tots        w_Ører
      *
     c                   select
     c                   when      w_Ører < 0.250
     c                   eval      b2tots = b2tots - w_Ører
     c                   when      w_Ører > 0.749
     c                   eval      b2tots = b2tots - w_Ører + 1
     c                   other
     c                   eval      b2tots = b2tots - w_Ører + 0,5
     c                   endsl
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Kalkulator for omregning mellom enheter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kalkulator    begsr
      *
     c                   if        c1enh1 <> *blank
      *
     c                   eval      c1ant1 = 0
     c                   eval      c1ant2 = 0
     c                   eval      c1ant3 = 0
      *
     c                   select
     c                   when      c1enh1 = w_enh1
     c                   eval      c1ant1 = c1anta
     c                   if        w_omr2 <> 0
     c                   eval(h)   c1ant2 = c1anta / w_omr2
     c                   endif
     c                   if        w_omr3 <> 0
     c                   eval(h)   c1ant3 = c1anta / w_omr3
     c                   endif
     c                   when      c1enh1 = w_enh2
     c                   eval(h)   c1ant1 = c1anta * w_omr2
     c                   eval      c1ant2 = c1anta
     c                   if        w_omr3 <> 0
     c                   eval(h)   c1ant3 = c1ant1 / w_omr3
     c                   endif
     c                   when      c1enh1 = w_enh3
     c                   eval(h)   c1ant1 = c1anta * w_omr3
     c                   if        w_omr2 <> 0
     c                   eval(h)   c1ant2 = c1ant1 / w_omr2
     c                   endif
     c                   eval      c1ant3 = c1anta
     c                   endsl
      *
     c                   endif
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Display subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
     c                   eval      *in22 = *off
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Tøm subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Fyll opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
      * Initierer SQL for søk
      *
     c                   if        b_open_sok = *on
      *
     c                   select
     c                   when      w_seqe = 'SO'
     c/exec sql
     c+                  declare sok_o cursor for
     c+                  select vvsfir,
     c+                         vvsogr,
     c+                         vvstek,
     c+                         vvsvar
     c+                  from   vvsopf
     c+                  where  vvsfir = :w_firm and
     c+                         vvsogr = :b2ogrp and
     c+                         vvstek like :w_ste1
     c+                  order by vvsfir, vvsogr, vvstek
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_o
     c/end-exec
     c/exec sql
     c+                  open sok_o
     c/end-exec
      *
     c                   when      w_seqe = 'SH'
     c/exec sql
     c+                  declare sok_h cursor for
     c+                  select vvsfir,
     c+                         vvsogr,
     c+                         vvshgr,
     c+                         vvstek,
     c+                         vvsvar
     c+                  from   vvsopf
     c+                  where  vvsfir = :w_firm and
     c+                         vvsogr = :b2ogrp and
     c+                         vvshgr = :b2hgrp and
     c+                         vvstek like :w_ste1
     c+                  order by vvsfir, vvsogr, vvshgr, vvstek
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_h
     c/end-exec
     c/exec sql
     c+                  open sok_h
     c/end-exec
      *
     c                   when      w_seqe = 'SU'
     c/exec sql
     c+                  declare sok_u cursor for
     c+                  select vvsfir,
     c+                         vvsogr,
     c+                         vvshgr,
     c+                         vvsugr,
     c+                         vvstek,
     c+                         vvsvar
     c+                  from   vvsopf
     c+                  where  vvsfir = :w_firm and
     c+                         vvsogr = :b2ogrp and
     c+                         vvshgr = :b2hgrp and
     c+                         vvsugr = :b2ugrp and
     c+                         vvstek like :w_ste1
     c+                  order by vvsfir, vvsogr, vvshgr, vvsugr, vvstek
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_u
     c/end-exec
     c/exec sql
     c+                  open sok_u
     c/end-exec
      *
     c                   when      w_seqe = 'S '
     c/exec sql
     c+                  declare sok cursor for
     c+                  select vvsfir,
     c+                         vvstek,
     c+                         vvsvar
     c+                  from   vvsopf
     c+                  where  vvsfir = :w_firm and
     c+                         vvstek like :w_ste1
     c+                  order by vvsfir, vvstek
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok
     c/end-exec
     c/exec sql
     c+                  open sok
     c/end-exec
      *
     c                   endsl
      *
     c                   endif
      *
      * Fyller opp subfile
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'O'
     c     vvarl6_key    reade     vvarl6                                 15
     c                   when      w_seqe = 'H'
     c     vvarl7_key    reade     vvarl7                                 15
     c                   when      w_seqe = 'U'
     c     vvarl8_key    reade     vvarl8                                 15
     c                   when      w_seqe = 'SO'
     c/exec sql
     c+                  fetch next
     c+                  from  sok_o
     c+                  into  :vvsfir,
     c+                        :vvsogr,
     c+                        :vvstek,
     c+                        :vvsvar
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'SH'
     c/exec sql
     c+                  fetch next
     c+                  from  sok_h
     c+                  into  :vvsfir,
     c+                        :vvsogr,
     c+                        :vvshgr,
     c+                        :vvstek,
     c+                        :vvsvar
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'SU'
     c/exec sql
     c+                  fetch next
     c+                  from  sok_u
     c+                  into  :vvsfir,
     c+                        :vvsogr,
     c+                        :vvshgr,
     c+                        :vvsugr,
     c+                        :vvstek,
     c+                        :vvsvar
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'S '
     c/exec sql
     c+                  fetch next
     c+                  from  sok
     c+                  into  :vvsfir,
     c+                        :vvstek,
     c+                        :vvsvar
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'V'
     c                   read      vvarl1                                 15
     c                   when      w_seqe = 'N'
     c                   read      vvarl3                                 15
     c                   when      w_seqe = 'L'
     c                   read      vvarl4                                 15
     c                   other
     c                   read      vvarl2                                 15
     c                   endsl
      *
     c                   if        *in15 = *on
     c                   goto      end_crt
     c                   endif
      *
     c                   if        w_seqe = 'SO' or
     c                             w_seqe = 'SH' or
     c                             w_seqe = 'SU' or
     c                             w_seqe = 'S '
      *
     c                   if        w_ste2 <> *blank
      *
     c                   eval      w_posi = %scan(%trim(w_ste2):%trim(vvstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
      *
     c                   if        w_ste3 <> *blank
     c                   eval      w_posi = %scan(%trim(w_ste3):%trim(vvstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        w_ste4 <> *blank
     c                   eval      w_posi = %scan(%trim(w_ste4):%trim(vvstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        w_ste5 <> *blank
     c                   eval      w_posi = %scan(%trim(w_ste5):%trim(vvstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
     c                   endif
      *
     c                   endif
      *
     c                   eval      vvarlr_vare = vvsvar
     c     vvarlr_key    chain     vvarlr                             91
     c                   if        *in91 = *on
     c                   iter
     c                   endif
      *
     c                   endif
      *
     c                   if        vvfirm <> w_firm
     c                   eval      *in15 = *on
     c                   goto      end_crt
     c                   endif
      *
      * henter varetype
      *
5.70 c                   call      'VL710R'
5.70 c                   parm                    p_firm
5.70 c                   parm                    vvvare
5.70 c                   parm                    bwlage
5.70 c                   parm                    w_vtyp
6.20 c                   parm                    w_utda
6.20 c                   parm                    w_ldor
6.20 c                   parm                    b_inlr
      *
     c                   if        *in83 = *off and
5.70 c                             w_vtyp = 'S'
     c                   iter
     c                   endif
      *
      * Skriver til subfile
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1tek1 = vvtek1
     c                   eval      b1tek2 = vvtek2
     c                   eval      b1kpri = vvkpri
5.70 c                   eval      b1vtyp = w_vtyp
     c                   eval      b1vare = vvvare
      *
6.21 c                   if        w_utda <> *loval
6.21 c                   if        w_utda <= w_udat
     c                   eval      b1tek1 = %subst(vvtek1:1:22) +
     c                                      ' ** UTGÅTT **'
6.21 c                   else
6.21 c                   eval      b1tek1 = %subst(vvtek1:1:22) +
6.21 c                                      ' ** UTGÅR  **'
6.21 c                   endif
     c                   endif
6.21 c                   if        vvnuda <> *loval and
6.21 c                             vvnuda <= w_udat
6.21 c                   eval      b1tek1 = %subst(vvtek1:1:22) +
6.21 c                                      ' **NOBB.UTG**'
6.21 c                   endif
      *
     c                   eval      w_lety = 0
     c                   time                    w_utim
      *
     c                   call      'VP710R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   parm                    w_lety
     c                   parm                    bnbdat
     c                   parm                    w_utim
     c                   parm                    b1enhe
     c                   parm                    b1sapr
     c                   parm                    w_bupr
     c                   parm                    w_tipr
      *
     c                   if        w_bupr <> 0
     c                   eval      b1kpri = '*'
     c                   eval      b1sapr = w_bupr
     c                   endif
      *
     c                   if        w_tipr <> 0
     c                   if        w_bupr = 0 or
     c                             w_tipr <= w_bupr
     c                   eval      b1kpri = 'T'
     c                   eval      b1sapr = w_tipr
     c                   endif
     c                   endif
      *
     c                   select
     c                   when      *in80 = *on
     c                   eval      b1nypo = vvvare
     c                   when      *in81 = *on
     c                   eval      b1nypo = vvlvar
     c                   when      *in82 = *on
6.30 c                   if        w_lv_nobb > 0
6.30 c                   eval      b1nypo = %editc(w_lv_nobb : 'Z')
6.30 c                   else
     c                   eval      b1nypo = %editc(vvnobb : 'Z')
6.30 c                   endif
     c                   endsl
      *
     c                   if        *in84 = *off
     c                   eval      w_inkl = b1sapr
     c                   exsr      add_mva
     c                   eval      b1sapr = w_inkl
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      wvisrn = w_sfrn
      *
     c                   eval      b_open_sok = *off
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Overgruppe
      *
     c                   if        wactfe = 'B2OGRP'
     c                   eval      b2hgrp = 0
     c                   eval      b2ugrp = 0
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    w_otxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        wactfe = 'B2HGRP'
     c                   eval      b2ugrp = 0
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    b2hgrp
     c                   parm                    w_htxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Undergruppe
      *
     c                   if        wactfe = 'B2UGRP'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    b2hgrp
     c                   parm                    b2ugrp
     c                   parm                    w_utxt
     c                   goto      end_spØrr
     c                   endif
      *
     c     end_spØrr     endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_aarr
     c                   parm                    p_wsid
     c                   parm                    p_line
     c                   parm                    p_tots
      *
      * Key for oppslag på vare
      *
     c     vvarlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlr_vare
      *
      * Key for posisjonering på varenummer
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for posisjonering på varetekst
      *
     c     vvarl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl2_tek1
      *
      * Key for posisjonering på nobb-nummer
      *
     c     vvarl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl3_nobb
      *
      * Key for posisjonering på leverandørs varenummer
      *
     c     vvarl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl4_lvar
      *
      * Key for søk på overgruppe
      *
     c     vvarl6_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl6_ogrp
      *
      * Key for søk på overgruppe/hovedgruppe
      *
     c     vvarl7_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl7_ogrp
     c                   kfld                    vvarl7_hgrp
      *
      * Key for søk på overgruppe/hovedgruppe/undergruppe
      *
     c     vvarl8_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl8_ogrp
     c                   kfld                    vvarl8_hgrp
     c                   kfld                    vvarl8_ugrp
      *
      * Key for oppslag på butkk-kasse-register
      *
     c     bwsdl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_wsid
      *
      * Key for oppslag på butikk-kode-register
      *
     c     bstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på bruker-register
      *
     c     fusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fusrl1_user
      *
      * Key for oppslag på kunde-register
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på kundeprosjekt-register
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      * Key for oppslag på butikk-hode-register
      *
     c     bmhel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_aarr
     c                   kfld                    w_wsid
     c                   kfld                    w_numm
      *
      * Key for oppslag på butikk-linje-register
      *
     c     bmdtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_aarr
     c                   kfld                    w_wsid
     c                   kfld                    w_numm
     c                   kfld                    w_line
      *
      *
      * Henter nøkkel-verdier fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_aarr = p_aarr
     c                   eval      w_wsid = p_wsid
     c                   eval      w_numm = 0
     c                   eval      w_line = p_line
      *
      * Henter dagens dato
      *
     c                   time                    w_udat
      *
      * Henter opplysninger fra butikk-kasse-register
      *
     c     bwsdl1_key    chain     bwsdl1r                            90
      *
      * Hent opplysninger fra butikk-kode-register
      *
     c     bstsl1_key    chain     bstsl1r                            90
      *
      * Henter opplysninger fra bruker-register
      *
     c                   eval      fusrl1_user = l_user
     c     fusrl1_key    chain     fusrl1                             90
      *
      * Henter opplysninger fra butikk-hode
      *
     c     bmhel1_key    chain     bmhel1r                            90
     c                   if        *in90 = *on
     c                   time                    bnbdat
     c                   time                    bntime
     c                   endif
      *
      * Finner moms-sats
      *
     c                   call      'FÅ705R'
     c                   parm                    w_firm
     c                   parm                    bnbdat
     c                   parm                    w_mvap
     c                   parm                    w_mvat
     c                   parm                    w_mvaf
      *
      * Beregner sum som skal vises på skjermen
      *
     c                   eval      w_tots = p_tots
      *
     c                   exsr      beregn_total
      *
      * Fyller opp skjermbildet for første visning
      *
     c                   eval      rkunl1_kund = bnkund
     c     rkunl1_key    chain     rkunl1                             90
     c                   eval      b2kund = bnkund
     c                   eval      b2knav = rknavn
      *
     c                   if        bnkpro <> 0
     c                   eval      fkprl1_kund = bnkund
     c                   eval      fkprl1_kpro = bnkpro
     c     fkprl1_key    chain     fkprl1                             90
     c                   eval      b2kpro = bnkpro
     c                   eval      b2pnav = flnavn
     c                   endif
      *
     c                   eval      *in22 = *on
     c                   eval      *in80 = *on
5.70  *
5.70  * henter prisgruppe
5.70  *
5.70 c                   call      'VL711R   '
5.70 c                   parm                    w_prgr
      *
     c     vvarl2_key    setll     vvarl2
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
