     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JV601R
      * Beskrivelse . . : Utskrift av vareregister, pris-/vareinformasjon
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       210596 HKO  Nytt program
      *       280506 HKO  Endret parameter ved kall av JV750R
      *       280508 bof  Utvidet parm. til JV750R
5.70  * PRGR  191108 bof  Utvidet med prisgruppe
6.10  *       250809 bof  Fjernet 000 fra pakning, ligger på vvarpf
6.10  *       260809 bof  Endret statuskode fra num. til alfa.
6.30  *       280114 bof  Utvidet nøkkel med lev.nr i JVPKPF
6.32  *       140314 bof  Lagt inn test på fra /til dato på pakning
6.33  *       060814 bof  Utvidelse med fraktbetingelse
6.3x  * 6.30  040615 bof  utvidet parm i forb. med prgr. på betingelse
6.3y  * 6.30  050815 bof  utv. parm i forb. med prgr. på frakt + oppdat.
6.34  * 6.30  010416 bof  rettelse i parm til jv750r                   .
8.01  *PRG2   150622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjvarl9    if   e           k disk    rename(jvarpfr:jvarl9r)
     fjvdtl1    if   e           k disk    rename(jvdtpfr:jvdtl1r)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     fjvpkl1    if   e           k disk    rename(jvpkpfr:jvpkl1r)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fausrl1    if   e           k disk    rename(ausrpfr:ausrl1r)
      *
     fjv601p    o    e             printer
      *
      * Definering av parametre
      *
     d p_parm          s            120
      *
      * Definering av local data area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
      *
      * Datastruktur for parameterliste -inn
      *
     d                 ds
     d d_list                       120
     d  d_firm                        3  0 overlay(d_list)
     d  d_valg                        1  0 overlay(d_list:4)
     d  d_ldor                        6  0 overlay(d_list:5)
     d  d_eusr                       10    overlay(d_list:11)
     d  d_ogrf                        2  0 overlay(d_list:21)
     d  d_hgrf                        2  0 overlay(d_list:23)
     d  d_ugrf                        3  0 overlay(d_list:25)
     d  d_ogrt                        2  0 overlay(d_list:28)
     d  d_hgrt                        2  0 overlay(d_list:30)
     d  d_ugrt                        3  0 overlay(d_list:32)
     d  d_varf                       15    overlay(d_list:35)
     d  d_vart                       15    overlay(d_list:50)
     d  d_edaf                         d   overlay(d_list:65) datfmt(*iso)
     d  d_edat                         d   overlay(d_list:75) datfmt(*iso)
      *
      * Definering av variable i nøkler
      *
     d jvarl9_ldor     s                   like(jvldor)
     d jvarl9_ogrp     s                   like(jvogrp)
     d jvarl9_hgrp     s                   like(jvhgrp)
     d jvarl9_ugrp     s                   like(jvugrp)
     d jvdtl1_dvar     s                   like(jvdvar)
     d jvpkl1_vare     s                   like(jwvare)
6.30 d jvpkl1_ldor     s                   like(jwldor)
     d jlevl1_ldor     s                   like(jlldor)
     d ausrl1_user     s                   like(abuser)
      *
      * Definering av variable
      *
     d b_fØrste        s              1
6.32 d b_pakn          s              1    inz(*off)
      *
     d w_firm          s              3  0
     d w_lety          s              1  0
     d w_ldor          s              6  0
     d w_lvar          s             15
     d w_eldo          s              6  0
     d w_gdat          s               d   datfmt(*iso)
     d w_enhe          s              3
     d w_enh2          s              3
     d w_enh3          s              3
     d w_eni1          s              3
     d w_eni2          s              3
     d w_eni3          s              3
     d w_enp1          s              3
     d w_enp2          s              3
     d w_enp3          s              3
     d w_ofp1          s             12  6
     d w_ofp2          s             12  6
     d w_ofp3          s             12  6
     d w_ofv2          s             12  6
     d w_ofv3          s             12  6
     d w_ofi1          s             12  6
     d w_ofi2          s             12  6
     d w_ofi3          s             12  6
     d w_bldo          s              6  0
     d w_bogr          s              2  0
     d w_bhgr          s              2  0
     d w_bugr          s              3  0
     d w_bmod          s              8  0
     d w_bvar          s             15
     d w_bvty          s              1
     d w_blet          s              1  0
     d w_bbet          s             35
     d w_ntpr          s              8  2
     d w_ifak          s              8  6
     d w_pogr          s              2  0
     d w_phgr          s              2  0
     d w_pugr          s              3  0
     d w_pldo          s              6  0
     d w_pmod          s              8  0
     d w_pvar          s             15
     d w_pkpr          s              1
     d w_pvty          s              1
     d w_plet          s              1  0
     d w_kfak          s              8  6
     d w_kbel          s              7  2
     d w_sfak          s              8  6
     d w_midl          s              1  0
     d w_inpr          s              9  2
     d w_kopr          s              9  2
     d w_s1im          s              9  2
     d w_s2pr          s              9  2
     d w_s2im          s              9  2
     d w_s3pr          s              9  2
     d w_s3im          s              9  2
8.01 d w_prgr          s              2
8.01 d w_prut          s              2
8.01 d w_pgbe          s              2
8.01 d w_pgfr          s              2
6.3y d w_oppd          s              1
6.32 d w_dato          s                   like(jwodat)
6.33 d w_ffak          s              8  6
6.33 d w_inp2          s              9  2
6.34 d w_utgp          s              1  0
      *
     d w_utim          s              6  0
     d w_gprk          s              1  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Header
      *
     c                   exsr      hode
      *
      * Detalj
      *
     c                   exsr      detalj
      *
      * Avslutter prorgram
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering og utskrift av hode-opplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode          begsr
      *
      * Legger inn arb.stasjon og bruker i faste felter
      *
     c                   eval      a1firm = d_firm
     c                   eval      a1user = l_user
     c                   eval      a1wsid = l_wsid
     c                   eval      a1date = udate
     c                   time                    w_utim
     c                   eval      a1time = w_utim
      *
      * Henter firmaopplysninger
      *
     c     afirl1_key    chain     afirl1r
     c                   eval      a1fnav = aafnav
      *
      * Henter parametre med beskrivelse
      *
     c                   eval      a2ldor = d_ldor
     c                   eval      a2eusr = d_eusr
     c                   eval      a2ogrf = d_ogrf
     c                   eval      a2hgrf = d_hgrf
     c                   eval      a2ugrf = d_ugrf
     c                   eval      a2ogrt = d_ogrt
     c                   eval      a2hgrt = d_hgrt
     c                   eval      a2ugrt = d_ugrt
     c                   eval      a2varf = d_varf
     c                   eval      a2vart = d_vart
      *
     c                   if        d_ldor <> 0
     c                   eval      jlevl1_ldor = d_ldor
     c     jlevl1_key    chain     jlevl1
     c                   if        %found
     c                   eval      a2lnav = jlnavn
     c                   endif
     c                   endif
      *
     c                   if        d_eusr <> *blank
     c                   eval      ausrl1_user = d_eusr
     c     ausrl1_key    chain     ausrl1
     c                   if        %found
     c                   eval      a2unav = abnavn
     c                   endif
     c                   endif
      *
     c                   if        d_edaf <> *loval
     c     *dmy          move      d_edaf        a2edaf
     c                   endif
      *
     c                   if        d_edat <> *loval
     c     *dmy          move      d_edat        a2edat
     c                   endif
      *
      * Skriver heading
      *
     c                   write     a1hdr
     c                   write     a2hdr
     c                   write     a3hdr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utplukk til - og utkskrift av detaljlinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     detalj        begsr
      *
     c                   eval      jvarl9_ldor = d_ldor
     c                   eval      jvarl9_ogrp = d_ogrf
     c                   eval      jvarl9_hgrp = d_hgrf
     c                   eval      jvarl9_ugrp = d_ugrf
     c     jvarl9_key    setll     jvarl9
     c                   read      jvarl9
     c                   dow       not %eof
      *
      * Test på leverandør
      *
     c                   if        d_ldor <> 0 and
     c                             jvldor <> d_ldor
     c                   leave
     c                   endif
      *
      * Test på varegruppe
      *
     c                   if        d_ogrt <> 0 and
     c                             jvogrp > d_ogrt
     c                   leave
     c                   endif
      *
     c                   if        d_hgrt <> 0 and
     c                             jvogrp = d_ogrt and
     c                             jvhgrp > d_hgrt
     c                   leave
     c                   endif
      *
     c                   if        d_ugrt <> 0 and
     c                             jvogrp = d_ogrt and
     c                             jvhgrp = d_hgrt and
     c                             jvugrp > d_ugrt
     c                   leave
     c                   endif
      *
      * Test på bruker
      *
     c                   if        d_eusr <> *blank and
     c                             jveusr <> d_eusr
     c                   goto      neste_vare
     c                   endif
      *
      * Test på varenummer
      *
     c                   if        d_varf <> *blank or
     c                             d_vart <> *blank
     c                   if        jvvare < d_varf or
     c                             jvvare > d_vart
     c                   goto      neste_vare
     c                   endif
     c                   endif
      *
      * Test på endringsdato
      *
     c                   if        d_edaf <> *loval or
     c                             d_edat <> *loval
     c                   if        jvedat < d_edaf or
     c                             jvedat > d_edat
     c                   goto      neste_vare
     c                   endif
     c                   endif
      *
      * Behandling ved brudd på leverandør
      *
     c                   if        d_ldor = 0 and
     c                             jvldor <> a4ldor
     c                   eval      a4ldor = jvldor
     c                   eval      jlevl1_ldor = jvldor
     c     jlevl1_key    chain     jlevl1
     c                   if        %found
     c                   eval      a4lnav = jlnavn
     c                   endif
     c                   if        b_fØrste = *off
     c                   write     a1hdr
     c                   write     a3hdr
     c                   endif
     c                   write     a4hdr
     c                   eval      b_fØrste = *off
     c                   endif
      *
      * Henter vare-detalj informasjon
      *
     c                   eval      jvdtl1_dvar = jvvare
     c     jvdtl1_key    chain     jvdtl1
     c                   if        not %found
     c                   eval      jvdsko = 0
     c                   eval      jvdpko = *blank
     c                   endif
      *
      * Flytter verdier til detaljlinje
      *
     c                   eval      d1ogrp = jvogrp
     c                   eval      d1hgrp = jvhgrp
     c                   eval      d1ugrp = jvugrp
     c                   eval      d1tek1 = jvtek1
     c                   eval      d1vare = jvvare
     c                   eval      d1nobb = jvnobb
     c                   eval      d1pkod = jvdpko
     c                   eval      d1skod = jvdsko
6.10 c                   eval      d1eko1 = jvpenh
     c                   eval      d1gdat = 0
     c                   eval      d1udat = 0
     c                   eval      d1eko2 = *blank
     c                   eval      d1eko3 = *blank
     c                   eval      d1lnav = *blank
      *
     c                   if        jvudat <> *loval
     c     *dmy          move      jvudat        d1udat
     c                   endif
      *
     c                   if        w_gdat <> *loval
     c     *dmy          move      w_gdat        d1gdat
     c                   endif
      *
      * Henter vareenhet og opptil to alternative enheter
      *
     c                   eval      jvpkl1_vare = jvvare
6.30 c                   eval      jvpkl1_ldor = d_ldor
     c     jvpkl1_key    setll     jvpkl1
     c     jvpkl1_key    reade     jvpkl1
     c                   dow       not %eof
6.32  *
6.32  * sjekker fra / tildato
6.32 c                   exsr      sjekk_pkdato
6.32 c                   if        b_pakn = *on
      *
      *
     c                   if        jwenhe <> d1eko1
     c                   if        d1eko2 = *blank
     c                   eval      d1eko2 = jwenhe
     c                   else
     c                   if        d1eko3 = *blank
     c                   eval      d1eko3 = jwenhe
     c                   endif
     c                   endif
     c                   endif
      *
6.32 c                   endif
      *
     c     jvpkl1_key    reade     jvpkl1
     c                   enddo
      *
      * Henter enhet-1 og prisinformasjon fra fellesprogram
      *
     c                   eval      w_ldor = 0
     c                   eval      w_lety = 0
     c                   eval      w_gprk = 0
6.3y c                   eval      w_oppd = *blank
6.34 c                   eval      w_utgp = 1
      *
     c                   call      'JV750R'
     c                   parm                    w_firm
     c                   parm                    jvvare
5.70 c                   parm                    w_prgr
     c                   parm                    w_ldor
     c                   parm                    jvdpko
     c                   parm                    jvdsko
     c                   parm                    w_lety
     c                   parm                    w_gprk
6.34 c                   parm                    w_dato
6.34 c                   parm                    w_utgp
      *
     c                   parm                    d1osts
      *
     c                   parm                    d1ldor
     c                   parm                    w_lvar
     c                   parm                    w_eldo
     c                   parm                    d1pris
     c                   parm                    w_gdat
      *
     c                   parm                    w_enhe
     c                   parm                    d1enh1
     c                   parm                    w_enh2
     c                   parm                    w_enh3
     c                   parm                    w_eni1
     c                   parm                    w_eni2
     c                   parm                    w_eni3
     c                   parm                    w_enp1
     c                   parm                    w_enp2
     c                   parm                    w_enp3
     c                   parm                    w_ofp1
     c                   parm                    w_ofp2
     c                   parm                    w_ofp3
     c                   parm                    w_ofv2
     c                   parm                    w_ofv3
     c                   parm                    w_ofi1
     c                   parm                    w_ofi2
     c                   parm                    w_ofi3
      *
     c                   parm                    w_bldo
     c                   parm                    w_bogr
     c                   parm                    w_bhgr
     c                   parm                    w_bugr
     c                   parm                    w_bmod
     c                   parm                    w_bvar
     c                   parm                    w_bvty
     c                   parm                    w_blet
     c                   parm                    w_bbet
     c                   parm                    w_ntpr
     c                   parm                    w_ifak
      *
     c                   parm                    w_pogr
     c                   parm                    w_phgr
     c                   parm                    w_pugr
     c                   parm                    w_pldo
     c                   parm                    w_pmod
     c                   parm                    w_pvar
     c                   parm                    w_pkpr
     c                   parm                    w_pvty
     c                   parm                    w_plet
     c                   parm                    w_kfak
     c                   parm                    w_kbel
     c                   parm                    w_sfak
      *
     c                   parm                    w_midl
     c                   parm                    w_inpr
     c                   parm                    w_kopr
     c                   parm                    d1s1pr
     c                   parm                    w_s1im
     c                   parm                    w_s2pr
     c                   parm                    w_s2im
     c                   parm                    w_s3pr
     c                   parm                    w_s3im
     c                   parm                    d1dekn
5.70 c                   parm                    w_prut
6.33 c                   parm                    w_ffak
6.33 c                   parm                    w_inp2
6.3x c                   parm                    w_pgbe
6.3y c                   parm                    w_pgfr
6.3y c                   parm                    w_oppd
      *
6.33 c                   eval(h)   w_inpr = w_inpr * w_ffak
      *
      * Henter navn på prisgivende leverandør
      *
     c                   if        d1ldor <> 0
     c                   eval      jlevl1_ldor = d1ldor
     c     jlevl1_key    chain     jlevl1                             92
     c                   if        *in92 = *off
     c                   eval      d1lnav = jlnavn
     c                   endif
     c                   endif
      *
      * Skriver detaljlinje
      *
     c                   exsr      skriv_dtl
      *
     c     neste_vare    tag
      *
     c                   read      jvarl9
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift av detaljlinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_dtl     begsr
      *
      * Skriv detaljlinje
      *
     c                   write     d1dtl                                30
      *
     c                   exsr      overflow
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for overflow
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     overflow      begsr
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   write     a3hdr
     c                   endif
      *
     c                   endsr
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  * Subrutine for å sjekke om dato er innenfor fra /til dato
6.32  * hvis *loval i begge datoer = ok
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  *
6.32 c     sjekk_pkdato  begsr
6.32  *
6.32 c                   eval      b_pakn = *on
6.32  *
6.32 c                   if        jwfdat = *loval and
6.32 c                             jwtdat = *loval
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   if        jwfdat <= w_dato and
6.32 c                             jwtdat >  w_dato
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   if        jwfdat = *loval  and
6.32 c                             jwtdat >  w_dato
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   if        jwfdat <= w_dato and
6.32 c                             jwtdat =  *loval
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   eval      b_pakn = *off
6.32  *
6.32 c                   endsr
6.32  *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Paramtre
      *
     c     *entry        plist
     c                   parm                    p_parm
      *
      * Key les på vare
      *
     c     jvarl9_key    klist
     c                   kfld                    jvarl9_ldor
     c                   kfld                    jvarl9_ogrp
     c                   kfld                    jvarl9_hgrp
     c                   kfld                    jvarl9_ugrp
      *
      * Key les på vare-detalj
      *
     c     jvdtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jvdtl1_dvar
      *
      * Key for oppslag firma
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på vare-pakning
      *
     c     jvpkl1_key    klist
     c                   kfld                    jvpkl1_vare
6.30 c                   kfld                    jvpkl1_ldor
      *
      * Key for oppslag leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for oppslag bruker
      *
     c     ausrl1_key    klist
     c                   kfld                    ausrl1_user
      *
      *
      * Flytter parameter til datasturuktur
      *
     c                   eval      d_list = p_parm
      *
     c                   eval      w_firm = d_firm
     c                   eval      a4ldor = 999999
      *
     c                   eval      b_fØrste = *on
6.34 c                   time                    w_dato
5.70  *
5.70  * Henter prisgruppe
5.70  *
5.70 c                   call      'VL711R   '
5.70 c                   parm                    w_prgr
      *
      *
     c                   endsr
