     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RC664R                                              *
      * Beskrivelse . . : Danner posteringer fra inkassobyrå                  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign.*
      * --------- ------  ---------------------------------------------- ---- *
      * V5.40     051205  Regnskapsår feil i buntfil                     HFL  *
6.20  * V6.20     180412  Flytter blanke til RTPROS                      NHO  *
9.01  *           060718  Rettelse av null still bilagsteller            bof  *
9.02  *           150190  Rettet rettelse 9.01                           sst  *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frjoupf    ip   e           k disk
     fraa4l1    uf a e           k disk    rename(raa4pfr:raa4l1r)
     frbunl1    if   e           k disk    rename(rbunpfr:rbunl1r)
     frbunpf    o    e             disk
     frtrapf    o    e             disk
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
610  d rbunl1_nivo     s                   like(rsnivo)
610  d rbunl1_memb     s                   like(rsmemb)
610  d w_memb          s                   like(rjmemb)
610  d w_mem1          s                   like(rjmemb)
     d w_bsum          s                   like(rsbsum)
     d w_bunt          s                   like(rsbunt)
     d w_linj          s                   like(rtlinj)
     d w_fØrs          s                   like(rsfØrs)
     d w_sist          s                   like(rssist)
     d w_4sgb          s                   like(ra4sgb)
     d w_4sig          s                   like(ra4sig)
     d w_firm          s                   like(rjfirm)
      *
     d raa4l1_4aar     s                   like(ra4aar)
     d rbunl1_bunt     s                   like(rjbunt)
      *
     irjoupfr
     i                                          rjfirm        l3
     i                                          rjraar        l3
     i                                          rjrper        l3
     i                                          rjbref        l2
     i                                          rjresn        l1
      *
     c     dummy         tag
610   *
610  c                   if        rjmemb > w_memb
610  c                   move      *on           *inlr
610  c                   goto      slutt
610  c                   endif
610   *
610  c                   if        rjmemb <> w_memb
610  c                   move      *off          *inl1
610  c                   move      *off          *inl2
610  c                   move      *off          *inl3
610  c                   goto      slutt
610  c                   endif
      *
      * Brudd på Firma/År/Periode
      *
     c   l3              do                                                              .....
      *                                                                 .
      * Finn ledig bunt-nr.
      *                                                                 .
610  c                   eval      rbunl1_nivo = *blank
610  c                   eval      rbunl1_memb = w_mem1
     c                   eval      w_firm = rjfirm                                           .
     c                   move      99999         rbunl1_bunt                                 .
     c     rbunl1_key    setgt     rbunl1                                                    .
     c                   readp     rbunl1                                 62                 .
      *                                                                 .
     c                   if        *in62 = *on
     c                   eval      w_bunt = 1                                                .
     c                   else
     c                   eval      w_bunt = rsbunt + 1                                       .
     c                   endif
      *                                                                 .
     c                   enddo                                                           .....
      *
      * Brudd referanse
      *
     c   l2              do
      *
      * Hent sist brukte bilagsnummer
      *                                                                 .
     c                   move      rjraar        raa4l1_4aar
     c     raa4l1_key    chain     raa4l1                             91
      *                                                                 .
     c                   if        *in91 = *on
     c                   eval      w_4sig = 0
9.02 c                   eval      w_4sgb = 0
     c                   else
9.02 c**                 eval      w_4sig = ra4sig + 1
9.02 c                   eval      w_4sgb = ra4sgb + 1
     c                   endif
      *                                                                 .
9.02 c**                 eval      ra4sig = w_4sig
9.02 c                   eval      ra4sgb = w_4sgb
      *                                                                 .
     c                   if        *in91 = *off
     c                   update    raa4l1r
     c                   else
     c                   write     raa4l1r
     c                   endif
      *                                                                 .
     c                   enddo
      *
      * Behandler kun poster med beløp utfylt
      *
     c                   if        rjneto = *zero                                        .....
     c                   goto      slutt                                                     .
     c                   endif                                                           .....
      *                                                                 .
     c                   eval      w_linj = w_linj + 1
      *
     c                   eval      w_bsum = w_bsum + rjneto
      *
      * Legg ut postering
      *
     c                   eval      rtreid = 'T'
     c                   eval      rtfirm = w_firm
     c                   eval      rtbunt = w_bunt
     c                   eval      rtlinj = w_linj
     c                   eval      rtstat = *blank
     c                   move      rjbdat        rtbdat
     c                   eval      rtbelØ = rjneto
     c                   eval      rtrper = *zero
9.02 c                   eval      rtbiln = w_4sgb
      *
     c                   if        rtbelØ > *zeros
     c                   eval      rtdavd = rjavde
     c                   eval      rtdkto = rjkont
     c                   eval      rtdsps = rjspes
     c                   eval      rtdmva = rjmvak
     c                   eval      rtcavd = *zero
     c                   eval      rtckto = *zero
     c                   eval      rtcsps = *zero
     c                   eval      rtcmva = *blank
     c                   else
     c                   eval      rtcavd = rjavde
     c                   eval      rtckto = rjkont
     c                   eval      rtcsps = rjspes
     c                   eval      rtcmva = rjmvak
     c                   eval      rtdavd = *zero
     c                   eval      rtdkto = *zero
     c                   eval      rtdsps = *zero
     c                   eval      rtdmva = *blank
     c                   eval      rtbelØ = 0 - rtbelØ
     c                   endif
      *
6.20 c*                  eval      rtpros = *zero                               Prosjekt
6.20 c                   eval      rtpros = *blank                              Prosjekt
     c                   eval      rtrefn = rjrefn                              Referanse
     c                   move      rjbdat        rtfdat                         Forfall
     c                   eval      rtresn = rjresn                              Reskontronr.
     c                   eval      rtbilk = rjbilk                              Bilags-kode
     c                   move      rjtext        rttext                         Bilags-txt
      *
610  c                   eval      rtnivo = *blank
610  c                   eval      rtmemb = w_mem1
610  c                   time                    rtedat
610  c                   time                    rtetim
610  c                   eval      rteusr = l_user
     c                   write     rtrapfr
      *
     c     slutt         tag
      *
     c                   if        *inl3 = *on
     c                   eval      w_fØrs = w_linj
     c                   endif
      *
      * Oppdater buntrecord
      *
     cl3                 eval      w_sist = w_linj
      *
     c***                move      'B'           rsreid
     cl3                 move      w_firm        rsfirm
     cl3                 move      w_bunt        rsbunt
     cl3                 eval      rsstat = *blank
E5.40cl3                 move      rjraar        rsraar
     cl3                 movel     rjrper        rsrper
     cl3                 move      w_fØrs        rsfØrs
     cl3                 move      w_sist        rssist
     cl3                 move      udate         rspdat
     cl3                 move      w_bsum        rsbsum
     cl3                 move      w_bsum        rspsum
     cl3                 move      w_bsum        rspdeb
     cl3                 move      w_bsum        rspkre
      *
610  cl3                 eval      rsnivo = *blank
610  cl3                 eval      rsmemb = w_mem1
610  cl3                 time                    rsedat
610  cl3                 time                    rsetim
610  cl3                 eval      rseusr = l_user
     cl3                 write     rbunpfr                                       SKRIV BUNT
      *
     c     *inzsr        begsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
610  c     *entry        plist
610  c                   parm                    w_memb                         user
610  c                   parm                    w_mem1                         user
      *
     c                   eval      w_linj = 1
     c                   eval      w_fØrs = 2
      *
     c                   eval      w_bunt = 0
      *
      * Key for statuskode 1
      *
     c     key01         klist
     c                   kfld                    w_firm
      *
      * Key for statuskode 4
      *
     c     raa4l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    raa4l1_4aar
      *
      * Key for file RBUNL1 nummerisk
      *
     c     rbunl1_key    klist
610  c                   kfld                    rbunl1_nivo
610  c                   kfld                    rbunl1_memb
     c                   kfld                    w_firm
     c                   kfld                    rbunl1_bunt
      *
     c                   endsr
