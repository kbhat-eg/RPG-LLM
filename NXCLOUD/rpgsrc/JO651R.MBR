     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JO651R
      * Beskrivelse . . : Batch program som oppdaterer vareinformasjon i
      *                   EVR. Oppdateringen skjer på bakgrunn av
      *                   utplukksparametre og matche-registeret.
      *
      *                   Skriver ut kontolliste eller journalliste på
      *                   bakgrunn av utplukksparametre.
      *
      *               NB! Hvis programmet stopper i en oppdaterings-sekvens,
      *                   kan programmet kjøres på nytt ved først å
      *                   endre tilbake varenummer i vareregister EVR !!
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       170197 HKO  Nytt program
      *       170397 HKO  Lagt inn overstyring av prisgivende leverandør
      *       130597 HKO  Henter EAN-nummer fra vare og forpakninger,
      *                   og legger dette i EAN-registeret.
      *                   Forutsetningen er at nummeret er UPC-12
      *                   eller EAN-13
      *       250897 HKO  Lagt inn overstyring for å kunne beholde alle
      *                   også varetekst-genererte søketekster i EVR.
      *                   Endret logikk for oppdatering av søketekster.
      *       010398 HKO  Byttet logikk for oppdatering av EVR-tabeller
      *                   med kall til program FV710R
      *                   Flyttet oppdatering av JVARLU og JMATLU til
      *                   slutten av oppdateringssekvensen.
      *       050798 HKO  Lagt inn overstyring for å kopiere priskode/
      *                   lagerkode fra EVR til LVR.
      *                   Priskode/lagerkode blir ikke kopiert dersom det
      *                   allerede ligger verdier på disse på varen i LVR.
      *                   Tilsvarende test er lagt inn for varetekst.
      *                   Dersom egen varetekst er utfylt på varen i LVR
      *                   blir denne kopiert til EVR
      *       031198 HKO  Fjernet firma fra søketekst-register
      *       090399 HKO  Lagt inn test på om modulstikkord skal tas med
      *       130102 HKO  Fjernet bytting av varenummer i S36-statistikken
      *       080703 HKO  Henter riktig lev.varenr for prisgiver, og
      *                   skriver riktig lev.varenr ut på listen.
      *       120803 HKO  Endret rekkefølge for oppretting av EAN-nummer
      *                   Endrer timestamp på hoved EAN-nummer dersom dette
      *                   finnes fra før
      *       180104 HKO  Oppretter varedetalj dersom den ikke finnes
      *       041204 HKO  Endret parameter ved kall av VV740R og FO740C
      *       140907 BOF  Enhet blir oppdatert på EAN-reg.
5.60  *       181108 BOF  Endringer i forb. oppdatering av ean-nr
6.11  * 6.10  100709 bof  Utivdet eannr til 14 og brukt std. oppslag
6.12  *       250909 bof  Utvidet med pakn.info pr. lev +++
6.30  * 6.30  260114 bof  Omskriving i forb. endring pakning VA
6.30  *       270114 bof  Fjernet JVPPPF
6.32  *       140314 bof  Lagt inn test på fra /til dato på pakning
6.33  * 6.30  270314 bof  feilretting + test om varepris VA er utgått
6.34  * 6.30  110714 bof  endret nøkkel i varepris - dato
6.35  * 6.30  020816 bof  Feilretting vedr. test på til prisdato.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjmatl6    if   e           k disk    rename(jmatpfr:jmatl6r)
     fjmatlu    uf   e           k disk    rename(jmatpfr:jmatlur)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvarlu    uf   e           k disk    rename(vvarpfr:vvarlur)
     fjvdtlu    uf a e           k disk    rename(jvdtpfr:jvdtlur)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjvarlu    uf   e           k disk    rename(jvarpfr:jvarlur)
     fvsoklu    uf a e           k disk    rename(vsokpfr:vsoklur)
     fvsokl1    if   e           k disk    rename(vsokpfr:vsokl1r)
     fjsokl1    if   e           k disk    rename(jsokpfr:jsokl1r)
     fveanl1    if   e           k disk    rename(veanpfr:veanl1r)
     fveanlu    uf a e           k disk    rename(veanpfr:veanlur)
6.12 fvveplu    uf   e           k disk    rename(vveppfr:vveplur)
     fjvprl1    if   e           k disk    rename(jvprpfr:jvprl1r)
6.12 fjvpkl2    if   e           k disk    rename(jvpkpfr:jvpkl2r)
6.12 fvvepl2    if   e           k disk    rename(vveppfr:vvepl2r)
      *
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
      *
     fjo651p    o    e             printer
      *
      * Definering av Local data area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
      *
      * Datastruktur for parameterliste -inn
      *
     d                 ds
     d d_list                        32
     d  d_firm                        3  0 overlay(d_list)
     d  d_ldo2                        6  0 overlay(d_list:4)
     d  d_ogrp                        2  0 overlay(d_list:10)
     d  d_hgrp                        2  0 overlay(d_list:12)
     d  d_ugrp                        3  0 overlay(d_list:14)
     d  d_ldo3                        6  0 overlay(d_list:17)
     d  d_behp                        1  0 overlay(d_list:23)
     d  d_beht                        1  0 overlay(d_list:24)
     d  d_behg                        1  0 overlay(d_list:25)
     d  d_oppd                        1  0 overlay(d_list:26)
      *
      * Definering av variable i nøkler
      *
     d jmatl6_vkod     s                   like(jovkod)
     d jmatl6_ldo2     s                   like(joldo2)
     d jmatlu_var1     s                   like(jovar1)
     d vvarl1_vare     s                   like(vvvare)
     d vvarlu_vare     s                   like(vvvare)
     d jvdtlu_dvar     s                   like(jvdvar)
     d jvarl1_vare     s                   like(jvvare)
     d jvarlu_vare     s                   like(jvvare)
     d vsoklu_vare     s                   like(vsvare)
     d vsokl1_vare     s                   like(vsvare)
     d vsokl1_stek     s                   like(vsstek)
     d jsokl1_vare     s                   like(jsvare)
     d veanl1_eann     s                   like(vxeann)
     d veanlu_eann     s                   like(vxeann)
     d jvprl1_vare     s                   like(jxvare)
     d jvprl1_ldor     s                   like(jxldor)
6.12 d jvpkl2_vare     s                   like(jwvare)
     d jlevl1_ldor     s                   like(jlldor)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d rlevl1_levr     s                   like(rllevr)
6.12 d vvepl2_pgti     s                   like(vepgti)
6.12 d vveplu_pvar     s                   like(vepvar)
6.12 d vveplu_penh     s                   like(vepenh)
6.12 d vveplu_pldo     s                   like(vepldo)
      *
      * Definering av variable
      *
     d p_list          s             32
     d p_behp          s              1  0
     d p_beht          s              1  0
     d p_behg          s              1  0
     d p_oppd          s              1  0
      *
     d w_timestamp     s             12  0
      *
     d p_firm          s                   like(jofirm)
     d p_ldo2          s                   like(joldo2)
     d p_ogrp          s                   like(jvogrp)
     d p_hgrp          s                   like(jvhgrp)
     d p_ugrp          s                   like(jvugrp)
     d p_ldo3          s                   like(joldo2)
      *
     d b_inlr          s              1    inz(*off)
6.32 d b_pakn          s              1    inz(*off)
6.34 d b_pris          s              1    inz(*off)
      *
     d w_firm          s                   like(jofirm)
     d w_enum          s                   like(jlenum)
     d w_kmod          s                   like(jlkmod)
     d w_lvar          s                   like(vvlvar)
6.32 d w_dato          s                   like(jwodat)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Utskrift av header
      *
     c                   exsr      hode
      *
      * Behandling av kontrolliste/journalliste, og utskrift av deltajlinjer
      *
     c                   exsr      behandling
      *
      * Avslutt
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering og utskrift av hode-opplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode          begsr
      *
      * Legger inn arb.stasjon og bruker i faste felter
      *
     c                   eval      a1firm = w_firm
     c                   eval      a1user = l_user
     c                   eval      a1wsid = l_wsid
      *
     c                   time                    w_timestamp
     c                   move      w_timestamp   a1date
     c                   movel     w_timestamp   a1time
      *
     c                   if        p_oppd = 1
     c                   eval      a1otyp = 'Kontrolliste'
     c                   else
     c                   eval      a1otyp = 'Journalliste'
     c                   endif
      *
      * Henter firmaopplysninger
      *
     c     afirl1_key    chain     afirl1r                            90
     c                   eval      a1fnav = aafnav
      *
      * Henter parametre med beskrivelse
      *
     c                   eval      a2ldo2 = p_ldo2
     c                   eval      a2ogrp = p_ogrp
     c                   eval      a2hgrp = p_hgrp
     c                   eval      a2ugrp = p_ugrp
     c                   eval      a2ldo3 = p_ldo3
     c                   eval      a2behp = p_behp
     c                   eval      a2beht = p_beht
     c                   eval      a2behg = p_behg
      *
     c                   eval      jlevl1_ldor = p_ldo2
     c     jlevl1_key    chain     jlevl1                             90
     c                   if        *in90 = *off
     c                   eval      a2lna2 = jlnavn
     c                   eval      w_enum = jlenum
     c                   eval      w_kmod = jlkmod
     c                   endif
      *
     c                   if        p_ogrp <> 0
     c                   eval      vogrl1_oogr = p_ogrp
     c     vogrl1_key    chain     vogrl1                             90
     c                   if        *in90 = *off
     c                   eval      a2utxt = vgotxt
     c                   endif
     c                   endif
      *
     c                   if        p_hgrp <> 0
     c                   eval      vhgrl1_hogr = p_ogrp
     c                   eval      vhgrl1_hhgr = p_hgrp
     c     vhgrl1_key    chain     vhgrl1                             90
     c                   if        *in90 = *off
     c                   eval      a2utxt = vghtxt
     c                   endif
     c                   endif
      *
     c                   if        p_ugrp <> 0
     c                   eval      vugrl1_uogr = p_ogrp
     c                   eval      vugrl1_uhgr = p_hgrp
     c                   eval      vugrl1_uugr = p_ugrp
     c     vugrl1_key    chain     vugrl1                             90
     c                   if        *in90 = *off
     c                   eval      a2utxt = vgutxt
     c                   endif
     c                   endif
      *
     c                   if        p_ldo3 <> 0
     c                   eval      jlevl1_ldor = p_ldo3
     c     jlevl1_key    chain     jlevl1                             90
     c                   if        *in90 = *off
     c                   eval      a2lna3 = jlnavn
     c                   eval      w_enum = jlenum
     c                   endif
     c                   endif
      *
      * Skriver heading
      *
     c                   write     a1hdr
     c                   write     a2hdr
     c                   write     a3hdr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av poster som mather utplukkskriteriene
      * Behandler utskrift av detaljlinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
     c     jmatl6_key    reade     jmatl6                                 90
     c                   dow       *in90 = *off
      *
      * Sjekker om vare skal være med på utplukket
      *
     c                   if        jokode = 0
     c                   goto      les_neste
     c                   endif
      *
     c                   eval      vvarl1_vare = jovar2
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   goto      les_neste
     c                   endif
      *
     c                   eval      vvarl1_vare = jovar1
     c     vvarl1_key    chain     vvarl1                             91
     c                   if        *in91 = *on
     c                   goto      les_neste
     c                   endif
      *
     c                   eval      jvarl1_vare = jovar2
     c     jvarl1_key    chain     jvarl1                             91
     c                   if        *in91 = *on
     c                   goto      les_neste
     c                   endif
      *
     c                   if        p_ogrp <> 0 and
     c                             p_ogrp <> jvogrp
     c                   goto      les_neste
     c                   endif
      *
     c                   if        p_hgrp <> 0 and
     c                             p_hgrp <> jvhgrp
     c                   goto      les_neste
     c                   endif
      *
     c                   if        p_ugrp <> 0 and
     c                             p_ugrp <> jvugrp
     c                   goto      les_neste
     c                   endif
      *
     c                   exsr      finn_lvar
      *
      * Skriver detaljlinje på kvittering/journalliste.
      *
     c                   exsr      detalj
      *
      * Oppdaterer eget- og lokalt vareregister dersom journalliste
      *
     c                   if        p_oppd = 2
     c                   exsr      oppdater
     c                   endif
      *
      * Leser neste post og tester utvalg
      *
     c     les_neste     tag
      *
     c     jmatl6_key    reade     jmatl6                                 90
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av eget- og lokalt vareregister dersom
      * journalliste
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdater      begsr
      *
      * Bytter varenummer og oppdaterer vare i EVR
      *
     c                   clear                   vvarlur
      *
     c                   eval      vvarlu_vare = jovar1
     c     vvarlu_key    chain     vvarlur                            91
     c                   if        *in91 = *on
     c                   goto      end_oppd
     c                   endif
      *
     c                   eval      vvvare = jvvare
     c                   eval      vvnobb = jvnobb
     c                   eval      vvnmod = jvmodn
     c                   eval      vvnlev = jvldor
     c                   eval      vvldor = w_enum
      *
     c                   exsr      finn_lvar
      *
     c                   eval      vvlvar = w_lvar
      *
     c                   if        p_beht = 0
     c                   if        jvete1 = *blank
     c                   eval      vvtek1 = jvtek1
     c                   eval      vvtek2 = jvtek2
     c                   else
     c                   eval      vvtek1 = jvete1
     c                   eval      vvtek2 = jvete2
     c                   endif
     c                   endif
      *
     c                   if        p_behg = 0
     c                   if        jveogr = 0
     c                   eval      vvogrp = jvogrp
     c                   eval      vvhgrp = jvhgrp
     c                   eval      vvugrp = jvugrp
     c                   else
     c                   eval      vvogrp = jveogr
     c                   eval      vvhgrp = jvehgr
     c                   eval      vvugrp = jveugr
     c                   endif
     c                   endif
      *
     c                   update    vvarlur
      *
      * Kaller program for bytting av varenummer i EVR
      *
     c                   call      'VV740R'
     c                   parm                    w_firm
     c                   parm                    jovar1
     c                   parm                    jvvare
     c                   parm                    b_inlr
      *
      * Kaller program for bytting av varenummer i lager/innkjøp
      *
     c                   call      'FO740C'
     c                   parm                    w_firm
     c                   parm                    jovar1
     c                   parm                    jvvare
     c                   parm                    b_inlr
      *
      * Kopierer søketekster fra LVR til EVR, med unntak av søketekster
      * generert av produsentnavn, leverandørnavn og varetekst
      * Søketeksten blir ikke kopiert over dersom den allerede finnes i
      * EVR søketekstregister
      *
     c                   eval      jsokl1_vare = jvvare
     c     jsokl1_key    setll     jsokl1
     c     jsokl1_key    reade     jsokl1
     c                   dow       not %eof
     c                   if        jsokod = 'L' or
     c                             jsokod = 'M' and
     c                             w_kmod = 1
     c                   eval      vsokl1_vare = jsvare
     c                   eval      vsokl1_stek = jsstek
     c     vsokl1_key    chain     vsokl1
     c                   if        not %found
     c                   eval      vsfirm = w_firm
     c                   eval      vsvare = jsvare
     c                   eval      vsstek = jsstek
     c                   write     vsoklur
     c                   endif
     c                   endif
     c     jsokl1_key    reade     jsokl1
     c                   enddo
      *
      * Kaller program for oppdatering av søketekster i EVR
      *
     c                   call      'VV750R'
     c                   parm                    w_firm
     c                   parm                    jvvare
      *
      * Dersom vare i LVR har EAN-nummer på enhet eller vare som ikke
      * ligger på varen i EVR, opprettes EAN-nummer i EAN-registeret.
      * Det forutsettes av nummeret er et UPC-12 eller EAN-13 nummer
6.12  *
6.12  * finner pakninger, før man slår opp og finner info pr. lev.
6.12 c                   eval      jvpkl2_vare = jvvare
6.12 c     jvpkl2_key    setll     jvpkl2
6.12 c     jvpkl2_key    reade     jvpkl2
6.12 c                   dow       not %eof
6.32  *
6.32  * sjekker fra / tildato
6.32 c                   exsr      sjekk_pkdato
6.32 c                   if        b_pakn = *on
6.12  *
6.30 c                   eval      jlevl1_ldor = jwldor
6.12 c     jlevl1_key    chain     jlevl1
6.12 c                   if        %found(jlevl1) and
6.12 c                             jlenum <> 0
      *
6.30 c                   if        jwgtin >= 100000000000 and
6.30 c                             jwgtin <= 99999999999999
6.12 c                   eval      vveplu_pvar = jwvare
6.12 c                   eval      vveplu_penh = jwenhe
6.12 c                   eval      vveplu_pldo = jlenum
6.12 c     vveplu_key    chain     vveplu                             92
6.12 c                   if        %found(vveplu)
6.30 c                   eval      vepgti = jwgtin
6.11 c                   eval      vepeus = l_user
6.11 c                   time                    vepeti
6.11 c                   time                    vepeda
6.12 c                   update    vveplur
     c                   exsr      rydd_ean
5.60 c                   endif
6.12 c                   endif
6.12 c                   endif
6.32 c                   endif
      *
     c     jvpkl2_key    reade     jvpkl2
     c                   enddo
      *
     c                   if        jvean1 >= 100000000000 and
6.11 c                             jvean1 <= 99999999999999
6.11  *
6.11  *sjekker om ean-nr allerede ligger på vare cenhet
6.12 c                   eval      vvepl2_pgti = jvean1
6.12 c     vvepl2_key    chain     vvepl2
6.12 c                   if        not %found(vvepl2)
6.11  *
5.60 c                   clear                   veanlur
     c                   eval      veanl1_eann = jvean1
     c     veanl1_key    chain     veanl1
     c                   if        not %found
     c                   eval      vxfirm = w_firm
     c                   eval      vxvare = jvvare
     c                   eval      vxeann = jvean1
5.60 c                   eval      vxenhe = *blank
     c                   eval      vxeusr = l_user
     c                   time                    vxtime
     c                   time                    vxetim
     c                   time                    vxedat
     c                   write     veanlur
     c                   else
5.60 c                   if        vxvare <> jvvare
     c                   eval      veanlu_eann = jvean1
     c     veanlu_key    chain     veanlur
5.60 c                   if        %found(veanlu)
5.60 c                   eval      vxvare = jvvare
5.60 c                   eval      vxenhe = *blank
     c                   time                    vxtime
     c                   update    veanlur
5.60 c                   endif
5.60 c                   endif
     c                   endif
6.11 c                   endif
     c                   endif
      *
      * Oppdaterer LVR-vareregister med varetekst og varegruppe fra EVR
      *
     c                   if        p_beht = 1 and jvete1 = *blank or
     c                             p_behg = 1 and jveogr = 0
     c                   eval      jvarlu_vare = jvvare
     c     jvarlu_key    chain     jvarlur                            91
     c                   if        *in91 = *off
      *
     c                   if        p_beht = 1
     c                   eval      jvete1 = vvtek1
     c                   eval      jvete2 = vvtek2
     c                   endif
      *
     c                   if        p_behg = 1
     c                   eval      jveogr = vvogrp
     c                   eval      jvehgr = vvhgrp
     c                   eval      jveugr = vvugrp
     c                   endif
      *
     c                   update    jvarlur
      *
     c                   endif
     c                   endif
      *
      * Oppdaterer vare-detalj med priskode/varetype fra EVR
      *
     c                   if        p_behp = 1
      *
     c                   clear                   jvdtlur
      *
     c                   eval      jvdtlu_dvar = jvvare
     c     jvdtlu_key    chain     jvdtlur
      *
     c                   eval      jvdpko = vvkpri
     c                   if        vvvtyp = 'S'
     c                   eval      jvdsko = 1
     c                   endif
      *
     c                   if        %found
     c                   update    jvdtlur
     c                   else
     c                   eval      jvdfir = w_firm
     c                   eval      jvdvar = jvvare
     c                   eval      jvdhko = 1
     c                   write     jvdtlur
     c                   endif
      *
     c                   endif
      *
      * Oppdaterer matche-registeret med 'overført EVR'
      *
     c                   eval      jmatlu_var1 = jovar1
     c     jmatlu_key    chain     jmatlur                            91
     c                   if        *in91 = *off
     c                   eval      jovkod = 1
     c                   update    jmatlur
     c                   endif
      *
     c     end_oppd      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne leverandørs varenummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_lvar     begsr
      *
     c                   eval      w_lvar = jvlvar
      *
      * Behandling dersom prisgivende leverandør er oppgitt
      *
     c                   if        p_ldo3 <> 0 and
     c                             p_ldo3 <> p_ldo2
     c                   eval      jvprl1_vare = jvvare
     c                   eval      jvprl1_ldor = p_ldo3
6.34 c                   exsr      hent_pris
6.34 c                   if        b_pris = *off or
     c                             jxlvar <> *blank
6.30 c                   if        (jxtdat =  *loval) or
6.30 c                             (jxtdat <> *loval and
6.35 c                             jxtdat >= w_dato)
     c                   eval      w_lvar = jxlvar
6.30 c                   endif
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine utskrift av detaljlinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     detalj        begsr
      *
     c                   eval      d1ogrp = vvogrp
     c                   eval      d1hgrp = vvhgrp
     c                   eval      d1ugrp = vvugrp
     c                   eval      d1ogr2 = jvogrp
     c                   eval      d1hgr2 = jvhgrp
     c                   eval      d1ugr2 = jvugrp
     c                   eval      d1var1 = vvvare
     c                   eval      d1var2 = jvvare
     c                   eval      d1tek1 = vvtek1
     c                   eval      d1lva1 = vvlvar
     c                   eval      d1lva2 = w_lvar
     c                   eval      d1kode = jokode
      *
     c                   if        jvete1 = *blank
     c                   eval      d1te21 = jvtek1
     c                   eval      d1te22 = jvtek2
     c                   else
     c                   eval      d1te21 = jvete1
     c                   eval      d1te22 = jvete2
     c                   endif
      *
     c                   eval      d1lnav = *blank
     c                   if        vvldor <> 0
     c                   eval      rlevl1_levr = vvldor
     c     rlevl1_key    chain     rlevl1                             91
     c                   if        *in91 = *off
     c                   eval      d1lnav = rlnavn
     c                   endif
     c                   endif
      *
      * Skriv detaljlinje
      *
     c                   write     d1dtl                                30
      *
      * Er det overflow ?
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   write     a3hdr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å rydde ean-nr på veanpf som ligger på vvenpf
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     rydd_ean      begsr
6.11  *
6.12 c                   eval      veanlu_eann = vepgti
     c     veanlu_key    chain     veanlu
     c                   if        %found(veanlu)
     c                   delete    veanlur
     c                   endif
      *
     c                   endsr
      *
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  * Subrutine for å sjekke om dato er innenfor fra /til dato
6.32  * hvis *loval i begge datoer = ok
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  *
6.32 c     sjekk_pkdato  begsr
6.32  *
6.32 c                   eval      b_pakn = *on
6.32  *
6.32 c                   if        jwfdat = *loval and
6.32 c                             jwtdat = *loval
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   if        jwfdat <= w_dato and
6.32 c                             jwtdat >  w_dato
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   if        jwfdat = *loval  and
6.32 c                             jwtdat >  w_dato
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   if        jwfdat <= w_dato and
6.32 c                             jwtdat =  *loval
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   eval      b_pakn = *off
6.32  *
6.32 c                   endsr
6.32  *
6.34  *---------------------------------------------------------------------------
6.34  * Henter siste gyldige pris
6.34  *---------------------------------------------------------------------------
6.34  *
6.34 c     hent_pris     begsr
6.34  *
6.34 c                   eval      b_pris = *off
6.34 c     jvprl1_key    setll     jvprl1
6.34 c     jvprl1_key    reade     jvprl1
6.34 c                   dow       not  %eof(jvprl1)
6.34 c                   if        w_dato >= jxgdat
6.34 c                   leavesr
6.24 c                   endif
6.34 c     jvprl1_key    reade     jvprl1
6.34 c                   enddo
6.34 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_list
      *
      * Key for les i matcheregisteret
      *
     c     jmatl6_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jmatl6_vkod
     c                   kfld                    jmatl6_ldo2
      *
      * Key for oppdatering av matcheregisteret
      *
     c     jmatlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jmatlu_var1
      *
      * Key for oppslag på EVR-vareregister
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppdatering av EVR-vareregister
      *
     c     vvarlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlu_vare
      *
      * Key for oppdatering av vare-detalj
      *
     c     jvdtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jvdtlu_dvar
      *
      * Key for oppslag på vare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for oppdatering av vare
      *
     c     jvarlu_key    klist
     c                   kfld                    jvarlu_vare
      *
      * Key for oppdatering av EVR søketekst-register
      *
     c     vsoklu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vsoklu_vare
      *
      * Key for oppslag på EVR søketekst-register
      *
     c     vsokl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vsokl1_vare
     c                   kfld                    vsokl1_stek
      *
      * Key for les på søketekst
      *
     c     jsokl1_key    klist
     c                   kfld                    jsokl1_vare
      *
      * Key for oppslag på EAN-register
      *
     c     veanl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    veanl1_eann
      *
      * Key for oppdatering av EAN-register
      *
     c     veanlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    veanlu_eann
      *
      * Key for oppslag på vare-pris
      *
     c     jvprl1_key    klist
     c                   kfld                    jvprl1_vare
     c                   kfld                    jvprl1_ldor
      *
      * Key for oppslag på vare-pakning
      *
6.12 c     jvpkl2_key    klist
6.12 c                   kfld                    jvpkl2_vare
      *
      * Key for oppslag på firma
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag på EVR-leverandørregister
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
6.11  *
6.11  * Key for oppslag på vare enhet
6.11  *
6.12 c     vvepl2_key    klist
6.12 c                   kfld                    w_firm
6.12 c                   kfld                    vvepl2_pgti
6.12  *
6.12  * Key for skrive  på vare enhet
6.12  *
6.12 c     vveplu_key    klist
6.12 c                   kfld                    w_firm
6.12 c                   kfld                    vveplu_pvar
6.12 c                   kfld                    vveplu_penh
6.12 c                   kfld                    vveplu_pldo
      *
      *
      * Splitter parameterliste i enkelt-parametre
      *
     c                   eval      d_list = p_list
     c                   eval      p_firm = d_firm
     c                   eval      p_ldo2 = d_ldo2
     c                   eval      p_ogrp = d_ogrp
     c                   eval      p_hgrp = d_hgrp
     c                   eval      p_ugrp = d_ugrp
     c                   eval      p_ldo3 = d_ldo3
     c                   eval      p_behp = d_behp
     c                   eval      p_beht = d_beht
     c                   eval      p_behg = d_behg
     c                   eval      p_oppd = d_oppd
      *
      * Setter startverdi for les i matche-registeret
      *
     c                   eval      w_firm = p_firm
     c                   time                    w_dato
      *
     c                   eval      jmatl6_vkod = 0
     c                   eval      jmatl6_ldo2 = p_ldo2
     c     jmatl6_key    setll     jmatl6
      *
     c                   endsr
