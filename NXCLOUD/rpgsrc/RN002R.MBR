     h option(*nodebugio) datedit(*dmy)
     h decedit('0.')
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASNAVIOKO
      * Program . . . . : RN002R
      * Beskrivelse . . : Leser linjer fra BOVF til NAV transaksjonsfil
      *
      * Spesialversjon. :
      *
      * Tilpasset for . :
      *
      * ----- ------ ---- -------------------------------------------------
      *       300714 ask  Nytt program
      *       230217 bkv  Rekompilert i 630 fordi IBUTST hadde 29 kolonner når A
6.31  *       270217 ask  Lagt inn henting av kid fra historisk fakturaregister
6.32  *       270217 ask  Legger ut kortkid alfa pga. endring i NAV
6.33  *       010317 ask  Lagt inn konvertering av bilagskoder - falt ut fra 6.2
6.34  *       270617 ask  Endret rutiner for konvertering av bilagskoder øreavru
7.00  *       150221 ask  Lagt inn kode for transtype FAKT/KRED - her hardkodet
7.01  *       250221 ask  Lagt inn kode for betalingsbetingelse - her hardkodet
7.02  *       240321 ask  Legger ut bongnummer på IBUTST
7.03  *       220421 ask  Henter kid fra BREFPF om den finnes
8.01  * 800   030521 ask  Henter kid fra historisk fakturaregister med kreditref
8.03  * 800   280521 ask  Oppdaterer sql med firma der det mangler, fikset funks
8.04  *       290921 ask  Dropper overføring av poster med bilagssum = 0
8.05  *       101121 ask  Lagt inn nytt bilagsnummer pr. bong for SAP
8.06  *       071221 ask  Sjekker om depositumsbetaling, flagger det
8.07  *       070422 ask  Legger kid og ordrereferanse ut bare på reskotropost
8.08  *       190522 sst  Kidd bare på Santanderpost dersom Santander benyttes
8.09  *       170622 sst  Ikke kort kidd dersom lang kidd benyttes. Styres av rutine 'FFAKT'
8.10  *       140922 sst  Styre 8.09 med switch: IKKE_KORT_KIDD_NÅR_LANG'
8.11  *       160623 mst  Fjerner salgsordrereferanse til NAV/BC
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     fbovfl1    if   e           k disk    rename(bovfpfr:bovfl1r)
     fibutst    o    e           k disk    rename(ibutst:ibutstr)
6.31 fsohel1    if   e           k disk    rename(sohepfr:sohel1r)
6.33 fikonir    if   e           k disk    rename(ikonst:ikonstr)
6.34 flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
8.08 frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
8.08 fraa1l1    if   e           k disk    rename(raa1pfr:raa1l1r)
8.09 faforl1    if   e           k disk    rename(aforpfr:aforl1r)
      *
      * Definering av konstanter
      *
     d lo              c                   'abcdefghijklmnopqrstuvwxyzæøåÆØÅ'
     d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÆØÅ'
      *
      * Definering av Local data area
      *
     d                uds
8.08 d l_filg                931    933
     d l_firm                944    946  0
     d l_user                911    920
      *
      * Definering av variabler i nøkler
      *
     d bovfl1_peri     s                   like(bfperi)
6.31 d sohel1_aarr     s                   like(soaarr)
6.31 d sohel1_binr     s                   like(sobinr)
6.33 d ikonir_bfra     s                   like(ikbfra)
8.08 d rkunl1_kund     s                   like(rkkund)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
6.33 d w_bilk          s              2  0
6.31 d w_kidr          s              9  0
6.31 d w_aarr          s              4  0
6.31 d w_kidd          s                   like(sokidd)
6.32 d w_kida          s              9
6.34 d w_obil          s                   like(laabil)
7.02 d w_pos           s              2  0
8.05 d w_kode          s              1
8.05 d w_type          s              2
8.05 d w_fell          s              6
8.05 d w_fast          s             10
8.05 d w_biln          s              8  0
      *
8.05 d w_btex          s             20    inz(' ')
8.05 d w_sumd          s             10  2 inz(0)
8.05 d w_sumk          s             10  2 inz(0)
8.05 d w_antp          s              5  0 inz(0)
8.05 d w_timb          s               z
      *
8.05 d w_dumtim        s               z
8.05 d w_dumsum        s             11  2
8.05 d w_dumusr        s             10
8.05 d w_dumerr        s              2
8.05 d w_dumant        s              5  0
8.06 d w_bong          s             12
8.08 d w_sant          s              1
8.08 d w_navn          s                   like(rknavn)
8.09 d w_langk         s              1
8.10  *
8.10 d u_ikknl         s              1    inz(*off)
8.03  *
8.03   dcl-s co402_verdi1  char(1);
8.03   dcl-s co402_verdi2  char(2048);
8.03   dcl-pr co402r extpgm('CO402R');
8.03     p_filgrp            char(3)     const;
8.03     p_firm              packed(3:0) const;
8.03     p_lager             packed(2:0) const;
8.03     p_nokkel            char(50)    const;
8.03     p_verdi1            char(1);
8.03     p_verdi2            char(2048);
8.03   end-pr;
      *
8.02 d b_posb          s              1    inz(*off)
8.02 d b_siste         s              1    inz(*off)
      *
7.03 c/Exec SQL
7.03 c+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR,
7.03 c+             commit=*none
7.03 c/End-Exec
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram - leser poster fra BOVFL1 og skriver til IBUTST
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      bovfl1_peri = 0
     c     bovfl1_key    setll     bovfl1
     c                   read      bovfl1
     c                   dow       not %eof
8.04  *
8.04  * Sjekker om "0" - bilag
8.04  *
8.04 c                   if        bfbelp = 0
8.04 c                   goto      les_neste
8.04 c                   endif
8.05  *
8.05  * Sjekker om bilagsnummer skal byttes ut
8.05  *
8.05 c                   if        b_posb = *on
8.05 c                   if        bftext <> w_btex
8.05 c                   exsr      ny_bong
8.05 c                   endif
8.05  *
8.05 c                   eval      bfbiln = w_biln
8.05 c                   eval      w_antp = w_antp + 1
8.05 c                   if        bfdkto > 0
8.05 c                   eval      w_sumd = w_sumd + bfbelp
8.05 c                   endif
8.05  *
8.05 c                   if        bfckto > 0
8.05 c                   eval      w_sumk = w_sumk + bfbelp
8.05 c                   endif
8.05 c                   eval      w_btex = bftext
8.05 c                   endif
6.33  *
6.33  * Konverterer bilagskoder for øreavrunding
6.33  *
6.34 c                   if        bfbilk <> w_obil
6.33 c                   eval      w_bilk = bfbilk
6.33 c                   else
6.33 c                   eval      ikonir_bfra = w_bilk
6.33 c     ikonir_key    chain     ikonir
6.33 c                   if        %found
6.33 c                   eval      bfbilk = ikbtil
6.33 c                   endif
6.33 c                   endif
6.31  *
6.31 c                   exsr      finn_kid
6.32 c                   eval      w_kida = %EditC(w_kidr :'X')
6.11  *
     c                   eval      ibfirm = bffirm
     c                   eval      ibbiln = bfbiln
     c                   eval      ibbdat = bfbdat
     c                   eval      ibfdat = bffdat
     c                   eval      ibbilk = bfbilk
     c                   eval      ibtext = bftext
     c                   eval      ibdavd = bfdavd
     c                   eval      ibdkto = bfdkto
     c                   eval      ibdsps = bfdsps
     c                   eval      ibdmom = bfdmom
     c                   eval      ibcavd = bfcavd
     c                   eval      ibckto = bfckto
     c                   eval      ibcsps = bfcsps
     c                   eval      ibcmom = bfcmom
     c                   if        bfckto > 0
     c                   eval      ibbelp = bfbelp * -1
     c                   else
     c                   eval      ibbelp = bfbelp
     c                   endif
     c                   eval      ibmvag = bfmvag
     c                   eval      ibkund = bfkund
     c                   eval      ibvalk = bfvalk
     c                   eval      ibvalb = bfvalb
     c                   eval      ibproj = bfproj
     c                   eval      ibakti = bfakti
      *
8.07 c                   if        bfdkto = bfkund or
8.07 c                             bfckto = bfkund
8.07 c                   eval      ibkidi = w_kidd
8.07 c                   eval      ibkidr = w_kida
8.09 c                   if        w_langk = 'J'
8.10 c                             and u_ikknl = *on
8.09 c                   eval      ibkidr = *blank
8.09 c                   endif
      *
8.07 c                   eval      ibkref = bfkref
8.07 c                   else
8.07 c                   eval      ibkidi = '  '
8.07 c                   eval      ibkidr = '  '
8.07 c                   eval      ibkref = 0
8.07 c                   endif
8.11 c                   eval      ibkref = 0
8.08  *
8.08  *    Ved Santander legges kid kun ut på Santanderpost.
8.08  *
8.08 c                   if        w_sant = 'J'
8.08 c                   eval      rkunl1_kund = *zero
8.08 c                   if        bfdkto > ra1hkt
8.08 c                             and bfdkto <= RA1HRS
8.08 c                   eval      rkunl1_kund = bfdkto
8.08 c                   endif
8.08 c                   if        bfckto > ra1hkt
8.08 c                             and bfckto <= ra1hrs
8.08 c                   eval      rkunl1_kund = bfckto
8.08 c                   endif
8.08 c                   if        rkunl1_kund > 0
8.08 c     rkunl1_key    chain     rkunl1
8.08 c     lo:up         xlate     rknavn        w_navn
8.08 c                   if        %found(rkunl1)
8.08 c                             and (%scan('SANTANDER':w_navn)) > 0
8.08 c                   else
8.08 c                   eval      ibkidi = '  '
8.08 c                   eval      ibkidr = '  '
8.08 c**                 eval      ibkref = 0
8.08 c                   endif
8.08 c                   endif
8.08 c                   endif
      *
7.00 c                   eval      ibtype = 'FAKT'
7.01 c                   eval      ibbetb = 0
     c                   eval      ibodat = w_date
     c                   eval      ibotim = w_time
     c                   eval      ibousr = l_user
      *
7.02 c                   eval      w_pos = %scan('/' : bftext)
8.01 c                   if        w_pos > 0
7.02 c                   eval      ibbong = %subst(ibtext:1:w_pos-1)
8.01 c                   else
8.01 c                   eval      ibbong = *blank
8.01 c                   endif
8.06  *
8.06  * Sjekk om depositum
8.06  *
8.06 c                   if        bfkref <> 0
8.06 c                   exsr      sjk_depo
8.06 c                   else
8.06 c                   eval      ibdepo = 'N'
8.06 c                   endif
7.02  *
     c                   write     ibutstr
      *
8.04 c     les_neste     tag
     c                   read      bovfl1
     c                   enddo
8.05  *
8.05  * Sjekker om bilagsnummer skal byttes ut - siste runde
8.05  *
8.05 c                   if        b_posb = *on
8.05 c                   eval      b_siste = *on
8.05 c                   exsr      ny_bong
8.05 c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      *inlr = *on
     c                   return
6.31  *
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  * Subrutine for å hente kid-informasjon
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  *
6.31 c     finn_kid      begsr
6.31  *
6.31 c                   eval      w_kidd = *blank
6.31 c                   eval      w_kidr = 0
7.03  *
7.03  * Fra BREFPF
7.03  *
7.03 c/Exec SQL
7.03 c+  select bxkidr
7.03 c+    into :w_kidd
7.03 c+  from brefpf
8.03 c+    where bxfirm = :w_firm and bxbiln = :bfbiln
7.03 c+
7.03 c/End-Exec
7.03 c                   if        w_kidd <> *blank
7.03 c                   goto      end_kid
7.03 c                   endif
7.03  *
8.01  * Fra SOHEPF med bilagsnummer
7.03  *
6.31 c                   eval      w_aarr = %subdt(bfbdat:*years)
6.31 c                   eval      sohel1_aarr = w_aarr
6.31 c                   eval      sohel1_binr = bfbiln
6.31  *
6.31 c     sohel1_key    setll     sohel1
6.31 c     sohel1_key    reade     sohel1
6.31 c                   dow       not %eof
6.31 c                   if        sokidd <> *blanks or
6.31 c                             sokidr <> 0
6.31 c                   eval      w_kidd = sokidd
6.31 c                   eval      w_kidr = sokidr
6.31 c                   goto      end_kid
6.31 c                   endif
6.31 c     sohel1_key    reade     sohel1
6.31 c                   enddo
6.31  *
8.01 c                   if        w_kidd <> *blank
8.01 c                   goto      end_kid
8.01 c                   endif
8.01  *
8.01  * Fra SOHEPF med kreditreferanse
8.01  *
8.01 c                   eval      w_aarr = %subdt(bfbdat:*years)
8.01 c                   eval      sohel1_aarr = w_aarr
8.01 c                   eval      sohel1_binr = bfkref
8.01  *
8.01 c     sohel1_key    setll     sohel1
8.01 c     sohel1_key    reade     sohel1
8.01 c                   dow       not %eof
8.01 c                   if        sokidd <> *blanks or
8.01 c                             sokidr <> 0
8.01 c                   eval      w_kidd = sokidd
8.01 c                   eval      w_kidr = sokidr
8.01 c                   goto      end_kid
8.01 c                   endif
8.01 c     sohel1_key    reade     sohel1
8.01 c                   enddo
6.31  *
6.31 c     end_kid       endsr
8.05  *
8.05  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.05  * Subrutine for å håndtere brudd på bong
8.05  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.05  *
8.05  /FREE
8.05   begsr ny_bong;
8.05
8.05  // Første gang - hent batch ident og slett gammel bilagslogg
8.05
8.05   if w_btex = ' ';
8.05
8.05     exec sql
8.05      select rlbts1
8.05        into :w_timb
8.05       from rlobst
8.05       where
8.05        rlbfir = :w_firm and
8.05        rlbbil = :bfbiln and rlbslt = ' ' and rlbomk = ' ' ;
8.05
8.05     exec sql
8.05      delete from rlobst
8.05       where
8.05        rlbfir = :w_firm and rlbts1 = :w_timb;
8.05       if sqlcod = 0;
8.05        commit;
8.05       endif;
8.05
8.05     leavesr;
8.05   endif;
8.05
8.05  // Skriv logg på bilagsnummer nivå, nullstill verdier og henter nytt bilagsnummer
8.05
8.05     w_dumtim = *loval;
8.05     w_dumsum = 0;
8.05     w_dumant = 0;
8.05     w_dumusr = '  ';
8.05     w_dumerr = '  ';
8.05
8.05   exec sql
8.05         insert into rlobst
8.05          (RLBFIR, RLBBIL, RLBTS1,RLBAN1,RLBSD1,RLBSK1,RLBBR1,
8.05           RLBTS2,RLBAN2,RLBSD2,RLBSK2,RLBBR2,RLBFF2,RLBFT2,
8.05           RLBTS3,RLBAN3,RLBSD3,RLBSK3,RLBBR3,RLBFF3,RLBFT3,
8.05           RLBTS4,RLBAN4,RLBSD4,RLBSK4,RLBBR4,RLBFF4,RLBFT4,
8.05           RLBTS5, RLBBR5, RLBTS6, RLBBR6)
8.05        values(:w_firm,:w_biln,:w_timb,:w_antp,
8.05               :w_sumd,:w_sumk,:l_user,
8.05               :w_dumtim,:w_dumant,:w_dumsum,:w_dumsum,:w_dumusr,
8.05               '  ',:w_dumerr,
8.05               :w_dumtim,:w_dumant,:w_dumsum,:w_dumsum,:w_dumusr,
8.05               '  ',:w_dumerr,
8.05               :w_dumtim,:w_dumant,:w_dumsum,:w_dumsum,:w_dumusr,
8.05               '  ',:w_dumerr,
8.05               :w_dumtim,:w_dumusr,:w_dumtim,:w_dumusr);
8.05       if sqlcod = 0;
8.05        commit;
8.05       endif;
8.05
8.05  // Oppdaterer bilagsnummer på logg linje poster med denne bongen
8.05
8.05   exec sql
8.05         update rlodst
8.05          set rldbin = :w_biln
8.05          where rldfir = :w_firm and rldts1 = :w_timb and rldbtx = :w_btex;
8.05       if sqlcod = 0;
8.05        commit;
8.05       endif;
8.05
8.05      w_antp = 0;
8.05      w_sumd = 0;
8.05      w_sumk = 0;
8.05
8.05      if b_siste = *off;
8.05       exsr hent_bilag;
8.05      endif;
8.05  *
8.05   endsr;
8.05  /END-FREE
8.05  *
8.05  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.05  * Subrutine for å overstyrende bilagsnummerserie
8.05  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.05  *
8.05 c     hent_bilag    begsr
8.05  *
8.05 c                   eval      w_fast = *blank
8.05 c                   eval      w_fell = 'POSBIL'
8.05 c                   eval      w_type = *blank
8.05 c                   eval      w_kode = '0'
8.05  *
8.05  *  Kaller opp subprogram for henting av nummer fra NUMMER-REGISTER
8.05  *
8.05 c                   call      'AS100R'
8.05 c                   parm                    w_kode
8.05 c                   parm                    w_firm
8.05 c                   parm                    w_fell
8.05 c                   parm                    w_type
8.05 c                   parm                    w_fast
8.05 c                   parm                    w_biln
8.05  *
8.05 c                   endsr
8.06  *
8.06  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.06  * Subrutine for å sjekke om ordre på bong har depositum
8.06  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.06  *
8.06 c     sjk_depo      begsr
8.06  *
8.06  /FREE
8.06
8.06   w_aarr = %subdt(bfbdat:*y);
8.06
8.06   exec sql
8.06      select bdbong
8.06        into :w_bong
8.06        from bodtpf
8.06       where bdfirm = :w_firm and
8.06                 bdaarr = :w_aarr and
8.06                 bdornr = :bfkref and
8.06             bdvare = (select baadva from bstspf);
8.06   if sqlcod < 0 or
8.06      sqlcod = 100;
8.06      ibdepo = 'N';
8.06     else;
8.06      ibdepo = 'J';
8.06   endif;
8.06  /END-FREE
8.06  *
8.06 c                   endsr
8.05  *
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  * Subrutine for initiering av program
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  *
6.21 c     *inzsr        begsr
      *
      * Key for posisjonering på posteringsfil
      *
     c     bovfl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bovfl1_peri
6.31  *
6.31  * Key for oppslag på fakturanummer
6.31  *
6.31 c     sohel1_key    klist
6.31 c                   kfld                    w_firm
6.31 c                   kfld                    sohel1_aarr
6.31 c                   kfld                    sohel1_binr
6.33  *
6.33  * Key for les på konverteringstabell
6.33  *
6.33 c     ikonir_key    klist
6.33 c                   kfld                    w_firm
6.33 c                   kfld                    ikonir_bfra
6.34  *
6.34  * Key for les på status-register
6.34  *
6.34 c     lstsl1_key    klist
6.34 c                   kfld                    w_firm
8.08  *
8.08  * Key for les på kunderegister
8.08  *
8.08 c     rkunl1_key    klist
8.08 c                   kfld                    w_firm
8.08 c                   kfld                    rkunl1_kund
8.08  *
8.08  * Key for les på konto/kunde/lev oppl
8.08  *
8.08 c     raa1l1_key    klist
8.08 c                   kfld                    w_firm
6.34  *
6.34  * Klargjør for øreavrundings konvertering
6.34  *
     c                   eval      w_firm = l_firm
     c                   time                    w_date
     c                   time                    w_time
6.34 c     lstsl1_key    chain     lstsl1
6.34 c                   eval      w_obil = laabil
8.08  *
8.08 c     raa1l1_key    chain     raa1l1
8.05  *
8.05  * Sjekker om bilagsserie for bilag pr. bong er lagt oppp
8.05  *
8.05 c                   eval      b_posb = *off
8.05  *
8.05 c                   exsr      hent_bilag
8.05  *
8.05 c                   if        w_biln > 0
8.05 c                   eval      b_posb = *on
8.05 c                   endif
8.09  *
8.09  *   Benyttes lang eller kort kidd?
8.09  *
8.09 c     'FFAKT     '  chain     aforl1
8.09 c                   if        %found(aforl1)
8.09 c                             and afkidk = 1
8.09 c                   eval      w_langk = 'J'
8.09 c                   else
8.09 c                   eval      w_langk = 'N'
8.09 c                   endif
8.08  *
8.08  * Kjører firmaet Santander?
8.08  *
8.08   CallP CO402R(l_filg:w_firm:0:'FO530R_702':
8.08                    co402_verdi1:co402_verdi2);
8.08   if co402_verdi1 = '1';
8.08     eval w_sant = 'J';
8.08   else;
8.08     w_sant      = ' ';
8.08   endif;
6.21  *
8.10  *
8.10  * Skal Kort kidd fjernes når lang kidd benyttes
8.10  *
8.10   CallP CO402R(l_filg:w_firm:0:'IKKE_KORT_KIDD_NÅR_LANG':
8.10                   co402_verdi1:co402_verdi2);
8.10     if co402_verdi1 = '1';
8.10         u_ikknl = *on;
8.10     endif;
6.01 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
