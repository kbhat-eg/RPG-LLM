     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASSTAT
      * Program . . . . : SI610R
      * Beskrivelse . . : Kopierer parameter til arbeidsfil
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       281208 KOB  Nytt program
      * V5.60 220109 KOB  Korrigert feil ved firma <> 000
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fslpml1    if   e           k disk    rename(slpmpfr:slpml1r)
     f                                     prefix(x:1)
     fslpwpf    o    e           k disk    rename(slpwpfr:slpwpfr)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_navn                951    980
      *
      * Definering av variable
      *
     d w_firm          s                   like(srfirm)
     d w_type          s                   like(srtype)
      *
      * Legg ut parametre i arbeidsfil
      *
     c     slpml1_key    setll     slpml1r
     c     slpml1_key    reade     slpml1r                                61
     c                   dow       *in61 = *off
     c                   eval      srfirm = xrfirm
     c                   eval      srtype = xrtype
     c                   eval      srkode = xrkode
     c                   eval      srvalg = xrvalg
     c                   eval      srrest = xrrest
     c                   eval      srsyst = xrsyst
     c                   write     slpwpfr
     c     slpml1_key    reade     slpml1r                                61
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     C     *entry        plist
     C                   parm                    p_type           10
      *
     c                   eval      w_type = p_type
     c                   eval      w_firm = l_firm
      *
      * Key for file PARAMETER-REGISTER
      *
     c     slpml1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_type
      *
     c                   endsr
