     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FU706R
      * Beskrivelse . . : Henter inn siste kode fra loggfilen
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       160307 AMD  Nytt program
      *       151107 bof  endret slik at siste kode blir hentet
5.60  *       070108 bof  Utvidet nøkkel med år
5.60  *       020508 bof  Henter siste kode som har med kjørekontor å gjøre
5.60  *                   Hopper over ordrebekreftelse
6.NU  * R6.30 260514 bsa  Programmet gjennomgått mtp nummerutvidelser.
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
3.32 fflogl2    if   e           k disk    rename(flogpfr:flogl2r)
3.32 fvlkolr    if   e           k disk    rename(vlkopfr:vlkolrr)
      *
      * Definering av parametre
      *
5.60 d p_aarr          s                   like(fuaarr)
6.NU d p_numm          s                   like(funumm)
     d p_suff          s                   like(fusuff)
     d p_kode          s                   like(fukode)
      *
     d s_kode          s                   like(fukode)
      *
      * Definering av Local data area (LDA)
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
5.60 d flogl2_aarr     s                   like(fuaarr)
     d flogl2_numm     s                   like(funumm)
     d flogl2_suff     s                   like(fusuff)
     d flogl2_date     s                   like(fudate)
     d flogl2_time     s                   like(futime)
     d vlkolr_jtyp     s                   like(vajtyp)
      *
      * Definering av variable
      *
     d w_firm          s                   like(fufirm)
      *
      * --------------------------------------------------------------------
      * Hoved - logikk
      * --------------------------------------------------------------------
      *
      * Finner høyeste kode i loggfilen
      * og returnerer denne.
      *
     c                   exsr      hent_logg
      *
     c                   goto      avslutt
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av siste transaksjon i logg - file
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_logg     begsr
     c                   eval      p_kode = *blank
     c                   eval      s_kode = *blank
      *
      * Henter siste post fra logg - filen
      *
5.60 c                   eval      flogl2_aarr = p_aarr
     c                   eval      flogl2_numm = p_numm
     c                   eval      flogl2_suff = p_suff
     c                   eval      flogl2_date = *hival
     c                   eval      flogl2_time = *hival
      *
     c     flogl2_key    setll     flogl2r
     c                   readp     flogl2r
     c                   dow       not %eof(flogl2) and
     c                             fuaarr = p_aarr and
     c                             funumm = p_numm and
     c                             fusuff = p_suff
      *
     c                   if        %eof
     c                   if        s_kode = '1'
     c                   eval      p_kode = s_kode
     c                   else
     c                   eval      p_kode = *blank
     c                   endif
     c                   goto      end_sr
     c                   endif
      *
5.60 c                   if        fuaarr = p_aarr and
     c                             funumm = p_numm and
     c                             fusuff = p_suff
      *
     c                   if        fukode > '1'
     c                   eval      p_kode = fukode
     c                   goto      end_sr
     c                   else
     c                   eval      s_kode = fukode
     c                   endif
     c                   endif
      *
     c                   readp     flogl2r
     c                   enddo
      *
     c     end_sr        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
5.60 c                   parm                    p_aarr
6.NU c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_kode
      *
      * Key for logg - filen
      *
     c     flogl2_key    klist
     c                   kfld                    w_firm
5.60 c                   kfld                    flogl2_aarr
     c                   kfld                    flogl2_numm
     c                   kfld                    flogl2_suff
     c                   kfld                    flogl2_date
     c                   kfld                    flogl2_time
      *
      * Key for les av loggkode - type
      *
     c     vlkolr_key    klist
     c                   kfld                    w_firm
5.60 c                   kfld                    vlkolr_jtyp
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
