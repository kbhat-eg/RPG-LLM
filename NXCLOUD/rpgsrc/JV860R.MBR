     H DFTACTGRP(*NO) ACTGRP(*NEW)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JV860R
      * Beskrivelse . . : Hovedprogam  for XML fra NOBB
      *
      *                   Leser katalog hvor NOBB XML filer finnes.
      *                   XML filene sorteres og behandles i "rett" rekkefølge,
      *                   dvs sortert på filnavn (som er et tall)
      *                   Etter behandling lages kvitteringsfil og innlest XML
      *                   flyttes til en annen katalog
      *                   Til slutt slettes gamle innlest XML
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       040414 BKV  Nytt program
6.30  * R6.30 070414 BKV  Sletter gamle filer fra lest katalogen
6.31  * R6.30 070414 BKV  Gamle filer: endret fra 14 dager til 3 dager
6.32  * R6.30 020614 BKV  Logge sist kjørt til jstspf
6.33  * R6.30 190614 BKV  Gamle filer: endret fra 3 dager til 1 dag
6.34  * R6.30 040914 BKV  Stopper programmet hvis ikke JSTSPF kan leses
6.35  * R6.30 300614 BKV  Korrigering av parameter def
6.36  * R6.30 050914 BKV  Dummy behandler enhet filen
6.37  * R6.30 040914 BKV  Ny parameter for oppdatering av JSTSPF
6.38  * R6.30 240418 BKV  Rydder i gamle kvit filer + fix sjekk lengde filnavn
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fafpslr    if   e           k disk    rename(afpspfr:afpslrr)
6.32 f                                     infsr(FileErr)
6.32 fjstsl1    if   e           k disk    rename(jstspfr:jstsl1r)
6.32 f                                     infsr(FileErr)
6.32 fjstslu    uf   e           k disk    rename(jstspfr:jstslur)
6.32 f                                     infsr(FileErr)
6.32  *
      *
      *
     D*dirBaseNavn     s             21a   Inz('/home/asp/bmp/nobb80/')
     D*dirInnNavn      s             25a   Inz('/home/asp/bmp/nobb80/inn/')
     D dirBaseNavn     s            256a   Varying
     D dirFInnNavn     s            256a   Varying
6.30 D dirFLestNavn    s            256a   Varying
6.38 D dirFKvitNavn    s            256a   Varying
     D dirInnNavn      s              4a   Inz('inn/')
     D dirKvitNavn     s              5a   Inz('kvit/')
     D dirLestNavn     s              5a   Inz('lest/')
     D dirFeilNavn     s              5a   Inz('feil/')
     D kvitExt         s              3a   Inz('txt')
     D vareFnavn       s              4a   Inz('VARE')
     D modulFnavn      s              4a   Inz('MODU')
     D prisFnavn       s              4a   Inz('PRIS')
     D grupFnavn       s              4a   Inz('GRUP')
     D deltFnavn       s              4a   Inz('LEVE')
     D prodFnavn       s              4a   Inz('PROD')
6.36 D enheFnavn       s              4a   Inz('ENHE')
     D typeFNavn       s             10a   varying
      *
     d b_ferd          s              1    inz(*off)
      *
      * Definering av variabler i nøkler
      *
     d afpslr_ufil     s                   like(l_filg)
     d afpslr_uide     s                   like(afuide)
      *
      * Definering av tabell med meldinger
     d a_meld          s            100    dim(3) ctdata perrcd(1)
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_stat          s              1
      * Brukes som parametre til jobblogging
     d p_jnav          s             15
     d p_jbid          s             41
     d p_jtxt          s             35
     d p_jsts          s              2  0
     d p_jmld          s            200
     d w_xmlf          s              5  0
     d w_xmlk          s              5  0
      *
      * Definering av Local Data Area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_filg                931    933
      *
     d w_firm          s              3  0
      *
      * kode for å lese filer og kataloger er hentet fra her:
      *   http://www.scottklement.com/rpg/ifs_ebook/ifs_ebook.html
      ** API call to open a stream file
      **
     D open            PR            10I 0 extproc('open')
     D   path                          *   value options(*string)
     D   oflag                       10I 0 value
     D   mode                        10U 0 value options(*nopass)
     D   codepage                    10U 0 value options(*nopass)
      **********************************************************************
      *  Flags for use in open()
      *
      * More than one can be used -- add them together.
      **********************************************************************
      *  00000000000000000000000000000001          Reading Only
     D O_RDONLY        C                   1
      *  00000000000000000000000000000010          Writing Only
     D O_WRONLY        C                   2
      *  00000000000000000000000000000100          Reading & Writing
     D O_RDWR          C                   4
      *  00000000000000000000000000001000          Create File if needed
     D O_CREAT         C                   8
      *  00000000000000000000000000010000          Exclusively create --
      *                                              open will fail if it
      *                                              already exists.
     D O_EXCL          C                   16
      *  00000000000000000000000000100000          Assign a CCSID to new
      *                                            file.
     D O_CCSID         C                   32
      *  00000000000000000000000001000000          Truncate file to 0 bytes
     D O_TRUNC         C                   64
      *  00000000000000000000000100000000          Append to file
      *                                            (write data at end only)
     D O_APPEND        C                   256
      *  00000000000000000000010000000000          Synchronous write
     D O_SYNC          C                   1024
      *  00000000000000000000100000000000          Sync write, data only
     D O_DSYNC         C                   2048
      *  00000000000000000001000000000000          Sync read
     D O_RSYNC         C                   4096
      *  00000000000000001000000000000000          No controlling terminal
     D O_NOCTTY        C                   32768
      *  00000000000000010000000000000000          Share with readers only
     D O_SHARE_RDONLY  C                   65536
      *  00000000000000100000000000000000          Share with writers only
     D O_SHARE_WRONLY  C                   131072
      *  00000000000001000000000000000000          Share with read & write
     D O_SHARE_RDWR    C                   262144
      *  00000000000010000000000000000000          Share with nobody.
     D O_SHARE_NONE    C                   524288
      *  00000000100000000000000000000000          Assign a code page
     D O_CODEPAGE      C                   8388608
      *  00000001000000000000000000000000          Open in text-mode
     D O_TEXTDATA      C                   16777216
      /if defined(*V5R2M0)
      *  00000010000000000000000000000000          Allow text translation
      *                                            on newly created file.
      * Note: O_TEXT_CREAT requires all of the following flags to work:
      *           O_CREAT+O_TEXTDATA+(O_CODEPAGE or O_CCSID)
     D O_TEXT_CREAT    C                   33554432
      /endif
      *  00001000000000000000000000000000          Inherit mode from dir
     D O_INHERITMODE   C                   134217728
      *  00100000000000000000000000000000          Large file access
      *                                            (for >2GB files)
     D O_LARGEFILE     C                   536870912

     D RW              C                   6
     D R               C                   4
     D OWNER           C                   64
     D GROUP           C                   8

      ** API call to write data to a stream file
      **
     D write           PR            10I 0 extproc('write')
     D   fildes                      10I 0 value
     D   buf                           *   value
     D   nbyte                       10U 0 value
      ** API call to close a stream file
      **
     D close           PR            10I 0 extproc('close')
     D   fildes                      10I 0 value


      *--------------------------------------------------------------------
      * Rename File or Directory
      *
      * int rename(const char *old, const char *new)
      *
      *  Note: By defailt, if a file with the new name already exists,
      *        rename will fail with an error.  If you define
      *        RENAMEUNLINK and a file with the new name already exists
      *        it will be unlinked prior to renaming.
      *--------------------------------------------------------------------
      /if defined(RENAMEUNLINK)
     D rename          PR            10I 0 ExtProc('Qp0lRenameUnlink')
     D   old                           *   Value options(*string)
     D   new                           *   Value options(*string)
      /else
     D rename          PR            10I 0 ExtProc('Qp0lRenameKeep')
     D   old                           *   Value options(*string)
     D   new                           *   Value options(*string)
      /endif

      * dirent structure http://www.scottklement.com/rpg/ifs_ebook/ifs_ebook.html
      *
     D p_dirent        s               *
     D dirent          ds                  based(p_dirent)
     D   d_reserved1                 16A
     D   d_fileno_gen_id...
     D                               10U 0
     D   d_fileno                    10U 0
     D   d_reclen                    10U 0
     D   d_reserved3                 10I 0
     D   d_reserved4                  8A
     D   d_nlsinfo                   12A
     D     nls_ccsid                 10I 0 OVERLAY(d_nlsinfo:1)
     D     nls_cntry                  2A   OVERLAY(d_nlsinfo:5)
     D     nls_lang                   3A   OVERLAY(d_nlsinfo:7)
     D     nls_reserv                 3A   OVERLAY(d_nlsinfo:10)
     D   d_namelen                   10U 0
     D   d_name                     640A
      *
      * sjekk http://www.scottklement.com/rpg/ifs_ebook/ifs_ebook.html
6.30 D p_statds        S               *
6.30 D statds          DS                  BASED(p_statds)
6.30 D  st_mode                      10U 0
6.30 D  st_ino                       10U 0
6.30 D  st_nlink                      5U 0
6.30 D  st_pad                        2A
6.30 D  st_uid                       10U 0
6.30 D  st_gid                       10U 0
6.30 D  st_size                      10I 0
6.30 D  st_atime                     10I 0
6.30 D  st_mtime                     10I 0
6.30 D  st_ctime                     10I 0
6.30 D  st_dev                       10U 0
6.30 D  st_blksize                   10U 0
6.30 D  st_allocsize                 10U 0
6.30 D  st_objtype                   12A
6.30 D  st_codepage                   5U 0
6.30 D  st_reserved1                 62A
6.30 D  st_ino_gen_id                10U 0
      *
6.30 D filnavn         S           1024A   varying
6.30 D fnmedkat        S           2048A   varying
6.30 D mystat          S                   like(statds)
6.30 D epoch           S               Z   inz(z'1970-01-01-00.00.00.000000')
6.30 D lagetdato       S               Z
6.30 D fildato         S               D
6.30 D mytime          S               T
6.30 D w_slett_d       S               D
      *
      *--------------------------------------------------------------------
      * Open a Directory
      *
      * DIR *opendir(const char *dirname)
      *--------------------------------------------------------------------
     D opendir         PR              *   EXTPROC('opendir')
     D  dirname                        *   VALUE options(*string)
      *
      *--------------------------------------------------------------------
      * Close a directory
      *
      * int closedir(DIR *dirp)
      *--------------------------------------------------------------------
     D closedir        PR            10I 0 EXTPROC('closedir')
     D  dirhandle                      *   VALUE
      *
      *--------------------------------------------------------------------
      * Read Directory Entry
      *
      * struct dirent *readdir(DIR *dirp)
      *--------------------------------------------------------------------
     D readdir         PR              *   EXTPROC('readdir')
     D  dirp                           *   VALUE
      *
      *--------------------------------------------------------------------
      * Delete/unlik a file
      *
      * int unlink(DIR *dirp)
      *--------------------------------------------------------------------
6.30 D unlink          PR            10I 0 ExtProc('unlink')
6.30 D   path                          *   Value options(*string)

      *--------------------------------------------------------------------
      * Change Directory
      *
      * int chdir(const char *path)
      *--------------------------------------------------------------------
     D chdir           PR            10I 0 ExtProc('chdir')
     D   path                          *   Value Options(*string)
      *
      *
6.30 D stat            PR            10I 0 ExtProc('stat')
6.30 D   path                          *   value options(*string)
6.30 D   buf                           *   value
      *
      *
     D fd              S             10I 0
     D sisteDato       s             10a
6.37 D oppdatSts       s              1  0 inz(0)
      *
     D dir             s               *
     D dlengde         S             10i 0
      *
      * array med alle xml filer
     d w_batch         s              4  0 inz(2000)
     D katV            ds                  Qualified Dim(2000)
     D  fnavn                        50a
     D  dnavn                       200a
     D  fnavnP                      250a
     D  flen                         10i 0
6.35 D  fplen                         3i 0
      *
     D numF            s             10i 0
     d i               s              5  0

     D filen           s            200a
     D filenEtter      s            200a   varying
     D filenFeil       s            200a   varying
     D filenKvitt      s            200a   varying

     D P_VareXml       PR                  ExtPgm('JV860R_XML')
     D   filNavn                    255    options(*varsize)
6.35 D   lengde                       3p 0 const
     D   sisteDato                   10a
6.37 D   oppdatSts                    1p 0 const

     D P_ModulXml      PR                  ExtPgm('JV863R_XML')
     D   filNavn                    255    options(*varsize)
6.35 D   lengde                       3p 0 const
     D   sisteDato                   10a

     D P_PrisXml       PR                  ExtPgm('JV865R_XML')
     D   filNavn                    255    options(*varsize)
6.35 D   lengde                       3p 0 const
     D   sisteDato                   10a

     D P_VarGXml       PR                  ExtPgm('JV867R_XML')
     D   filNavn                    255    options(*varsize)
6.35 D   lengde                       3p 0 const
     D   sisteDato                   10a
      *
     D P_DeltXml       PR                  ExtPgm('JV868R_XML')
     D   filNavn                    255    options(*varsize)
6.35 D   lengde                       3p 0 const
     D   sisteDato                   10a
      *
     D P_ProdXml       PR                  ExtPgm('JV869R_XML')
     D   filNavn                    255    options(*varsize)
6.35 D   lengde                       3p 0 const
     D   sisteDato                   10a
6.32  *
6.32  * Datastruktur for informasjon ved exceptions
6.32  *
6.32 d PgmError       sds
6.32 d  proc_name        *proc
6.32 d  pgm_status       *STATUS
6.32 d  prv_status            16     20s 0
6.32 d  line_numm             21     28
6.32 d  routine          *ROUTINE
6.32 d  parms            *PARMS
6.32 d  excp_type             40     42
6.32 d  excp_numm             43     46
6.32 d  pgm_lib               81     90
6.32 d  excp_data             91    170
6.32 d  excp_id              171    174
6.32 d  date                 191    198
6.32 d  year                 199    200s 0
6.32 d  last_file            201    208
6.32 d  file_info            209    243
6.32 d  job_name             244    253
6.32 d  job_user             254    263
6.32 d  job_numb             264    269s 0
6.32  *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c
6.30 c                   eval      w_slett_d = %Date()
6.33 c     w_slett_d     subdur    1:*days       w_slett_d
6.34 c     jstsl1_key    chain     jstsl1
6.34 c                   if        not %found
6.34 c                   eval      p_stat = '1'
6.34 c                   eval      p_jsts = 40
6.34 c                   eval      p_jmld = *blanks
6.34 c                   eval      p_jmld = 'Fikk ikke lest jstspf'
      *
6.34 c                   call      'AB705R'
6.34 c                   parm                    p_jbid
6.34 c                   parm                    p_jsts
6.34 c                   parm                    p_jmld
6.34 c                   goto      avslutt
6.34 c                   endif
      *
      * Hent inn NOBB 80 XML path som skal brukes
      *
     c                   eval      afpslr_ufil = l_filg
     c                   eval      afpslr_uide = 'NOBB80XML'
     c     afpslr_key    chain     afpslrr
     c                   if        %found
     c                   eval      dirBaseNavn = *blanks
     c                   movel     afudss        dirBaseNavn
     c                   eval      dirFInnNavn = %Trim(dirBaseNavn)
     c                                 + %Trim(dirInnNavn)
     c                   else
     c                   eval      p_stat = '1'
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      w_xmlf = 0
     c                   eval      w_xmlk = 0
      * leser filer i batch i tilfelle mange filer
     c                   dow       b_ferd = *off
     c                   eval      numF = 0
     c                   exsr      beh_xmlf
     c                   enddo
     c                   exsr      log_xmlf
     c
6.30 c                   exsr      beh_lest
6.38 c                   exsr      beh_kvit
6.32  *
6.32  * Oppdaterer statusregister med dato for innlesing
6.32  *
6.32 c     jstslu_key    chain     jstslu
6.32 c                   time                    jaaopd
6.32 c                   update    jstslur
6.32  *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     avslutt       tag
     c                   if        p_stat = '1'
     c                   eval      p_jsts = 50
     c                   eval      p_jmld = 'Alvorlig feil oppstod i JV860R'
      *
     c                   call      'AB705R'
     c                   parm                    p_jbid
     c                   parm                    p_jsts
     c                   parm                    p_jmld
      *
     c                   else
      *
     c                   endif
      *
      * Vi kvitterer ut jobben fra jobbstatusfil
     c                   call      'AB710R'
     c                   parm                    p_jnav
     c                   parm                    p_jbid
      *
     c                   eval      *inlr = *on
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av xml filer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     beh_xmlf      begsr

      * list alle filer i katalogen
     c                   eval      dlengde = %len(%trim(dirFInnNavn))
     c                   eval      dir = opendir(%trim(dirFInnNavn))
     c
     c                   if        dir = *NULL
     c                   eval      p_stat = '1'
     c                   goto      avslutt
     c                   endif
      * looper alle vare filene, legger de i array
     c                   eval      p_dirent = readdir(dir)
     c                   dow       p_dirent <> *NULL
     c                   eval      typeFNavn = %subst(d_name:15:4)
6.38 c                   if        (d_namelen > 18 ) and (
6.38 c                             typeFNavn = %trim(vareFnavn)
     c                             or typeFNavn = %trim(modulFnavn)
     c                             or typeFNavn = %trim(prisFnavn)
     c                             or typeFNavn = %trim(grupFnavn)
     c                             or typeFNavn = %trim(deltFnavn)
     c                             or typeFNavn = %trim(prodFnavn)
6.36 c                             or typeFNavn = %trim(enheFnavn)
6.38 c                             )
     c                   eval      numF = numF + 1
     c                   eval      katV(numF).fnavn = %subst(d_name:1:d_namelen)
     c                   eval      katV(numF).dnavn =
     c                                %subst(%trim(dirFInnNavn):1:dlengde)
     c                   eval      katV(numF).fnavnP =
     c                                %subst(%trim(dirFInnNavn):1:dlengde)
     c                                + %subst(d_name:1:d_namelen)
     c                   eval      katV(numF).flen = d_namelen
     c                   eval      katV(numF).fplen = d_namelen + dlengde
     c                   endif
     c                   if        numF = (w_batch - 1)
     c                   leave
     c                   endif
     c                   eval      p_dirent = readdir(dir)
     c                   enddo
     c                   callp     closedir(dir)
     c
     c                   eval      w_xmlf = w_xmlf + numF
      * fant ingen filer
     c                   if        numF = 0
     c                   eval      b_ferd = *on
     c                   leavesr
     c                   endif
      * sorterer  vare filene, for å lese inn i rett rekkefølge
     c                   SORTA(A)  %subarr(katV(*).fnavn : 1 : numF)
      * looper alle filene
     c                   for       i = 1 to numF
     c                   eval      typeFNavn = %subst(katV(i).fnavn:15:4)
     c                   eval      sisteDato = *blank
     c                   eval      filen = katV(i).fnavnP
     c                   eval      filenEtter = %trim(dirBaseNavn) + dirLestNavn
     c                                +  %subst(katV(i).fnavn:1:katV(i).flen)
     c                   eval      filenFeil  = %trim(dirBaseNavn) + dirFeilNavn
     c                                +  %subst(katV(i).fnavn:1:katV(i).flen)
     c                   eval      filenKvitt =
     c                               %subst(katV(i).fnavn:1:katV(i).flen-3) +
     c                               kvitExt
     c                   exsr      log_xmlnavn
     c                   select
     c                   when      %trim(typeFNavn) = vareFnavn
6.37 c                   callp     P_VareXml(filen: katV(i).fplen : sisteDato:
6.37 c                                oppdatSts)
     c                   when      %trim(typeFNavn) = modulFnavn
     c                   callp     P_ModulXml(filen: katV(i).fplen : sisteDato)
     c                   when      %trim(typeFNavn) = prisFnavn
     c                   callp     P_PrisXml(filen: katV(i).fplen : sisteDato)
     c                   when      %trim(typeFNavn) = grupFnavn
     c                   callp     P_VarGXml(filen: katV(i).fplen : sisteDato)
     c                   when      %trim(typeFNavn) = deltFnavn
     c                   callp     P_DeltXml(filen: katV(i).fplen : sisteDato)
     c                   when      %trim(typeFNavn) = prodFnavn
     c                   callp     P_ProdXml(filen: katV(i).fplen : sisteDato)
6.36 c                   when      %trim(typeFNavn) = enheFnavn
6.36  *            dummy behandlig av enhet filen, slik at den blir slettet
6.36 c                   eval      sisteDato = %CHAR(%DATE)
     c                   other
     c                   iter
     c                   endsl
      *
      * hvis ikke funnet siste dato, så har noe feilet... lager ikke kvittering
     c                   if        %trim(sisteDato) = ''
      * flytter xml fil som har feilet
     c                   if        rename(%trimr(filen):%trimr(filenFeil)) < 0
     c                   eval      p_stat = '1'
     c                   goto      avslutt
     c                   endif
     c                   leavesr
     c                   end
     c
      * lag kvitteringsfil - lages og åpnes, for å kunne skrive text
     c                   if        chdir(%Trim(dirBaseNavn)+dirKvitNavn) >= 0
     c                   eval      fd = open(filenKvitt :
     c                               O_CREAT+O_WRONLY+O_CODEPAGE :
     c                               RW*OWNER + RW*GROUP + R : 1252  )
     c                   if        fd < 0
     c                   eval      p_stat = '1'
     c                   goto      avslutt
     c                   endif
     c                   callp     close(fd)
     c* åpnes igjen for å skrive text
     c                   eval      fd = open(filenKvitt :
     c                               O_WRONLY+O_TEXTDATA)
     c                   if        fd < 0
     c                   eval      p_stat = '1'
     c                   goto      avslutt
     c                   endif
     c                   callp     write(fd: %addr(sisteDato): %size(sisteDato))
     c                   callp     close(fd)
      * flytter xml fil som er lest
     c                   if        rename(%trimr(filen):%trimr(filenEtter)) < 0
     c                   eval      p_stat = '1'
     c                   goto      avslutt
     c                   endif
     c                   eval      w_xmlk = w_xmlk + 1
     c                   else
     c                   goto      avslutt
     c                   endif
     c                   endfor                                                 looper alle filene
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for logging av xml filer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     log_xmlf      begsr
      *
     c                   eval      p_jsts = 10
     c                   eval      p_jmld = *blanks
     c                   eval      p_jmld = %trim(a_meld(1)) +' '+ %char(w_xmlf)
      *
     c                   call      'AB705R'
     c                   parm                    p_jbid
     c                   parm                    p_jsts
     c                   parm                    p_jmld
      *
     c                   eval      p_jsts = 10
     c                   eval      p_jmld = *blanks
     c                   eval      p_jmld = %trim(a_meld(2)) +' '+ %char(w_xmlk)
      *
     c                   call      'AB705R'
     c                   parm                    p_jbid
     c                   parm                    p_jsts
     c                   parm                    p_jmld
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for logging av filnavn
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     log_xmlnavn   begsr
      *
     c                   eval      p_jsts = 10
     c                   eval      p_jmld = *blanks
     c                   eval      p_jmld = %trim(a_meld(3)) +' '+ %trimr(filen)
      *
     c                   call      'AB705R'
     c                   parm                    p_jbid
     c                   parm                    p_jsts
     c                   parm                    p_jmld
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av leste xml filer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.30 c     beh_lest      begsr
6.30  * list alle filer i katalogen
6.30 c                   eval      dirFLestNavn = %trim(dirBaseNavn)
6.30 c                               + dirLestNavn
6.30 c                   eval      dlengde = %len(%trim(dirFLestNavn))
6.30 c                   eval      dir = opendir(%trim(dirFLestNavn))
6.30 c
6.30 c                   if        dir = *NULL
6.30 c                   eval      p_stat = '1'
6.30 c                   goto      avslutt
6.30 c                   endif
6.30 c                   eval      p_statds = %addr(mystat)
6.30  * looper alle filene
6.30 c                   eval      p_dirent = readdir(dir)
6.30 c                   dow       p_dirent <> *NULL
6.30 c                   eval      typeFNavn = %subst(d_name:15:4)
6.38 c                   if        (d_namelen > 18 ) and (
6.38 c                             typeFNavn = %trim(vareFnavn)
6.30 c                             or typeFNavn = %trim(modulFnavn)
6.30 c                             or typeFNavn = %trim(prisFnavn)
6.30 c                             or typeFNavn = %trim(grupFnavn)
6.30 c                             or typeFNavn = %trim(deltFnavn)
6.30 c                             or typeFNavn = %trim(prodFnavn)
6.36 c                             or typeFNavn = %trim(enheFnavn)
6.38 c                             )
6.30
6.30 c                   eval      filnavn = %subst(d_name:1:d_namelen)
6.30 c                   eval      fnmedkat = dirFLestNavn  + filnavn
6.30 c                   if        stat(fnmedkat: %addr(mystat))=0
6.38 c                   exsr      slett_fil
6.30 c                   endif
6.30
6.30 c                   endif
6.30 c                   eval      p_dirent = readdir(dir)
6.30 c                   enddo
      *
6.30 c                   endsr
      *
6.38  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.38  * Subrutine for behandling av kvit  xml filer
6.38  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.38  *
6.38 c     beh_kvit      begsr
6.38  * list alle filer i katalogen
6.38 c                   eval      dirFKvitNavn = %trim(dirBaseNavn)
6.38 c                               + dirKvitNavn
6.38 c                   eval      dlengde = %len(%trim(dirFKvitNavn))
6.38 c                   eval      dir = opendir(%trim(dirFKvitNavn))
6.38 c
6.38 c                   if        dir = *NULL
6.38 c                   eval      p_stat = '1'
6.38 c                   goto      avslutt
6.38 c                   endif
6.38 c                   eval      p_statds = %addr(mystat)
6.38  * looper alle filene
6.38 c                   eval      p_dirent = readdir(dir)
6.38 c                   dow       p_dirent <> *NULL
6.38 c                   eval      typeFNavn = %subst(d_name:15:4)
6.38 c                   if        (d_namelen > 18 ) and (
6.38 c                             typeFNavn = %trim(vareFnavn)
6.38 c                             or typeFNavn = %trim(modulFnavn)
6.38 c                             or typeFNavn = %trim(prisFnavn)
6.38 c                             or typeFNavn = %trim(grupFnavn)
6.38 c                             or typeFNavn = %trim(deltFnavn)
6.38 c                             or typeFNavn = %trim(prodFnavn)
6.38 c                             or typeFNavn = %trim(enheFnavn)
6.38 c                             )
6.38
6.38 c                   eval      filnavn = %subst(d_name:1:d_namelen)
6.38 c                   eval      fnmedkat = dirFKvitNavn  + filnavn
6.38 c                   if        stat(fnmedkat: %addr(mystat))=0
6.38 c                   exsr      slett_fil
6.38 c                   endif
6.38
6.38 c                   endif
6.38 c                   eval      p_dirent = readdir(dir)
s.38 c                   enddo
6.38  *
6.38 c                   endsr

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     C* Sjekker filer i lest katalog, og slette gamle filer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.38 C     slett_fil     begsr
      *
6.30 C* finn opprettet dato på filen
6.30 c     epoch         adddur    st_mtime:*S   lagetdato
6.30 c                   move      lagetdato     fildato
6.30 c                   if        fildato < w_slett_d
6.30 c                   if        unlink(%trimr(fnmedkat)) < 0
6.30 c                   eval      p_stat = '1'
6.30 c                   goto      avslutt
6.30 c                   endif
6.30 c                   endif
6.30 C                   endsr
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  * Error rutine for file errors
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  *
6.32 c     FileErr       begsr
      *
6.32 c                   eval      p_stat = '1'
6.32 c                   eval      p_jsts = 50
6.32 c                   eval      p_jmld = 'Fil ' + %trim(last_file) +
6.32 c                             ' feil. Info=' + %trim(excp_data) +
6.32 c                             '. Job=' + %trim(job_name) + '/' +
6.32 c                             %trim(job_user) + '/' +
6.32 c                             %char(job_numb)
6.32  *
6.32 c                   call      'AB705R'
6.32 c                   parm                    p_jbid
6.32 c                   parm                    p_jsts
6.32 c                   parm                    p_jmld
6.32  *
6.32 c                   goto      avslutt
6.32  *
6.32 c                   endsr
6.32  *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for propertiesfil
      *
     c     afpslr_key    klist
     c                   kfld                    afpslr_ufil
     c                   kfld                    afpslr_uide
6.32  *
6.32  * Key for oppslag på status
6.32  *
6.32 c     jstsl1_key    klist
6.32 c                   kfld                    w_firm
6.32  *
6.32  * Key for oppdatering av kode/status
6.32  *
6.32 c     jstslu_key    klist
6.32 c                   kfld                    w_firm
6.32  *
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = l_firm
      *
      * Vi starter jobblogging
      *
     c                   eval      p_jnav = 'NOBB80XML'
     c                   eval      p_jtxt = *blank
     c                   call      'AB700R'
     c                   parm                    p_jnav
     c                   parm                    p_jbid
     c                   parm                    p_jtxt

     c                   endsr
**
Antall xml filer:
Antall kvitteringer laget:
Begynner filnavn:
