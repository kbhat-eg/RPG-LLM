      /TITLE  ASSHOP: Fjerning fra historisk bongregister hovedproram
     hoption(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASSHOP
      * Program . . . . : BO441R
      * Beskrivelse . . : Fjerning fra historisk bongregister -
      *                                hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
5.30  * R5.30 141004 HFL  I bonghode og linje er bongnr. 12A.
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fboheld    if   e           k disk    rename(bohepfr:boheldr)
     fbohelu    uf   e           k disk    rename(bohepfr:bohelur)
     fbodtlu    uf   e           k disk    rename(bodtpfr:bodtlur)
      *
      * Definering av parametre
      *
     d p_afir          s              3
     d p_dato          s               d   datfmt(*iso)
      *
      * Definering av variable i nøkler
      *
     d bohelu_aarr     s                   like(boaarr)
     d bohelu_wsid     s                   like(bowsid)
5.30 d bohelu_numm     s                   like(bobong)
     d bodtlu_aarr     s                   like(bdaarr)
     d bodtlu_wsid     s                   like(bdwsid)
5.30 d bodtlu_numm     s                   like(bdbong)
      *
      * Definering av variable
      *
     d w_firm          s                   like(bofirm)
     d w_date          s                   like(bodate)
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Hovedprogram                                                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     boheld_key    setll     boheld
     c                   read      boheld                                 60
      *
     c                   dow       *in60 = *off
     c                             and bodate < p_dato
      *
     c                   exsr      oppdat                                       Backup/slett
      *
     c                   read      boheld                                 60
      *
     c                   enddo
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sletting av fakturahode og fakturalinje tabeller
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdat        begsr
      *
     c                   eval      bohelu_aarr = boaarr
     c                   eval      bohelu_wsid = bowsid
5.30 c                   eval      bohelu_numm = bobong
      *
     c                   eval      bodtlu_aarr = boaarr
     c                   eval      bodtlu_wsid = bowsid
5.30 c                   eval      bodtlu_numm = bobong
      *
     c     bohelu_key    chain     bohelu                             61
     c                   if        *in61 = *off
      *
     c                   delete    bohelur                                      Slette hode
      *
     c     bodtlu_key    setll     bodtlu
     c     bodtlu_key    reade     bodtlu                                 61
      *
     c                   dow       *in61 = *off
     c                   delete    bodtlur                                      Slette linje
     c     bodtlu_key    reade     bodtlu                                 61
     c                   enddo
      *
     c                   endif
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_afir
     c                   parm                    p_dato
      *
      * Key for les på bong-hode-tabell
      *
     c     boheld_key    klist
     c                   kfld                    w_firm
      *
      * Key for sletting fra bong-hode-tabell
      *
     c     bohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bohelu_aarr
     c                   kfld                    bohelu_wsid
     c                   kfld                    bohelu_numm
      *
      * Key for sletting fra faktura-linje -tabell
      *
     c     bodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bodtlu_aarr
     c                   kfld                    bodtlu_wsid
     c                   kfld                    bodtlu_numm
      *
      * Initierer nøkkel-verdier
      *
     c                   move      p_afir        w_firm
      *
     c                   endsr
