     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLOGI
      * Program . . . . : MB105R
      * Beskrivelse . . : ABC-analyse - abc koder alle
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       151004 bø   Nytt program
      *       020107 bø   Antall ordrelinjer og solgt ut på bildet
      *       131207 bø   Lagt på enhet i spørrebilde
5.60  *       080808 bø   Benytte ABC-kode på lager
5.70  *       021208 bof  Utvidet med prisgruppe
5.63  *       190309 bof  Kaller MM747R for oppd.av abckode på prognose
      * 6.10  180609 BSA  Oppdatert med sporingsinformasjon
6.20  * 6.20  120112 BSA  Utvidet VP750R med kampanje
6.21  * 6.20  310812 bof  Nytt felt, endringsdato for faste data som vises
6.nu  * 6.30  040614 bof  Sjekket mpt. nummerutvidelse
7.xx  * 7.00  180316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS          *
      *
8.01  *PRG2   160622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fmabclr    if   e           k disk    rename(mabcpfr:mabclrr)
     fmabdl1    if   e           k disk    rename(mabdpfr:mabdl1r)
     fmabdl3    if   e           k disk    rename(mabdpfr:mabdl3r)
     fmabdl5    if   e           k disk    rename(mabdpfr:mabdl5r)
     fmabdl7    if   e           k disk    rename(mabdpfr:mabdl7r)
     fmabdlr    if   e           k disk    rename(mabdpfr:mabdlrr)
     fmabdlu    uf a e           k disk    rename(mabdpfr:mabdlur)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
5.60 fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     fvvarlu    uf   e           k disk    rename(vvarpfr:vvarlur)
5.60 fvlaglu    uf   e           k disk    rename(vlagpfr:vlaglur)
      *
     fMB105D    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_lagk          s                   like(mblagk)
     d p_vsor          s                   like(mbvsor)
     d p_ogrp          s                   like(mbogrp)
     d p_hgrp          s                   like(mbhgrp)
     d p_ugrp          s                   like(mbugrp)
     d p_kabc          s                   like(mbkabc)
     d p_valg          s              2
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
     d h1kabc          ds
     d  d_kabc                        1
     d  d_kxyz                        1
      *
      * Definering av variabler i nøkler
      *
     d mabdl1_vare     s                   like(mbvare)
     d mabdl3_kabc     s                   like(mbkabc)
     d mabdl3_omse     s                   like(mbomse)
     d mabdl5_abpr     s                   like(mbabpr)
     d mabdl7_omse     s                   like(mbomse)
     d mabdlr_vare     s                   like(mbvare)
     d mabdlu_vare     s                   like(mbvare)
     d vvarl1_vare     s                   like(vvvare)
     d vvarlu_vare     s                   like(vvvare)
5.60 d vlaglu_vare     s                   like(vlvare)
5.60 d vlaglu_lage     s                   like(vllage)
5.60 d vlagl1_vare     s                   like(vlvare)
5.60 d vlagl1_lage     s                   like(vllage)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_abck          s              1    inz(*off)
     d b_blaf          s              1    inz(*off)
      *
     d w_firm          s                   like(mbfirm)
     d w_lagk          s                   like(mblagk)
     d w_vsor          s                   like(mbvsor)
     d w_ogrp          s                   like(mbogrp)
     d w_hgrp          s                   like(mbhgrp)
     d w_ugrp          s                   like(mbugrp)
     d w_kabc          s                   like(mbkabc)
     d w_akkp          s                   like(mbabpr)
     d w_dato          s                   like(mbedat)
     d w_lety          s              1  0
     d w_kopr          s              9  2
     d w_inpr          s              9  2
     d w_abc           s              1
     d w_xyz           s              1
     d w_vares         s                   like(vvvare)
     d w_tek1          s                   like(vvtek1)
5.60 d w_labc          s                   like(vvlabc)
8.01 d w_prgr          s              2
5.70 d w_avde          s              4  0
5.70 d w_lage          s                   like(vllage)
6.20 d w_kamp          s              5
      *
     d s_vare          s                   like(mbvare)
     d s_vsor          s                   like(mbvsor)
     d s_ogrp          s                   like(mbogrp)
     d s_hgrp          s                   like(mbhgrp)
     d s_ugrp          s                   like(mbugrp)
     d s_kabc          s                   like(mbkabc)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(12)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inka
     c                   exsr      xh1win
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkc or *inkl
     c                   eval      p_valg = 'F3'
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2tagb
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkg
     c                   eval      p_valg = 'F7'
     c                   goto      avslutt
     c                   when      *inki
     c                   eval      p_valg = 'F9'
     c                   goto      avslutt
     c                   when      *inkk
     c                   if        *in51 = *off
     c                   eval      *in51 = *on
     c                   else
     c                   eval      *in51 = *off
     c                   endif
     c                   exsr      forny
     c                   goto      b2taga
     c                   when      *inkm
     c                   exsr      xh2win
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2kabc <> *blank or
     c                             b2vare <> *blank or
     c                             b2abpr <> 0 or
     c                             b2omse <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
      *
     c                   if        b_blaf = *off
     c                   select
     c                   when      w_seqe = 'D'
     c                   eval      mabdl3_kabc = b1kabc
     c     mabdl3_key    setll     mabdl3
     c                   when      w_seqe = 'C'
     c                   eval      mabdl7_omse = b1omse
     c     mabdl7_key    setll     mabdl7
     c                   when      w_seqe = 'B'
     c                   eval      mabdl1_vare = b1vare
     c     mabdl1_key    setll     mabdl1
     c                   other
     c                   eval      mabdl5_abpr = b1abpr
     c     mabdl5_key    setll     mabdl5
     c                   endsl
     c                   endif
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
     c                   eval      *in50 = *off
      *
      * Abc-kode
      *
     c                   if        b2kabc <> *blank
     c                   eval      w_seqe = 'D'
     c                   eval      mabdl3_kabc = b2kabc
     c     mabdl3_key    setll     mabdl3
     c                   goto      end_pos
     c                   endif
      *
      * Varenr.
      *
     c                   if        b2vare <> *blank
     c                   eval      *in50 = *on
     c                   eval      w_seqe = 'B'
     c                   eval      mabdl1_vare = b2vare
     c     mabdl1_key    setll     mabdl1
     c                   goto      end_pos
     c                   endif
      *
      * Prosent
      *
     c                   if        b2abpr <> 0
     c                   eval      w_seqe = ' '
     c                   eval      mabdl5_abpr = b2abpr
     c     mabdl5_key    setll     mabdl5
     c                   goto      end_pos
     c                   endif
      *
      * Omsetning
      *
     c                   if        b2omse <> 0
     c                   eval      w_seqe = 'C'
     c                   eval      mabdl7_omse = b2omse
     c     mabdl7_key    setll     mabdl7
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2kabc = *blank
     c                   eval      b2vare = *blank
     c                   eval      b2omse = 0
     c                   eval      b2abpr = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1vare = b1vare
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   exsr      forny
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      c1vare = b1vare
     c                   exsr      xc3bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      mabdlr_vare = b1vare
     c     mabdlr_key    chain     mabdlr
      *
     c                   if        %found
     c                   movel     mbvare        vvarl1_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found(vvarl1)
     c                   movel     vvtek1        c2navn
     c                   eval      c2enhs  = vvenhs
     c                   else
     c                   eval      c2navn = 'Ukjent vare'
     c                   endif
     c                   eval      c2oabc = mboabc
     c                   eval      c2eabc = mbeabc
5.60  *
5.60 c                   if        w_lagk <> *blank
5.60 c                   movel     mbvare        vlagl1_vare
5.60 c                   movel     w_lagk        vlagl1_lage
5.60 c     vlagl1_key    chain     vlagl1                             91
5.60 c                   if        vllabc = 1
5.60 c                   eval      c2klab = 1
5.60 c                   else
5.60 c                   eval      c2klab = 0
5.60 c                   endif
5.60 c                   else
     c                   if        vvlabc = 1
     c                   eval      c2klab = 1
     c                   else
     c                   eval      c2klab = 0
     c                   endif
      *
     c                   endif
      *
     c                   eval      c2anta = mbanta
     c                   eval      c2anto = mbanto
     c                   eval      c2omse = mbomse
     c                   eval      c2user = mbuser
     c                   if        mbodat <> *loval
     c     *dmy          move      mbodat        c2odat
     c                   else
     c                   eval      c2odat = 0
     c                   endif
     c                   if        mbedat <> *loval
     c     *dmy          move      mbedat        c2edat
     c                   else
     c                   eval      c2edat = 0
     c                   endif
     c                   else
     c                   eval      c2navn = *blank
     c                   eval      c2oabc = *blank
     c                   eval      c2eabc = *blank
     c                   eval      c2klab = 0
     c                   eval      c2omse = 0
     c                   eval      c2user = *blank
     c                   eval      c2odat = 0
     c                   eval      c2edat = 0
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2eabc <> 'AX' and
     c                             c2eabc <> 'AY' and
     c                             c2eabc <> 'AZ' and
     c                             c2eabc <> 'BX' and
     c                             c2eabc <> 'BY' and
     c                             c2eabc <> 'BZ' and
     c                             c2eabc <> 'CX' and
     c                             c2eabc <> 'CY' and
     c                             c2eabc <> 'CZ' and
     c                             c2eabc <> *blank
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   mabdlur
      *
     c                   eval      mabdlu_vare = b1vare
     c     mabdlu_key    chain     mabdlur
      *
     c                   eval      mbeabc = c2eabc
     c                   if        mbeabc <> *blank
     c                   eval      mbkabc = c2eabc
     c                   move      c2eabc        mbkxyz
     c                   else
     c                   eval      mbkabc = c2oabc
     c                   move      c2oabc        mbkxyz
     c                   endif
      *
     c                   time                    mbedat
     c                   time                    mbetim
     c                   eval      mbuser = l_user
      *
     c                   if        %found
6.10 c                   time                    mbedat
6.10 c                   time                    mbetim
6.10 c                   eval      mbeusr = l_user
     c                   update    mabdlur
     c                   endif
5.60  *
5.60  * hvis lagerkode er *blank, så oppdateres vare ellers resp. lager
5.60  *
5.60 c                   if        w_lagk = *blank
      *
      * Oppdatering av vareregister med at lås av kode eller oppløs av lås
      *
     c                   eval      vvarlu_vare = b1vare
     c     vvarlu_key    chain     vvarlur
     c                   if        %found(vvarlu)
     c                   if        c2klab = 0
     c                   eval      vvlabc = 0
     c                   else
     c                   eval      vvlabc = 1
     c                   if        c2eabc <> *blank
     c                   eval      vvkabc = c2eabc
     c                   else
     c                   eval      vvkabc = c2oabc
     c                   endif
     c                   endif
6.10 c                   time                    vvedat
6.10 c                   time                    vvetim
6.10 c                   eval      vveusr = l_user
     c                   update    vvarlur
     c                   endif
5.60 c                   else
5.60  * Oppdatering av lagerreg. med at lås av kode eller oppløs av lås
5.60  *
5.60 c                   eval      vlaglu_vare = b1vare
5.60 c                   move      w_lagk        vlaglu_lage
5.60 c     vlaglu_key    chain     vlaglur
5.60 c                   if        %found(vlaglu)
5.60 c                   if        c2klab = 0
5.60 c                   eval      vllabc = 0
5.60 c                   else
5.60 c                   eval      vllabc = 1
5.60 c                   if        c2eabc <> *blank
5.60 c                   eval      vlkabc = c2eabc
5.60 c                   else
5.60 c                   eval      vlkabc = c2oabc
5.60 c                   endif
5.63 c                   exsr      upd_prog_abc
5.60 c                   endif
6.10 c                   time                    vledat
6.10 c                   time                    vletim
6.10 c                   eval      vleusr = l_user
6.21 c                   time                    vlfeda
6.21 c                   time                    vlfeti
6.21 c                   eval      vlfeus = l_user
5.60 c                   update    vlaglur
5.60 c                   endif
5.60  *
5.60 c                   endif
     c
      *
      * Oppdater subfile
      *
      *
     c                   eval      b_forn = *on
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc3bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      mabdlr_vare = b1vare
     c     mabdlr_key    chain     mabdlr
      *
     c                   if        %found
     c                   movel     mbvare        vvarl1_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found(vvarl1)
     c                   movel     vvtek1        c3navn
     c                   eval      c3enhs  = vvenhs
     c                   else
     c                   eval      c3navn = 'Ukjent vare'
     c                   endif
     c                   eval      c3oabc = mboabc
     c                   eval      c3eabc = mbeabc
5.60  *
5.60 c                   if        w_lagk <> *blank
5.60 c                   movel     mbvare        vlagl1_vare
5.60 c                   movel     w_lagk        vlagl1_lage
5.60 c     vlagl1_key    chain     vlagl1                             91
5.60 c                   if        vllabc = 1
5.60 c                   eval      c3klab = vlkabc
5.60 c                   else
5.60 c                   eval      c3klab = *blank
5.60 c                   endif
5.60 c                   else
     c                   if        vvlabc = 1
     c                   eval      c3klab = vvkabc
     c                   else
     c                   eval      c3klab = *blank
     c                   endif
      *
     c                   endif
      *
     c                   eval      c3anto = mbanto
     c                   eval      c3anta = mbanta
     c                   eval      c3omse = mbomse
     c                   eval      c3user = mbuser
     c                   if        mbodat <> *loval
     c     *dmy          move      mbodat        c3odat
     c                   else
     c                   eval      c3odat = 0
     c                   endif
     c                   if        mbedat <> *loval
     c     *dmy          move      mbedat        c3edat
     c                   else
     c                   eval      c3edat = 0
     c                   endif
     c                   exsr      finn_pris
     c                   eval      c3pris = w_kopr
     c                   else
     c                   eval      c3navn = *blank
     c                   eval      c3oabc = *blank
     c                   eval      c3eabc = *blank
     c                   eval      c3klab = *blank
     c                   eval      c3anto = 0
     c                   eval      c3anta = 0
     c                   eval      c3pris = 0
     c                   eval      c3omse = 0
     c                   eval      c3user = *blank
     c                   eval      c3odat = 0
     c                   eval      c3edat = 0
     c                   endif
      *
     c     c3taga        tag
      *
     c                   exfmt     c3bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c3bld
     c                   endif
      *
     c     end_c3bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * xh1win  Bilde for behandling av bla-alternativer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xh1win        begsr
      *
     c     h1taga        tag
      *
     c                   write     h1win
     c                   exfmt     h1win
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc
     c                   goto      end_h1win
     c                   endsl
      *
     c                   if        h1kabc <> *blank
     c                   eval      b_blaf = *on
     c                   else
     c                   eval      b_blaf = *off
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'D'
     c                   eval      mabdl3_kabc = *blank
     c     mabdl3_key    setll     mabdl3
     c                   when      w_seqe = 'C'
     c                   eval      mabdl7_omse = *hival
     c     mabdl7_key    setll     mabdl7
     c                   when      w_seqe = 'B'
     c                   eval      mabdl1_vare = *blank
     c     mabdl1_key    setll     mabdl1
     c                   other
     c                   eval      mabdl5_abpr = *hival
     c     mabdl5_key    setll     mabdl5
     c                   endsl
      *
     c     end_h1win     endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * xh2win  Bilde for forklaringer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xh2win        begsr
      *
     c     h2taga        tag
      *
     c                   write     h2win
     c                   exfmt     h2win
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc
     c                   goto      end_h2win
     c                   endsl
      *
     c     end_h2win     endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
     c                   eval      w_akkp = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'D'
     c                   read      mabdl3
     c                   when      w_seqe = 'C'
     c                   read      mabdl7
     c                   when      w_seqe = 'B'
     c                   read      mabdl1
     c                   other
     c                   read      mabdl5
     c                   endsl
      *
     c                   if        %eof or
     c                             mbfirm <> w_firm or
     c                             mblagk <> w_lagk or
     c                             mbvsor <> w_vsor or
     c                             mbogrp <> w_ogrp or
     c                             mbhgrp <> w_hgrp or
     c                             mbugrp <> w_ugrp
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
      * Uaktuell abc-kode ?
      *
     c                   move      mbkabc        w_xyz
     c                   movel     mbkabc        w_abc
     c                   if        h1kabc <> *blank
     c                   eval      b_abck = *off
      * bare xyz
     c                   if        d_kabc = *blank
     c                   if        d_kxyz <> w_xyz
     c                   iter
     c                   else
     c                   eval      b_abck = *on
     c                   endif
     c                   endif
      * bare abc
     c                   if        b_abck = *off
     c                   if        d_kxyz = *blank
     c                   if        d_kabc <> w_abc
     c                   iter
     c                   else
     c                   eval      b_abck = *on
     c                   endif
     c                   endif
     c                   endif
      * både og
     c                   if        b_abck = *off
     c                   if        h1kabc <> mbkabc
     c                   iter
     c                   else
     c                   eval      b_abck = *on
     c                   endif
     c                   endif
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'D'
     c                   eval      mabdl3_kabc = mbkabc
     c                   when      w_seqe = 'C'
     c                   eval      mabdl7_omse = mbomse
     c                   when      w_seqe = 'B'
     c                   eval      mabdl1_vare = mbvare
     c                   other
     c                   eval      mabdl5_abpr = mbabpr
     c                   endsl
      *
     c                   movel     mbvare        vvarl1_vare
     c     vvarl1_key    chain     vvarl1                             91
     c                   if        *in91 = *off
     c                   movel     vvtek1        b1navn
     c                   else
     c                   eval      b1navn = 'Ukjent vare'
     c                   endif
5.60 c                   eval      w_labc = vvlabc
5.60 c                   if        w_lagk <> *blank
5.60 c                   movel     mbvare        vlagl1_vare
5.60 c                   movel     w_lagk        vlagl1_lage
5.60 c     vlagl1_key    chain     vlagl1
5.60 c                   eval      w_labc = vllabc
5.60 c                   endif
      *
      *
     c                   eval      b1valg = 0
     c                   eval      b1kabc = mbkabc
     c                   eval      b1vare = mbvare
     c                   eval      b1text = *blank
     c                   if        *in51  = *off
     c                   eval      b1text = %subst(b1vare:1:8) + ' ' +
     c                                      %subst(b1navn:1:28)
     c                   else
     c                   movel     b1navn        b1text
     c                   endif
     c                   eval      b1abpr = mbabpr
     c                   eval      b1enhe = mbenhe
     c                   eval      b1felt = *blank
     c                   if        *in51 = *off
     c                   eval      b1felt = %editw(b1abpr:'   ,   -')
     c                   eval      b1omse = mbomse
     c                   else
     c                   movel     b1enhe        b1felt
     c                   eval      b1omse = mbanta
     c                   endif
     c                   eval(h)   b1abpr = mbabpr
     c                   if        *in50  = *off
     c                   eval(h)   b1akkp = mbabap
     c                   else
     c                   eval      b1akkp = 0
     c                   endif
     c                   if        w_labc = 1
     c                   eval      b1labc = '*'
     c                   else
     c                   eval      b1labc = *blank
     c                   endif
     c                   if        mbeabc <> *blank and
     c                             mbeabc <> mboabc
     c                   eval      b1oabc = mboabc
     c                   else
     c                   eval      b1oabc = *blank
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'D'
     c     mabdl3_key    setgt     mabdl3
     c                   readp     mabdl3
     c                   when      w_seqe = 'B'
     c     mabdl1_key    setgt     mabdl1
     c                   readp     mabdl1
     c                   when      w_seqe = 'C'
     c     mabdl7_key    setgt     mabdl7
     c                   readp     mabdl7
     c                   other
     c     mabdl5_key    setgt     mabdl5
     c                   readp     mabdl5
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'D'
     c                   readp     mabdl3
     c                   when      w_seqe = 'B'
     c                   readp     mabdl1
     c                   when      w_seqe = 'C'
     c                   readp     mabdl7
     c                   other
     c                   readp     mabdl5
     c                   endsl
      *
     c                   if        %eof or
     c                             mbfirm <> w_firm or
     c                             mblagk <> w_lagk or
     c                             mbvsor <> w_vsor or
     c                             mbogrp <> w_ogrp or
     c                             mbhgrp <> w_hgrp or
     c                             mbugrp <> w_ugrp
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'D'
     c                   eval      mabdl3_kabc = mbkabc
     c                   when      w_seqe = 'B'
     c                   eval      mabdl1_vare = mbvare
     c                   when      w_seqe = 'C'
     c                   eval      mabdl7_omse = mbomse
     c                   other
     c                   eval      mabdl5_abpr = mbabpr
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'D'
     c     mabdl3_key    setll     mabdl3
     c                   when      w_seqe = 'C'
     c     mabdl7_key    setll     mabdl7
     c                   when      w_seqe = 'B'
     c     mabdl1_key    setll     mabdl1
     c                   other
     c     mabdl5_key    setll     mabdl5
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Finne prisen på en vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_pris     begsr
      *
      *
     c                   eval      w_lety = 0
6.20 c                   eval      w_kamp = *blank
     c                   time                    w_dato
      *
     c                   call      'VP750R'
     c                   parm                    w_firm
     c                   parm                    mbvare
5.70 c                   parm                    w_prgr
     c                   parm                    w_lety
     c                   parm                    w_dato
     c                   parm                    mbenhe
     c                   parm                    w_kopr
     c                   parm                    w_inpr
6.20 c                   parm                    w_kamp
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
     c                   if        w_afld = 'B2VARE'
     c                   movel     b2vare        w_vares
     c                   call      'VV500R  '
     c                   parm                    w_firm
     c                   parm                    w_vares
     c                   parm                    w_tek1
     c                   movel     w_vares       b2vare
     c                   goto      end_spØrr
     c                   endif
      *
     c     end_spØrr     endsr
      *
5.70  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.70  *  Subrutine for å hente prisgruppe
5.70  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.70 c     hent_prisgr   begsr
5.70  *
5.70 c                   movel     w_lagk        w_lage
5.70 c                   eval      w_avde = 0
5.70 c                   call      'VL712R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    w_lage
5.70 c                   parm                    w_avde
5.70 c                   parm                    w_prgr
5.70  *
5.70 c                   endsr
5.63  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.63  * Subrutine for å oppatere abc-kode på prognose
5.63  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.63  *
5.63 c     upd_prog_abc  begsr
5.63  *
5.63 c                   call      'MM747R    '
5.63 c                   parm                    vlfirm
5.63 c                   parm                    vlvare
5.63 c                   parm                    vllage
5.63 c                   parm                    vlkabc
5.63  *
5.63 c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     C     *ENTRY        PLIST
     C                   PARM                    p_lagk
     C                   PARM                    p_vsor
     C                   PARM                    p_ogrp
     C                   PARM                    p_hgrp
     C                   PARM                    p_ugrp
     C                   PARM                    p_valg
      *
      * Key for les av abc hode
      *
     c     mabclr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vsor
     c                   kfld                    w_ogrp
     c                   kfld                    w_hgrp
     c                   kfld                    w_ugrp
     c                   kfld                    w_lagk
      *
      * Key for posisjonering på vare
      *
     c     mabdl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_lagk
     c                   kfld                    w_vsor
     c                   kfld                    w_ogrp
     c                   kfld                    w_hgrp
     c                   kfld                    w_ugrp
     c                   kfld                    mabdl1_vare
      *
      * Key for posisjonering på omsetning
      *
     c     mabdl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_lagk
     c                   kfld                    w_vsor
     c                   kfld                    w_ogrp
     c                   kfld                    w_hgrp
     c                   kfld                    w_ugrp
     c                   kfld                    mabdl3_kabc
      *
      * Key for posisjonering på prosent
      *
     c     mabdl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_lagk
     c                   kfld                    w_vsor
     c                   kfld                    w_ogrp
     c                   kfld                    w_hgrp
     c                   kfld                    w_ugrp
     c                   kfld                    mabdl5_abpr
      *
      * Key for posisjonering på omsetning
      *
     c     mabdl7_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_lagk
     c                   kfld                    w_vsor
     c                   kfld                    w_ogrp
     c                   kfld                    w_hgrp
     c                   kfld                    w_ugrp
     c                   kfld                    mabdl7_omse
      *
      * Key for oppdatering av abc-detaljer
      *
     c     mabdlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_lagk
     c                   kfld                    w_vsor
     c                   kfld                    w_ogrp
     c                   kfld                    w_hgrp
     c                   kfld                    w_ugrp
     c                   kfld                    mabdlu_vare
      *
      * Key for les av abc-detaljer
      *
     c     mabdlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_lagk
     c                   kfld                    w_vsor
     c                   kfld                    w_ogrp
     c                   kfld                    w_hgrp
     c                   kfld                    w_ugrp
     c                   kfld                    mabdlr_vare
      *
      * Key for les av vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for upd av vare
      *
     c     vvarlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlu_vare
5.60  *
5.60  * Key for upd av lager
5.60  *
5.60 c     vlaglu_key    klist
5.60 c                   kfld                    w_firm
5.60 c                   kfld                    vlaglu_vare
5.60 c                   kfld                    vlaglu_lage
5.60  *
5.60  * Key for les av lager
5.60  *
5.60 c     vlagl1_key    klist
5.60 c                   kfld                    w_firm
5.60 c                   kfld                    vlagl1_vare
5.60 c                   kfld                    vlagl1_lage
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_lagk = p_lagk
     c                   eval      w_vsor = p_vsor
     c                   eval      w_ogrp = p_ogrp
     c                   eval      w_hgrp = p_hgrp
     c                   eval      w_ugrp = p_ugrp
     c                   eval      w_kabc = p_kabc
     c                   eval      b2kabc = p_kabc
     c                   eval      b2lagk = p_lagk
     c                   eval      b2ogrp = p_ogrp
     c                   eval      b2hgrp = p_hgrp
     c                   eval      b2ugrp = p_ugrp
      *
     c     mabclr_key    chain     mabclr
     c                   eval      b2abap  = maabap
     c                   eval      b2abbp  = maabbp
     c                   eval      b2abcp  = maabcp
     c                   eval      b2xyxp  = maxyxp
     c                   eval      b2xyyp  = maxyyp
     c                   eval      b2xyzp  = maxyzp
      *
5.70 c                   exsr      hent_prisgr
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      mabdl5_abpr = 999,9999999
     c     mabdl5_key    setll     mabdl5
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
