     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : NN830R
      * Beskrivelse . . : Nettbutikk, ordreinngang
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       071002 HKO  Nytt program
5.10  *       250903 MHA  Legge til felt for Fra-system
      *       110504 HKO  Defaulter Fra-system til NBU dersom dette ikke
      *                   er oppgitt
      * 6.10  250609 BSA  Oppdatert med sporingsinformasjon
6.20  * 6.20  040411 NHO  Kun brukere som har samme avd. som kunden på
      *                   nettordren skal ha mail.. ellers som før
6.21  * 6.20  251112 bof  Rettelse av 6.20
6.nu  * 6.30  170614 bof  Endring i hht. nummerutvidelse
6.30  * 6.30  270117 bof  Test på leveringsdato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fnohel6    if   e           k disk    rename(nohepfr:nohel6r)
     fnohelu    uf a e           k disk    rename(nohepfr:nohelur)
     fnodtlu    uf a e           k disk    rename(nodtpfr:nodtlur)
     ffusrl1    if   e           k disk    rename(fusrpfr:fusrl1r)
     fnkeol1    if   e           k disk    rename(nkeopfr:nkeol1r)
6.20 frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
6.20 fausrl1    if   e           k disk    rename(ausrpfr:ausrl1r)
      *
      * Definering av parametre
      *
     d p_uniq          s             15
     d p_dtqi          s             10
     d p_dtqo          s             10
     d p_bibl          s             10
     d p_feil          s              1
     d p_firm          s              3  0
     d p_lage          s              2
     d p_avde          s              4  0
     d p_inpl          s              5p 0
     d p_outl          s              5p 0
     d p_wait          s              5p 0
     d p_keyo          s              2
     d p_keyl          s              3p 0
     d p_sndl          s              3p 0
     d p_sndi          s             36
      *
      * Definering av LDA
      *
     d                uds
6.10 d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av datastrukturer
      *
      * Datakø - inn
      *
     d                 ds
     d d_inpu                      1024
     d  d_rect                        1    overlay(d_inpu:1)
      *
      * Datakø - inn (ordre-hode)
      *
     d                 ds
5.10 d d_hode                       658
     d  d_nref                        6    overlay(d_hode:  2)
     d  d_kund                        6    overlay(d_hode:  8)
     d  d_knav                       35    overlay(d_hode: 14)
     d  d_kpro                        6    overlay(d_hode: 49)
     d  d_pnav                       35    overlay(d_hode: 55)
     d  d_navn                       30    overlay(d_hode: 90)
     d  d_adr1                       30    overlay(d_hode:120)
     d  d_adr2                       30    overlay(d_hode:150)
     d  d_ponr                       15    overlay(d_hode:180)
     d  d_sted                       30    overlay(d_hode:195)
     d  d_hlet                        1    overlay(d_hode:225)
     d  d_hlda                        8    overlay(d_hode:226)
     d  d_dref                       20    overlay(d_hode:234)
     d  d_rekv                       20    overlay(d_hode:254)
     d  d_unav                       30    overlay(d_hode:274)
     d  d_utlf                       15    overlay(d_hode:304)
     d  d_umob                       15    overlay(d_hode:319)
     d  d_ufax                       15    overlay(d_hode:334)
     d  d_umai                       50    overlay(d_hode:349)
     d  d_hmld                      256    overlay(d_hode:399)
5.10 d  d_frsp                        1    overlay(d_hode:655)
5.10 d  d_fsys                        3    overlay(d_hode:656)
      *
      * Datakø - inn (ordre-linje)
      *
     d                 ds
     d d_linj                       150
     d  d_vare                        8    overlay(d_linj: 2)
     d  d_tek1                       35    overlay(d_linj:10)
     d  d_enhe                        3    overlay(d_linj:45)
     d  d_anta                       13    overlay(d_linj:48)
     d  d_ntpr                       11    overlay(d_linj:61)
     d  d_llet                        1    overlay(d_linj:72)
     d  d_llda                        8    overlay(d_linj:73)
     d  d_lmld                       70    overlay(d_linj:81)
      *
      * Definering av variable i nøkler
      *
6.20 d ausrl1_user     s                   like(abuser)
6.20 d rkunl1_kund     s                   like(rkkund)
     d nohel6_fsys     s                   like(nofsys)
     d nohel6_nref     s                   like(nonref)
     d nkeol1_kode     s                   like(nkkode)
      *
      * Definering av variable
      *
     d b_nega          s              1
      *
     d w_dato_6        s              6  0
     d w_posi          s              2  0
     d w_anta          s             11
     d w_ntpr          s              9
      *
     d w_firm          s                   like(nofirm)
     d w_line          s                   like(ndline)
6.20 d w_avde          s                   like(noavde)
      *
     d w_tkod          s              1    inz('0')
     d w_fell          s              6
     d w_ttyp          s              2    inz(*blank)
     d w_fast          s             10    inz(*blank)
6.nu d w_numm          s              8  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser og behandler datakø (input)
      *
     c                   dou       d_inpu = *blank
      *
     c                   eval      d_inpu = *blank
      *
     c                   call      'QRCVDTAQ'
     c                   parm                    p_dtqi
     c                   parm                    p_bibl
     c                   parm                    p_inpl
     c                   parm                    d_inpu
     c                   parm                    p_wait
     c                   parm                    p_keyo
     c                   parm                    p_keyl
     c                   parm                    p_uniq
     c                   parm                    p_sndl
     c                   parm                    p_sndi
      *
     c                   if        d_inpu <> *blank
     c                   exsr      behandling
     c                   endif
      *
     c                   enddo
      *
      * Sender melding til ordrebehandlere
      *
     c                   exsr      send_melding
      *
      * Avslutt
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av ordreinngang
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
     c                   select
     c                   when      d_rect = '1'
     c                   exsr      behandl_hode
     c                   when      d_rect = '2'
     c                   exsr      behandl_linje
     c                   endsl
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av ordre-hode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandl_hode  begsr
      *
     c                   eval      d_hode = d_inpu
      *
     c                   if        d_fsys = *blank
     c                   eval      d_fsys = 'NBU'
     c                   endif
      *
      * Avryter dersom referanse finnes fra før
      *
     c                   move      d_nref        nohel6_nref
     c                   eval      nohel6_fsys = d_fsys
     c     nohel6_key    chain     nohel6
     c                   if        %found
     c                   eval      p_feil = '2'
     c                   goto      avslutt
     c                   endif
      *
      * Initierer linjenummer
      *
     c                   eval      w_line = 0
      *
      * Skriver til ekstern ordre-hode
      *
     c                   eval      nofirm = w_firm
5.10 c                   eval      nofsys = %trim(d_fsys)
5.10 c                   eval      notell = 0
     c                   eval      nonref = *blank
     c                   eval      nofrsp = 0
     c                   eval      nolage = 0
     c                   eval      noavde = 0
     c                   eval      nouser = *blank
     c                   eval      nokund = 0
     c                   eval      noknav = %trim(d_knav)
     c                   eval      nokpro = 0
     c                   eval      nopnav = %trim(d_pnav)
     c                   eval      noadr1 = %trim(d_adr1)
     c                   eval      noadr2 = %trim(d_adr2)
     c                   eval      noponr = %trim(d_ponr)
     c                   eval      nosted = %trim(d_sted)
     c                   eval      nolety = 0
     c                   eval      noldat = *loval
     c                   eval      nodref = %trim(d_dref)
     c                   eval      norekv = %trim(d_rekv)
     c                   eval      nomeld = %trim(d_hmld)
     c                   eval      nounav = %trim(d_unav)
     c                   eval      noutlf = %trim(d_utlf)
     c                   eval      noumob = %trim(d_umob)
     c                   eval      noufax = %trim(d_ufax)
     c                   eval      noumai = %trim(d_umai)
     c                   eval      nonumm = 0
     c                   eval      noeusr = *blank
      *
     c                   time                    nobdat
     c                   time                    noodat
     c                   time                    nootim
     c                   time                    noedat
     c                   time                    noetim
      *
     c                   move      d_nref        nonref
     c                   move      p_lage        nolage
     c                   move      p_avde        noavde
     c                   move      d_kund        nokund
     c                   move      d_kpro        nokpro
     c                   move      d_hlet        nolety
      *
     c                   if        nokpro <> 0
     c                   eval      nonavn = nopnav
     c                   else
     c                   eval      nonavn = noadr1
     c                   eval      noadr1 = noadr2
     c                   eval      noadr2 = *blank
     c                   endif
      *
     c                   if        d_hlda <> *blank
     c                   move      d_hlda        w_dato_6
6.30 c     *dmy          test(d)                 w_dato_6               38
6.30 c                   if        *in38 = *on
6.30 c                   time                    noldat
6.30 c                   else
     c     *ymd          move      w_dato_6      noldat
6.30 c                   endif
     c                   endif
      *
      * Henter neste nummer fra nummer-serie
      *
     c                   eval      nkeol1_kode = %trim(d_fsys)
     c     nkeol1_key    chain     nkeol1r
     c                   if        %found
     c                   eval      w_fell = %trim(nkteln)
     c                   else
     c                   eval      w_fell = 'FNETTB'
     c                   endif
      *
     c                   call      'AS100R'
     c                   parm                    w_tkod
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_ttyp
     c                   parm                    w_fast
     c                   parm                    w_numm
      *
     c                   eval      notell = w_numm
      *
6.20  * Sjekk om kunde har avdeling
6.20 c                   eval      rkunl1_kund = nokund
6.20 c     rkunl1_key    chain     rkunl1r
6.20  *
6.20 c                   if        %found and
6.20 c                             rkavde <> *zero
6.20 c                   eval      w_avde = rkavde
6.20 c                   eval      *in32 = '1'
6.20 c                   else
6.20 c                   eval      w_avde = *zero
6.20 c                   eval      *in32 = '0'
6.20 c                   endif
      * Skriver til fil
      *
6.10 c                   time                    noodat
6.10 c                   time                    nootim
6.10 c                   eval      noousr = l_user
6.10 c                   time                    noedat
6.10 c                   time                    noetim
6.10 c                   eval      noeusr = l_user
     c                   write     nohelur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av ordre-linje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandl_linje begsr
      *
     c                   eval      d_linj = d_inpu
      *
      * Skriver til ekstern ordre-linje
      *
     c                   eval      w_line = w_line + 1
      *
     c                   eval      ndfirm = w_firm
     c                   eval      ndfsys = nofsys
     c                   eval      ndtell = notell
     c                   eval      ndnref = nonref
     c                   eval      ndline = w_line
     c                   eval      ndvare = d_vare
     c                   eval      ndtek1 = %trim(d_tek1)
     c                   eval      ndtek2 = *blank
     c                   eval      ndanta = 0
     c                   eval      ndenhe = %trim(d_enhe)
     c                   eval      ndntpr = 0
     c                   eval      ndlety = 0
     c                   eval      ndldat = *loval
      *
     c                   time                    ndodat
     c                   time                    ndedat
     c                   time                    ndetim
      *
     c                   move      d_llet        ndlety
      *
     c                   if        d_llda <> *blank
     C                   move      d_llda        w_dato_6
     c     *ymd          move      w_dato_6      ndldat
     c                   endif
      *
      * Utleder antall og pris
      *
     c                   eval      w_posi = %scan('-':d_anta)
     c                   if        w_posi > 0
     c                   eval      b_nega = *on
     c                   else
     c                   eval      b_nega = *off
     c                   endif
      *
     c     '-':'0'       xlate     d_anta        d_anta
     c     ' ':'0'       xlate     d_anta        d_anta
     c     '-':'0'       xlate     d_ntpr        d_ntpr
     c     ' ':'0'       xlate     d_ntpr        d_ntpr
      *
     c                   eval      w_posi = %scan(',':d_anta)
     c                   if        w_posi <> 0
     c                   eval      w_anta = %subst(d_anta:2:w_posi-2) +
     c                                      %subst(d_anta:w_posi+1:13-w_posi)
     c                   move      w_anta        ndanta
     c                   else
     c                   move      d_anta        ndanta
     c                   endif
     c                   select
     c                   when      w_posi = 0
     c                   eval      ndanta = ndanta * 1000
     c                   when      w_posi = 12
     c                   eval      ndanta = ndanta * 100
     c                   when      w_posi = 11
     c                   eval      ndanta = ndanta * 10
     c                   endsl
      *
     c                   eval      w_posi = %scan(',':d_ntpr)
     c                   if        w_posi <> 0
     c                   eval      w_ntpr = %subst(d_ntpr:2:w_posi-2) +
     c                                      %subst(d_ntpr:w_posi+1:11-w_posi)
     c                   move      w_ntpr        ndntpr
     c                   else
     c                   move      d_ntpr        ndntpr
     c                   endif
     c                   select
     c                   when      w_posi = 0
     c                   eval      ndntpr = ndntpr * 100
     c                   when      w_posi = 10
     c                   eval      ndntpr = ndntpr * 10
     c                   endsl
      *
     c                   if        b_nega = *on
     c                   eval      ndanta = ndanta * -1
     c                   eval      ndntpr = ndntpr * -1
     c                   endif
      *
6.10 c                   time                    ndodat
6.10 c                   time                    ndotim
6.10 c                   eval      ndousr = l_user
6.10 c                   time                    ndedat
6.10 c                   time                    ndetim
6.10 c                   eval      ndeusr = l_user
     c                   write     nodtlur
      *
      * Dersom melding, legges denne ut som egen linje
      *
     c                   if        d_lmld <> *blank
      *
     c                   eval      d_lmld = %trim(d_lmld)
      *
     c                   eval      w_line = w_line + 1
      *
     c                   eval      d_lmld = %trim(d_lmld)
      *
     c                   eval      ndline = w_line
     c                   eval      ndvare = *blank
     c                   eval      ndtek1 = %subst(d_lmld:1:35)
     c                   eval      ndtek2 = %subst(d_lmld:36:35)
     c                   eval      ndanta = 0
     c                   eval      ndenhe = *blank
     c                   eval      ndntpr = 0
     c                   eval      ndlety = 0
     c                   eval      ndldat = *loval
      *
     c                   time                    ndedat
     c                   time                    ndetim
6.10 c                   eval      ndeusr = l_user
      *
     c                   time                    ndodat
6.10 c                   time                    ndotim
6.10 c                   eval      ndousr = l_user
     c                   write     nodtlur
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sende melding til ordrebehandlere
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     send_melding  begsr
      *
     c     fusrl1_key    setll     fusrl1
     c     fusrl1_key    reade     fusrl1
     c                   dow       not %eof
      *
6.20  * Sjekk om bruker har samme avdeling som kunde på nettordre
6.20 c                   if        *in32 = '1'
6.21 c                   eval      ausrl1_user = fbuser
6.20 c     ausrl1_key    chain     ausrl1
6.20 c                   if        %found and
6.20 c                             absavd <> w_avde
6.20 c                   goto      nyles
6.20 c                   endif
6.20 c                   endif
6.20  *
     c                   if        fbko10 = 1
     c                   call      'NN821C'
     c                   parm                    fbuser
     c                   endif
      *
6.20 c     nyles         tag
6.20  *
     c     fusrl1_key    reade     fusrl1
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ERROR-rutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *pssr         begsr
      *
     c                   eval      p_feil = '1'
     c                   goto      avslutt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_uniq
     c                   parm                    p_dtqi
     c                   parm                    p_dtqo
     c                   parm                    p_bibl
     c                   parm                    p_feil
     c                   parm                    p_firm
     c                   parm                    p_lage
     c                   parm                    p_avde
     c                   parm                    p_inpl
     c                   parm                    p_outl
     c                   parm                    p_wait
     c                   parm                    p_keyo
     c                   parm                    p_keyl
     c                   parm                    p_sndl
     c                   parm                    p_sndi
      *
6.20  * Key for file : GPL-BRUKER-REGISTER
6.20  *
6.20 c     ausrl1_key    klist
6.20 c                   kfld                    ausrl1_user
6.20  *
6.20  * Key for file : KUNDE-REGISTER
6.20  *
6.20 c     rkunl1_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på ekstern ordre-hode
      *
     c     nohel6_key    klist
     c                   kfld                    w_firm
     c                   kfld                    nohel6_fsys
     c                   kfld                    nohel6_nref
      *
      * Key for oppslag på ekstern ordre-kode
      *
     c     nkeol1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    nkeol1_kode
      *
      * Key for oppslag på ofak-bruker
      *
     c     fusrl1_key    klist
     c                   kfld                    w_firm
      *
      *
      * Hent firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
