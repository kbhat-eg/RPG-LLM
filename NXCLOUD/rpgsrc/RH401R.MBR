     H datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * System. . . . . : ASOKON
      * Program . . . . : RH401R
      * Beskrivelse . . : Hovedbok - sletting av poster
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
T5.40 * V5.40 170106 HFL  Slettet ikke annet enn firma 000
      * V5.40 140306 KOB  Lagt inn sletting av poster i RZUMPF
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frhtrl1    if   e           k disk    rename(rhtrpfr:rhtrl1r)
     frhtrlu    uf a e           k disk    rename(rhtrpfr:rhtrlur)
     frzuml1    if   e           k disk    rename(rzumpfr:rzuml1r)
     frzumlu    uf a e           k disk    rename(rzumpfr:rzumlur)
      *
     d rhtrlu_raar     s                   like(rbraar)
     d rhtrlu_kont     s                   like(rbkont)
     d rhtrlu_spes     s                   like(rbspes)
     d rhtrlu_avde     s                   like(rbavde)
     d rhtrlu_rper     s                   like(rbrper)
     d rhtrlu_bdat     s                   like(rbbdat)
     d rhtrlu_tist     s                   like(rbtist)
      *
     d rzumlu_raar     s                   like(rzraar)
     d rzumlu_biln     s                   like(rzbiln)
     d rzumlu_bdat     s                   like(rzbdat)
     d rzumlu_tell     s                   like(rztell)
      *
      * Definering av Local data area
      *
     d                uds
     d d_raar                300    303  0
     d d_valg                         1
      *
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d w_firm          s                   like(rbfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sletting av poster
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser hovedboksposter
      *
     c                   eval      w_firm = l_firm
      *
     c     rhtrl1_key    setll     rhtrl1r                                90
      *
     c     rhtrl1_les    tag
      *
     c                   read      rhtrl1r                                90
      *
T5.40c                   if        *in90  = *on or
T5.40c                             rbfirm >  l_firm
T5.40c                   goto      zum
T5.40c                   endif
      *
     c                   if        rbraar <  d_raar
      *
     c                   eval      rhtrlu_raar = rbraar
     c                   eval      rhtrlu_kont = rbkont
     c                   eval      rhtrlu_spes = rbspes
     c                   eval      rhtrlu_avde = rbavde
     c                   eval      rhtrlu_rper = rbrper
     c                   eval      rhtrlu_bdat = rbbdat
     c                   eval      rhtrlu_tist = rbtist
     c     rhtrlu_key    chain     rhtrlur
      *
     c                   if        %found(rhtrlu)
     c                   delete    rhtrlur
     c                   endif
     c                   endif
      *
     c                   goto      rhtrl1_les
      *
     c     zum           tag
      *
      * Leser sumrecords
      *
     c     rzuml1_key    setll     rzuml1r                                91
      *
     c     rzuml1_les    tag
      *
     c                   read      rzuml1r                                91
      *
T5.40c                   if        *in91  = *on or
T5.40c                             rzfirm >  l_firm
T5.40c                   goto      avslutt
T5.40c                   endif
      *
     c                   if        rzraar <  d_raar
      *
     c                   eval      rzumlu_raar = rzraar
     c                   eval      rzumlu_biln = rzbiln
     c                   eval      rzumlu_bdat = rzbdat
     c                   eval      rzumlu_tell = rztell
     c     rzumlu_key    chain     rzumlur
      *
     c                   if        %found(rzumlu)
     c                   delete    rzumlur
     c                   endif
     c                   endif
      *
     c                   goto      rzuml1_les
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for hovedboksposter
      *
     c     rhtrl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for hovedboksposter
      *
     c     rhtrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhtrlu_raar
     c                   kfld                    rhtrlu_kont
     c                   kfld                    rhtrlu_spes
     c                   kfld                    rhtrlu_avde
     c                   kfld                    rhtrlu_rper
     c                   kfld                    rhtrlu_bdat
     c                   kfld                    rhtrlu_tist
      *
      * Key for sumposter
      *
     c     rzuml1_key    klist
     c                   kfld                    w_firm
      *
      * Key for hovedboksposter
      *
     c     rzumlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rzumlu_raar
     c                   kfld                    rzumlu_biln
     c                   kfld                    rzumlu_bdat
     c                   kfld                    rzumlu_tell
      *
     c                   endsr
