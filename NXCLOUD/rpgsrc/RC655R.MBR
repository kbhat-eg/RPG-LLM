     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RC655R                                              *
      * Beskrivelse . . : Inkasso - overføring til samlefil/faktura           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
T3.30 * R3.30      100902 Skriver ikke 99 og XX-rec. dersom det ikke er       *
T3.30 *                   noen poster i oppdraget.                            *
T5.40 * R5.40      020206 Avdeling legges ut etter firma (som i v3.30)   HFL  *
T5.40 *                   Endring også i datastruktur RC01PFDS           HFL  *
SIN   * R5.40      080306 Spes. Sønnico: Skal aldri skrive XX-record     HFL  *
SIN   * R5.40      220306 Skriver ikke tomme 99-records                  HFL  *
      * R5.60      021108 Endret til bruk av ny timestamp på RKWBPF      KOB  *
6.11  * R6.10      210912 Lagt inn datastrukturer for filer direkte      ASK  *
      *                   inn i programmet. Fjernet tekst på fakturaref       *
6.12  * R6.10      140113 Lagt inn kontroll om saldo har blitt 0 eller   ASK  *
      *                   neg. etter kjørt forslag. Unngår tomme poster       *
6.13  * R6.20      090514 Lagt inn varsel om poster er dannet            ASK  *
6.nu  * R6.30      190615 Endringer mtp nummerutvidelser                 bsa  *
9.01  * R6.30  sst 160119 Konv. av Æ,Ø og Å Ikke fullført enda                *
9.02  * R6.30  sst 250119 Fjernet 'Konv. av Æ,Ø og Å Ikke fullført enda'      *
7.00  * R6.30  sst 050419 Legge ut '.' i pos 80 for Collectio          '      *
7.01  * R7.00  sst 171019 Ikke legge ut rentepros, på kredit poster Collectio *
7.nx  * K000   bsa 171120 Endringer reltart til One Nexstep                   *
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frkwbpf    if   e           k disk    rename(rkwbpfr:rkwbpfr)
     frkwapf    if   e           k disk
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     fapospf    if   e           k disk
     frca0lu    uf a e           k disk    rename(rca0pfr:rca0lur)
     frktrlu    uf   e           k disk    rename(rktrpfr:rktrlur)
     frkwblu    uf   e           k disk    rename(rkwbpfr:rkwblur)
     frcnfpf    o  a e           k disk
     frcnspf    o  a e           k disk
      *
     d                uds
      *
     d  pfirm                300    302  0
     d  pnavn                303    332
     d  puser                333    342
     d  pkdat                343    348  0
     d  praar                349    352  0
     d  prper                353    354  0
     d  pavdf                355    358  0
     d  pavdt                359    362  0
     d  pkatf                363    366  0
     d  pkatt                367    370  0
     d  pself                371    374  0
     d  pselt                375    378  0
     d  pknrf                379    384  0
     d  pknrt                385    390  0
     d  kun                  391    450  0 dim(10)
     d  pknr1                391    396  0
     d  pknr2                397    402  0
     d  pknr3                403    408  0
     d  pknr4                409    414  0
     d  pknr5                415    420  0
     d  pknr6                421    426  0
     d  pknr7                427    432  0
     d  pknr8                433    438  0
     d  pknr9                439    444  0
     d  pknr0                445    450  0
     d  pfdat                451    456  0
     d  plope                457    459  0
     d  ppkod                460    460
     d  ppros                461    463  1
     d  pgrns                464    465  0
     d  pgbyr                466    466
     d  pmini                467    468  0
     d  pbskr                469    469
     d  pegrn                470    470
     d  pajor                471    471
     d  ptext                472    472  0
     d  ppunr                473    473  0
     d  ppbeh                474    474
     d  palfa                475    475
     d  pdfgg                476    481  0
     d  pparam               300    490
      *
     d lo              c                   ','
     d up              c                   ' '
      *
     d  l_user               911    920
     d  l_fgrp               931    933
     d  l_firm               944    946  0
     d  l_fnav               951    980
      *
     d w_stat          s              1
     d w_gkod          s              4  0
     d w_gtxt          s             20
6.13 d w_apos          s              1  0
6.11  *
 .    * Forsendelse - Type 01 : Kundeopplysninger (Startrecord for oppdrag)
 .    *
 .   d                 ds
 .   d rc01ld                        80
 .   d  rc01ty                        2    overlay(rc01ld)
 .   d  rc01kn                       12    overlay(rc01ld:3)
 .   d  rc01op                        1    overlay(rc01ld:15)
 .   d  rc01t1                       20    overlay(rc01ld:16)
 .   d   rc01fg                       3    overlay(rc01t1)
 .   d   rc01fi                       3  0 overlay(rc01t1:4)
 .   d   rc01av                       4  0 overlay(rc01t1:7)
 .   d  rc01t2                       20    overlay(rc01ld:36)
7.00 d  rc01end                       1    overlay(rc01ld:80)
 .    *
 .    * Forsendelse - Type 02 : Skyldner, diverse opplysninger
 .    *
 .   d                 ds
 .   d rc02ld                        80
 .   d  rc02ty                        2    overlay(rc02ld)
 .   d  rc02pf                       11    overlay(rc02ld:3)
 .   d  rc02rf                       20    overlay(rc02ld:14)
 .   d   rc02id                       4    overlay(rc02rf)
 .   d   rc02fg                       3    overlay(rc02rf:5)
 .   d   rc02fi                       3  0 overlay(rc02rf:8)
 .   d   rc02ku                       6  0 overlay(rc02rf:11)
 .   d   rc02lØ                       4    overlay(rc02rf:17)
 .   d  rc02bk                       11    overlay(rc02ld:34)
 .   d  rc02da                        8    overlay(rc02ld:45)
7.00 d  rc02end                       1    overlay(rc02ld:80)
 .    *
 .    * Forsendelse - Type 03 : Skyldner, navneopplysninger
 .    *
 .   d                 ds
 .   d rc03ld                        80
 .   d  rc03ty                        2    overlay(rc03ld)
 .   d  rc03na                       36    overlay(rc03ld:3)
 .   d  rc03n2                       36    overlay(rc03ld:39)
 .   d  rc03pf                        1    overlay(rc03ld:75)
7.00 d  rc03end                       1    overlay(rc03ld:80)
 .    *
 .    * Forsendelse - Type 04 : Skyldner, adresseopplysninger
 .    *
 .   d                 ds
 .   d rc04ld                        80
 .   d  rc04ty                        2    overlay(rc04ld)
 .   d  rc04ad                       30    overlay(rc04ld:3)
 .   d  rc04st                       24    overlay(rc04ld:33)
7.00 d  rc04end                       1    overlay(rc04ld:80)
 .    *
 .    * Forsendelse - Type 07 : Skyldner, adresseopplysninger
 .    *
 .   d                 ds
 .   d rc07ld                        80
 .   d  rc07ty                        2    overlay(rc07ld)
 .   d  rc07pn                        5    overlay(rc07ld:3)
 .   d  rc07ps                       24    overlay(rc07ld:8)
 .   d  rc07lk                        2    overlay(rc07ld:32)
 .   d  rc07tp                       14    overlay(rc07ld:34)
 .   d  rc07tj                       14    overlay(rc07ld:48)
 .   d  rc07ta                       14    overlay(rc07ld:62)
7.00 d  rc07end                       1    overlay(rc07ld:80)
 .    *
 .    * Forsendelse - Type 08 : Fakturaer
 .    *
 .   d                 ds
 .   d rc08ld                        80
 .   d  rc08ty                        2    overlay(rc08ld)
 .   d  rc08fd                        8    overlay(rc08ld:3)
 .   d  rc08ff                        8    overlay(rc08ld:11)
 .   d  rc08rs                        4  2 overlay(rc08ld:19)
 .   d  rc08tp                       10  2 overlay(rc08ld:23)
 .   d   rc08tf                       1    overlay(rc08tp)
6.nu d  rc08fn                       22    overlay(rc08ld:33)
6.nu d   rc08bn                       8    overlay(rc08fn)
6.nu d   rc08bl                       1    overlay(rc08fn:9)
6.nu d   rc08tx                      13    overlay(rc08fn:10)
6.nu d   rc08sl                       1    overlay(rc08ld:55)
7.00 d  rc08end                       1    overlay(rc08ld:80)
 .    *
 .    * Forsendelse - Type 09 : Kommentarer
 .    *
 .   d                 ds
 .   d rc09ld                        80
 .   d  rc09ty                        2    overlay(rc09ld)
 .   d  rc09tx                       60    overlay(rc09ld:3)
 .   d  rc09td                        8    overlay(rc09ld:63)
7.00 d  rc09end                       1    overlay(rc09ld:80)
 .    *
 .    * Forsendelse - Type 99 : Sum record oppdrag
 .    *
 .   d                 ds
 .   d rc99ld                        80
 .   d  rc99ty                        2    overlay(rc99ld)
 .   d  rc99sk                       10  2 overlay(rc99ld:3)
 .   d   rc99sf                       1    overlay(rc99sk)
 .   d  rc99af                        3  0 overlay(rc99ld:13)
 .   d  rc99ak                        3  0 overlay(rc99ld:16)
 .   d  rc99ko                       10  2 overlay(rc99ld:19)
7.00 d  rcend                         1    overlay(rc99ld:80)
 .    *
 .    * Forsendelse - Type XX : Sum record for hele forsendelsen
 .    *
 .   d                 ds
 .   d rcxxld                        80
 .   d  rcxxty                        2    overlay(rcxxld)
 .   d  rcxxal                        6  0 overlay(rcxxld:3)
 .   d  rcxxhs                       10  2 overlay(rcxxld:9)
 .   d   rcxxhf                       1    overlay(rcxxhs)
 .    *
 .    * Returer - Kvitteringsretur for innleste saker
 .    *
 .   d                 ds
 .   d rcn1ld                        80
 .   d  rcn1rf                       20    overlay(rcn1ld)
 .   d   rcn1id                       4    overlay(rcn1rf)
 .   d   rcn1fg                       3    overlay(rcn1rf:5)
 .   d   rcn1fi                       3  0 overlay(rcn1rf:8)
 .   d   rcn1ku                       6  0 overlay(rcn1rf:11)
 .   d   rcn1lØ                       4    overlay(rcn1rf:17)
6.nu d  rcn1fr                       22    overlay(rcn1ld:21)
6.nu d   rcn1fa                       8  0 overlay(rcn1fr)
6.nu d** rcn1tx                      14    overlay(rcn1fr)
6.nu d   rcn1tx                      14    overlay(rcn1fr:9)
6.nu d  rcn1sn                        8    overlay(rcn1ld:43)
6.nu d  rcn1hs                       10  2 overlay(rcn1ld:51)
 .    *
 .    * Rapport - Direkte Innbetalinger/Feilsendinger/Korrigeringer
 .    *
 .   d                 ds
 .   d rcn2ld                        52
 .   d  rcn2ty                        2    overlay(rcn2ld)
 .   d  rcn2rf                       20    overlay(rcn2ld:3)
 .   d   rcn2sn                       8    overlay(rcn2rf)
 .   d   rcn2rs                      12    overlay(rcn2rf:09)
 .   d  rcn2kn                       12    overlay(rcn2ld:23)
 .   d  rcn2da                        8  0 overlay(rcn2ld:35)
6.11 d  rcn2be                       10  2 overlay(rcn2ld:43)
      *
     d w_firm          s                   like(rnfirm)
     d w_kund_gml      s                   like(rnkund)
     d rkwblu_kund     s                   like(rnkund)
     d rkwblu_bdat     s                   like(rnbdat)
     d rkwblu_tist     s                   like(rwtist)
     d n               s              3  0
     d wantfa          s              3  0
     d wantko          s              3  0
     d wantli          s              6  0
     d warb1           s              4  0
     d wkjdat          s              6  0
     d wnbelØ          s                   like(rnbelØ)
      *
     d wnbdat          s              8  0
     d wnfdat          s              8  0
      *
     d wpÅr            s              2  0
     d wpÅr4           s              4  0
     d wpdag           s              2  0
     d wqdatÅ          s              2  0
     d wsumfa          s             10  2
     d wsumhs          s             10  2
     d wtomÅ           s              4  0
     d wtomd           s              2  0
     d wtomm           s              2  0
     d xiforf          s              6  0
      *
     d w_bdat          s              6  0
     d w_fdat          s              6  0
      *
     c     rkwbpf_key    setll     rkwbpfr                                60
     c     rkwbpf_key    reade     rkwbpfr                                60
      *
     c                   dow       *in60 = *off
      *
      * Sjekk om ny kunde
      *
     c                   if        w_kund_gml <> 0 and
     c                             rnkund <> w_kund_gml
     c                   exsr      brd_kunde
     c                   endif
      *
     c                   if        rnkund <> w_kund_gml
     c                   exsr      beh_kunde
     c                   eval      w_kund_gml = rnkund
     c                   endif
      *
      * Sjekk om saldo <= 0
      *
     c                   if        rksald <= 0
     c                   goto      neste
     c                   endif
      *
      * Legg ut Faktura
      *
     c                   move      '08'          rc08ty
     c                   move      rnbdat        wnbdat
     c                   move      wnbdat        rc08fd
      *
     c                   move      rnfdat        wnfdat
     c                   move      wnfdat        rc08ff
      *
     c                   move      rc0rnt        rc08rs
7.01 c                   if        rc0inf  = 'Collectio'
7.01 c                             and rnrest < 0
7.01 c                   eval      rc08rs = *zero
7.01 c                   endif
      *
     c                   if        rnrest >= *zeros
     c                   eval      rc08tp = rnrest
     c                   else
     c                   eval      rc08tp = 0 - rnrest
     c                   move      '-'           rc08tf
     c                   endif
      *
     c                   movel     rnbiln        rc08bn
     c                   move      *blank        rc08bl
6.11 c                   movel     *blank        rc08tx
     c**                 eval      rc08sl = '.'
7.nx c**                 move      rc08ld        rcns80
7.nx c                   movel     rc08ld        rcn130
9.01 c                   exsr      skrivrec
6.13 c                   eval      w_apos = 1
      *
      * Summer til sumrecord
      *
     c                   eval      wantfa = wantfa + 1
     c                   eval      wantli = wantli + 1
     c                   eval      wsumfa = wsumfa + rnrest
     c                   eval      wsumhs = wsumhs + rnrest
      *
      * Legg ut transaksjon i saksregisteret
      *
     c                   move      ' '           rcnsta
     c                   move      rnfirm        rcnfir
     c                   move      rkkund        rcnask
     c                   move      rnbiln        rcnasb
     c                   move      rnbdat        rcbdat
     c                   move      rc0cnk        rcncnk
     c                   move      rc02rf        rcnref
     c                   eval      rcnsak = *zeros
     c                   move      rnrest        rcnsto
     c                   eval      rcnomk = *zeros
     c                   eval      rcnrnt = *zeros
     c                   eval      rcnprv = *zeros
     c                   eval      rcndat = *loval
     c                   eval      rcnavr = *blank
     c                   eval      rcninb = *zeros
     c                   move      rc0inr        rcninr
     c                   write     rcnfpfr
      *
      * Merk transaksjonsfil for oppdatering
      *
     c                   eval      rkwblu_kund = rnkund
     c                   eval      rkwblu_bdat = rnbdat
     c                   eval      rkwblu_tist = rwtist
     c     rkwblu_key    chain     rkwblu                             62
      *
     c                   if        *in62 = *off
     c                   move      'I'           rnstat
     c                   update    rkwblur
     c                   endif
      *
     c     neste         tag
      *
     c     rkwbpf_key    reade     rkwbpfr                                60
      *
     c                   enddo
      *
     c/eject
      *
     c     slutt         tag
      *
     c                   exsr      brd_kunde
      *
      * Legg ut totaler til inkassobyrå
      *
     c                   move      'XX'          rcxxty
     c                   eval      wantli = wantli + 1
     c                   move      wantli        rcxxal
     c                   eval      rcxxhs = wsumhs
      *
     c                   if        wsumhs < *zeros
     c                   eval      rcxxhs = 0 - wsumhs
     c                   move      '-'           rcxxhf
     c                   endif
      *
7.nx c**                 move      rcxxld        rcns80
7.nx c                   movel     rcxxld        rcn130
      *
T3.30 * Skriver ikke XX-rec. dersom det ikke er noen poster i oppdraget
      *
T3.30c                   if        rcxxhs <> *zeros and
T3.30c                             rcxxal <> 2
SIN   * Skal aldri skrive XX-record
SIN  c*                  write     rcnspfr
T3.30c                   endif
      *
     c                   eval      *inlr = *on
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     skrivrec      begsr
      *
9.02 c*                  eval      rcns80 = %xlate('Ø':x'2A':rcns80)
9.02 c*                  eval      rcns80 = %xlate('Å':x'46':rcns80)
9.02 c*                  eval      rcns80 = %xlate('Æ':x'EF':rcns80)
9.02 c*                  eval      rcns80 = %xlate('å':x'90':rcns80)
9.02 c*                  eval      rcns80 = %xlate('ø':x'A0':rcns80)
9.02 c*                  eval      rcns80 = %xlate('æ':x'77':rcns80)
     c                   write     rcnspfr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for behandling av kunde                               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     beh_kunde     begsr
      *
      * Sett indikatorer av
      *
     c                   eval      *in25 = *off
      *
      * Blanker ut arbeidsfelt
      *
     c                   eval      page = 0
     c                   eval      wsumfa = 0
     c                   eval      wsumhs = 0
     c                   eval      wantfa = 0
     c                   eval      wantko = 0
      *
      *  Les kunderegister og poststedregister
      *
     c                   move      rnkund        rkkund
     c     rkunl1_key    chain     rkunl1                             10
      *
      * Sjekk om saldo har blitt 0 eller
      * negativ siden forslaget ble kjørt
      *
     c                   if        rksald <= 0
6.12 c                   goto      end_beh
     c                   eval      *in19 = *on
     c                   endif
      *
     c                   move      rkponr        apponr
     c     apponr        chain     apospf                             10
      *
     c                   if        *in10 = *on
     c                   eval      apsted = *blank
     c                   move      rksted        apsted
     c                   endif
      *
      * Legg ut startrecord for oppdrag
      *
     c                   move      '01'          rc01ty
     c                   move      rc0cnk        rc01kn
     c                   move      ppkod         rc01op
     c                   eval      rc01t1 = *blank
     c                   eval      rc01fg = l_fgrp
     c                   eval      rc01fi = l_firm
     c                   eval      rc01t2 = *blank
      *
      * Hent avdelingstekst
      *
     c                   if        rnavde <> *zeros
     c                   eval      w_gkod = rnavde
     c                   else
     c                   eval      w_gkod = rkavde
     c                   endif
      *
     c                   call      'RS707R'
     c                   parm                    w_stat
     c                   parm                    w_gkod
     c                   parm                    w_gtxt
     c
T5.40c                   eval      rc01av = w_gkod
     c                   eval      rc01t2 = w_gtxt
     c                   eval      wantli = wantli + 1
7.nx c**                 move      rc01ld        rcns80
7.nx c                   movel     rc01ld        rcn130
9.01 c                   exsr      skrivrec
     c**                 write     rcnspfr
      *
      * Legg ut Skyldner, diverse opplysninger
      *
     c                   move      '02'          rc02ty
      *
     c                   if        rkftnr <> *zeros
     c                   movel     rkftnr        rc02pf
     c                   else
     c                   eval      rc02pf = *blank
     c                   endif
      *
     c                   eval      rc02rf = *blank
     c                   move      'ZXCN'        rc02id
     c                   move      l_fgrp        rc02fg
     c                   move      rnfirm        rc02fi
     c                   move      rkkund        rc02ku
     c                   eval      rc02lØ = *blank
      *
     c                   if        rkftnr <> *zeros
     c                   move      rkbgir        rc02bk
     c                   else
     c                   eval      rc02bk = *blank
     c                   endif
      *
     c                   eval      rc02da = *blank
     c                   eval      wantli = wantli + 1
7.nx c**                 move      rc02ld        rcns80
7.nx c                   movel     rc02ld        rcn130
9.01 c                   exsr      skrivrec
     c**                 write     rcnspfr
      *
      * Legg ut Skyldner, navneopplysninger
      *
     c                   move      '03'          rc03ty
     c     lo:up         xlate     rknavn        rc03na
      *
     c                   eval      rc03n2 = *blank
     c                   move      'P'           rc03pf
      *
     c                   if        rkftnr <> *zeros
     c                   move      'F'           rc03pf
     c                   endif
      *
     c                   eval      wantli = wantli + 1
7.nx c**                 move      rc03ld        rcns80
7.nx c                   movel     rc03ld        rcn130
9.01 c                   exsr      skrivrec
     c**                 write     rcnspfr
      *
      * Legg ut Skyldner, adresseopplysninger
      *
     c                   move      '04'          rc04ty
     c                   eval      rc04ad = *blank
     c                   movel     rkgate        rc04ad
     c                   eval      rc04st = *blank
     c                   movel     rkadr2        rc04st
     c                   eval      wantli = wantli + 1
7.nx c**                 move      rc04ld        rcns80
7.nx c                   movel     rc04ld        rcn130
9.01 c                   exsr      skrivrec
     c**                 write     rcnspfr
      *
      * Legg ut Skyldner, adresseopplysninger
      *
     c                   move      '07'          rc07ty
     c                   movel     rkponr        rc07pn
     c                   eval      rc07ps = *blank
     c                   movel     apsted        rc07ps
     c                   move      rkland        rc07lk
      *
     c                   if        rc07lk = *blank
     c                   move      ' N'          rc07lk
     c                   endif
      *
     c                   move      rktlfn        rc07tj
     c                   eval      wantli = wantli + 1
7.nx c**                 move      rc07ld        rcns80
7.nx c                   movel     rc07ld        rcn130
9.01 c                   exsr      skrivrec
     c**                 write     rcnspfr
      *
6.12 c     end_beh       endsr
      *
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for brudd på kundenummer                              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     brd_kunde     begsr
      *
      * Legg ut Sum oppdrag
      *
     c                   move      '99'          rc99ty
     c                   eval      rc99sk = wsumfa
      *
     c                   if        wsumfa < *zeros
     c                   eval      rc99sk = 0 - wsumfa
     c                   move      '-'           rc99sf
     c                   endif
      *
     c                   move      wantfa        rc99af
     c                   move      wantko        rc99ak
     c                   eval      wantli = wantli + 1
7.nx c**                 move      rc99ld        rcns80
7.nx c                   movel     rc99ld        rcn130
      *
T5.40 * Skriver ikke tomme 99-records
      *
T5.40c                   if        rc99sk = *zero and
T5.40c                             rc99af = *zero and
T5.40c                             rc99ak = *zero
T5.40c                   goto      forbi
T5.40c                   endif
      *
9.01 c                   exsr      skrivrec
     c**                 write     rcnspfr
T5.40c     forbi         tag
      *
     c                   endsr
     c/eject
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
6.13  *
6.13  * Parameterliste
6.13  *
6.13 c     *entry        plist
6.13 c                   parm                    w_apos
      *
     c                   eval      n = *zeros
      *
     c     rcnkey        klist
     c                   kfld                    rcnref
      *
     c     rktrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rnkund
     c                   kfld                    rnbdat
     c                   kfld                    rntist
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkkund
      *
     c     rkwbpf_key    klist
     c                   kfld                    w_firm
      *
     c     rkwblu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkwblu_kund
     c                   kfld                    rkwblu_bdat
     c                   kfld                    rkwblu_tist
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
     c     rkwapf_key    klist
     c                   kfld                    rwkkey
      *
     c                   move      l_firm        w_firm
      *
      * Hent firmaopplysninger
      *
     c     afirl1_key    chain     afirl1                             10
      *
      * Hent inn parameter
      *
     c                   eval      rwkkey = *blanks
     c                   movel     w_firm        rwkkey
     c     rkwapf_key    chain     rkwapf                             10
     c                   move      rwkrec        pparam
      *
      *  Finner faste opplysninger
      *
     c                   movel     w_firm        rc0fir
     c     rc0fir        chain     rca0lu                             10
      *
     c                   if        *in10 = *on
     c                   eval      rc0inr = 1
     c                   write     rca0lur
     c                   else
     c                   eval      rc0inr = rc0inr + 1
     c                   update    rca0lur
     c                   endif
      *
      *  Snu kjøredato for kontroll av forfallsdato
      *
     c                   move      pkdat         wkjdat
     c                   move      pkdat         warb1
     c                   movel     pkdat         wpdag
     c                   move      pkdat         wpÅr
     c                   movel     wpÅr          wkjdat
     c                   move      wpdag         wkjdat
      *
     c                   if        wpÅr > 80
     c                   eval      wpÅr4 = wpÅr + 1900
     c                   else
     c                   eval      wpÅr4 = wpÅr + 2000
     c                   endif
      *
     c                   eval      wsumfa = 0
     c                   eval      wsumhs = 0
     c                   eval      wantli = 0
6.13 c                   eval      w_apos = 0
7.00  *
7.00 c                   if        rc0inf  = 'Collectio'
7.00 c                   eval      rc01end = '.'
7.00 c                   eval      rc02end = '.'
7.00 c                   eval      rc03end = '.'
7.00 c                   eval      rc04end = '.'
7.00 c                   eval      rc07end = '.'
7.00 c                   eval      rc08end = '.'
7.00 c                   eval      rc09end = '.'
7.00 c                   endif
      *
      *  Legger ut FMS record for EVRY overføring
      *
     c*                  eval      rcns80 = 'FMS01NOBFL101    NOCRR101'
     c*                  write     rcnspfr
      *
     c                   endsr
      *
