     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : NO121R
      * Beskrivelse . . : Ekstern ordre-hode, overføring av EDI-bestilling
      *                   til ordre
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       300903 GRO  Kopi av NO120R for EDI-best.
5.32  * R5.32 080904 AMD  Lagt inn ordretype for nummerserie (se dok)
5.53  * R5.53 210307 AMD  Lagt på mva på memosaldo for å få riktigere
      *                   beregning av kredittgrense
5.50  * R5.50 060807 KOB  Lagt inn kontroll mot beløpsgrense almenning
5.60  * R5.60 191107 AMD  Lagt inn løsning for kontroll av grense for
      *                   almenningsrabatter
      *
      * 6.10  240609 BSA  Oppdatert med sporingsinformasjon
6.11  * 6.10  200810 bof  Lagt på fakturatype fra kunden
6.20  * 6.20  221110 NHO  Proj er nå alfa
6.21  * V6.20 040511 AMD  Flere muligheter rundt kreditt-alternativer
6.22  * V6.20 140611 bsa  Memosaldo ligger på eget register
6.23  * 6.20  280915 BOF  Lagt inn ryddeprogram ved oppr.av ordre
6.nu  * R6.30 170614 bof  Programmet gjennomgått mtp nummerutvidelser.
6.31  * 6.30  120914 BKV  Henter overstyrt info fra kundeprosjekt
6.32  * V6.30 150616 bsa  Oppdaterer ordrenummer på kopiregister hvis
6.32  *                   EDI bestilling.
6.33  * V6.30 020719 bsa  Endret tabellstruktur
6.34  * V6.30 080719 bsa  Oppdaterer også ordrenummer på kopiregister
6.34  *                   hodet.
6.35  * V6.30 080719 bsa  Kompletterer med informasjon
6.36  * V6.30 090719 bsa  Legger ut tekstinformasjon
6.37  * V6.30 120719 bsa  Takler også skaffevarer. NB Priser osv blir
6.37  *                   ikke rekalkulert. Disse må vedlikeholdes og
6.37  *                   oppdateres manuelt.
      *
7.01  * V7.00 021019 bsa  Legger ut avdeling på ordrehodet.
7.02  * V7.00 101019 bsa  Endrer logikken for oppdatering av EDHE og EDDT
7.02  *                   Reverserer store deler av 6.32/6.33/6.34. Programmet
7.02  *                   er en blåkopi av NO120R, men logikken er endret siden
7.02  *                   informasjonen som er tilgjengelig er litt forksjellig.
7.03  * V7.00 291019 bsa  Feil i overføring av tekstlinjer
7.04  * V7.00 070220 bsa  Utvidet kall til ND105 med parameter.
7.05  * K000  040620 bsa  Legger eksternt ref til kundens bestillingsnummer
7.09  * K000  180620 bsa  Endret feilmeldinger
7.10  * K000  291220 bsa  Endrer logikken på hvordan vår ref oppdateres
7.11  * K000  160321 bsa  Hvis fakturakunde må denne hentes inn og
7.11  * K000              legges på ordrehodet.
7.12  * MGR   151216 sst  Ber flytende Memosaldo m/ordre pr ant. dager
7.12  *       170117 sst  Hente parameter meosaldo fra AFPSPF
7.13  * 7.00  250320 bsa  Innfører switchstyring av memosaldo
      *
8.01  * K000  080222 bsa  Hvis vi overfører ordre hvor det allerde
8.01  * K000              finnes et tilbud med samme eksternreferanse,
8.01  * K000              skal tilbudet slettes.
8.02  * K000  080222 bsa  Hvis det finnes parentref, skal ikke priser
8.02  * K000              eller rabatter endres.
8.03  * K000  100222 bsa  Hvis systemkode 'TBU', forslå tilbud.
8.04  * K000  231122 sst  Flyttet ant memo-dager og utv.kreditgrense til  UTVIDET_MEMOSALDO (nx)
8.05  * 8.xx  200122 bsa  Skriver tekst som har kommet som melding til
8.05  * 8.xx              toptekst på ordren som opprettes. NB Forutsetter
8.05  * 8.xx              at bestillingen har kommet elektronisk.
8.06  * 8.xx  240222 bsa  Henter inn evnt leveringsbetingelse fra EDI, og
8.06  * 8.xx              legger den som forslag på ordren (FOLBTX)
8.07  *PRG2   220622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
8.08  * K000  130824 bsa  Henter nummerserie basert på feil ordretype.
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fnohel1    if   e           k disk    rename(nohepfr:nohel1r)
     fnodtl1    if   e           k disk    rename(nodtpfr:nodtl1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     ffusrl1    if   e           k disk    rename(fusrpfr:fusrl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
6.22 frkmel1    if   e           k disk    rename(rkmepfr:rkmel1r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     fnohelu    uf   e           k disk    rename(nohepfr:nohelur)
8.01 fnoh2l1    if   e           k disk    rename(noh2pfr:noh2l1r)
     ffohelu    uf a e           k disk    rename(fohepfr:fohelur)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
7.02 feddtl3    uf   e           k disk    rename(eddtpfr:eddtl3r)
7.02 fedhel4    uf   e           k disk    rename(edhepfr:edhel4r)
6.36 fedtxlr    if   e           k disk    rename(edtxpfr:edtxlrr)
7.10 fra09l1    if   e           k disk    rename(ra09pfr:ra09l1r)
8.04 f*afpslr    if   e           k disk    rename(afpspfr:afpslrr)
8.05 ffotxlu    uf a e           k disk    rename(fotxpfr:fotxlur)
8.06 fedh2l1    if   e           k disk    rename(edh2pfr:edh2l1r)
      *
     fno120d    cf   e             workstn
      *
      * Definering av parametre
      *
     d p_firm          s                   like(ndfirm)
     d p_fsys          s                   like(ndfsys)
     d p_tell          s                   like(ndtell)
      *
      * Definering av Local data area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
7.12 d l_filg                931    933
      *
      * Definering av variabler i nøkler
      *
     d nohel1_fsys     s                   like(nofsys)
     d nohel1_tell     s                   like(notell)
     d nodtl1_fsys     s                   like(ndfsys)
     d nodtl1_tell     s                   like(ndtell)
     d votyl1_otyp     s                   like(vaotyp)
     d fusrl1_user     s                   like(fbuser)
     d rkunl1_kund     s                   like(rkkund)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
     d nohelu_fsys     s                   like(nofsys)
     d nohelu_tell     s                   like(notell)
     d fohelu_numm     s                   like(fonumm)
     d fohelu_suff     s                   like(fosuff)
     d fodtl1_numm     s                   like(fdnumm)
     d fodtl1_suff     s                   like(fdsuff)
     d fodtl1_line     s                   like(fdline)
7.02 d eddtl3_fsys     s                   like(edfsys)
7.02 d eddtl3_tell     s                   like(edtell)
7.02 d eddtl3_line     s                   like(edline)
7.02 d edhel4_fsys     s                   like(eofsys)
7.02 d edhel4_tell     s                   like(eotell)
6.36 d edtxlr_fsys     s                   like(etfsys)
6.36 d edtxlr_tell     s                   like(ettell)
6.36 d edtxlr_ldor     s                   like(etldor)
6.36 d edtxlr_ldat     s                   like(etldat)
6.36 d edtxlr_line     s                   like(etline)
6.36 d edtxlr_type     s                   like(ettype)
6.36 d edtxlr_tlin     s                   like(ettlin)
7.10 d ra09l1_ikod     s                   like(raikod)
8.04 d*afpslr_ufil     s                   like(afufil)
8.04 d*afpslr_uide     s                   like(afuide)
8.05 d fotxlu_numm     s                   like(ftnumm)
8.05 d fotxlu_suff     s                   like(ftsuff)
8.05 d fotxlu_line     s                   like(ftline)
      *
      * Definering av variable
      *
3.11 d b_sper          s              1    inz(*off)
8.02 d b_pref          s              1    inz(*off)
      *
     d w_kode          s              1
     d w_fast          s             10
     d w_sald          s             11  2
5.60 d w_sal2          s             11  2
     d w_tots          s             11  2
     d w_totr          s             11  2
     d w_totk          s             11  2
5.60 d w_tota          s             11  2
5.60 d w_totn          s             11  2
5.60 d w_tot1          s             11  2
5.60 d w_tot2          s             11  2
     d w_fell          s              6
3.11 d w_valg          s              2  0
3.11 d w_text          s             70
      *
     d w_firm          s                   like(nofirm)
     d w_numm          s                   like(fonumm)
     d w_suff          s                   like(fosuff)
     d w_otyp          s                   like(footyp)
     d w_line          s                   like(fdline)
6.37 d w_vsta          s              1  0
5.32 d w_onum          s                   like(vaonum)
5.32 d w_type          s                   like(vaonum)
5.53  *
7.11 d s_kund          s                   like(rkkund)
7.12  *w_utvgrns = utvidelse av memosaldo(Mestergruppen)
7.12 d w_utvgrns       s              2  0
7.12  *w_memodagr = antall dager ordre skal med i memosaldo
7.12 d w_memodagr      s              3  0
7.12 d w_3x            s              3
7.12 d p_fir           s              3
7.12 d p_kun           s              6
7.12 d w_kgrn          s             11  2
5.53  *
5.53  * Parametre til RS205R - hent mva %
5.53  *
5.53 d w_stat          s              1
5.53 d w_mvak          s              1
5.53 d w_mvtx          s             30
5.53 d w_mvas          s              6  2
5.53 d w_mvat          s              5  4
5.53 d w_mvaf          s             10  9
5.53 d w_bpro          s              5  2
5.53 d w_date          s               d   datfmt(*iso)
5.50 d w_gral          s                   like(rkgral)
8.01 d w_aktp          s             15
8.01 d w_sgru          s             60
8.01 d w_kkko          s              2  0
8.01 d w_slko          s              2  0
8.01 d w_osuf          s              2  0
8.01 d w_typd          s              4
8.01 d w_kom1          s             30
8.01 d w_kom2          s             30
8.01 d w_opdt          s              1
      *
8.01 d s_numm          s                   like(fonumm)
8.01 d s_suff          s                   like(fosuff)
8.01 d s_firm          s                   like(nofirm)
8.01 d s_scan          s             15
8.01 d s_fsys          s              3
      *
7.13 d u_713           s              1    inz(*off)                            Kredit og memodager
      *
7.13   dcl-s co402_verdi1  char(1);
7.13   dcl-s co402_verdi2  char(2048);
7.13   DCL-PR CO402R EXTPGM('CO402R');
7.13     p_filgrp            char(3)     const;
7.13     p_firm              packed(3:0) const;
7.13     p_lager             packed(2:0) const;
7.13     p_nokkel            char(50)    const;
7.13     p_verdi1            char(1);
7.13     p_verdi2            char(2048);
7.13   END-PR;
      *
8.01  * Uten denne aksepteres ikke *loval som dato
8.01 C/Exec SQL
8.01 C+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR
8.01 C+  , commit=*none
8.01 C/End-Exec
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Avbryter dersom ekstern ordre ikke finnes
      *
     c                   eval      nohel1_fsys = p_fsys
     c                   eval      nohel1_tell = p_tell
     c     nohel1_key    chain     nohel1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
8.01  *
8.01  * Hent også evnt info fra proffnett
8.01  *
8.01 c     nohel1_key    chain     noh2l1
8.01 c                   if        not %found
8.01 c                   eval      n2pref = *blanks
8.01 c                   endif
      *
      * Hent ordretype
      *
     c                   eval      w_otyp = faaot8
     c                   eval      w_fast = l_wsid
      *
     c                   if        w_otyp = *blank
     c                   eval      w_otyp = faaot2
     c                   endif
8.03  *
8.03 c                   if        p_fsys = 'TBU'
8.03 c                   eval      w_otyp = 'T'
8.03 c                   endif
      *
8.03 c                   eval      b1otyp = w_otyp
      *
      * Henter informasjon og kredittsperre og kredittgrense
      * Ved kredittsperre stoppes videre overføring
      *
     c                   eval      rkunl1_kund = nokund
     c     rkunl1_key    chain     rkunl1
7.12  * Beregne memosaldo pr dato, Mestergruppen
7.13 c                   if        u_713 = *on
7.12 c                   if        w_memodagr > 0
7.12 c                   move      w_firm        p_fir
7.12 c                   move      rkunl1_kund   p_kun
7.12 c                   call      'FO763R'
7.12 c                   parm                    p_fir
7.12 c                   parm                    p_kun
7.12 c                   endif
7.13 c                   endif
6.22  * Henter inn memosaldo
6.22 c     rkunl1_key    chain     rkmel1
      *
7.09 c                   if        rkkres = 1
7.09 c                   eval      *in41 = *on
7.09 c                   eval      b_sper = *on
7.09 c                   goto      b1taga
7.09 c                   endif
      *
6.21 c                   if        rkkres = 1
     c                   eval      votyl1_otyp = w_otyp
     c     votyl1_key    chain     votyl1
3.35 c                   if        vaoakk > 0
     c                   eval      b_sper = *on
     c                   eval      *in31 = *on
7.09 c                   goto      b1taga
     c                   endif
     c                   endif
      *
     c                   if        rkgrns <> 0
5.53 c                   eval      w_sald = rksald + (rkmems * w_mvat)
7.12  *   Utvide kreditgrense   (Mestergruppen løsning 2017_01)  sst
7.13 c                   if        u_713 = *on
7.12 c                   if        w_sald < rkgrns * 1000
7.12 c                   eval      w_kgrn = rkgrns * 1000 *
7.12 c                                      (1+w_utvgrns/100)
7.12 c                   else
7.12 c                   eval      w_kgrn = rkgrns * 1000
7.12 c                   endif
7.12 c                   if        w_sald > w_kgrn
     c                   eval      *in42 = *on
     c                   endif
7.13 c                   else
7.12 c                   eval      w_sald = rksald + (rkmems * w_mvat)
     c                   if        w_sald > rkgrns
7.12 c                   eval      *in42 = *on
7.12 c                   endif
7.13 c                   endif
     c                   endif
5.50  *
5.50  * Sjekk beløpsgrense almenning fra kunde dersom utfylt
5.50  *
5.50 c                   if        rkgral <> 0
5.50 c                   eval      w_gral = rkgral
5.50  *
5.50 c                   else
5.50  *
5.50  * Ellers hent beløpsgrense almenning
5.50  *
5.50 c                   call      'FO106R'
5.50 c                   parm                    nokund
5.50 c                   parm                    w_gral
5.50  *
5.50 c                   endif
5.50  *
5.50 c                   if        w_gral <> 0
5.50 c                   eval      w_sald = rkkjal + rkmema
5.50 c                   if        w_sald > w_gral
5.50 c                   eval      *in43 = *on
5.50 c                   endif
5.50 c                   endif
      *
      * Behandler bildet
      *
     c     b1taga        tag
      *
     c                   exfmt     b1bld
      *
      * F3=Avslutt
      *
B001 c                   if        *inkc or *inkl
 001 c                   goto      avslutt
E001 c                   endif
      *
      * Avbryter ved kredittsperre
      *
B001 c                   if        b_sper = *on
 001 c                   goto      avslutt
E001 c                   endif
      *
8.08 c                   eval      w_otyp = b1otyp
      *
      * Oppretter ordre-hode
      *
     c                   exsr      ordre_hode
      *
      * Oppretter ordre-linje
      *
     c                   exsr      ordre_linje
      *
3.11  * Oppdater ordrehode og memosaldo med verdier fra ordrelinjer
      *
     c                   exsr      oppdat_ordre
      *
3.11  * Oppdater ekstern ordre-hode med ordrenummer
      *
     c                   eval      nohelu_fsys = p_fsys
     c                   eval      nohelu_tell = p_tell
     c     nohelu_key    chain     nohelur
     c                   if        %found
     c                   eval      nonumm = fonumm
6.10 c                   time                    noedat
6.10 c                   time                    noetim
6.10 c                   eval      noeusr = l_user
     c                   update    nohelur
     c                   endif
      *
      * Kaller program for ordrebehandling
      *
     c                   eval      w_valg = 2
      *
     c                   call      'FO100R'
     c                   parm                    w_valg
     c                   parm                    fonumm
     c                   parm                    fosuff
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppretter ordre-hode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ordre_hode    begsr
      *
      * Hent neste ledig nummer i.h.t. ordretype
      *
     c                   exsr      hent_nummer
8.06  *
8.06  * Slå opp mot evnt tilleggsregister på mottatt EDI medling
8.06  *
8.06 c                   if        nofsys = 'EDI' or
8.06 c                             nofsys = 'EHF' or
8.06 c                             nofsys = 'SMK'
8.06  *
8.06 c     nohel1_key    chain     edh2l1
8.06 c                   if        not %found
8.06 c                   eval      e2lbex = *blanks
8.06 c                   endif
8.06  *
8.06 c                   endif
      *
      * Initierer fil
      *
     c                   eval      fosuff = 00
     c                   eval      fomkod = 0
     c                   eval      fobind = 'N'
      *
     c                   eval      fofirm = nofirm
     c                   eval      fonumm = w_numm
8.03 c**                 eval      footyp = w_otyp
8.03 c                   eval      footyp = b1otyp
     c                   eval      folety = nolety
6.35 c                   eval      folage = nolage
     c                   eval      fobdat = *loval
     c                   eval      foldat = noldat
     c                   eval      fobetb = 0
     c                   eval      foselg = 0
6.35 c                   eval      folmat = nolmat
8.06 c                   eval      folbtx = e2lbex
     c                   eval      fokund = nokund
     c                   eval      fokpro = nokpro
     c                   eval      fonavn = nonavn
     c                   eval      foadr1 = noadr1
     c                   eval      foadr2 = noadr2
     c                   eval      fosted = %trim(noponr) + ' ' + nosted
6.20 c**                 eval      foproj = 0
6.20 c                   eval      foproj = *blank
     c                   eval      foakti = *blank
     c                   eval      fovref = *blank
     c                   eval      fodref = nodref
     c                   eval      forekv = norekv
     c                   eval      fodate = *loval
     c                   eval      fotime = *loval
     c                   eval      forkat = 0
3.38 c                   eval      fonref = nonref
7.05 c                   eval      fokbnr = nonref
3.38 c                   eval      fofsys = nofsys
7.01 c                   eval      foavde = noavde
7.01  * Hvis avdeling er 0, hent fra kunde
7.01 c                   if        foavde = 0
7.01 c                   eval      foavde = rkavde
7.01 c                   endif
      *
     c                   eval      w_suff = fosuff
      *
     c                   time                    fobdat
     c                   time                    fodate
     c                   time                    fotime
      *
      * Henter kundeinformasjon
      *
7.11 c                   eval      s_kund = 0
     c                   eval      rkunl1_kund = nokund
     c     rkunl1_key    chain     rkunl1
     c                   if        %found
      *
     c                   eval      fobetb = rkbetb
     c                   eval      fodist = rkdist
6.35 c                   if        folmat = 0
     c                   eval      folmat = rkform
6.35 c                   endif
     c                   eval      folbet = rklbet
     c                   eval      fovalk = rkvalk
     c                   eval      forkat = rkrabk
     c                   eval      fosped = rksped
     c                   eval      fokjor = rkkjØr
     c                   eval      fomkod = rkmkod
     c                   eval      fokobk = rkordb
     c                   eval      fokgfa = rkfagb
     c                   eval      fokgfr = rkfrgb
     c                   eval      fokgek = rkekgb
KOB  c                   eval      forabp = rkorab
3.38 c                   eval      foneto = rkneto
6.11 c                   eval      fofaty = rkfaty
      *
7.10 c**                 if        rkslgr <> 0
     c                   eval      foselg = rkslgr
7.10 c**                 endif
      *
     c                   if        fonavn = *blank and
     c                             foadr1 = *blank and
     c                             foadr2 = *blank
     c                   eval      fonavn = rknavn
     c                   eval      foadr1 = rkgate
     c                   eval      foadr2 = rkadr2
     c                   eval      fosted = rksted
     c                   endif
7.11  *
7.11  * Sjekk om det er fakturakunde på kunden
7.11  *
7.11 c                   if        rkfaku <> 0
7.11 c                   eval      s_kund = rkfaku
7.11 c                   eval      rkunl1_kund = rkfaku
7.11 c     rkunl1_key    chain     rkunl1
7.11 c                   if        %found
7.11 c                   eval      fobetb = rkbetb
7.11 c                   eval      fodist = rkdist
7.11 c                   eval      folbet = rklbet
7.11 c                   eval      fovalk = rkvalk
7.11 c                   eval      forkat = rkrabk
7.11 c                   eval      fosped = rksped
7.11 c                   eval      fokjor = rkkjØr
7.11 c                   eval      fomkod = rkmkod
7.11 c                   eval      fokobk = rkordb
7.11 c                   eval      fokgfa = rkfagb
7.11 c                   eval      fokgfr = rkfrgb
7.11 c                   eval      fokgek = rkekgb
7.11 c                   eval      forabp = rkorab
7.11 c                   eval      foneto = rkneto
7.11 c                   eval      fofaty = rkfaty
7.11 c                   eval      foselg = rkslgr
7.11 c                   eval      fokund = s_kund
7.11 c                   eval      folkun = nokund
7.11 c                   eval      fokuna = rkalfa
7.11 c                   endif
7.11 c                   endif
      *
     c                   endif
7.10  *
7.10  * Endel av koden er flyttet - logikken blir slik
7.10  * Selger hentes fra tilknyttet brukeren
7.10  * Slår opp på selger, og henter navnet på selger og
7.10  * legger som vår ref på ordren.
7.10  * Hvis selger ikke ligger på bruker, hentes den fra kunden.
      *
      * Hent brukerinformasjon
      *
     c                   if        nouser <> *blank
     c                   eval      fusrl1_user = nouser
     c                   else
     c                   eval      fusrl1_user = l_user
     c                   endif
      *
     c     fusrl1_key    chain     fusrl1
     c                   if        %found
7.10 c**                 eval      fovref = fbvref
7.10 c                   if        fbselg <> 0
     c                   eval      foselg = fbselg
7.10 c                   endif
     c                   endif
7.10  *
7.10  * Henter vår ref fra selger
7.10  *
7.10 c                   eval      ra09l1_ikod = foselg
7.10 c     ra09l1_key    chain     ra09l1r
7.10 c                   if        %found
7.10 c                   eval      fovref = raitxt
7.10 c                   endif
      *
      * Henter kundeprosjekt-informasjon
      *
     c                   if        nokpro <> 0
     c                   eval      fkprl1_kund = nokund
     c                   eval      fkprl1_kpro = nokpro
     c     fkprl1_key    chain     fkprl1
     c                   if        %found
     c                   eval      foproj = flproj
     c                   if        florka <> 0
     c                   eval      forkat = florka
6.31 c                   eval      fobetb = flbetb
6.31 c                   eval      fomkod = flmkod
6.31 c                   eval      fokgek = flekgb
6.31 c                   eval      foneto = flneto
6.31 c                   eval      fokgfa = flfagb
     c                   endif
     c                   endif
      *
     c                   endif
      *
      * Skriver til fil
      *
6.10 c                   time                    fodate
6.10 c                   time                    fotime
6.10 c                   eval      fooous = l_user
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = l_user
     c                   write     fohelur
8.01  *
8.01  * Sjekk om dette er en TBU ordre, med parent ref
8.01  *
8.02 c                   eval      b_pref = *off
8.01 c                   if        %trim(n2pref) <> *blanks
8.01  * Finnes det et tilbud på denne referansen fra før, skal den slettes
8.01 c                   eval      s_scan = %trim(n2pref)
8.01 c                   eval      s_fsys = 'TBU'
8.01 c                   eval      s_numm = 0
8.01 c                   eval      s_suff = 0
8.01  * Sett kode for at priser ikke skal endres
8.02 c                   eval      b_pref = *on
8.01 c/exec sql
8.01 c+ select fofirm,
8.01 c+        fonumm,
8.01 c+        fosuff
8.01 c+ into   :s_firm,
8.01 c+        :s_numm,
8.01 c+        :s_suff
8.01 c+ from   fohepf
8.01 c+ where  fofirm = :w_firm and
8.01 c+        fofsys = :s_fsys and
8.01 c+        fonref = :s_scan
8.01 c+ fetch first 1 rows only
8.01 c/end-exec
8.01  *
8.01 c                   if        s_numm <> 0
8.01  *
8.01 c                   eval      w_aktp = 'SLETTET'
8.01 c                   eval      w_slko = 99
8.01 c                   eval      w_sgru = 'Overført TBU ordre'
8.01 c                   eval      w_kkko = 0
8.01  *
8.01 c                   call      'XL700R'
8.01 c                   parm                    s_numm
8.01 c                   parm                    w_aktp
8.01 c                   parm                    w_slko
8.01 c                   parm                    w_kkko
8.01 c                   parm                    w_sgru
8.01 c
8.01  *
8.01  *  Kaller opp subprogrammet for sletting av ordre
8.01  *
8.01 c                   eval      w_typd = '*KOP'
8.01 c                   eval      w_kom1 = 'Overført TBU ordre'
8.01 c                   eval      w_kom2 = *blanks
8.01 c                   eval      w_osuf = 0
8.01 c                   eval      w_opdt = *blanks
8.01  *
8.01 c                   call      'FO410R'
8.01 c                   parm                    w_firm
8.01 c                   parm                    s_numm
8.01 c                   parm                    s_suff
8.01 c                   parm                    w_typd
8.01 c                   parm                    w_kom1
8.01 c                   parm                    w_kom2
8.01 c                   parm                    w_opdt
8.01  *
8.01 c                   endif
8.01  *
8.01 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppretter ordre-linje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ordre_linje   begsr
      *
      * Henter høyeste linjenummer fra ordre-linje
      *
     c                   exsr      hent_linjenr
      *
      * Dersom det ligger melding på ekstern ordre-hode, splittes denne og
      * legges ut som tekstlinjer
      *
7.03  * Blir dobbelt hvis vi sender NOMELD også
7.03 c                   if        1 = 2
     c                   if        nomeld <> *blank
      *
     c                   eval      w_text = %subst(nomeld:1:70)
     c                   eval      w_line = w_line + 10
     c                   call      'FD111R'
     c                   parm                    w_firm
     c                   parm                    fonumm
     c                   parm                    fosuff
     c                   parm                    w_line
     c                   parm                    faatty
     c                   parm                    w_text
      *
     c                   if        %subst(nomeld:71:70) <> *blank
     c                   eval      w_text = %subst(nomeld:71:70)
     c                   eval      w_line = w_line + 10
     c                   call      'FD111R'
     c                   parm                    w_firm
     c                   parm                    fonumm
     c                   parm                    fosuff
     c                   parm                    w_line
     c                   parm                    faatty
     c                   parm                    w_text
     c                   endif
      *
     c                   if        %subst(nomeld:141:70) <> *blank
     c                   eval      w_text = %subst(nomeld:141:70)
     c                   eval      w_line = w_line + 10
     c                   call      'FD111R'
     c                   parm                    w_firm
     c                   parm                    fonumm
     c                   parm                    fosuff
     c                   parm                    w_line
     c                   parm                    faatty
     c                   parm                    w_text
     c                   endif
      *
     c                   if        %subst(nomeld:211:46) <> *blank
     c                   eval      w_text = %subst(nomeld:211:46)
     c                   eval      w_line = w_line + 10
     c                   call      'FD111R'
     c                   parm                    w_firm
     c                   parm                    fonumm
     c                   parm                    fosuff
     c                   parm                    w_line
     c                   parm                    faatty
     c                   parm                    w_text
     c                   endif
      *
     c                   endif
7.03 c                   endif
6.36  *
6.36  * Sjekk om det er tekstlinjer som skal legges ut først på ordren
8.05  * Teksten legges som topptekst - ikke først på ordren.
6.36  *
6.36 c                   eval      edtxlr_fsys = nofsys
6.36 c                   eval      edtxlr_tell = notell
6.36 c                   eval      edtxlr_ldor = 0
6.36 c                   eval      edtxlr_ldat = *loval
6.36 c                   eval      edtxlr_line = 0
6.36 c                   eval      edtxlr_type = 'H'
6.36 c                   eval      edtxlr_tlin = 0
6.36 c     edtxlr_key    setll     edtxlr
6.36 c     edtxlr_key2   reade     edtxlr
6.36 c                   dow       not %eof
6.36  *
6.36 c                   if        %trim(ettek1) <> *blanks
6.36  *
6.36 c                   eval      w_text = %trim(ettek1)
6.36 c                   eval      w_line = w_line + 10
8.05  *
8.05 c                   eval      fotxlu_numm = fonumm
8.05 c                   eval      fotxlu_suff = fosuff
8.05 c                   eval      fotxlu_line = w_line
8.05 c     fotxlu_key    chain     fotxlu
8.05 c                   if        not %found
8.05 c                   eval      ftfirm = fofirm
8.05 c                   eval      ftnumm = fonumm
8.05 c                   eval      ftsuff = fosuff
8.05 c                   eval      ftline = w_line
8.05 c                   eval      fttext = w_text
8.05  *
8.05 c                   time                    ftodat
8.05 c                   time                    ftotim
8.05 c                   eval      ftousr = l_user
8.05 c                   time                    ftedat
8.05 c                   time                    ftetim
8.05 c                   eval      fteusr = l_user
8.05  *
8.05 c                   write     fotxlur
8.05 c                   endif
8.05  * Skriver ikke på linjenivå
8.05 c*                  call      'FD111R'
8.05 c*                  parm                    w_firm
8.05 c*                  parm                    fonumm
8.05 c*                  parm                    fosuff
8.05 c*                  parm                    w_line
8.05 c*                  parm                    faatty
8.05 c*                  parm                    w_text
6.36  *
6.36 c                   endif
6.36  *
6.36 c     edtxlr_key2   reade     edtxlr
6.36 c                   enddo
      *
      * Leser alle ekstern ordre-linjer, og legger disse ut som ordre-linjer
      *
     c                   eval      nodtl1_fsys = p_fsys
     c                   eval      nodtl1_tell = p_tell
     c     nodtl1_key    setll     nodtl1
     c     nodtl1_key    reade     nodtl1
     c                   dow       not %eof
      *
6.37 c                   eval      w_vsta = 0
     c                   eval      w_kode = *blank
     c                   eval      w_line = w_line + 10
      *
     c                   if        ndvare <> *blank
6.37  *
6.37  * Hent statuskode, slik at vi kaller riktig program.
6.37  *
6.37 c                   call      'ND105R'
6.37 c                   parm                    w_firm
6.37 c                   parm                    w_vsta
6.37 c                   parm                    nolage
6.37 c                   parm                    ndvare
6.37 c                   parm                    ndtek1
6.37 c                   parm                    ndanta
6.37 c                   parm                    ndenhe
6.37 c                   parm                    ndlety
6.37 c                   parm                    ndldat
7.04 c                   parm                    ndfsys
      *
6.37 c                   if        w_vsta = 9
6.37 c                   call      'FD109R'
6.37 c                   parm                    w_kode
6.37 c                   parm                    w_firm
6.37 c                   parm                    fonumm
6.37 c                   parm                    fosuff
6.37 c                   parm                    w_line
6.37 c                   parm                    faalty
6.37 c                   parm                    ndlety
6.37 c                   parm                    ndvare
6.37 c                   parm                    ndanta
6.37 c                   parm                    ndenhe
6.37 c                   parm                    ndldat
6.37 c                   else
8.02  * Hvis parentref er dette er ordre som ikke skal endre priser
8.02 c                   if        b_pref = *on
8.02 c                   call      'FD116R'
8.02 c                   parm                    w_kode
8.02 c                   parm                    w_firm
8.02 c                   parm                    fonumm
8.02 c                   parm                    fosuff
8.02 c                   parm                    w_line
8.02 c                   parm                    faalty
8.02 c                   parm                    ndlety
8.02 c                   parm                    ndvare
8.02 c                   parm                    ndanta
8.02 c                   parm                    ndenhe
8.02 c                   parm                    ndldat
8.02 c                   parm                    ndkpri
8.02 c                   parm                    ndntpr
8.02 c                   parm                    ndrab1
8.02 c                   parm                    ndrab2
8.02 c                   parm                    ndkamp
8.02 c                   else
     c                   call      'FD106R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    fonumm
     c                   parm                    fosuff
     c                   parm                    w_line
     c                   parm                    faalty
     c                   parm                    ndlety
     c                   parm                    ndvare
     c                   parm                    ndanta
     c                   parm                    ndenhe
     c                   parm                    ndldat
8.02 c                   endif
6.37 c                   endif
      *
     c                   else
     c                   eval      w_text = ndtek1 + ndtek2
     c                   call      'FD111R'
     c                   parm                    w_firm
     c                   parm                    fonumm
     c                   parm                    fosuff
     c                   parm                    w_line
     c                   parm                    faatty
     c                   parm                    w_text
     c                   endif
6.32  *
6.32  * Oppdater med ordre og linjenummer på kopiregister
6.32  *
7.02 c                   eval      eddtl3_fsys = ndfsys
7.02 c                   eval      eddtl3_tell = ndtell
7.02 c                   eval      eddtl3_line = ndline
7.02 c     eddtl3_key    chain     eddtl3r
7.02 c                   if        %found
7.02 c                   eval      edonum = fonumm
7.02 c                   eval      edosuf = fosuff
7.02 c                   eval      edolin = w_line
7.02 c                   time                    ededat
7.02 c                   time                    edetim
7.02 c                   eval      edeusr = l_user
7.02 c                   update    eddtl3r
7.02 c                   endif
6.34  *
6.34  * Oppdater med ordre på kopiregister
6.34  *
7.02 c                   eval      edhel4_fsys = ndfsys
7.02 c                   eval      edhel4_tell = ndtell
7.02 c     edhel4_key    chain     edhel4r
7.02 c                   if        %found
7.02 c                   eval      eonumm = fonumm
7.02 c                   eval      eosuff = fosuff
7.02 c                   time                    eoedat
7.02 c                   time                    eoetim
7.02 c                   eval      eoeusr = l_user
7.02 c                   update    edhel4r
7.02 c                   endif
6.36  *
6.36  * Sjekk om det er tekstlinjer som skal legges ut først på ordren
6.36  *
6.36 c                   eval      edtxlr_fsys = ndfsys
6.36 c                   eval      edtxlr_tell = ndtell
6.36 c                   eval      edtxlr_ldor = 0
6.36 c                   eval      edtxlr_ldat = *loval
6.36 c                   eval      edtxlr_line = ndline
6.36 c                   eval      edtxlr_type = 'L'
6.36 c                   eval      edtxlr_tlin = 0
6.36 c     edtxlr_key    setll     edtxlr
6.36 c     edtxlr_key2   reade     edtxlr
6.36 c                   dow       not %eof
6.36  *
6.36 c                   if        %trim(ettek1) <> *blanks
6.36  *
6.36 c                   eval      w_text = %trim(ettek1)
6.36 c                   eval      w_line = w_line + 10
6.36 c                   call      'FD111R'
6.36 c                   parm                    w_firm
6.36 c                   parm                    fonumm
6.36 c                   parm                    fosuff
6.36 c                   parm                    w_line
6.36 c                   parm                    faatty
6.36 c                   parm                    w_text
6.36  *
6.36 c                   endif
6.36  *
6.36 c     edtxlr_key2   reade     edtxlr
6.36 c                   enddo
      *
     c     nodtl1_key    reade     nodtl1
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter inn ordrenummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_nummer   begsr
      *
     c                   eval      w_kode = '0'
     c                   eval      w_fell = faaork
     c                   eval      w_fast = l_wsid
5.32 c                   eval      w_type = w_otyp
5.32  *
5.32  * Hente inn eventuell overstyrt
5.32  * ordretype for nummerserie
5.32  *
5.32 c                   call      'VA752R'
5.32 c                   parm                    w_firm
5.32 c                   parm                    w_otyp
5.32 c                   parm                    w_onum
5.32  *
5.32 c                   if        w_onum <> *blank
5.32 c                   eval      w_type = w_onum
5.32 c                   endif
      *
      * Kaller subprogram for henting av nummer fra nummer-register
      *
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
5.32 c                   parm                    w_type
     c                   parm                    w_fast
     c                   parm                    w_numm
6.23  *
6.23  * sjekker at ordrenr ikke har tidligere relasjoner liggende
6.23  *
6.23 c                   call      'FO415R '
6.23 c                   parm                    w_firm
6.23 c                   parm                    w_numm
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å hente høyeste linjenummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_linjenr  begsr
      *
     c                   eval      fodtl1_numm = fonumm
     c                   eval      fodtl1_suff = fosuff
     c                   eval      fodtl1_line = 9999
     c     fodtl1_key    setll     fodtl1
     c                   readp     fodtl1
     c                   if        not %eof and
     c                             fdfirm = w_firm and
     c                             fdnumm = fonumm and
     c                             fdsuff = fosuff
     c                   eval      w_line = fdline
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer ordre og memosaldo
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdat_ordre  begsr
      *
      * Henter ordretotal
      *
     c                   eval      w_tots = 0
     c                   eval      w_totr = 0
     c                   eval      w_totk = 0
5.60 c                   eval      w_tota = 0
5.60 c                   eval      w_totn = 0
5.60 c                   eval      w_tot1 = 0
5.60 c                   eval      w_tot2 = 0
      *
     c     fodtl1_key2   setll     fodtl1
     c     fodtl1_key2   reade     fodtl1
     c                   dow       not %eof
     c                   eval      w_tots = w_tots + fdsums
     c                   eval      w_totr = w_totr + fdsumr
     c                   eval      w_totk = w_totk + fdsumk
5.60  *
5.60 c                   eval      w_totn = fdsums
5.60 c                   eval      w_tot1 = 0
5.60 c                   eval      w_tot2 = 0
5.60  *
5.60 c                   if        fdrab1 <> 0
5.60 c                   eval      w_tot1 = w_totn * fdrab1 / 100
5.60 c                   eval      w_totn = w_totn - w_tot1
5.60 c                   endif
5.60  *
5.60 c                   if        fdrab2 <> 0
5.60 c                   eval      w_tot2 = w_totn * fdrab2 / 100
5.60 c                   eval      w_totn = w_totn - w_tot2
5.60 c                   endif
5.60  *
5.60  * NB!  w_tot1
5.60  * og   w_tot2 brukes om
5.60  * igjen og null-stilles
5.60  *
5.60 c                   eval      w_tot1 = 0
5.60 c                   eval      w_tot2 = 0
5.60  *
5.60 c                   if        fdbrr1 <> 0
5.60 c                   eval      w_tot1 = w_totn * fdbrr1 / 100
5.60 c                   eval      w_totn = w_totn - w_tot1
5.60 c                   endif
5.60  *
5.60 c                   if        fdbrr2 <> 0
5.60 c                   eval      w_tot2 = w_totn * fdbrr2 / 100
5.60 c                   eval      w_totn = w_totn - w_tot2
5.60 c                   endif
5.60  *
5.60 c                   eval      w_tota = w_tota + w_tot1
5.60 c                   eval      w_tota = w_tota + w_tot2
5.60  *
     c     fodtl1_key2   reade     fodtl1
     c                   enddo
      *
      * Beregner justeringsbeløp, og oppdaterer memosaldo
      *
     c                   eval      w_kode = *blank
     c                   eval      w_sald = w_tots - w_totr
5.60  *
5.60 c                   eval      w_sal2 = w_tota
      *
     c                   call      'FA922R'
     c                   parm                    w_firm
     c                   parm                    footyp
     c                   parm                    fokund
     c                   parm                    w_sald
5.60 c                   parm                    w_sal2
      *
      * Oppdaterer ordre-hode med ny saldo
      *
     c                   eval      fohelu_numm = fonumm
     c                   eval      fohelu_suff = fosuff
     c     fohelu_key    chain     fohelur
      *
     c                   eval      fotots = w_tots
     c                   eval      fototr = w_totr
     c                   eval      fototk = w_totk
5.60 c                   eval      fobrra = w_tota
      *
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = l_user
     c                   update    fohelur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_fsys
     c                   parm                    p_tell
      *
      * Key for oppslag på ekstern ordre-hode
      *
     c     nohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    nohel1_fsys
     c                   kfld                    nohel1_tell
      *
      * Key for les på ekstern ordre-linje
      *
     c     nodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    nodtl1_fsys
     c                   kfld                    nodtl1_tell
      *
      * Key for oppslag på ordre-type
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for oppslag på ordre-status
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på ordre-bruker
      *
     c     fusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fusrl1_user
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      * Key for oppdatering av ekstern ordre-hode
      *
     c     nohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    nohelu_fsys
     c                   kfld                    nohelu_tell
      *
      * Key for oppdatering av ordre-hode
      *
     c     fohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelu_numm
     c                   kfld                    fohelu_suff
      *
      * Key for oppslag på ordre-linje
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
     c                   kfld                    fodtl1_line
      *
     c     fodtl1_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
6.32  *
6.32  * Key for les på ekstern ordre-linje - kopiregister
6.32  *
7.02 c     eddtl3_key    klist
7.02 c                   kfld                    w_firm
7.02 c                   kfld                    eddtl3_fsys
7.02 c                   kfld                    eddtl3_tell
7.02 c                   kfld                    eddtl3_line
6.34  *
6.34  * Key for les på ekstern ordre-hode - kopiregister
6.34  *
7.02 c     edhel4_key    klist
7.02 c                   kfld                    w_firm
7.02 c                   kfld                    edhel4_fsys
7.02 c                   kfld                    edhel4_tell
7.06  *
6.36  * Key for les på ekstern ordre-linje - kopiregister
6.36  *
6.36 c     edtxlr_key    klist
6.36 c                   kfld                    w_firm
6.36 c                   kfld                    edtxlr_fsys
6.36 c                   kfld                    edtxlr_tell
6.36 c                   kfld                    edtxlr_ldor
6.36 c                   kfld                    edtxlr_ldat
6.36 c                   kfld                    edtxlr_line
6.36 c                   kfld                    edtxlr_type
6.36 c                   kfld                    edtxlr_tlin
6.36  *
6.36  * Key for les på ekstern ordre-linje - kopiregister
6.36  *
6.36 c     edtxlr_key2   klist
6.36 c                   kfld                    w_firm
6.36 c                   kfld                    edtxlr_fsys
6.36 c                   kfld                    edtxlr_tell
6.36 c                   kfld                    edtxlr_ldor
6.36 c                   kfld                    edtxlr_ldat
6.36 c                   kfld                    edtxlr_line
6.36 c                   kfld                    edtxlr_type
7.10  *
7.10  * Key for oppslag på selger
7.10  *
7.10 c     ra09l1_key    klist
7.10 c                   kfld                    w_firm
7.10 c                   kfld                    ra09l1_ikod
7.12  *
7.12  * Key for propertiesfil
7.12  *
8.04 c*    afpslr_key    klist
8.04 c*                  kfld                    afpslr_ufil
8.04 c*                  kfld                    afpslr_uide
8.05  *
8.05  * Key for top/bunn tekst linjer
8.05  *
8.05 c     fotxlu_key    klist
8.05 c                   kfld                    w_firm
8.05 c                   kfld                    fotxlu_numm
8.05 c                   kfld                    fotxlu_suff
8.05 c                   kfld                    fotxlu_line
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
      * Henter informasjon fra ordre-status
      *
     c     fstsl1_key    chain     fstsl1
5.53  *
5.53  * Hent mva % for beregning av memosaldo inkl. mva
5.53  *
5.53 c                   eval      w_stat = *blank
5.53 c                   eval      w_mvak = '3'
5.53 c                   time                    w_date
5.53  *
5.53 c                   call      'RS205R'
5.53 c                   parm                    w_stat
5.53 c                   parm                    w_mvak
5.53 c                   parm                    w_mvtx
5.53 c                   parm                    w_date
5.53 c                   parm                    w_mvas
5.53 c                   parm                    w_mvat
5.53 c                   parm                    w_mvaf
5.53 c                   parm                    w_bpro
7.12  *
7.12  *   Hent inn % utvidelse av kreditgrense
7.12  *
8.04 c*                  eval      afpslr_ufil = l_filg
8.04 c*                  eval      afpslr_uide = 'KRGRNSUTV'
8.04 c*    afpslr_key    chain     afpslrr
8.04 c*                  if        %found(afpslr)
8.04 c*                  movel     afudss        w_utvgrns
8.04 c*                  endif
7.12  *
7.12  *   Hent inn ant dager fram ordre skal med i memosaldo
8.04 c*                  eval      afpslr_uide = 'MEMODAGER'
8.04 c*    afpslr_key    chain     afpslrr
8.04 c*                  if        %found(afpslr)
8.04 c*                  movel     afudss        w_3x
8.04 c*                  move      w_3x          w_memodagr
8.04 c*                  endif
7.12  *
7.13  * Utvidet memosaldo
8.04  *   Hent inn % utvidelse av kreditgrense
8.04  *   Hent inn ant dager fram ordre skal med i memosaldo
7.13  *
8.04 c                   eval      w_memodagr = 999
8.04 c                   eval      w_utvgrns  = 0
7.09  *
7.13   CallP CO402R(l_filg:w_firm:0:'UTVIDET_MEMOSALDO':
7.13                    co402_verdi1:co402_verdi2);
7.13   if co402_verdi1 = '1';
7.13     u_713 = *on;
8.04     if  %subst(co402_verdi2:1:3) <> *blank;
8.04       w_memodagr = %dec(%subst(co402_verdi2:1:3):3:0);
8.04     endif;
8.04     if  %subst(co402_verdi2:133:2) <> *blank;
8.04       w_utvgrns = %dec(%subst(co402_verdi2:133:2):2:0);
8.01     endif;
7.13   endif;
      *
     c                   endsr
