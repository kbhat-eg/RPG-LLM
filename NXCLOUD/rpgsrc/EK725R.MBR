     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : EK723R
      * Beskrivelse . . : Kunde bonus, skriver ut bonusbrev pr. kunde
      *
      * Spesialversjon. : Brukes kun på Degernes
      * Tilpasset for . : Degernes
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       010209 ASK  Nytt program
9.01  *       040310 bof  Takle flere bonusgrupper (inntil 99)
6.10  * 6.10  110811 bsa  Tar hensyn til valgt bonustype
6.11  * 6.10  020911 bsa  Sjekker om typen er byggende
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fekboi1    if   e           k disk    rename(ekbost:ekboi1r)
     feppsl1    if   e           k disk    rename(eppspfr:eppsl1r)
      *
     fek723p    o    e             printer
9.01  *
9.01  * Tabeller
9.01  *
9.01 d a_merk          s              4    dim(99)
9.01 d a_besk          s             35    dim(99)
9.01 d a_pros          s              3  1 dim(99)
9.01 d a_bgru          s             11  2 dim(99)
9.01 d a_sbon          s              9  0 dim(99)
      *
     d x               s              2  0
      *
      * Definering av parametre
      *
     d p_firm          s              3
     d p_paar          s              4
6.10 d p_tbot          s              2
      *
      * Definering av Local data area
      *
     d                uds
     d l_dato                101    108
     d l_tek1                201    270
     d l_tek2                301    370
     d l_tek3                401    470
      *
      * Definering av variabler i nøkler
      *
     d eppsl1_aarr     s                   like(epaarr)
     d ekboi1_bkun     s                   like(ekbkun)
      *
      * Definering av variable
      *
     d w_firm          s                   like(ekfirm)
6.10 d w_tbot          s              2
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Legger inn faste verdier i brev
      *
     c                   exsr      prosent
     c                   exsr      tekst
      *
      * Kaller utskrift-prosedyre for alle kunder det er knyttet
      * bonusavtale i mot
      *
     c     rkunl1_key    setll     rkunl1
     c     rkunl1_key    reade     rkunl1
     c                   dow       not %eof
      *
     c                   eval      ekboi1_bkun = rkkund
     c     ekboi1_key    chain     ekboi1
     c                   if        %found
6.10 c                   if        w_tbot <> *blank and
6.10 c                             w_tbot <> ekboty
6.10 c                   goto      ny_tbot
6.10 c                   endif
6.11  * Er denne byggende
6.11 c                   if        ekebyg = 1
6.11 c                   goto      ny_tbot
6.11 c                   endif
6.11  *
     c                   exsr      beregn
6.10 c     ny_tbot       tag
     c                   endif
      *
     c     rkunl1_key    reade     rkunl1
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Leser bonusgrunlag og bonus pr. kunde og akkumulerer pr. varegruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     beregn        begsr
      *
9.01 c                   eval      a_bgru(*) = 0
9.01 c                   eval      a_sbon(*) = 0
      *
     c     ekboi1_key    setll     ekboi1
     c     ekboi1_key    reade     ekboi1
     c                   dow       not %eof
      *
9.01 c                   eval      x = %lookup(%subst(ekmerk:1:4):a_merk)
9.01 c                   if        x > 0
9.01 c                   eval      a_bgru(x) = a_bgru(x) + ekbgru
9.01 c                   eval      a_sbon(x) = a_sbon(x) + eksbon
9.01 c                   endif
      *
     c     ekboi1_key    reade     ekboi1
     c                   enddo
      *
      * Legg inn kundeinformasjon
      *
     c                   eval      a1kunr = rkkund
     c                   eval      a1kuna = rknavn
     c                   eval      a1kuga = rkgate
     c                   eval      a1kuad = rkadr2
     c                   eval      a1kups = rksted
      *
9.01 c                   write     a1hdr
      *
      * Summerer og skriver ut kundebrev
      *
9.01 c                   eval      x = 1
9.01 c                   dow       x < 99
9.01 c                   eval      t1subg = t1subg + a_bgru(x)
9.01 c                   eval      t1subn = t1subn + a_sbon(x)
9.01 C                   eval      x = x + 1
9.01 c                   enddo
9.01  *
9.01  * skriver bonusgrupper
9.01  *
9.01 c                   eval      x = 1
9.01 c                   dow       x < 99
9.01  *
9.01 c                   if        a_merk(x) = *blank
9.01 c                   leave
9.01 c                   endif
9.01  *
9.01 c                   eval      d1merk = a_merk(x)
9.01 c                   movel     a_besk(x)     d1besk
9.01 c                   eval      d1bgru = a_bgru(x)
9.01 c                   eval      d1sbon = a_sbon(x)
9.01 c                   eval      d1pros = a_pros(x)
9.01  *
9.01 c                   write     d1det                                40
9.01 c                   if        *in40 = *on
9.01 c                   write     a1hdr
9.01 c                   eval      *in40 = *off
9.01 c                   endif
9.01  *
9.01 C                   eval      x = x + 1
9.01  *
9.01 c                   enddo
9.01  *
      *
     c                   write     t1tot
      *
      * Nullstiller akkumulatorer etter utskrift
      *
     c                   eval      t1subg = 0
      *
     c                   eval      t1subn = 0
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Leser prosentregister og legger inn prosentsatser i utfelter
      * Leser bonusgrunlag og bonus pr. kunde og akkumulerer pr. varegruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     prosent       begsr
9.01  *
9.01  * Fyller tabellene med prosent,bonusgruppe og beskrivelse
9.01  *
9.01 c                   eval      a_merk(*) = *blank
9.01 c                   eval      a_besk(*) = *blank
9.01 c                   eval      a_pros(*) = 0
9.01 c                   eval      x = 0
9.01  *
9.01 c                   movel     p_paar        eppsl1_aarr
9.01 c     eppsl1_key    setll     eppsl1
9.01 c     eppsl1_key    reade     eppsl1
9.01 c                   dow       not  %eof(eppsl1)
9.01 c                   eval      x = x + 1
9.01 c                   eval      a_merk(x) = epmerk
9.01 c                   eval      a_pros(x) = eppros
9.01 c                   eval      a_besk(x) = epbesk
9.01 c     eppsl1_key    reade     eppsl1
9.01 c                   enddo
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Leser inn undertekster og dato fra parameter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     tekst         begsr
      *
     c                   eval      a1aarr = p_paar
     c                   move      l_dato        a1dato
     c                   eval      t1tek1 = l_tek1
     c                   eval      t1tek2 = l_tek2
     c                   eval      t1tek3 = l_tek3
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_paar
6.10 c                   parm                    p_tbot
      *
      * Key for les på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for les på kundebonus
      *
     c     ekboi1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ekboi1_bkun
      *
      * Key for les av bonussatser
      *
     c     eppsl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    eppsl1_aarr
      *
      * Henter parameter
      *
     c                   move      p_firm        w_firm
     c                   move      p_paar        eppsl1_aarr
6.10 c                   eval      w_tbot = p_tbot
      *
     c                   endsr
