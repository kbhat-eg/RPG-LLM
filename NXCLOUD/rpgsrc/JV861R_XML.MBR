     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JV861R_XML
      * Beskrivelse . . : Oppdater fra NOBB, vare
      *
      *                   Oppdaterer/oppretter varer fra
      *                   NOBB 80 (XML-format)
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       040414 BKV
6.30  * R6.30 230414 BKV  Støtte for NIB nr
6.31  * R6.30 240414 BKV  Støtte for NIB nr
6.32  * R6.30 160714 BKV  Utvidket log record
6.33  * R6.30 150515 BKV  d_lvar og d_pvar økt til 20
6.34  * R6.30 070416 BKV  Logge utgåtte vare-leverandør
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     f                                     infsr(FileErr)
6.31 fjvarl3    if   e           k disk    rename(jvarpfr:jvarl3r)
6.31 f                                     prefix(xa:2)
6.31 f                                     infsr(FileErr)
     fjvdtl1    if   e           k disk    rename(jvdtpfr:jvdtl1r)
     f                                     infsr(FileErr)
     fjvarlu    uf a e           k disk    rename(jvarpfr:jvarlur)
     f                                     infsr(FileErr)
     fjstsl1    if   e           k disk    rename(jstspfr:jstsl1r)
     f                                     infsr(FileErr)
     fjmodl1    if   e           k disk    rename(jmodpfr:jmodl1r)
     f                                     infsr(FileErr)
     fjvdtlu    uf a e           k disk    rename(jvdtpfr:jvdtlur)
     f                                     infsr(FileErr)
     fjvbelu    uf a e           k disk    rename(jvbepfr:jvbelur)
     f                                     infsr(FileErr)
     fjvbel1    if   e           k disk    rename(jvbepfr:jvbel1r)
     f                                     infsr(FileErr)
     fjvallu    uf a e           k disk    rename(jvalpfr:jvallur)
     f                                     infsr(FileErr)
     fjvavlr    if   e           k disk    rename(jvavpfr:jvavlrr)
     f                                     infsr(FileErr)
     fjvavlu    uf a e           k disk    rename(jvavpfr:jvavlur)
     f                                     infsr(FileErr)
     fjvnrlu    uf a e           k disk    rename(jvnrpfr:jvnrlur)
     f                                     infsr(FileErr)
     fjvtxlu    uf a e           k disk    rename(jvtxpfr:jvtxlur)
     f                                     infsr(FileErr)
     fjvomlu    uf a e           k disk    rename(jvompfr:jvomlur)
     f                                     infsr(FileErr)
     fjkatlu    uf a e           k disk    rename(jkatpfr:jkatlur)
     f                                     infsr(FileErr)
      *
      * Definering av parametre
      *
     d p_stat          s              1
      *
      * Brukes som parametre til jobblogging
      *
     d p_jbid          s             41
     d p_jmld          s            200
     d p_jsts          s              2  0
      *
      * Parameter
      *
     d p_vrec          s           5900
     d p_brec          s            100    dim(100)
     d p_trec          s            100    dim(5)
     d p_frec          s            100    dim(5)
     d p_arec          s            100    dim(5)
     d p_nrec          s            200
     d p_krec          s             50    dim(20)
6.34 d p_job_log       s            150
     d p_inlr          s              1
      *
      * Job log out
      *
     d                 ds
6.34 d d_job_log                    150
     d  w_vare                        8    overlay(d_job_log:2)
     d  w_ojva                        8    overlay(d_job_log:10)
     d   w_ojva_num                   8  0 overlay(w_ojva:1)
     d  w_jvar                        8    overlay(d_job_log:18)
     d   w_jvar_num                   8  0 overlay(w_jvar:1)
     d  w_jvdt                        8    overlay(d_job_log:26)
     d   w_jvdt_num                   8  0 overlay(w_jvdt:1)
     d  w_jvpk                        8    overlay(d_job_log:34)
     d   w_jvpk_num                   8  0 overlay(w_jvpk:1)
     d  w_jvpg                        8    overlay(d_job_log:42)
     d   w_jvpg_num                   8  0 overlay(w_jvpg:1)
     d  w_jvpke                       8    overlay(d_job_log:50)
     d   w_jvpke_num                  8  0 overlay(w_jvpke:1)
     d  w_jvpku                       8    overlay(d_job_log:58)
     d   w_jvpku_num                  8  0 overlay(w_jvpku:1)
     d  w_jvpru                       8    overlay(d_job_log:66)
     d   w_jvpru_num                  8  0 overlay(w_jvpru:1)
6.32 d  w_jvlevn                      8    overlay(d_job_log:74)
6.32 d   w_jvlevn_num                 8  0 overlay(w_jvlevn:1)
6.32 d  w_jvleve                      8    overlay(d_job_log:82)
6.32 d   w_jvleve_num                 8  0 overlay(w_jvleve:1)
6.34 d  w_jvlevu                      8    overlay(d_job_log:90)
6.34 d   w_jvlevu_num                 8  0 overlay(w_jvlevu:1)
6.34 d  w_lnr                         5  0 overlay(d_job_log:98)
6.34 d  w_jbid                       41    overlay(d_job_log:103)

      *
      * Definering av variable
      *
     d n               s              2  0
     d  d_lukk         s              1  0
     d w_welm          s           4000
     d w_str_i         s             78
     d b_blnk          s              1    inz(*off)
     d b_frtx          s              1    inz(*off)
     d w_str_78        s             78
     d w_posi          s              4  0
     d w_line          s              3  0
     d d_otyp          s                   like(jvotyp)
     d d_otx1          s                   like(jvotx1)

      *
      *  Dataelementer vare
      *
     d                 ds
     d d_vrec                      5900
     d  d_stat                        1    overlay(d_vrec:2)
     d   d_stat_num                   1  0 overlay(d_stat:1)
     d  d_vare                        8    overlay(d_vrec:3)
     d  d_nobb                        8    overlay(d_vrec:11)
     d   d_nobb_num                   8  0 overlay(d_nobb:1)
     d  d_vtx1                       35    overlay(d_vrec:19)
     d  d_vtx2                       35    overlay(d_vrec:54)
     d  d_modn                        8    overlay(d_vrec:89)
     d   d_modn_num                   8  0 overlay(d_modn:1)
6.33 d  d_lvar                       20    overlay(d_vrec:97)
6.33 d  d_pvar                       20    overlay(d_vrec:117)
6.33 d  d_eann                       13    overlay(d_vrec:137)
6.33 d   d_eann_num                  13  0 overlay(d_eann:1)
6.33 d  d_stre                       15    overlay(d_vrec:150)
6.33 d  d_leve                        1    overlay(d_vrec:165)
6.33 d   d_leve_num                   1  0 overlay(d_leve:1)
6.33 d  d_doki                       20    overlay(d_vrec:166)
6.33 d  d_preh                        3    overlay(d_vrec:186)
6.33 d  d_ete1                       35    overlay(d_vrec:189)
6.33 d  d_mmk1                        5    overlay(d_vrec:224)
6.33 d  d_mmk2                        5    overlay(d_vrec:229)
6.33 d  d_mlk1                        3    overlay(d_vrec:234)
6.33 d  d_mlk2                        3    overlay(d_vrec:237)
6.33 d  d_ftxt                      256    overlay(d_vrec:240)
6.33 d  d_crea                       10    overlay(d_vrec:496)
6.33 d   d_crea_iso                    d   overlay(d_crea:1) datfmt(*iso)
6.33 d  d_upda                       10    overlay(d_vrec:506)
6.33 d   d_upda_iso                    d   overlay(d_upda:1) datfmt(*iso)
6.33 d  d_dele                       10    overlay(d_vrec:516)
6.33 d   d_dele_iso                    d   overlay(d_dele:1) datfmt(*iso)
6.33 d  d_gsts                        1    overlay(d_vrec:526)
6.33 d   d_gsts_num                   1  0 overlay(d_gsts:1)
6.33 d  d_enob                        8    overlay(d_vrec:527)
6.33 d   d_enob_num                   8  0 overlay(d_enob:1)
6.33 d  d_utgd                       10    overlay(d_vrec:535)
6.33 d   d_utgd_iso                    d   overlay(d_utgd:1) datfmt(*iso)
6.33 d  d_vtype                       2    overlay(d_vrec:545)
6.33 d  d_vtxlang                   100    overlay(d_vrec:547)
6.33 d  d_uarb                        5    overlay(d_vrec:647)
6.33 d   d_uarb_num                   1  0 overlay(d_uarb:1)
6.33 d  d_lfor                        5    overlay(d_vrec:652)
6.33 d   d_lfor_num                   1  0 overlay(d_lfor:1)
6.33 d  d_hdat                        5    overlay(d_vrec:657)
6.33 d   d_hdat_num                   1  0 overlay(d_hdat:1)
6.33 d  d_tfro                        5    overlay(d_vrec:662)
6.33 d   d_tfro_num                   1  0 overlay(d_tfro:1)
6.33 d  d_fjernes                     5    overlay(d_vrec:667)
6.33 d  d_hpse                        5    overlay(d_vrec:672)
6.33 d   d_hpse_num                   1  0 overlay(d_hpse:1)
6.33 d  d_senh                        5    overlay(d_vrec:677)
6.33 d  d_unnr                        4    overlay(d_vrec:682)
6.33 d  d_fanr                        6    overlay(d_vrec:686)
6.33 d  d_fabs                      100    overlay(d_vrec:692)
6.33 d  d_fgru                       30    overlay(d_vrec:792)
6.33 d  d_embm1                      30    overlay(d_vrec:822)
6.33 d  d_embm2                      30    overlay(d_vrec:852)
6.33 d  d_embm3                      30    overlay(d_vrec:882)
6.33 d  d_tono                       15    overlay(d_vrec:912)
6.33 d  d_toeu                       15    overlay(d_vrec:927)
6.33 d  d_sort                       10    overlay(d_vrec:942)
6.33 d  d_sldo                        6    overlay(d_vrec:952)
6.33 d   d_sldo_num                   6  0 overlay(d_sldo:1)
6.33 d  d_kate                        3    overlay(d_vrec:958)
6.33 d   d_kate_num                   3  0 overlay(d_kate:1)
6.33 d  d_bran                       35    overlay(d_vrec:961)
6.33 d  d_utko                        1    overlay(d_vrec:996)
6.33 d  d_opps                      100    overlay(d_vrec:997)
6.33 d  d_mers                      100    overlay(d_vrec:1097)
6.33 d  d_kaom                       40    overlay(d_vrec:1197)
6.33 d  d_besk                     4000    overlay(d_vrec:1237)
      *
      *  Dataelementer består av vare
      *
     d d_brec                       100
     d  d_uvar                        8    overlay(d_brec:2)
     d   d_uvar_num                   8  0 overlay(d_uvar:1)
     d  d_ubnob                       8    overlay(d_brec:10)
     d   d_ubnob_num                  8  0 overlay(d_ubnob:1)
     d  d_ugti                       14    overlay(d_brec:18)
     d   d_ugti_num                  14  0 overlay(d_ugti:1)
     d  d_uenh                        3    overlay(d_brec:32)
     d  d_uant                       15    overlay(d_brec:35)
     d   d_uant_num                  12  6 overlay(d_uant:1)
      *
      *  Dataelementer TUNKoblinger / FINFOKoblinger
      *
     d                 DS
     d d_tfrec                      100
     d  d_bnob                        8    overlay(d_tfrec:2)
     d   d_bnob_num                   8  0 overlay(d_bnob:1)
     d  d_btyp                        1    overlay(d_tfrec:10)
     d  d_bvar                       15    overlay(d_tfrec:11)
     d  d_btxt                       50    overlay(d_tfrec:26)
      *
      * Avgifter
      *
     d                 DS
     d d_arec                       100
     d  d_anob                        8    overlay(d_arec:2)
     d   d_anob_num                   8  0 overlay(d_anob:1)
     d  d_akod                        3    overlay(d_arec:10)
     d  d_atxt                       50    overlay(d_arec:13)
      *
      * NRF
      *
     d                 DS
     d d_nrec                       200
     d  d_nnob                        8a   overlay(d_nrec:2)
     d   d_nnob_num                   8  0 overlay(d_nnob:1)
     d  d_nnrf                       10a   overlay(d_nrec:10)
     d   d_nnrf_num                   8  0 overlay(d_nnrf:1)
     d  d_ngrp                       10a   overlay(d_nrec:20)
     d  d_nldo                       10a   overlay(d_nrec:30)
     d  d_ntx1                       60a   overlay(d_nrec:40)                   Dim
     d  d_ntx2                       60a   overlay(d_nrec:100)                  Navn
     d  d_ntx3                       40a   overlay(d_nrec:160)                  Tillegg
      * Kategori
     d                 DS
     d d_krec                        50
     d  d_katc                        2    overlay(d_krec:2)
     d   d_katc_num                   2  0 overlay(d_katc:1)
     d  d_kata                        3    overlay(d_krec:4)
     d   d_kata_num                   3  0 overlay(d_kata:1)
     d  d_kato                       40    overlay(d_krec:7)

      *
      * Definering av variable i nøkler
      *
     d jvarl1_vare     s                   like(jvvare)
     d jvarlu_vare     s                   like(jvvare)
     d jmodl1_modn     s                   like(jumodn)
     d jvdtl1_dvar     s                   like(jvdvar)
     d jvbelr_uvar     s                   like(jvuvar)
     d jvbelu_uvar     s                   like(jvuvar)
     d jvbelu_ubar     s                   like(jvubva)
     d jvallu_btyp     s                   like(jvbtyp)
     d jvallu_bvar     s                   like(jvbvar)
     d jvallu_bnob     s                   like(jvbnob)
     d jvavlr_anob     s                   like(jvanob)
     d jvavlr_akod     s                   like(jvakod)
     d jvavlu_anob     s                   like(jvanob)
     d jvavlu_akod     s                   like(jvakod)
     d jvnrlu_nnob     s                   like(jvnnob)
     d jvnrlu_nnrf     s                   like(jvnnrf)
     d jvtxlu_vare     s                   like(jvtvar)
     d jvomlu_nnob     s                   like(jvonob)
     d jvomlu_otyp     s                   like(jvotyp)
     d jkatlu_kate     s                   like(jkkate)
6.31 d jvarl3_nobb     s                   like(jvnobb)
      *
      * Definering av variable for debug
      *
      * d jvvare_db       s                   like(jvvare)
      * d db1             s                   like(jvvare)
      *
      *
      * Definering av variable
      *
     d b_vfel          s              1    inz(*off)
     d w_stat          s                   like(jvstat)
     d w_firm          s                   like(jvdfir)
      *
      *
      *
      * Definering av Local Data Area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      *
      * Datastruktur for informasjon ved exceptions
      *
     d PgmError       sds
     d  proc_name        *proc
     d  pgm_status       *STATUS
     d  prv_status            16     20s 0
     d  line_numm             21     28
     d  routine          *ROUTINE
     d  parms            *PARMS
     d  excp_type             40     42
     d  excp_numm             43     46
     d  pgm_lib               81     90
     d  excp_data             91    170
     d  excp_id              171    174
     d  date                 191    198
     d  year                 199    200s 0
     d  last_file            201    208
     d  file_info            209    243
     d  job_name             244    253
     d  job_user             254    263
     d  job_numb             264    269s 0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

     c                   eval      d_job_log = p_job_log
     c                   eval      p_jbid = w_jbid
     c                   if        p_inlr = *on
     c                   eval      d_vrec = p_vrec
     c
     c                   if        (d_nobb_num <> *zero)
     c                   exsr      vare_init
     c
     c                   exsr      uttr_nobb
     c
     c                   exsr      skr_vare
     c
      * Består av vare
     c                   exsr      del_bvare
     c                   for       n = 1 to 100
     c                   eval      d_brec = p_brec(n)
     c                   if        (d_uvar_num = *zero)
     c                   leave
     c                   endif
     c                   exsr      skr_bvare
     c                   endfor
      * TUNKoblinger
     c                   eval      d_tfrec = p_trec(1)
     c                   if        (d_bnob_num <> *zero)
     c                   exsr      del_valt
     c                   for       n = 1 to 5
     c                   eval      d_tfrec = p_trec(n)
     c                   if        (d_bnob_num = *zero)
     c                   leave
     c                   endif
     c                   exsr      skr_valt
     c                   endfor
     c                   endif
      * FINFOKoblinger
     c                   eval      d_tfrec = p_frec(1)
     c                   if        (d_bnob_num <> *zero)
     c                   exsr      del_valt
     c                   for       n = 1 to 5
     c                   eval      d_tfrec = p_frec(n)
     c                   if        (d_bnob_num = *zero)
     c                   leave
     c                   endif
     c                   exsr      skr_valt
     c                   endfor
     c                   endif
      * Avgifter
     c                   for       n = 1 to 5
     c                   eval      d_arec = p_arec(n)
     c                   if        (d_anob_num <> *zero)
     c                   exsr      skr_vagf
     c                   else
     c                   leave
     c                   endif
     c                   endfor
      * NRF
     c                   eval      d_nrec = p_nrec
     c                   if        (d_nnob_num <> *zero)
     c                   exsr      skr_nrfd
     c                   endif
     c
      * Varebeskrivelse
     c                   if        (%Trim(d_besk) <> '')
     c                   exsr      beh_fritekst
     c                   endif
      * Oppsalg
     c                   if        (%Trim(d_opps) <> '')
     c                   eval      d_otyp = 'O'
     c                   eval      d_otx1 = d_opps
     c                   exsr      skr_oppmer
     c                   endif
      * Mersalg
     c                   if        (%Trim(d_mers) <> '')
     c                   eval      d_otyp = 'M'
     c                   eval      d_otx1 = d_mers
     c                   exsr      skr_oppmer
     c                   endif
     c
     c                   endif
      * Kategori
     c                   eval      d_krec = p_krec(1)
     c                   if        (d_katc_num > 0)
     c                   for       n = 1 to 20
     c                   eval      d_krec = p_krec(n)
     c                   if        (d_kato = *blank)
     c                   leave
     c                   endif
     c                   exsr      skr_kateg
     c                   endfor
     c                   endif
     c
     c                   eval      p_job_log = d_job_log
     c
     c                   else
     c                   eval      *inlr = *on
     c
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     avslutt       tag
     c
     c                   if        p_inlr <> *on
     c                   eval      *inlr = *on
     c                   endif
     c
     c                   return
     c
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av vareinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     vare_init     begsr
     c
     c                   if        d_enob = ''
     c                   eval      d_enob_num = *zero
     c                   endif
     c                   if        %char(d_utgd_iso) = ''
     c                   eval      d_utgd_iso = *loval
     c                   endif
     c                   if        %char(d_crea_iso) = ''
     c                   eval      d_crea_iso = *loval
     c                   endif
     c                   if        %char(d_upda_iso) = ''
     c                   eval      d_upda_iso = *loval
     c                   endif
     c                   if        %char(d_dele_iso) = ''
     c                   eval      d_dele_iso = *loval
     c                   endif
     c
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_vare      begsr
      *
     c                   if        b_vfel = *on
     c                   leavesr
     c                   endif
     c
     c                   clear                   jvarlur
     c
6.30 c                   eval      jvarl1_vare = d_vare
     c
     c     jvarl1_key    chain     jvarl1
     c
      * --------------------------
      *
      * Behandling avhengig av om vare er endret eller ny
      *
     c                   if        %found(jvarl1)
     c                   exsr      endr_vare
     c                   else
     c                   exsr      ny_vare
     c                   endif
     c
      *
      * Oppretter eller oppdaterer vare
      *
     c                   eval      jvarlu_vare = jvvare
     c     jvarlu_key    chain     jvarlur
      *
     c                   eval      jvogrp = 0
     c                   eval      jvhgrp = 0
     c                   eval      jvugrp = 0
     c                   eval      jvtek1 = d_vtx1
     c                   eval      jvtek2 = d_vtx2
     c                   eval      jvftxt = d_ftxt
     c                   eval      jvnobb = d_nobb_num
     c                   eval      jvmodn = d_modn_num
     c                   eval      jvprod = 0
     c                   eval      jvpvar = d_pvar
     c                   eval      jvldor = 0
     c                   eval      jvlvar = d_lvar
     c*                  eval      jvleve = d_leve_num
     c*                  eval      jvdoki = d_doki
     c                   eval      jvsenh = d_senh
     c                   eval      jvpenh = d_preh
     c                   eval      jvmmk1 = d_mmk1
     c                   eval      jvmmk2 = d_mmk2
     c                   eval      jvmlk1 = d_mlk1
     c                   eval      jvmlk2 = d_mlk2
     c                   eval      jvunnr = d_unnr
     c                   eval      jvfanr = d_fanr
     c                   eval      jvfabs = d_fabs
     c                   eval      jvemgr = d_fgru
     c                   eval      jvembm = d_embm1
     c                   eval      jvtype = d_vtype
     c                   eval      jvltxt = d_vtxlang
     c                   eval      jvuarb = d_uarb_num
     c                   eval      jvlfor = d_lfor_num
     c                   eval      jvhdat = d_hdat_num
     c                   eval      jvtfro = d_tfro_num
     c                   eval      jvhpse = d_hpse_num
     c                   eval      jvtono = d_tono
     c                   eval      jvtoeu = d_toeu
     c                   eval      jvsort = d_sort
     c                   eval      jvsldo = d_sldo_num
     c                   eval      jvkate = d_kate_num
     c                   eval      jvbran = d_bran
     c                   eval      jvutko = d_utko
     c                   eval      jvgsts = d_gsts_num
     c                   eval      jvenob = d_enob_num
     c                   eval      jvudat = d_utgd_iso
     c                   eval      jveusr = l_user
     c                   eval      jvnoda = d_crea_iso
     c                   eval      jvnbed = d_upda_iso
     c                   eval      jvnsda = d_dele_iso
      *
     c                   time                    jvedat
     c                   time                    jvetim
      *
     c                   if        jvmodn <> 0
     c                   eval      jmodl1_modn = jvmodn
     c     jmodl1_key    chain     jmodl1
     c                   if        %found(jmodl1)
     c                   eval      jvldor = juldor
     c                   eval      jvprod = juprod
     c                   eval      jvogrp = juogrp
     c                   eval      jvhgrp = juhgrp
     c                   eval      jvugrp = juugrp
     c                   endif
     c                   endif
      *
     c                   if        %found(jvarlu)
     c                   eval      jvstat = w_stat
     c                   eval      w_ojva_num = w_ojva_num + 1
     c                   update    jvarlur
     c                   else
     c                   time                    jvodat
     c                   eval      w_jvar_num = w_jvar_num + 1
     c                   write     jvarlur
     c                   endif
     c
     c                   eval      w_vare = jvvare
      *
      * Oppretter vare-detalj
      *
     c                   eval      jvdtl1_dvar = jvvare
     c     jvdtl1_key    chain     jvdtl1
     c                   if        not %found(jvdtl1)
     c                   eval      jvdfir = w_firm
     c                   eval      jvdvar = jvvare
     c                   eval      jvdpko = *blank
     c                   eval      jvdsko = 0
     c                   eval      jvdhko = 1
     c                   eval      jvdnod = d_crea_iso
     c                   eval      jvdned = d_upda_iso
     c                   eval      jvdnsd = d_dele_iso
     c                   eval      w_jvdt_num = w_jvdt_num + 1
     c                   write     jvdtlur
     c                   endif
     c
     c
     c                   endsr
     c
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av endret vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     endr_vare     begsr
      *
      * Dersom varestatus forskjellig fra 0, 1 og 4 beholdes varestatus
      *
     c                   if        jvstat = 0 or
     c                             jvstat = 1 or
     c                             jvstat = 4
     c                   eval      w_stat = 1
     c                   else
     c                   eval      w_stat = jvstat
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av ny vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ny_vare       begsr
      *
     c                   eval      jvstat = 2
      *
6.30 c                   eval      jvvare = d_vare
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av vareelement : NOBB-nr.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_nobb     begsr
      *
6.30 c                   if        %LEN(%TRIM(d_nobb)) <> 8
     c                   eval      b_vfel = *on
     c                   endif
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av består av varer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_bvare     begsr
      *
6.31 c                   eval      jvarl3_nobb = %Int(d_ubnob)
      *
6.31 c     jvarl3_key    chain     jvarl3
6.31 c                   if        not %found
6.31 c                   leavesr
6.31 c                   endif

     c                   clear                   jvbelur
     c
     c                   eval                    jvuvar = d_uvar
6.31 c                   eval                    jvubva = xavare
     c                   eval                    jvuenh = d_uenh
     c                   eval                    jvugti = d_ugti_num
     c                   eval                    jvuant = d_uant_num
     c                   eval                    jvueus = l_user
      *
     c                   time                    jvuoda
     c                   time                    jvueda
     c                   time                    jvueti
      *
     c                   write     jvbelur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sletting av består av varer på JVBEPF
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     del_bvare     begsr
      *
      * Sletter tilhørende består av varer
      *
     c                   eval      jvbelr_uvar = d_nobb
      *
     c                   eval      jvbelu_uvar = d_nobb
     c                   eval      jvbelu_ubar = *loval
     c     jvbelu_key    setll     jvbelu
     c     jvbelr_key    reade     jvbelur
     c                   dow       not %eof
     c                   delete    jvbelur
     c     jvbelr_key    reade     jvbelur
     c                   enddo
      *
     c                   endsr

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av Vare-Alternative varenr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_valt      begsr
      *
     c                   clear                   jvallur
      *
     c                   eval      jvbnob = d_bnob_num
     c                   eval      jvbtyp = d_btyp
     c                   eval      jvbvar = d_bvar
     c                   eval      jvbtxt = d_btxt
      *
     c                   time                    jvbeda
     c                   time                    jvbeti
     c                   time                    jvboda
     c                   time                    jvboti
     c                   eval      jvbusr = l_user
      *
     c                   write     jvallur
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sletting av Vare-Alternative varenr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     del_valt      begsr
      *
      *
     c                   eval      jvallu_btyp = d_btyp
     c                   eval      jvallu_bnob = d_bnob_num
     c                   eval      jvallu_bvar = *blanks
     c     jvallu_key    setll     jvallu
     c     jvallu_key2   reade     jvallur
     c                   dow       not %eof
     c                   delete    jvallur
     c     jvallu_key2   reade     jvallur
     c                   enddo
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av Vare Avgift
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_vagf      begsr
      *
     c                   eval      jvavlu_anob = d_anob_num
     c                   eval      jvavlu_akod = d_akod
     c     jvavlu_key    chain     jvavlur
      *
     c                   eval      jvatxt = d_atxt
      *
     c                   time                    jvaeda
     c                   time                    jvaeti
     c                   eval      jvausr = l_user
      *
     c                   if        %found
     c                   update    jvavlur
     c                   else
     c                   eval      jvanob = d_anob_num
     c                   eval      jvakod = d_akod
     c                   time                    jvaoda
     c                   time                    jvaoti
     c                   write     jvavlur
     c                   endif
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av NRF Data
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_nrfd      begsr
      *
     c                   clear                   jvnrlur
      *
     c                   eval      jvnrlu_nnob = d_nnob_num
     c                   eval      jvnrlu_nnrf = d_nnrf_num
     c     jvnrlu_key    chain     jvnrlur
      *
     c                   eval      jvngrp = d_ngrp
     c                   eval      jvnldo = d_nldo
     c                   eval      jvntx1 = d_ntx1
     c                   eval      jvntx2 = d_ntx2
     c                   eval      jvntx3 = d_ntx3
      *
     c                   time                    jvneda
     c                   time                    jvneti
     c                   eval      jvnusr = l_user
      *
     c                   if        %found
     c                   update    jvnrlur
     c                   else
     c                   eval      jvnnob = d_nnob_num
     c                   eval      jvnnrf = d_nnrf_num
     c                   time                    jvnoda
     c                   time                    jvnoti
     c                   write     jvnrlur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av Varebeskrivelse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     beh_fritekst  begsr
      *
     c                   eval      w_line = 0
     c                   eval      w_welm = d_besk
     c                   eval      b_blnk = *off
     c                   eval      b_frtx = *off
      * Sletter 'gammel' fritekst
6.30 c                   eval      jvtxlu_vare = jvvare
     c     jvtxlu_key    setll     jvtxlu
     c     jvtxlu_key    reade     jvtxlur
     c                   dow       not %eof
     c                   delete    jvtxlur
     c     jvtxlu_key    reade     jvtxlur
     c                   enddo
      * Splitter opp teksten i strenger på 78 posisjoner
     c                   dow       b_frtx = *off
      *
     c                   eval      w_posi = 78
     c                   eval      w_str_78 = *blank
     c                   movel     w_welm        w_str_78
     c                   eval      b_blnk = *off
      * Vi begynner å lese bakfra
     c                   dow       b_blnk = *off
     c                   if        %subst(w_str_78:w_posi:1) = *blank
     c                   eval      b_blnk = *on
     c                   else
     c                   eval      w_posi  = w_posi - 1
     c                   endif
     c                   if        w_posi < 2
     c                   eval      b_blnk = *on
     c                   eval      w_posi = 78
     c                   endif
     c                   enddo
      *
     c                   if        w_posi > 0
     c                   eval      w_str_i = %subst(w_str_78:1:w_posi)
     c                   exsr      oppd_fritekst
     c                   endif
     c                   eval      w_welm = %subst(w_welm:w_posi)
     c                   if        %trim(w_welm) = *blanks
     c                   eval      b_frtx = *on
     c                   endif
      *
     c                   enddo
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer varebeskrivelse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_fritekst begsr
      *
     c                   clear                   jvtxlur
      *
     c                   eval      w_line = w_line + 1
      *
6.30 c                   eval      jvtvar = jvvare
     c                   eval      jvtlin = w_line
     c                   eval      jvttxt = %trim(w_str_i)
     c                   eval      jvteus = l_user
      *
     c                   time                    jvtoda
     c                   time                    jvteda
     c                   time                    jvteti
      *
     c                   write     jvtxlur
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Ny oppsalg/mersalg
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_oppmer    begsr
      *
     c                   clear                   jvomlur
      *
     c                   eval      jvomlu_nnob = d_nobb_num
     c                   eval      jvomlu_otyp = d_otyp
     c     jvomlu_key    chain     jvomlur


     c                   eval      jvotx1 = d_otx1
     c                   eval      jvoous = l_user
     c                   eval      jvoeus = l_user
     c                   time                    jvoeda
     c                   time                    jvoeti
      *
     c                   if        %found
     c                   update    jvomlur
     c                   else
     c                   eval      jvonob = d_nobb_num
     c                   eval      jvotyp = d_otyp
     c                   time                    jvooda
     c                   time                    jvooti
     c                   write     jvomlur
     c                   endif
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Ny kategori
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_kateg     begsr
      *
     c                   clear                   jkatlur
      *
     c                   eval      jkatlu_kate = d_kata_num
     c     jkatlu_key    chain     jkatlur
      *
      *
     c                   eval      jknavn = d_kato
     c                   eval      jkeusr = l_user
     c                   time                    jkedat
     c                   time                    jketim
      *
     c                   if        %found
     c                   update    jkatlur
     c                   else
     c                   eval      jkkate = d_kata_num
     c                   time                    jkodat
     c                   time                    jkotim
     c                   write     jkatlur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Error rutine for file errors
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     FileErr       begsr
      *
     c                   eval      p_stat = '1'
     c                   eval      p_jsts = 50
     c                   eval      p_jmld = 'Fil ' + %trim(last_file) +
     c                             ' feil. Info=' + %trim(excp_data) +
     c                             '. Job=' + %trim(job_name) + '/' +
     c                             %trim(job_user) + '/' +
     c                             %char(job_numb)

      *
     c                   call      'AB705R'
     c                   parm                    p_jbid
     c                   parm                    p_jsts
     c                   parm                    p_jmld
      *
     c                   goto      avslutt
      *
     c                   endsr

     c
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_vrec
     c                   parm                    p_brec
     c                   parm                    p_trec
     c                   parm                    p_frec
     c                   parm                    p_arec
     c                   parm                    p_nrec
     c                   parm                    p_krec
     c                   parm                    p_job_log
     c                   parm                    p_inlr
     c
      *
      * Key for oppslag på vare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
     c
      *
      * Key for oppslag på vare-detalj
      *
     c     jvdtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jvdtl1_dvar
      *
      * Key for oppslag på modul
      *
     c     jmodl1_key    klist
     c                   kfld                    jmodl1_modn
     c
      *
      * Key for oppdatering av vare
      *
     c     jvarlu_key    klist
     c                   kfld                    jvarlu_vare
     c
      *
      * Key for oppdatering av består av vare
      *
     c     jvbelu_key    klist
     c                   kfld                    jvbelu_uvar
     c                   kfld                    jvbelu_ubar
     c
      *
      * Key for oppslag av består av vare
      *
     c     jvbelr_key    klist
     c                   kfld                    jvbelr_uvar
     c
6.31  *
6.31  * Key for posisjonering på Nobb-nummer
6.31  *
6.31 c     jvarl3_key    klist
6.31 c                   kfld                    jvarl3_nobb
      *
      * Key for oppdatering av alt.varenr
      *
     c     jvallu_key    klist
     c                   kfld                    jvallu_bnob
     c                   kfld                    jvallu_btyp
     c                   kfld                    jvallu_bvar
      *
      * Key for les på alt.varenr
      *
     c     jvallu_key2   klist
     c                   kfld                    jvallu_bnob
     c                   kfld                    jvallu_btyp
      *
      * Key for oppdatering av avgift
      *
     c     jvavlu_key    klist
     c                   kfld                    jvavlu_anob
     c                   kfld                    jvavlu_akod
      *
      * Key for les av avgift
      *
     c     jvavlr_key    klist
     c                   kfld                    jvavlr_anob
     c                   kfld                    jvavlr_akod
      *
      * Key for oppdatering av avgift
      *
     c     jvnrlu_key    klist
     c                   kfld                    jvnrlu_nnob
     c                   kfld                    jvnrlu_nnrf
      *
      * Key for oppdatering av modul-tekst
      *
     c     jvtxlu_key    klist
     c                   kfld                    jvtxlu_vare
      *
      * Key for oppdatering av oppsalg mersalg
      *
     c     jvomlu_key    klist
     c                   kfld                    jvomlu_nnob
     c                   kfld                    jvomlu_otyp
      *
      * Key for oppdatering av kategori
      *
     c     jkatlu_key    klist
     c                   kfld                    jkatlu_kate
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = l_firm
      *
      *
     c                   endsr
