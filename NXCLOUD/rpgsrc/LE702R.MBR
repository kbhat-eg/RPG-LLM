     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System  . . . . : ASLAGR
      *  Program . . . . : LE702R
      *  Beskrivelse . . : Reversering av telle-liste utfra telle-bunt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      * --------- ------  --------------------------------------------------
      *  6.10  170609 BSA  Oppdatert med sporingsinformasjon
8.01  *  800   200422 ask  Mulighet for å slette linje i telleliste
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     flbchl1    if   e           k disk    rename(lbchpfr:lbchl1r)
     flbhelu    uf   e           k disk    rename(lbhepfr:lbhelur)
     flbdtl1    if   e           k disk    rename(lbdtpfr:lbdtl1r)
     fltellu    uf   e           k disk    rename(ltelpfr:ltellur)
     fltell2    if   e           k disk    rename(ltelpfr:ltell2r)
      *
      *  Parameter-inn
      *
     d                 ds
     d p_prec                        20
     d p_firm                         3  0 overlay(p_prec)
     d p_batc                        10    overlay(p_prec:4)
     d p_numm                         6  0 overlay(p_prec:14)
     d p_kode                         1    overlay(p_prec:20)
      *
      *  Local Data Area
      *
     d                uds
6.10 d l_user                911    920
     d l_firm                944    946  0
      *
      *
      *   Definering av variable i nøkler
      *
     d lbchl1_batc     s                   like(lcbatc)
     d lbdtl1_batc     s                   like(lvbatc)
     d lbdtl1_line     s                   like(lvline)
     d lbdtl1_numm     s                   like(lvnumm)
     d lbhelu_batc     s                   like(lubatc)
     d lbhelu_numm     s                   like(lunumm)
     d ltelll2_batc    s                   like(lebatc)
     d ltellu_batc     s                   like(lebatc)
     d ltellu_line     s                   like(leline)
     d ltellu_numm     s                   like(lenumm)
     d ltellu_suff     s                   like(lesuff)
     d ltell2_dlop     s                   like(ledlop)
     d ltell2_lage     s                   like(lelage)
     d ltell2_numm     s                   like(lenumm)
     d ltell2_suff     s                   like(lesuff)
     d ltell2_vare     s                   like(levare)
     d vvarl1_vare     s                   like(vvvare)
     d vvenl1_vare     s                   like(vevare)
      *
      *   Definering av variable
      *
     d w_parm          s             20
     d w_firm          s                   like(vvfirm)
     d w_omrl          s                   like(veomre)
     d w_omrs          s                   like(veomre)
8.01 d w_slet          s              1
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Behandler LAGERBUNT-HODE-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Blanker kode for at bunten er overført
      *
     c     lbhelu_key    chain     lbhelu
     c                   if        %found and
     c                             lukode = 1
     c                   eval      lukode = 0
6.10 c                   time                    luedat
6.10 c                   time                    luetim
6.10 c                   eval      lueusr = l_user
     c                   update    lbhelur
     c                   else
     c                   goto      end_pgm
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Behandler LAGERBUNT-LINJE-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Lese file
      *
     c     lbdtl1_key    setll     lbdtl1
     c                   read      lbdtl1
      *
     c                   dow       not %eof
      *
      *  Hopper ut dersom ikke samme batchnummer, samme buntnummer
      *
     c                   if        lbdtl1_batc <> lvbatc
     c                   leave
     c                   endif
     c                   if        lbdtl1_numm <> lvnumm
     c                   leave
     c                   endif
      *
      *  Kun linjer som ikke er tekstlinjer skal oppdatere hovedliste
      *  Ikke oppdatering dersom antall er null
      *
     c                   if        lvantt <> 0
     c                   exsr      opptel
     c                   endif
      *
     c                   read      lbdtl1                                 15
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Oppdatering av LAGER-TELLE-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     opptel        begsr
      *
      *  Dersom linjenummer finnes i bunt-fil benyttes det
      *
     c                   if        lvtlin <> 0
     c                   eval      leline = lvtlin
     c                   else
      *
      *  Hent innformasjon fra LAGER-TELLE-VARE-REGISTER
      *
     c                   eval      ltell2_vare = lvvare
     c                   eval      ltell2_lage = lulage
     c                   eval      ltell2_dlop = lvdlop
     c     ltell2_key    chain     ltell2
     c                   endif
      *
      *  Oppdaterer LAGER-TELLE-REGISTER
      *
     c                   eval      ltellu_line = leline
8.01 c     ltellu_key    chain     ltellu
8.01 c                   if        not %found(ltellu)
8.01 c                   goto      end_opptel
8.01 c                   endif
      *
      *  Hent opplysninger fra VARE-REGISTER
      *
     c                   eval      vvarl1_vare = lvvare
     c     vvarl1_key    chain     vvarl1r
     c                   if        %found
     c                   exsr      omr_antall
     c                   endif
      *
     c                   eval      leantt = leantt - lvantt
     c                   eval      leantb = leantb - lvantt
      *
     c                   if        leantt < 0
     c                   eval      leantt = 0
     c                   endif
     c                   if        leantb < 0
     c                   eval      leantb = 0
     c                   endif
      *
8.01 c                   if        lutype = 'DIVERSE' and
8.01 c                             leantt = 0
8.01 c                   delete    ltellur
8.01 c                   goto      end_opptel
8.01 c                   endif
      *
8.01 c                   if        w_slet = '1' and
8.01 c                             leantt = 0 and
8.01 c                             leantb = 0
8.01 c                   delete    ltellur
8.01 c                   goto      end_opptel
8.01 c                   endif
      *
6.10 c                   time                    leedat
6.10 c                   time                    leetim
6.10 c                   eval      leeusr = l_user
     c                   update    ltellur
      *
8.01 c     end_opptel    endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for omregning av antall fra opptalt til lagerenhet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     omr_antall    begsr
      *
      * Dersom opptalt enhet er lik lagerenhet beholdes antall
      *
     c                   if        lvenh1 = vvenhl
     c                   goto      end_omr
     c                   endif
      *
      * Finner omregningsfaktor volum for lagerenhet og for opptalt enhet
      * Antall regnes om etter forholdet mellom disse
      *
     c                   eval      vvenl1_vare = lvvare
     c     vvenl1_key    setll     vvenl1
     c     vvenl1_key    reade     vvenl1
     c                   dow       not %eof
     c                   if        lvenh1 = veenhe
     c                   eval      w_omrl = veomre
     c                   endif
     c                   if        vvenhl = veenhe
     c                   eval      w_omrs = veomre
     c                   endif
     c     vvenl1_key    reade     vvenl1
     c                   enddo
      *
     c                   if        w_omrs = 0
     c                   eval      w_omrs = 1
     c                   endif
      *
     c                   if        w_omrl <> 0
     c                   eval      lvantt = lvantt * w_omrl / w_omrs
     c                   else
     c                   eval      lvantt = 0
     c                   endif
      *
     c     end_omr       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for file : VARE-REGISTER
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for les på vare-enhet-register
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
      *
      *  Key for BATCH-REGISTER
      *
     c     lbchl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbchl1_batc
      *
      *  Key for LAGERBUNT-HODE-REGISTER
      *
     c     lbhelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbhelu_batc
     c                   kfld                    lbhelu_numm
      *
      *  Key for LAGERBUNT-LINJE-REGISTER
      *
     c     lbdtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbdtl1_batc
     c                   kfld                    lbdtl1_numm
     c                   kfld                    lbdtl1_line
      *
      *  Key for LAGER-TELLE-REGISTER
      *
     c     ltellu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ltellu_batc
     c                   kfld                    ltellu_numm
     c                   kfld                    ltellu_suff
     c                   kfld                    ltellu_line
      *
      *  Key for LAGER-TELLE-VARE-REGISTER
      *
     c     ltell2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ltelll2_batc
     c                   kfld                    ltell2_numm
     c                   kfld                    ltell2_suff
     c                   kfld                    ltell2_vare
     c                   kfld                    ltell2_lage
     c                   kfld                    ltell2_dlop
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    w_parm
8.01 c                   parm                    w_slet
      *
     c                   eval      p_prec = w_parm
      *
      *  Legge inn opplysninger i nøklene
      *
     c                   eval      w_firm = p_firm
      *
     c                   eval      lbchl1_batc = p_batc
     c                   eval      lbhelu_batc = p_batc
     c                   eval      lbdtl1_batc = p_batc
     c                   eval      ltellu_batc = p_batc
     c                   eval      ltelll2_batc = p_batc
     c                   eval      lbhelu_numm = p_numm
     c                   eval      lbdtl1_numm = p_numm
     c                   eval      ltellu_suff = 0
     c                   eval      ltell2_suff = 0
     c                   eval      lbdtl1_line = 0
      *
     c                   eval      ltellu_line = 0
      *
      *  Hent opplysninger fra BATCH-REGISTER
      *
     c     lbchl1_key    chain     lbchl1r
     c                   eval      ltellu_numm = lcnumm
     c                   eval      ltell2_numm = lcnumm
      *
     c                   endsr
