     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VT600R
      * Beskrivelse . . : Tilbudspriser, utskrift - parameter
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       140799 HKO  Nytt program
5.70  * prgr  280509 HKO  Lagt inn valg av lager i forb. prisgruppe
7.xx  * 7.00  010416 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
8.01  *PRG2   150622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvt600d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Datastruktur for parameterliste -ut
      *
     d                 ds
     d d_list                        64
     d  d_firm                        3  0 overlay(d_list)
     d  d_dato                         d   overlay(d_list:4) datfmt(*iso)
     d  d_kamp                        5    overlay(d_list:14)
     d  d_fper                         d   overlay(d_list:19) datfmt(*iso)
     d  d_tper                         d   overlay(d_list:29) datfmt(*iso)
     d  d_fogr                        2  0 overlay(d_list:39)
     d  d_fhgr                        2  0 overlay(d_list:41)
     d  d_fugr                        3  0 overlay(d_list:43)
     d  d_togr                        2  0 overlay(d_list:46)
     d  d_thgr                        2  0 overlay(d_list:48)
     d  d_tugr                        3  0 overlay(d_list:50)
5.70 d  d_lage                        2  0 overlay(d_list:54)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s              3  0
     d w_utxt          s             30
     d w_fper          s               d   datfmt(*iso)
     d w_tper          s               d   datfmt(*iso)
     d w_fvgr          s              7  0
     d w_tvgr          s              7  0
5.70 d w_jkod          s              2  0
5.70 d w_jtxt          s             20
5.70 d w_stat          s              1
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Behandler bildet
      *
     c     b1tag         tag
      *
     c                   exfmt     b1bld
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b1tag
     c                   endsl
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1tag
     c                   endif
      *
      * Kaller utskriftsprogram
      *
     c                   eval      d_firm = w_firm
     c                   eval      d_dato = *loval
     c                   eval      d_kamp = b1kamp
     c                   eval      d_fper = *loval
     c                   eval      d_tper = *loval
     c                   eval      d_fogr = b1fogr
     c                   eval      d_fhgr = b1fhgr
     c                   eval      d_fugr = b1fugr
     c                   eval      d_togr = b1togr
     c                   eval      d_thgr = b1thgr
     c                   eval      d_tugr = b1tugr
5.70 c                   eval      d_lage = b1lage
      *
     c                   if        b1dato <> 0
     c     *dmy          move      b1dato        d_dato
     c                   endif
      *
     c                   if        b1fper <> 0
     c     *dmy          move      b1fper        d_fper
     c                   endif
      *
     c                   if        b1tper <> 0
     c     *dmy          move      b1tper        d_tper
     c                   endif
      *
     c                   call      'VT600C'
     c                   parm                    d_list
      *
     c                   goto      b1tag
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av spørringer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Overgruppe
      *
     c                   if        w_afld = 'B1FOGR'
     c                   eval      b1fhgr = 0
     c                   eval      b1fugr = 0
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b1fogr
     c                   parm                    w_utxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'B1TOGR'
     c                   eval      b1thgr = 0
     c                   eval      b1tugr = 0
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b1togr
     c                   parm                    w_utxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        w_afld = 'B1FHGR'
     c                   eval      b1fugr = 0
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    b1fogr
     c                   parm                    b1fhgr
     c                   parm                    w_utxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'B1THGR'
     c                   eval      b1tugr = 0
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    b1togr
     c                   parm                    b1thgr
     c                   parm                    w_utxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Undergruppe
      *
     c                   if        w_afld = 'B1FUGR'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    b1fogr
     c                   parm                    b1fhgr
     c                   parm                    b1fugr
     c                   parm                    w_utxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'B1TUGR'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    b1togr
     c                   parm                    b1thgr
     c                   parm                    b1tugr
     c                   parm                    w_utxt
     c                   goto      end_spØrr
     c                   endif
      *
5.70 c                   if        w_afld = 'B1LAGE'
5.70 c                   call      'RA510R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    b1lage
5.70 c                   parm                    w_jtxt
5.70 c                   goto      end_spØrr
5.70 c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
      * Dato, kampanje eller tilbudsperiode må oppgis
      *
     c                   if        b1dato = 0 and
     c                             b1kamp = *blank and
     c                             b1fper = 0 and
     c                             b1tper = 0
     c                   eval      *in31  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * Dato, kampanje og tilbudsperiode kan ikke oppgis samtidig
      *
     c                   if        b1dato <> 0 and
     c                             b1kamp <> *blank or
     c                             b1dato <> 0 and
     c                             b1fper <> 0 or
     c                             b1kamp <> *blank and
     c                             b1fper <> 0
     c                   eval      *in32  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * Dato
      *
     c                   if        b1dato <> 0
     c     *dmy          test(d)                 b1dato                 33
     c                   if        *in33 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
      * Tilbudsperiode
      *
     c                   if        b1fper <> 0
     c     *dmy          test(d)                 b1fper                 34
     c                   if        *in34 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
     c                   if        b1tper <> 0
     c     *dmy          test(d)                 b1tper                 35
     c                   if        *in35 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
     c                   if        b1fper <> 0 and
     c                             b1tper = 0
     c                   eval      b1tper = b1fper
     c                   endif
      *
     c                   if        b1tper <> 0 and
     c                             b1fper = 0
     c                   eval      *in36  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c                   if        b1fper <> 0
      *
     c     *dmy          move      b1fper        w_fper
      *
     c                   if        b1tper <> 0
     c     *dmy          move      b1tper        w_tper
     c                   else
     c                   eval      w_tper = *loval
     c                   endif
      *
     c                   if        w_fper > w_tper
     c                   eval      *in37  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c                   endif
      *
      * Varegruppe
      *
     c                   if        b1fogr <> 0 or
     c                             b1fhgr <> 0 or
     c                             b1fugr <> 0
     c                   if        b1togr = 0 and
     c                             b1thgr = 0 and
     c                             b1tugr = 0
     c                   eval      b1togr = b1fogr
     c                   eval      b1thgr = b1fhgr
     c                   eval      b1tugr = b1fugr
     c                   endif
     c                   endif
      *
     c                   eval      w_fvgr = b1fogr * 100000 +
     c                                      b1fhgr * 1000 + b1fugr
      *
     c                   eval      w_tvgr = b1togr * 100000 +
     c                                      b1thgr * 1000 + b1tugr
      *
     c                   if        w_fvgr > w_tvgr
     c                   eval      *in38  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
5.70 c                   if        b1lage <> 0
5.70 c                   if        w_afld = 'B1LAGE'
5.70 c                   call      'RA510R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    b1lage
5.70 c                   parm                    w_jtxt
5.70 c                   eval      *in39  = *on
5.70 c                   eval      b_feil = *on
5.70 c                   goto      end_sjekk
5.70 c                   endif
5.70 c                   endif
      *
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *
      * Henter firmakode fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
