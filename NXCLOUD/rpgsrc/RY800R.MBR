     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : RY800R
      * Beskrivelse . . : Legger HOLD på alle varer for å holde dem
      *                   tilbake fra oppdatering
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       060498 HKO  Nytt program
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjvdtlu    uf a e           k disk    rename(jvdtpfr:jvdtlur)
      *
      * Definering av Local data area (*LDA)
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d jvdtlu_dvar     s                   like(jvdvar)
      *
      * Definering av variable
      *
     d w_firm          s                   like(jvdfir)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *loval        setll     jvarl1
     c                   read      jvarl1                                 60
     c                   dow       *in60 = *off
      *
     C                   clear                   jvdtlur
      *
     c                   eval      jvdtlu_dvar = jvvare
     c     jvdtlu_key    chain     jvdtlur                            61
     c                   eval      jvdhko = 1
     c                   if        *in61 = *off
     c                   update    jvdtlur
     c                   else
     c                   eval      jvdfir = w_firm
     c                   eval      jvdvar = jvvare
     c                   write     jvdtlur
     c                   endif
      *
     c                   read      jvarl1                                 60
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppdatering av vare-detalj
      *
     c     jvdtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jvdtlu_dvar
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
