     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VP130R
      * Beskrivelse . . : Prisgruppe knytninger, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       261108 bof  Nytt program
7.xx  * 7.00  130516 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
8.01  *PRG2   150622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvpgrl1    if   e           k disk    rename(vpgrpfr:vpgrl1r)
     fvpgrl2    if   e           k disk    rename(vpgrpfr:vpgrl2r)
     fvpgrlr    if   e           k disk    rename(vpgrpfr:vpgrlrr)
     fra30l1    if   e           k disk    rename(ra30pfr:ra30l1r)
     fra07l1    if   e           k disk    rename(ra07pfr:ra07l1r)
     fra10l1    if   e           k disk    rename(ra10pfr:ra10l1r)
     fvpgrlu    uf a e           k disk    rename(vpgrpfr:vpgrlur)
      *
     fVP130d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d vpgrl1_glag     s                   like(vpglag)
     d vpgrl1_gavd     s                   like(vpgavd)
     d vpgrl2_gavd     s                   like(vpgavd)
     d vpgrlr_glag     s                   like(vpglag)
     d vpgrlr_gavd     s                   like(vpgavd)
     d vpgrlu_glag     s                   like(vpglag)
     d vpgrlu_gavd     s                   like(vpgavd)
     d ra30l1_dkod     s                   like(redkod)
     d ra07l1_gkod     s                   like(ragkod)
     d ra10l1_jkod     s                   like(rajkod)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(vpgfir)
     d w_stat          s              1
     d w_txt           s             30
      *
     d s_glag          s                   like(vpglag)
     d s_gavd          s                   like(vpgavd)
      *
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2glag <> 0  or
     c                             b2gavd <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      vpgrl2_gavd = b1gavd
     c     vpgrl2_key    setll     vpgrl2
     c                   other
     c                   eval      vpgrl1_glag = b1glag
     c     vpgrl1_key    setll     vpgrl1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Rabattgruppe
      *
     c                   if        b2glag <> 0
     c                   eval      w_seqe = *blank
     c                   eval      vpgrl1_glag = b2glag
     c     vpgrl1_key    setll     vpgrl1
     c                   goto      end_pos
     c                   endif
      *
      * gprgrivelse
      *
     c                   if        b2gavd <> 0
     c                   eval      w_seqe = 'B'
     c                   eval      vpgrl2_gavd = b2gavd
     c     vpgrl2_key    setll     vpgrl2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2glag = 0
     c                   eval      b2gavd = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1glag = b1glag
     c                   eval      c1gavd = b1gavd
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1glag = b1glag
     c                   eval      k1gavd = b1gavd
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1glag = b1glag
     c                   eval      d1gavd = b1gavd
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1glag = b1glag
     c                   eval      c1gavd = b1gavd
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1glag = b2glag
     c                   eval      c1gavd = b2gavd
     c                   eval      c1ltxt = *blank
     c                   eval      c1atxt = *blank
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Spørring
      *
     c                   if        *inkd
     c                   exsr      spØrring
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1glag = 0 and
     c                             c1gavd = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Validering
      *
     c                   if        c1glag <> 0
     c                   eval      ra10l1_jkod = c1glag
     c     ra10l1_key    chain     ra10l1
     c                   if        not %found(ra10l1)
     c                   eval      *in31  = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
     c                   if        c1gavd <> 0
     c                   eval      ra07l1_gkod = c1gavd
     c     ra07l1_key    chain     ra07l1
     c                   if        not %found(ra07l1)
     c                   eval      *in32  = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      vpgrlr_glag = c1glag
     c     vpgrlr_key    chain     vpgrlr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1glag = 0
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2glag = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      vpgrlr_glag = c1glag
     c                   eval      vpgrlr_gavd = c1gavd
     c     vpgrlr_key    chain     vpgrlr
      *
      *
     c                   if        %found
     c                   eval      c2gprg = vpgprg
     c                   eval      c2eusr = vpgeus
     c     *dmy          move      vpgeda        c2edat
     c                   exsr      hent_data
     c                   else
     c                   eval      c2gprg = *blank
     c                   eval      c2ptxt = *blank
     c                   eval      c2eusr = *blank
     c                   eval      c2edat = 0
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1glag = k1glag
     c                   eval      c1gavd = k1gavd
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   exsr      spØrring
     c                   goto      c2taga
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2gprg = *blank
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
     c                   eval      ra30l1_dkod = c2gprg
     c     ra30l1_key    chain     ra30l1
     c                   if        not %found(ra30l1)
     c                   eval      *in31  = *on
     c                   goto      c2taga
     c                   endif
     c                   eval      c2ptxt = redtxt
      *
      * Oppdatering
      *
     c                   clear                   vpgrlur
      *
     c                   eval      vpgrlu_glag = c1glag
     c                   eval      vpgrlu_gavd = c1gavd
     c     vpgrlu_key    chain     vpgrlur
      *
     c                   eval      vpgprg = c2gprg
      *
     c                   time                    vpgeda
     c                   time                    vpgeti
     c                   eval      vpgeus = l_user
      *
     c                   if        %found
     c                   update    vpgrlur
     c                   else
     c                   eval      vpgfir = w_firm
     c                   eval      vpglag = c1glag
     c                   eval      vpgavd = c1gavd
     c                   write     vpgrlur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1gprg = vpgprg
     c                   eval      b1ptxt = c2ptxt
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      vpgrlr_glag = d1glag
     c                   eval      vpgrlr_gavd = d1gavd
     c     vpgrlr_key    chain     vpgrlr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      vpgrlu_glag = d1glag
     c                   eval      vpgrlu_gavd = d1gavd
     c     vpgrlu_key    chain     vpgrlur
      *
     c                   delete    vpgrlur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_glag = k1glag
     c                   eval      s_gavd = k1gavd
      *
      * Fyller kopierings-bildet
      *
     c                   eval      vpgrlr_glag = k1glag
     c                   eval      vpgrlr_gavd = k1gavd
     c     vpgrlr_key    chain     vpgrlr
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      * Spørring
      *
     c                   if        *inkd
     c                   exsr      spØrring
     c                   goto      k1taga
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1glag = 0   and
     c                             k1gavd = 0
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Validering
      *
     c                   if        k1glag <> 0
     c                   eval      ra10l1_jkod = k1glag
     c     ra10l1_key    chain     ra10l1
     c                   if        not %found(ra10l1)
     c                   eval      *in31  = *on
     c                   goto      k1taga
     c                   endif
     c                   endif
     c                   if        k1gavd <> 0
     c                   eval      ra07l1_gkod = k1gavd
     c     ra07l1_key    chain     ra07l1
     c                   if        not %found(ra07l1)
     c                   eval      *in32  = *on
     c                   goto      k1taga
     c                   endif
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      vpgrlr_glag = k1glag
     c                   eval      vpgrlr_gavd = k1gavd
     c     vpgrlr_key    chain     vpgrlr
     c                   if        %found
     c                   eval      *in33 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1glag = s_glag
     c                   eval      c1gavd = s_gavd
     c                   exsr      xc2bld
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      vpgrl2
     c                   other
     c                   read      vpgrl1
     c                   endsl
      *
     c                   if        %eof or
     c                             vpgfir <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      vpgrl2_gavd = vpgavd
     c                   other
     c                   eval      vpgrl1_glag = vpglag
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1glag = vpglag
     c                   eval      b1gavd = vpgavd
     c                   eval      b1gprg = vpgprg
     c                   eval      ra30l1_dkod = vpgprg
     c     ra30l1_key    chain     ra30l1
     c                   if        %found(ra30l1)
     c                   eval      b1ptxt = redtxt
     c                   else
     c                   eval      b1ptxt = *blank
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'B'
     c     vpgrl2_key    setgt     vpgrl2
     c                   readp     vpgrl2
     c                   other
     c     vpgrl1_key    setgt     vpgrl1
     c                   readp     vpgrl1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   readp     vpgrl2
     c                   other
     c                   readp     vpgrl1
     c                   endsl
      *
     c                   if        %eof or
     c                             vpgfir <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      vpgrl2_gavd = vpgavd
     c                   other
     c                   eval      vpgrl1_glag = vpglag
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'B'
     c     vpgrl2_key    setll     vpgrl2
     c                   other
     c     vpgrl1_key    setll     vpgrl1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
     c                   if        w_afld = 'C1GAVD'
     c                   call      'RA507R'
     c                   parm                    w_firm
     c                   parm                    c1gavd
     c                   parm                    c1atxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'K1GAVD'
     c                   call      'RA507R'
     c                   parm                    w_firm
     c                   parm                    k1gavd
     c                   parm                    w_txt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'C1GLAG'
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    c1glag
     c                   parm                    c1ltxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'K1GLAG'
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    k1glag
     c                   parm                    w_txt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'C2GPRG'
     c                   call      'RA530R'
     c                   parm                    w_firm
     c                   parm                    c2gprg
     c                   parm                    c2ptxt
     c                   goto      end_spØrr
     c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_data     begsr
      *
     c                   eval      c1ltxt = *blank
     c                   eval      c1atxt = *blank
     c                   eval      c2ptxt = *blank
      *
     c                   eval      ra10l1_jkod = vpglag
     c     ra10l1_key    chain     ra10l1
     c                   if        %found(ra10l1)
     c                   eval      c1ltxt = rajtxt
     c                   endif
     c                   eval      ra07l1_gkod = vpgavd
     c     ra07l1_key    chain     ra07l1
     c                   if        %found(ra07l1)
     c                   eval      c1atxt = ragtxt
     c                   endif
     c                   eval      ra30l1_dkod = vpgprg
     c     ra30l1_key    chain     ra30l1
     c                   if        %found(ra30l1)
     c                   eval      c2ptxt = redtxt
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for posisjonering på rabattgruppe
      *
     c     vpgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vpgrl1_glag
     c                   kfld                    vpgrl1_gavd
      *
      * Key for posisjonering på gprgrivelse
      *
     c     vpgrl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vpgrl2_gavd
      *
      * Key for les på rabattgruppe
      *
     c     vpgrlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vpgrlr_glag
     c                   kfld                    vpgrlr_gavd
      *
      * Key for oppdatering av rabattgruppe
      *
     c     vpgrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vpgrlu_glag
     c                   kfld                    vpgrlu_gavd
      *
      * Key for les av gyldige eprisgruppe
      *
     c     ra30l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra30l1_dkod
      *
      * Key for les av avdeling
      *
     c     ra07l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra07l1_gkod
      *
      * Key for les av lager
      *
     c     ra10l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra10l1_jkod
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      vpgrl1_glag = 0
     c     vpgrl1_key    setll     vpgrl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
