      /TITLE  Tilrettelegger trans fra scanning
     h decedit(',') datedit(*dmy.)
     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * System. . . . . : ASOKON
      * Program . . . . : RFP14R
      * Beskrivelse . . : Tildeler periode fra bilagsdato dersom = 0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * V6.10 030511 sst  Nytt program basert på RFP15R
6.10  * V6.10 130711 sst  Oppdat alle med RTAUTX = 'X' for backup tatt
6.30  * V6.30 150617 sst  Ta vare på reskontronr pr bilag for Navisjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frcmpl1    uf   e           k disk    rename(rcmppfr:rcmpl1r)
     f                                     prefix(x:1)
6.30 frcmrlu    uf a e           k disk    rename(rcmrpfr:rcmrlur)
6.30 f                                     prefix(RR:2)
6.30 fraa1l1    if   e           k disk    rename(raa1pfr:raa1l1r)
      *
      *
     d                uds
     d l_user                911    920
     d l_fgrp                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av array's
      *
     d per             s              2  0 dim(13)
      *
      * Definering av variable
      *
     d w_peri          s              6  0
     d w_amd           s              6  0
     d w_20            s              2  0
     d w_40            s              4  0
      *
610  d w_memb          s             10
610  d w_jkey          s             41
610  d w_jsta          s              2  0
610  d w_jmld          s            200
610  d w_stat          s              1  0
      *
6.30 d rcmrlu_firm     s                     like(xtfirm)
6.30 d rcmrlu_biln     s                     like(xtbiln)
6.30 d raa1l1_firm     s                     like(xtfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedrutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      * Les transaksjonsfile til e.o.f.
      *
     c                   eval      w_stat = 1
     c                   read      rcmpl1
6.12 c                   if        %eof(rcmpl1)
6.12 c                   eval      w_jsta = 10
6.12 c                   eval      w_jmld = 'Ingen poster fra Scanning'
6.12 c                   call      'AB705R'
6.12 c                   parm                    w_jkey
6.12 c                   parm                    w_jsta
6.12 c                   parm                    w_jmld
6.12 c                   endif
      *
     c                   dow       not %eof(rcmpl1)
      *
6.30  *   Lagrer lev.nr dette bilag dersom det finnes på transen
6.30  *   for NAV
6.30 c                   if           xtdkto > ra1hrs
6.30 c                             or xtckto > ra1hrs
     c                             or xtrper > 0
6.30 c                   eval      rcmrlu_firm = xtfirm
6.30 c                   eval      rcmrlu_biln = xtbiln
6.30 c     rcmrlu_key    chain     rcmrlu
6.30 c                   if        xtdkto > ra1hrs
6.30 c                   eval      rrresn = xtdkto
6.30 c                   endif
6.30 c                   if        xtckto > ra1hrs
6.30 c                   eval      rrresn = xtckto
6.30 c                   endif
6.30 c                   if        xtrper > 0
6.30 c                   eval      rrrper = xtrper
6.30 c                   eval      rrraar = xtraar
6.30 c                   endif
6.30 c                   if        %found(rcmrlu)
6.30 c                   update    rcmrlur
6.30 c                   else
6.30 c                   eval      rrfirm = xtfirm
6.30 c                   eval      rrbiln = xtbiln
6.30 c                   write     rcmrlur
6.30 c                   endif
6.30 c                   endif
6.30  *
     c                   if        xtfirm = l_firm and
     c                             xtfgrp = l_fgrp
      *    Sett w_stat = 0 for å indikere at det finnes transer
     c                   eval      w_stat = 0
      *    Setter periode av bilagsdato dersom periode = 0 fra compello
     c                   if        xtrper = *zero
     c     *ymd          move      xtbdat        w_amd
     c                   eval      w_40   = w_amd/100
     c                   eval      w_20   = w_amd/10000
     c                   eval      xtrper = w_40 - w_20 * 100
     c                   eval      xtraar = w_20 + 2000
      *
     c                   endif
     c                   endif
6.10 c                   eval      xtautx = 'X'
6.10 c                   update    rcmpl1r
      *
     c     nyles         tag
      *
     c                   read      rcmpl1
      *
     c                   enddo
      *
      * Avslutt program
      *
     c                   eval      *inlr = *on
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
610  c     *entry        plist
610  c                   parm                    w_memb
610  c                   parm                    w_jkey
610  c                   parm                    w_stat
6.30  *
6.30  * Key for henting av levnr på posteringer til NAV
6.30  *
6.30 c     rcmrlu_key    klist
6.30 c                   kfld                    rcmrlu_firm
6.30 c                   kfld                    rcmrlu_biln
6.30  *
6.30  * Key for les av statuskoder 1 (Sperret periode)
6.30  *
6.30 c     raa1l1_key    klist
6.30 c                   kfld                    raa1l1_firm
6.30  *
6.30 c                   eval      raa1l1_firm = l_firm
6.30 c     raa1l1_key    chain     raa1l1
6.30 c                   if        not %found(raa1l1)
6.30 c                   eval      ra1hrs = 599999
6.30 c                   endif
6.30  *
     c                   endsr
