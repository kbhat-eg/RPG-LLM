      /TITLE  Felles, utskrift kontaktperson - rapport
     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RF631R                                        *
      * Beskrivelse . . : Felles, utskrift kontaktperson - rapport      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
      * R5.50     290806  Nytt program.                                  HFL
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KA = F1  = Valg av firma                                  *
      *       KC = F3  = Exit                                           *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     frkunlr    if   e           k disk    rename(rkunpfr:rkunlrr)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     frlevlr    if   e           k disk    rename(rlevpfr:rlevlrr)
     frukpl5    if   e           k disk    rename(rukppfr:rukpl5r)
     frukpl6    if   e           k disk    rename(rukppfr:rukpl6r)
     frukpl7    if   e           k disk    rename(rukppfr:rukpl7r)
     frf631p    o    e             printer oflind(*in30)
      *
     d                 ds
     d w_tdat                         8  0
     d  w_taar                        4  0 overlay(w_tdat)
     d  w_tmnd                        2  0 overlay(w_tdat:5)
     d  w_tdag                        2  0 overlay(w_tdat:7)
      *
     d w_xdat          s             10d   datfmt(*iso)
      *
      * Datastruktur for parameter
     d                uds
      *
     d pkund                 300    300                                         Pr. dato
     d pknrf                          6  0                                      Nr. f.o.m.
     d pknrt                          6  0                                      Nr. t.o.m.
     d pkavf                          4  0                                      Avdeling f.o.m.
     d pkavt                          4  0                                      Avdeling t.o.m.
     d pkktf                          4  0                                      Kategori f.o.m.
     d pkktt                          4  0                                      Kategori t.o.m.
     d pself                          4  0                                      Selger f.o.m.
     d pselt                          4  0                                      Selger t.o.m.
     d pkdif                          4  0                                      Distrikt f.o.m.
     d pkdit                          4  0                                      Distrikt t.o.m.
     d plevr                          1                                         Pr. år
     d plnrf                          6  0                                      Nr. f.o.m.
     d plnrt                          6  0                                      Nr. t.o.m.
     d plavf                          4  0                                      Avdeling f.o.m.
     d plavt                          4  0                                      Avdeling t.o.m.
     d plktf                          4  0                                      Kategori f.o.m.
     d plktt                          4  0                                      Kategori t.o.m.
     d pldif                          4  0                                      Distrikt f.o.m.
     d pldit                          4  0                                      Distrikt t.o.m.
     d palle                          1                                         Alfa/Numerisk
      *
     d l_wsid                901    910                                         Bruker
     d l_user                911    920                                         Bruker
     d l_firm                944    946  0                                      Firma
     d l_fnav                951    980                                         Navn
      *
      * Definering av variabler i nøkler
      *
     d rkunl1_kund     s                   like(rkkund)
     d rkunlr_kund     s                   like(rkkund)
     d rlevl1_levr     s                   like(rllevr)
     d rlevlr_levr     s                   like(rllevr)
     d rukpl5_alfa     s                   like(rualfa)
     d rukpl5_kpnr     s                   like(rukpnr)
     d rukpl6_kund     s                   like(rukund)
     d rukpl7_levr     s                   like(rulevr)
      *
      * Definering av variable
      *
     d w_firm          s                   like(rkfirm)
     d w_kund          s                   like(rkkund)
     d w_levr          s                   like(rllevr)
     d w_alfa          s                   like(rualfa)
     d w_kpnr          s                   like(rukpnr)
     d w_tell          s              6  0
      *
     c     *in80         caseq     *on           les_kunde                               .....
     c     *in81         caseq     *on           les_lever                               .....
     c                   cas                     les_alle                                .....
     c                   endcs                                                           .....
      *
     c     slutt         tag
      *
     c                   eval      *inlr = *on
      *
     c*r                 write     total
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *   SUBRUTINE FOR Å behandle kunders kontaktpersoner
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     c     les_kunde     begsr
      *
      * Les kunderegister
      *
     c                   read      rkunl1                                 10                 .
     c                   dow       *in10 = *off
      *
     c                   if        rkfirm > l_firm                                       .....
     c                   eval      *in10 = *on                                               .
     c                   endif                                                           .....
      *
     c                   if        *in10 = *on
     c                   goto      slutt                                        FERDIG
     c                   endif
      *
      * Sjekk kundenr. fra/til
      *
     c                   if        pknrt  <> *zero                                       .....
     c                   if        rkkund < pknrf or                                     .....
     c                             rkkund > pknrt                                        .....
     c                   goto      ikke_k                                                    .
     c                   endif                                                           .....
     c                   endif                                                           .....
      *
      * Sjekk avdeling fra/til
      *
     c                   if        pkavt  <> *zero                                       .....
     c                   if        rkavde < pkavf or                                     .....
     c                             rkavde > pkavt                                        .....
     c                   goto      ikke_k                                                    .
     c                   endif                                                           .....
     c                   endif                                                           .....
      *
      * Sjekk kundekategori fra/til
      *
     c                   if        pkktt  <> *zero                                       .....
     c                   if        rkkatg < pkktf or                                     .....
     c                             rkkatg > pkktt                                        .....
     c                   goto      ikke_k                                                    .
     c                   endif                                                           .....
     c                   endif                                                           .....
      *
      * Sjekk selger fra/til
      *
     c                   if        pselt  <> *zero                                       .....
     c                   if        rkslgr < pself or                                     .....
     c                             rkslgr > pselt                                        .....
     c                   goto      ikke_k                                                    .
     c                   endif                                                           .....
     c                   endif                                                           .....
      *
      * Sjekk distrikt fra/til
      *
     c                   if        pkdit  <> *zero                                       .....
     c                   if        rkdist < pkdif or                                     .....
     c                             rkdist > pkdit                                        .....
     c                   goto      ikke_k                                                    .
     c                   endif                                                           .....
     c                   endif                                                           .....
      *
      * Selekterte kunder
      *
      * Finnes kontaktperson på kunden?
      *
     c                   eval      rukpl6_kund = rkkund                                      .
     c     rukpl6_key    setll     rukpl6                                 60
      *
     c                   if        *in60 = *on
     c                   if        *in30 = *on
     c                   write     head1
     c                   eval      *in30 = *off
     c                   endif                                                           .....
     c                   write     head2
     c                   write     kund1
     c*                  if        rkadr2 <> *blank                                      .....
     c*                  write     kund1a                                                    .
     c*                  endif                                                           .....
     c                   write     head2
      *
     c     rukpl6_key    reade     rukpl6                                 61
     c                   dow       *in61 = *off
      *
      * Dersom kontaktperson både hos kunde og leverandør
      *
     c                   eval      *in85 = *off
     c                   eval      wlnavn = *blank
      *
      * Henter eventuelt leverandørnavn
      *
     c                   if        rulevr > *zero
     c                   eval      *in85 = *on
     c                   eval      rlevlr_levr = rulevr
     c     rlevlr_key    chain     rlevlr
     c                   if        %found
     c                   eval      wlnavn = rlnavn
     c                   endif                                                           .....
     c                   endif                                                           .....
      *
     c                   if        ruedat <> *loval
     c     *dmy          move      ruedat        w_edat
     c                   else                                                            .....
     c                   move      *zeros        w_edat
     c                   endif                                                           .....
      *
     c                   if        *in30 = *on
     c                   write     head1
     c                   eval      *in30 = *off
     c                   endif                                                           .....
     c                   write     kpers                                                     .
     c     rukpl6_key    reade     rukpl6                                 61
     c                   enddo                                                           .....
     c                   endif                                                           .....
      *
     c     ikke_k        tag
     c                   read      rkunl1                                 10                 .
     c                   enddo
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *   SUBRUTINE FOR Å behandle leverandørs kontaktpersoner
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     c     les_lever     begsr
      *
      * Les leverandørregister
      *
     c                   read      rlevl1                                 10                 .
     c                   dow       *in10 = *off
      *
     c                   if        rlfirm > l_firm                                       .....
     c                   eval      *in10 = *on                                               .
     c                   endif                                                           .....
      *
     c                   if        *in10 = *on
     c                   goto      slutt                                        FERDIG
     c                   endif
      *
      * Sjekk leverandørnr. fra/til
      *
     c                   if        plnrt  <> *zero                                       .....
     c                   if        rllevr < plnrf or                                     .....
     c                             rllevr > plnrt                                        .....
     c                   goto      ikke_l                                                    .
     c                   endif                                                           .....
     c                   endif                                                           .....
      *
      * Sjekk avdeling fra/til
      *
     c                   if        plavt  <> *zero                                       .....
     c                   if        rlavde < plavf or                                     .....
     c                             rlavde > plavt                                        .....
     c                   goto      ikke_l                                                    .
     c                   endif                                                           .....
     c                   endif                                                           .....
      *
      * Sjekk leverandørkategori fra/til
      *
     c                   if        plktt  <> *zero                                       .....
     c                   if        rlkatg < plktf or                                     .....
     c                             rlkatg > plktt                                        .....
     c                   goto      ikke_l                                                    .
     c                   endif                                                           .....
     c                   endif                                                           .....
      *
      * Sjekk distrikt fra/til
      *
     c                   if        pldit  <> *zero                                       .....
     c                   if        rldist < pldif or                                     .....
     c                             rldist > pldit                                        .....
     c                   goto      ikke_l                                                    .
     c                   endif                                                           .....
     c                   endif                                                           .....
      *
      * Selekterte leverandører
      *
      * Finnes kontaktperson på leverandøren?
      *
     c                   eval      rukpl7_levr = rllevr                                      .
     c     rukpl7_key    setll     rukpl7                                 60
      *
     c                   if        *in60 = *on
     c                   if        *in30 = *on
     c                   write     head1
     c                   eval      *in30 = *off
     c                   endif                                                           .....
     c                   write     head2
     c                   write     levr1
     c                   write     head2
      *
     c     rukpl7_key    reade     rukpl7                                 61
     c                   dow       *in61 = *off
      *
      * Dersom kontaktperson både hos kunde og leverandør
      *
     c                   eval      *in86 = *off
     c                   eval      wknavn = *blank
      *
      * Henter eventuelt kundenavn
      *
     c                   if        rukund > *zero
     c                   eval      *in86 = *on
     c                   eval      rkunlr_kund = rukund
     c     rkunlr_key    chain     rkunlr
     c                   if        %found
     c                   eval      wknavn = rknavn
     c                   endif                                                           .....
     c                   endif                                                           .....
      *
     c                   if        ruedat <> *loval
     c     *dmy          move      ruedat        w_edat
     c                   else                                                            .....
     c                   move      *zeros        w_edat
     c                   endif                                                           .....
      *
     c                   if        *in30 = *on
     c                   write     head1
     c                   eval      *in30 = *off
     c                   endif                                                           .....
     c                   write     kpers                                                     .
     c     rukpl7_key    reade     rukpl7                                 61
     c                   enddo                                                           .....
     c                   endif                                                           .....
      *
     c     ikke_l        tag
     c                   read      rlevl1                                 10                 .
     c                   enddo
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *   SUBRUTINE FOR Å behandle alle kontaktpersoner
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     c     les_alle      begsr
      *
      * Les kontaktpersoner alfa
      *
     c                   read      rukpl5                                 61
     c                   dow       *in61 = *off
      *
      * Dersom kontaktperson hos kunde
      *
     c                   eval      *in86 = *off
     c                   eval      wknavn = *blank
      *
      * Henter eventuelt kundenavn
      *
     c                   if        rukund > *zero
     c                   eval      *in86 = *on
     c                   eval      rkunlr_kund = rukund
     c     rkunlr_key    chain     rkunlr
     c                   if        %found
     c                   eval      wknavn = rknavn
     c                   endif                                                           .....
     c                   endif                                                           .....
      *
      * Dersom kontaktperson hos leverandør
      *
      * Henter eventuelt leverandørnavn
      *
     c                   eval      *in85 = *off
     c                   eval      wlnavn = *blank
      *
     c                   if        rulevr > *zero
     c                   eval      *in85 = *on
     c                   eval      rlevlr_levr = rulevr
     c     rlevlr_key    chain     rlevlr
     c                   if        %found
     c                   eval      wlnavn = rlnavn
     c                   endif                                                           .....
     c                   endif                                                           .....
      *
     c                   if        ruedat <> *loval
     c     *dmy          move      ruedat        w_edat
     c                   else                                                            .....
     c                   move      *zeros        w_edat
     c                   endif                                                           .....
      *
      * Skriver 4 kontaktpersoner pr. side
      *
     c                   if        w_tell = 4
     c                   write     head1
     c                   eval      w_tell = *zero
     c                   endif                                                           .....
     c                   eval      w_tell = w_tell + 1
      *
     c                   write     kpers                                                     .
     c                   read      rukpl5                                 61
     c                   enddo                                                           .....
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Behandle parameter
      *
     c                   select                                                          .....
     c                   when      pkund = 'J'                                           .....
     c                   eval      *in80 = *on                                               .
     c                   when      plevr = 'J'                                           .....
     c                   eval      *in81 = *on                                               .
     c                   other                                                           .....
     c                   eval      *in82 = *on                                               .
     c                   endsl                                                           .....
      *
      * Key for kunderegister
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for kunderegister
      *
     c     rkunlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunlr_kund
      *
      * Key for leverandørregister
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for leverandørregister
      *
     c     rlevlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevlr_levr
      *
      * Key for kontaktpersonregister alfabetisk
      *
     c     rukpl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rukpl5_alfa
     c                   kfld                    rukpl5_kpnr
      *
      * Key for kontaktpersonregister pr. kunde
      *
     c     rukpl6_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rukpl6_kund
      *
      * Key for kontaktpersonregister pr. leverandør
      *
     c     rukpl7_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rukpl7_levr
      *
      * Sette startverdier
      *
     c                   move      l_firm        w_firm
      *
     c                   select
     c                   when      *in80 = *on                                           .....
     c     rkunl1_key    setll     rkunl1                                                    .
     c                   when      *in81 = *on                                           .....
     c     rlevl1_key    setll     rlevl1                                                    .
     c                   other                                                           .....
     c                   eval      rukpl5_alfa = *loval                                      .
     c                   eval      rukpl5_kpnr = *zero                                       .
     c     rukpl5_key    setll     rukpl5                                                    .
     c                   endsl                                                           .....
      *
      * Skriv heading på første side
      *
     c                   write     head1
     c                   write     headsl
      *
     c                   if        *in80
     c                   if        pknrt > *zero
     c                   write     headkl
     c                   endif
     c                   if        pkavt > *zero
     c                   write     headav
     c                   endif
     c                   if        pkktt > *zero
     c                   write     headkt
     c                   endif
     c                   if        pselt > *zero
     c                   write     headse
     c                   endif
     c                   if        pkdit > *zero
     c                   write     headdi
     c                   endif
     c                   endif
      *
     c                   if        *in81
     c                   if        plnrt > *zero
     c                   write     headkl
     c                   endif
     c                   if        plavt > *zero
     c                   write     headav
     c                   endif
     c                   if        plktt > *zero
     c                   write     headkt
     c                   endif
     c                   if        pldit > *zero
     c                   write     headdi
     c                   endif
     c                   endif
      *
     c                   if        *in82
     c                   write     head2
     c                   endif
      *
     c                   endsr
