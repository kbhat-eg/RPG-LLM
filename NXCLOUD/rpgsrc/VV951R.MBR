     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VV951R
      * Beskrivelse . . : Vare, oppretter vare-query-fil
      *
      *                   Oppretter fil for QUERY-formål som inneholder
      *                   vareinformasjon, nyeste EAN-nummer, rabattgruppe,
      *                   enhetsinformasjon og nyeste pris/tilbudspris
      *                   pr. enhet/leveringstype
      *
      *                   Programmet kalles via VV951C for først å tømme
      *                   vare-query-filen
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       070200 HKO  Nytt program
      *       160603 HKO  Lagt inn VPDEKN som viser DG på pris
      *       300303 HKO  Lagt inn beskrivelse
      *       070903 HKO  VVKHEV inneholder verdi fra laveste nivå med
      *                   innhold
5.70  * PRGR  301108 bof  Utvidet med prisgruppe
6.20  *       010411 bof  Utvidet med prisgiver på vvprpf
6.21  *       040511 bof  Utvidet med vargr./lev.nr på vtilpf
6.22  *       100215 bsa  Skiller mellom opprettelse på enhet og vare
6.23  *       150716 bof  teste på at vpsaim ikke blir for stort
6.31  *       140219 bkv  Henter gtin for enhet og lager ldor
      *
8.01  *PRG2   140622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fveanl2    if   e           k disk    rename(veanpfr:veanl2r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     fvvprl1    if   e           k disk    rename(vvprpfr:vvprl1r)
     fvtill1    if   e           k disk    rename(vtilpfr:vtill1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fvahgl1    if   e           k disk    rename(vahgpfr:vahgl1r)
     fvaugl1    if   e           k disk    rename(vaugpfr:vaugl1r)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fra30l1    if   e           k disk    rename(ra30pfr:ra30l1r)
      *
     fvvaqpf    o  a e           k disk    rename(vvaqpfr:vvaqpfr)
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
6.20 d l_lage               1001   1002  0
      *
      * Definering av variable i nøkler
      *
     d vvenl1_vare     s                   like(vevare)
     d vvprl1_vare     s                   like(vpvare)
6.20 d vvprl1_ldor     s                   like(vpldor)
     d vvprl1_prgr     s                   like(vpprgr)
     d vvprl1_enhe     s                   like(vpenhe)
6.21 d vtill1_ogrp     s                   like(vtogrp)
6.21 d vtill1_hgrp     s                   like(vthgrp)
6.21 d vtill1_ugrp     s                   like(vtugrp)
6.21 d vtill1_ldor     s                   like(vtldor)
     d vtill1_vare     s                   like(vtvare)
     d vtill1_prgr     s                   like(vtprgr)
     d vtill1_lety     s                   like(vtlety)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d vahgl1_hhgr     s                   like(vfhhgr)
     d vaugl1_uhgr     s                   like(vfuhgr)
     d vaugl1_uugr     s                   like(vfuugr)
     d rlevl1_levr     s                   like(rllevr)
      *
      * Definering av variable
      *
     d b_oppr          s              1    inz(*off)
6.22 d b_vopr          s              1    inz(*off)
     d b_eann          s              1    inz(*off)
6.20 d b_inlr          s              1    inz(*off)
      *
     d w_udat          s               d   datfmt(*iso)
     d w_mvap          s              6  2
     d w_mvat          s              5  4
     d w_mvaf          s             10  9
     d w_kode          s              1  0
8.01 d w_prgr          s              2
     d w_eann          s                   like(vxeann)
6.20 d w_ldor          s                   like(vpldor)
6.31 d w_ldo2          s                   like(vvldor)
6.20 d w_lage          s              2  0
      *
     d w_firm          s                   like(vvfirm)
     d s_lety          s                   like(vplety)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter statusinformasjon
      *
     c     fstsl1_key    chain     fstsl1
      *
      * Leser hele vareregisteret
      *
     c     vvarl1_key    setll     vvarl1
     c     vvarl1_key    reade     vvarl1                                 90
     c                   dow       *in90 = *off
      *
      * Initierer flagg for oppdatering
      *
     c                   eval      b_oppr = *off
      *
      * Henter nyeste EAN-nummer
6.13  **
6.31 c*                   call      'VE712R'
6.31 c*                   parm                    w_firm
6.31 c*                   parm                    vvvare
6.31 c*                   parm                    w_eann
6.31 c*                   parm                    b_eann
6.31  *
6.31 c*                   eval      vxeann = w_eann
      *
      * Henter beskrivelse
      *
     c                   eval      vogrl1_oogr = vvogrp
     c     vogrl1_key    chain     vogrl1
      *
     c                   eval      vhgrl1_hogr = vvogrp
     c                   eval      vhgrl1_hhgr = vvhgrp
     c     vhgrl1_key    chain     vhgrl1
      *
     c                   eval      vugrl1_uogr = vvogrp
     c                   eval      vugrl1_uhgr = vvhgrp
     c                   eval      vugrl1_uugr = vvugrp
     c     vugrl1_key    chain     vugrl1
      *
     c                   if        vvldor <> 0
     c                   eval      rlevl1_levr = vvldor
     c     rlevl1_key    chain     rlevl1
     c                   endif
      *
     c                   eval      vahgl1_hhgr = vvahgr
     c     vahgl1_key    chain     vahgl1
      *
     c                   eval      vaugl1_uhgr = vvahgr
     c                   eval      vaugl1_uugr = vvaugr
     c     vaugl1_key    chain     vaugl1
      *
      * Finner kontohenvisning
      *
     c                   if        vvkhev = *blank and
     c                             %found(vugrl1) and
     c                             vgukhv <> *blank
     c                   eval      vvkhev = vgukhv
     c                   endif
      *
     c                   if        vvkhev = *blank and
     c                             %found(vhgrl1) and
     c                             vghkhv <> *blank
     c                   eval      vvkhev = vghkhv
     c                   endif
      *
     c                   if        vvkhev = *blank and
     c                             %found(vogrl1) and
     c                             vgokhv <> *blank
     c                   eval      vvkhev = vgokhv
     c                   endif
      *
     c                   if        vvkhev = *blank
     c                   eval      vvkhev = faakhv
     c                   endif
6.20  *
6.20  * Henter leverandør for vare og lager
6.20  *
6.20 c                   call      'VL721R  '
6.20 c                   parm                    w_firm
6.20 c                   parm                    vvvare
6.20 c                   parm                    w_lage
6.20 c                   parm                    w_ldor
6.20 c                   parm                    b_inlr
      *
      * Leser alle enheter på vare, henter priser og tilbudspriser, og
      * oppretter vare i vare-query-fil
      *
     c                   eval      vvenl1_vare = vvvare
     c     vvenl1_key    setll     vvenl1
     c     vvenl1_key    reade     vvenl1r                                91
     c                   dow       *in91 = *off
      *
6.22 c                   eval      b_vopr = *off
      *
6.20  * 1. leverandør for lageret
6.20 c                   eval      vvprl1_ldor = w_ldor
6.20 c                   exsr      pr_prgr
6.22 c**                 if        b_oppr = *off  and
6.22 c                   if        b_vopr = *off  and
6.20 c                             w_ldor <> vvldor
6.20  * 2. hovedleverandør
6.20 c                   eval      vvprl1_ldor = vvldor
6.20 c                   exsr      pr_prgr
6.22 c**                 if        b_oppr = *off
6.22 c                   if        b_vopr = *off
6.20  * siste mulighet...
6.20 c                   eval      vvprl1_ldor = 0
6.20 c                   exsr      pr_prgr
6.20 c                   endif
6.20 c                   endif
      *
     c     vvenl1_key    reade     vvenl1r                                91
     c                   enddo
6.31  *
6.31  * Henter nyeste EAN-nummer
6.31  *
6.31 c                   if        w_ldor <> 0
6.31 c                   eval      w_ldo2 = w_ldor
6.31 c                   else
6.31 c                   eval      w_ldo2 = vvldor
6.31 c                   endif
6.31 c                   call      'VE713R'
6.31 c                   parm                    w_firm
6.31 c                   parm                    w_ldo2
6.31 c                   parm                    vvvare
6.31 c                   parm                    veenhe
6.31 c                   parm                    w_eann
6.31 c                   parm                    b_eann
6.31  *
6.31 c                   eval      vxeann = w_eann
6.31  *
      * Oppretter vare i vare-query-registeret dersom den ikke allerede er
      * opprettet
      *
     c                   if        b_oppr = *off
     c                   write     vvaqpfr
     c                   endif
      *
      * Initierer før neste les
      *
     c                   clear                   vvaqpfr
      *
     c     vvarl1_key    reade     vvarl1                                 90
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  * Subrutine som behandler pr. pristgruppe
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  *
6.20 c     pr_prgr       begsr
6.20 c                   eval      vvprl1_prgr = *blank
6.20 c                   exsr      behandling
6.20 c                   if        w_prgr <> *blank
6.20 c                   eval      vvprl1_prgr = w_prgr
6.20 c                   exsr      behandling
6.20 c                   endif
6.20  *
6.20 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine som henter prisinformasjon pr. enhet
      * Oppretter vare i vare-query-fil pr. enhet pr. leveringstype
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
     c                   eval      s_lety = -1
      *
     c                   eval      vvprl1_vare = vvvare
     c                   eval      vvprl1_enhe = veenhe
     c     vvprl1_key    setll     vvprl1
     c     vvprl1_key    reade     vvprl1r                                92
     c                   dow       *in92 = *off
      *
     c                   if        vplety <> s_lety
      *
     c                   eval      b_oppr = *on
6.22 c                   eval      b_vopr = *on
      *
6.23 c                   if        (vpsapr*w_mvat) < 9999999,99
     c                   eval      vpsaim = vpsapr * w_mvat
6.23 c                   else
6.23 c                   eval      vpsaim = 9999999,99
6.23 c                   endif
      *
     c                   if        vpsaim <> 0
     c                   eval      w_kode = 4
     c                   call      'FA909R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   parm                    w_kode
     c                   parm                    vpsaim
     c                   endif
      *
     c                   eval      vpdekn = 0
      *
     c                   if        vpsapr <> 0
     c                   select
     c                   when      (100 - (vpkopr * 100 / vpsapr)) < -999,99
     c                   eval      vpdekn = -999,99
     c                   when      (100 - (vpkopr * 100 / vpsapr)) > 999,99
     c                   eval      vpdekn = 999,99
     c                   other
     c                   eval(h)   vpdekn = 100 - (vpkopr * 100 / vpsapr)
     c                   endsl
     c                   endif
      *
     c                   eval      vtfdat = *loval
     c                   eval      vtftim = *loval
     c                   eval      vttdat = *loval
     c                   eval      vtftim = *loval
     c                   eval      vtkamp = *blank
     c                   eval      vttipr = 0
     c                   eval      vttiim = 0
     c                   eval      vtkopr = 0
     c                   eval      vtedat = *loval
     c                   eval      vtetim = *loval
     c                   eval      vteusr = *blank
      *
6.21 c                   eval      vtill1_ogrp = 0
6.21 c                   eval      vtill1_hgrp = 0
6.21 c                   eval      vtill1_ugrp = 0
6.21 c                   eval      vtill1_ldor = 0
     c                   eval      vtill1_vare = vvvare
     c                   eval      vtill1_lety = vplety
5.70 c                   eval      vtill1_prgr = vpprgr
     c     vtill1_key    chain     vtill1
     c                   if        %found
      *
     c                   select
     c                   when      vtenh1 = veenhe
     c                   eval      vttipr = vttip1
     c                   eval      vtkopr = vtkop1
     c                   when      vtenh2 = veenhe
     c                   eval      vttipr = vttip2
     c                   eval      vtkopr = vtkop2
     c                   when      vtenh3 = veenhe
     c                   eval      vttipr = vttip3
     c                   eval      vtkopr = vtkop3
     c                   endsl
      *
     c                   eval      vttiim = vttipr * w_mvat
      *
     c                   if        vttiim <> 0
     c                   eval      w_kode = 4
     c                   call      'FA909R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   parm                    w_kode
     c                   parm                    vttiim
     c                   endif
      *
     c                   endif
      *
6.31  *
6.31  * Henter nyeste EAN-nummer
6.31  *
6.31 c                   if        w_ldor <> 0
6.31 c                   eval      w_ldo2 = w_ldor
6.31 c                   else
6.31 c                   eval      w_ldo2 = vvldor
6.31 c                   endif
6.31 c                   call      'VE713R'
6.31 c                   parm                    w_firm
6.31 c                   parm                    w_ldo2
6.31 c                   parm                    vvvare
6.31 c                   parm                    veenhe
6.31 c                   parm                    w_eann
6.31 c                   parm                    b_eann
6.31  *
6.31 c                   eval      vxeann = w_eann
      *
     c                   write     vvaqpfr
      *
     c                   eval      s_lety = vplety
      *
     c                   endif
      *
     c     vvprl1_key    reade     vvprl1r                                92
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for les på vareregister
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for les på vare-enhet-register
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
      *
      * Key for les på vare-pris-register
      *
     c     vvprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvprl1_vare
6.20 c                   kfld                    vvprl1_ldor
5.70 c                   kfld                    vvprl1_prgr
     c                   kfld                    vvprl1_enhe
      *
      * Key for les på tilbuds-register
      *
     c     vtill1_key    klist
     c                   kfld                    w_firm
6.21 c                   kfld                    vtill1_ogrp
6.21 c                   kfld                    vtill1_hgrp
6.21 c                   kfld                    vtill1_ugrp
6.21 c                   kfld                    vtill1_ldor
     c                   kfld                    vtill1_vare
5.70 c                   kfld                    vtill1_prgr
     c                   kfld                    vtill1_lety
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag på alt. hovedgruppe
      *
     c     vahgl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vahgl1_hhgr
      *
      * Key for oppslag på alt. undergruppe
      *
     c     vaugl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vaugl1_uhgr
     c                   kfld                    vaugl1_uugr
      *
      * Key for oppslag på kode/status
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
      * Henter mva-faktor
      *
     c                   time                    w_udat
      *
     c                   call      'FÅ705R'
     c                   parm                    w_firm
     c                   parm                    w_udat
     c                   parm                    w_mvap
     c                   parm                    w_mvat
     c                   parm                    w_mvaf
5.70  *
5.70  * Henter prisgruppe
5.70  *
5.70 c                   call      'VL711R'
5.70 c                   parm                    w_prgr
      *
6.20 c                   eval      w_lage = l_lage
      *
     c                   endsr
