      /TITLE  Saldooverføring
     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RH691R                                              *
      * Beskrivelse . . : Saldooverføring                                     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      * V3.40     090402  Fjernet oppdatering av buntsummer/midlertidig       *
      * V5.20     220204  Korrigert regnskapsår/periode                       *
      * V5.40     070505  Lagt inn nullstilling av IB i INZR-rutinen          *
6.11  * R6.10     280909  Lagt inn sporingsinfomasjon                     BSA *
6.20  * R6.20     170611  Ny post i nummerserie dersom år ikke ligger     BSA *
      *                   inne fra før
6.21  * R6.20     180412  Flytter blank til RTPROS                       NHO  *
6.30  * R6.30     040719  Utvidet saldofelter                            bsa  *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frhsal1    if   e           k disk    rename(rhsapfr:rhsal1r)
6.20 f*raa4lu    uf   e           k disk    rename(raa4pfr:raa4lur)
6.20 fraa4lu    uf a e           k disk    rename(raa4pfr:raa4lur)
     fra19l1    if   e           k disk    rename(ra19pfr:ra19l1r)
     frbunlu    uf a e           k disk    rename(rbunpfr:rbunlur)
     frtrapf    o  a e             disk    rename(rtrapfr:rtrapfr)
     frhsalu    uf a e           k disk    rename(rhsapfr:rhsalur)
      *
6.30 d sal             s             15  2 dim(14)                              Saldo
      *
610  d rbunlu_nivo     s                   like(rsnivo)
610  d rbunlu_memb     s                   like(rsmemb)
610  d w_memb          s                   like(rsmemb)
     d                uds
     d rhpkda                300    305  0
     d rhpaar                         4  0
     d rhpovr                         1
      *
     d l_user                911    920
     d l_firm                944    946  0
     d l_navn                951    970
      *
     d w_ssdk          s              1
     d w_paar          s                   like(rhpaar)
     d w_biln          s                   like(rtbiln)
     d w_bunt          s                   like(rsbunt)
     d w_fors          s                   like(rsfØrs)
     d w_firm          s                   like(rsfirm)
     d w_linj          s              5  0
     d w_pdeb          s                   like(rspdeb)
     d w_pkre          s                   like(rspkre)
     d w_psum          s                   like(rspsum)
     d w_rper          s                   like(rsrper)
     d w_sist          s                   like(rssist)
      *
     d ra19l1_skod     s                   like(raskod)
     d rhsalu_kont     s                   like(rikont)
     d rhsalu_spes     s                   like(rispes)
     d rhsalu_avde     s                   like(riavde)
     d rhsalu_raar     s                   like(riraar)
     d rhsalu_type     s                   like(ritype)
      *
      *  Saldo-register - Kontonr.rekkefølge
      *
     irhsal1r
     i              risa01                      sal(01)
     i              risa02                      sal(02)
     i              risa03                      sal(03)
     i              risa04                      sal(04)
     i              risa05                      sal(05)
     i              risa06                      sal(06)
     i              risa07                      sal(07)
     i              risa08                      sal(08)
     i              risa09                      sal(09)
     i              risa10                      sal(10)
     i              risa11                      sal(11)
     i              risa12                      sal(12)
     i              risa13                      sal(13)
     i              risa00                      sal(14)
      *
      /eject
      *
     c     rhsal1_key    setll     rhsal1r                                60
     c     rhsal1_key    reade     rhsal1                                 60
      *
     c                   dow       *in60 = *off
      *
      * Kontroll av regnskapsår/rekordtype
      *
     c                   if        riraar <> w_paar or
     c                             ritype <> 1
     c                   goto      neste
     c                   endif
      *
      * Kontroll av kontoklasse
      *
     c                   movel     rikont        ra19l1_skod
     c     ra19l1_key    chain     ra19l1                                        klasse-
      *
     c                   if        not%found                                    Kode
     c                   goto      neste                                         finnes
     c                   endif                                                    ikke
      *
      * Kontroll av statuskode
      *
     c                   movel     rassdk        w_ssdk
      *
     c                   if        w_ssdk <> '1'
     c                   goto      neste
     c                   endif
      *
     c                   xfoot     sal           rtbelØ
      *
      * Kontroll av beløp = 0
      *
     c                   if        rtbelØ = *zero
     c                   goto      neste
     c                   endif
      *
      * Kontroll av type overføring
      *
     c                   if        rhpovr = 'M'
     c                   exsr      saldo
     c                   else
     c                   exsr      postering
     c                   endif
      *
     c     neste         tag
      *
     c     rhsal1_key    reade     rhsal1                                 60
      *
     c                   enddo
      *
      /eject
      *
     c     slutt         tag
      *
      *  Oppdater buntrecord i posteringsfile
      *
     c                   if        rhpovr = 'P'
     c                   move      w_firm        rsfirm
     c                   move      w_bunt        rsbunt
     c                   eval      rsstat = *blank
     c                   move      01            rsrper
     c                   move      rhpaar        rsraar
     c                   move      w_fors        rsfØrs
     c                   move      w_sist        rssist
     c     *dmy          move      rhpkda        rspdat
     c                   move      w_psum        rsbsum
     c                   move      w_psum        rspsum
     c                   move      w_pdeb        rspdeb
     c                   move      w_pkre        rspkre
      *
610  c                   eval      rsnivo = *blank
610  c                   eval      rsmemb = w_memb
610  c                   time                    rsedat
610  c                   time                    rsetim
610  c                   eval      rseusr = l_user
     c                   write     rbunlur
     c                   endif
      *
     c                   eval      *inlr = *on
      *
     c/eject
      *
      * Legg ut saldo-record (IB)
      *
     c     saldo         begsr
      *
     c                   eval      rhsalu_kont = rikont
     c                   eval      rhsalu_spes = rispes
     c                   eval      rhsalu_avde = riavde
     c                   eval      rhsalu_raar = rhpaar
     c                   eval      rhsalu_type = ritype
     c     rhsalu_key    chain     rhsalu
      *
     c                   eval      risa00= rtbelØ
6.11 c                   time                    riedat
6.11 c                   time                    rietim
6.11 c                   eval      rieusr = l_user
      *
     c                   if        not%found
     c                   eval      riraar = rhpaar
     c                   eval      risa01= *zeros
     c                   eval      risa02= *zeros
     c                   eval      risa03= *zeros
     c                   eval      risa04= *zeros
     c                   eval      risa05= *zeros
     c                   eval      risa06= *zeros
     c                   eval      risa07= *zeros
     c                   eval      risa08= *zeros
     c                   eval      risa09= *zeros
     c                   eval      risa10= *zeros
     c                   eval      risa11= *zeros
     c                   eval      risa12= *zeros
     c                   eval      risa13= *zeros
6.11 c                   time                    riodat
6.11 c                   time                    riotim
6.11 c                   eval      riousr = l_user
     c                   write     rhsalur
     c                   else
     c                   update    rhsalur
     c                   endif
      *
     c                   endsr
      *
     c/eject
      *
      * Legg ut postering (IB)
      *
     c     postering     begsr
      *
      * Summer posteringer til buntsum
      *
     c                   eval      w_sist = w_sist + 1
     c                   eval      w_linj = w_linj + 1
      *
      * Legg ut postering
      *
     c                   eval      rtreid ='T'
     c                   eval      rtfirm = w_firm
     c                   eval      rtbunt = w_bunt
     c                   eval      rtlinj = w_linj
     c                   eval      rtstat = *blank
     c     *dmy          move      rhpkda        rtbdat
     c                   eval      rtrper = *zero
     c                   eval      rtraar = rhpaar
     c                   eval      rtbiln = w_biln
     c                   eval      rtdavd = *zeros
     c                   eval      rtdkto = *zeros
     c                   eval      rtdsps = *zeros
     c                   eval      rtdmva = *blank
     c                   eval      rtcavd = *zeros
     c                   eval      rtckto = *zeros
     c                   eval      rtcsps = *zeros
     c                   eval      rtcmva = *blank
      *
     c                   if        rtbelØ > *zero
      *
     c                   eval      rtdavd = riavde
     c                   eval      rtdkto = rikont
     c                   eval      rtdsps = rispes
     c                   eval      rtdmva = *zero
      *
     c                   else
      *
     c                   eval      rtbelØ = 0 - rtbelØ
     c                   eval      rtcavd = riavde
     c                   eval      rtckto = rikont
     c                   eval      rtcsps = rispes
     c                   eval      rtcmva = *zeros
      *
     c                   endif
      *
6.21 c**                 eval      rtpros = *zeros
6.21 c                   eval      rtpros = *blank
     c                   eval      rtrefn = *zeros
     c                   eval      rtfdat = *loval
     c                   eval      rtkrab = *zero
     c                   eval      rtvalk = *blank
     c                   eval      rtvalb = *zeros
     c                   eval      rtakti = *blank
     c                   eval      rtmngd = *zeros
     c                   eval      rtmngk = *zeros
     c                   eval      rtresn = *zeros
     c                   eval      rtvirk = *blank
     c                   eval      rtatte = *zeros
     c                   eval      rtkrys = *blank
     c                   eval      rtkidi = *blank
     c                   eval      rtbilk = 98
     c                   eval      rttext = 'Saldooverføring'
     c                   eval      rtbetb = *zeros
     c                   eval      rtdeck = *blank
      *
610  c                   eval      rtnivo = *blank
610  c                   eval      rtmemb = w_memb
610  c                   time                    rtedat
610  c                   time                    rtetim
610  c                   eval      rteusr = l_user
     c                   write     rtrapfr
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
610  c     *entry        plist
610  c                   parm                    w_memb                         user
     c                   move      l_firm        w_firm
     c                   eval      w_paar = rhpaar - 1
      *
      * Nullstill IB
      *
     c     rhsal1_key    setll     rhsal1r                                60
     c     rhsal1_key    reade     rhsal1                                 60
      *
     c                   dow       *in60 = *off
      *
     c                   if        riraar = rhpaar and
     c                             ritype = 1
      *
     c                   eval      rhsalu_kont = rikont
     c                   eval      rhsalu_spes = rispes
     c                   eval      rhsalu_avde = riavde
     c                   eval      rhsalu_raar = riraar
     c                   eval      rhsalu_type = ritype
     c     rhsalu_key    chain     rhsalu
     c                   eval      risa00 = 0
6.11 c                   time                    riedat
6.11 c                   time                    rietim
6.11 c                   eval      rieusr = l_user
     c                   update    rhsalur
      *
     c                   endif
      *
     c     rhsal1_key    reade     rhsal1                                 60
      *
     c                   enddo
      *
      *  Finn ledig buntnr. for IB-poster hvis permanent overføring
      *
     c                   if        rhpovr = 'P'
      *
610  c                   eval      rbunlu_nivo = *blank
610  c                   eval      rbunlu_memb = w_memb
     c                   eval      rsbunt = 99999
     c     rbunlu_key    setgt     rbunlu
     c                   readp     rbunlu                                 10
      *
     c                   if        *in10 = *on
     c                   eval      w_bunt = 1
     c                   else
     c                   eval      w_bunt = rsbunt + 1
     c                   endif
      *
     c                   eval      w_linj = 1
      *
     c                   eval      w_sist = w_linj
     c                   eval      w_psum = 0
     c                   eval      w_fors = 2
     c                   eval      w_sist = 1
      *
      * Hent sist brukte bilagsnummer
      *
     c                   move      rhpaar        ra4aar
     c     raa4lu_key    chain     raa4lu
      *
     c                   if        %found
     c                   eval      w_biln = ra4sgb + 1
     c                   move      w_biln        ra4sgb
     c                   update    raa4lur
      *                                                      .
6.20 c                   else
6.20 c                   eval      ra4sgb = 1
6.20 c                   eval      w_biln = 1
6.20 c                   write     raa4lur
6.20 c                   endif
      *
      *
     c                   endif
      *
      * Key for SALDOREGISTER
      *
     c     rhsal1_key    klist
     c                   kfld                    w_firm
      *
      * Key for SALDOREGISTER
      *
     c     rhsalu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhsalu_kont
     c                   kfld                    rhsalu_spes
     c                   kfld                    rhsalu_avde
     c                   kfld                    rhsalu_raar
     c                   kfld                    rhsalu_type
      *
      * Key for file RBUNL1 nummerisk
      *
     c     rbunlu_key    klist
610  c                   kfld                    rbunlu_nivo
610  c                   kfld                    rbunlu_memb
     c                   kfld                    w_firm
     c                   kfld                    rsbunt
      *
      * Key for statuskode
      *
     c     raa4lu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra4aar
      *
      * Key for kontklasser
      *
     c     ra19l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra19l1_skod
      *
     c                   endsr
