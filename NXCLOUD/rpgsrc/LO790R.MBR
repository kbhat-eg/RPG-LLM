     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LO790R
      *  Beskrivelse . . : Generere bestilling i EDIFAKT-format
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. : Kopiert fra LO760R og tilpasset
      *  Tilpasset for . : Best. til Norgros (erstatter XML best.)
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      *  --------- ------  -------------------------------------------------
      *  R5.52     110907  Legger også ut NAD;UD hvis kundeordre,   TSV
      *                    endring ifgm. endring til EDI på Norgros
      *  R5.53     120907  Null i DP EAN (C082 3039) hvis best. på  TSV
      *                    kundeordre
      *  R5.54     140907  blanker NAD når ean-verd er 0            TSV
      *
      *  R5.55     170907  Kunderef. tekst som ekstra parameter     TSV
6.20  *  R6.20     060114  Endret GLN nummer                        bsa
6.30  *   6.30     010314  Bruker EDI kode på RA31PF på ED09        bof
6.nu  *   6.30     120614  Endring iforb. med nummerutvidelse       bof
6.31  *   6.30 bkv 180515  Endret LVAR pga økt fra 15 til 20 tegn
6.32  *   6.30 bsa 020217  Fjernet sjekk mot testflagg, da Logiq ikke
6.32  *   6.30             har tester på dette.
6.33  * 6.30  101018 bof  Utvidet med trelast sortiment
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *    50-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     fraa8l1    if   e           k disk    rename(raa8pfr:raa8l1r)
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
     fvltyl1    if   e           k disk    rename(vltypfr:vltyl1r)
     fra09lr    if   e           k disk    rename(ra09pfr:ra09lrr)
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flotxl1    if   e           k disk    rename(lotxpfr:lotxl1r)
     flodtl1    if   e           k disk    rename(lodtpfr:lodtl1r)
     flldilr    if   e           k disk    rename(lldipfr:lldilrr)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fveanl2    if   e           k disk    rename(veanpfr:veanl2r)
     fmjlol1    if   e           k disk    rename(mjlopfr:mjlol1r)
R5.52frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
R5.52ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
6.30 fra31l1    if   e           k disk    rename(ra31pfr:ra31l1r)
     flwedpf    o    e           k disk
     flwelpf    o    e           k disk
      *
      *  Kommunikasjons-linje til IBM
      *
     d                 ds
     d d_ed99                        80
     d  d_fmsr99                      5    overlay(d_ed99)     inz('FMS01')
     d  d_avsn99                      8    overlay(d_ed99:6)
     d  d_skil99                      4    overlay(d_ed99:14)  inz('    ')
     d  d_mott99                      8    overlay(d_ed99:18)  inz('NOIBMDIN')
      *
      *  Definering av datastrukturer for EDI-format
      *
     d                 ds
     d d_ed00                        80
     d  d_id00                        4    overlay(d_ed00)     inz('ED00')
     d  d_decs00                      1    overlay(d_ed00:5)   inz(',')
     d  d_send00                     13  0 overlay(d_ed00:6)
     d  d_mott00                     13  0 overlay(d_ed00:19)
     d  d_gdat00                      6    overlay(d_ed00:32)
     d  d_gtim00                      4    overlay(d_ed00:38)
     d  d_test00                      1    overlay(d_ed00:42)  inz('0')
     d  d_kva100                      3    overlay(d_ed00:43)  inz('137')
     d  d_mdat00                     12    overlay(d_ed00:46)
     d  d_for100                      3    overlay(d_ed00:58)  inz('203')
     d  d_kva200                      3    overlay(d_ed00:61)  inz('2  ')
     d  d_levd00                      8    overlay(d_ed00:64)
     d  d_for200                      3    overlay(d_ed00:72)  inz('102')
      *
     d                 ds
     d d_ed01                        80
     d  d_id01                        4    overlay(d_ed01)     inz('ED01')
     d  d_mref01                     10    overlay(d_ed01:5)
     d  d_mtyp01                      6    overlay(d_ed01:15)  inz('ORDERS')
     d  d_mver01                      3    overlay(d_ed01:21)  inz('D  ')
     d  d_mrel01                      3    overlay(d_ed01:24)  inz('93A')
     d  d_mkon01                      2    overlay(d_ed01:27)  inz('UN')
     d  d_mfor01                      6    overlay(d_ed01:29)  inz('TBF10 ')
     d  d_mkod01                      3    overlay(d_ed01:35)  inz('220')
     d  d_benr01                     35    overlay(d_ed01:38)
     d  d_resp01                      3    overlay(d_ed01:73)  inz('AC ')
      *
     d                 ds
     d d_ed02                        80
     d  d_id02                        4    overlay(d_ed02)     inz('ED02')
     d  d_ftxq02                      3    overlay(d_ed02:5)   inz('ORI')
     d  d_ftxt02                     70    overlay(d_ed02:8)
      *
     d                 ds
     d d_ed03                        80
     d  d_id03                        4    overlay(d_ed03)     inz('ED03')
     d  d_refq03                      3    overlay(d_ed03:5)
     d  d_rtx103                      5    overlay(d_ed03:8)
6.nu d  d_ref103                      8    overlay(d_ed03:13)
6.nu d  d_rtx203                      5    overlay(d_ed03:21)
6.nu d  d_ref203                      8    overlay(d_ed03:26)
5.54 d  d_fix                        25    overlay(d_ed03:5)
5.55 d  d_text                       35    overlay(d_ed03:8)
      *
     d                 ds
     d d_ed04                        80
     d  d_id04                        4    overlay(d_ed04)     inz('ED04')
     d  d_parq04                      3    overlay(d_ed04:5)
     d  d_part04                     13  0 overlay(d_ed04:8)
     d  d_porg04                      3    overlay(d_ed04:21)  inz('9  ')
5.54 d  d_part04_blk                 13    overlay(d_ed04:8)
      *
     d                 ds
     d d_ed05                        80
     d  d_id05                        4    overlay(d_ed05)     inz('ED05')
     d  d_pnav05                     30    overlay(d_ed05:5)
     d  d_pad105                     30    overlay(d_ed05:35)
      *
     d                 ds
     d d_ed06                        80
     d  d_id06                        4    overlay(d_ed06)     inz('ED06')
     d  d_pad206                     30    overlay(d_ed06:5)
     d  d_ppnu06                      4    overlay(d_ed06:35)
     d  d_ppst06                     30    overlay(d_ed06:39)
      *
     d                 ds
     d d_ed07                        80
     d  d_id07                        4    overlay(d_ed07)     inz('ED07')
     d  d_refq07                      3    overlay(d_ed07:5)   inz('VA ')
     d  d_forp07                      2    overlay(d_ed07:8)   inz('NO')
     d  d_fore07                      9    overlay(d_ed07:10)
     d  d_fors07                      3    overlay(d_ed07:19)  inz('MVA')
     d  d_ctfu07                      3    overlay(d_ed07:22)  inz('PD ')
     d  d_kont07                     30    overlay(d_ed07:25)
      *
     d                 ds
     d d_ed08                        80
     d  d_id08                        4    overlay(d_ed08)     inz('ED08')
     d  d_komm08                     70    overlay(d_ed08:5)
     d  d_komq08                      3    overlay(d_ed08:75)
      *
     d                 ds
     d d_ed09                        80
     d  d_id09                        4    overlay(d_ed09)     inz('ED09')
     d  d_trsq09                      3    overlay(d_ed09:5)   inz('20 ')
     d  d_trko09                      3    overlay(d_ed09:8)
     d  d_fmko09                      8    overlay(d_ed09:11)  inz('Z02')
     d  d_lebq09                      3    overlay(d_ed09:19)  inz('6  ')
     d  d_levb09                      3    overlay(d_ed09:22)
     d  d_levq09                      3    overlay(d_ed09:25)  inz('106')
     d  d_lskq09                      3    overlay(d_ed09:28)  inz('   ')
     d  d_lbst09                     13    overlay(d_ed09:31)
     d  d_lsko09                      3    overlay(d_ed09:44)  inz('   ')
      *
     d                 ds
     d d_ed10                        80
     d  d_id10                        4    overlay(d_ed10)     inz('ED10')
     d  d_vlin10                      6  0 overlay(d_ed10:5)
     d  d_eanr10                     15    overlay(d_ed10:11)
     d  d_eank10                      3    overlay(d_ed10:26)  inz('EN ')
     d  d_piq110                      3    overlay(d_ed10:29)  inz('5  ')
     d  d_alt110                     15    overlay(d_ed10:32)
     d  d_alk110                      3    overlay(d_ed10:47)  inz('NO1')
     d  d_piq210                      3    overlay(d_ed10:50)  inz('1  ')
     d  d_alt210                     15    overlay(d_ed10:53)
     d  d_alk210                      3    overlay(d_ed10:68)  inz('SA ')
      *
     d                 ds
     d d_ed11                        80
     d  d_id11                        4    overlay(d_ed11)     inz('ED11')
     d  d_txk111                      3    overlay(d_ed11:5)   inz('F  ')
     d  d_txt111                     35    overlay(d_ed11:8)
     d  d_txt211                     35    overlay(d_ed11:43)
      *
     d                 ds
     d d_ed12                        80
     d  d_id12                        4    overlay(d_ed12)     inz('ED12')
     d  d_kvaq12                      3    overlay(d_ed12:5)   inz('21 ')
     d  d_kvah12                     11  0 overlay(d_ed12:8)
     d  d_kvas12                      1    overlay(d_ed12:19)  inz(',')
     d  d_kvad12                      3  3 overlay(d_ed12:20)
     d  d_benh12                      3    overlay(d_ed12:23)
      *
     d                 ds
     d d_ed13                        80
     d  d_id13                        4    overlay(d_ed13)     inz('ED13')
     d  d_lftq13                      3    overlay(d_ed13:5)   inz('LIN')
     d  d_lftx13                     70    overlay(d_ed13:8)
      *
      * Datastruktur for mottakers lokasjonsnummer
      *
     d d_lknr          ds
     d  d_eana                       13
     d    d_eann                     13  0 overlay(d_eana:1)
     d  d_fill                        7
      *
      * Definering av LDA
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d lohel1_numm     s                   like(lonumm)
     d lohel1_suff     s                   like(losuff)
     d lotxl1_numm     s                   like(ltnumm)
     d lotxl1_suff     s                   like(ltsuff)
     d lodtl1_numm     s                   like(ldnumm)
     d lodtl1_suff     s                   like(ldsuff)
     d lodtl1_line     s                   like(ldline)
     d lldilr_ldor     s                   like(lpldor)
     d lldilr_lage     s                   like(lplage)
     d rlevl1_levr     s                   like(rllevr)
     d vvarl1_vare     s                   like(vvvare)
     d veanl2_vare     s                   like(vxvare)
     d ra09lr_ikod     s                   like(raikod)
     d vltyl1_ltyp     s                   like(valtyp)
     d mjlol1_lage     s                   like(mjlage)
     d mjlol1_avde     s                   like(mjavde)
     d mjlol1_skaf     s                   like(mjskaf)
5.52 d fohel1_loornr   s                   like(loornr)
5.52 d fohel1_loorsu   s                   like(loorsu)
5.52 d rkunl1_fokund   s                   like(fokund)
6.30 d ra31l1_ekod     s                   like(reekod)
      *
      *  Definering av variable i parameter
      *
     d p_numm          s                   like(lonumm)
     d p_suff          s                   like(losuff)
5.55 d p_text          s             35
      *
      * Definering av øvrige variable
      *
     d w_porg04        s                   like(d_porg04)
     d w_firm          s                   like(lofirm)
     d w_dalf          s              8
     d w_balf          s              6
     d w_salf          s              2
     d w_levb          s              3
     d w_lage          s                   like(lplage)
     d w_tr01          s              1  0
     d w_refn          s             10    inz('1         ')
     d w_udat          s               d   datfmt(*iso)
     d w_utim          s               t   timfmt(*iso)
     d w_dato_8        s              8  0
     d w_time_6        s              6  0
     d w_dato_alf      s              8
     d w_time_alf      s              4
     d w_dat6_alf      s              6
     d w_tim4_alf      s              4
     d w_pibm          s              8
     d w_hean          s             13  0
     d w_fean          s             13  0
     d w_lean          s             13  0
     d w_sean          s             13  0
6.33 d w_utda          s                   like(vvedat)
6.33 d w_lv_ldor       s                   like(vvldor)
6.33 d w_lv_vtyp       s                   like(vvvtyp)
6.33 d w_lv_nobb       s                   like(vvnobb)
6.33 d w_lv_modn       s                   like(vvnmod)
6.33 d w_lv_lldo       s                   like(vvldor)
6.33 d w_lv_llva       s                   like(vvlvar)
6.33 d b_inlr          s              1    inz(*off)
      *
     d b_text          s              1    inz(*off)
     d b_lnut          s              1    inz(*off)
     d b_ed10          s              1    inz(*off)
     d b_logp          s              1    inz(*off)
      *
     d digits          c                   ' 0123456789'
      *
5.52  * Fast EAN for NORGROS.
      *
6.20 d*c_eano          s             13  0 inz(7080000915798)
6.20 d c_eano          s             13  0 inz(7080000864102)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Uthenting av kode-, ordrehode-, leverandør- og lageropplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Hent faste data
      *
     c     raa8l1_key    chain     raa8l1
     c                   if        not %found
     c                   goto      end_pgm
     c                   endif
      *
     c                   eval      w_pibm = ra8ibm
     c                   eval      w_hean = ra8ean
     c                   eval      w_fean = w_hean
     c                   eval      w_lean = w_hean
     c                   eval      w_lage = *blank
      *
      * Hent bestillingsinfo
      *
     c     lohel1_key    chain     lohel1r
     c                   if        not %found
     c                   goto      end_pgm
     c                   endif
     c                   eval      w_levb = %subst(lolbtx:1:3)
      *
      * Hent distribusjonsinformasjon
      *
     c                   eval      lldilr_ldor = loldor
     c                   move      lolage        lldilr_lage
     c     lldilr_key    chain     lldilr
     c                   if        not %found
     c                   goto      end_pgm
     c                   endif
      *
     c                   eval      d_lknr = lplknr
      *
      * Hent leverandørinformasjon
      *
     c                   eval      rlevl1_levr = loldor
     c     rlevl1_key    chain     rlevl1
     c                   if        not %found
     c                   goto      end_pgm
     c                   endif
      *
     c***                eval      w_sean = rleanl
     c                   eval      w_sean = c_eano
      *
6.32 c**                 if        ra8tst <> *blank or
6.32 c**                           rledik <> *blank
6.32 c**                 eval      d_test00 = '1'
6.32 c**                 endif
      *
     c                   if        rlflev <> 0
     c                   eval      rlevl1_levr = rlflev
     c     rlevl1_key    chain     rlevl1
     c                   if        %found and
     c                             rleanl <> 0
     c                   eval      w_fean = rleanl
     c                   endif
     c                   endif
      *
      * Hent selgerinformasjon  (e-mail)
      *
     c                   eval      ra09lr_ikod = loinnk
     c     ra09lr_key    chain     ra09lr
     c                   if        not %found
     c                   eval      raikod = 0
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Utlegging av kommunikasjonsinfo (IBM-FMS)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * ED99
      *
     c                   if        lpmott = 'IBM            '
     c                   eval      d_avsn99 = w_pibm
      *
     c                   if        lptype = 'FTP      '
     c                   eval      edilin = d_ed99
     c                   write     lwedpfr
     c                   eval      logrec = d_ed99
     c                   write     lwelpfr
     c                   endif
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Utlegging av hode-linjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * ED00  - Info til tjenestesegment
      *
     c                   eval      d_send00 = w_hean
      *
     c                   if        d_eana <> *blank and
     c                             %len(%trim(d_eana)) = 13 and
     c                             %check(digits : d_eana) = 0
     c                   eval      d_mott00 = d_eann
     c                   else
     c                   eval      d_mott00 = w_sean
     c                   endif
      *
     c                   eval      d_gdat00 = w_dat6_alf
     c                   eval      d_gtim00 = w_tim4_alf
     c                   eval      d_mdat00 = w_dato_alf + w_time_alf
      *
     c                   if        loldat <> *loval
     c     *iso          move      loldat        w_dato_8
     c                   move      w_dato_8      d_levd00
     c                   else
     c                   eval      d_levd00 = w_dato_alf
     c                   endif
      *
     c                   eval      edilin = d_ed00
     c                   write     lwedpfr
     c                   eval      logrec = d_ed00
     c                   write     lwelpfr
      *
      * ED01 - Meldings start informasjon
      *
     c                   eval      d_mref01 = w_refn
     c                   movel     lonumm        d_benr01
     c                   eval      edilin = d_ed01
     c                   write     lwedpfr
     c                   eval      logrec = d_ed01
     c                   write     lwelpfr
      *
      * ED02 - Fritekst på hodenivå
      *
     c                   eval      lotxl1_numm = lonumm
     c                   eval      lotxl1_suff = losuff
     c     lotxl1_key    setll     lotxl1
     c     lotxl1_key    reade     lotxl1
      *
     c                   dow       not %eof
     c                   eval      d_ftxt02 = lttext
     c                   eval      edilin = d_ed02
     c                   write     lwedpfr
     c                   eval      logrec = d_ed02
     c                   write     lwelpfr
      *
     c     lotxl1_key    reade     lotxl1
     c                   enddo
      *
      * ED03 - Referanse fra bestilling og evt. kundeordre
      *
     c                   eval      d_refq03 = 'CR '
     c                   eval      d_rtx103 = 'BEST '
     c                   movel     lonumm        d_ref103
     c                   if        loornr <>  0
     c                   eval      d_rtx203 = '/ONR '
     c                   movel     loornr        d_ref203
     c                   endif
     c                   eval      edilin = d_ed03
     c                   write     lwedpfr
     c                   eval      logrec = d_ed03
     c                   write     lwelpfr
5.54  * TESTING HARDKODE
      * ED03 - Kunderef. fra eget parameter ulikt *blankt
      *
5.55 c                   if        p_text <> *blanks
     c                   eval      d_fix = *blanks
     c                   eval      d_refq03 = 'PD '
5.55 c                   eval      d_text = p_text
     c                   eval      edilin = d_ed03
     c                   write     lwedpfr
     c                   eval      logrec = d_ed03
5.54 c                   write     lwelpfr
5.55 c                   endif
      *
      * ED04 - ED08 - Partsidentifisering
      *
      * BY -kjøper
      *
     c                   eval      d_parq04 = 'BY '
     c                   eval      d_part04 = w_hean
     c                   eval      edilin = d_ed04
     c                   write     lwedpfr
     c                   eval      logrec = d_ed04
     c                   write     lwelpfr
      *
      *  Foretaksnummer og kjøpers referanse
      *
     c                   movel     aafftn        d_fore07
     c                   eval      d_kont07 = lovref
     c                   eval      edilin = d_ed07
     c                   write     lwedpfr
     c                   eval      logrec = d_ed07
     c                   write     lwelpfr
      *
      *  Kommunikasjonsinformasjon
      *
     c                   if        raikod <> 0 and
     c                             raiema <> *blank
     c                   eval      d_komm08 = raiema
     c                   eval      d_komq08 = 'EM '
     c                   eval      edilin = d_ed08
     c                   write     lwedpfr
     c                   eval      logrec = d_ed08
     c                   write     lwelpfr
     c                   endif
      *
      * SU -selger
      *
     c                   eval      d_parq04 = 'SU '
     c                   eval      d_part04 = w_sean
     c                   eval      edilin = d_ed04
     c                   write     lwedpfr
     c                   eval      logrec = d_ed04
     c                   write     lwelpfr
      *
      * IV -fakturammotager
      *
     c                   eval      d_parq04 = 'IV '
     c                   eval      d_part04 = w_fean
     c                   eval      edilin = d_ed04
     c                   write     lwedpfr
     c                   eval      logrec = d_ed04
     c                   write     lwelpfr
      *
      * SF -Hentested
      *
     c                   if        w_levb = 'EXW'
     c                   eval      d_parq04 = 'SF '
     c                   eval      d_part04 = w_sean
     c                   eval      edilin = d_ed04
     c                   write     lwedpfr
     c                   eval      logrec = d_ed04
     c                   write     lwelpfr
     c                   endif
      *
      * DP -varemottaker - bruker logistikkpkt. hvis det er registrert.
      *
     c                   exsr      finn_logpkt
      *
     c                   if        b_logp = *off
     c                   eval      d_parq04 = 'DP '
5.52 c                   if        loornr =  0
5.52 c                   eval      d_part04 = 0
5.52 c                   else
     c                   eval      d_part04 = w_lean
5.52 c                   endif
5.54 c                   eval      w_porg04 = d_porg04
5.54 c                   if        d_part04 = 0
5.54 c                   eval      d_porg04 = *blanks
5.54 c                   eval      d_part04_blk = *blanks
5.54 c                   endif
     c                   eval      edilin = d_ed04
     c                   write     lwedpfr
     c                   eval      logrec = d_ed04
     c                   write     lwelpfr
5.54 c                   eval      w_porg04 = d_porg04
      *
     c                   eval      d_pnav05 = lonavn
     c                   eval      d_pad105 = loadr1
     c                   eval      edilin = d_ed05
     c                   write     lwedpfr
     c                   eval      logrec = d_ed05
     c                   write     lwelpfr
      *
     c                   eval      d_pad206 = loadr2
     c                   eval      d_ppnu06 = %subst(losted:1:4)
     c                   eval      d_ppst06 = %subst(losted:6)
     c                   eval      edilin = d_ed06
     c                   write     lwedpfr
     c                   eval      logrec = d_ed06
     c                   write     lwelpfr
     c                   endif
      *
5.52  * UD  -ultimate costumer, hvis kundeordre oppgitt på best.
     c                   if         loornr <> 0
      * gr.lag
     c                   eval       fohel1_loornr = loornr
     c                   eval       fohel1_loorsu = loorsu
     c     fohel1_key    chain     fohel1
     c                   if        not %found
     c                   goto      skipp_ud
     c                   endif
     c                   eval       rkunl1_fokund = fokund
     c     rkunl1_key    chain     rkunl1
     c                   if        not %found
     c                   goto      skipp_ud
     c                   endif
      * -04
     c                   eval      d_parq04 = 'UD '
     c                   eval      d_part04 = rkeank
5.54 c                   eval      w_porg04 = d_porg04
5.54 c                   if        d_part04 = 0
5.54 c                   eval      d_porg04 = *blanks
5.54 c                   eval      d_part04_blk = *blanks
5.54 c                   endif
     c                   eval      edilin = d_ed04
     c                   write     lwedpfr
     c                   eval      logrec = d_ed04
     c                   write     lwelpfr
5.54 c                   eval      w_porg04 = d_porg04
      * -05
     c                   eval      d_pnav05 = fokuna
     c                   eval      d_pad105 = foadr1
     c                   eval      edilin = d_ed05
     c                   write     lwedpfr
     c                   eval      logrec = d_ed05
     c                   write     lwelpfr
      * -06
     c                   eval      d_pad206 = foadr2
     c                   eval      d_ppnu06 = %subst(fosted:1:4)
     c                   eval      d_ppst06 = %subst(fosted:6)
     c                   eval      edilin = d_ed06
     c                   write     lwedpfr
     c                   eval      logrec = d_ed06
     c                   write     lwelpfr
      * ferdig
     c     skipp_ud      tag
5.52 c                   endif
      *
6.30  * henter EDI kode fra forsendelsesmåte bestilling
6.30 c                   eval      ra31l1_ekod = lolmat
6.30 c     ra31l1_key    chain     ra31l1
6.30 c                   if        %found(ra31l1) and
6.30 c                             reeedi <> 0
6.30 c                   eval      lolmat = reeedi
6.30 c                   endif
      *
      * ED09 - Transport og leveringsinformasjon
      *
     c                   move      lolmat        w_tr01
     c                   movel     w_tr01        d_trko09
     c                   movel     w_tr01        d_fmko09
      *
     c                   if        w_levb = 'FCA' or
     c                             w_levb = 'FAS' or
     c                             w_levb = 'FOB' or
     c                             w_levb = 'CFR' or
     c                             w_levb = 'CIF' or
     c                             w_levb = 'CPT' or
     c                             w_levb = 'CIP' or
     c                             w_levb = 'DES' or
     c                             w_levb = 'DEQ' or
     c                             w_levb = 'DDU' or
     c                             w_levb = 'DDP' or
     c                             w_levb = 'EXW' or
     c                             w_levb = 'DAF'
     c                   eval      d_levb09 = w_levb
     c                   else
     c                   eval      d_levb09 = 'FOB'
     c                   endif
      *
     c                   eval      d_lbst09 = *blank
      *
     c                   eval      edilin = d_ed09
     c                   write     lwedpfr
     c                   eval      logrec = d_ed09
     c                   write     lwelpfr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Utlegging av detalj-linjer, tekstlinjer blir fritekst
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lodtl1_key    setll     lodtl1r
     c     lodtl1_key    reade     lodtl1r
      *
     c                   dow       not %eof
      *
      * Intern linjetype?
      *
     c                   exsr      sjekk_type
     c                   if        b_lnut = *off
     c                   goto      neste_linje
     c                   endif
      *
      * ED13 - Fritekstlinje
      *
     c                   if        b_text = *on
     c                   if        ldtek1 <> *blank or
     c                             ldtek2 <> *blank
     c                   eval      d_lftx13 = ldtek1 + ldtek2
     c                   if        b_ed10 = *on
     c                   eval      edilin = d_ed13
     c                   write     lwedpfr
     c                   eval      logrec = d_ed13
     c                   write     lwelpfr
     c                   eval      d_lftx13 = *blank
     c                   endif
     c                   endif
     c                   goto      neste_linje
     c                   endif
      *
      * ED10 - Nummer identifikasjon
      *
     c                   eval      d_vlin10 = ldline
      *
     c                   if        ldlvre = 1
     c                   movel     ldnobb        d_alt110
6.31 c*                   eval      d_alt210 = ldlvar
6.31 c                   movel     ldlvar        d_alt210
     c                   eval      d_eanr10 = *blank
     c                   else
     c                   exsr      finn_varenr
     c                   endif
      *
     c                   eval      edilin = d_ed10
     c                   write     lwedpfr
     c                   eval      logrec = d_ed10
     c                   write     lwelpfr
     c                   eval      b_ed10 = *on
      *
      * ED11 - Varebeskrivelse
      *
     c                   eval      d_txt111 = ldtek1
     c                   eval      d_txt211 = ldtek2
     c                   eval      edilin = d_ed11
     c                   write     lwedpfr
     c                   eval      logrec = d_ed11
     c                   write     lwelpfr
      *
      * ED12 - Mengde/enhetsangivelse
      *
     c*                  eval      d_kvah12 = %inth(ldanta)
     c                   eval      d_kvah12 = %int(ldanta)
     c                   eval      d_kvad12 = ldanta - d_kvah12
     c                   eval      d_benh12 = ldenh1
     c                   eval      edilin = d_ed12
     c                   write     lwedpfr
     c                   eval      logrec = d_ed12
     c                   write     lwelpfr
      *
     c                   if        d_lftx13 <> *blank
     c                   eval      edilin = d_ed13
     c                   write     lwedpfr
     c                   eval      logrec = d_ed13
     c                   write     lwelpfr
     c                   eval      d_lftx13 = *blank
     c                   endif
      *
     c     neste_linje   tag
     c     lodtl1_key    reade     lodtl1r                                94
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finne ean-nummer og nobb-nummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_varenr   begsr
      *
     c                   eval      veanl2_vare = ldvare
     c     veanl2_key    setll     veanl2
     c     veanl2_key    reade     veanl2
     c                   if        %found and
     c                             vxeann <> 0
     c                   movel     vxeann        d_eanr10
     c                   else
     c                   eval      d_eanr10 = *blank
     c                   endif
      *
      * henter info om logistikk-vare
6.33 c                   call      'VL710R'
6.33 c                   parm                    w_firm
6.33 c                   parm                    ldvare
6.33 c                   parm                    ldlage
6.33 c                   parm                    w_lv_vtyp
6.33 c                   parm                    w_utda
6.33 c                   parm                    w_lv_ldor
6.33 c                   parm                    b_inlr
6.33 c                   parm                    w_lv_nobb
6.33 c                   parm                    w_lv_modn
6.33 c                   parm                    w_lv_lldo
6.33 c                   parm                    w_lv_llva
     c                   if        ldnobb = 0
     c                   eval      vvarl1_vare = ldvare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
6.33 c                   if        w_lv_nobb <> 0
6.33 c                   movel     w_lv_nobb     d_alt110
6.33 c                   movel     w_lv_llva     d_alt210
6.33 c                   else
     c                   movel     vvnobb        d_alt110
6.31 c*                   eval      d_alt210 = vvlvar
6.31 c                   movel     vvlvar        d_alt210
6.33 c                   endif
     c                   else
     c                   eval      d_alt110 = *blanks
     c                   eval      d_alt210 = *blanks
     c                   endif
     c                   else
     c                   movel     ldnobb        d_alt110
6.31 c*                   eval      d_alt210 = ldlvar
6.31 c                   movel     ldlvar        d_alt210
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å sjekke om fritekstlinje som skal sendes
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_type    begsr
      *
     c                   eval      b_text = *off
     c                   eval      b_lnut = *on
      *
      * Les linjetype
      *
     c                   eval      vltyl1_ltyp = ldltyp
     c     vltyl1_key    chain     vltyl1
     c                   if        %found and
     c                             valktx = 1
     c                   eval      b_text = *on
     c                   endif
      *
      * Ikke skrives ut?
      *
     c                   if        lootyp = valo01 or
     c                             lootyp = valo02 or
     c                             lootyp = valo03 or
     c                             lootyp = valo04
     c                   eval      b_lnut = *off
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finne logistikkpunkt (varemottager)
      *
     c     finn_logpkt   begsr
      *
     c                   eval      b_logp = *off
      *
     c                   eval      mjlol1_lage = lolage
     c                   eval      mjlol1_avde = loavde
      *
     c                   if        loornr <> *zero
     c                   eval      mjlol1_skaf = 'S'
     c     mjlol1_key    chain     mjlol1
     c                   if        %found
     c                   eval      b_logp = *on
     c                   else
     c                   eval      mjlol1_avde = *zero
     c     mjlol1_key    chain     mjlol1
     c                   if        %found
     c                   eval      b_logp = *on
     c                   endif
     c                   endif
     c                   endif
      *
     c                   if        b_logp = *off
     c                   eval      mjlol1_lage = lolage
     c                   eval      mjlol1_avde = loavde
     c                   eval      mjlol1_skaf = *blank
     c     mjlol1_key    chain     mjlol1
     c                   if        %found
     c                   eval      b_logp = *on
     c                   else
     c                   eval      mjlol1_avde = *zero
     c     mjlol1_key    chain     mjlol1
     c                   if        %found
     c                   eval      b_logp = *on
     c                   endif
     c                   endif
     c                   endif
      *
     c                   if        b_logp = *off
     c                   leavesr
     c                   endif
      *
     c                   eval      d_parq04 = 'DP '
     c                   eval      d_part04 = mjeann
      *
     c                   eval      edilin = d_ed04
     c                   write     lwedpfr
     c                   eval      logrec = d_ed04
     c                   write     lwelpfr
      *
     c                   eval      d_pnav05 = mjsted
     c                   eval      d_pad105 = mjadr1
     c                   eval      edilin = d_ed05
     c                   write     lwedpfr
     c                   eval      logrec = d_ed05
     c                   write     lwelpfr
      *
     c                   eval      d_pad206 = mjadr2
      *
     c                   if        %subst(mjpste:1:4) <> *blank and
     c                             %check(digits : %subst(mjpste:1:4)) = 0
     c                   eval      d_ppnu06 = %subst(mjpste:1:4)
     c                   eval      d_ppst06 = %trim(%subst(mjpste:5))
     c                   else
     c                   eval      d_ppnu06 = *blank
     c                   eval      d_ppst06 = %trim(mjpste)
     c                   endif
      *
     c                   eval      edilin = d_ed06
     c                   write     lwedpfr
     c                   eval      logrec = d_ed06
     c                   write     lwelpfr
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Tar inn parameter fra forrige program
      *
     c     *entry        plist
     c                   parm                    p_numm
     c                   parm                    p_suff
5.55 c                   parm                    p_text
      *
      *  Key for file les FIRMA-OPPLYSNINGER
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      *  Key for file les FASTE DATA
      *
     c     raa8l1_key    klist
     c                   kfld                    w_firm
      *
      *  Key for file les LAGER-KODE
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      *  Key for les av BESTILLING-HODE
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
     c                   kfld                    lohel1_suff
      *
      *  Key for les av BESTILLING-TEKST-REGISTER
      *
     c     lotxl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lotxl1_numm
     c                   kfld                    lotxl1_suff
      *
      *  Key for file les BESTILLING-LINJE
      *
     c     lodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtl1_numm
     c                   kfld                    lodtl1_suff
      *
      * Key for oppslag på leverandør distribusjon
      *
     c     lldilr_key    klist
     C                   kfld                    w_firm
     c                   kfld                    lldilr_ldor
     c                   kfld                    lldilr_lage
      *
      *  Key for les av LEVERANDØR
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
R5.52 *  Key for les av KUNDEORDRE via best.kundeordre
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_loornr
     c                   kfld                    fohel1_loorsu
      *
R5.52 *  Key for les av KUNDE via kundeordre
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_fokund
      *
      * Key for les av vare-register
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for les av ean-register
      *
     c     veanl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    veanl2_vare
      *
      * Key for les av innkjøper
      *
     c     ra09lr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra09lr_ikod
      *
      * Key for oppslag på linjetype
      *
     c     vltyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltyl1_ltyp
      *
      * Key for posisjonering logistikkpunkt
      *
     c     mjlol1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mjlol1_lage
     c                   kfld                    mjlol1_avde
     c                   kfld                    mjlol1_skaf
6.30  *
6.30  * Key for oppslag på linjetype
6.30  *
6.30 c     ra31l1_key    klist
6.30 c                   kfld                    w_firm
6.30 c                   kfld                    ra31l1_ekod
      *
      *  Legger inn verdier i nøklene
      *
     c                   eval      w_firm = l_firm
     c                   eval      lohel1_numm = p_numm
     c                   eval      lohel1_suff = p_suff
     c                   eval      lodtl1_numm = p_numm
     c                   eval      lodtl1_suff = p_suff
      *
      *  Hent opplysninger fra FIRMA og LAGER-KODE
      *
     c     lstsl1_key    chain     lstsl1r
      *
     c     afirl1_key    chain     afirl1r
      *
      *  Legger inn dagens dato
      *
     C                   time                    w_udat
     C                   time                    w_utim
      *
     c     *iso          move      w_udat        w_dato_8
     c     *hms          move      w_utim        w_time_6
     c                   move      w_dato_8      w_dato_alf
     c                   movel     w_time_6      w_time_alf
     c                   move      w_dato_8      w_dat6_alf
     c                   movel     w_time_6      w_tim4_alf
      *
      *  Bygger nøkkel for loginformasjon
      *
     c                   eval      logind = 'O'
     c                   eval      logtyp = 'ORDR'
     c                   eval      logdat = w_udat
     c                   eval      logtid = w_utim
      *
     c                   endsr
