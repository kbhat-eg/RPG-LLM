      /TITLE  ASLAGR Lagerregister og lagerbok, bytter enhet på vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LL750R
      * Beskrivelse . . : Lagerregister og lagerbok, bytter enhet på vare
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       151199 ASK  Nytt program
      *       070307 bof  Omberegner også anbrekk
      * 6.10  260609 BSA  Oppdatert med sporingsinformasjon
6.11  * 6.10  050612 bof  Justert logikk vedr. lagerenhet
7.01  * 7.00  180719 bsa  Endret logikk rundt omregningsfaktor.
7.01  *                   Lagerenheten er enda ikke oppdatert. Bruk
7.01  *                   faktor fra angitt enhet i parameter.
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvhisl3    uf   e           k disk    rename(vhispfr:vhisl3r)
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     fvlaglu    uf   e           k disk    rename(vlagpfr:vlaglur)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
      *
6.10  * Definering av Local data area
6.10  *
6.10 d                uds
6.10 d l_user                911    920
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vhfirm)
     d p_vare          s                   like(vhvare)
     d p_enhe          s                   like(vhenhl)
      *
      * Definering av variable i nøkler
      *
     d vhisl3_vare     s                   like(vhvare)
     d vlagl1_vare     s                   like(vlvare)
     d vlaglu_vare     s                   like(vlvare)
     d vlaglu_lage     s                   like(vllage)
     d vvenl1_vare     s                   like(vevare)
     d vvenl1_enhe     s                   like(veenhe)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vlfirm)
     d w_omrl          s                   like(veomre)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram  - Historisk Lagerbok
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Finner omregningsfaktor volum for ny lagerenhet
      *
     c                   eval      vvenl1_vare = p_vare
     c                   eval      vvenl1_enhe = p_enhe
     c     vvenl1_key    chain     vvenl1                             90
     c                   if        *in90 = *on or
     c                             veomre = 0
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      w_omrl = veomre
      *
      * Leser alle rader i lagerbok for vare, og omregner antall i.h.t.
      * lagerenhet
      *
     c                   eval      vhisl3_vare = p_vare
     c     vhisl3_key    setll     vhisl3
     c     vhisl3_key    reade     vhisl3                                 90
     c                   dow       *in90 = *off
      *
     c                   if        vhenhl <> p_enhe
      *
     c                   eval      vvenl1_enhe = vhenhl
     c     vvenl1_key    chain     vvenl1                             91
     c                   if        *in91 = *off and
     c                             veomre <> 0
      *
     c                   eval      vhenhl = p_enhe
     c                   eval(h)   vhanta = vhanta * veomre / w_omrl
     c                   eval(h)   vhbehl = vhbehl * veomre / w_omrl
6.10 c                   time                    vhedat
6.10 c                   time                    vhetim
6.10 c                   eval      vheusr = l_user
     c                   update    vhisl3r
     c                   goto      les_neste
     c                   endif
      *
     c                   endif
      *
     c                   unlock    vhisl3
      *
     c     les_neste     tag
      *
     c     vhisl3_key    reade     vhisl3                                 90
     c                   enddo
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram  - Lagerregister
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser alle rader i lager for vare, og omregner antall i.h.t.
      * ny lagerenhet
      *
     c                   eval      *in90 = *off
     c                   eval      *in91 = *off
      *
     c                   eval      vlagl1_vare = p_vare
     c     vlagl1_key    setll     vlagl1
     c     vlagl1_key    reade     vlagl1                                 90
     c                   dow       *in90 = *off
      *
6.11 c*                  if        vlenhl <> p_enhe
      *
7.01  * Skal ikke hente inn omregningsfaktor fra lagerenhet, da denne
7.01  * enda har gammel verdi. Bruk verdier vi har fra før.
7.01 c*                  eval      vvenl1_enhe = vlenhl
7.01 c*    vvenl1_key    chain     vvenl1                             91
7.01 c*                  if        *in91 = *off and
7.01 c*                            veomre <> 0
7.01 c                   if        veomre <> 0
      *
     c                   eval      vlaglu_vare = vlvare
     c                   eval      vlaglu_lage = vllage
     c     vlaglu_key    chain     vlaglur                            91
     c                   if        *in91 = *off
     c                   eval      vlenhl = p_enhe
     c                   eval(h)   vlpakk = vlpakk * veomre / w_omrl
     c                   eval(h)   vloord = vloord * veomre / w_omrl
     c                   eval(h)   vlores = vlores * veomre / w_omrl
     c                   eval(h)   vlbbes = vlbbes * veomre / w_omrl
     c                   eval(h)   vlbres = vlbres * veomre / w_omrl
     c                   eval(h)   vlinng = vlinng * veomre / w_omrl
     c                   eval(h)   vlutta = vlutta * veomre / w_omrl
     c                   eval(h)   vljust = vljust * veomre / w_omrl
     c                   eval(h)   vltell = vltell * veomre / w_omrl
     c                   eval(h)   vlsalg = vlsalg * veomre / w_omrl
     c                   eval(h)   vlretu = vlretu * veomre / w_omrl
     c                   eval(h)   vlb101 = vlb101 * veomre / w_omrl
     c                   eval(h)   vlbehl = vlbehl * veomre / w_omrl
     c                   eval(h)   vlmini = vlmini * veomre / w_omrl
     c                   eval(h)   vlmaks = vlmaks * veomre / w_omrl
     c                   eval(h)   vlbmen = vlbmen * veomre / w_omrl
     c                   eval(h)   vlbpun = vlbpun * veomre / w_omrl
     c                   eval(h)   vlsikl = vlsikl * veomre / w_omrl
     c                   eval(h)   vlbmin = vlbmin * veomre / w_omrl
6.10 c                   time                    vledat
6.10 c                   time                    vletim
6.10 c                   eval      vleusr = l_user
     c                   update    vlaglur
     c                   endif
      *
     c                   endif
6.11 c*                  endif
      *
     c     vlagl1_key    reade     vlagl1                                 90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_enhe
      *
      * Key for les/oppdatering på historisk lagerbok
      *
     c     vhisl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhisl3_vare
      *
      * Key for les på lagerregister
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl1_vare
      *
      * Key for oppdatering på lagerregister
      *
     c     vlaglu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlaglu_vare
     c                   kfld                    vlaglu_lage
      *
      * Key for oppslag på vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
     c                   kfld                    vvenl1_enhe
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
