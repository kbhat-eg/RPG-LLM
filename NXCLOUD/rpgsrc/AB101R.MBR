     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : Administrasjon
      * Program . . . . : AB101R
      * Beskrivelse . . : Eksport av brukere.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       050224 bsa  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     fausrl1    if   e           k disk    rename(ausrpfr:ausrl1r)
     flxl4st    uf a e           k disk    rename(lxl4st:lxl4str)
      *
      * Definering av datastruktur
      *
     d                 ds
     d d_dato                         6  0
     d  d_aar                         2  0 overlay(d_dato:1)
     d  d_mnd                         2  0 overlay(d_dato:3)
     d  d_dag                         2  0 overlay(d_dato:5)
      *
     d                 ds
     d d_tid                          6  0
     d  d_tim                         2  0 overlay(d_tid:1)
     d  d_min                         2  0 overlay(d_tid:3)
     d  d_sek                         2  0 overlay(d_tid:5)
      *
     d                 ds
     d x_tid                          8
     d  x_tim                         2  0 overlay(x_tid:1)
     d  x_fil3                        1    overlay(x_tid:3) inz(':')
     d  x_min                         2  0 overlay(x_tid:4)
     d  x_fil4                        1    overlay(x_tid:6) inz(':')
     d  x_sek                         2  0 overlay(x_tid:7)
      *
      * Definering av Local data area
      *
     d*lda            uds
     d* l_fnav               951    980
      *
      * Definering av variable
      *
     d w_numm          s              8  0
     d w_kode          s              1
     d w_fell          s              6
     d w_type          s              2
     d w_fast          s             10
     d w_dato          s              6
     d w_tid           s              6
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_excel_url     s            100
     d w_firm          s              3  0
      *
     d p_excel_url     s            100
      *
     d u_excl          s              1    inz(*off)
      *
      * Eksternt program
      *
       dcl-s co402_verdi1  char(1);
       dcl-s co402_verdi2  char(2048);
       DCL-PR CO402R EXTPGM('CO402R');
         p_filgrp            char(3)     const;
         p_firm              packed(3:0) const;
         p_lager             packed(2:0) const;
         p_nokkel            char(50)    const;
         p_verdi1            char(1);
         p_verdi2            char(2048);
       END-PR;
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandle brukere                                               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c                   read      ausrl1
     c                   dow       not %eof
      *
     c                   clear                   lxl4str
      *
      * Hent info om brukerprofilen
      *
      /Free
      *
       exec sql
        select uppsod,
               uppsot
        into   :w_dato,
               :w_tid
        from   usrprf
        where  upuprf = :abuser;

      /End-Free
      *
     c                   move      w_dato        d_dato
     c                   move      w_tid         d_tid
     c                   eval      x_tim = d_tim
     c                   eval      x_min = d_min
     c                   eval      x_sek = d_sek
      * Legg ut brukerinformasjon i fila
      *
     c                   eval      lx4ide = w_numm
     c                   eval      lx4usr = abuser
     c                   eval      lx4nam = abnavn
     c                   if        %trim(w_dato) <> *blanks
     c     *ymd          move      d_dato        lx4pad
     c                   else
     c                   eval      lx4pad = *loval
     c                   endif
     c*    *hms          move      x_tid         lx4pat
     c                   if        %trim(w_tid) <> *blanks
     c     *hms          move      d_tid         lx4pat
     c                   else
     c                   eval      lx4pat = *loval
     c                   endif
     c                   eval      lx4lag = ablagr
     c                   eval      lx4avd = absavd
      *
     c                   write     lxl4str
      *
     c                   read      ausrl1
     c                   enddo
      *
      *  Kaller opp program for exccel eksport
      *
     c                   if        u_excl = *on
     c                   eval      p_excel_url = w_excel_url
     c                   call      'AB101C'
     c                   parm                    p_excel_url
     c                   parm                    w_numm
      *
     c/exec sql
     c+ DELETE FROM LXL4ST WHERE LX4IDE = :w_numm
     c/end-exec
       exec sql commit;
      *
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Henter inn l√∏penummer teller
      *
     c                   eval      w_firm = 0
     c                   eval      w_numm = 0
     c                   eval      w_kode = '0'
     c                   eval      w_fell = 'NXTKLI'
     c                   eval      w_type = *blank
     c                   eval      w_fast = *blank
     c                   eval      w_numm = *zero
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
     c                   parm                    w_numm
      *
      * OBS!!  Felles for alle filgrupper og firma
      *
       CallP CO402R('   ':0:0:'NX_EXCEL_URL':
                        co402_verdi1:co402_verdi2);
       if co402_verdi1 = '1';
         u_excl = *on;
         w_excel_url = co402_verdi2;
       endif;
      *
     c                   endsr
