      /TITLE  GL321R Navnsetting av filer til/fra banken
     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * System  . . . . : ASOKON                                        *
      * Program . . . . : GL321R                                        *
      * Beskrivelse . . : Navnsetting av filer til/fra banken          g*
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
      *           151198  Nytt program                             RH   *
      * 14113     141103  Lagt inn eget felt for TFF.              RH   *
      * 09056     090506  Tar med inn ny parameter for komm.type.  RH   *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KC = F3  = Exit                                           *
      *       KL = F12 = Annuller                                       *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     fgl321d    cf   e             workstn
     fgpcfpf    uf a e           k disk
      *
     d lda            uds
     d l_fgrp                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
09065d                 ds
09065d w_navn                        20
09065d  w_pos9                        1    overlay(w_navn:9)
      *
     d a1ffgr          s              3
     d w_arow          s              2  0
     d w_acol          s              3  0
     d wpffgr          s              3
09065d wpkomm          s              4
     d wpffir          s              3
     d wwffir          s              3  0
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Kommentarer til program.                                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Programmet vedlikeholder file GUSTPF, felles sekvensteller.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      *
      * A1BLD  - A1 Registrering av foretaksnummer
      * A2BLD  - A2 Vedlikehold av teller.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Test på parameterinformasjon.                                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     a1tag         tag
      *
      * Hent opp informasjon
      *
     c     key01         chain     gpcfpf                             60
      *
      * Legger over verdier til skjermbildet:
      *
     c                   movel     wpffgr        a1ffgr
      *
     c                   if        *in60 = *on
     c                   eval      a1frem = *blanks
     c                   eval      a1fmot = *blanks
     c                   eval      a1favr = *blanks
     c                   eval      a1focr = *blanks
     c                   eval      a1fkto = *blanks
     c                   eval      a1fink = *blanks
14113c                   eval      a1ftff = *blanks
     c                   else
     c                   unlock    gpcfpf
     c                   movel     gdfrem        a1frem
     c                   movel     gdfmot        a1fmot
     c                   movel     gdfavr        a1favr
     c                   movel     gdfocr        a1focr
     c                   movel     gdfkto        a1fkto
     c                   movel     gdfink        a1fink
14113c                   movel     gdftff        a1ftff
     c                   endif
      *
     c     b1tag         tag
      *
     c                   exfmt     a1win
      *
      * Avslutt program ved F3:
      *
     c     *inkc         cabeq     *on           xslutt
      *
      * Dersom kommunikasjonstype er "PC", så kan innholdet i feltene
      * ikke overstige 8 tegn!
      *
09065c                   if        wpkomm = 'PC'
09065c                   move      *off          *in30
      *  Remitteringsfile:
09065c                   movel     a1frem        w_navn
09065c                   if        w_pos9 <> *blanks
09065c                   move      *on           *in30
09065c                   endif
      *  Mottak
09065c                   movel     a1fmot        w_navn
09065c                   if        w_pos9 <> *blanks
09065c                   move      *on           *in30
09065c                   endif
      *  Avregning
09065c                   movel     a1favr        w_navn
09065c                   if        w_pos9 <> *blanks
09065c                   move      *on           *in30
09065c                   endif
      *  OCR
09065c                   movel     a1focr        w_navn
09065c                   if        w_pos9 <> *blanks
09065c                   move      *on           *in30
09065c                   endif
      *  Kontoinformasjon
09065c                   movel     a1fkto        w_navn
09065c                   if        w_pos9 <> *blanks
09065c                   move      *on           *in30
09065c                   endif
      *  Inkassoreturer
09065c                   movel     a1fink        w_navn
09065c                   if        w_pos9 <> *blanks
09065c                   move      *on           *in30
09065c                   endif
      *  Feilreturer
09065c                   movel     a1ftff        w_navn
09065c                   if        w_pos9 <> *blanks
09065c                   move      *on           *in30
09065c                   endif
      *
09065c                   if        *IN30 = *on
09065c                   goto      b1tag
09065c                   endif
      *
09065c                   endif
      *
      * Lagrer nye verdier.
      *
     c     key01         chain     gpcfpf                             60
      *
     c                   movel     wpffgr        gdffgr
     c                   movel     wwffir        gdffir
     c                   movel     a1frem        gdfrem
     c                   movel     a1fmot        gdfmot
     c                   movel     a1favr        gdfavr
     c                   movel     a1focr        gdfocr
     c                   movel     a1fkto        gdfkto
     c                   movel     a1fink        gdfink
14113c                   movel     a1ftff        gdftff
      *
     c     a1fmot        cabeq     *blanks       xslutt
      *
     c                   if        *in60 = *on
     c                   write     gpcfpfr
     c                   else
     c                   update    gpcfpfr
     c                   endif
      *
      /space 3
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    wpffgr
     c                   parm                    wpffir
09065c                   parm                    wpkomm
      *
     c                   movel     wpffir        wwffir
     c                   eval      w_arow = 2
     c                   eval      w_acol = 15
      *
     c     *dtaara       define    *lda          lda
      *
      *
      * Key for RPCFPF:
      *
     c     key01         klist
     c                   kfld                    wpffgr
     c                   kfld                    wwffir
      *
     c                   endsr
