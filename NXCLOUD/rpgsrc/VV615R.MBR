     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VV615R
      * Beskrivelse . . : Vare, utskrift dekningskontroll - parameter
      *                   XLS
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       260118 bof  Nytt program
7.xx  *       040418 bkv  NX cloud
6.31  *       241018 bof  Omskriving - liste lagres på IFS
6.32  *       101018 bof  Skal ikke vise bildet mens det kjører
6.33  *       270418 BOF  Utvidet med lager
6.34  *       041019 BOF  Hent lager fra aktiv bruker.
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fra06l1    if   e           k disk    rename(ra06pfr:ra06l1r)
6.33 fra10l1    if   e           k disk    rename(ra10pfr:ra10l1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
      *
     fvv615d    cf   e             workstn
      *
      * Local data area
      *
     d                uds
     d l_firm                944    946  0
6.34 d l_lage               1001   1002  0
      *
      * Datastruktur for parameterliste -ut
      *
     d                 ds
6.33 d d_list                        44
     d  d_firm                        3  0 overlay(d_list:1)
     d  d_ogrp                        2  0 overlay(d_list:4)
     d  d_hgrp                        2  0 overlay(d_list:6)
     d  d_ugrp                        3  0 overlay(d_list:8)
     d  d_rkaf                        4  0 overlay(d_list:11)
     d  d_rkat                        4  0 overlay(d_list:15)
     d  d_kund                        6  0 overlay(d_list:19)
     d  d_kpro                        6  0 overlay(d_list:25)
     d  d_dato                         d   overlay(d_list:31) datfmt(*iso)
6.33 d  d_lage                        2  0 overlay(d_list:43)
      *
      * Definering av variabler i nøkler
      *
6.33 d ra10l1_jkod     s                   like(rajkod)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d ra06l1_fkod     s                   like(rafkod)
     d rkunl1_kund     s                   like(rkkund)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
     d w_udat          s               d   datfmt(*iso)
     d time_a          s              8a
     d time_hms        s              8a   inz('16:23:01')
      *
     d w_firm          s                   like(vgofir)
     d w_knav          s                   like(rknavn)
     d w_pnav          s                   like(flnavn)
     d w_rtxt          s                   like(raftxt)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send bilde
      *
6.34 c                   eval      b1lage = l_lage
     c     b1taga        tag
      *
     c                   exfmt     b1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc = *on or *inkl = *on
     c                   goto      avslutt
     c                   when      *inkd = *on
     c                   exsr      spØrring
     c                   goto      b1taga
     c                   endsl
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_inp
     c                   if        b_feil = *on
     c                   goto      b1taga
     c                   endif
      *
      * Flytter skjermfelter til datastruktur -ut
      *
     c                   eval      d_firm = w_firm
     c                   eval      d_ogrp = b1ogrp
     c                   eval      d_hgrp = b1hgrp
     c                   eval      d_ugrp = b1ugrp
     c                   eval      d_rkaf = b1rkaf
     c                   eval      d_rkat = b1rkat
     c                   eval      d_kund = b1kund
     c                   eval      d_kpro = b1kpro
6.33 c                   eval      d_lage = b1lage
      *
     c     *dmy          move      b1dato        d_dato
     c                   if        d_rkat = 0
     c                   eval      d_rkat = 999
     c                   endif
      *
      * Kaller overføringsprogram
      *
6.31 c                   call      'VV615C'
     c                   parm                    d_list
      *
      /free
       time_a = %char(%time():*HMS);
      /end-free
     c                   eval      b1bmsg = 'Jobb startet:' + time_a
      *
6.32 c**                 goto      b1taga
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   move      *on           *inlr
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørringer (fd)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Overgruppe
      *
     c                   if        w_afld = 'B1OGRP'
     c                   eval      b1hgrp = 0
     c                   eval      b1ugrp = 0
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b1ogrp
     c                   parm                    b1utxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        w_afld = 'B1HGRP'
     c                   eval      b1ugrp = 0
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    b1ogrp
     c                   parm                    b1hgrp
     c                   parm                    b1utxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Undergruppe
      *
     c                   if        w_afld = 'B1UGRP'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    b1ogrp
     c                   parm                    b1hgrp
     c                   parm                    b1ugrp
     c                   parm                    b1utxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Rab kat. fra
      *
     c                   if        w_afld = 'B1RKAF'
     c                   call      'RA506R'
     c                   parm                    w_firm
     c                   parm                    b1rkaf
     c                   parm                    w_rtxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Rab kat. til
      *
     c                   if        w_afld = 'B1RKAT'
     c                   call      'RA506R'
     c                   parm                    w_firm
     c                   parm                    b1rkat
     c                   parm                    w_rtxt
     c                   goto      end_spØrr
     c                   endif
      *
      * kunde
      *
     c                   if        w_afld = 'B1KUND'
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    b1kund
     c                   parm                    w_knav
     c                   goto      end_spØrr
     c                   endif
      *
      * kunde  prosjekt
      *
     c                   if        w_afld = 'B1KPRO'
     c                   call      'FL500R'
     c                   parm                    w_firm
     c                   parm                    b1kund
     c                   parm                    b1kpro
     c                   parm                    w_knav
     c                   parm                    w_pnav
     c                   goto      end_spØrr
     c                   endif

      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_inp     begsr
      *
     c                   eval      b_feil = *off
     c                   eval      b1utxt = *blank
6.33  *
6.33  * Lager
6.33  *
6.33 c                   if        b1lage <> 0
6.33 c                   eval      ra10l1_jkod = b1lage
6.33 c     ra10l1_key    chain     ra10l1                             90
6.33 c                   if        not %found(ra10l1)
6.33 c                   eval      *in39  = *on
6.33 c                   eval      b_feil = *on
6.33 c                   goto      end_sjekk
6.33 c                   endif
6.33 c                   endif
      *
      * Overgruppe
      *
     c                   if        b1ogrp <> 0
     c                   eval      vogrl1_oogr = b1ogrp
     c     vogrl1_key    chain     vogrl1                             90
     c                   if        *in90  = *on
     c                   eval      *in31  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      b1utxt = vgotxt
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        b1hgrp <> 0
     c                   eval      vhgrl1_hogr = b1ogrp
     c                   eval      vhgrl1_hhgr = b1hgrp
     c     vhgrl1_key    chain     vhgrl1                             90
     c                   if        *in90  = *on
     c                   eval      *in32  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      b1utxt = vghtxt
     c                   endif
      *
      * Undergruppe
      *
     c                   if        b1ugrp <> 0
     c                   eval      vugrl1_uogr = b1ogrp
     c                   eval      vugrl1_uhgr = b1hgrp
     c                   eval      vugrl1_uugr = b1ugrp
     c     vugrl1_key    chain     vugrl1                             90
     c                   if        *in90  = *on
     c                   eval      *in33  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      b1utxt = vgutxt
     c                   endif
      *
      * rabkat. fra
      *
     c                   if        b1rkaf <> 0
     c                   eval      ra06l1_fkod = b1rkaf
     c     ra06l1_key    chain     ra06l1                             90
     c                   if        *in90  = *on
     c                   eval      *in35  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
      * rabkat. til
      *
     c                   if        b1rkat <> 0
     c                   eval      ra06l1_fkod = b1rkat
     c     ra06l1_key    chain     ra06l1                             90
     c                   if        *in90  = *on
     c                   eval      *in36  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
      * kunde
      *
     c                   if        b1kund <> 0
     c                   eval      rkunl1_kund = b1kund
     c     rkunl1_key    chain     rkunl1                             90
     c                   if        *in90  = *on
     c                   eval      *in37  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
      * kunde kundeprosjekt
      *
     c                   if        b1kpro <> 0
     c                   eval      fkprl1_kund = b1kund
     c                   eval      fkprl1_kpro = b1kpro
     c     fkprl1_key    chain     fkprl1                             90
     c                   if        *in90  = *on
     c                   eval      *in38  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
      * Dato
      *
     c                   if        b1dato = 0
     c     *dmy          move      w_udat        b1dato
     c                   endif
      *
     c     *dmy          test(d)                 b1dato                 34
     c                   if        *in34
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag på rab.kat
      *
     c     ra06l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra06l1_fkod
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på kunde    prosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
6.33  *
6.33  * Key for oppslag på prisgruppe
6.33  *
6.33 c     ra10l1_key    klist
6.33 c                   kfld                    w_firm
6.33 c                   kfld                    ra10l1_jkod
      *
      *
      * Henter firmanummer fra LDA
      *
     c                   eval      w_firm = l_firm
      *
      * Initierer skjerfelter
      *
     c                   time                    w_udat
     c     *dmy          move      w_udat        b1dato
      *
     c                   eval      b1bmsg = 'Jobb ikke startet'
      *
     c                   endsr
