     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LI725R
      * Beskrivelse . . : Sjekker status på automatisk oppdatering av EDI - flagger avvik
      *
      * Spesialversjon. : Mestergruppen
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       091116 bsa  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fgedgl1    if   e           k disk    rename(gedgpfr:gedgl1r)
      *
      * Definering av Local Data Arae (LDA)
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Parameter
      *
     d p_stat          s              1
     d p_mot1          s             50
     d p_mot2          s             50
     d p_mot3          s             50
     d p_send          s             50
     d p_subj          s            100
     d p_txt1          s            100
     d p_txt2          s            100
     d p_txt3          s            100
     d p_txt4          s            100
     d p_avsl          s            100
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variable i parametre
      *
      *
      * Definering av variable
      *
      *
     d w_firm          s                   like(ggfirm)
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_year          s              4  0
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(10)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandle åpningsopplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Er automatisk oppdatering kjøret i dag.
      *
     c     gedgl1_key    chain     gedgl1
     c                   if        not %found or
     c                             ggstat = 0
     c                   goto      avslutt
     c                   endif
      *
      *  Sjekk at innlesing er kjørt i dag, og at den er avsluttet korrekt
      *  Hvis ikke må vi varsle.
      *
     c                   if        w_date > ggsdat
     c                   exsr      sub_varsle
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for aktivere mailsending
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sub_varsle    begsr
      *
      * Sjekk om det finnes minst en email adresse
      *
     c                   if        %trim(ggmai1) = *blanks  and
     c                             %trim(ggmai2) = *blanks  and
     c                             %trim(ggmai3) = *blanks
     c                   goto      end_varsle
     c                   endif
      *
      * Laster inn info om mail som skal varsle
      *
     c                   eval      p_mot1 = ggmai1
     c                   eval      p_mot2 = ggmai2
     c                   eval      p_mot3 = ggmai3
     c                   eval      p_send = 'bsaethe@online.no'
     c                   eval      p_subj = 'Feil med automatisk overføring av +
     c                                      EDI meldinger hos Mestergruppen' +
     c                                      %char(w_date)
     c                   eval      p_txt1 = ggtxt1 + ggtxt2
     c                   movel     ggtxt3        p_txt3
     c                   eval      p_txt4 = *blanks
     c                   eval      p_avsl = 'Sendt av automatisk kontrollrutine'
     c                   call      'AF726R'
     c                   parm                    p_mot1
     c                   parm                    p_mot2
     c                   parm                    p_mot3
     c                   parm                    p_send
     c                   parm                    p_subj
     c                   parm                    p_txt1
     c                   parm                    p_txt2
     c                   parm                    p_txt3
     c                   parm                    p_txt4
     c                   parm                    p_avsl
      *
     c     end_varsle    endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c**   *entry        plist
     c**                 parm                    p_stat
      *
      * Key for file : Automatisdk oppdatering register
      *
     c     gedgl1_key    klist
     c                   kfld                    w_firm
      *
      * Henter verdier fra parmametre
      *
     c                   time                    w_date
     c                   time                    w_time
     c                   eval      w_year = %subdt(w_date:*years)
     c                   eval      w_firm = l_firm
      *
     c                   endsr
