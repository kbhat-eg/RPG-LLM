     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System  . . . . : ASLAGR
      *  Program . . . . : LQ605R
      *  Beskrivelse . . : Utskrift av Pakkseddel-Lageroverføring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      * --------- ------  -------------------------------------------------
6.nu  *  6.30     040614  Utvidelser vedr. nummerutvidelse         bof
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flstsl1    if   e           k disk     rename(lstspfr:lstsl1r)
     fllhel1    if   e           k disk     rename(llhepfr:llhel1r)
     flldtl1    if   e           k disk     rename(lldtpfr:lldtl1r)
     flq605p    o    e             printer
      *
      * Parameter-inn
      *
     d                 ds
     d d_prec                        64
6.nu d  d_numm                        8  0 overlay(d_prec)
6.nu d  d_kode                        1    overlay(d_prec:9)
6.nu d  d_peri                        6  0 overlay(d_prec:10)
     d  d_dato                       10d   datfmt(*iso)
6.nu d                                     overlay(d_prec:16)
6.nu d  d_dumm                        1    overlay(d_prec:26)
      *
      *  Local Data Area
      *
     d                uds
     d l_firm                944    946  0
     d l_navn                951    980
      *
      * Definering av variable i nøkler
      *
     d lldtl1_numm     s                   like(lsnumm)
     d llehl1_numm     s                   like(lqnumm)
      *
      * Definering av variable
      *
     d w_stat          s              1    inz(*off)
     d w_firm          s                   like(laafir)
     d w_gtxt          s             20
     d w_avde          s              4  0
     d w_parm          s             64
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Definering av formater
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Benyttede formater
      *  - - - - - - - - - -
      *
      *  A1HDR  - Heading A1 på rapport
      *  D1DET  - Detail  D1 på rapport
      *  T1TOT  - Total   T1 på rapport
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  A1HDR Behandle heading A1
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Hent opplysninger fra LAGERBUNT-HODE
      *
     c     llhel1_key    chain     llhel1
      *
      *  Snur dato
      *
     c                   eval      a1bdat = 0
     c                   eval      a1ldat = 0
      *
B001 c                   if        d_dato <> *loval
 001 c     *dmy          move      d_dato        a1bdat
E001 c                   endif
      *
B001 c                   if        lqdate <> *loval
 001 c     *dmy          move      lqdate        a1ldat
E001 c                   endif
      *
      *  Hent,   Tekst for avdeling
      *
     c                   eval      w_avde = lqlage
     c                   exsr      sbavde
     c                   eval      a1fad1 = w_gtxt
      *
     c                   eval      a1avde = lqlast
     c                   eval      w_avde = lqlast
     c                   exsr      sbavde
     c                   eval      a1navn = w_gtxt
      *
      *  Legger inn opplysninger i utfeltene
      *
     c                   eval      a1numm = lqnumm
      *
      * Skrive heading
      *
     c                   write     a1hdr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  D1DET Behandle file
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Lese file
      *
     c     lldtl1_key    setll     lldtl1
     c     lldtl1_key    reade     lldtl1
      *
B001 c                   dow       not %eof
 001  *
 001  *  Legger inn opplysninger i utfeltene
 001  *
 001 c                   eval      d1vare = lsvare
 001 c                   eval      d1tek1 = lstek1
 001 c                   eval      d1anta = lsanta
 001 c                   eval      d1enh1 = lsenh1
 001  *
 001  *  Sjekker om dette er en tekst-linje
 001  *
 001 c                   eval      *in51 = *off
B002 c                   if        lstype = 'TX'
 001 c                   eval      *in51 = *on
E002 c                   endif
 001  *
 001  *  Behandle record
 001  *
 001 c                   write     d1det                                30
 001  *
 001  *  Er det overflow ?
 001  *
B002 c                   if        *in30 = *on
 002 c                   write     a1hdr
E002 c                   endif
 001  *
     c     lldtl1_key    reade     lldtl1
E001 c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  T1TOT Behandle total T1
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   write     t1tot                                30
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sjekk/Hent tekst for avdeling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     sbavde        begsr
      *
     c                   call      'RS707R'
     c                   parm                    w_stat
     c                   parm                    w_avde
     c                   parm                    w_gtxt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Tar inn parameter fra forrige program
      *
     c     *entry        plist
     c                   parm                    w_parm
      *
     c                   eval      d_prec = w_parm
     c                   eval      a1fnav = l_navn
      *
      *  Key for status
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      *  Key for LAGERBUNT-HODE
      *
     c     llhel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    llehl1_numm
      *
      *  Key for LAGERBUNT-LINJE
      *
     c     lldtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lldtl1_numm
      *
      *  Legge inn verdier i key
      *
     c                   eval      w_firm = l_firm
     c                   eval      llehl1_numm = d_numm
     c                   eval      lldtl1_numm = d_numm
      *
      *  Hent opplysninger fra LAGER-STATUS-KODER
      *
     c     lstsl1_key    chain     lstsl1r
      *
     c                   endsr
