      /title  ASSHOP Bytting av kundenummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASSHOP
      * Program . . . . : BO790R
      * Beskrivelse . . : Bytting av kundenummer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       030500 HKO  Nytt program
      * 6.10  110909 BSA  Oppdatert med sporingsinformasjon
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fbohele    uf   e           k disk    rename(bohepfr:boheler)
     fbohelf    uf   e           k disk    rename(bohepfr:bohelfr)
     fbstslu    uf   e           k disk    rename(bstspfr:bstslur)
      *
      * Definering av parametere
      *
     d p_firm          s                   like(bofirm)
     d p_gkun          s                   like(bokund)
     d p_kund          s                   like(bokund)
      *
      * Definering av Local data area
      *
6.10 d                uds
6.10 d l_user                911    920
      *
      * Definering av variable i nøkler
      *
     d bohele_kund     s                   like(bokund)
     d bohelf_kund     s                   like(bokund)
      *
      * Definering av variable
      *
     d w_firm          s                   like(bofirm)
     d w_gkun          s                   like(bokund)
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter firma og gammelt kundenummer fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_gkun = p_gkun
      *
      * Bong-hode - Fakturakunde
      *
     c                   eval      bohele_kund = p_gkun
     c     bohele_key    setll     bohele
     c     bohele_key    reade     bohele                                 90
     c                   dow       *in90 = *off
     c                   eval      bokund = p_kund
6.10 c                   time                    boedat
6.10 c                   time                    boetim
6.10 c                   eval      boeusr = l_user
     c                   update    boheler
     c     bohele_key    reade     bohele                                 90
     c                   enddo
      *
      * Bong-hode - Varekunde
      *
     c                   eval      bohelf_kund = p_gkun
     c     bohelf_key    setll     bohelf
     c     bohelf_key    reade     bohelf                                 90
     c                   dow       *in90 = *off
     c                   eval      bolkun = p_kund
6.10 c                   time                    boedat
6.10 c                   time                    boetim
6.10 c                   eval      boeusr = l_user
     c                   update    bohelfr
     c     bohelf_key    reade     bohelf                                 90
     c                   enddo
      *
      * Status
      *
     c     key01         chain     bstslur                            90
     c                   if        *in90 = *off
     c                   if        baakun = p_gkun
     c                   eval      baakun = p_kund
     c                   endif
6.10 c                   time                    baaeda
6.10 c                   time                    baaeti
6.10 c                   eval      baaeus = l_user
     c                   update    bstslur
     c                   endif
      *
      * Avslutt program
      *
     c***                eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine bor initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_gkun
     c                   parm                    p_kund
      *
      * Key for les på firma
      *
     c     key01         klist
     c                   kfld                    w_firm
      *
      * Key for bong-hode - Fakturakunde
      *
     c     bohele_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bohele_kund
      *
      * Key for bong-hode - Varekunde
      *
     c     bohelf_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bohelf_kund
      *
     c                   endsr
