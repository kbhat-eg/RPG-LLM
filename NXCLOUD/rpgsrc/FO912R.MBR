     H/TITLE  ASOFAK: Utskrift av Tilbud
     h decedit('0,') datedit(*dmy.) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : FO912R
      * Beskrivelse . . : Utskrift av Tilbud
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
3.12  * R3.12 310800 AMD  Lagt inn mulighet for utprint av avdelingsnavn
3.12  *                   i stedet for firmanavn dersom ønskelig
3.13  * R3.13 110900 AMD  Dersom vare er hentet fra LVR settes det inn
3.13  *                   2 desimaler som standard (finnes jo ikke i EVR)
3.33  * R3.33 310501 AMD  Lagt inn utskrift av telefon/fax/mobil.
5.01  * R5.01 221002 AMD  Løsning for å kunne sende e-mail
5.02  * R5.02 260903 AMD  Automatisk opplegg for subject (emne) ved
5.02  *                   sending av mail.
5.21  * R5.21 261103 AMD  Overstyrer med mobilnummer fra ordrehode
5.21  *                   dersom oppgitt.
5.22  * R5.22 031203 AMD  Skrive tekst 2 dersom den finnes i varereg.
5.22  *                   + lagt inn utskrift av leveringstype
5.22  *                   + lagt inn skråstrek mellom telefon / telefax
5.41  * R5.41 150605 AMD  Hente styring på firmaopplysninger fra LDA.
5.61  * R5.61 200608 AMD  Tar hensyn til epost-adresse på kundeprosjekt
6.10  * R6.10 070909 BSA  Utskriften endret pga utvidelse av referansefelter.
6.20  * R6.20 250511 NHO  Mobil nr skal legges ut dersom 1 i felt
      *                   FAAMSA i fil FSTSPF
6.nu  * R6.30 030614 bsa  Programmet gjennomgått mtp nummerutvidelser.
6.30  * 6.30  220514 BKV  Utvidet parameterliste til FÅ707R
8.01  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     FFPSOL1    IPE  E             DISK    RENAME(FPSOPFR:FPSOL1R)
     FFSTSL1    IF   E           K DISK    RENAME(FSTSPFR:FSTSL1R)
     FVOTYL1    IF   E           K DISK    RENAME(VOTYPFR:VOTYL1R)
     FVLTYL1    IF   E           K DISK    RENAME(VLTYPFR:VLTYL1R)
     FVPKOL1    IF   E           K DISK    RENAME(VPKOPFR:VPKOL1R)
     FFOHEL1    IF   E           K DISK    RENAME(FOHEPFR:FOHEL1R)
     FFODTL1    IF   E           K DISK    RENAME(FODTPFR:FODTL1R)
     FFOTXL1    IF   E           K DISK    RENAME(FOTXPFR:FOTXL1R)
     FVVARL1    IF   E           K DISK    RENAME(VVARPFR:VVARL1R)
     Frkunl1    IF   E           K DISK    RENAME(RKUNPFR:rkunl1r)
     FAFORL1    IF   E           K DISK    RENAME(AFORPFR:AFORL1R)
6.20 fra09l1    if   e           k disk    rename(ra09pfr:ra09l1r)
     FFO912P    O    E             PRINTER
      *
      * - - - - - - - - - - - - - -
      * Henter inn data fra LDA
      * - - - - - - - - - - - - - -
      *
     D                UDS
5.41 D  l_prfi               750    750  0
     D  l_ruti               800    809
5.01 D  l_outq               810    819
     D  l_alin               856    858  0
     D  l_firm               944    946  0
5.01  *
5.01  * Definering av parametre
5.01  *
5.01 d p_mail          s              1
5.01 d p_kund          s              6  0
5.61 d p_kpro          s              6  0
6.nu d p_numm          s              8  0
5.02 d p_selg          s              4  0
      *
      * Definering av variable
6.20 d ra09l1_ikod     s                   like(raikod)
      *
6.20 d w_selgn         s             16
6.20 d                 ds
6.20 d x_mobi                        12
6.20 d  d_tegn                        1    overlay(x_mobi:1)
6.20 d  d_mobn                       11    overlay(x_mobi:2)
      *
3.12 d w_prfi          s              1  0
3.12 d w_ffir          s              3  0
3.12 d w_avde          s              4  0
3.12 d w_fnav          s             30
3.12 d w_fad1          s             30
3.12 d w_fad2          s             30
3.12 d w_fste          s             30
3.12 d w_fftx          s             30
3.12 d w_tfax          s              8
3.12 d w_ftlf          s             11
3.12 d w_ffax          s             11
3.33 d w_tlfn          s             20
3.33 d w_tlfx          s             20
3.33 d w_mobn          s             20
6.30 d w_mail          s             50
6.30 d w_weba          s             50
     d wpcbda          s               d   datfmt(*iso)
     d wpcfda          s               d   datfmt(*iso)
      *
      *
     IFPSOL1R       01
     I                                          FKSSRT        L4
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av formater                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Benyttede formater
      * - - - - - - - - - -
      *
      * A1HDR  - Heading A1 på rapport
      * A2HDR  - Heading A2 på rapport
      * A4HDR  - Heading A4 på rapport, Ordrehode over varelinje
      * D1DET  - Detail  D1 på rapport, Varelinje
      * E1DET  - Detail  E1 på rapport, Tekstlinje
      * O1DET  - Detail  O1 på rapport, Overføring til/fra neste side
      * T1TOT  - Total   T1 på rapport
      *
      *
      * Forklaring på variabler
      * - - - - - - - - - - - -
      *
      * D1ANT0 - Antall med 0 desimaler
      * D1ANT1 - Antall med 1 desimaler
      * D1ANT2 - Antall med 2 desimaler
      * D1ANT3 - Antall med 3 desimaler
      * D1NPRI - Netto  pris
      * D1BPRI - Brutto pris
      * D1NBEL - Netto  beløp
      * D1BBEL - Brutto beløp
      * D1RABP - Rabatt i prosent (Sum prosent 1 og 2)
      * D1RABB - Rabatt i beløp
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Styre-rutine                                                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Initierings-rutine
      *
     C   01FCYCLE        CASEQ     *ZERO         XSTRSR
     C                   ENDCS
      *
      * Ordrehode
      *
     C     FKSKOD        CABEQ     'H'           TAG01
      *
      * Varelinje
      *
     C     FKSKOD        CABEQ     'V'           TAG02
      *
      * Kommentar/
      * tekstlinje
      *
     C     FKSKOD        CABEQ     'K'           TAG03
      *
      * Ferdig
      *
     C                   GOTO      XSLUTT
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * A1HDR Behandle heading A1 + A2                                  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     TAG01         TAG
      *
      * Null-stilling av variabler
      *
     C**> (BARE PRISER)    Z-ADD0         O1TRSP
      *
      * Hent ordre-heading
      *
     C                   MOVE      FKSONR        WWNUMM
     C                   MOVE      FKSSUF        WWSUFF
     C     KEY01         CHAIN     FOHEL1R                            61
      *
      * Snu dato
      *
     c                   if        fobdat <> *loval
     c     *dmy          move      fobdat        a1bdat
     c                   endif
      *
     c                   if        fotdat <> *loval
     c     *dmy          move      fotdat        a1tdat
     c                   endif
      *
      * Legg inn ordrenummer
      *
     C                   MOVE      FONUMM        A1NUMM
     C                   MOVEL     '/'           A1SUFF
     C                   MOVE      FOSUFF        A1SUFF
      *
      * Flere ordre på samme
      * blankett ?
      *
     C  NL4              GOTO      TAG01A
      *
      * Hente inn kunderegister
      *
     C                   MOVE      FOKUND        WWKUND
     C     KEY04         CHAIN     rkunl1r                            64
     C                   MOVE      FOKUND        A1KUND
     C                   MOVE      RKKATG        A1KATG
     C                   MOVE      RKNAVN        A1NAVN
     C                   MOVE      RKGATE        A1GATE
     C                   MOVE      RKADR2        A1ADR2
     C                   MOVE      RKSTED        A1STED
      *
      * Sjekke tilbuds-kunden
      *
     C     FOKUND        IFEQ      W0KUND
     C                   MOVE      FOLKUN        FOKUND
     C                   MOVE      FONAVN        A1NAVN
     C                   MOVE      FOADR1        A1GATE
     C                   MOVE      FOADR2        A1ADR2
     C                   MOVE      FOSTED        A1STED
     C                   MOVE      *ZERO         FOLKUN
     C                   MOVE      *BLANK        FONAVN
     C                   MOVE      *BLANK        FOADR1
     C                   MOVE      *BLANK        FOADR2
     C                   MOVE      *BLANK        FOSTED
     C                   END
      *
      * Finner hvilken mva-sats
      * som skal benyttes
      *
     c                   call      'FÅ705R'
     c                   parm                    wwfirm
     c                   parm                    fobdat
     c                   parm                    wpmvas
     c                   parm                    wpmvat
     c                   parm                    wpmvaf
      *
      * Hente inn betalingsbetingelser
      *
     C                   exsr      sbbetb
      *
      * Er det registrert
      * leveringsadresse?
      *
     C                   MOVE      *OFF          *IN31
     C     FONAVN        IFNE      *BLANK
     C                   MOVE      *ON           *IN31
     C                   MOVE      FONAVN        A2NAVN
     C                   MOVE      FOADR1        A2ADR1
     C                   MOVE      FOADR2        A2ADR2
     C                   MOVE      FOSTED        A2STED
     C                   ENDIF
      *
      * Er det registrert
      * leveringsdato?
      *
     C                   MOVE      *OFF          *IN32
     C     FOLDAT        IFNE      *loval
     C                   MOVE      *ON           *IN32
     C                   ENDIF
      *
      * Er det registrert
      * kundens referanse?
      *
     C                   MOVE      *OFF          *IN33
     C     FODREF        IFNE      *BLANK
     C                   MOVE      *ON           *IN33
     C                   MOVE      FODREF        A1DREF
     C                   ENDIF
      *
      * Er det registrert
      * leveringsmåte?
      *
     C                   MOVE      *OFF          *IN34
     C     FOLMTX        IFNE      *BLANK
     C                   MOVE      *ON           *IN34
     C                   MOVE      FOLMTX        A1LMTX
     C                   ENDIF
      *
      * Er det registrert
      * rekvisisjon?
      *
     C                   MOVE      *OFF          *IN35
     C     FOREKV        IFNE      *BLANK
     C                   MOVE      *ON           *IN35
     C                   MOVE      FOREKV        A1REKV
     C                   ENDIF
3.33  *
3.33  * Hente inn telefon/fax/mobil fra kunde
3.33  *
3.33 c                   call      'FÅ710R'
3.33 c                   parm                    wwfirm
3.33 c                   parm                    a1kund
3.33 c                   parm                    fokpro
3.33 c                   parm                    w_tlfn
3.33 c                   parm                    w_tlfx
3.33 c                   parm                    w_mobn
3.33 c                   eval      a1tlfn = w_tlfn
3.33 c                   eval      a1tlfx = w_tlfx
3.33 c                   eval      a1mobn = w_mobn
5.21  *
5.21  * Overstyre med mobilnummer fra ordrehode
5.21  * dersom oppgitt
5.21  *
5.21 c                   if        fomobi <> *blank
5.21 c                   eval      a1mobn = 'Mob: ' + fomobi
5.21 c                   endif
      *
      * Flytte øvrige felter
      *
     C                   MOVE      FOREKV        A1REKV
     C     *hms          MOVE      FOTIME        A1TIME
     C                   MOVE      FOSELG        A1SELG
     C                   MOVE      FOVREF        A1VREF
6.20  *
6.20 C                   eval      x_mobi = *blank
6.20 C                   eval      x_mobn = *blank
6.20  *
6.20 C                   if        faamsa = 1
6.20 C                   eval      ra09l1_ikod = foselg
6.20 C     ra09l1_key    chain     ra09l1
6.20 C                   if        raimob <> *blank
6.20 C                   move      '/'           d_tegn
6.20 C                   move      raimob        d_mobn
6.20 C                   move      x_mobi        x_mobn
6.20 C                   endif
6.20 C                   endif
6.20  *
5.01  *
5.01  * Legg ut kundenummer i parameter
5.01  *
5.01 c                   if        l_outq = 'E_MAIL_ASP'
5.01 c                   eval      p_kund = fokund
5.61 c                   eval      p_kpro = fokpro
5.02 c                   eval      p_numm = fonumm
5.02 c                   eval      p_selg = foselg
5.01 c                   endif
3.12  *
3.12  *  Hent inn firma/avd. opplysninger dersom
3.12  *  kode for det er satt i rutineregister
3.12  *
5.41 c                   if        l_prfi = 1 or
5.41 c                             l_prfi = 2
5.41 c                   eval      w_prfi = l_prfi
3.12 c                   eval      w_ffir = wwfirm
3.12 c                   eval      w_avde = foavde
3.12  *
3.12 c                   call      'FÅ707R'
3.12 c                   parm                    w_prfi
3.12 c                   parm                    w_ffir
3.12 c                   parm                    w_avde
3.12 c                   parm                    w_fnav
3.12 c                   parm                    w_fad1
3.12 c                   parm                    w_fad2
3.12 c                   parm                    w_fste
3.12 c                   parm                    w_fftx
3.12 c                   parm                    w_tfax
3.12 c                   parm                    w_ftlf
3.12 c                   parm                    w_ffax
6.30 c                   parm                    w_mail
6.30 c                   parm                    w_weba
3.12  *
3.12 c                   eval      a1fnav = w_fnav
3.12 c                   eval      a1fad1 = w_fad1
3.12 c                   eval      a1fad2 = w_fad2
3.12 c                   eval      a1fste = w_fste
3.12 c                   eval      a1fftx = w_fftx
3.12 c                   eval      a1tfax = w_tfax
3.12 c                   eval      a1ftlf = w_ftlf
3.12 c                   eval      a1ffax = w_ffax
3.12 c                   endif
5.22  *
5.22  * Legger inn '/' mellom
5.22  * telefon og telefax
5.22  *
5.22 c                   eval      a1skra =  ' '
5.22 c                   if        a1ftlf <> *blank and
5.22 c                             a1ffax <> *blank
5.22 c                   eval      a1skra =  '/'
5.22 c                   endif
      *
      * Skrive heading
      *
     C                   Z-ADD     0             WLTELL
     C                   WRITE     A1HDR
     C                   WRITE     A2HDR
      *
      * Skriv heading over
      * varelinjer
      *
     C     VAOSAM        IFEQ      1
     C                   ADD       2             WLTELL
     C                   WRITE     A4HDR                                30
     C                   ENDIF
      *
     C                   GOTO      XSLUTT
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Heading U/L4-brudd                                              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     TAG01A        TAG
      *
      * Skriv heading over
      * varelinjer
      *
     C     VAOSAM        IFEQ      1
     C                   ADD       2             WLTELL
     C                   WRITE     A4HDR                                30
     C                   ENDIF
      *
     C                   GOTO      XSLUTT
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * D1DET Behandle detail D1                                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     TAG02         TAG
      *
      * Nullstilling av variabler
      *
      *
     C                   MOVE      *BLANK        D1VARE
     C                   MOVE      *BLANK        D1TEK1
     C                   MOVE      *BLANK        D1TEK2
     C                   MOVE      *BLANK        D1ENH1
     C                   MOVE      *BLANK        D1TINE
5.22 c                   move      *zero         d1lety
     C                   MOVE      *ZERO         D1ANT0
     C                   MOVE      *ZERO         D1ANT1
     C                   MOVE      *ZERO         D1ANT2
     C                   MOVE      *ZERO         D1ANT3
     C                   MOVE      *OFF          *IN44
     C                   MOVE      *OFF          *IN46
      *
     C                   MOVE      FKSONR        WWNUMM
     C                   MOVE      FKSSUF        WWSUFF
     C                   MOVE      FKSLIN        WWLINE
     C     KEY02         CHAIN     FODTL1R                            62
      *
      * Hent linje-type
      *
     C                   MOVE      FDLTYP        WWLTYP
     C     KEY08         CHAIN     VLTYL1R                            68
      *
      * Sjekker om linjen ikke skrives på ordretypen
      *
     C     FOOTYP        CABEQ     VALO01        XSLUTT
      *
     C     FOOTYP        CABEQ     VALO02        XSLUTT
      *
     C     FOOTYP        CABEQ     VALO03        XSLUTT
      *
     C     FOOTYP        CABEQ     VALO04        XSLUTT
      *
      * Sjekker om tekstlinjer
      *
     C     VALKTX        CABEQ     1             TAG02A
      *
      * Oppslag mot vareregister
      *
     C                   MOVE      FDVARE        WWVARE
     C     KEY11         CHAIN     VVARL1R                            69
      *
      * Sjekk om tilbud/netto-pris
      *
     C     FDSUMR        IFEQ      *ZERO
     C                   MOVE      *ON           *IN46
     C                   MOVE      *BLANK        D1TINE
     C                   MOVE      FDkpri        WWPKOD
     C     KEY19         CHAIN     VPKOL1R                            69
     C     *IN69         IFEQ      *OFF
     C                   MOVE      vaptxt        D1TINE
     C                   END
     C                   END
3.13  *
3.13  * Dersom vare er hentet fra LVR,
3.13  * sett inn  2 desimaler som std.
3.13  *
3.13 c                   if        fdlvre = 1
3.13 c                   eval      vvdesi = 2
3.13 c                   endif
      *
      * Test på antall desimaler
      *
     C     VVDESI        COMP      0                                      40
     C     VVDESI        COMP      1                                      41
     C     VVDESI        COMP      2                                      42
     C     VVDESI        COMP      3                                      43
     C   40              Z-ADD     FDANTA        D1ANT0
     C   41              Z-ADD     FDANTA        D1ANT1
     C   42              Z-ADD     FDANTA        D1ANT2
     C   43              Z-ADD     FDANTA        D1ANT3
      *
      * Varelinje
      *
     C                   MOVE      FDVARE        D1VARE
     C                   MOVEL     FDTEK1        D1TEK1
     C                   MOVE      FDENH1        D1ENH1
     C                   Z-ADD     FDsapr        D1BPRI
5.22 c                   z-add     fdlety        d1lety
      *
      * Netto pris
      *
     C                   Z-ADD     FDANTA        W1ANTA
     C     W1ANTA        IFEQ      0
     C                   Z-ADD     1             W1ANTA
     C                   ENDIF
     C     FDSUMS        SUB       FDSUMR        W1NPRI
     C     W1NPRI        DIV       W1ANTA        D1NPRI
      *
      * Beregne snittrabatt
      *
     C     100           SUB       FDRAB1        W1RAB1
     C                   MULT      FDRAB2        W1RAB1
     C                   DIV       100           W1RAB1
     C                   ADD       FDRAB1        W1RAB1
     C                   Z-ADD     W1RAB1        D1RABP
     C                   Z-ADD     FDSUMR        D1RABB
      *
      * Sum Beløp
      *
     C                   Z-ADD     FDSUMS        D1BBEL
     C     FDSUMS        SUB       FDSUMR        D1NBEL
      *
      * Skal mva. beregnes ?
      *
     C     AFMVAK        IFEQ      1
     C                   MOVE      *ON           *IN37
      *
     C                   ENDIF
      *
      * Akkumuler til overføringssum
      *
     C**>  (BARE PRISER)   ADD  D1NBEL    O1TRSP
      *
      * Skriv varelinjer
      *
     C     TAG02A        TAG
     C                   ADD       1             WLTELL
     C     VALKTX        IFNE      1
     C                   WRITE     D1DET                                30
     C                   ELSE
     C                   MOVEL     FDTEK1        D1TEK1
     C                   MOVEL     FDTEK2        D1TEK2
     C                   MOVE      *ON           *IN44
     C                   WRITE     D2DET                                30
     C                   END
      *
      * Er det overflow ?
      *
     C     *IN30         CASEQ     *ON           XOVRFL
     C                   ENDCS
5.22  *
5.22  * Skriv varelinje for tekst 2
5.22  * dersom den er oppgitt
5.22  *
5.22 c                   if        fdtek2 <> *blank
5.22 c                   if        valktx <> 1
5.22 c                   add       1             wltell
5.22 c                   eval      d1tek2 = fdtek2
5.22 c                   write     d1det2                               30
5.22  *
5.22  * Er det overflow ?
5.22  *
5.22 c                   if        *in30 = *on
5.22 c                   exsr      xovrfl
5.22 c                   endif
5.22  *
5.22 c                   endif
5.22 c                   endif
      *
     C                   GOTO      XSLUTT
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Behandling av tekstlinjer                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     TAG03         TAG
      *
     C                   MOVE      FKSONR        WWNUMM
     C                   MOVE      FKSSUF        WWSUFF
     C                   MOVE      FKSLIN        WWLINE
     C     KEY03         CHAIN     FOTXL1R                            63
      *
      * Gå ut dersom det
      * ikke er oppsamling,
      * og linje-nummer er
      * større enn 5000.
      * Da skal print fore-
      * gå tilslutt på L4.
      *
     C     VAOSAM        IFEQ      0
     C     FKSLIN        IFGE      5000
     C                   GOTO      XSLUTT
     C                   END
     C                   ENDIF
      *
     C                   MOVEL     FTTEXT        E1TEXT
     C                   ADD       1             WLTELL
     C                   WRITE     E1DET                                30
      *
      * Er det overflow ?
      *
     C     *IN30         CASEQ     *ON           XOVRFL
     C                   ENDCS
      *
     C     XSLUTT        TAG
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * T1TOT Behandle total T1                                         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      * Finne ut om det er
      * plass til å skrive
      * avslutning uten å
      * skifte til ny side
      * først.
      *
      * Trekker ut heading
      *
     CL4   l_alin        SUB       24            WLTEST
      *
      * Trekker ut antall
      * linjer skrevet
      *
     CL4                 SUB       WLTELL        WLTEST
      *
      * Trekker ut plassbehov
      * for avslutning
      *
     CL4                 SUB       10            WLTEST
      *
      * Skrive ny heading ?
      *
     CL4   WLTEST        IFLT      0
     CL4                 WRITE     A1HDR
     CL4                 WRITE     A2HDR
     CL4                 Z-ADD     0             WLTELL
     CL4                 ENDIF
      *
      * Skrive totalsum
      *
     C**>                  WRITET1TOT                  30
      *
      * Skrive tekstlinjer
      * dersom det ikke er
      * oppsamling og linje
      * nummer er større enn
      * 5000.
      *
     CL4   VAOSAM        IFEQ      0                                                     .....
     CL4                 MOVE      FKSONR        WWNUMM                                      .
     CL4                 MOVE      FKSSUF        WWSUFF                                      .
     CL4                 Z-ADD     5000          WWLINE                                      .
     CL4   KEY03         SETLL     FOTXL1R                                                   .
     CL4                 READ      FOTXL1R                                63                 .
     CL4   *IN63         CABEQ     *ON           TAG99                                       .
      *                                                                 .
     CL4   WWNUMM        DOWEQ     FTNUMM                                                ... .
     CL4   WWSUFF        IFEQ      FTSUFF                                                  . .
      *                                                               . .
     CL4                 MOVEL     FTTEXT        E1TEXT                                    . .
     CL4                 ADD       1             WLTELL                                    . .
     CL4                 WRITE     E1DET                                30                 . .
      *                                                               . .
      * Er det overflow ?                                             . .
      *                                                               . .
     CL4   *IN30         IFEQ      *ON                                                     . .
     CL4                 WRITE     A1HDR                                                   . .
     CL4                 WRITE     A2HDR                                                   . .
     CL4                 Z-ADD     0             WLTELL                                    . .
     CL4                 ENDIF                                                             . .
     CL4                 ENDIF                                                             . .
      *                                                               . .
     CL4                 READ      FOTXL1R                                63               . .
     CL4   *IN63         CABEQ     *ON           TAG99                                     . .
     CL4                 ENDDO                                                           ... .
     CL4                 ENDIF                                                           .....
      *
     CL4   TAG99         TAG
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Sjekk/Hent tekst for betalingsbetingelser         RS203R       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     SBBETB        BEGSR
      *
     c                   eval      wpcbda = *loval
     c                   eval      wpcfda = *loval
      *
     c                   call      'FÅ703R'
     c                   parm                    wwfirm
     c                   parm                    fobetb
     c                   parm                    wpcbda
     c                   parm                    wpretk
     c                   parm                    wpcfda
     c                   parm                    wpctx1
     c                   parm                    wpctx2
      *
     C                   MOVE      *BLANK        A1BTX1
     C                   MOVE      *BLANK        A1BTX2
      *
     C     WPRETK        IFEQ      *BLANK
     C                   MOVEL     WPCTX1        A1BTX1
     C                   MOVEL     WPCTX2        A1BTX2
     C                   ENDIF
      *
     C                   ENDSR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for overflow                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XOVRFL        BEGSR
      *
     C                   MOVE      *ON           *IN45
     C**> (BARE PRISER)    WRITEO1DET
     C                   WRITE     A1HDR
     C                   WRITE     A2HDR
     C                   MOVE      *OFF          *IN45
     C**> (BARE PRISER)    WRITEO1DET
     C                   Z-ADD     0             WLTELL
      *
     C                   ENDSR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for oppstart                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XSTRSR        BEGSR
      *
     C                   Z-ADD     1             FCYCLE            1 0
      *
      * Legger inn firma i nøkkel
      *
     C                   MOVE      FKSFIR        WWFIRM
      *
      * Hente Kunde-tilbuds-nummer
      *
     C     KEY09         CHAIN     FSTSL1R                            69
      *
     C     *IN69         IFEQ      *OFF
     C                   MOVE      FAAKUN        W0KUND
     C                   END
      *
      * Slår opp ordretype
      *
     C                   MOVE      FKSOTY        WWOTYP
     C     KEY07         CHAIN     VOTYL1R                            67
      *
      * Er ordretype negativ ?
      *
     C     VAOKRE        IFEQ      '-'
     C                   MOVE      *ON           *IN36
     C                   ENDIF
      *
     C                   ENDSR
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     *INZSR        BEGSR
5.01  *
5.01  * Parametre
5.01  *
5.01 c     *entry        plist
5.01 c                   parm                    p_mail
5.01 c                   parm                    p_kund
5.61 c                   parm                    p_kpro
5.02 c                   parm                    p_numm
5.02 c                   parm                    p_selg
      *
     C     *LIKE         DEFINE    A1BTX1        WPCTX1
     C     *LIKE         DEFINE    A1BTX2        WPCTX2
     C     *LIKE         DEFINE    FAAKUN        W0KUND
     C     *LIKE         DEFINE    VAOTYP        WWOTYP
     C     *LIKE         DEFINE    FOKUND        WWKUND
     C     *LIKE         DEFINE    FOFIRM        WWFIRM
     C     *LIKE         DEFINE    FOWSID        WWWSID
     C     *LIKE         DEFINE    FONUMM        WWNUMM
     C     *LIKE         DEFINE    FOSUFF        WWSUFF
     C     *LIKE         DEFINE    FDLINE        WWLINE
     C     *LIKE         DEFINE    FDLTYP        WWLTYP
     C     *LIKE         DEFINE    VAPKOD        WWPKOD
     C     *LIKE         DEFINE    FDNUMM        SANUMM
     C     *LIKE         DEFINE    VVVARE        WWVARE
     C     *LIKE         DEFINE    FDANTA        W1ANTA
     C     *LIKE         DEFINE    FDsapr        W1RAB1
     C     *LIKE         DEFINE    FDsapr        W1NPRI
     C     *LIKE         DEFINE    FDsapr        D1RABB
      *
     C     *LIKE         DEFINE    FDsapr        D1BPRI
     C     *LIKE         DEFINE    FDRAB1        D1RABP
     C     *LIKE         DEFINE    FDSUMS        D1NBEL
     C     *LIKE         DEFINE    FDSUMS        D1BBEL
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     C                   MOVE      *BLANK        WPRETK            1
     C                   MOVE      *ZERO         WPMVAS            6 2
     C                   MOVE      *ZERO         WPMVAT            5 4
     C                   MOVE      *ZERO         WPMVAF           10 9
      *
     C                   MOVE      *BLANK        WWRUTI           10
     C                   MOVE      *ZERO         WLTELL            3 0
     C                   MOVE      *ZERO         WLTEST            3 0
6.20  * Key for posisjonering på selgere
6.20  *
6.20 c     ra09l1_key    klist
6.20 c                   kfld                    wwfirm
6.20 c                   kfld                    ra09l1_ikod
      *
      * Key for Heading-file
      *
     C     KEY01         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWNUMM
     C                   KFLD                    WWSUFF
      *
      * Key for Detalj-file
      *
     C     KEY02         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWNUMM
     C                   KFLD                    WWSUFF
     C                   KFLD                    WWLINE
      *
      * Key for Tekst-linjer
      *
     C     KEY03         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWNUMM
     C                   KFLD                    WWSUFF
     C                   KFLD                    WWLINE
      *
      * Key for kunderegister
      *
     C     KEY04         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWKUND
      *
      * Key for firmaregister
      *
     C     KEY06         KLIST
     C                   KFLD                    WWFIRM
      *
      * Key for ordretyper
      *
     C     KEY07         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWOTYP
      *
      * Key for linjetyper
      *
     C     KEY08         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWLTYP
      *
      * Key for statusregister
      *
     C     KEY09         KLIST
     C                   KFLD                    WWFIRM
      *
      * Key for formular-register
      *
     C     KEY10         KLIST
     C                   KFLD                    WWRUTI
      *
      * Key for vare-register
      *
     C     KEY11         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWVARE
      *
      * Key for priskode-register
      *
     C     KEY19         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWPKOD
      *
      * Hente inn rutinenavn
      * og meldingstekst
      *
     C                   MOVE      l_ruti        WWRUTI
      *
     C     KEY10         CHAIN     AFORL1R                            69
     C  N69              MOVE      AFTXT1        A1TXT1
     C*>N69                MOVE AFMLD1    T1MLD1
5.01  *
5.01  * Sjekker om dette skal sendes
5.01  * som e-mail
5.01  *
5.01 c                   if        l_outq = 'E_MAIL_ASP'
5.01 c                   eval      p_mail = 'J'
5.01 c                   endif
      *
     C                   ENDSR
      *
