     h option(*nodebugio) datedit(*dmy) decedit(',')
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASSHOP
      * Program . . . . : BU924R
      * Beskrivelse . . : Butikk, henter kundeprosjekt-informasjon
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       250505 HKO  Nytt program
6.20  * 6.20  250511 bsa  prosjekt er alfa
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     faposl1    if   e           k disk    rename(apospfr:aposl1r)
      *
      * Definering av parametre
      *
     d p_firm          s              3
     d p_kund          s              6  0
     d p_kpro          s              6  0
     d p_pinf          s            400
      *
      * Definering av datastrukturer
      *
      * Datastruktur - kundeinformasjon
      *
     d                 ds
     d d_pinf                       400
     d  d_fdat                         d   overlay(d_pinf:1)   datfmt(*iso)
     d  d_tdat                         d   overlay(d_pinf:11)  datfmt(*iso)
     d  d_navn                       35    overlay(d_pinf:21)
     d  d_adr1                       35    overlay(d_pinf:56)
     d  d_adr2                       35    overlay(d_pinf:91)
     d  d_ponr                       10    overlay(d_pinf:126)
     d  d_sted                       35    overlay(d_pinf:136)
     d  d_kprs                       35    overlay(d_pinf:171)
     d  d_tlfa                       15    overlay(d_pinf:206)
     d  d_tlfp                       15    overlay(d_pinf:221)
     d  d_mobn                       15    overlay(d_pinf:236)
     d  d_mail                       50    overlay(d_pinf:251)
     d  d_selg                        4  0 overlay(d_pinf:301)
     d  d_priv                        1  0 overlay(d_pinf:305)
     d  d_kjor                        2  0 overlay(d_pinf:306)
     d  d_kjnr                        5  0 overlay(d_pinf:308)
6.20 d  d_proj                        6    overlay(d_pinf:313)
     d  d_orka                        3  0 overlay(d_pinf:319)
     d  d_okun                        6  0 overlay(d_pinf:322)
     d  d_okpr                        6  0 overlay(d_pinf:328)
     d  d_opda                         d   overlay(d_pinf:334) datfmt(*iso)
     d  d_odat                         d   overlay(d_pinf:344) datfmt(*iso)
     d  d_edat                         d   overlay(d_pinf:354) datfmt(*iso)
     d  d_etim                         t   overlay(d_pinf:364) timfmt(*iso)
     d  d_eusr                       10    overlay(d_pinf:372)
     d  d_dumm                        1    overlay(d_pinf:400) inz('*')
      *
      * Definering av variabler i nøkler
      *
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
     d aposl1_ponr     s                   like(apponr)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Utleder firma fra parameter
      *
     c                   move      p_firm        w_firm
      *
      * Avbryter dersom kundeprosjekt ikke finnes
      *
     c                   eval      fkprl1_kund = p_kund
     c                   eval      fkprl1_kpro = p_kpro
     c     fkprl1_key    chain     fkprl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
      * Henter poststed
      *
     c                   if        flponr <> 0
     c                   eval      aposl1_ponr = flponr
     c     aposl1_key    chain     aposl1
     c                   endif
      *
      * Skriver til datastruktur
      *
     c                   eval      d_fdat = flfdat
     c                   eval      d_tdat = fltdat
     c                   eval      d_navn = flnavn
     c                   eval      d_adr1 = fladr1
     c                   eval      d_adr2 = fladr2
     c                   eval      d_ponr = *blank
     c                   eval      d_sted = apsted
     c                   eval      d_kprs = flkprs
     c                   eval      d_tlfa = fltlfa
     c                   eval      d_tlfp = fltlfp
     c                   eval      d_mobn = fltlfm
     c                   eval      d_mail = flmail
     c                   eval      d_selg = flselg
     c                   eval      d_priv = flpriv
     c                   eval      d_kjor = flkjor
     c                   eval      d_kjnr = flkjnr
     c                   eval      d_proj = flproj
     c                   eval      d_orka = florka
     c                   eval      d_okun = flokun
     c                   eval      d_okpr = flokpr
     c                   eval      d_opda = flopda
     c                   eval      d_odat = fledat
     c                   eval      d_edat = fledat
     c                   eval      d_etim = fletim
     c                   eval      d_eusr = fleusr
      *
     c                   movel     flponr        d_ponr
      *
      * Skriver til parametre
      *
     c                   eval      p_pinf = d_pinf
      *
      *
      * Avslutt
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameter
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_kund
     c                   parm                    p_kpro
     c                   parm                    p_pinf
      *
      * Key for oppslag på kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      * Key for oppslag på poststed-register
      *
     c     aposl1_key    klist
     c                   kfld                    aposl1_ponr
      *
     c                   endsr
