     H/TITLE  ASOFAK: Blanker kode i ordre-register etter utskrift
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO618R
      * Beskrivelse . . : Blanker kode i ordre-register etter utskrift
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
5.41  * R5.41 031106 AMD  Ordre som faktureres blir nå inntil videre
      *                   logget i slette-register.  I den forbindelse
      *                   legges det på slette-type 'FAKT' på disse.
      * 6.10  240609 BSA  Oppdatert med sporingsinformasjon
6.nu  * 6.30  230614 bof  sjekket mtp. nummerutvidelse
      *
8.01  *K0000  190723 bsa  Sletter summering av utplukket fakturakjøring
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     FFPSOLU    IP   E           K DISK
     F                                     RENAME(FPSOPFR:FPSOLUR)
      *
     FVOTYL1    IF   E           K DISK
     F                                     RENAME(VOTYPFR:VOTYL1R)
      *
     FFOHELU    UF   E           K DISK
     F                                     RENAME(FOHEPFR:FOHELUR)
8.01 fffakiu    uf   e           k disk    rename(ffakst:ffakiur)
      *
     D                UDS
6.10 d l_user                911    920
     D  DSSFIR               944    946  0
      *
8.01 d ffakiu_ksrt     s                   like(faksrt)
8.01 d ffakiu_knum     s                   like(faknum)
8.01 d ffakiu_ksuf     s                   like(faksuf)
5.01  *
5.01  * Definering av variable
5.01  *
5.01 d w_opdt          s              1
8.01 d w_firm          s                   like(fakfir)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandler ordrene som er skrevet ut                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      *  Hopper ut dersom dette ikke er en HEAD-linje
      *
     C     FKSKOD        CABNE     'H'           XSLUTT
      *
      *  Legger inn verdier i nøkklene
      *
     C                   MOVE      FKSFIR        HEDFIR
8.01 c                   eval      w_firm = fksfir
     C                   MOVE      FKSONR        HEDNUM
     C                   MOVE      FKSSUF        HEDSUF
      *
      *  Hent opplysninger fra ORDRETYPE-REGISTER
      *
     C                   MOVE      FKSFIR        OTYFIR
     C                   MOVE      FKSOTY        OTYTYP
     C     OTYKEY        CHAIN     VOTYL1R                            90
      *
      *  Sletter ordren etter at faktura/Kreditnota er skrevet
      *
     C     VAOAKK        IFEQ      3
5.41 C                   MOVE      'FAKT'        WTYPED
5.01 c                   move      'R'           w_opdt
     C                   CALL      'FO410R'
     C                   PARM                    HEDFIR
     C                   PARM                    HEDNUM
     C                   PARM                    HEDSUF
     C                   PARM                    WTYPED
     C                   PARM                    WPKOM1
     C                   PARM                    WPKOM2
5.01 c                   parm                    w_opdt
8.01  *
8.01  * Slett forkeomst av sum fakturering
8.01  *
8.01 c                   eval      ffakiu_ksrt = fkssrt
8.01 c                   eval      ffakiu_knum = 0
8.01 c                   eval      ffakiu_ksuf = 0
8.01 c     ffakiu_key    chain     ffakiur
8.01 c                   if        %found
8.01 c                   delete    ffakiu
8.01 c                   endif
8.01  *
8.01 c                   eval      ffakiu_knum = fksonr
8.01 c                   eval      ffakiu_ksuf = fkssuf
8.01 c     ffakiu_key    chain     ffakiur
8.01 c                   if        %found
8.01 c                   delete    ffakiu
8.01 c                   endif
      *
     C                   GOTO      XSLUTT
     C                   END
      *
      *  Opphever merkingen av ordren slik at den kan behandles igjen
      *
     C     HEDKEY        CHAIN     FOHELUR                            90
     C     *IN90         IFEQ      *OFF
     C                   MOVE      *ZERO         FOKODE
     C                   MOVE      *loval        FOKODA
     C                   MOVE      *loval        FOKOTI
     C                   MOVE      *BLANK        FOKOUS
     C                   MOVE      *BLANK        FOKOWS
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = l_user
     C                   UPDATE    FOHELUR
     C                   END
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     XSLUTT        TAG
      *
     CLR                 RETURN
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     CSR   *INZSR        BEGSR
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     C                   MOVE      *BLANK        WTYPED            4
     C                   MOVE      *BLANK        WPKOM1           30
     C                   MOVE      *BLANK        WPKOM2           30
      *
      *  Redefinering av nøkler
      *
     C     *LIKE         DEFINE    VAOFIR        OTYFIR
     C     *LIKE         DEFINE    VAOTYP        OTYTYP
      *
     C     *LIKE         DEFINE    FOFIRM        HEDFIR
     C     *LIKE         DEFINE    FONUMM        HEDNUM
     C     *LIKE         DEFINE    FOSUFF        HEDSUF
      *
      *  Key for file : ORDRE-TYPE-REGISTER
      *
     C     OTYKEY        KLIST
     C                   KFLD                    OTYFIR
     C                   KFLD                    OTYTYP
      *
      *  Key for file : ORDRE-HODE-REGISTER
      *
     C     HEDKEY        KLIST
     C                   KFLD                    HEDFIR
     C                   KFLD                    HEDNUM
     C                   KFLD                    HEDSUF
8.01  *
8.01  *  Key for file : Ordre-summering
8.01  *
8.01 c     ffakiu_key    klist
8.01 c                   kfld                    w_firm
8.01 c                   kfld                    ffakiu_ksrt
8.01 c                   kfld                    ffakiu_knum
8.01 c                   kfld                    ffakiu_ksuf
      *
     CSR                 ENDSR
