     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : NM711R
      * Beskrivelse . . : Nettbutikk, eksport av kunde søkefil
      *
      * Spesialversjon. : Brukes i kundesøk/sjekk i ehandelsportal
      * Tilpasset for . : Mestergruppen
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       090516 ASK  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fanbll3    if   e           k disk    rename(anblpfr:anbll3r)
     fanbhl1    if   e           k disk    rename(anbhpfr:anbhl1r)
      *
     fnetkuns   uf a e           k disk
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
     d l_fgr                 931    933
      *
      * Definering av datastrukturer
      *
      * Recordbeskrivelse - meldingshode
      *
     d                 ds
     d d_unh                         41
     d  d_unhrcd                      3    overlay(d_unh: 1) inz('UNH')
     d  d_unhfill1                    1    overlay(d_unh: 4) inz(';')
     d  d_unhrefno                   26    overlay(d_unh: 5)
     d  d_unhfill2                    1    overlay(d_unh:31) inz(';')
     d  d_unhtypeid                   6    overlay(d_unh:32) inz('IMLIMP')
     d  d_unhfill3                    1    overlay(d_unh:38) inz(';')
     d  d_unhversion                  3    overlay(d_unh:39) inz('1.0')
      *
      * Recordbeskrivelse - meldingsstart
      *
     d                 ds
     d d_bgm                         17
     d  d_bgmrcd                      3    overlay(d_bgm: 1) inz('BGM')
     d  d_bgmfill1                    1    overlay(d_bgm: 4) inz(';')
     d  d_bgmetypekod                10    overlay(d_bgm: 5) inz('IMLDATACSO')
     d  d_bgmfill2                    2    overlay(d_bgm:15) inz(';;')
     d  d_bgmfunction                 1    overlay(d_bgm:17) inz('1')
      *
      * Recordbeskrivelse - organisasjon
      *
     d                 ds
     d d_org                        126
     d  d_orgrcd                      3    overlay(d_org: 1) inz('ORG')
     d  d_orgfill1                    1    overlay(d_org: 4) inz(';')
     d  d_orgid                      30    overlay(d_org: 5)
     d  d_orgfill2                    1    overlay(d_org:35) inz(';')
     d  d_orgstoreid                 30    overlay(d_org:36)
     d  d_orgfill3                    1    overlay(d_org:66) inz(';')
     d  d_orgname                    60    overlay(d_org:67)
      *
      * Recordbeskrivelse - kunde
      *
     d                 ds
     d d_cus                        550
     d  d_cusrcd                      3    overlay(d_cus:  1) inz('CSO')
     d  d_cusfill1                    1    overlay(d_cus:  4) inz(';')
     d  d_cusnum                      6  0 overlay(d_cus:  5)
     d  d_cusfill2                    1    overlay(d_cus: 11) inz(';')
     d  d_cusorg                      9  0 overlay(d_cus: 12)
     d  d_cusfill3                    1    overlay(d_cus: 21) inz(';')
     d  d_cusnam                     35    overlay(d_cus: 22)
     d  d_cusfill4                    2    overlay(d_cus: 57) inz(';;')
     d  d_cusad1                     35    overlay(d_cus: 59)
     d  d_cusfill5                    1    overlay(d_cus: 94) inz(';')
     d  d_cusad2                     35    overlay(d_cus: 95)
     d  d_cusfill6                    1    overlay(d_cus:130) inz(';')
     d  d_cuspon                      4  0 overlay(d_cus:131)
     d  d_cusfill7                    1    overlay(d_cus:135) inz(';')
     d  d_cuspad                     35    overlay(d_cus:136)
     d  d_cusfill8                    1    overlay(d_cus:171) inz(';')
     d  d_cusgti                     13  0 overlay(d_cus:172)
     d  d_cusfill9                    1    overlay(d_cus:185) inz(';')
     d  d_cuscre                      1  0 overlay(d_cus:186)
      *
      * Recordbeskrivelse - meldingshale
      *
     d                 ds
     d d_unt                         43
     d  d_untrcd                      3    overlay(d_unt: 1) inz('UNT')
     d  d_untfill1                    1    overlay(d_unt: 4) inz(';')
     d  d_untnosegmen                12  0 overlay(d_unt: 5)
     d  d_untfill2                    1    overlay(d_unt:17) inz(';')
     d  d_untrefno                   26    overlay(d_unt:18)
      *
      * Definering av variabler i nøkler
      *
     d anbll3_fgr      s                   like(aolfgr)
     d anbll3_fir      s                   like(aolfir)
      *
      * Definering av variable
      *
     d w_tmst          s               z
     d w_tell          s             12  0
      *
     d w_firm          s                   like(rkfirm)
      *
      * Definering av parametre
      *
     d p_stat          s              1
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      p_stat = *blank
      *
      * Henter data om nettbutikk og eier. Avslutter hvis ingen finnes.
      *
     c                   eval      anbll3_fgr = l_fgr
     c                   eval      anbll3_fir = l_firm
     c     anbll3_key    chain     anbll3r
     c                   if        not %found
     c                   eval      p_stat = 'E'
     c                   goto      avslutt
     c                   endif
     c     aoleie        chain     anbhl1r
     c                   if        not %found
     c                   eval      p_stat = 'E'
     c                   goto      avslutt
     c                   endif
      *
      * Meldingshode
      *
     c                   time                    w_tmst
     c                   move      w_tmst        d_unhrefno
      *
     c                   eval      data = d_unh
     c                   write     netkunds
      *
      * Meldingsstart
      *
     c                   eval      data = d_bgm
     c                   write     netkunds
      *
      * Organisasjon
      *
     c                   eval      d_orgid = aoheie
     c                   eval      d_orgstoreid = aolbut
     c                   eval      d_orgname = aohnav
      *
     c                   eval      data = d_org
     c                   write     netkunds
      *
      * Kunde informasjon
      *
     c     rkunl1_key    setll     rkunl1
     c     rkunl1_key    reade     rkunl1
     c                   dow       %eof = *off
      *
     c                   if        rkftnr <> 0 and
     c                             rkeank <> 0 and
     c                             rkpass <> 'J'
      *
     c                   eval      w_tell = w_tell + 1
      *
     c                   eval      d_cusnum = rkkund
     c                   eval      d_cusorg = rkftnr
     c                   eval      d_cusnam = rknavn
     c                   eval      d_cusad1 = rkgate
     c                   eval      d_cusad2 = rkadr2
     c                   eval      d_cuspon = rkponr
     c                   eval      d_cuspad = %subst(rksted:6:25)
     c                   eval      d_cuscre = rkkres
     c                   eval      d_cusgti = rkeank
     c                   eval      data = d_cus
     c                   write     netkunds
      *
     c                   endif
      *
     c     rkunl1_key    reade     rkunl1
     c                   enddo
      *
      * Meldingshale
      *
     c                   eval      d_untnosegmen = w_tell
     c                   eval      d_untrefno = d_unhrefno
      *
     c                   eval      data = d_unt
     c                   write     netkunds
      *
      *
      * Avslutt
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_stat
      *
      * Key for les på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på nettbutikk
      *
     c     anbll3_key    klist
     c                   kfld                    anbll3_fgr
     c                   kfld                    anbll3_fir
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
