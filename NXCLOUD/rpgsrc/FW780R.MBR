     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FW780R
      * Beskrivelse . . : Lev.vare, oppretter varer i EVR
      *
      *                   Programmet overfører nye varer fra leverandør
      *                   til vareregisteret på bakgrunn av krav til
      *                   utfylte og kvalitetssikrede felter i FLVAPF
      *
      *                   Programmet er en hurtigfunksjon for oppretting
      *                   av varer i vareregister via "bakveien"
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       111202 HKO  Nytt program
      *       010805 HKO  Fjernet skriving til kolonnen VELINJ
      *                   Utvidet vare-enhet med opprettet dato
      *       040805 HKO  Flyttet vekt og voluminformasjon fra vare til
      *                   vare-enhet.
      * 6.10  260609 BSA  Oppdatert med sporingsinformasjon
6.11  * 6.10  080709 bof  Oppdaterer ean-nr på vare-enhet
6.12  * 6.10  240909 bof  Oppdaterer ean-nr på vare-enhet-prisgiver
6.20  * 6.20  030411 bof  Utvidet vvprpf med prisgiver
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fflval3    if   e           k disk    rename(flvapfr:flval3r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvarl5    if   e           k disk    rename(vvarpfr:vvarl5r)
     fvvarlu    uf a e           k disk    rename(vvarpfr:vvarlur)
     fvvprlu    uf a e           k disk    rename(vvprpfr:vvprlur)
     fvvenlu    uf a e           k disk    rename(vvenpfr:vvenlur)
6.12 fvveplu    uf a e           k disk    rename(vveppfr:vveplur)
     fveanlu    uf a e           k disk    rename(veanpfr:veanlur)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     fvvaslu    uf   e           k disk    rename(vvaspfr:vvaslur)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
6.12 fvvepl2    if   e           k disk    rename(vveppfr:vvepl2r)
      *
      * Local data area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
      *
      * Definering av parametre
      *
     d p_firm          s              3
     d p_ldor          s              6
      *
      * Definering av variabler i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vvarl5_lvar     s                   like(vvlvar)
6.11 d veanlu_eann     s                   like(vxeann)
     d vvaslu_vare     s                   like(vvvare)
6.11 d vvepl2_pgti     s                   like(vepgti)
6.12 d vveplu_pvar     s                   like(vepvar)
6.12 d vveplu_penh     s                   like(vepenh)
6.12 d vveplu_pldo     s                   like(vepldo)
      *
      * Definering av variable
      *
     d w_indi          s              2  0
     d w_stat          s              1  0
     d w_timestamp     s             12  0
     d w_kvar          s              1  0
6.11 d w_eann          s             14  0
      *
     d w_firm          s                   like(fwfirm)
     d w_ldor          s                   like(fwldor)
     d w_vare          s                   like(vvvare)
     d w_varf          s                   like(vvvare)
      *
6.11 d b_dupp          s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Behandling av varer
      *
     c                   exsr      behandling
      *
      * Avslutt
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting av varer i vareregister
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
     c     flval3_key    setll     flval3
     c     flval3_key    reade     flval3
     c                   dow       not %eof
      *
      * Sjekker at et minimum av felter er utfyllt
      *
     c                   if        fwtvar = *blank and
     c                             fwlvar = *blank
     c                   goto      les_neste
     c                   endif
      *
     c                   if        fwogrp = 0 or
     c                             fwhgrp = 0 or
     c                             fwugrp = 0 or
     c                             fwtek1 = *blank or
     c                             fwenhe = *blank or
     c                             fwsapr = 0 or
     c                             fwinpr = 0
     c                   goto      les_neste
     c                   endif
      *
      * Sjekker om varen finnes fra før
      *
     c                   if        fwtvar <> *blank
     c                   eval      vvarl1_vare = fwtvar
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   goto      les_neste
     c                   endif
     c                   else
     c                   eval      vvarl5_lvar = fwlvar
     c     vvarl5_key    chain     vvarl5
     c                   if        %found
     c                   goto      les_neste
     c                   endif
     c                   endif
      *
      * Oppdaterer vareregister
      *
     c                   exsr      oppdater
      *
      * Leser neste rad
      *
     c     les_neste     tag
      *
     c     flval3_key    reade     flval3
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av vareregister
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdater      begsr
      *
     c                   eval      w_vare = *blank
     c                   eval      w_varf = *blank
      *
     c                   if        w_stat <> 0
     c                   goto      end_oppd
     c                   endif
      *
      * Tildeling av varenummer
      * Dersom 'benytt lev. varenummer' er valgt, og tildelt varenummer er
      * forskjellig fra blank, settes varenummer lik tildelt varenummer.
      * Ellers kalles program for å finne neste ledige varenummer
      * Avbryter oppdateringen dersom neste ledige varenummer ikke ble
      * funnet eller dersom dette er lik eller overstiger 10000000
      *
     c                   if        fwtvar <> *blank
     c                   eval      w_vare = fwtvar
     c                   endif
      *
     c                   if        w_vare = *blank
     c                   call      'VV700R'
     c                   parm                    w_firm
     c                   parm                    w_varf
     c                   parm                    w_vare
     c                   endif
      *
     c                   if        w_vare = *blank or
     c                             w_vare >= '10000000'
     c                   goto      end_oppd
     c                   endif
      *
      * Legger inn data og oppretter vare
      *
     c                   clear                   vvarlur
      *
     c                   eval      vvfirm = w_firm
     c                   eval      vvvare = w_vare
     c                   eval      vvogrp = fwogrp
     c                   eval      vvhgrp = fwhgrp
     c                   eval      vvugrp = fwugrp
     c                   eval      vvtek1 = fwtek1
     c                   eval      vvtek2 = fwtek2
     c                   eval      vvldor = fwldor
     c                   eval      vvlvar = fwlvar
     c                   eval      vvnobb = fwnobb
     c                   eval      vvkpri = *blank
     c                   eval      vvvtyp = 'L'
     c                   eval      vvkhev = *blank
     c                   eval      vvenhe = fwenhe
     c                   eval      vvdesi = 2
     c                   eval      vvkstr = 00
     c                   eval      vvbmen = fwbmen
     c                   eval      vvenhs = fwenhe
     c                   eval      vvenhl = fwenhe
      *
     c                   time                    vvodat
6.10 c                   time                    vvotim
6.10 c                   eval      vvousr = l_user
     c                   time                    vvedat
     c                   time                    vvetim
     c                   eval      vveusr = l_user
      *
     c                   write     vvarlur
      *
      * Oppretter vare-pris
      *
     c                   eval      vpfirm = w_firm
     c                   eval      vpvare = vvvare
6.20 c                   eval      vpldor = vvldor
6.20 c                   eval      vpprgr = *blank
     c                   eval      vplety = 0
     c                   eval      vpenhe = fwenhe
     c                   eval      vppdat = *loval
     c                   eval(h)   vpsapr = fwsapr
     c                   eval(h)   vpinpr = fwinpr
     c                   eval(h)   vpkopr = vpinpr
     c                   eval      vpbupr = 0
6.10 c                   time                    vpodat
6.10 c                   time                    vpotim
6.10 c                   eval      vpousr = l_user
     c                   time                    vppdat
     c                   time                    vpedat
     c                   time                    vpetim
     c                   eval      vpeusr = l_user
      *
     c                   write     vvprlur
      *
      * Oppretter vare-enhet
      *
6.11 c                   clear                   vvenlur
      *
     c                   eval      vefirm = w_firm
     c                   eval      vevare = vvvare
     c                   eval      veenhe = fwenhe
     c                   eval      vesaen = 1
     c                   eval      veinen = 1
     c                   eval      vepben = 1
     c                   eval      veomre = 1
     c                   eval      veleng = fwleng
     c                   eval      vebred = fwbred
     c                   eval      vehoyd = fwhoyd
     c                   eval      vevekt = fwvekt
     c                   eval      vevolu = fwvolu
      *
     c                   if        vevolu = 0 and
     c                             veleng <> 0 and
     c                             vebred <> 0 and
     c                             vehoyd <> 0
     c                   eval(h)   vevolu = (veleng / 1000) *
     c                                      (vebred / 1000) *
     c                                      (vehoyd / 1000)
     c                   endif
      *
     c                   time                    veodat
6.10 c                   time                    veotim
6.10 c                   eval      veousr = l_user
     c                   time                    veedat
     c                   time                    veetim
     c                   eval      veeusr = l_user
      *
     c                   write     vvenlur
      *
6.12 c                   eval      w_eann = fweann
6.12 c                   exsr      sjekk_ean
6.12 c                   if        b_dupp = *off
6.12 c                   eval      vveplu_pvar = vvvare
6.12 c                   eval      vveplu_penh = fwenhe
6.12 c                   eval      vveplu_pldo = w_ldor
6.12 c     vveplu_key    chain     vveplur
6.12 c                   eval      vepgti = w_eann
6.12 c                   if        %found(vveplu)
6.12 c                   eval      vepeus = l_user
6.12 c                   time                    vepeti
6.12 c                   time                    vepeda
6.12 c                   update    vveplur
6.12 c                   else
6.12 c                   eval      vepvar = vveplu_pvar
6.12 c                   eval      vepenh = vveplu_penh
6.12 c                   eval      vepldo = vveplu_pldo
6.12 c                   eval      vepeus = l_user
6.12 c                   time                    vepeti
6.12 c                   time                    vepeda
6.12 c                   eval      vepous = l_user
6.12 c                   time                    vepoti
6.12 c                   time                    vepoda
6.12 c                   write     vveplur
6.12 c                   endif
6.12 c                   endif
      *
6.11  * Hvis den finnes på ean-reg. så slettes den - skal
6.11  * ligge på vareenhet.
6.11  *
6.11 c                   if        fweann > 0
6.11  *
6.11 c                   eval      veanlu_eann = fweann
6.11 c     veanlu_key    chain     veanlu                             91
6.11 c                   if        %found(veanlu)
6.11 c                   delete    veanlur
6.11 c                   endif
6.11  *
6.11 c                   endif
      *
      * Oppretter vare i hovedlager dersom det er lagervare
      *
     c                   if        vvvtyp = 'L'
     c     fstsl1_key    chain     fstsl1
     c                   if        %found and
     c                             faalag = 1
     c                   call      'LL760R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   endif
     c                   endif
      *
      * Sletter vare i register over slettede vare
      *
     c                   eval      vvaslu_vare = vvvare
     c     vvaslu_key    chain     vvaslur
     c                   if        %found
     c                   delete    vvaslur
     c                   endif
      *
      * Kaller program for oppdatering av søketekster
      *
     c                   call      'VV750R'
     c                   parm                    w_firm
     c                   parm                    vvvare
      *
     c     end_oppd      endsr
      *
6.11  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.11  * Subrutine for å sjekke om ean-nr ligger inne fra før
6.11  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.11  *
6.11 c     sjekk_ean     begsr
6.11 c                   eval      b_dupp = *off
6.11  *
6.11 c                   eval      vvepl2_pgti = w_eann
6.11 c     vvepl2_key    chain     vvepl2
6.11 c                   if        %found(vvepl2)
6.11 c                   eval      b_dupp = *on
6.11 c                   endif
6.11  *
6.11 c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ldor
      *
      * Key for les på leverandør vareregister
      *
     c     flval3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
     c     vvarl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    vvarl5_lvar
      *
      * Key for oppslag på EAN-register
      *
6.11 c     veanlu_key    klist
     c                   kfld                    w_firm
6.11 c                   kfld                    veanlu_eann
6.12  *
6.12  * Key for oppslag på vare-enhet   med ean-nr
6.12  *
6.12 c     vvepl2_key    klist
6.12 c                   kfld                    w_firm
6.11 c                   kfld                    vvepl2_pgti
6.12  *
6.12  * Key for oppdatering av EVR-vare-enhet-prisgiver
6.12  *
6.12 c     vveplu_key    klist
6.12 c                   kfld                    w_firm
6.12 c                   kfld                    vveplu_pvar
6.12 c                   kfld                    vveplu_penh
6.12 c                   kfld                    vveplu_pldo
      *
      * Key for oppdatering av vare-sletter-register
      *
     c     vvaslu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvaslu_vare
      *
      * Key for oppslag på firmaregister
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på OFAK-koderegister
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      *
      * Henter firma og leverandør fra parameter
      *
     c                   move      p_firm        w_firm
     c                   move      p_ldor        w_ldor
      *
     c                   endsr
