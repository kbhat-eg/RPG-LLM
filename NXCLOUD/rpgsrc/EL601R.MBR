     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : EL601R
      * Beskrivelse . . : Leverandør bonus, skriver ut grunnlag
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       060224 bsa  Nytt program
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     felboir    if   e           k disk    rename(elbost:elboirr)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fvbotl1    if   e           k disk    rename(vbotpfr:vbotl1r)
     flxl5st    uf a e           k disk    rename(lxl5st:lxl5str)
      *
      * Definering av parametre
      *
     d p_firm          s              3
     d p_paar          s              4
     d p_tbot          s              2
     d b_bonu          s              1    inz(*off)
      *
      * Parameter overføringer
      *
     d                uds
     d paarf                 300    303  0                                      Pr. år
     d paart                 304    307  0                                      Pr. år
     d palt1                 308    308  0                                      Type
     d plnrf                 316    321  0                                      Knr. f.o.m.
     d plnrt                 322    327  0                                      Knr. t.o.m.
     d ptbot                 328    329                                         Bonustype
      *
      *  Hva skal skrives på liste                                Valg: J/N=Ja/N
     d pabon                 330    330                                         Alle m/avtale
      *
     d l_wsid                901    910                                         Bruker
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980                                         Navn
      *
      * Definering av variabler i nøkler
      *
     d elboir_blev     s                   like(elblev)
     d rlevl1_levr     s                   like(rllevr)
     d vbotl1_tbot     s                   like(vatbot)
      *
      * Definering av variable
      *
     d w_firm          s                   like(elfirm)
     d w_tbot          s              2
     d w_levr          s              6  0
     d w_bgrl          s             11  2
     d w_aar           s              4  0
     d w_excel_url     s            100
     d w_subg          s             11  2
     d w_subn          s              9  0
     d w_numm          s              8  0
     d w_kode          s              1
     d w_fell          s              6
     d w_type          s              2
     d w_fast          s             10
     d w_lnvn          s             30
     d w_btxt          s             30
      *
     d s_levr          s              6  0
     d s_tbot          s              2
      *
     d p_excel_url     s            100
      *
     d u_excl          s              1    inz(*off)
      *
      * Eksternt program
      *
       dcl-s co402_verdi1  char(1);
       dcl-s co402_verdi2  char(2048);
       DCL-PR CO402R EXTPGM('CO402R');
         p_filgrp            char(3)     const;
         p_firm              packed(3:0) const;
         p_lager             packed(2:0) const;
         p_nokkel            char(50)    const;
         p_verdi1            char(1);
         p_verdi2            char(2048);
       END-PR;
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      b_bonu = *off
      *
     c                   eval      w_bgrl = 0
     c                   eval      w_subn = 0
      *
     c                   eval      s_tbot = *blank
     c                   eval      s_levr = 0
      *
     c                   eval      elboir_blev  =  plnrf
     c     elboir_key    setll     elboir
     c     elboir_keyl   reade     elboir
      *
     c                   dow       not  %eof(elboir)
     c                   if        elblev > plnrt
     c                   goto      end_levr
     c                   endif
      *
     c                   if        ptbot <> *blank and
     c                             elboty <> ptbot
     c                   goto      nyles2
     c                   endif
      * Hent bonustypetekst
     c                   if        s_tbot <> elboty
     c                   exsr      finn_btext
     c                   eval      s_tbot = elboty
     c                   endif
      * Hent leverandørtekst
     c                   if        s_levr <> elblev
      * Detaljert eller summert ?
     c                   if        palt1 = 0 and
     c                             s_levr <> 0
     c                   eval      lx5ide = w_numm
     c                   eval      lx5ble = s_levr
     c                   eval      lx5bln = w_lnvn
     c                   eval      lx5lev = 0
     c                   eval      lx5ogr = 0
     c                   eval      lx5hgr = 0
     c                   eval      lx5ugr = 0
     c                   eval      lx5mod = 0
     c                   eval      lx5var = *blanks
     c                   eval      lx5enh = *blanks
     c                   eval      lx5lty = 0
     c                   eval      lx5bty = *blanks
     c                   eval      lx5bna = *blanks
     c                   eval      lx5fda = *loval
     c                   eval      lx5tda = *loval
     c                   eval      lx5mrk = *blanks
     c                   eval      lx5bgr = w_bgrl
     c                   eval      lx5bon = w_subn
     c                   eval      lx5bda = *loval
     c                   write     lxl5str
     c                   endif
     c                   exsr      finn_ltext
     c                   eval      s_levr = elblev
     c                   eval      w_bgrl = 0
     c                   eval      w_subn = 0
     c                   endif
      *
      * Legg ut info i fila tilexcel
      *
     c                   eval      w_bgrl = w_bgrl + elbgru
     c                   eval      w_subn = w_subn + elsbon
      *
      * Detaljert eller summert ?
     c                   if        palt1 = 1
     c                   eval      lx5ide = w_numm
     c                   eval      lx5ble = elblev
     c                   eval      lx5bln = w_lnvn
     c                   eval      lx5lev = elldor
     c                   eval      lx5ogr = elogrp
     c                   eval      lx5hgr = elhgrp
     c                   eval      lx5ugr = elugrp
     c                   eval      lx5mod = elmodn
     c                   eval      lx5var = elvare
     c                   eval      lx5enh = elenhe
     c                   eval      lx5lty = ellety
     c                   eval      lx5bty = elboty
     c                   eval      lx5bna = w_btxt
     c                   eval      lx5fda = elfdat
     c                   eval      lx5tda = eltdat
     c                   eval      lx5mrk = elmerk
     c                   eval      lx5bgr = elbgru
     c                   eval      lx5bon = elsbon
     c                   eval      lx5bda = elbdat
      *
     c                   write     lxl5str
     c                   endif
      *
     c     nyles2        tag
      *
     c     elboir_keyl   reade     elboir
     c                   enddo
      *
      *  Kaller opp program for exccel eksport
      *
     c     end_levr      tag
      *
      * Detaljert eller summert ?
     c                   if        palt1 = 0 and
     c                             s_levr <> 0
     c                   eval      lx5lev = s_levr
     c                   eval      lx5bna = w_lnvn
     c                   eval      lx5ogr = 0
     c                   eval      lx5hgr = 0
     c                   eval      lx5ugr = 0
     c                   eval      lx5mod = 0
     c                   eval      lx5var = *blanks
     c                   eval      lx5enh = *blanks
     c                   eval      lx5lty = 0
     c                   eval      lx5bty = *blanks
     c                   eval      lx5bna = *blanks
     c                   eval      lx5fda = *loval
     c                   eval      lx5tda = *loval
     c                   eval      lx5mrk = *blanks
     c                   eval      lx5bgr = elbgru
     c                   eval      lx5bon = elsbon
     c                   eval      lx5bda = elbdat
     c                   write     lxl5str
     c                   endif
      *
     c                   if        u_excl = *on
     c                   eval      p_excel_url = w_excel_url
     c                   call      'EL601C'
     c                   parm                    p_excel_url
     c                   parm                    w_numm
     c                   endif
      *
     c/exec sql
     c+ DELETE FROM LXL5ST WHERE LX5IDE = :w_numm
     c/end-exec
       exec sql commit;
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
      *
     c                   return
      *--------------------------------------------------------------
      * Finn leverandørtekst
      *--------------------------------------------------------------
     c     finn_ltext    begsr
      *
     c                   eval      w_lnvn = *blank
     c                   eval      rlevl1_levr = elblev
     c     rlevl1_key    chain     rlevl1
     c                   if        %found
     c                   eval      w_lnvn = rlnavn
     c                   endif
      *
     c                   endsr
      /eject
      *--------------------------------------------------------------
      * Finn tekst for bonustype
      *--------------------------------------------------------------
     c     finn_btext    begsr
      *
     c                   eval      w_btxt = *blank
     c                   eval      vbotl1_tbot = elboty
     c     vbotl1_key    chain     vbotl1
     c                   if        %found
     c                   eval      w_btxt = vatbes
     c                   endif
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for oppslag leverandører
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for oppslag bonustype
      *
     c     vbotl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vbotl1_tbot
      *
      * Key for les av bonusgrunnlag
      *
     c     elboir_key    klist
     c                   kfld                    w_firm
     c                   kfld                    elboir_blev
      *
      * Key for les av bonusgrunnlag
      *
     c     elboir_keyl   klist
     c                   kfld                    w_firm
      *
      * Henter inn løpenummer teller
      *
     c                   eval      w_firm = 0
     c                   eval      w_numm = 0
     c                   eval      w_kode = '0'
     c                   eval      w_fell = 'NXTKLI'
     c                   eval      w_type = *blank
     c                   eval      w_fast = *blank
     c                   eval      w_numm = *zero
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
     c                   parm                    w_numm
      *
      * Henter parameter
      *
     c                   eval      w_firm = l_firm
      *
      * OBS!!  Felles for alle filgrupper og firma
      *
       CallP CO402R('   ':0:0:'NX_EXCEL_URL':
                        co402_verdi1:co402_verdi2);
       if co402_verdi1 = '1';
         u_excl = *on;
         w_excel_url = co402_verdi2;
       endif;
      *
      *
     c                   endsr
