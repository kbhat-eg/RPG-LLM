     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : NXCLOUD
      * Program . . . . : RA137R
      * Beskrivelse . . : Vedlikehold av ekstern lagerinformasjon
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       061022 ASK  Nytt program
8.01  * 800   211122 ask  Enderet ekstern lagerkode fra N(2,0) til A(20) etter ønske fra NGN
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fra37iu    uf a e           k disk    rename(ra37st:ra37iur)
     fra37ir    if   e           k disk    rename(ra37st:ra37irr)
     fra37i1    if   e           k disk    rename(ra37st:ra37i1r)
     fra37i2    if   e           k disk    rename(ra37st:ra37i2r)
      *
     fra137d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980

      *
      * Definering av variabler i nøkler
      *
     d ra37iu_ruid     s                   like(rkeuid)
     d ra37ir_ruid     s                   like(rkeuid)
     d ra37i1_elag     s                   like(rkeela)
     d ra37i2_elag     s                   like(rkeela)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_firm          s                   like(rkefir)
     d w_lage          s                   like(rkeela)
     d w_ldor          s                   like(rkelev)
     d w_glnn          s             14  0
     d w_latx          s             20
     d w_letx          s             30
     d w_fmtx          s             20
      *
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
     c/exec sql
     c+ SET OPTION DatFmt=*ISO,
     c+            SRTSEQ=*LANGIDUNQ,
     c+            LANGID=NOR,
     c+            commit=*none
     c/end-exec

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   eval      b_oppr = *on
     c                   exsr      xc2bld
     c                   exsr      forny
     c                   eval      b_oppr = *off
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
8.01 c                   eval      ra37i1_elag = ' '
     c     ra37i1_key    setll     ra37i1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      ra37ir_ruid = b1uid
     c     ra37ir_key    chain     ra37ir
      *
     c                   if        %found and
     c                             b_oppr = *off
     c                   eval      c2elag = rkeela
     c                   eval      c2eltx = rkenav
     c                   eval      c2ead1 = rkead1
     c                   eval      c2ead2 = rkead2
     c                   eval      c2este = rkeste
     c                   eval      c2dgln = rkegln
     c                   eval      c2ldor = rkelev
     c                   eval      w_ldor = rkelev
     c                   exsr      finn_lev
     c                   eval      c2ltek = w_letx
     c     *dmy          move      rkeeda        c2edat
     c                   eval      c2eusr = rkeeus
     c                   eval      *in30 = *on
     c                   else
8.01 c                   eval      c2elag = ' '
     c                   eval      c2eltx = '  '
     c                   eval      c2ead1 = '  '
     c                   eval      c2ead2 = '  '
     c                   eval      c2este = '  '
     c                   eval      c2dgln = 0
     c                   eval      c2ldor = 0
     c                   eval      c2ltek = '  '
     c                   eval      b1uid  = 0
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   eval      *in31 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in31
     c                   goto      end_c2bld
     c                   endif
      *
     c                   if        *inkd
     c                   if        w_afld = 'C2LDOR'
     c                   exsr      lev_sok
     c                   endif
     c                   goto      c2taga
     c                   endif
      *
      * Sjekk at lager ikke er brukt før
      *
     c                   if        b_oppr = *on
     c                   eval      ra37i2_elag = c2elag
     c     ra37i2_key    chain     ra37i2
     c                   if        %found(ra37i2)
     c                   exsr      xc2msg1
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
      * Sjekk at nødvendig informasjon er fylt ut
      *
8.01 c                   if        c2elag = '  '
     c                   eval      m2felt = 'Ekst. lagernummer'
     c                   exsr      xc2msg2
     c                   goto      c2taga
     c                   endif
     c                   if        c2eltx = ' '
     c                   eval      m2felt = 'Ekst. lagernavn'
     c                   exsr      xc2msg2
     c                   goto      c2taga
     c                   endif
     c                   if        c2ead1 = ' '
     c                   eval      m2felt = 'Adresse1 felt  '
     c                   exsr      xc2msg2
     c                   goto      c2taga
     c                   endif
     c                   if        c2este = ' '
     c                   eval      m2felt = 'Postadresse    '
     c                   exsr      xc2msg2
     c                   goto      c2taga
     c                   endif
     c                   if        c2dgln = 0
     c                   eval      m2felt = 'GLN            '
     c                   exsr      xc2msg2
     c                   goto      c2taga
     c                   endif
     c                   if        c2ldor = 0
     c                   eval      m2felt = 'Leverandør      '
     c                   exsr      xc2msg2
     c                   goto      c2taga
     c                   endif
      *
      * Sjekk GLN informasjon
      *
     c                   eval      b_feil = *off
     c                   exsr      sjk_glnl
     c                   if        b_feil = *on
     c                   exsr      xc2msg3
     c                   goto      c2taga
     c                   endif
      *
     c                   exsr      sjk_glnd
     c                   if        b_feil = *on
     c                   exsr      xc2msg4
     c                   goto      c2taga
     c                   endif
      *
      * Oppdatering
      *
     c                   eval      ra37iu_ruid = b1uid
     c     ra37iu_key    chain     ra37iur
      *
     c                   eval      rkenav = c2eltx
     c                   eval      rkead1 = c2ead1
     c                   eval      rkead2 = c2ead2
     c                   eval      rkeste = c2este
     c                   eval      rkegln = c2dgln
     c                   eval      rkelev = c2ldor
     c                   time                    rkeeti
     c                   time                    rkeeda
     c                   eval      rkeeus = l_user
      *
     c                   if        %found
     c                   update    ra37iur
     c                   else
     c                   eval      rkefir = w_firm
     c                   eval      rkeela = c2elag
     c                   write     ra37iur
     c                   endif
      *
     c                   eval      b_forn = *on
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
     c                   eval      *in31  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av feilmelding
      * Kobling til selgende lager er lagt opp før
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2msg1       begsr
      *
     c                   exfmt     c2msg1
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av feilmelding
      * Ugyldig selgende lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2msg2       begsr
      *
     c                   exfmt     c2msg2
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av feilmelding
      * duplikat GLN på lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2msg3       begsr
      *
     c                   exfmt     c2msg3
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av feilmelding
      * duplikat GLN på distribusjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2msg4       begsr
      *
     c                   exfmt     c2msg4
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      ra37ir_ruid = b1uid
     c     ra37ir_key    chain     ra37ir
     c                   if        %found
     c                   eval      d1elag = rkeela
     c                   eval      d1eltx = rkenav
     c                   else
     c                   goto      end_d1win
     c                   endif
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
      * Sletter rad
      *
     c                   eval      ra37iu_ruid = b1uid
     c     ra37iu_key    chain     ra37iu
      *
     c                   if        %found
     c                   delete    ra37iu
     c                   endif
      *
     c                   eval      b_forn = *on
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      ra37i1
      *
     c                   if        %eof or
     c                             rkefir <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1uid  = rkeuid
     c                   eval      b1elag = rkeela
     c                   eval      b1eltx = rkenav
     c                   eval      b1dgln = rkegln
     c                   eval      b1dldo = rkelev
     c                   eval      w_ldor = rkelev
     c                   exsr      finn_lev
     c                   eval      b1dlna = w_letx
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     ra37i1_key    setgt     ra37i1
     c                   readp     ra37i1
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     ra37i1
      *
     c                   if        %eof or
     c                             rkefir <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   enddo
      *
     c                   if        %eof
     c     ra37i1_key    setll     ra37i1
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for leverandørsøk
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lev_sok       begsr
      *
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    w_ldor
     c                   parm                    w_letx
     c                   eval      c2ldor = w_ldor
     c                   eval      c2ltek = w_letx
      *
     c                   endsr

      //
      // Subrutine for finne leverandørnavn
      //
       begsr finn_lev;

         exec SQL
           select rlnavn into :w_letx
             from rlevpf
             where rlfirm = :w_firm
               and rllevr = :w_ldor;

         if sqlcod < 0 or
            sqlcod = 100;
           w_letx = '  ';
         endif;

       endsr;

      //
      // Subrutine for sjekke GLN duplikat lager
      //
       begsr sjk_glnl;

         exec SQL
           select mjlage into :m3lage
             from mjlopf
             where mjfirm = :w_firm
               and mjeann = :c2dgln;

         if sqlcod = 0;
           b_feil = *on;
         endif;

       endsr;

      //
      // Subrutine for sjekke GLN duplikat eksternt lager
      //
       begsr sjk_glnd;

         exec SQL
           select rkeela into :m4lage
             from ra37st
             where rkefir = :w_firm
               and rkegln = :c2dgln;

         if sqlcod = 0 and
            m4lage <> c2elag;
           b_feil = *on;
         endif;

       endsr;
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppdatering av eksternt lager (uid)
      *
     c     ra37iu_key    klist
     c                   kfld                    ra37iu_ruid
      *
      * Key for oppslag på eksternt lager  (uid)
      *
     c     ra37ir_key    klist
     c                   kfld                    ra37ir_ruid
      *
      * Key for posisjonering på eksternt lager
      *
     c     ra37i1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra37i1_elag
      *
      * Key for posisjonering på eksternt lager
      *
     c     ra37i2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra37i2_elag
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
     c                   eval      *in22 = *on
     c                   eval      ra37ir_ruid = 0
8.01 c                   eval      ra37i1_elag = ' '
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c     ra37i1_key    setll     ra37i1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
