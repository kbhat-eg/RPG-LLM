     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON
      * Program . . . . : RK716R
      * Beskrivelse . . : Overføring til SGFact-format
      *
      *
      * Spesialversjon. : faktoring mot SG Finans
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       281205 AMD  Nytt program
9.01  *  9.01 201113 AMD  Justering, nå ønskes også Poststed lagt ut i
9.01  *                   filen, ikke bare postnummer. Forutsetter at
9.01  *                   det kun er norske kunder som kjøres til
9.01  *                   factoring, og henter ut alt etter postnummer
9.02  *  9.02 240815 BKV  Lagt på landkode og valutakode
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fraa9l1    if   e           k disk    rename(raa9pfr:raa9l1r)
     frefal1    if   e           k disk    rename(refapfr:refal1r)
     frekul1    if   e           k disk    rename(rekupfr:rekul1r)
     fref2pf    uf a e             disk
     frek2pf    uf a e             disk
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av variable
      *
     d w_klie          s              4  0
     d w_kkus          s             24
     d w_ksif          s              1  0
      *
      * Definering av variabler i nøkler
      *
     d w_firm          s                   like(rnfirm)
      *
      * Datastruktur for fakturalinjer
      *
     d                 ds
     d d_frec_1                     256
     d  d_trsk_1                      1  0 overlay(d_frec_1:1)
     d  d_klie_1                      4  0 overlay(d_frec_1:2)
     d  d_kund_1                      9  0 overlay(d_frec_1:6)
     d  d_fakt_1                      8  0 overlay(d_frec_1:15)
     d  d_dato_1                      6  0 overlay(d_frec_1:23)
     d  d_forf_1                      6  0 overlay(d_frec_1:29)
     d  d_fbel_1                     11  2 overlay(d_frec_1:35)
     d* d_kidd_1                     16    overlay(d_frec_1:46)
      *
      * Datastruktur for kundetabell
      *
     d                 ds
     d d_krec_2                     256
     d  d_trsk_2                      1    overlay(d_krec_2:1)
     d  d_vers_2                      4  0 overlay(d_krec_2:2)
     d  d_klie_2                      4  0 overlay(d_krec_2:6)
     d  d_kund_2                      9  0 overlay(d_krec_2:10)
     d  d_ftak_2                     11  0 overlay(d_krec_2:19)
     d  d_knav_2                     35    overlay(d_krec_2:30)
     d  d_fil1_2                     20    overlay(d_krec_2:65)
     d  d_adr1_2                     30    overlay(d_krec_2:85)
     d  d_ponr_2                      4  0 overlay(d_krec_2:115)
     d  d_byby_2                     23    overlay(d_krec_2:119)
     d  d_kprs_2                     20    overlay(d_krec_2:142)
     d  d_tlff_2                     12    overlay(d_krec_2:162)
     d  d_fil2_2                     20    overlay(d_krec_2:174)
     d  d_land_2                      2    overlay(d_krec_2:194)
9.02 d  d_fil3_2                     53    overlay(d_krec_2:196)
9.02 d  d_valk_2                      3    overlay(d_krec_2:249)
      *
      * Datastruktur for kid-feltet
      *
      *  96834YYYYYYYY00Z
      *  YYYYYYYY = fakturanummer (8-siffer)
      *  Z = kontrollsiffer modulus 10
      *
     d                 ds
     d d_kkid                        16
     d  d_kkus                       15    overlay(d_kkid)
     d   d_fast                       1  0 overlay(d_kkus:1)
     d   d_ktvr                       4  0 overlay(d_kkus:2)
     d   d_kfak                       8  0 overlay(d_kkus:6)
     d   d_null                       2  0 overlay(d_kkus:14)
     d  d_kmod                        1  0 overlay(d_kkid:16)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Lese Kunde-transaksjoner
      *
     c                   read      refal1
     c                   dow       not %eof
      *
      * Oppdater faktura-tabell
      *
     c                   exsr      xopd_fakt
     c                   read      refal1
      *
     c                   enddo
      *
      * Lese Kunde-register
      *
     c                   read      rekul1
     c                   dow       not %eof
      *
      * Oppdater kunde-tabell
      *
     c                   exsr      xopd_kund
     c                   read      rekul1
      *
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdatering av fil for faktura-poster
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xopd_fakt     begsr
      *
      * Fakturaposter
      *
     c                   eval      d_klie_1 = w_klie
     c                   eval      d_kund_1 = rnkund
     c                   eval      d_fakt_1 = rnbiln
      *
      * Snu fortegn på beløp
      * dersom negativt
      *
     c                   if        rnbelØ   < 0
     c                   eval      d_fbel_1 = rnbelØ * -1
     c                   eval      d_trsk_1 = 9
     c                   else
     c                   eval      d_fbel_1 = rnbelØ
     c                   eval      d_trsk_1 = 1
     c                   endif
      *
      * Legge ut dato til 8 numerisk
      *
     c                   z-add     0             d_dato_1
     c                   if        rnbdat <> *loval
     c     *ymd          move      rnbdat        d_dato_1
     c                   endif
      *
     c                   z-add     0             d_forf_1
     c                   if        rnfdat <> *loval
     c     *ymd          move      rnfdat        d_forf_1
     c                   endif
      *
      * Legger ut felt for KID
      *
     c                   eval      d_fast = 9
     c                   eval      d_ktvr = w_klie
     c                   eval      d_kfak = rnbiln
     c                   eval      d_null = *zeros
      *
     c                   move      d_kkus        w_kkus
     c                   eval      w_ksif = 0
      *
      * Beregning av sjekksiffer på kiddfelt (lang versjon)
      *
     c                   call      'AK720R'
     c                   parm                    w_kkus
     c                   parm                    w_ksif
      *
     c                   eval      d_kmod = w_ksif
      *
      * Legger inn kiddnummer
      *
     c*                  eval      d_kidd_1 = d_kkid
      *
     c                   eval      refrec   = d_frec_1
      *
     c                   write     ref2pfr
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdatering av fil for kunde-tabell
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xopd_kund     begsr
      *
      * Kunderegister
      *
     c                   eval      d_trsk_2 = 'K'
9.01 c                   eval      d_vers_2 = 9409
     c                   eval      d_klie_2 = w_klie
     c                   eval      d_kund_2 = rkkund
     c                   eval      d_ftak_2 = 0
      *
     c                   if        rkftnr <>  0
     c                   eval      d_ftak_2 = rkftnr
     c                   endif
     >*                  if        rkpenr <>  0
     >*                  eval      d_ftak_2 = rkpenr
     >*                  endif
      *
     c                   eval      d_knav_2 = rknavn
     c                   eval      d_fil1_2 = *blank
     c                   eval      d_adr1_2 = rkgate
     c                   eval      d_ponr_2 = rkponr
9.01 c*                  eval      d_byby_2 = *blank
9.01 c                   eval      d_byby_2 = %subst(rksted:6:23)
     c                   eval      d_tlff_2 = rktlfn
     c                   eval      d_fil2_2 = *blank
9.02 c                   if        rkland <> ''
9.02 c                   eval      d_land_2 = rkland
9.02 c                   else
9.02 c                   eval      d_land_2 = 'NO'
9.02 c                   endif
9.02 c                   eval      d_fil3_2 = *blank
9.02 c                   if        rkvalk <> ''
9.02 c                   eval      d_valk_2 = rkvalk
9.02 c                   else
9.02 c                   eval      d_valk_2 = 'NOK'
9.02 c                   endif
     c
     c
     c
      *
     c                   movel     rkpers        d_kprs_2
     >*                  eval      d_adr2_2 = rksted
      *
     c                   eval      rekrec   = d_krec_2
      *
     c                   write     rek2pfr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for statustabell factoring
      *
     c     raa9l1_key    klist
     c                   kfld                    w_firm
      *
      * Key for kundetransaksjoner
      *
     c     refal1_key    klist
     c                   kfld                    w_firm
      *
      * Key for kunderegister
      *
     c     rekul1_key    klist
     c                   kfld                    w_firm
      *
      * Legge inn firmanummer
      *
     c                   eval      w_firm = l_firm
      *
      * Hent inn klientnummer
      *
     c     raa9l1_key    chain     raa9l1r
     c                   if        %found
     c                   move      ra9kli        w_klie
     c                   endif
      *
     c                   endsr
      *
