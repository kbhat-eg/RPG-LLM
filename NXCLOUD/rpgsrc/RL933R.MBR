      /TITLE  ASOKON Salderer leverandører med 0 i saldo
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * System. . . . . : ASOKON
      * Program . . . . : RL933R
      * Beskrivelse . . : Salderer leverandører med 0 i saldo
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
6.10  * V6.10 230909 BSA  Lagt inn sporingsfelter
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     frltrlu    uf   e           k disk    rename(rltrpfr:rltrlur)
      *
     d rltrlu_firm     s                   like(rofirm)
     d rltrlu_levr     s                   like(rolevr)
      *
     d w_mld1          c                   'Vennligst vent .........     '
     d w_mld2          c                   'Saldering pågår.........     '
     d wactli          s              2  0
     d wactpo          s              3  0
     d wmld01          s             30
     d wmld02          s             30
      *
      * Definering av LDA
      *
     d                uds
6.10 d l_user                911    920
      *
      * Send meldingsbildet
      *
     c                   eval      wactli = 4
     c                   eval      wactpo = 44
     c                   eval      wmld01 = w_mld1
     c                   eval      wmld02 = w_mld2
      *
     c                   call      'AA006C'
     c                   parm                    wactli
     c                   parm                    wactpo
     c                   parm                    wmld01
     c                   parm                    wmld02
      *
      * Leser alle leverandører
      *
     c                   read      rlevl1                                 90
     c                   dow       *in90 = *off
      *
     c                   if        rlsald = *zeros
     c                   exsr      poster
     c                   endif
      *
     c                   read      rlevl1                                 90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av poster
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     poster        begsr
      *
      * Leser leverandørposter
      *
     c                   eval      rltrlu_firm = rlfirm
     c                   eval      rltrlu_levr = rllevr
      *
     c     rltrlu_key    setll     rltrlur                                90
     c     rltrlu_key    reade     rltrlur                                90
      *
     c                   dow       *in90 = *off
      *
     c                   eval      rorest = *zeros
6.10 c                   time                    roedat
6.10 c                   time                    roetim
6.10 c                   eval      roeusr = l_user
     c                   update    rltrlur
      *
     c     rltrlu_key    reade     rltrlur                                90
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for leverandørposter
      *
     c     rltrlu_key    klist
     c                   kfld                    rltrlu_firm
     c                   kfld                    rltrlu_levr
      *
     c                   endsr
