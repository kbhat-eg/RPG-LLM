      /TITLE  Utskrift av varebeholdningsliste
     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : LL603R
      * Beskrivelse . . : Utskrift av varebeholdningsliste
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.
      * --------- ------  --------------------------------------------------
      *           020200  Nytt program                             ASK
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flwa1l1    if   e           k disk    rename(lwa1pfr:lwa1l1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
      *
     fll603p    o    e             printer
      *
      * Local Data Area
      *
     d                uds
     d l_wsid                901    910
     d l_user                        10
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      *  Parameter-rec
      *
     d                 ds
     d p_prec                       128
     d  p_fvar                        8  0 overlay(p_prec)
     d  p_tvar                        8  0 overlay(p_prec:9)
     d  p_flag                        2  0 overlay(p_prec:17)
     d  p_tlag                        2  0 overlay(p_prec:19)
     d  p_fogr                        2  0 overlay(p_prec:21)
     d  p_togr                        2  0 overlay(p_prec:23)
     d  p_fhgr                        2  0 overlay(p_prec:25)
     d  p_thgr                        2  0 overlay(p_prec:27)
     d  p_fugr                        3  0 overlay(p_prec:29)
     d  p_tugr                        3  0 overlay(p_prec:32)
     d  p_levn                        6  0 overlay(p_prec:35)
     d  p_sakb                        6  0 overlay(p_prec:41)
     d  p_sort                        1  0 overlay(p_prec:45)
     d  p_brud                        1  0 overlay(p_prec:46)
      *
      * Definering av variable i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
      *
      * Definering av variable
      *
     d w_parm          s            128
     d w_firm          s                   like(vvfirm)
     d w_dato          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_vare          s              8  0
     d w_lage          s                   like(mllage)
     d w_oogr          s                   like(vgoogr)
     d w_hhgr          s                   like(vghhgr)
     d w_uugr          s                   like(vguugr)
     d w_adrs          s             30
     d w_ponr          s              4  0
     d w_pste          s             30
     d w_avde          s              4  0
     d w_tell          s              3  0
      *
     d b_feil          s              1    inz(*off)
     d b_init          s              1    inz(*on)
      *
      * - - - - - - - - - -
      * Benyttede formater
      * - - - - - - - - - -
      *
      * A1HDR  - Heading A1 på rapport
      * A2HDR  - Lager heading
      * A3HDR  - Overgruppe heading
      * A4HDR  - Hovedgruppe heading
      * A5HDR  - Undergruppe heading
      * D1DET  - Detail  D1 på rapport
      *
      * Skriv første heading
      *
     c                   write     a1hdr                                30
      *
      * Lese file
      *
     c                   eval      w_lage = *loval
     c                   eval      w_oogr = *loval
     c                   eval      w_hhgr = *loval
     c                   eval      w_uugr = *loval
      *
     c                   read      lwa1l1                                 90
      *
     c                   dow       *in90 = *off
      *
      *  Brudd på varenummer ?
      *
     c                   if        p_brud  = 0 and
     c                             mlvare <> w_vare
     c                   exsr      sum_t1
     c                   endif
      *
      * Brudd på lager ?
      *
     c                   if        p_sort  > 2 and
     c                             mllage <> w_lage
     c                   exsr      hed_a2
     c                   endif
      *
      * Sjekk om sortert på varegruppe
      *
     c                   if        p_sort = 2
     c                             or p_sort = 4
      *
      * Brudd på overgruppe ?
      *
     c                   if        mlogrp <> w_oogr
     c                   exsr      hed_a3
     c                   endif
      *
      * Brudd på hovedgruppe ?
      *
     c                   if        mlhgrp <> w_hhgr
     c                   exsr      hed_a4
     c                   endif
      *
      * Brudd på undergruppe ?
      *
     c                   if        mlugrp <> w_uugr
     c                   exsr      hed_a5
     c                   endif
      *
     c                   endif
      *
      * Utskrift av detaljer
      *
     c                   movel     mlvare        d1vare
     c                   movel     mlvare        vvarl1_vare
     c     vvarl1_key    chain     vvarl1                             91
     c                   if        *in91 = *off
     c                   eval      d1tek1 = vvtek1
     c                   else
     c                   eval      d1tek1 = 'Ukjent vare'
     c                   endif
      *
     c                   eval      d1enhl = vvenhl
     c                   eval      d1lage = mllage
     c                   eval(h)   d1hiin = mlinng
     c                   eval(h)   d1hisa = mlsalg + mlutta
     c                   eval(h)   d1hiju = mljust
     c                   eval(h)   d1behl = mlbehl
     c                   eval(h)   d1beve = mlbeve
     c                   eval(h)   d1iord = mlpakk + mloord
     c                   eval(h)   d1ibes = mlbbes
      *
     c                   write     d1det                                30
     c                   if        *in30 = *on
     c                   exsr      overflow
     c                   endif
      *
      * Summer til total
      *
     c                   if        p_brud = 0
     c                   eval      w_tell = w_tell + 1
     c                   eval      w_vare = mlvare
     c                   eval      t1hiin = t1hiin + d1hiin
     c                   eval      t1hisa = t1hisa + d1hisa
     c                   eval      t1hiju = t1hiju + d1hiju
     c                   eval      t1behl = t1behl + d1behl
     c                   eval      t1beve = t1beve + d1beve
     c                   eval      t1iord = t1iord + d1iord
     c                   eval      t1ibes = t1ibes + d1ibes
     c                   endif
      *
     c                   read      lwa1l1                                 90
      *
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Skriv sum varenummer (Flere lager)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sum_t1        begsr
      *
     c                   if        w_tell > 1
     c                             and p_sort < 3
     c                   write     t1sum                                30
     c                   endif
      *
     c                   eval      t1hiin = 0
     c                   eval      t1hisa = 0
     c                   eval      t1hiju = 0
     c                   eval      t1behl = 0
     c                   eval      t1beve = 0
     c                   eval      t1iord = 0
     c                   eval      t1ibes = 0
     c                   eval      w_tell = 0
      *
     c     end_t1        endsr
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Brudd på lager?
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hed_a2        begsr
      *
     c                   eval      w_lage = mllage
     c                   if        p_sort < 3
     c                   goto      end_a2
     c                   endif
      *
      *
     c                   eval      a2lage = mllage
      *
     c                   call      'FÅ720R'
     c                   parm                    w_firm
     c                   parm                    mllage
     c                   parm                    a2ltek
     c                   parm                    w_adrs
     c                   parm                    w_ponr
     c                   parm                    w_pste
     c                   parm                    w_avde
     c                   parm                    b_feil
      *
     c                   if        b_feil = *on
     c                   eval      a2ltek = 'Ukjent lager'
     c                   endif
      *
     c                   if        b_init = *off
     c                   exsr      overflow
     c                   else
     c                   eval      b_init = '0'
     c                   endif
     c                   write     a2hdr                                30
      *
     c     end_a2        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Brudd på overgruppe ?
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hed_a3        begsr
      *
     c                   eval      w_oogr = mlogrp
     c                   eval      w_hhgr = *loval
     c                   eval      w_uugr = *loval
      *
     c                   eval      vogrl1_oogr = mlogrp
     c     vogrl1_key    chain     vogrl1                             92
     c                   eval      a3ogrp = mlogrp
     c                   if        *in92 = *off
     c                   eval      a3ogtx = vgotxt
     c                   else
     c                   eval      a3ogtx = 'Ukjent varegruppe'
     c                   endif
      *
     c                   write     a3hdr                                30
     c                   if        *in30 = *on
     c                   exsr      overflow
     c                   endif
      *
     c     end_a3        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Brudd på hovedgruppe ?
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hed_a4        begsr
      *
     c                   eval      w_hhgr = mlhgrp
     c                   eval      w_uugr = *loval
      *
     c                   eval      vhgrl1_hogr = mlogrp
     c                   eval      vhgrl1_hhgr = mlhgrp
     c     vhgrl1_key    chain     vhgrl1                             93
     c                   eval      a4hgrp = mlhgrp
     c                   if        *in93 = *off
     c                   eval      a4hgtx = vghtxt
     c                   else
     c                   eval      a4hgtx = 'Ukjent varegruppe'
     c                   endif
      *
     c                   write     a4hdr                                30
     c                   if        *in30 = *on
     c                   exsr      overflow
     c                   endif
      *
     c     end_a4        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Brudd på undergruppe ?
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hed_a5        begsr
      *
     c                   eval      w_uugr = mlugrp
      *
     c                   eval      vugrl1_uogr = mlogrp
     c                   eval      vugrl1_uhgr = mlhgrp
     c                   eval      vugrl1_uugr = mlugrp
     c     vugrl1_key    chain     vugrl1                             94
     c                   eval      a5ugrp = mlugrp
     c                   if        *in94 = *off
     c                   eval      a5ugtx = vgutxt
     c                   else
     c                   eval      a5ugtx = 'Ukjent varegruppe'
     c                   endif
      *
     c                   write     a5hdr                                30
     c                   if        *in30 = *on
     c                   exsr      overflow
     c                   endif
      *
     c     end_a5        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  OVFLOW    Behandling av overflow
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     overflow      begsr
      *
      *  Skriver heading
      *
     c                   write     a1hdr                                30
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for les av vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for les av overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for les av overgruppe/hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for les av overgruppe/hovedgruppe/undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    w_parm
     c                   eval      p_prec = w_parm
      *
     c                   time                    w_dato
     c                   time                    w_time
     c                   eval      w_firm = l_firm
      *
      * Redigere heading
      *
     c     *dmy          move      w_dato        a1date
     c     *hms          move      w_time        a1time
     c                   eval      a1user = l_user
     c                   eval      a1wsid = l_wsid
     c                   eval      a1firm = l_firm
     c                   eval      a1fnav = l_fnav
      *
     c                   eval      *in71  = p_sort = 1
     c                   eval      *in72  = p_sort = 2
     c                   eval      *in73  = p_sort = 3
     c                   eval      *in74  = p_sort = 4
      *
     c                   endsr
