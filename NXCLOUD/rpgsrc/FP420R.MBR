     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FP420R
      * Beskrivelse . . : Vedlikehold av etikettinformasjon, varer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       161103 KOB  Lagt inn lager i etikettregisteret
      * V5.30 130304 KOB  Rettet feil vedr sletting av post m/lager <> 0
      * V5.30 271004 KOB  Rettet feil vedr oppslag på vare uten lager
      * V5.50 280906 KOB  Fjernet styring av prisenhet 2
      * 6.10  260609 BSA  Oppdatert med sporingsinformasjon
6.11  * 6.10  140710 BSA  "Ryddet" i vedlikeholdet. Lagt inn lager og siste
6.11  *                   utskrevet pris og GTIN nr. Vedlikeholdet benytter
6.11  *                   nå lager fra subfilen, og ikke fra lda.
6.12  * 6.10  070212 BSA  "Pris 2 hentes fra" vedlikeholdes ikke
7.xx  * 7.00  090316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvetil1    if   e           k disk    rename(vetipfr:vetil1r)
     fvetilr    if   e           k disk    rename(vetipfr:vetilrr)
     fvetilu    uf a e           k disk    rename(vetipfr:vetilur)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
6.11 fra10l1    if   e           k disk    rename(ra10pfr:ra10l1r)
      *
     ffp420d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
     d l_lage               1001   1002  0
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d vetil1_lage     s                   like(vmlage)
     d vetil1_vare     s                   like(vmvare)
     d vetilr_lage     s                   like(vmlage)
6.11 d ra10l1_lage     s                   like(vmlage)
     d vetilr_vare     s                   like(vmvare)
     d vetilu_lage     s                   like(vmlage)
     d vetilu_vare     s                   like(vmvare)
     d vvarl1_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(vmfirm)
     d w_vare          s                   like(vvvare)
     d w_lage          s                   like(vmlage)
     d w_tek1          s                   like(vvtek1)
6.11 d w_txt1          s             20
      *
     d s_vare          s                   like(vmvare)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(13)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meleingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2tagb
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2vare <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      vetil1_lage = w_lage
     c                   movel     b1vare        vetil1_vare
     c     vetil1_key    setll     vetil1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Vare
      *
     c                   if        b2vare <> 0
     c                   eval      w_seqe = *blank
     c                   eval      vetil1_lage = w_lage
     c                   movel     b2vare        vetil1_vare
     c     vetil1_key    setll     vetil1
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2vare = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1vare = b1vare
6.11 c                   eval      c1lage = b1lage
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1vare = b1vare
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1vare = b1vare
     c                   eval      d1lage = b1lage
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1vare = b1vare
6.11 c                   eval      c1lage = b1lage
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1vare = b2vare
     c                   eval      c1tek1 = *blank
6.11 c                   eval      c1lage = l_lage
6.11 c                   eval      c1jtxt = *blank
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      c1taga
     c                   endsl
      *
     c                   if        c1vare = 0
     c                   goto      end_c1bld
     c                   endif
      *
     c                   movel     c1vare        vvarl1_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   eval      c1tek1 = vvtek1
     c                   else
     c                   eval      *in33 = *on
     c                   goto      c1taga
     c                   endif
6.11  * Sjekk at det er gyldig lager
6.11 c                   eval      ra10l1_lage = c1lage
6.11 c     ra10l1_key    chain     ra10l1
6.11 c                   if        %found
6.11 c                   eval      c1jtxt = rajtxt
6.11 c                   else
6.11 c                   eval      *in34 = *on
6.11 c                   goto      c1taga
6.11 c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
6.11 c                   eval      vetilr_lage = c1lage
6.11 c**                 eval      vetilr_lage = w_lage
     c                   movel     c1vare        vetilr_vare
     c     vetilr_key    chain     vetilr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1vare = 0
     c                   eval      c1tek1 = *blank
6.11 c                   eval      c1lage = *zero
6.11 c                   eval      c1jtxt = *blank
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2vare = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
6.11 c                   eval      vetilr_lage = c1lage
6.11 c**                 eval      vetilr_lage = b1lage
     c                   movel     c1vare        vetilr_vare
     c     vetilr_key    chain     vetilr
      *
     c                   if        %found
     c                   eval      c2etyp = vmetyp
     c                   eval      c2etpr = vmetpr
     c                   eval      c2fen1 = vmfen1
6.12 c                   eval      c2fen2 = vmfen2
6.11 c                   eval      c2sapr = vmsapr
6.11 c                   eval      c2gtin = vmgtin
     c                   eval      c2sdat = 0
     c                   eval      c2tdat = 0
     c                   if        vmsdat <> *loval
     c     *dmy          move      vmsdat        c2sdat
     c                   endif
     c                   if        vmtdat <> *loval
     c     *dmy          move      vmtdat        c2tdat
     c                   endif
     c                   else
     c                   eval      c2etyp = 0
     c                   eval      c2etpr = 0
     c                   eval      c2fen1 = 0
6.12 c                   eval      c2fen2 = 0
     c                   eval      c2sdat = 0
     c                   eval      c2tdat = 0
6.11 c                   eval      c2sapr = 0
6.11 c                   eval      c2gtin = 0
     c                   endif
      *
     c                   movel     c1vare        vvarl1_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   eval      c1tek1 = vvtek1
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1vare = k1vare
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
6.11  *
6.11 c                   eval      ra10l1_lage = c1lage
6.11 c     ra10l1_key    chain     ra10l1
6.11 c                   if        %found
6.11 c                   eval      c1jtxt = rajtxt
6.11 c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   vetilur
      *
6.11 c**                 eval      vetilu_lage = b1lage
6.11 c                   eval      vetilu_lage = c1lage
     c                   movel     c1vare        vetilu_vare
     c     vetilu_key    chain     vetilur
      *
     c                   eval      vmetyp = c2etyp
     c                   eval      vmetpr = c2etpr
     c                   eval      vmfen1 = c2fen1
6.12 c                   eval      vmfen2 = c2fen2
     c                   eval      vmsdat = *loval
     c                   eval      vmtdat = *loval
      *
     c                   if        c2sdat <> 0
     c     *dmy          move      c2sdat        vmsdat
     c                   endif
     c                   if        c2tdat <> 0
     c     *dmy          move      c2tdat        vmtdat
     c                   endif
      *
6.10 c                   time                    vmetim
6.10 c                   time                    vmedat
6.10 c                   eval      vmeusr = l_user
      *
     c                   if        %found
     c                   update    vetilur
     c                   else
     c                   eval      vmfirm = w_firm
6.11 c**                 eval      vmlage = w_lage
6.11 c                   eval      vmlage = c1lage
     c                   movel     c1vare        vmvare
6.10 c                   time                    vmotim
6.10 c                   time                    vmodat
6.10 c                   eval      vmousr = l_user
     c                   write     vetilur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1vare = c1vare
     c                   eval      b1etyp = c2etyp
     c                   eval      b1etpr = c2etpr
     c                   eval      b1fen1 = c2fen1
6.12 c                   eval      b1fen2 = c2fen2
     c                   eval      b1sdat = c2sdat
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      vetilr_lage = d1lage
     c                   movel     d1vare        vetilr_vare
     c     vetilr_key    chain     vetilr
      *
     c                   if        vmsdat <> *loval
     c     *dmy          move      vmsdat        d1sdat
     c                   else
     c                   eval      d1sdat = 0
     c                   endif
      *
     c                   eval      d1tek1 = b1tek1
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      vetilu_lage = d1lage
     c                   movel     d1vare        vetilu_vare
     c     vetilu_key    chain     vetilur
      *
     c                   if        %found
     c                   delete    vetilur
     c                   endif
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   movel     k1vare        s_vare
      *
      * Fyller kopierings-bildet
      *
     c                   eval      vetilr_lage = w_lage
     c                   movel     k1vare        vetilr_vare
     c     vetilr_key    chain     vetilr
      *
     c                   eval      vvarl1_vare = vmvare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   eval      k1tek1 = vvtek1
     c                   endif
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_k1win
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      k1taga
     c                   endsl
      *
      * Sjekk om utfylt
      *
     c                   if        k1vare = 0
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      vetilr_lage = w_lage
     c                   movel     k1vare        vetilr_vare
     c     vetilr_key    chain     vetilr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekk om vare finnes
      *
     c                   movel     k1vare        vvarl1_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   eval      *in33 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   movel     s_vare        c1vare
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      vetil1
      *
     c                   if        %eof or
     c                             vmfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      vetil1_vare = vmvare
      *
     c                   eval      b1valg = 0
     c                   eval      b1vare = 0
     c                   eval      b1lage = vmlage
     c                   eval      b1tek1 = *blank
     c                   eval      b1etyp = vmetyp
     c                   eval      b1etpr = vmetpr
     c                   eval      b1fen1 = vmfen1
     c                   eval      b1fen2 = vmfen2
     c                   eval      b1sdat = 0
      *
     c                   movel     vmvare        b1vare
      *
     c                   if        vmsdat <> *loval
     c     *dmy          move      vmsdat        b1sdat
     c                   endif
      *
     c                   eval      vvarl1_vare = vmvare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   eval      b1tek1 = vvtek1
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     vetil1_key    setgt     vetil1
     c                   readp     vetil1
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     vetil1
      *
     c                   if        %eof or
     c                             vmfirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   eval      vetil1_vare = vmvare
      *
     c                   enddo
      *
     c                   if        %eof
     c     vetil1_key    setll     vetil1
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
     c                   if        w_afld = 'B2VARE'
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_tek1
     c                   movel     w_vare        b2vare
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'C1VARE'
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    c1tek1
     c                   movel     w_vare        c1vare
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'K1VARE'
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_tek1
     c                   movel     w_vare        k1vare
     c                   goto      end_spØrr
     c                   endif
6.11  *
6.11 c                   if        w_afld = 'C1LAGE'
6.11 c                   call      'RA510R'
6.11 c                   parm                    w_firm
6.11 c                   parm                    c1lage
6.11 c                   parm                    w_txt1
6.11 c                   eval      c1jtxt = w_txt1
6.11 c                   goto      end_spØrr
6.11 c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på vare
      *
     c     vetil1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vetil1_vare
     c                   kfld                    vetil1_lage
      *
      * Key for les på etikettinformasjon
      *
     c     vetilr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vetilr_vare
     c                   kfld                    vetilr_lage
      *
      * Key for oppdatering av etikettinformasjon
      *
     c     vetilu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vetilu_vare
     c                   kfld                    vetilu_lage
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
6.11  *
6.11  * Key for oppslag på lagerregister
6.11  *
6.11 c     ra10l1_key    klist
6.11 c                   kfld                    w_firm
6.11 c                   kfld                    ra10l1_lage
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_lage = l_lage
      *
      * Initierer skjermbildet
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      vetil1_lage = w_lage
     c                   eval      vetil1_vare = *blank
     c     vetil1_key    setll     vetil1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
