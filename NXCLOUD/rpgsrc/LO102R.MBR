     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LO102R
      *  Beskrivelse . . : Registrering av Best/Innkjøp,  Opplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      *  --------- ------  ------------------------------------------------
      *   6.10     180609  Oppdatert med sporingsinformasjon       BSA
6.11  *   6.11     220911  Leveringsmåtetekst kom ikke med         AMD
6.30  *   6.30     160214  Validering av leveringsmåte ifølge AVALPF  BOF
6.31  *   6.30     290314  Ny kode for forsendelsesmåte bestilling  bof
6.nu  *   6.30     280514  Utvidet mtp. nummerutvidelse             bof
7.xx  *   7.00     020617  Rekompilert                              bkv
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flohelu    uf   e           k disk    rename(lohepfr:lohelur)
6.30 favall1    if   e           k disk    rename(avalpfr:avall1r)
     flo102d    cf   e             workstn
      *
      * Local Data Area
      *
     d                uds
6.10 d  l_user               911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variable i parametre
      *
     d p_stat          s              1
     d p_firm          s                   like(lofirm)
     d p_numm          s                   like(lonumm)
     d p_suff          s                   like(losuff)
      *
      * Definering av variable i nøkler
      *
     d lohel1_numm     s                   like(lonumm)
     d lohel1_suff     s                   like(losuff)
     d lohelu_numm     s                   like(lonumm)
     d lohelu_suff     s                   like(losuff)
6.30 d avall1_apgm     s                   like(avapgm)
      *
      * Definering av variable
      *
     d w_firm          s                   like(lofirm)
     d w_lage          s                   like(lolage)
     d w_text          s             20
     d w_adrs          s             30
     d w_ponr          s              4  0
     d w_palf          s              4
     d w_pste          s             30
     d w_avde          s              4  0
     d w_feil          s              1
     d w_itxt          s                   like(d1intx)
     d w_qtxt          s             20
     d w_retk          s              1
     d w_kode          s              2  0
      *
6.30  * Firmastyrt validering av felt
6.30  *
6.30 d u_lmat          s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  D1 Behandler vedlikehold av faste opplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     d1taga        tag
      *
     c                   eval      d1latx = *blank
     c                   eval      d1lmtx = *blank
      *
      *  Hent opplysninger fra BESTILLING-HODE-REGISTER
      *
     c     lohel1_key    chain     lohel1r
B001 c                   if        %found
 001 c                   eval      d1innk = loinnk
 001 c                   eval      d1lage = lolage
 001 c                   eval      w_lage = lolage
 001 c                   eval      d1lmat = lolmat
 001 c                   movel     lolmtx        d1lmtx
E001 c                   endif
      *
      *  Hent,   Tekst for lager, tekst for innkjøper
6.11  *          og tekst for forsendelsesmåte
      *
     c                   exsr      sblagr
     c                   exsr      sbinnk
6.11 c                   exsr      sbform
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Behandler skjermbilde  D1
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Send alt
      *
     c     d1tagb        tag
     c                   exfmt     d1bld
      *
      *  F3=Gå tilbake til forrige bilde
      *
B001 c                   if        *inkc = *on
 001 c                   eval      p_stat = '9'
 001 c                   goto      end_pgm
E001 c                   endif
      *
      *  F4=Spørring
      *
B001 c                   if        *inkd = *on
 001 c                   exsr      sp_input
 001 c                   goto      d1tagb
E001 c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Behandler sjekking av opplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Sjekk,  Finnes innkjøper i KODE-REGISTER
      *
     c                   eval      d1intx = *blank
B001 c                   if        d1innk <> 0
 001 c                   exsr      sbinnk
B002 c                   if        w_retk <> *blank
 002 c                   move      *on           *in33
 002 c                   goto      d1tagb
E002 c                   endif
E001 c                   endif
      *
      *  Sjekk,  Finnes lager i KODE-REGISTER
      *
     c                   exsr      sblagr
B001 c                   if        w_feil = *on
 001 c                   eval      *in34 = *on
 001 c                   goto      d1tagb
E001 c                   endif
      *
6.30  * Sjekk,  Er det krav til Leveringsmåte
6.30 c                   if        u_lmat = *on and
6.30 c                             d1lmat =  0
6.30 c                   eval      *in38 = *on
6.30 c                   goto      d1tagb
6.30 c                   endif
6.30  *
      *
      *  Sjekk,  Finnes forsendelsesmåtekode i KODE-REGISTER
      *
B001 c                   if        d1lmat <> *zero
6.30  *                  if        d1lmat <> lolmat
 002 c                   exsr      sbform
B003 c                   if        w_retk <> *blank
 003 c                   eval      *in38 = *on
 003 c                   goto      d1tagb
E003 c                   endif
6.30 c*                  endif
E001 c                   endif
      *
      *  Dersom noen valg er endret, sendt bilde pånytt
      *
B001 c                   if        d1lage <> lolage
 001 c                             or d1lmat <> lolmat
 001 c                   eval      lolage = d1lage
 001 c                   eval      lolmat = d1lmat
 001 c                   goto      d1tagb
E001 c                   endif
      *
      *  Oppdaterer BESTILLING-HODE-REGISTER
      *
     c     lohelu_key    chain     lohelur
B001 c                   if        %found
 001 c                   eval      loinnk = d1innk
 001 c                   eval      lolage = d1lage
 001 c                   eval      lolmat = d1lmat
 001 c                   movel     d1lmtx        lolmtx
 001  *
 001  * Oppdaterer leveringsadresse med lager dersom
 001  * ingen ting annet ligger i leveringsadresse.
 001  *
B002 c                   if        lonavn = *blank
 002 c                   eval      lonavn = w_text
 002 c                   eval      loadr1 = w_adrs
 002 c                   move      w_ponr        w_palf
 002 c                   eval      losted = %trim(w_palf + ' ' + w_pste)
E002 c                   endif
 001  *
6.10 c                   time                    loedat
6.10 c                   time                    loetim
6.10 c                   eval      loeusr = l_user
 001 c                   update    lohelur
E001 c                   endif
      *
      * Endre lagerbeholdninger dersom lager er endret
      *
B001 c                   if        lolage <> w_lage
 001 c                   call      'LO732R'
 001 c                   parm                    w_firm
 001 c                   parm                    lonumm
 001 c                   parm                    losuff
 001 c                   parm                    lootyp
 001 c                   parm                    w_lage
E001 c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Spørring på inputfelter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sp_input      begsr
      *
      *  Spørring på innkjøper (selger)
      *
     c                   if        w_afld = 'D1INNK'
     c                   call      'RA509R'
     c                   parm                    w_firm
     c                   parm                    d1innk
     c                   parm                    d1intx
     c                   goto      end_sp
     c                   endif
      *
      *  Spørring på lager
      *
     c                   if        w_afld = 'D1LAGE'
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    d1lage
     c                   parm                    w_text
     c                   movel     w_text        d1latx
     c                   eval      lolage = d1lage
     c                   goto      end_sp
     c                   endif
      *
      *  Spørring på forsendelsemåte
      *
     c                   if        w_afld = 'D1LMAT'
6.30 c                   call      'RA533R'
     c                   parm                    w_firm
     c                   parm                    d1lmat
     c                   parm                    w_text
     c                   movel     w_text        d1lmtx
     c                   eval      lolmat = d1lmat
     c                   goto      end_sp
     c                   endif
      *
     c     end_sp        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sjekk/Hent tekst for innkjøper
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sbinnk        begsr
      *
     c                   call      'RS709R'
     c                   parm                    w_retk
     c                   parm                    d1innk
     c                   parm                    w_itxt
      *
     c                   eval      d1intx = *blank
      *
B001 c                   if        w_retk = *blank
 001 c                   movel     w_itxt        d1intx
E001 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sjekk/Hent tekst for lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sblagr        begsr
      *
     c                   eval      p_firm = p_firm
     c                   eval      w_kode = d1lage
      *
     c                   call      'FÅ720R'
     c                   parm                    w_firm
     c                   parm                    w_kode
     c                   parm                    w_text
     c                   parm                    w_adrs
     c                   parm                    w_ponr
     c                   parm                    w_pste
     c                   parm                    w_avde
     c                   parm                    w_feil
      *
     c                   eval      d1latx = *blank
      *
B001 c                   if        w_feil = *off
 001 c                   eval      d1latx = w_text
E001 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sjekk/Hent tekst for forsendelsesmåte
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sbform        begsr
      *
     c                   eval      w_retk = *blank
      *
6.31 c                   call      'RS731R'
     c                   parm                    w_retk
     c                   parm                    d1lmat
     c                   parm                    w_qtxt
      *
     c                   eval      d1lmtx = *blank
      *
B001 c                   if        w_retk = *blank
 001 c                   movel     w_qtxt        d1lmtx
E001 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Tar inn parameter fra forrige program
      *
     c     *entry        plist
     c                   parm                    p_stat
     c                   parm                    p_firm
     c                   parm                    p_numm
     c                   parm                    p_suff
      *
      *  Key for les av BESTILLING-HODE
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
     c                   kfld                    lohel1_suff
      *
      *  Key for oppdatering av BESTILLING-HODE
      *
     c     lohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelu_numm
     c                   kfld                    lohelu_suff
6.30  *
6.30  * Key for file : Firmastyrt validering
6.30  *
6.30 c     avall1_key    klist
6.30 c                   kfld                    w_firm
6.30 c                   kfld                    avall1_apgm
      *
      *  Legger inn verdier i nøklene
      *
     c                   eval      w_firm = p_firm
     c                   eval      lohel1_numm = p_numm
     c                   eval      lohel1_suff = p_suff
     c                   eval      lohelu_numm = p_numm
     c                   eval      lohelu_suff = p_suff
6.30  *
6.30  * Firmastyrt validering av utvalgte felter
6.30  *
6.30  *
6.30 c                   eval      avall1_apgm = 'LO102R'
6.30 c     avall1_key    setll     avall1
6.30 c     avall1_key    reade     avall1
6.30 c                   dow       not %eof(avall1)
6.30 c                   if        avaflt = 'LOLMAT'  and
6.30 c                             avaval = 1
6.30 c                   eval      u_lmat = *on
6.30 c                   endif
6.30 c     avall1_key    reade     avall1
6.30 c                   enddo
      *
     c                   endsr
