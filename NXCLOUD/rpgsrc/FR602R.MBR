     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FR602R
      * Beskrivelse . . : Utskrift av rabattmatrise, matrise
      *
      *                   NB! Programmet skal kjøres i batch !!
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       090599 HKO  Nytt program
      *       200300 HKO  Lagt inn leverandør som nivå i rabattmatrisen
      *                   Endret testing av input
      *       170204 HKO  Erstattet rabattgruppe med varegruppe og modul
5.70  * PRGR  211008 bof  Utvidet med prisgruppe                       l
5.71  *       080210 bof  Prisgruppe ved kall til FR700R               l
      * 6.10  250609 BSA  Oppdatert med sporingsinformasjon
8.01  *PRG2   100622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffrabl1    if   e           k disk    rename(frabpfr:frabl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     ffrawlu    uf a e           k disk    rename(frawpfr:frawlur)
     ffrawl1    if   e           k disk    rename(frawpfr:frawl1r)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
      *
     ffr602p    o    e             printer
      *
      * Definering av parametre
      *
     d p_list          s            120
      *
      * Defineringa av LDA
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
      *
      * Datastruktur for parameterliste -inn
      *
     d                 ds
     d d_list                       120
     d  d_firm                        3  0 overlay(d_list:  1)
     d  d_rkaf                        3  0 overlay(d_list:  4)
     d  d_rkat                        3  0 overlay(d_list:  7)
     d  d_kkaf                        4  0 overlay(d_list: 10)
     d  d_kkat                        4  0 overlay(d_list: 14)
     d  d_kunf                        6  0 overlay(d_list: 18)
     d  d_kunt                        6  0 overlay(d_list: 24)
     d  d_kprf                        6  0 overlay(d_list: 30)
     d  d_kprt                        6  0 overlay(d_list: 36)
     d  d_vgrf                        7  0 overlay(d_list: 42)
     d   d_ogrf                       2  0 overlay(d_list: 42)
     d   d_hgrf                       2  0 overlay(d_list: 44)
     d   d_ugrf                       3  0 overlay(d_list: 46)
     d  d_vgrt                        7  0 overlay(d_list: 49)
     d   d_ogrt                       2  0 overlay(d_list: 49)
     d   d_hgrt                       2  0 overlay(d_list: 51)
     d   d_ugrt                       3  0 overlay(d_list: 53)
     d  d_ldof                        6  0 overlay(d_list: 56)
     d  d_ldot                        6  0 overlay(d_list: 62)
     d  d_modf                        8  0 overlay(d_list: 68)
     d  d_modt                        8  0 overlay(d_list: 76)
     d  d_varf                       15    overlay(d_list: 84)
     d  d_vart                       15    overlay(d_list: 99)
8.01 d  d_prgr                        2    overlay(d_list:114)
      *
      * Definering av variable i nøkler
      *
     d rkunl1_kund     s                   like(rkkund)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
     d vvarl1_vare     s                   like(vvvare)
5.70 d frawl1_prgr     s                   like(frprgr)
     d frawl1_rkat     s                   like(frrkat)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d rlevl1_levr     s                   like(rllevr)
      *
      * Definering av variable
      *
     d b_avsl_les      s              1    inz(*off)
     d b_oflw          s              1    inz(*off)
     d b_inlr          s              1    inz(*on)
     d b_null          s              1    inz(*on)
      *
     d w_udat          s               d   datfmt(*iso)
     d w_utim          s               t   timfmt(*iso)
     d w_vgrp          s              7  0
      *
     d w_firm          s                   like(frfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser rabattmatrisen, og kopierer denne over til arbeidsfil
      * sammen med tilleggsopplysninger
      *
     c                   exsr      utplukk
      *
      * Skriver Header
      *
     c                   exsr      hode
      *
      * Leser arbeidstabell, og skriver detaljlinjer
      *
5.70 c                   eval      frawl1_prgr = d_prgr
     c                   eval      frawl1_rkat = d_rkaf
     c     frawl1_key    setll     frawl1
     c                   read      frawl1
     c                   dow       not %eof  and
     c                             b_avsl_les = *off
     c                   exsr      detalj
     c                   read      frawl1
     c                   enddo
      *
      * Avslutter program
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp arbeidsfil.
      * Hele rabattmatrisen kopieres til arbeidsfil, og kompletteres med
      * rabattkategori for kunde/kundeprosjekt og med rabattgruppe for vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     utplukk       begsr
      *
     c     frabl1_key    setll     frabl1
     c     frabl1_key    reade     frabl1
     c                   dow       not %eof
      *
      * Behandling av kunde/kundeprosjekt
      *
     c                   if        frkund <> 0 or
     c                             frkpro <> 0
      *
      * Henter eventuell rabattkategori fra kundeprosjekt
      *
     c                   eval      frrkat = 0
     c                   if        frkpro <> 0
     c                   eval      fkprl1_kund = frkund
     c                   eval      fkprl1_kpro = frkpro
     c     fkprl1_key    chain     fkprl1
     c                   if        %found and
     c                             florka <> 0
     c                   eval      frrkat = florka
     c                   endif
     c                   endif
      *
      * Henter rabattkategori og kundekategori fra kunde
      *
     c                   eval      rkunl1_kund = frkund
     c     rkunl1_key    chain     rkunl1
     c                   if        %found
     c                   if        frrkat = 0
     c                   eval      frrkat = rkrabk
     c                   endif
     c                   eval      frkkat = rkkatg
     c                   endif
      *
     c                   endif
      *
      * Henter varegruppe, leverandør og modul fra vare
      *
     c                   if        frvare <> *blank
     c                   eval      vvarl1_vare = frvare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   eval      frogrp = vvogrp
     c                   eval      frhgrp = vvhgrp
     c                   eval      frugrp = vvugrp
     c                   eval      frldor = vvldor
     c                   eval      frmodn = vvnmod
     c                   endif
     c                   endif
      *
6.10 c                   time                    frodat
6.10 c                   time                    frotim
6.10 c                   eval      frousr = l_user
6.10 c                   time                    fredat
6.10 c                   time                    fretim
6.10 c                   eval      freusr = l_user
     c                   write     frawlur
      *
     c     frabl1_key    reade     frabl1
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering og utskrift av hode-opplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode          begsr
      *
      * Legger inn arb.stasjon og bruker i faste felter
      *
     c                   eval      a1firm = w_firm
     c                   eval      a1user = l_user
     c                   eval      a1wsid = l_wsid
     c                   eval      a1date = udate
     c     *hms          move      w_utim        a1time
      *
      * Henter firmaopplysninger
      *
     c     afirl1_key    chain     afirl1r                            90
     c                   eval      a1fnav = aafnav
      *
      * Henter parametre
      *
5.70 c                   eval      a2prgr = d_prgr
     c                   eval      a2rkaf = d_rkaf
     c                   eval      a2rkat = d_rkat
     c                   eval      a2kkaf = d_kkaf
     c                   eval      a2kkat = d_kkat
     c                   eval      a2kunf = d_kunf
     c                   eval      a2kunt = d_kunt
     c                   eval      a2kprf = d_kprf
     c                   eval      a2kprt = d_kprt
     c                   eval      a2vgrf = d_vgrf
     c                   eval      a2vgrt = d_vgrt
     c                   eval      a2ldof = d_ldof
     c                   eval      a2ldot = d_ldot
     c                   eval      a2modf = d_modf
     c                   eval      a2modt = d_modt
     c                   eval      a2varf = d_varf
     c                   eval      a2vart = d_vart
      *
      * Skriver heading
      *
     c                   write     a1hdr
     c                   write     a2hdr
     c                   write     a3hdr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utplukk og utskrift av detaljlinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     detalj        begsr
      *
      * Test på valg
      *
5.70 c                   if        d_prgr <> *blank and
5.70 c                             frprgr > d_prgr
5.70 c                   eval      b_avsl_les = *on
5.70 c                   leavesr
5.70 c                   endif
      *
     c                   if        d_rkat <> 0 and
     c                             frrkat > d_rkat
     c                   eval      b_avsl_les = *on
     c                   leavesr
     c                   endif
      *
     c                   if        d_kkaf <> 0 and
     c                             frkkat < d_kkaf or
     c                             d_kkat <> 0 and
     c                             frkkat > d_kkat
     c                   leavesr
     c                   endif
      *
     c                   if        d_kunf <> 0 and
     c                             frkund < d_kunf or
     c                             d_kunt <> 0 and
     c                             frkund > d_kunt
     c                   leavesr
     c                   endif
      *
     c                   if        d_kprf <> 0 and
     c                             frkpro < d_kprf or
     c                             d_kprt <> 0 and
     c                             frkpro > d_kprt
     c                   leavesr
     c                   endif
      *
     c                   eval      w_vgrp = frogrp * 100000 +
     c                                      frhgrp * 1000 + frugrp
     c                   if        d_vgrf <> 0 and
     c                             w_vgrp < d_vgrf or
     c                             d_vgrt <> 0 and
     c                             w_vgrp > d_vgrt
     c                   leavesr
     c                   endif
      *
     c                   if        d_ldof <> 0 and
     c                             frldor < d_ldof or
     c                             d_ldot <> 0 and
     c                             frldor > d_ldot
     c                   leavesr
     c                   endif
      *
     c                   if        d_modf <> 0 and
     c                             frmodn < d_modf or
     c                             d_modt <> 0 and
     c                             frmodn > d_modt
     c                   leavesr
     c                   endif
      *
     c                   if        d_varf <> *blank and
     c                             frvare < d_varf or
     c                             d_vart <> *blank and
     c                             frvare > d_vart
     c                   leavesr
     c                   endif
      *
      * Flytter verdier til detaljlinje
      *
5.70 c                   eval      d1prgr = frprgr
     c                   eval      d1rkat = frrkat
     c                   eval      d1kkat = frkkat
     c                   eval      d1kund = frkund
     c                   eval      d1knav = *blank
     c                   eval      d1kpro = frkpro
     c                   eval      d1pnav = *blank
     c                   eval      d1ogrp = frogrp
     c                   eval      d1hgrp = frhgrp
     c                   eval      d1ugrp = frugrp
     c                   eval      d1gtxt = *blank
     c                   eval      d1ldor = frldor
     c                   eval      d1lnav = *blank
     c                   eval      d1modn = frmodn
     c                   eval      d1vare = frvare
     c                   eval      d1tek1 = *blank
     c                   eval      d1enhe = frenhe
     c                   eval      d1lety = frlety
     c                   eval      d1nota = frnota
     c                   eval      d1dggj = 0
      *
      * Henter beskrivelse
      *
     c                   if        frkund <> 0
     c                   eval      rkunl1_kund = frkund
     c     rkunl1_key    chain     rkunl1
     c                   if        %found
     c                   eval      d1knav = rknavn
     c                   endif
     c                   endif
      *
     c                   if        frkpro <> 0
     c                   eval      fkprl1_kund = frkund
     c                   eval      fkprl1_kpro = frkpro
     c     fkprl1_key    chain     fkprl1
     c                   if        %found
     c                   eval      d1pnav = flnavn
     c                   endif
     c                   endif
      *
     c                   if        frogrp <> 0
     c                   eval      vogrl1_oogr = frogrp
     c     vogrl1_key    chain     vogrl1
     c                   if        %found
     c                   eval      d1gtxt = vgotxt
     c                   endif
     c                   endif
      *
     c                   if        frhgrp <> 0
     c                   eval      vhgrl1_hogr = frogrp
     c                   eval      vhgrl1_hhgr = frhgrp
     c     vhgrl1_key    chain     vhgrl1
     c                   if        %found
     c                   eval      d1gtxt = vghtxt
     c                   endif
     c                   endif
      *
     c                   if        frugrp <> 0
     c                   eval      vugrl1_uogr = frogrp
     c                   eval      vugrl1_uhgr = frhgrp
     c                   eval      vugrl1_uugr = frugrp
     c     vugrl1_key    chain     vugrl1
     c                   if        %found
     c                   eval      d1gtxt = vgutxt
     c                   endif
     c                   endif
      *
     c                   if        frldor <> 0
     c                   eval      rlevl1_levr = frldor
     c     rlevl1_key    chain     rlevl1
     c                   if        %found
     c                   eval      d1lnav = rlnavn
     c                   endif
     c                   endif
      *
     c                   if        frvare <> *blank
     c                   eval      vvarl1_vare = frvare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   eval      d1tek1 = vvtek1
     c                   endif
     c                   endif
      *
      * Kalkulerer DG for rabattlinje
      *
     c                   call      'FR700R'
     c                   parm                    w_firm
5.71 c                   parm                    frprgr
     c                   parm                    frogrp
     c                   parm                    frhgrp
     c                   parm                    frugrp
     c                   parm                    frldor
     c                   parm                    frmodn
     c                   parm                    frvare
     c                   parm                    frenhe
     c                   parm                    frlety
     c                   parm                    frrab1
     c                   parm                    frra1d
     c                   parm                    frra1f
     c                   parm                    frra1t
     c                   parm                    frrab2
     c                   parm                    frra2d
     c                   parm                    frra2f
     c                   parm                    frra2t
     c                   parm                    frrabn
     c                   parm                    frrand
     c                   parm                    frranf
     c                   parm                    frrant
     c                   parm                    frrabb
     c                   parm                    frrabd
     c                   parm                    frrbuf
     c                   parm                    frrbut
     c                   parm                    frrabt
     c                   parm                    frratd
     c                   parm                    frratf
     c                   parm                    frratt
     c                   parm                    frdekn
     c                   parm                    frdekd
     c                   parm                    frdekf
     c                   parm                    frdekt
     c                   parm                    w_udat
     c                   parm                    w_utim
     c                   parm                    b_inlr
     c                   parm                    d1dggj
      *
      * Skriver felles detaljlinje
      *
     c                   write     d1dtl                                30
     c                   eval      b_oflw = *off
     c                   if        *in30 = *on
     c                   eval      b_oflw = *on
     c                   endif
      *
      * Skriver detaljlinje avhengig av om det er DG eller rabatt
      *
     c                   eval      b_null = *on
      *
      * Dekningsgrad
      *
     c                   if        frdekn <> 0 or
     c                             frdekd <> 0
     c                   eval      d1dekn = frdekn
     c                   eval      d1dekd = frdekd
     c                   eval      d1dekf = 0
     c                   eval      d1dekt = 0
     c                   if        frdekf <> *loval
     c     *dmy          move      frdekf        d1dekf
     c                   endif
     c                   if        frdekt <> *loval
     c     *dmy          move      frdekt        d1dekt
     c                   endif
     c                   eval      b_null = *off
     c                   write     d1dtl_dg                             30
     c                   exsr      overflow
     c                   endif
      *
      * Linjerabatt-1
      *
     c                   if        frrab1 <> 0 or
     c                             frra1d <> 0
     c                   eval      d1rab_txt = 'Rabatt-1:'
     c                   eval      d1raba = frrab1
     c                   eval      d1rabd = frra1d
     c                   eval      d1rabf = 0
     c                   eval      d1rabt = 0
     c                   if        frra1f <> *loval
     c     *dmy          move      frra1f        d1rabf
     c                   endif
     c                   if        frra1t <> *loval
     c     *dmy          move      frra1t        d1rabt
     c                   endif
     c                   eval      b_null = *off
     c                   write     d1dtl_rab                            30
     c                   exsr      overflow
     c                   endif
      *
      * Linjerabatt-2
      *
     c                   if        frrab2 <> 0 or
     c                             frra2d <> 0
     c                   eval      d1rab_txt = 'Rabatt-2:'
     c                   eval      d1raba = frrab2
     c                   eval      d1rabd = frra2d
     c                   eval      d1rabf = 0
     c                   eval      d1rabt = 0
     c                   if        frra2f <> *loval
     c     *dmy          move      frra2f        d1rabf
     c                   endif
     c                   if        frra2t <> *loval
     c     *dmy          move      frra2t        d1rabt
     c                   endif
     c                   eval      b_null = *off
     c                   write     d1dtl_rab                            30
     c                   exsr      overflow
     c                   endif
      *
      * Rabatt på nettopris
      *
     c                   if        frrabn <> 0 or
     c                             frrand <> 0
     c                   eval      d1rab_txt = 'Rabatt-N:'
     c                   eval      d1raba = frrabn
     c                   eval      d1rabd = frrand
     c                   eval      d1rabf = 0
     c                   eval      d1rabt = 0
     c                   if        frranf <> *loval
     c     *dmy          move      frranf        d1rabf
     c                   endif
     c                   if        frrant <> *loval
     c     *dmy          move      frrant        d1rabt
     c                   endif
     c                   eval      b_null = *off
     c                   write     d1dtl_rab                            30
     c                   exsr      overflow
     c                   endif
      *
      * Rabatt på bunnpris
      *
     c                   if        frrabb <> 0 or
     c                             frrabd <> 0
     c                   eval      d1rab_txt = 'Rabatt-B:'
     c                   eval      d1raba = frrabb
     c                   eval      d1rabd = frrabd
     c                   eval      d1rabf = 0
     c                   eval      d1rabt = 0
     c                   if        frrbuf <> *loval
     c     *dmy          move      frrbuf        d1rabf
     c                   endif
     c                   if        frrbut <> *loval
     c     *dmy          move      frrbut        d1rabt
     c                   endif
     c                   eval      b_null = *off
     c                   write     d1dtl_rab                            30
     c                   exsr      overflow
     c                   endif
      *
      * Rabatt på tilbudspris
      *
     c                   if        frrabt <> 0 or
     c                             frratd <> 0
     c                   eval      d1rab_txt = 'Rabatt-T:'
     c                   eval      d1raba = frrabt
     c                   eval      d1rabd = frratd
     c                   eval      d1rabf = 0
     c                   eval      d1rabt = 0
     c                   if        frratf <> *loval
     c     *dmy          move      frratf        d1rabf
     c                   endif
     c                   if        frratt <> *loval
     c     *dmy          move      frratt        d1rabt
     c                   endif
     c                   eval      b_null = *off
     c                   write     d1dtl_rab                            30
     c                   exsr      overflow
     c                   endif
      *
      * Null-rabatt
      *
     c                   if        b_null = *on
     c                   write     d1dtl_spc                            30
     c                   exsr      overflow
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for overflow
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     overflow      begsr
      *
     c                   if        *in30 = *on or
     c                             b_oflw = *on
     c                   write     a1hdr
     c                   write     a3hdr
     c                   eval      b_oflw = *off
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Paramtre
      *
     c     *entry        plist
     c                   parm                    p_list
      *
      * Key for les på rabattmatrise
      *
     c     frabl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på kunderegister
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      * Key for oppslag på vareregister
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for posisjonering på arbeidstabell
      *
     c     frawl1_key    klist
     c                   kfld                    w_firm
5.70 c                   kfld                    frawl1_prgr
     c                   kfld                    frawl1_rkat
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for oppslag på firma
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      *
      * Flytter parameter til datasturuktur
      *
     c                   eval      d_list = p_list
      *
     c                   eval      w_firm = d_firm
      *
      * Henter dagens dato
      *
     c                   time                    w_udat
     c                   time                    w_utim
      *
     c                   endsr
