      /title  ASOFAK Lev.vare, henter vareregister - Andvord
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FW721R
      * Beskrivelse . . : Danning av leverandør vareregister, Andvord
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       050100 HKO  Nytt program
      *       130501 HKO  Tilpasset innlesing fra regneaks-mal
5.70  * prgr  150609 bof  Utvidet med prisgruppe
      * 6.10  230609 BSA  Oppdatert med sporingsinformasjon
6.20  *       290811 bof  Utvider vvprpf med leverandør
8.01  *PRG2   100622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fflvwpf    if   e           k disk
     ffenkl1    if   e           k disk    rename(fenkpfr:fenkl1r)
     fvvarl5    if   e           k disk    rename(vvarpfr:vvarl5r)
     fvvprl1    if   e           k disk    rename(vvprpfr:vvprl1r)
     fflvapf    o  a e             disk
      *
6.10  * Local data area
6.10  *
6.10 d                uds
6.10 d l_user                911    920
      * Definering av datastruktur
      *
      *  Recordbeskrivelse - Andvord
      *
     d                 ds
     d d_inpu                       256
     d  d_vare                       15    overlay(d_inpu:1)                    Varenr
     d  d_tek1                       35    overlay(d_inpu:16)                   Varetekst-1
     d  d_tek2                       13    overlay(d_inpu:51)                   Varetekst-2/Varians
     d  d_pvar                       18    overlay(d_inpu:64)                   Prod.varenr
     d  d_anta                        7    overlay(d_inpu:82)                   Antall i annen enhet
     d  d_enhe                        4    overlay(d_inpu:89)                   Enhet
     d  d_inpr                       10    overlay(d_inpu:93)                   Innkjøpspris
     d   d_inpr_kr                    7  0 overlay(d_inpr:1)                    Innkjøpspris - kr
     d   d_inpr_Øre                   2  2 overlay(d_inpr:9)                    Innkjøpspris - øre
     d  d_vgrp                        6    overlay(d_inpu:103)                  Varegruppe
     d  d_eann                       13    overlay(d_inpu:109)                  EAN-nr
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_ldor          s              6  0
      *
      * Definering av variable i nøkler
      *
     d fenkl1_enhf     s                   like(feenhf)
     d vvarl5_ldor     s                   like(vvldor)
     d vvarl5_lvar     s                   like(vvlvar)
6.20 d vvprl1_ldor     s                   like(vpldor)
5.70 d vvprl1_prgr     s                   like(vpprgr)
5.70 d vvprl1_vare     s                   like(vpvare)
     d vvprl1_enhe     s                   like(vpenhe)
     d vvprl1_lety     s                   like(vplety)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
5.70 d w_prgr          s                   like(vpprgr)
     d w_vtxt          s             71
     d w_posi          s              2  0
     d w_anta          s              7
     d w_char          s              1
      *
     d w_tek1          s                   like(fwtek1)
     d w_tek2          s                   like(fwtek2)
     d w_sapr          s                   like(vpsapr)
     d w_inpr          s                   like(vpinpr)
      *
      * Definering av konstanter
      *
     d Lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d Up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser input-fil, og legger data ut på leverandør vareregister
      *
     c                   read      flvwpf                                 90
     c                   dow       *in90 = *off
      *
     c                   eval      d_inpu = fwirad
      *
      * Oversetter til numeriske felt
      *
     c     ' ':'0'       xlate     d_inpr        d_inpr
     c     ' ':'0'       xlate     d_vgrp        d_vgrp
      *
      * Oversetter tekster til ascii og store bokstaver
      *
     c                   eval      d_vare = %trim(d_vare)
     c                   eval      d_tek1 = %trim(d_tek1)
     c                   eval      d_tek2 = %trim(d_tek2)
      *
     c     X'46':'Æ'     xlate     d_vare        d_vare
     c     X'A0':'æ'     xlate     d_vare        d_vare
     c     X'77':'Ø'     xlate     d_vare        d_vare
     c     X'90':'ø'     xlate     d_vare        d_vare
     c     X'2A':'Å'     xlate     d_vare        d_vare
     c     X'EF':'å'     xlate     d_vare        d_vare
      *
     c     X'46':'Æ'     xlate     d_tek1        d_tek1
     c     X'A0':'æ'     xlate     d_tek1        d_tek1
     c     X'77':'Ø'     xlate     d_tek1        d_tek1
     c     X'90':'ø'     xlate     d_tek1        d_tek1
     c     X'2A':'Å'     xlate     d_tek1        d_tek1
     c     X'EF':'å'     xlate     d_tek1        d_tek1
      *
     c     X'46':'Æ'     xlate     d_tek2        d_tek2
     c     X'A0':'æ'     xlate     d_tek2        d_tek2
     c     X'77':'Ø'     xlate     d_tek2        d_tek2
     c     X'90':'ø'     xlate     d_tek2        d_tek2
     c     X'2A':'Å'     xlate     d_tek2        d_tek2
     c     X'EF':'å'     xlate     d_tek2        d_tek2
      *
     c     Lo:Up         xlate     d_tek1        d_tek1
     c     Lo:Up         xlate     d_tek2        d_tek2
      *
      * Skriver til leverandør vareregister
      *
     c                   eval      fwfirm = p_firm
     c                   eval      fwldor = p_ldor
     c                   eval      fwekod = *blank
     c                   eval      fwtek1 = d_tek1
     c                   eval      fwtek2 = d_tek2
     c                   eval      fwlvar = d_vare
     c                   eval      fweann = 0
     c                   eval      fwnobb = 0
     c                   eval      fwenhe = d_enhe
     c                   eval      fwinpr = d_inpr_kr + d_inpr_Øre
     c                   eval      fwsapr = 0
     c                   eval      fwdato = *loval
     c                   eval      fwbmen = 0
     c                   eval      fwlvae = *blank
     c                   eval      fwtvar = *blank
     c                   eval      fwhkod = 1
      *
     c                   eval      fwline = fwline + 1
      *
     c                   move      d_eann        fweann
      *
     c                   time                    fwdato
      *
      * Konverterer enhet
      *
     c                   eval      fenkl1_enhf = d_enhe
     c     fenkl1_key    chain     fenkl1                             91
     c                   if        *in91 = *off
     c                   eval      fwenhe = feenht
     c                   endif
      *
      * Henter gamle priser, og beregner ny salgspris
      *
     c                   exsr      salgspris
      *
      * Skriver til fil
      *
6.10 c                   time                    fwodat
6.10 c                   time                    fwotim
6.10 c                   eval      fwousr = l_user
6.10 c                   time                    fwedat
6.10 c                   time                    fwetim
6.10 c                   eval      fweusr = l_user
     c                   write     flvapfr
      *
     c                   read      flvwpf                                 90
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for konkatinering av varetekst-1 og varetekst-2
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     konkatiner    begsr
      *
     c                   eval      w_tek1 = *blank
     c                   eval      w_tek2 = *blank
     c                   eval      w_vtxt = *blank
      *
      * Trimmer varetekst-1
      *
     c     ' '           scan      fwtek1:1      w_posi                   92
     c                   dow       w_posi <> 1
      *
     c                   if        *in92 = *off
     c                   eval      w_posi = 36
     c                   endif
      *
     c                   eval      w_tek1 = %trim(w_tek1) + ' ' +
     c                                      %subst(fwtek1:1:w_posi-1)
      *
     c                   if        w_posi >= 35
     c                   leave
     c                   endif
      *
     c                   eval      fwtek1 = %trim(
     c                                      %subst(fwtek1:w_posi:36-w_posi))
      *
     c     ' '           scan      fwtek1:1      w_posi                   92
     c                   enddo
      *
      * Trimmer varetekst-2
      *
     c                   if        fwtek2 <> *blank
      *
     c     ' '           scan      fwtek2:1      w_posi                   92
     c                   dow       w_posi <> 1
      *
     c                   if        *in92 = *off
     c                   eval      w_posi = 36
     c                   endif
      *
     c                   eval      w_tek2 = %trim(w_tek2) + ' ' +
     c                                      %subst(fwtek2:1:w_posi-1)
      *
     c                   if        w_posi >= 35
     c                   leave
     c                   endif
      *
     c                   eval      fwtek2 = %trim(
     c                                      %subst(fwtek2:w_posi:36-w_posi))
      *
     c     ' '           scan      fwtek2:1      w_posi                   92
     c                   enddo
      *
     c                   endif
      *
      * Konkatinerer varetekst-1 og varetekst-2
      *
     c                   eval      w_vtxt = %trim(w_tek1) + %trim(w_tek2)
      *
      * Gjør antall om til alfa-felt, og konkatinerer dette inn i vare-
      * teksten
      *
     c                   move      d_anta        w_anta
      *
     c                   eval      w_posi = 1
     c                   eval      w_char = %subst(w_anta:w_posi:1)
      *
     c                   dow       w_char = '0' and
     c                             w_posi < 7
     c                   eval      w_posi = w_posi + 1
     c                   eval      w_char = %subst(w_anta:w_posi:1)
     c                   enddo
      *
     c                   if        w_char <> '0'
     c                   eval      w_vtxt = %trim(w_vtxt) + ' ' +
     c                                      %subst(w_anta:w_posi:8-w_posi)
     c                   endif
      *
      * Splitter varetekst i varetekst-1 og varetekst-2
      *
     c                   eval      fwtek1 = %trim(%subst(w_vtxt:1:35))
     c                   eval      fwtek2 = %trim(%subst(w_vtxt:36:35))
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for beregning av ny salgspris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     salgspris     begsr
      *
     c                   eval      w_sapr = 0
     c                   eval      w_inpr = 0
      *
     c                   eval      vvarl5_ldor = fwldor
     c                   eval      vvarl5_lvar = fwlvar
     c     vvarl5_key    chain     vvarl5                             92
     c                   if        *in92 = *on
     c                   goto      end_salgspris
     c                   endif
      *
      * Henter eksisterende priser på leveringstype 0
      *
     c                   eval      vvprl1_vare = vvvare
6.20 c                   eval      vvprl1_ldor = vvldor
     c                   eval      vvprl1_enhe = fwenhe
     c                   eval      vvprl1_lety = 0
     c     vvprl1_key    setll     vvprl1
     c     vvprl1_key    reade     vvprl1                                 92
     c                   dow       *in92 = *off
      *
     c                   eval      w_sapr = vpsapr
     c                   eval      w_inpr = vpinpr
      *
     c                   if        w_inpr = 0
     c                   eval      w_inpr = vpkopr
     c                   endif
      *
     c                   leave
      *
     c     vvprl1_key    reade     vvprl1                                 92
     c                   enddo
      *
      * Finner ny salgspris etter endring i kostpris
      *
     c                   eval      fwsapr = w_sapr
      *
     c                   if        fwinpr <> 0 and
     c                             w_inpr <> 0 and
     c                             fwinpr <> w_inpr
     c                   eval(h)   fwsapr = fwsapr * (fwinpr / w_inpr)
     c                   endif
      *
     c     end_salgspris endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ldor
      *
      * Key for oppslag på enhet-konvertering
      *
     c     fenkl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fenkl1_enhf
      *
      * Key for oppslag på vare
      *
     c     vvarl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl5_ldor
     c                   kfld                    vvarl5_lvar
      *
      * Key for les på vare-pris
      *
     c     vvprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvprl1_vare
6.20 c                   kfld                    vvprl1_ldor
5.70 c                   kfld                    w_prgr
     c                   kfld                    vvprl1_enhe
     c                   kfld                    vvprl1_lety
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
5.70  *
5.70  * Henter prisgruppe
5.70  *
5.70 c                   call      'VL711R   '
5.70 c                   parm                    w_firm
5.70 c                   parm                    w_prgr
      *
     c                   endsr
