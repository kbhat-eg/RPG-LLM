     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : Vare-pris, prisjustering
      * Beskrivelse . . : Prisjustering
      *
      *                   Diverse-varer, og varer som er merket med 'kjus=1'
      *                   blir ikke behandlet
      *                   DG for beregning av salgspris blir kun benyttet på
      *                   varer med kostpris
      *                   Dersom det finnes priser nyere enn -eller lik
      *                   prisdato, oppdateres disse med endringen. Ellers
      *                   opprettes nye priser på prisdato.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       010699 HKO  Nytt program
      *       210200 HKO  Lagt inn valg for utelating av varer i.h.t.
      *                   priskode
5.70  * PRGR  051108 BOF  utvidet med prisgruppe
      * 6.10  290609 BSA  Oppdatert med sporingsinformasjon
6.11  * 6.10  181110 BOF  Justert feil vedr. utvalg på prisgruppe
6.20  * 6.20  010411 BOF  Utvidet med vare-pris med prisgiver
6.21  * 6.20  190511 bø   Utvidet vl710r med div. info
6.30  * 6.30  170918 bkv  Utvidet med trelast sortiment
      *
8.01  *PRG2   150622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvprl1    if   e           k disk    rename(vvprpfr:vvprl1r)
     fvvprlu    uf a e           k disk    rename(vvprpfr:vvprlur)
6.30 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vvfirm)
     d p_ldor          s                   like(vvldor)
     d p_ogrp          s                   like(vvogrp)
     d p_hgrp          s                   like(vvhgrp)
     d p_ugrp          s                   like(vvugrp)
     d p_varf          s                   like(vvvare)
     d p_vart          s                   like(vvvare)
     d p_dato          s               d   datfmt(*iso)
     d p_spro          s              5  2
     d p_kpro          s              5  2
     d p_ipro          s              5  2
     d p_bpro          s              5  2
     d p_dekn          s              4  2
     d p_nypr          s              1  0
     d p_orpv          s              1  0
     d p_ntpv          s              1  0
     d p_bupv          s              1  0
     d p_lety          s              1
8.01 d p_prgr          s              2
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
5.70 d l_lage               1001   1002  0
      *
      * Definering av variabler i nøkler
      *
     d vvprl1_vare     s                   like(vpvare)
     d vvprlu_vare     s                   like(vpvare)
6.20 d vvprlu_ldor     s                   like(vpldor)
5.70 d vvprlu_prgr     s                   like(vpprgr)
     d vvprlu_enhe     s                   like(vpenhe)
     d vvprlu_lety     s                   like(vplety)
     d vvprlu_pdat     s                   like(vppdat)
      *
      * Definering av variable
      *
     d b_frst          s              1    inz(*off)
     d w_udat          s               d   datfmt(*iso)
     d w_dato          s               d   datfmt(*iso)
      *
     d w_firm          s                   like(vvfirm)
     d w_sapr          s                   like(vpsapr)
     d w_kopr          s                   like(vpkopr)
     d w_inpr          s                   like(vpinpr)
     d w_bupr          s                   like(vpbupr)
     d w_lety_gml      s                   like(vplety)
     d w_enhe_gml      s                   like(vpenhe)
     d w_lety          s              1
5.70 d w_lage          s              2  0
6.21 d w_vtyp          s                   like(vvvtyp)
6.21 d w_utda          s                   like(vvudat)
6.21 d w_ldor          s                   like(vvldor)
6.21 d b_inlr          s              1    inz(*off)
6.30 d w_lv_nobb       s                   like(vvlnob)
6.30 d w_lv_modn       s                   like(vvlmod)
6.30 d w_lv_lldo       s                   like(vvlnle)
6.30 d w_lv_llva       s                   like(vvllva)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     vvarl1_key    setll     vvarl1
     c     vvarl1_key    reade     vvarl1                                 90
     c                   dow       *in90 = *off
      *
      * Tester om vare skal være med
      *
     c                   if        p_ldor <> 0 and
     c                             vvldor <> p_ldor
     c                   goto      neste_vare
     c                   endif
      *
     c                   if        p_ogrp <> 0 and
     c                             vvogrp <> p_ogrp
     c                   goto      neste_vare
     c                   endif
      *
     c                   if        p_hgrp <> 0 and
     c                             vvhgrp <> p_hgrp
     c                   goto      neste_vare
     c                   endif
      *
     c                   if        p_ugrp <> 0 and
     c                             vvugrp <> p_ugrp
     c                   goto      neste_vare
     c                   endif
      *
     c                   if        p_varf <> *blank and
     c                             vvvare <  p_varf
     c                   goto      neste_vare
     c                   endif
      *
     c                   if        p_vart <> *blank and
     c                             vvvare >  p_vart
     c                   leave
     c                   endif
      *
     c                   if        p_orpv = 1 and
     c                             vvkpri = ' '
     c                   goto      neste_vare
     c                   endif
      *
     c                   if        p_ntpv = 1 and
     c                             vvkpri = 'N'
     c                   goto      neste_vare
     c                   endif
      *
     c                   if        p_bupv = 1 and
     c                             vvkpri = 'B'
     c                   goto      neste_vare
     c                   endif
      *
     c                   if        vvkjus = 1
     c                   goto      neste_vare
     c                   endif
      *
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    vvvare
5.70 c                   parm                    w_lage
6.21 c                   parm                    w_vtyp
6.21 c                   parm                    w_utda
6.21 c                   parm                    w_ldor
6.21 c                   parm                    b_inlr
6.30 c                   parm                    w_lv_nobb
6.30 c                   parm                    w_lv_modn
6.30 c                   parm                    w_lv_lldo
6.30 c                   parm                    w_lv_llva
      *
6.21 c                   if        w_vtyp = 'D'
     c                   goto      neste_vare
     c                   endif
      *
      * Oppdaterer priser i vareregisteret
      *
     c                   exsr      behandling
      *
     c     neste_vare    tag
      *
     c     vvarl1_key    reade     vvarl1                                 90
     c                   enddo
      *
      * Avslutt
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av priser knyttet mot vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
      * Leser vare-pris-registeret for varen. Dersom det finnes priser nyere
      * enn -eller lik prisdato, oppdateres disse med endringen. Ellers
      * opprettes nye priser.
      * Leser alle prisgrupper, leveringstyper og enheter
      *
     c                   eval      b_frst = *on
     c                   eval      w_lety_gml = -1
     c                   eval      w_enhe_gml = *blank
      *
     c                   eval      vvprl1_vare = vvvare
     c     vvprl1_key    setll     vvprl1
     c     vvprl1_key    reade     vvprl1                                 91
     c                   dow       *in91 = *off
      *
6.11 c                   if        (p_prgr =  *blank) or
6.11 c                             (p_prgr <> *blank and
6.11 c                             vpprgr = p_prgr)
      *
     c                   move      vplety        w_lety
     c                   if        p_lety = *blank or
     c                             p_lety = w_lety
      *
     c                   if        w_udat >= vppdat
      *
     c                   if        b_frst = *on or
     c                             w_lety_gml <> vplety or
     c                             w_enhe_gml <> vpenhe
     c                   exsr      oppdater
     c                   endif
      *
     c                   eval      b_frst = *off
     c                   eval      w_lety_gml = vplety
     c                   eval      w_enhe_gml = vpenhe
      *
     c                   endif
      *
     c                   endif
5.70  *
     c                   endif
      *
     c     vvprl1_key    reade     vvprl1                                 91
     c                   enddo
      *
     c     end_behandl   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av priser i vare-pris-registeret
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdater      begsr
      *
      * Finner eksisterende priser
      *
     c                   eval      w_sapr = vpsapr
     c                   eval      w_kopr = vpkopr
     c                   eval      w_inpr = vpinpr
     c                   eval      w_bupr = vpbupr
      *
      * Kalkulerer nye priser
      *
     c                   if        p_spro <> 0
     c                   eval(h)   w_sapr = vpsapr + (vpsapr * p_spro / 100)
     c                   endif
      *
     c                   if        p_kpro <> 0
     c                   eval(h)   w_kopr = vpkopr + (vpkopr * p_kpro / 100)
     c                   endif
      *
     c                   if        p_ipro <> 0
     c                   eval(h)   w_inpr = vpinpr + (vpinpr * p_ipro / 100)
     c                   endif
      *
     c                   if        p_bpro <> 0
     c                   eval(h)   w_bupr = vpbupr + (vpbupr * p_bpro / 100)
     c                   endif
      *
     c                   if        p_dekn <> 0 and
     c                             vpkopr <> 0
     c                   eval(h)   w_sapr = (100 * vpkopr) / (100 - p_dekn)
     c                   endif
      *
      * Avbryter dersom det ikke er prisendring
      *
     c                   if        w_sapr = vpsapr and
     c                             w_kopr = vpkopr and
     c                             w_inpr = vpinpr and
     c                             w_bupr = vpbupr
     c                   goto      end_oppd
     c                   endif
      *
      * Sletter priser nyere enn gjeldende pris
      *
     c                   if        p_nypr = 0
      *
     c     vppdat        adddur    1:*d          w_dato
      *
     c                   eval      vvprlu_vare = vpvare
6.20 c                   eval      vvprlu_ldor = vpldor
5.70 c                   eval      vvprlu_prgr = vpprgr
     c                   eval      vvprlu_enhe = vpenhe
     c                   eval      vvprlu_lety = vplety
     c                   eval      vvprlu_pdat = w_dato
     c     vvprlu_key    setll     vvprlur
     c     vvprlu_key2   reade     vvprlur                                92
     c                   dow       *in92 = *off
     c                   delete    vvprlur
     c     vvprlu_key2   reade     vvprlur                                92
     c                   enddo
      *
     c                   endif
      *
      * Oppretter/oppdaterer vare-pris
      *
     c                   eval      vvprlu_vare = vpvare
6.20 c                   eval      vvprlu_ldor = vpldor
5.70 c                   eval      vvprlu_prgr = vpprgr
     c                   eval      vvprlu_enhe = vpenhe
     c                   eval      vvprlu_lety = vplety
     c                   eval      vvprlu_pdat = p_dato
     c     vvprlu_key    chain     vvprlur                            92
      *
     c                   eval      vppdat = p_dato
     c                   eval      vpsapr = w_sapr
     c                   eval      vpkopr = w_kopr
     c                   eval      vpinpr = w_inpr
     c                   eval      vpbupr = w_bupr
     c                   eval      vpeusr = l_user
     c                   time                    vpedat
     c                   time                    vpetim
      *
     c                   if        *in92 = *off
     c                   update    vvprlur
     c                   else
6.10 c                   time                    vpodat
6.10 c                   time                    vpotim
6.10 c                   eval      vpousr = l_user
     c                   write     vvprlur
     c                   endif
      *
     c     end_oppd      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ldor
     c                   parm                    p_ogrp
     c                   parm                    p_hgrp
     c                   parm                    p_ugrp
     c                   parm                    p_varf
     c                   parm                    p_vart
     c                   parm                    p_dato
     c                   parm                    p_spro
     c                   parm                    p_kpro
     c                   parm                    p_ipro
     c                   parm                    p_bpro
     c                   parm                    p_dekn
     c                   parm                    p_nypr
     c                   parm                    p_orpv
     c                   parm                    p_ntpv
     c                   parm                    p_bupv
     c                   parm                    p_lety
5.70 c                   parm                    p_prgr
      *
      * Key for les på vareregister
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for les på vare-pris
      *
     c     vvprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvprl1_vare
      *
      * Key for oppdatering av vare-pris
      *
     c     vvprlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvprlu_vare
6.20 c                   kfld                    vvprlu_ldor
5.70 c                   kfld                    vvprlu_prgr
     c                   kfld                    vvprlu_enhe
     c                   kfld                    vvprlu_lety
     c                   kfld                    vvprlu_pdat
      *
      * Key for oppdatering av vare-pris
      *
     c     vvprlu_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvprlu_vare
6.20 c                   kfld                    vvprlu_ldor
5.70 c                   kfld                    vvprlu_prgr
     c                   kfld                    vvprlu_enhe
     c                   kfld                    vvprlu_lety
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
5.70 c                   eval      w_lage = l_lage
      *
      * Henter dagen dato
      *
     c                   time                    w_udat
      *
     c                   endsr
