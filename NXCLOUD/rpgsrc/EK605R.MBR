     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : EK605R
      * Beskrivelse . . : Kunde bonus, skriver ut kundebrev summert/detaljert
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       261011 bsa  Nytt program
6.10  *       080312 bsa  Skal vise fortegn på utskrift
6.20  *       130114 bsa  Detaljert oversikt skriver ut merknadsfelt
6.20  *                   da detaljnivå ikke sier kunden noe.
6.21  *       140114 bsa  Sjekker mot datointervallet på gyldighetsper
6.21  *                   om detaljlinjer skal være med.
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fekbti1    if   e           k disk    rename(ekbtst:ekbti1r)
     fekboic    if   e           k disk    rename(ekbost:ekboicr)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fvbotl1    if   e           k disk    rename(vbotpfr:vbotl1r)
      *
     fek605p    o    e             printer oflind(*in30)
      *
      * Definering av parametre
      *
     d p_paar          s              4  0
     d p_boty          s              2
     d p_alt1          s              1  0
     d p_kund          s              6  0
     d b_bonu          s              1    inz(*off)
     d b_kund          s              1    inz(*off)
      *
      * Definering av variabler i nøkler
      *
     d ekbti1_tbku     s                   like(ektbku)
     d ekboic_bkun     s                   like(ekbkun)
     d ekboic_boty     s                   like(ekboty)
     d rkunl1_kund     s                   like(rkkund)
     d vbotl1_tbot     s                   like(vatbot)
      *
      * Definering av variable
      *
     d w_firm          s                   like(ektfir)
     d w_tbot          s              2
     d w_kund          s              6  0
     d w_bgrl          s             11  2
     d w_aar           s              4  0
6.10 d t1subg          s             13  2
      *
      * Parameter overføringer
      *
     d                uds
      *
     d l_wsid                901    910                                         Bruker
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980                                         Navn
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      b_bonu = *off
      * Skriv ut heading
     c                   eval      a1aarr = p_paar
      *
     c                   eval      t1subg = 0
     c                   eval      t1subn = 0
      *
     c                   exsr      kal_kund
      *
      * Avslutt program
      *
     c                   eval      *inlr = *on
      *
     c                   return
      *--------------------------------------------------------------
      * Bonus på kundekategori                                      *
      *--------------------------------------------------------------
     c     kal_kund      begsr
      *
     c                   eval      w_tbot = *blank
     c                   eval      w_kund = 0
     c                   eval      b_kund = *off
      * Henter inn kundeinformasjon
     c                   exsr      finn_ktext
      * Skriv heading
     c                   if        b_kund = *on
     c                   write     a1hdr
     c                   eval      b_kund = *off
     c                   endif
      * Skriv heading for detaljer hvis den skal skrives
     c                   if        p_alt1 = 1
     c                   write     a2hdr
     c                   endif
      * Skriv linjer
     c                   write     a3hdr
      *
     c                   eval      ekbti1_tbku  =  p_kund
     c     ekbti1_key    setll     ekbti1
     c     ekbti1_key    reade     ekbti1
      *
     c                   dow       not  %eof(ekbti1)
      *
     c                   if        p_boty <> *blank and
     c                             ektbot <> p_boty
     c                   goto      nyles2
     c                   endif
      * Hent bonustypetekst
     c                   if        w_tbot <> ektbot
     c                   exsr      finn_btext
     c                   endif
      *
     c                   eval      d1bgru = ektbgr
     c                   eval      d1sbon = ektsbo
      *
     c                   if        ektsbo = 0 and
     c                             w_bgrl = 0
     c                   goto      nyles2
     c                   endif
      *
     c                   eval      t1subg = t1subg + ektbgr
     c                   eval      t1subn = t1subn + ektsbo
      *
     c                   write     d1det                                30
     c                   eval      b_bonu = *on
      *
     c                   if        *in30 = *on
     c                   exsr      xovrfl
     c                   endif
      * Hvis grunnlaget for avtalen skal skrives, les også underliggende nivå
     c                   if        p_alt1 = 1
     c                   exsr      kal_bgrl
     c                   endif
      *
     c     nyles2        tag
      *
     c     ekbti1_key    reade     ekbti1
     c                   enddo
      *
     c     end_kund      tag
      *
     c                   if        b_bonu = *on
     c                   write     t1tot
     c                   endif
      *
     c                   endsr
      *--------------------------------------------------------------
      * Bonus på kundekategori                                      *
      *--------------------------------------------------------------
     c     kal_bgrl      begsr
      *
     c                   eval      ekboic_bkun  =  ektbku
     c                   eval      ekboic_boty  =  ektbot
     c     ekboic_key    setll     ekboic
     c     ekboic_key    reade     ekboic
      *
     c                   dow       not  %eof(ekboic)
6.21 c*                  if        ekbdat <> *loval
6.21 c*                  extrct    ekbdat:*y     w_aar
6.21 c*                  else
6.21 c*                  eval      w_aar = p_paar
6.21 c*                  endif
6.21 c                   eval      w_aar = 0
6.21  * Årstall fra
6.21 c                   if        ekfdat <> *loval
6.21 c                   extrct    ekfdat:*y     w_aar
6.21 c                   endif
6.21  *
6.21 c                   if        w_aar <> 0 and
6.21 c                             w_aar = p_paar
6.21 c                   goto      utskrift
6.21 c                   endif
6.21  * Årstall til
6.21 c                   if        ektdat <> *loval
6.21 c                   extrct    ektdat:*y     w_aar
6.21 c                   endif
6.21  *
6.21 c                   if        w_aar <> 0 and
6.21 c                             w_aar = p_paar
6.21 c                   goto      utskrift
6.21 c                   endif
6.21  *
6.21 c                   if        w_aar = p_paar or
     c                             w_aar = 0
      *
6.21 c     utskrift      tag
      *
6.20 c                   eval      d2merk = ekmerk
6.20 c*                  eval      d2rabk = ekrkat
6.20 c*                  eval      d2kund = ekkund
6.20 c*                  eval      d2ogrp = ekogrp
6.20 c*                  eval      d2hgrp = ekhgrp
6.20 c*                  eval      d2ugrp = ekugrp
6.20 c*                  eval      d2ldor = ekldor
6.20 c*                  eval      d2vare = ekvare
     c*                  if        ekfdat <> *loval
     c*    *dmy          move      ekfdat        d2fdat
     c*                  else
     c*                  eval      d2fdat = 0
     c*                  endif
     c*                  if        ektdat <> *loval
     c*    *dmy          move      ektdat        d2tdat
     c*                  else
     c*                  eval      d2tdat = 0
     c*                  endif
     c                   eval      d2bgru = ekbgru
     c                   eval      d2sbon = eksbon
      *
     c                   write     d2det                                30
     c                   if        *in30 = *on
     c                   exsr      xovrfl
     c                   endif
      *
     c                   endif
     c     ekboic_key    reade     ekboic
     c                   enddo
      *
     c                   endsr
      *--------------------------------------------------------------
      * Finn kundetekst
      *--------------------------------------------------------------
     c     finn_ktext    begsr
      *
     c                   eval      w_kund = p_kund
     c                   eval      a1kunr = p_kund
     c                   eval      a1kuna = *blank
     c                   eval      a1kuga = *blank
     c                   eval      a1kuad = *blank
     c                   eval      a1kups = *blank
     c                   eval      rkunl1_kund = p_kund
     c     rkunl1_key    chain     rkunl1
     c                   if        %found
     c                   movel     rknavn        a1kuna
     c                   movel     rkgate        a1kuga
     c                   movel     rkadr2        a1kuad
     c                   movel     rksted        a1kups
     c                   endif
      *
     c                   eval      b_kund = *on
      *
     c                   endsr
      /eject
      *--------------------------------------------------------------
      * Finn tekst for bonustype
      *--------------------------------------------------------------
     c     finn_btext    begsr
      *
     c                   eval      w_tbot = ektbot
     c                   eval      d1boty = ektbot
     c                   eval      d1besk = *blank
     c                   eval      vbotl1_tbot = ektbot
     c     vbotl1_key    chain     vbotl1
     c                   if        %found
     c                   movel     vatbes        d1besk
     c                   endif
      *
     c                   endsr
      /eject
      *--------------------------------------------------------------
      * Rutine for håndtering av ny side                            *
      *--------------------------------------------------------------
     c     xovrfl        begsr
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   if        p_alt1 = 1
     c                   write     a2hdr
     c                   endif
     c                   write     a3hdr
     c                   eval      *in30 = *off
     c                   endif
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_kund
     c                   parm                    p_paar
     c                   parm                    p_alt1
     c                   parm                    p_boty
      * Key for oppslag kunder
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag bonustype
      *
     c     vbotl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vbotl1_tbot
      *
      * Key for les av bonusgrunnlag
      *
     c     ekbti1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ekbti1_tbku
      *
      * Key for les av bonusgrunnlag
      *
     c     ekboic_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ekboic_bkun
     c                   kfld                    ekboic_boty
      *
      * Henter parameter
      *
     c                   move      l_firm        w_firm
      *
     c                   endsr
