     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : VD700R
      * Beskrivelse . . : Varesortiment-linje, oppretter utplukksfil
      *
      *                   Parametrene p_svgr, p_sldo og p_stek forteller
      *                   om henholdsvis varegruppe, leverandør og
      *                   varetekst-1 skal være med på sorteringen.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       230101 HKO  Nytt program
      *       110903 HKO  Modul som nivå i varesortiment-linje
      *                   Endret logikk for uthenting
6.20  *       051110 bof  Utvidet med lager på heading
6.23  *       270913 bof  Hvis lager på heading skal det også sjekkes om
6.23  *                   leverandør i parameter har finnes på lageret
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.20 fvshel1    if   e           k disk    rename(vshepfr:vshel1r)
     fvsdtl1    if   e           k disk    rename(vsdtpfr:vsdtl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvarlb    if   e           k disk    rename(vvarpfr:vvarlbr)
     fvvarla    if   e           k disk    rename(vvarpfr:vvarlar)
     fvvarl9    if   e           k disk    rename(vvarpfr:vvarl9r)
6.23 fvlagl3    if   e           k disk    rename(vlagpfr:vlagl3r)
     fvvawpf    o    e             disk
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vvfirm)
     d p_vsor          s                   like(vdvsor)
6.20 d p_lage          s                   like(vdlage)
     d p_svgr          s              1
     d p_sldo          s              1
     d p_stek          s              1
      *
      * Definering av variabler i nøkler
      *
6.20 d vshel1_vsor     s                   like(vcvsor)
6.20 d vshel1_lage     s                   like(vclage)
     d vsdtl1_vsor     s                   like(vdvsor)
6.20 d vsdtl1_lage     s                   like(vdlage)
     d vvarl1_vare     s                   like(vvvare)
     d vvarlb_nmod     s                   like(vvnmod)
     d vvarla_ldor     s                   like(vvldor)
     d vvarla_ogrp     s                   like(vvogrp)
     d vvarla_hgrp     s                   like(vvhgrp)
     d vvarla_ugrp     s                   like(vvugrp)
     d vvarl9_ogrp     s                   like(vvogrp)
     d vvarl9_hgrp     s                   like(vvhgrp)
     d vvarl9_ugrp     s                   like(vvugrp)
6.23 d vlagl3_bldo     s                   like(vlbldo)
6.23 d vlagl3_lage     s                   like(vllage)
      *
      * Definering av variable
      *
     d w_ogrp_alf      s              2
     d w_hgrp_alf      s              2
     d w_ugrp_alf      s              3
     d w_ldor_alf      s              6
      *
     d w_firm          s                   like(vvfirm)
     d w_tek1          s                   like(vvtek1)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.20  * prøver først å finne sortiment med lager som følger med. Hvis det ikke finnes
6.20  * sortiment med dette, settes lager lik 0.
6.20 c                   eval      vshel1_vsor = p_vsor
6.20 c                   eval      vshel1_lage = p_lage
6.20 c     vshel1_key    chain     vshel1
6.20 c                   if        %found(vshel1)
6.20 c                   eval      vsdtl1_lage = p_lage
6.20 c                   else
6.20 c                   eval      vsdtl1_lage = 0
6.20 c                   endif
      *
      * Initierer nøkkel i utplukksfil
      *
     c                   eval      w_ogrp_alf = *blank
     c                   eval      w_hgrp_alf = *blank
     c                   eval      w_ugrp_alf = *blank
     c                   eval      w_ldor_alf = *blank
     c                   eval      w_tek1     = *blank
      *
      * Leser varesortiment-linje, og fyller utplukksfil
      *
     c                   eval      vsdtl1_vsor = p_vsor
     c     vsdtl1_key    setll     vsdtl1
     c     vsdtl1_key    reade     vsdtl1
     c                   dow       not %eof
      *
     c                   eval      vvarl1_vare = vdvare
      *
     c                   eval      vvarlb_nmod = vdmodn
      *
     c                   eval      vvarla_ldor = vdldor
     c                   eval      vvarla_ogrp = vdogrp
     c                   eval      vvarla_hgrp = vdhgrp
     c                   eval      vvarla_ugrp = vdugrp
      *
     c                   eval      vvarl9_ogrp = vdogrp
     c                   eval      vvarl9_hgrp = vdhgrp
     c                   eval      vvarl9_ugrp = vdugrp
      *
      * Posisjonerer og leser i vareregister i.h.t. varesortiment-linje
      *
     c                   select
     c                   when      vdvare <> *blank
     c     vvarl1_key    chain     vvarl1                             91
     c                   when      vdmodn <> 0
     c     vvarlb_key    setll     vvarlb
     c     vvarlb_key    reade     vvarlb                                 91
     c                   when      vdldor <> 0 and
     c                             vdugrp <> 0
     c     vvarla_key1   setll     vvarla
     c     vvarla_key1   reade     vvarla                                 91
     c                   when      vdldor <> 0 and
     c                             vdhgrp <> 0
     c     vvarla_key2   setll     vvarla
     c     vvarla_key2   reade     vvarla                                 91
     c                   when      vdldor <> 0 and
     c                             vdogrp <> 0
     c     vvarla_key3   setll     vvarla
     c     vvarla_key3   reade     vvarla                                 91
     c                   when      vdldor <> 0
     c     vvarla_key4   setll     vvarla
     c     vvarla_key4   reade     vvarla                                 91
     c                   when      vdugrp <> 0
     c     vvarl9_key1   setll     vvarl9
     c     vvarl9_key1   reade     vvarl9                                 91
     c                   when      vdhgrp <> 0
     c     vvarl9_key2   setll     vvarl9
     c     vvarl9_key2   reade     vvarl9                                 91
     c                   other
     c     vvarl9_key3   setll     vvarl9
     c     vvarl9_key3   reade     vvarl9                                 91
     c                   endsl
      *
      * Behandler varer i.h.t. sortiment-linje
      *
     c                   dow       *in91 = *off
      *
     c                   if        p_svgr = *on
     c                   movel     vvogrp        w_ogrp_alf
     c                   movel     vvhgrp        w_hgrp_alf
     c                   movel     vvugrp        w_ugrp_alf
     c                   endif
     c                   if        p_sldo = *on
     c                   movel     vvldor        w_ldor_alf
     c                   endif
     c                   if        p_stek = *on
     c                   eval      w_tek1 = vvtek1
     c                   endif
      *
     c                   eval      vwakey = w_ogrp_alf + w_hgrp_alf +
     c                                      w_ugrp_alf + w_ldor_alf +
     c                                      w_tek1 + vvvare
      *
     c                   write     vvawpfr
      *
     c                   select
     c                   when      vdvare <> *blank
     c                   leave
     c                   when      vdmodn <> 0
     c     vvarlb_key    reade     vvarlb                                 91
     c                   when      vdldor <> 0 and
     c                             vdugrp <> 0
     c     vvarla_key1   reade     vvarla                                 91
     c                   when      vdldor <> 0 and
     c                             vdhgrp <> 0
     c     vvarla_key2   reade     vvarla                                 91
     c                   when      vdldor <> 0 and
     c                             vdogrp <> 0
     c     vvarla_key3   reade     vvarla                                 91
     c                   when      vdldor <> 0
     c     vvarla_key4   reade     vvarla                                 91
     c                   when      vdugrp <> 0
     c     vvarl9_key1   reade     vvarl9                                 91
     c                   when      vdhgrp <> 0
     c     vvarl9_key2   reade     vvarl9                                 91
     c                   other
     c     vvarl9_key3   reade     vvarl9                                 91
     c                   endsl
      *
     c                   enddo
      *
6.23 c                   if        vdldor <> 0 and
6.23 c                             vclage <> 0
6.23 c                   exsr      les_lager
6.23 c                   endif
      *
     c     vsdtl1_key    reade     vsdtl1
     c                   enddo
      *
      *
      * Avslutt
      *
     c                   eval      *inlr = *on
     c                   return
6.23  *
6.23  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.23  * Subrutine : tar en ekstra les av vare-lager for evt. å finne varer med
6.23  * lev. pr. lager.
6.23  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.23  *
6.23 c     les_lager     begsr
6.23  *
6.23 c                   eval      w_ogrp_alf = *blank
6.23 c                   eval      w_hgrp_alf = *blank
6.23 c                   eval      w_ugrp_alf = *blank
6.23  *
6.23 c                   eval      vlagl3_bldo = vdldor
6.23 c                   eval      vlagl3_lage = vclage
6.23 c     vlagl3_key    setll     vlagl3
6.23 c     vlagl3_key    reade     vlagl3
6.23 c                   dow       not %eof(vlagl3)
6.23 c                   eval      vvarl1_vare = vlvare
6.23 c     vvarl1_key    chain     vvarl1
6.23 c                   if        %found(vvarl1) and
6.23 c                             vvldor <> vlbldo
6.23  *
6.23 c                   if        p_svgr = *on
6.23 c                   movel     vvogrp        w_ogrp_alf
6.23 c                   movel     vvhgrp        w_hgrp_alf
6.23 c                   movel     vvugrp        w_ugrp_alf
6.23 c                   endif
6.23 c                   if        p_sldo = *on
6.23 c                   movel     vvldor        w_ldor_alf
6.23 c                   endif
6.23 c                   if        p_stek = *on
6.23 c                   eval      w_tek1 = vvtek1
6.23 c                   endif
6.23  * skriver til fil
6.23 c                   eval      vwakey = w_ogrp_alf + w_hgrp_alf +
6.23 c                                      w_ugrp_alf + w_ldor_alf +
6.23 c                                      w_tek1 + vlvare
6.23  *
6.23 c                   write     vvawpfr
6.23  *
6.23 c                   endif
6.23 c     vlagl3_key    reade     vlagl3
6.23 c                   enddo
6.23  *
6.23 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vsor
     c                   parm                    p_svgr
     c                   parm                    p_sldo
     c                   parm                    p_stek
6.20 c                   parm                    p_lage
      *
      * Key for les på varesortiment-linje
      *
     c     vsdtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vsdtl1_vsor
6.20 c                   kfld                    vsdtl1_lage
6.20  *
6.20  * Key for les på varesortiment-heading
6.20  *
6.20 c     vshel1_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    vshel1_vsor
6.20 c                   kfld                    vshel1_lage
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for les på vare med modulnr
      *
     c     vvarlb_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlb_nmod
      *
      * Key for les på vare med leverandør
      *
     c     vvarla_key1   klist
     c                   kfld                    w_firm
     c                   kfld                    vvarla_ldor
     c                   kfld                    vvarla_ogrp
     c                   kfld                    vvarla_hgrp
     c                   kfld                    vvarla_ugrp
      *
     c     vvarla_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvarla_ldor
     c                   kfld                    vvarla_ogrp
     c                   kfld                    vvarla_hgrp
      *
     c     vvarla_key3   klist
     c                   kfld                    w_firm
     c                   kfld                    vvarla_ldor
     c                   kfld                    vvarla_ogrp
      *
     c     vvarla_key4   klist
     c                   kfld                    w_firm
     c                   kfld                    vvarla_ldor
      *
      * Key for les på vare med varegruppe
      *
     c     vvarl9_key1   klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl9_ogrp
     c                   kfld                    vvarl9_hgrp
     c                   kfld                    vvarl9_ugrp
      *
     c     vvarl9_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl9_ogrp
     c                   kfld                    vvarl9_hgrp
      *
     c     vvarl9_key3   klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl9_ogrp
6.23  *
6.23 c     vlagl3_key    klist
6.23 c                   kfld                    w_firm
6.23 c                   kfld                    vlagl3_bldo
6.23 c                   kfld                    vlagl3_lage
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
