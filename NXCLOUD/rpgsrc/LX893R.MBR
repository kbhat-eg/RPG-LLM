     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LX893R
      * Beskrivelse . . : Oppdaterer varetype på lager fra vareregister
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       221208 bof  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvlaglu    uf a e           k disk    rename(vlagpfr:vlaglur)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d vlaglu_firm     s                   like(vlfirm)
     d vlaglu_vare     s                   like(vlvare)
     d vlaglu_lage     s                   like(vllage)
     d vvarl1_firm     s                   like(vvfirm)
     d vvarl1_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   read      vlagl1
     c                   dow       not %eof(vlagl1)
      *
     c                   eval      vvarl1_firm = vlfirm
     c                   eval      vvarl1_vare = vlvare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found(vvarl1) and
     c                             (vvvtyp = 'L' or
     c                             vvvtyp = 'S')
     c                   exsr      upd_lager
     c                   endif
      *
     c     les_neste     tag
     c                   read      vlagl1
     c                   enddo
      *
      *
      * Avslutt program
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å flytte poster på lager til annet lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     upd_lager     begsr
      *
     c                   eval      vlaglu_firm = vlfirm
     c                   eval      vlaglu_vare = vlvare
     c                   eval      vlaglu_lage = vllage
     c     vlaglu_key    chain     vlaglu
     c                   if        %found(vlaglu)
     c                   eval      vlvtyp = vvvtyp
     c                   update    vlaglur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for oppdatering av LAGER
      *
     c     vlaglu_key    klist
     c                   kfld                    vlaglu_firm
     c                   kfld                    vlaglu_vare
     c                   kfld                    vlaglu_lage
      *
      *  Key for les av vare
      *
     c     vvarl1_key    klist
     c                   kfld                    vvarl1_firm
     c                   kfld                    vvarl1_vare
      *
      * Henter firma
      *
      *
     c                   endsr
