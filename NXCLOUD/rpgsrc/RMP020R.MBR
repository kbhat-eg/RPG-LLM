     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : NXCLOUD
      * Program . . . . : rmp020R
      * Beskrivelse . . : Vedlikehold - MapProff-transer
      *
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       300822 sst  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frmpflr    if   e           k disk    rename(rmpfpfr:rmpflrr)
     frmpfl1    if   e           k disk    rename(rmpfpfr:rmpfl1r)
     frmpflu    uf a e           k disk    rename(rmpfpfr:rmpflur)
      *
     frmp020d   cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d  l_dato               101    108
     d  l_tek1               201    270
     d  l_tek2               301    370
     d  l_tek3               401    470
     d  l_user               911    920
     d  l_firm               944    946  0
     d  l_fnav               951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d rmpflr_dato     s                   like(rmfkda)
      *
     d rmpfl1_dato     s                   like(rmfkda)
     d rmpfl1_time     s                   like(rmfkti)
     d rmpfl1_line     s                   like(rmftel)
      *
     d rmpflu_dato     s                   like(rmfkda)
     d rmpflu_time     s                   like(rmfkti)
     d rmpflu_line     s                   like(rmftel)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
     d b_oppr          s              1    inz(*off)
     d b_bonus         s              1    inz(*off)
     d b_betb          s              1    inz(*off)
7.01 d b_newm          s              1    inz(*off)
      *
     d w_udat          s               d   datfmt(*iso)
     d w_udat_8        s              8
     d w_firm_alf      s              3
     d w_kund_alf      s              6
     d w_paar_alf      s              4
     d w_m1ar          s              4  0
     d w_naar          s              4  0
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_type          s              2
     d w_fast          s             10
6.nu d w_numm          s              8  0
     d w_kode          s              1
     d w_fell          s              6
7.01 d w_mld1          s             50
7.01 d i               s              3S 0
      *
     d w_firm          s                   like(rmffir)
     d d_line          s                   like(rmftel)
      *
     d p_ok            s              1
      *
     d b_kuti          s              1    inz(*off)
      *
      * Definering av konstanter
      *
7.xx d c_sfil          s              2  0 inz(13)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c     rmpflr_key    setll     rmpflr
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Vedlikehold av bonus
      *
     c                   if        b1valg = 2
     c                   eval      c1dato = b1dato
     c                   eval      c1time = b1time
     c                   eval      c1line = b1line
     c                   eval      rmpfl1_dato = b1kdat
     c                   eval      rmpfl1_time = b1ktim
     c                   eval      rmpfl1_line = b1klin
     c                   exsr      xc2bld
     c                   eval      b_forn = *on
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slette bonus
      *
     c                   if        b1valg = 4
     c                   exsr      xd1win
     c                   eval      b_forn = *on
     c                   eval      b1valg = 0
     c                   iter
     c                   endif
      *
      * Vise bonus
      *
     c                   if        b1valg = 5
     c                   eval      c1dato = b1dato
     c                   eval      c1time = b1time
     c                   eval      c1line = b1line
     c                   eval      rmpfl1_dato = b1kdat
     c                   eval      rmpfl1_time = b1ktim
     c                   eval      rmpfl1_line = b1klin
     c                   exsr      xc2bld
     c                   eval      b_forn = *on
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      rmpflr
      *
     c                   if        %eof or
     c                             rmffir <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c     *ymd          move      rmfkda        b1dato
     c     *hms          move      rmfkti        b1time
     c                   eval      b1line = %char(rmftel)
     c                   eval      b1kdat = rmfkda
     c                   eval      b1ktim = rmfkti
     c                   eval      b1klin = rmftel
     c                   eval      b1tran = rmfrec
     c                   eval      b1trah = rmfrec
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     rmpflr_key    setgt     rmpflr
     c                   readp     rmpflr
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     rmpflr
      *
     c                   if        %eof or
     c                             rmffir <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   enddo
      *
     c                   if        %eof
     c     rmpflr_key    setll     rmpflr
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c     rmpfl1_key    chain     rmpfl1
      *
     c                   if        %found
     c                   eval      c1dato = b1dato
     c                   eval      c1time = b1time
     c                   eval      c1line = b1line
     c                   eval      c2kdat = b1kdat
     c                   eval      c2ktim = b1ktim
     c                   eval      c2klin = b1klin
      *  Overføre semikolon separert rec til felter
     c                   eval      i = 200
     c                   dow       i > 1
     c                             and %subst(b1trah:i:1) = ' '
     c                   eval      i = i - 1
     c                   enddo
      *
     c                   eval      %subst(b1trah:i+1:10) = ';;;;;;;;;;'
      *
      *  GLN-nummer
     c                   eval      i = %scan(';':b1trah)
     c                   eval      c2gln  = %trim(%subst(b1trah:1:i-1))
     c                   eval      b1trah = %subst(b1trah:i+1:200-i)
      *  Dato
     c                   eval      i = %scan(';':b1trah)
     c                   eval      c2dato = %trim(%subst(b1trah:1:i-1))
     c                   eval      b1trah = %subst(b1trah:i+1:200-i)
      *  Tid
     c                   eval      i = %scan(';':b1trah)
     c                   eval      c2time = %trim(%subst(b1trah:1:i-1))
     c                   eval      b1trah = %subst(b1trah:i+1:200-i)
      *  Linjenr
     c                   eval      i = %scan(';':b1trah)
     c                   eval      c2line = %trim(%subst(b1trah:1:i-1))
     c                   eval      b1trah = %subst(b1trah:i+1:200-i)
      *  EAN-nummer  Gtin
     c                   eval      i = %scan(';':b1trah)
     c                   eval      c2ean  = %trim(%subst(b1trah:1:i-1))
     c                   eval      b1trah = %subst(b1trah:i+1:200-i)
      *  Antall
     c                   eval      i = %scan(';':b1trah)
     c                   eval      c2ant  = %trim(%subst(b1trah:1:i-1))
     c                   eval      b1trah = %subst(b1trah:i+1:200-i)
      *  Ordrenr
     c                   eval      i = %scan(';':b1trah)
     c                   eval      c2onr  = %trim(%subst(b1trah:1:i-1))
     c                   eval      b1trah = %subst(b1trah:i+1:200-i)
      *  Avdeling    Navn
     c                   eval      i = %scan(';':b1trah)
     c                   eval      c2avd  = %trim(%subst(b1trah:1:i-1))
     c                   eval      b1trah = %subst(b1trah:i+1:200-i)
      *  Kundenr.   MalProff
     c                   eval      i = %scan(';':b1trah)
     c                   eval      c2knr  = %trim(%subst(b1trah:1:i-1))
     c                   eval      b1trah = %subst(b1trah:i+1:200-i)
      *  Kundenavn  MalProff
     c                   eval      i = %scan(';':b1trah)
     c                   eval      c2kun  = %trim(%subst(b1trah:1:i-1))
     c                   eval      b1trah = %subst(b1trah:i+1:200-i)
      *
     c                   eval      c2eusr = rmfeus
     c                   if        rmfeda <> *loval
     c     *dmy          move      rmfeda        c2edat
     c                   endif
     c                   if        rmfeti <> *loval
     c                   move      rmfeti        c2etid
     c                   endif
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   eval      *in31 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
     c                   if        *inkf
     c                   eval      *in31 = *off
     c                   goto      c2taga
     c                   endif
      *
      * Validering av input
      *
     c**                 if        c2dato = *blank
     c*                  eval      *in31 = *on
     c*                  goto      c2taga
     c*                  endif
      *
      * Validering av input
      *
     c*                  if        c2hbka < 0 or
7.02 c*                            c2hbka > 6
     c*                  eval      *in33 = *on
     c*                  goto      c2taga
     c*                  endif
      *
      * Oppdatering
      *
     c                   clear                   rmpflur
      *
     c                   eval      rmpflu_dato = c2kdat
     c                   eval      rmpflu_time = c2ktim
     c                   eval      rmpflu_line = c2klin
     c     rmpflu_key    chain     rmpflur
      *
     c                   time                    rmfeda
     c                   time                    rmfeti
     c                   eval      rmfeus = l_user
     c                   eval      rmfrec = %trim(c2gln) + ';' +
     c                                      %trim(c2dato) + ';' +
     c                                      %trim(c2time) + ';' +
     c                                      %trim(c2line) + ';' +
     c                                      %trim(c2ean) + ';' +
     c                                      %trim(c2ant) + ';' +
     c                                      %trim(c2onr) + ';' +
     c                                      %trim(c2avd) + ';' +
     c                                      %trim(c2knr) + ';' +
     c                                      %trim(c2kun) + ';'
      *
     c                   if        %found
     c                   update    rmpflur
     c                   endif
      *
      * Oppdater subfile
      *
     c*                  if        b1valg = 2
     c*                  eval      b1time = vatime
     c*                  endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
     c                   eval      *in31  = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sletting av bonus
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
      * Sletter bonus
      *
     c                   eval      d1dato = b1dato
     c                   eval      d1time = b1time
     c                   eval      d1line = b1line
     c                   eval      d1kdat = b1kdat
     c                   eval      d1ktim = b1ktim
     c                   eval      d1klin = b1klin
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * Sletter bonusnivå
      *
     c                   eval      b_forn = *on
      *
     c                   eval      rmpflu_dato = d1kdat
     c                   eval      rmpflu_time = d1ktim
     c                   eval      rmpflu_line = d1klin
     c     rmpflu_key    chain     rmpflur
     c                   delete    rmpflur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
6.23  *
6.23  * Kunde
6.23  *
6.23 c*                  if        w_afld = 'E1KUND'
6.23 c*                  call      'RK500R'
6.23 c*                  parm                    w_firm
6.23 c*                  parm                    e1kund
6.23 c*                  parm                    e1ktxt
6.23 c*                  leavesr
6.23 c*                  endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på bonusregister
      *
     c     rmpflr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rmpflr_dato
      *
      * Key for oppslag på bonusregister
      *
     c     rmpfl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rmpfl1_dato
     c                   kfld                    rmpfl1_time
     c                   kfld                    rmpfl1_line
      *
      * Key for oppslag på bonusregister
      *
     c     rmpflu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rmpflu_dato
     c                   kfld                    rmpflu_time
     c                   kfld                    rmpflu_line
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
     c                   time                    w_date
     c                   time                    w_time
      *
      * Initierer skjermbildet
      *
     c                   eval      b1dato = *blank
     c                   eval      b1time = *blank
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c     rmpflr_key    setll     rmpflr
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
