     h option(*nodebugio) datedit(*dmy)
     h decedit('0.')
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : RB003R
      * Beskrivelse . . : Leser linjer fra BOVF til kommaseparert til
      *                   (XLedger)
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * ----- ------ ---- -------------------------------------------------
      *       181012 nho  Visma  Business
6.22  * V6.20 051113 nho  Avstand mellom felter
6.23  * V6.20 141014 nho  Test på firma
6.24  * V6.20 120515 nho  Val.dato skal være lik bilagsdato
6.nu  * V6.30 230614 bof  sjekket mtp.nummerutvidelse
7.01  *K00    150321 sst  XLedger
7.03  *       120521 mst  Henter avvikende momskode, hvis den finnes
7.04  *K00    310521 sst  Ikke fakturanr m.m. på salgsposteringer
7.05  *       200921 sst  Justert 7.04 vedr test på kundenr 99999/10000
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     fbovfl1    ip   e           k disk
     fsohel1    if   e           k disk    rename(sohepfr:sohel1r)
7.00 frb10l1    if   e           k disk    rename(rb10pfr:rb10l1r)
7.03 frb00l1    if   e           k disk    rename(rb00pfr:rb00l1r)
      *
      *
     frv02pf    o    e           k disk
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
      *
     d valdat          s                   like(bffdat)
     d nval08          s              8  0
     d nval04          s              4  0
     d nfor08          s              8  0
     d nfor04          s              4  0
     d nbil08          s              8  0
     d nbil04          s              4  0
     d tell            s              1  0
     d f1momd          s                   like(bfdmom)
     d f1momc          s                   like(bfcmom)
6.30 d f1avde          s                   like(bfcavd)
     d zfbelp          s             12  4
     d w_binr          s                   like(sobinr)
     d w_firm          s                   like(sofirm)
     d w_aarr          s                   like(soaarr)
7.01 d x10             s             10
7.01 d x8              s              8
7.01 d xxbilk          s              5  0
7.01 d rb10l1_gkod     s                   like(raakod)
7.03 d rb00l1_gkod     s                   like(r00kod)
7.03 d w_type          s             10
7.04 d w_biln          s             10
7.04 d w_for08         s             10
      *
      * Datastruktur for valuteringsdato
     d                 ds
     d d_fdati                 1     10
     d  d_fa                   1      4
     d  d_f1                   5      5
     d  d_fn                   6      7
     d  d_f2                   8      8
     d  d_fd                   9     10
     d*
      * Nøkkel dato
     d                 ds
     d d_dati                  1     10
     d  d_aa                   1      4
     d  d_a1                   5      5
     d  d_mn                   6      7
     d  d_a2                   8      8
     d  d_dd                   9     10
      *
      *
      * Datastruktur for forfallsdato
     d                 ds
     d f_fdati                 1     10
     d  f_fa                   1      4
     d  f_f1                   5      5
     d  f_fn                   6      7
     d  f_f2                   8      8
     d  f_fd                   9     10
     d*
      *
      * Datastruktur for bilagsdato
     d                 ds
     d b_fdati                 1     10
     d  b_fa                   1      4
     d  b_f1                   5      5
     d  b_fn                   6      7
     d  b_f2                   8      8
     d  b_fd                   9     10
     d*
      *
      * Key for oppslag på faktura-hode-register
     c     sohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_aarr
     c                   kfld                    w_binr
7.03  * Key for posisjonering på koder (moms)
7.03  *
7.03 c     rb00l1_key    klist
7.03 c                   kfld                    w_firm
7.03 c                   kfld                    w_type
7.03 c                   kfld                    rb00l1_gkod
      *
      *
6.23  *
6.23 c                   if        l_firm <> bffirm
6.23 c                   goto      slutt
6.23 c                   endif
     c* Heading for kjøring
     c                   if        tell   = 0
     c                   add       1             tell
     c                   eval      rvdata = *blank
7.02  *   Linje 1
7.02 c                   eval      rvdata = %trim('@WaBnd(Descr, ValDt, SrcTp)')
     c                   write     rv02pfr
7.02  *
7.02  *   Linje 2
7.02 c                   eval      rvdata = *blank
7.02 c                   write     rv02pfr
7.02  *
7.02  *   Linje 3
7.02 c                   eval      rvdata = *blank
7.02 c                   time                    valdat
7.02 c                   movel     valdat        x10
7.02 c                   eval      x8 = %subst(x10:1:4) +
7.02 c                                  %subst(x10:6:2) +
7.02 c                                  %subst(x10:9:2)
7.02 c                   eval      rvdata = %trim('Kasse.fil;' +
7.02 c                                      x8 + ';12')
7.02 c                   write     rv02pfr
7.02  *
7.02  *   Linje 4
7.02 c                   eval      rvdata = *blank
7.02 c                   write     rv02pfr
7.02  *
7.02  *   Linje 5
7.02 c                   eval      rvdata = *blank
7.02 c                   eval      rvdata = %trim('@WaVo(VoNo,VoDt,txt,VoTp,Am,+
7.02 c                                             DbAcNo,CrAcNo,+
7.02 c                                             DbTrnC1,CrTrnC1,DbTxCd,+
7.02 c                                             CrTxCd,DbAcC1,CrAcC1,+
7.02 c                                             R1,R2,InvoNo,AgRef,DueDt,+
7.02 c                                             Cid')
7.02 c                   write     rv02pfr
7.02  *
7.02  *   Linje 6
7.02 c                   eval      rvdata = *blank
7.02 c                   write     rv02pfr
      *
     c                   eval      rvdata = *blank
      *
      *
      * Dagense dato
     c                   time                    valdat
     c                   movel     valdat        d_fdati
     c                   movel     d_fa          nval08
     c                   movel     d_fn          nval04
     c                   move      d_fd          nval04
     c                   move      nval04        nval08
      *
     c                   eval      rvdata = *blank
      *
     c                   endif
      *
      ***********************************************************
     c
     c* Blanker felt
     c                   eval      d_dati  = *blanks
      *
     c                   move      bfbdat        d_dati
      *
      * Forfallsdato
     c                   movel     bffdat        f_fdati
     c                   movel     f_fa          nfor08
     c                   movel     f_fn          nfor04
     c                   move      f_fd          nfor04
     c                   move      nfor04        nfor08
      *
      * Bilagsdato
     c                   movel     bfbdat        b_fdati
     c                   movel     b_fa          nbil08
     c                   movel     b_fn          nbil04
     c                   move      b_fd          nbil04
     c                   move      nbil04        nbil08
     c*
      * Beløp skal legges ut med 4 desimaler
     c                   z-add     bfbelp        zfbelp
      * Legger ut detaljer fra bovf
      * Nullstiller
     c                   eval      f1momd = '0'
     c                   eval      f1momc = '0'
      *
     c                   exsr      control_init
      *
7.00  * Sjekker om bilagstype skal konverteres
7.00 c                   eval      xxbilk = bfbilk
7.00 c                   eval      rb10l1_gkod = bfbilk
7.00 c     rb10l1_key    chain     rb10l1
7.00 c                   if        %found
7.00 c                   eval      xxbilk = rankod
7.00 c                   endif
7.00  *
     c                   if        bfdkto <> *zeros and
     c                             bfdkto >=  1000 and
     c                             bfdkto <=  9999
     c                   eval      f1momd = bfdmom
7.03  * Sjekker om momskode  skal konverteres
7.03 c                   eval      w_type      = 'MVAKODE'
7.03 c                   move      f1momd        rb00l1_gkod
7.03 c     rb00l1_key    chain     rb00l1
7.03 c                   if        %found
7.03 c                   movel     r00nko        f1momd
7.03 c                   endif
     c                   endif
      *
     c                   if        bfckto <> *zeros and
     c                             bfckto >=  1000 and
     c                             bfckto <=  9999
     c                   eval      f1momc = bfcmom
     c                   eval      f1momd = '0'
7.03  * Sjekker om momskode  skal konverteres
7.03 c                   eval      w_type      = 'MVAKODE'
7.03 c                   move      f1momc        rb00l1_gkod
7.03 c     rb00l1_key    chain     rb00l1
7.03 c                   if        %found
7.03 c                   movel     r00nko        f1momc
7.03 c                   endif
     c                   endif
6.30  * Legger ut avdeling
6.30 c                   eval      f1avde = *zeros
6.30  *
6.30 c                   if        bfcavd <> *zeros
6.30 c                   eval      f1avde = bfcavd
6.30 c                   endif
6.30  *
6.30 c                   if        bfdavd <> *zeros
6.30 c                   eval      f1avde = bfdavd
6.30 c                   endif
      *
7.04 c                   eval      w_biln  = %char(bfbiln)
7.04 c                   eval      w_for08 = %char(nfor08)
7.04  *
7.04  *  Enklete felter skal kun ut dersom det er kundenr i record
7.05 c                   if        bfckto < 10000
7.05 c                             and bfdkto < 10000
7.04 c                   eval      sokidd  = *blank
7.04 c                   eval      w_for08 = *blank
7.04 c                   eval      w_biln  = *blank
7.04 c                   endif
7.04  *
     c**********
     c                   eval      rvdata = *blank
      * Start
     c*                  eval      rvdata =  +
      * Bilagsnr.
     c                   eval      rvdata = %char(bfbiln)     + ';' +
      * Bil.dato
     c                                      %char(nbil08)     + ';' +
      * Val.dat fra heading
6.24 c                                      %char(nbil08)     + ';' +
      * Bil.type
7.01 c                                      %char(xxbilk)     + ';' +
      * Bil.tekst
7.01 c**                                    %trim(bftext)     + ';' +
7.01  * Beløp på linje
7.01 c                                      %char(zfbelp)     + ';' +
7.01  * Deb.konto
7.01 c                                      %char(bfdkto)     + ';' +
7.01  * Kredkonto
7.01 c                                      %char(bfckto)     + ';' +
7.01  * DBTmC1
7.01 c                                      ''                + ';' +
7.01  * CrTmC1
7.01 c                                      ''                + ';' +
      * Deb.mva kode
     c                                      %trim(f1momd)     + ';' +
      *
      * Kredit mva kode
     c                                      %trim(f1momc)     + ';' +
7.02  * DBAcC1
7.02 c                                      ''                + ';' +
7.02  * CrAcC1
7.02 c                                      ''                + ';' +
7.01  * R1  Avdeling??
7.01 c                                      %char(f1avde)     + ';' +
7.01  * R2  Prosjekt ??
7.01 c                                      %trim(bfproj)     + ';' +
      * Fakturanr (Bilagsnummer)
7.04 c                                      %trim(w_biln)     + ';' +
7.01  *
7.01  * AgRef  ??
7.01 c                                      ''                + ';' +
      *
      * Forfallsdato
     c                                      %trim(w_for08)     + ';' +
      * Kid nr ???????
     c                                      %trim(sokidd)
      *
     c                   write     rv02pfr
      *
6.23 c     slutt         tag
      *
     c     dd999         tag
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     control_init  begsr
      *
     c                   eval      w_firm = bffirm
     c                   eval      w_binr = bfbiln
     c                   move      d_aa          w_aarr
      * Finner siste faktura-hode for faktura
      *
     c     sohel1_key    setll     sohel1
     c     sohel1_key    reade     sohel1                                 90
     c                   if        *in90 = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   dow       *in90 = *off
     c     sohel1_key    reade     sohel1                                 90
     c                   enddo
      *
     c     avslutt       tag
     c                   endsr
      *
     c     *inzsr        begsr
      *
7.00  * Key for posisjonering på bilagskoder
7.00  *
7.00 c     rb10l1_key    klist
7.00 c                   kfld                    w_firm
7.00 c                   kfld                    rb10l1_gkod
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
