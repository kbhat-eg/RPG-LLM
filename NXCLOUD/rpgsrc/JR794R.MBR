     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JR794R
      * Beskrivelse . . : Leser fil på recordformat fra Felleskjøpet,
      *                   oppretter nye varer og oppdaterer priser på
      *                   eksisterende varer i LVR.
      *
      *                   Prisoppdatering skjer på bakgrunn av match på
      *                   leverandørens varenummer.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       221104 GRO  Nytt program
      *       140905 HKO  Byttet program for oppdatering av søktekster
6.10  *       160210 BSA  Endringer i databasefile
6.12  *       081010 bof  Lagt til JVPENH til jvarlu
6.30  * 6.30  280113 bof  Omskriving i forb. endring pakning VA
6.31  * 6.30  200217 bof  Uvidet med JVLEPF
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjrvapf    if   e           k disk
      *
     fjrapl1    if   e           k disk    rename(jrappfr:jrapl1r)
     fjvarl8    if   e           k disk    rename(jvarpfr:jvarl8r)
     fjvarlu    uf a e           k disk    rename(jvarpfr:jvarlur)
     fjvdtlu    o  a e           k disk    rename(jvdtpfr:jvdtlur)
     fjstslu    uf   e           k disk    rename(jstspfr:jstslur)
     fjvpklu    uf a e           k disk    rename(jvpkpfr:jvpklur)
     fjvprlu    uf a e           k disk    rename(jvprpfr:jvprlur)
     ffenkl1    if   e           k disk    rename(fenkpfr:fenkl1r)
     fvenhl1    if   e           k disk    rename(venhpfr:venhl1r)
     fjrvgl1    if   e           k disk    rename(jrvgpfr:jrvgl1r)
6.31 fjvlelu    uf a e           k disk    rename(jvlepfr:jvlelur)
      *
      * Definering av parametre
      *
     d p_list          s             32
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
      *
      * Definering av parameterliste -inn
      *
     d                 ds
     d d_list                        32
     d  d_firm                        3  0 overlay(d_list)
     d  d_rapp                        2  0 overlay(d_list:4)
     d  d_dato                         d   overlay(d_list:6) datfmt(*iso)
      *
      *  Recordbeskrivelse - Felleskjøpet
      *
     d                 ds
     d d_inpu                       200
     d  d_utvk                        1    overlay(d_inpu:2)                    Utvalgskode
     d  d_lvnr                        8    overlay(d_inpu:5)                    Varenr
     d  d_eann                       13    overlay(d_inpu:14)                   EAN-nr
     d  d_spst                        1    overlay(d_inpu:29)                   Sperrestatus
     d  d_enhe                        5    overlay(d_inpu:33)                   Enhet
     d  d_enhv                       12    overlay(d_inpu:40)                   Enhetsvekt
     d   d_enhv_h                     8  0 overlay(d_enhv:1)
     d   d_enhv_d                     3  3 overlay(d_enhv:10)
     d  d_vnvn                       24    overlay(d_inpu:54)                   Varenavn
     d  d_pris                       11    overlay(d_inpu:80)                   Pris
     d   d_pris_h                     8  0 overlay(d_pris:1)
     d   d_pris_d                     2  2 overlay(d_pris:10)
     d  d_pkod                        1    overlay(d_inpu:93)                   Priskode(C=pr 100kg)
     d  d_vhgr                        6    overlay(d_inpu:96)                   Varehovedgruppe
     d  d_avgi                        9    overlay(d_inpu:103)                  Avgift
     d   d_avgi_h                     6  0 overlay(d_avgi:1)
     d   d_avgi_d                     2  2 overlay(d_avgi:8)
     d  d_pkkv                        6    overlay(d_inpu:113)                  Pakningskvantum
     d  d_plkv                        6    overlay(d_inpu:120)                  Pallekvantum
     d  d_rabp                        6    overlay(d_inpu:127)                  Rabatt
     d   d_rabp_h                     3  0 overlay(d_rabp:1)
     d   d_rabp_d                     2  2 overlay(d_rabp:5)
      *
      * Definering av variable i nøkler
      *
     d jrapl1_rapp     s                   like(jrrapp)
     d jvarl8_ldor     s                   like(jvldor)
     d jvarl8_lvar     s                   like(jvlvar)
     d jvarlu_vare     s                   like(jvvare)
     d jvpklu_vare     s                   like(jwvare)
     d jvprlu_vare     s                   like(jxvare)
     d fenkl1_enhf     s                   like(feenhf)
     d venhl1_eenh     s                   like(vaeenh)
     d jrvgl1_grap     s                   like(jrgrap)
     d jrvgl1_gvgr     s                   like(jrgvgr)
6.31 d jvlelu_lvnr     s                   like(jvlvnr)
6.31 d jvlelu_lldo     s                   like(jvlldo)
      *
      * Definering av variable
      *
     d b_vare          s              1    inz(*off)
     d b_inlr          s              1
      *
     d w_firm          s              3  0
     d w_enhv          s             11  3
     d w_pris          s              9  2
     d w_avgi          s              9  2
     d w_rabp          s              5  2
     d w_prix          s             15  5
      *
     d w_eann          s                   like(jvean1)
     d w_stat          s                   like(jvstat)
      *
      * Definering av konstanter
      *
     d c_digi          s             15    inz('0123456789 ')
      *
     d Lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d Up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter rapportør-informasjon
      *
     c                   eval      jrapl1_rapp = d_rapp
     c     jrapl1_key    chain     jrapl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
      * Leser fil.
      *
     c                   read      jrvapf
     c                   dow       not %eof
     c                   exsr      behandling
     c                   read      jrvapf
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av varer/priser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
     c                   eval      d_inpu = jrvrad
      *
     c                   if        d_lvnr = *blank
     c                   goto      end_oppdat
     c                   endif
      *
     c                   eval      d_lvnr = %trim(d_lvnr)
      *
      * Varer med sperrestatus tas med?
      *
     c*                  if        d_spst = 'J'
     c*                  goto      end_oppdat
     c*                  endif
      *
      * Utleder varetekst
      *
     c                   eval      d_vnvn = %trim(d_vnvn)
      *
     c     X'46':'Æ'     xlate     d_vnvn        d_vnvn
     c     X'A0':'æ'     xlate     d_vnvn        d_vnvn
     c     X'77':'Ø'     xlate     d_vnvn        d_vnvn
     c     X'90':'ø'     xlate     d_vnvn        d_vnvn
     c     X'2A':'Å'     xlate     d_vnvn        d_vnvn
     c     X'EF':'å'     xlate     d_vnvn        d_vnvn
     c     X'46':'Æ'     xlate     d_enhe        d_enhe
     c     X'A0':'æ'     xlate     d_enhe        d_enhe
     c     X'77':'Ø'     xlate     d_enhe        d_enhe
     c     X'90':'ø'     xlate     d_enhe        d_enhe
     c     X'2A':'Å'     xlate     d_enhe        d_enhe
     c     X'EF':'å'     xlate     d_enhe        d_enhe
      *
     c     Lo:Up         xlate     d_vnvn        d_vnvn
     c     Lo:Up         xlate     d_enhe        d_enhe
      *
      * Korr. vekt, pris og rabattpros fra alfanum.felt
      *
     c                   eval      d_enhv = %xlate(' ':'0':d_enhv)
     c                   eval      w_enhv = d_enhv_h + d_enhv_d
     c                   eval      d_pris = %xlate(' ':'0':d_pris)
     c                   eval      w_pris = d_pris_h + d_pris_d
     c                   eval      d_avgi = %xlate(' ':'0':d_avgi)
     c                   eval      w_avgi = d_avgi_h + d_avgi_d
     c                   eval      d_rabp = %xlate(' ':'0':d_rabp)
     c                   eval      w_rabp = d_rabp_h + d_rabp_d
      *
      * Starter oppdatering
      *
     c                   clear                   jvarlur
     c                   eval      b_vare = *off
      *
     c                   eval      jvarl8_ldor = jrldor
     c                   eval      jvarl8_lvar = d_lvnr
     c     jvarl8_key    chain     jvarl8
     c                   if        %found
     c                   exsr      endr_vare
     c                   eval      b_vare = *on
     c                   else
     c                   exsr      ny_vare
     c                   endif
      *
     c                   eval      jvarlu_vare = jvvare
     c     jvarlu_key    chain     jvarlur
      *
      * Oppretter eller oppdaterer vare
      *
     c                   exsr      sjk_ean
      *
      * Finner varegruppe på bakgrunn av rapportørens varegruppe
      *
     c                   eval      jvogrp = 0
     c                   eval      jvhgrp = 0
     c                   eval      jvugrp = 0
      *
     c                   if        d_vhgr <> *blank
      *
     c                   eval      jrvgl1_grap = d_rapp
     c                   eval      jrvgl1_gvgr = %trim(d_vhgr)
     c     jrvgl1_key    chain     jrvgl1
     c                   if        %found
     c                   eval      jvogrp = jrgogr
     c                   eval      jvhgrp = jrghgr
     c                   eval      jvugrp = jrgugr
     c                   endif
      *
     c                   endif
      *
     c                   eval      jvtek1 = d_vnvn
     c                   eval      jvldor = jrldor
     c                   eval      jvlvar = d_lvnr
     c                   eval      jvpvar = *blank
     c                   eval      jvean1 = w_eann
     c                   eval      jveusr = l_user
     c                   time                    jvetim
     c                   time                    jvedat
6.12 c                   eval      jvpenh = *blank
6.12  *
6.12 c                   eval      venhl1_eenh = %subst(d_enhe:1:3)
6.12 c     venhl1_key    chain     venhl1r
6.12 c                   if        %found
6.12 c                   eval      jvpenh = %subst(d_enhe:1:3)
6.12 c                   endif
6.12  *
6.12 c                   if        jvpenh = *blank
6.12 c                   eval      fenkl1_enhf = %subst(d_enhe:1:3)
6.12 c     fenkl1_key    chain     fenkl1
6.12 c                   if        %found
6.12 c                   eval      jvpenh = feenht
6.12 c                   else
6.12 c                   eval      fenkl1_enhf = %subst(d_enhe:1:3)
6.12 c                   endif
6.12 c                   else
6.12 c                   eval      jvpenh = 'STK'
6.12 c                   endif
      *
      * Oppdaterer eller oppretter vare
      *
     c                   if        b_vare = *on
     c                   eval      jvstat = w_stat
     c                   update    jvarlur
     c                   else
     c                   write     jvarlur
     c                   endif
      *
      * Oppretter vare i vare-detalj
      *
     c                   if        b_vare = *off
     c                   clear                   jvdtlur
     c                   eval      jvdfir = w_firm
     c                   eval      jvdvar = jvvare
     c                   eval      jvdhko = 1
     c                   write     jvdtlur
     c                   endif
      *
      * Oppretter vare-pakning
      *
     c                   exsr      oppd_pakning
      *
      * Oppretter vare-pris
      *
     c                   exsr      oppd_pris
      *
      * Oppretter/oppdaterer søketekster
      *
     c                   eval      b_inlr = *on
      *
     c                   call      'JV790R'
     c                   parm                    w_firm
     c                   parm                    jvvare
     c                   parm                    b_inlr
      *
     c     end_oppdat    endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av endret vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Dersom varen allerede finnes i vareregisteret, slettes
      * eksisterende vare-pakninger og vare-priser
      *
     c     endr_vare     begsr
      *
      * Dersom varestatus forskjellig fra 0, 1 og 4 beholdes varestatus
      *
     c                   if        jvstat = 0 or
     c                             jvstat = 1 or
     c                             jvstat = 4
     c                   eval      w_stat = 1
     c                   else
     c                   eval      w_stat = jvstat
     c                   endif
      *
      * Sletter forpakninger som ikke er registert manuelt
      *
     c                   eval      jvpklu_vare = jvvare
     c     jvpklu_key    setll     jvpklu
     c     jvpklu_key    reade     jvpklur
     c                   dow       not %eof
     c                   if        jwrsts = 0
     c                   delete    jvpklur
     c                   else
     c                   unlock    jvpklu
     c                   endif
     c     jvpklu_key    reade     jvpklur                                91
     c                   enddo
      *
      * Sletter priser som ikke er registrert manuelt
      *
     c                   eval      jvprlu_vare = jvvare
     c     jvprlu_key    setll     jvprlu
     c     jvprlu_key    reade     jvprlur
     c                   dow       not %eof
     c                   if        jxrsts = 0
     c                   delete    jvprlur
     c                   else
     c                   unlock    jvprlu
     c                   endif
     c     jvprlu_key    reade     jvprlur
     c                   enddo
6.31  *
6.31  * Sletter eventuelt vare-leverandør
6.31  *
6.31 c                   eval      jvlelu_lvnr = jvvare
6.31 c                   eval      jvlelu_lldo = jrldor
6.31 c     jvlelu_key    setll     jvlelu
6.31 c     jvlelu_key    reade     jvlelur
6.31 c                   dow       not %eof
6.31 c                   delete    jvlelur
6.31 c     jvlelu_key    reade     jvlelur
6.31 c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av ny vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ny_vare       begsr
      *
     c                   eval      jvstat = 2
      *
      * Henter neste ledige varenummer, og oppdaterer statusregisteret
      *
     c     jstslu_key    chain     jstslu                                       Henter neste
     c                   eval      jaavsi = jaavsi + 1
     c                   update    jstslur
      *
     c                   movel     jaavsi        jvvare
      *
      * Opprettelsesdato
      *
     c                   time                    jvodat
     c                   time                    jvadat
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting av vare-pakning
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_pakning  begsr
      *
     c                   clear                   jvpklur
      *
     c                   eval      jwvare = jvvare
6.30 c                   eval      jwldor = jrldor
     c                   eval      jwpakn = '000'
     c                   eval      jwofak = 1
     c                   eval      jwrsts = 0
6.10 c**                 eval      jwbest = 0
     c                   eval      jweusr = l_user
      *
     c                   time                    jwodat
     c                   time                    jwedat
     c                   time                    jwetim
      *
     c                   eval      jwenhe = *blank
      *
     c                   eval      venhl1_eenh = %subst(d_enhe:1:3)
     c     venhl1_key    chain     venhl1r
     c                   if        %found
     c                   eval      jwenhe = %subst(d_enhe:1:3)
     c                   endif
      *
     c                   if        jwenhe = *blank
     c                   eval      fenkl1_enhf = %subst(d_enhe:1:3)
     c     fenkl1_key    chain     fenkl1
     c                   if        %found
     c                   eval      jwenhe = feenht
     c                   else
     c                   eval      fenkl1_enhf = %subst(d_enhe:1:3)
     c                   endif
     c                   else
     c                   eval      jwenhe = 'STK'
     c                   endif
      *
     c                   write     jvpklur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting av vare-pris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_pris     begsr
      *
     c                   clear                   jvprlur
      *
      * Sjekk om pris pr. 100 kg
      *
     c                   if        d_pkod = 'C'
     c                   eval      w_prix = (w_pris / 100) * w_enhv
     c                   eval(h)   jxpris = w_prix
     c                   else
     c                   eval      jxpris = w_pris
     c                   endif
      *
     c                   eval      jxvare = jvvare
     c                   eval      jxldor = jvldor
     c                   eval      jxlvar = jvlvar
     c                   eval      jxvalu = *blank
     c                   eval      jxgdat = d_dato
     c                   eval      jxtdat = *loval
     c                   eval      jxrsts = 0
     c                   eval      jxeusr = l_user
      *
     c                   time                    jxodat
     c                   time                    jxedat
     c                   time                    jxetim
      *
     c                   write     jvprlur
6.31  *
6.31  * Oppretter vare leverandør
6.31  *
6.31 c                   clear                   jvlelur
6.31  *
6.31 c                   eval      jvlvnr = jvvare
6.31 c                   eval      jvlldo = jvldor
6.31 c                   eval      jvlvei = 1
6.31 c                   eval      jvleus = l_user
6.31 c                   eval      jvlous = l_user
6.31  *
6.31 c                   time                    jvloda
6.31 c                   time                    jvleda
6.31 c                   time                    jvloti
6.31 c                   time                    jvleti
6.31 c                   write     jvlelur
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kontroll og oppd. av EAN-nr.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_ean       begsr
      *
     c                   eval      w_eann = 0
      *
     c     c_digi        check     d_eann
     c                   if        not %found
     c                   if        %len(%trim(d_eann)) > 12
     c                   move      d_eann        w_eann
     c                   leavesr
     c                   endif
     c                   endif
     c
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_list
      *
      * Key for oppslag på rapportør
      *
     c     jrapl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jrapl1_rapp
      *
      * Key for oppslag på vare m/lev.varenummer
      *
     c     jvarl8_key    klist
     c                   kfld                    jvarl8_ldor
     c                   kfld                    jvarl8_lvar
      *
      * Key for oppdatering av vare
      *
     c     jvarlu_key    klist
     c                   kfld                    jvarlu_vare
      *
      * Key for oppdatering av vare-pakning
      *
     c     jvpklu_key    klist
     c                   kfld                    jvpklu_vare
      *
      * Key for oppdatering av vare-pris
      *
     c     jvprlu_key    klist
     c                   kfld                    jvprlu_vare
      *
      * Key for oppdatering av kode/status
      *
     c     jstslu_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på enhet-konvertering
      *
     c     fenkl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fenkl1_enhf
      *
      * Key for oppslag på enhet (EVR)
      *
     c     venhl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    venhl1_eenh
      *
      * Key for oppslag på rapportør varegruppereferanse-register
      *
     c     jrvgl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jrvgl1_grap
     c                   kfld                    jrvgl1_gvgr
6.31  *
6.31  * Key for oppdatering av vare-pris
6.31  *
6.31 c     jvlelu_key    klist
6.31 c                   kfld                    jvlelu_lvnr
6.31 c                   kfld                    jvlelu_lldo
      *
      *
      * Fyller datastruktur fra parameter, og henter firma
      *
     c                   eval      d_list = p_list
     c                   eval      w_firm = d_firm
      *
     c                   endsr
