     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RH621R                                              *
      * Beskrivelse . . : Hovedbok - saldobalanse - utskrift                  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
T400  * V4.00     050302  Endret utskrift av seleksjoner                  HFL *
T400  *           050302  Seleksjon kostnadsbærer                         HFL *
T400  * V4.00     080402  Endret beregning av oppnådd/brukt               HFL *
T400  * V4.00     260402  Lagt inn parameter om bærer skal spesifiseres   HFL *
T500  * V5.00     270502  Sammenligning mot alternativt budsjett          KOB *
T510  * V5.00     300403  Lagt inn at tekst hentes fra kostnadsbærer      KOB *
T5.20 * V5.20     221203  4 felt har økt lengde fra 11.2 til 13.2.        HFL *
T5.20 * V5.20     120504  Endring i sammenligning med alt. budsjett.      HFL *
T5.30 * V5.30     260704  Skriver kontonavn i sumlinje pr. konto.         HFL *
T5.40 * V5.40     220806  Linjeskift rettet når type avvik = D.           HFL *
T5.40 * V5.40     060906  Utvidet en del felt                             HFL *
T5.50 * V5.50     230507  Lagt inn parameter om avd. skal spesifiseres.   HFL *
T5.50 * V5.50     210607  Når utskrift pr. kostnadsbærer, skrives konto-  HFL *
      *                   tekst på linjen.                                HFL *
pdf   * R6.10     120710  Innført PDF utskrift. Innebærer primært å finne BSA *
pdf   *                   generert spool, og kall av AP620R med dette som     *
pdf   *                   parametre.                                          *
6.11  * R6.10 071010 BSA  Overfører bacthtype som en del parameterlista. Viser
6.11  *                   hva slags rutine som kjøres. Legges med i arkivet.
6.20  * 6.20  051212 bsa  Tillegg for å berike metataggger
6.30  * R6.30 180816 bsa  Rokkert på rekkefølge av close printerfile
6.31  * R6.30 040719 bsa  Utvidet saldofelter
6.32  * R6.30 091219 bof  Endringer vedr. redef. etter utvidelse saldofelter
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frhsal1    if   e           k disk    rename(rhsapfr:rhsal1r)
     frhsal2    if   e           k disk    rename(rhsapfr:rhsal2r)
     frhsal3    if   e           k disk    rename(rhsapfr:rhsal3r)
     frhovlr    if   e           k disk    rename(rhovpfr:rhovlrr)
     fra28lr    if   e           k disk    rename(ra28pfr:ra28lrr)
      *
     frh621p    o    e             printer oflind(*in30)
pdf  f                                     usropn
      *+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      *               T   A   B   E   L   L   E   R                      +
      *+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      *
6.31 d sal             s             15  2 dim(13)
     d txt             s             30    dim(2) ctdata perrcd(1)
     d pav             s              4  0 dim(24)
     d uav             s              5  0 dim(8)
      *
     d                 ds
     d nybrd                         13  0
     d  nyavd                         4  0 overlay(nybrd)
     d  nykto                         5  0 overlay(nybrd:5)
     d   nygrp                        2  0 overlay(nykto:2)
     d    nykla                       1  0 overlay(nygrp)
     d  nysps                         4  0 overlay(nybrd:10)
      *
     d                 ds
     d gmbrd                         13  0
     d  gmavd                         4  0 overlay(gmbrd)
     d  gmkto                         5  0 overlay(gmbrd:5)
     d   gmgrp                        2  0 overlay(gmkto:2)
     d    gmkla                       1  0 overlay(gmgrp)
     d  gmsps                         4  0 overlay(gmbrd:10)
      *
     d  nyavdo         s              4  0
     d  gmavdo         s              4  0
      *
     d                uds
     d rhpkda                300    305  0
     d rhprÅr                         4  0
     d rhpper                         2  0
     d rhppef                         2  0
     d rhppet                         2  0
     d rhpavf                         4  0
     d rhpavt                         4  0
     d rhpbØf                         4  0
     d rhpbØt                         4  0
     d rhpktf                         5  0
     d rhpktt                         5  0
     d rhpsrt                         1
     d rhpsml                         1
     d rhpavv                         1
     d rhpkgs                         1
     d rhpkks                         1
     d rhpspb                         1
T5.50d rhpspa                         1
     d rhanul                         1
     d rhpa01                         4  0
     d rhpa02                         4  0
     d rhpa03                         4  0
     d rhpa04                         4  0
     d rhpa05                         4  0
     d rhpa06                         4  0
     d rhpa07                         4  0
     d rhpa08                         4  0
     d rhpa09                         4  0
     d rhpa10                         4  0
     d rhpa11                         4  0
     d rhpa12                         4  0
     d rhpa13                         4  0
     d rhpa14                         4  0
     d rhpa15                         4  0
     d rhpa16                         4  0
     d rhpa17                         4  0
     d rhpa18                         4  0
     d rhpa19                         4  0
     d rhpa20                         4  0
     d rhpa21                         4  0
     d rhpa22                         4  0
     d rhpa23                         4  0
     d rhpa24                         4  0
      *
pdf  d l_poff                584    584
pdf  d l_bach                595    635
pdf  d l_skje                637    637  0
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d antkto          s                   like(totkto)
     d antsps          s                   like(totkto)
E5.40d bev             s             +1    like(risa01)
6.32 d* difh            s             +1    like(risa01)
6.32 d* difp            s             +1    like(risa01)
6.32 d* lrdifh          s             +1    like(risa01)
6.32 d* lrdifp          s             +1    like(risa01)
E5.40d l1beÅr          s             +1    like(risa01)
E5.40d l1befj          s             +1    like(risa01)
6.32 d* l1bevp          s             +1    like(risa01)
E5.40d l1buÅr          s             +1    like(risa01)
E5.40d l1busÅ          s             +1    like(risa01)
E5.40d l1alÅr          s             +1    like(risa01)
E5.40d l1alsÅ          s             +1    like(risa01)
E5.40d l1saÅr          s             +1    like(risa01)
E5.40d l1safj          s             +1    like(risa01)
6.32 d* l1salh          s             +1    like(risa01)
E5.40d l1samh          s             10  0
E5.40d l1samp          s             10  0
6.32 d* l2bevp          s             +1    like(risa01)
6.32 d* l2salh          s             +1    like(risa01)
E5.40d l2samh          s             10  0
E5.40d l2samp          s             10  0
6.32 d* l3bevp          s             +1    like(risa01)
6.32 d* l3salh          s             +1    like(risa01)
E5.40d l3samh          s             10  0
E5.40d l3samp          s             10  0
6.32 d* l4bevp          s             +1    like(risa01)
6.32 d* l4salh          s             +1    like(risa01)
E5.40d l4samh          s             10  0
E5.40d l4samp          s             10  0
6.32 d* l5bevp          s             +1    like(risa01)
6.32 d* l5salh          s             +1    like(risa01)
E5.40d l5samh          s             10  0
E5.40d l5samp          s             10  0
     d m               s              2  0
     d n               s              2  0
     d x               s              2  0
     d tstav           s              7  0
     d prfjo           s                   like(rhprÅr)
     d proh            s              7  1
     d prop            s              7  1
     d risa01          s             11  2
E5.40d samh            s             +1    like(risa01)
E5.40d samp            s             +1    like(risa01)
E5.40d sum             s             +1    like(risa01)
     d totkto          s              5  0
     d totsid          s                   like(totkto)
     d wavde           s                   like(rhavde)
     d wmvak           s                   like(rhmvak)
     d wmvao           s                   like(rhmvao)
     d wtext           s                   like(rhtext)
      *
     d w_type          s                   like(ritype)
     d w_firm          s                   like(rhfirm)
     d w_xuav          s              6  0
     d p_stat          s              1
     d p_tx30          s             30
     d p_gkod          s                   like(rhavde)
     d p_Økod          s                   like(rhspes)
     d p_skod          s              2  0
     d p_tkod          s              2  0
     d avtext          s             30
     d grtext          s             30
     d kltext          s             30
6.11 d p_btyp          s            100
pdf   *
pdf  d splfname2       s             10a
pdf  d jobname2        s             10a
pdf  d username2       s             10a
pdf  d jobnbr2         s              6a
pdf  d splfnbr2        s             10i 0
pdf  d splfname3       s             10a
pdf  d jobname3        s             10a
pdf  d username3       s             10a
pdf  d jobnbr3         s              6a
pdf  d splfnbr3        s             10i 0
6.20 d p_tagg0         s             20
6.20 d p_tagg0val      s             50
6.20 d p_tagg1         s             20
6.20 d p_tagg1val      s             50
6.20 d p_tagg2         s             20
6.20 d p_tagg2val      s             50
6.20 d p_tagg3         s             20
6.20 d p_tagg3val      s             50
6.20 d p_tagg4         s             20
6.20 d p_tagg4val      s             50
6.20 d p_tagg5         s             20
6.20 d p_tagg5val      s             50
6.20 d p_tagg6         s             20
6.20 d p_tagg6val      s             50
6.20 d p_tagg7         s             20
6.20 d p_tagg7val      s             50
6.20 d p_tagg8         s             20
6.20 d p_tagg8val      s             50
6.20 d p_tagg9         s             20
6.20 d p_tagg9val      s             50
6.20 d p_refn          s             50
6.20 d p_dspl          s            100
pdf   *
pdf  d qsprilsp        pr                  extpgm('QSPRILSP')
pdf  d rcvvar                     32767a   options(*varsize)
pdf  d rcvvarlen                     10i 0 const
pdf  d format                         8a   const
pdf  d errorcode                  32767a   options(*varsize)
pdf   *
pdf  d sprl0100        ds
pdf  d  bytesrtn                     10i 0
pdf  d  bytesavl                     10i 0
pdf  d  splfname                     10a
pdf  d  jobname                      10a
pdf  d  username                     10a
pdf  d  jobnbr                        6a
pdf  d  splfnbr                      10i 0
pdf  d  systemname                    8a
pdf  d  createdate                    7a
pdf  d                                1a
pdf  d  createtime                    6a
pdf   *
pdf  d errorcode       ds
pdf  d  bytesprov                    10i 0 inz(0)
pdf  d  byteavail                    10i 0 inz(0)
      *
     d rhovlr_kont     s                   like(rikont)
     d rhovlr_spes     s                   like(rispes)
     d rhovlr_avde     s                   like(riavde)
     d ra28lr_Økod     s                   like(rebkod)
      *
     irhsal1r
      *  Saldo-register - Kontonr.rekkefølge
     i              risa01                      sal(01)
     i              risa02                      sal(02)
     i              risa03                      sal(03)
     i              risa04                      sal(04)
     i              risa05                      sal(05)
     i              risa06                      sal(06)
     i              risa07                      sal(07)
     i              risa08                      sal(08)
     i              risa09                      sal(09)
     i              risa10                      sal(10)
     i              risa11                      sal(11)
     i              risa12                      sal(12)
     i              risa13                      sal(13)
      *
     irhsal2r
      *  Saldo-register - Avdelingsrekkefølge
     i              risa01                      sal(01)
     i              risa02                      sal(02)
     i              risa03                      sal(03)
     i              risa04                      sal(04)
     i              risa05                      sal(05)
     i              risa06                      sal(06)
     i              risa07                      sal(07)
     i              risa08                      sal(08)
     i              risa09                      sal(09)
     i              risa10                      sal(10)
     i              risa11                      sal(11)
     i              risa12                      sal(12)
     i              risa13                      sal(13)
      *
     irhsal3r
      *  Saldo-register - Kostnadsbærerrekkefølge
     i              risa01                      sal(01)
     i              risa02                      sal(02)
     i              risa03                      sal(03)
     i              risa04                      sal(04)
     i              risa05                      sal(05)
     i              risa06                      sal(06)
     i              risa07                      sal(07)
     i              risa08                      sal(08)
     i              risa09                      sal(09)
     i              risa10                      sal(10)
     i              risa11                      sal(11)
     i              risa12                      sal(12)
     i              risa13                      sal(13)
      *
     c     dummy         tag
      *
     c                   select
     c                   when      rhpsrt = 'K'
     c     rhsal1_key    setll     rhsal1r                                61
     c     rhsal1_key    reade     rhsal1r                                61
     c                   when      rhpsrt = 'A'
     c     rhsal2_key    setll     rhsal2r                                61
     c     rhsal2_key    reade     rhsal2r                                61
     c                   when      rhpsrt = 'B'
     c     rhsal3_key    setll     rhsal3r                                61
     c     rhsal3_key    reade     rhsal3r                                61
     c                   endsl
      *
     c                   dow       *in61 = *off
      *
     c                   move      ritype        w_type
      *
      * Sjekk om brudd i kontrollbegrepet
      *
     c                   move      riavde        nyavd
     c                   move      rikont        nykto
     c                   move      rispes        nysps
     c                   move      riavde        nyavdo
      *
      * Sjekk årstall mot parameter
      *
     c                   if        riraar > rhprÅr
     c                   goto      neste
     c                   endif
      *
     c                   if        rhpsml = 'F' and
     c                             riraar < rhprÅr - 1
     c                   goto      neste
     c                   endif
      *
     c                   if        rhpsml = 'B' and
     c                             riraar < rhprÅr
     c                   goto      neste
     c                   endif
      *
     c                   if        rhpsml = 'A' and
     c                             riraar < rhprÅr
     c                   goto      neste
     c                   endif
      *
      * Sjekk kontonr. fra/til
      *
     c                   if        nykto < rhpktf or
     c                             nykto > rhpktt
     c                   goto      neste
     c                   endif
      *
      * Kostnadsbærer fra/til er utfylt
      *
     c                   if        nysps < rhpbØf or
     c                             nysps > rhpbØt
     c                   goto      neste
     c                   endif
      *
      * Sjekk om avdeling 0000 med
      *
     c                   if        rhanul = 'N' and
     c                             nyavd  = *zeros
     c                   goto      neste
     c                   endif
      *
     c                   if        rhanul = 'J' and
     c                             nyavd  = *zeros
     c                   goto      avd_med
     c                   endif
      *
      * Sjekk avdeling mot tabell
      *
     c                   if        tstav <> *zeros
      *
     c                   eval      n = 1
     c                   dou       n > 24
      *
     c                   if        pav(n) <> *zeros
      *
     c                   if        nyavd  = pav(n)
     c                   goto      avd_med
     c                   endif
      *
     c                   endif
      *
     c                   eval      n = n + 1
     c                   enddo
      *
     c                   endif
      *
      * Avdeling ikke i tabell -
      * sjekk avdeling fra/til
      *
     c                   if        nyavd < rhpavf or
     c                             nyavd > rhpavt
     c                   goto      neste
     c                   endif
      *
     c     avd_med       tag
      *
      *    Her behandles alle poster som skal være med
      *
      * Første gang - ikke test på brudd
      *
     c                   if        gmbrd = *zeros
     c                   move      nybrd         gmbrd
     c                   move      nyavdo        gmavdo
     c                   exsr      sr_forst
     c                   endif
      *
      * Dersom konto-rekkefølge, nullstill avd i bruddbegrep
T5.50 * når avd. ikke skal spesifiseres.
      *
     c                   if        rhpsrt = 'K' or
     c                             rhpsrt = 'B'
T5.50c                   if        *in22 = *off
     c                   eval      nyavd = *zeros
     c                   eval      gmavd = *zeros
T5.50c                   endif
     c                   endif
      *
      * Dersom bærer ikke skal spesifiseres, nullstill bærer i bruddbegrep
      *
     c                   if        rhpspb = 'N'
     c                   if        rhpsrt = 'K' or
     c                             rhpsrt = 'A'
     c                   eval      nysps = *zeros
     c                   eval      gmsps = *zeros
     c                   endif
     c                   endif
      *
      * Test hele bruddbegrepet - hvis like, summer laveste nivå
      *
     c                   if        nybrd = gmbrd
     c                   exsr      rsum
     c                   goto      neste
     c                   endif
      *
     c     brudde        tag
      *
      * Brudd på hele begrepet
      *
     c                   exsr      brhel
      *
      * Brudd på konto
      *
     c                   if        nykto <> gmkto
     c                   exsr      brkto
     c                   else
     c                   if        rhpsrt = 'B' and
     c                             nysps <> gmsps
     c                   exsr      brkto
     c                   else
     c                   if        rhpsrt = 'A' and
     c                             nyavd <> gmavd
     c                   exsr      brkto
     c                   endif
     c                   endif
     c                   endif
      *
      * Brudd på gruppe
      *
T5.50c                   if        *in22 = *off
     c                   if        nyavd <> gmavd or
     c                             nygrp <> gmgrp
     c                   exsr      brgrp
     c                   else
     c                   if        rhpsrt = 'B' and
     c                             nysps <> gmsps
     c                   exsr      brgrp
     c                   else
     c                   if        rhpsrt = 'A' and
     c                             nyavd <> gmavd
     c                   exsr      brgrp
     c                   endif
     c                   endif
     c                   endif
T5.50c                   else
T5.50c                   if        nygrp <> gmgrp
T5.50c                   exsr      brgrp
T5.50c                   endif
T5.50c                   endif
      *
      * Brudd på klasse
      *
T5.50c                   if        *in22 = *off
     c                   if        nyavd <> gmavd or
     c                             nykla <> gmkla
     c                   exsr      brkla
     c                   else
     c                   if        rhpsrt = 'B' and
     c                             nysps <> gmsps
     c                   exsr      brkla
     c                   else
     c                   if        rhpsrt = 'A' and
     c                             nyavd <> gmavd
     c                   exsr      brkla
     c                   endif
     c                   endif
     c                   endif
T5.50c                   else
T5.50c                   if        nykla <> gmkla
T5.50c                   exsr      brkla
T5.50c                   endif
T5.50c                   endif
      *
      * Brudd på avdeling  - (kun hvis sortert på avdeling)
      *
     c                   if        rhpsrt = 'A' and
     c                             nyavd <> gmavd
     c                   exsr      bravd
     c                   endif
      *
      * Brudd på kostnadsbærer  - (kun hvis sortert på kostnadsbærer)
      *
     c                   if        rhpsrt = 'B' and
     c                             nysps <> gmsps
     c                   exsr      brsps
     c                   endif
      *
      * Summer til L1-nivå
      *
     c                   exsr      rsum
      *
      * Flytt nytt brudd-begrep til gammelt
      *
     c                   move      nybrd         gmbrd
     c                   move      nyavdo        gmavdo
      *
     c     neste         tag
      *
     c                   select
     c                   when      rhpsrt = 'K'
     c     rhsal1_key    reade     rhsal1r                                61
     c                   when      rhpsrt = 'A'
     c     rhsal2_key    reade     rhsal2r                                61
     c                   when      rhpsrt = 'B'
     c     rhsal3_key    reade     rhsal3r                                61
     c                   endsl
      *
     c                   enddo
      *
     c     slutt         tag
      *
      * Brudd-behandling
      *
     c                   exsr      brhel
     c                   exsr      brkto
     c                   exsr      brgrp
     c                   exsr      brkla
      *
     c                   if        rhpsrt = 'A'
     c                   exsr      bravd
     c                   endif
      *
     c                   if        rhpsrt = 'B'
     c                   exsr      brsps
     c                   endif
      *
      * Beregn differanser debet/kredit - skriv siste side
      *
     c                   eval      lrdifp = lrbevd + lrbevk
     c                   eval      lrdifh = lrsald + lrsalk
      *
     c                   eval      totsid = totsid + 1
     c                   write     skrivt
      *
     c                   eval      m = *zeros
     c                   eval      n = *zeros
     c                   eval      x = *zeros
      *
     c                   eval      m = m + 1
     c                   eval      n = n + 1
      *
      * Dersom valg av enkeltavdelinger
      * skriv ut 8 og 8 avd.
      * på siste side
      *
     c                   if        tstav > *zeros
      *
     c                   dou       n > 24
      *
     c                   if        pav(n) <> *zeros
     c                   move      pav(n)        uav(m)
     c                   eval      x = x + 1
     c                   endif
      *
     c                   eval      m = m + 1
     c                   eval      n = n + 1
      *
     c                   if        m = 9
     c                   eval      uav01 = uav(1)
     c                   eval      uav02 = uav(2)
     c                   eval      uav03 = uav(3)
     c                   eval      uav04 = uav(4)
     c                   eval      uav05 = uav(5)
     c                   eval      uav06 = uav(6)
     c                   eval      uav07 = uav(7)
     c                   eval      uav08 = uav(8)
      *
     c                   xfoot     uav           w_xuav
     c                   if        w_xuav <> *zeros
     c                   write     siste
     c                   endif
     c                   eval      uav   = *zeros
     c                   eval      uav01 = *zeros
     c                   eval      uav02 = *zeros
     c                   eval      uav03 = *zeros
     c                   eval      uav04 = *zeros
     c                   eval      uav05 = *zeros
     c                   eval      uav06 = *zeros
     c                   eval      uav07 = *zeros
     c                   eval      uav08 = *zeros
     c                   eval      w_xuav = *zeros
     c                   move      1             m
     c                   endif
      *
     c                   enddo
      *
     c                   endif
pdf   * Generer xml for videre utskrift
pdf  c                   if        l_poff = '1'
pdf  c                   exsr      spoolnbr
6.30 c                   close     rh621p
6.30 c                   exsr      xml_create
pdf  c                   exsr      pdf_preview
pdf   * Avslutt jobbiden
pdf  c                   call      'AB710R'
pdf  c                   parm                    l_bach
pdf  c                   endif
      *
     c                   eval      *inlr = *on
     c/eject
      ****************************************************************
      *   Subrutine for behandling av alle aktuelle recordtyper      *
      ****************************************************************
      *
     c     rsum          begsr
      *
      * Summer til denne periode og hittil i år
      *
     c                   exsr      hsum
      *
      * Recordtype 1  - saldo
      *
     c                   if        w_type = 1
      *
      * Saldo i år
      *
     c                   if        riraar = rhprÅr
     c                   eval      l1beÅr = l1beÅr + bev
     c                   eval      l1saÅr = l1saÅr + sum
      *
      * Summer for siste side
      *
     c                   if        bev    > *zero
     c                   eval      lrbevd = lrbevd + bev
     c                   else
     c                   eval      lrbevk = lrbevk + bev
     c                   endif
      *
     c                   if        sum    > *zero
     c                   eval      lrsald = lrsald + sum
     c                   else
     c                   eval      lrsalk = lrsalk + sum
     c                   endif
      *
     c                   else
      *
      * Saldo i fjor
      *
     c                   if        riraar = prfjo
     c                   eval      l1befj = l1befj + bev
     c                   eval      l1safj = l1safj + sum
     c                   endif
     c                   endif
      *
     c                   endif
      *
      * Recordtype 3  - budsjett
      *
     c                   if        w_type = 3 and
     c                             riraar = rhprÅr
     c                   eval      l1buÅr = l1buÅr + bev
     c                   eval      l1busÅ = l1busÅ + sum
     c                   endif
      *
      * Recordtype 4  - alternativt budsjett
      *
     c                   if        w_type = 4 and
     c                             riraar = rhprÅr
     c                   eval      l1alÅr = l1alÅr + bev
     c                   eval      l1alsÅ = l1alsÅ + sum
     c                   endif
      *
     c                   endsr
     c/eject
      ****************************************************************
      *   Subrutine for beregning av denne periode og hittil         *
      ****************************************************************
      *
     c     hsum          begsr
      *
     c                   eval      bev = 0
     c                   eval      sum = 0
      *
     c                   eval      n = 1
      *
     c                   eval      sum = sum + risa00
      *
      * Utskrift av IB
      *
     c                   if        rhpper = 0
     c                   eval      bev = risa00
     c                   endif
      *
      * Summer periode fra 1 - til denne periode
      *
     c                   if        rhpper > 0
      *
     c                   dou       n > rhpper
      *
     c                   eval      sum = sum + sal(n)
      *
      * Summer bevegelse for periode fra  - til
      *
     c                   if        n >= rhppef and
     c                             n <= rhppet
     c                   eval      bev = bev + sal(n)
     c                   endif
      *
     c                   eval      n = n + 1
     c                   enddo
      *
     c                   endif
      *
     c                   endsr
     c/eject
      *
      ****************************************************************
      *   Subrutine for beregning ved brudd på spesifikasjon         *
      ****************************************************************
      *
     c     brhel         begsr
      *
     c                   eval      l1bevp = l1beÅr
     c                   eval      l1salh = l1saÅr
      *
      * Sammenligning mot budsjett
      *
     c                   if        rhpsml = 'B'
     c                   eval      l1samp = l1buÅr
     c                   eval      l1samh = l1busÅ
     c                   endif
      *
      * Sammenligning mot alternativt budsjett
T5.20 * Hvis alternativt budsjett ikke finnes, brukes budsjett.
      *
     c                   if        rhpsml = 'A'
T5.20c                   eval      l1samp = l1buÅr
T5.20c                   eval      l1samh = l1busÅ
T5.20c                   if        l1alÅr <> *zeros
     c                   eval      l1samp = l1alÅr
T5.20c                   endif
T5.20c                   if        l1alsÅ <> *zeros
     c                   eval      l1samh = l1alsÅ
T5.20c                   endif
     c                   endif
      *
      * Sammenligning mot fjorår
      *
     c                   if        rhpsml = 'F'
     c                   eval      l1samp = l1befj
     c                   eval      l1samh = l1safj
     c                   endif
      *
      * Sammenligning mot gjennomsnitt
      *
     c                   if        rhpsml = 'G'
     c                   eval(h)   l1samp = l1salh / rhpper
     c                   eval      l1samh = l1salh
     c                   endif
      *
      * Beregn differanser
      *
     c                   eval      difp = l1bevp - l1samp
     c                   eval      difh = l1salh - l1samh
      *
      * Beregn brukt/oppnådd
      *
     c                   if        rhpavv = 'P'
     c                   eval      samp = l1samp
     c                   eval      samh = l1samh
     c                   exsr      brukt
     c                   endif
      *
      * Adder til neste nivå
      *
     c                   eval      l2bevp = l2bevp + l1bevp
     c                   eval      l2salh = l2salh + l1salh
     c                   eval      l2samp = l2samp + l1samp
     c                   eval      l2samh = l2samh + l1samh
      *
      * Hent tekstrecord fra kontoplan-register
      *
     c                   eval      wtext = *blanks
T5.50c                   eval      wtextx = *blanks
T5.30c                   eval      l2text = *blanks
T5.50c                   eval      l2textx = *blanks
     c                   eval      wmvak = *blanks
     c                   eval      wmvao = *blanks
      *
     c                   move      gmkto         rhovlr_kont
     c                   move      gmsps         rhovlr_spes
KOB  c                   move      gmavdo        rhovlr_avde
     c     rhovlr_key    chain     rhovlr                             62
      *
     c                   if        *in62 = *on
     c                   move      *zeros        rhovlr_avde
     c     rhovlr_key    chain     rhovlr                             62
     c                   endif
      *
     c                   if        *in62 = *on
     c                   move      *zeros        rhovlr_spes
     c     rhovlr_key    chain     rhovlr                             62
     c                   endif
      *
     c                   if        *in62 = *off
     c                   move      rhtext        wtext
T5.50c                   movel     rhtext        wtextx
T5.30c                   movel     rhtext        l2text
T5.50c                   movel     rhtext        l2textx
     c                   move      rhmvak        wmvak
     c                   move      rhmvao        wmvao
T5.30c                   else
T5.30c                   movel     'S U M'       l2text
T5.50c                   movel     'S U M'       l2textx
     c                   endif
      *
      * Hvis kostnadsbærer finnes hentes
      * tekst på rapport derfra
T5.50 * Hentes ikke når pr. kostnadsbærer
      *
     c                   if        rhpsrt <> 'B'
     c                   if        rhpspb = 'J' and
     c                             gmsps <> 0
     c                   eval      ra28lr_Økod = gmsps
     c     ra28lr_key    chain     ra28lrr
      *
     c                   if        %found
     c                   move      rebtxt        wtext
T5.50c                   movel     rebtxt        wtextx
     c                   endif
     c                   endif
T5.50c                   endif
      *
T5.50c                   if        *in22 = *on
T5.50c                   eval      gmavdx = gmavd
T5.50c                   endif
      *
      * Skriv linje
      *
     c                   eval      totkto = totkto + 1
     c                   eval      antsps = antsps + 1
     c                   eval      antkto = antkto + 1
      *
     c                   if        *in30 = *on
     c                   exsr      skriv
     c                   eval      *in30 = *off
     c                   endif
      *
     c                   write     skriv1
      *
      * Nullstill arbeidsfelt på L1-nivå
      *
     c     l1null        tag
      *
     c                   eval      l1bevp = *zeros
     c                   eval      l1salh = *zeros
     c                   eval      l1samp = *zeros
     c                   eval      l1samh = *zeros
      *
     c                   eval      l1beÅr = *zeros
     c                   eval      l1befj = *zeros
      *
     c                   eval      l1buÅr = *zeros
     c                   eval      l1busÅ = *zeros
      *
     c                   eval      l1saÅr = *zeros
     c                   eval      l1safj = *zeros
      *
     c                   eval      l1alÅr = *zeros
     c                   eval      l1alsÅ = *zeros
      *
     c                   endsr
     c/eject
      *
      ****************************************************************
      *   Subrutine for beregning ved brudd på konto                 *
      ****************************************************************
      *
     c     brkto         begsr
      *
      * Beregn differanser
      *
     c                   eval      difp = l2bevp - l2samp
     c                   eval      difh = l2salh - l2samh
      *
      * Beregn brukt/oppnådd
      *
     c                   if        rhpavv = 'P'
     c                   eval      samp = l2samp
     c                   eval      samh = l2samh
     c                   exsr      brukt
     c                   endif
      *
      * Test om linjen skal skrives
      *
     c                   if        antsps > 1
      *
     c                   if        *in30 = *on
     c                   exsr      skriv
     c                   eval      *in30 = *off
     c                   endif
      *
     c                   eval      antkto = antkto + 1
     c                   write     skriv2
      *
     c                   endif
      *
      * Adder til neste nivå
      *
     c                   eval      l3bevp = l3bevp + l2bevp
     c                   eval      l3salh = l3salh + l2salh
     c                   eval      l3samp = l3samp + l2samp
     c                   eval      l3samh = l3samh + l2samh
      *
      * Nullstill arbeidsfelt på L2-nivå
      *
     c     l2null        tag
      *
     c                   eval      antsps = *zeros
     c                   eval      l2bevp = *zeros
     c                   eval      l2salh = *zeros
     c                   eval      l2samp = *zeros
     c                   eval      l2samh = *zeros
      *
     c                   endsr
     c/eject
      *
      ****************************************************************
      *   Subrutine for beregning ved brudd på kontogruppe           *
      ****************************************************************
      *
     c     brgrp         begsr
      *
      * Kontogruppe skrives bare hvis minst en konto er skrevet
      *
     c                   if        antkto > *zeros
      *
      * Beregn differanser
      *
     c                   eval      difp = l3bevp - l3samp
     c                   eval      difh = l3salh - l3samh
      *
      * Beregn brukt/oppnådd
      *
     c                   if        rhpavv = 'P'
     c                   eval      samp = l3samp
     c                   eval      samh = l3samh
     c                   exsr      brukt
     c                   endif
      *
      * Hent gruppe-tekst fra koderegisteret
      *
     c                   move      gmgrp         p_tkod
     c                   call      'RS720R'
     c                   parm                    p_stat
     c                   parm                    p_tkod
     c                   parm                    p_tx30
      *
     c                   if        p_stat = 'N'
     c                   eval      grtext = *blank
T5.50c                   eval      grtextx = *blank
     c                   movel     txt(2)        grtext
T5.50c                   movel     txt(2)        grtextx
     c                   else
     c                   move      p_tx30        grtext
T5.50c                   move      p_tx30        grtextx
     c                   endif
      *
      * Test om linjen skal skrives
      *
     c                   if        rhpkgs = 'J'
      *
     c                   if        *in30 = *on
     c                   exsr      skriv
     c                   eval      *in30 = *off
     c                   endif
      *
     c                   write     skriv3
     c                   endif
      *
     c                   endif
      *
      * Adder til neste nivå
      *
     c                   eval      l4bevp = l4bevp + l3bevp
     c                   eval      l4salh = l4salh + l3salh
     c                   eval      l4samp = l4samp + l3samp
     c                   eval      l4samh = l4samh + l3samh
      *
      * Nullstill arbeidsfelt på L3-nivå
      *
     c     l3null        tag
      *
     c                   eval      antkto = *zeros
     c                   eval      l3bevp = *zeros
     c                   eval      l3salh = *zeros
     c                   eval      l3samp = *zeros
     c                   eval      l3samh = *zeros
      *
     c                   endsr
     c/eject
      *
      ****************************************************************
      *   Subrutine for beregning ved brudd på kontoklasse           *
      ****************************************************************
      *
     c     brkla         begsr
      *
      * Beregn differanser
      *
     c                   eval      difp = l4bevp - l4samp
     c                   eval      difh = l4salh - l4samh
      *
      * Beregn brukt/oppnådd
      *
     c                   if        rhpavv = 'P'
     c                   eval      samp = l4samp
     c                   eval      samh = l4samh
     c                   exsr      brukt
     c                   endif
      *
      * Hent klasse-tekst fra koderegisteret
      *
     c                   eval      p_skod = *zero
     c                   move      gmkla         p_skod
     c                   call      'RS719R'
     c                   parm                    p_stat
     c                   parm                    p_skod
     c                   parm                    p_tx30
      *
     c                   if        p_stat = 'N'
     c                   eval      kltext = *blank
T5.50c                   eval      kltextx = *blank
     c                   movel     txt(1)        kltext
T5.50c                   movel     txt(1)        kltextx
     c                   else
     c                   move      p_tx30        kltext
T5.50c                   move      p_tx30        kltextx
     c                   endif
      *
      * Test om linjen skal skrives
      *
     c                   if        rhpkks = 'J'
      *
     c                   if        *in30 = *on
     c                   exsr      skriv
     c                   eval      *in30 = *off
     c                   endif
      *
     c                   write     skriv4
     c                   endif
      *
      * Adder til neste nivå
      *
     c                   eval      l5bevp = l5bevp + l4bevp
     c                   eval      l5salh = l5salh + l4salh
     c                   eval      l5samp = l5samp + l4samp
     c                   eval      l5samh = l5samh + l4samh
      *
      * Nullstill arbeidsfelt på L4-nivå
      *
     c     l4null        tag
      *
     c                   eval      l4bevp = *zeros
     c                   eval      l4salh = *zeros
     c                   eval      l4samp = *zeros
     c                   eval      l4samh = *zeros
      *
     c                   endsr
     c/eject
      *
      ****************************************************************
      *   Subrutine for beregning ved brudd på avdeling              *
      ****************************************************************
      *
     c     bravd         begsr
      *
      * Beregn differanser
      *
     c                   eval      difp = l5bevp - l5samp
     c                   eval      difh = l5salh - l5samh
      *
      * Beregn brukt/oppnådd
      *
     c                   if        rhpavv = 'P'
     c                   eval      samp = l5samp
     c                   eval      samh = l5samh
     c                   exsr      brukt
     c                   endif
      *
      * Hent avdelingstekst fra koderegisteret
      *
     c                   if        rhpsrt = 'A'
     c                   eval      avtext = *blank
     c                   move      gmavd         wavde
     c                   move      gmavd         p_gkod
     c                   call      'RS707R'
     c                   parm                    p_stat
     c                   parm                    p_gkod
     c                   parm                    p_tx30
      *
     c                   move      p_tx30        avtext
     c                   endif
      *
      * Skriv heading og linje
      *
     c                   if        *in30 = *on
     c                   exsr      skriv
     c                   eval      *in30 = *off
     c                   endif
      *
     c                   write     skriv5
      *
     c                   if        *in61 = *off
      *
      * Hent ny avdelingstekst fra koderegisteret og skriv ny heading
      *
     c                   if        rhpsrt = 'A'
     c                   eval      avtext = *blank
     c                   move      nyavd         wavde
     c                   move      nyavd         p_gkod
     c                   call      'RS707R'
     c                   parm                    p_stat
     c                   parm                    p_gkod
     c                   parm                    p_tx30
      *
     c                   move      p_tx30        avtext
     c                   endif
      *
     c                   exsr      skriv
      *
      * Nullstill arbeidsfelt på L5-nivå
      *
     c                   eval      l5bevp = *zeros
     c                   eval      l5salh = *zeros
     c                   eval      l5samp = *zeros
     c                   eval      l5samh = *zeros
      *
     c                   endif
      *
     c                   endsr
     c/eject
      *
      ****************************************************************
      *   Subrutine for beregning ved brudd på kostnadsbærer         *
      ****************************************************************
      *
     c     brsps         begsr
      *
      * Beregn differanser
      *
     c                   eval      difp = l5bevp - l5samp
     c                   eval      difh = l5salh - l5samh
      *
      * Beregn brukt/oppnådd
      *
     c                   if        rhpavv = 'P'
     c                   eval      samp = l5samp
     c                   eval      samh = l5samh
     c                   exsr      brukt
     c                   endif
      *
      * Hent kostnadsbærertekst fra koderegisteret
      *
     c                   if        rhpsrt = 'B'
     c                   eval      sptext = *blank
     c                   move      gmsps         wspes
     c                   move      gmsps         p_Økod
      *
     c                   call      'RS728R'
     c                   parm                    p_stat
     c                   parm                    p_Økod
     c                   parm                    p_tx30
      *
     c                   move      p_tx30        sptext
     c                   endif
      *
      * Skriv heading og linje
      *
     c                   if        *in30 = *on
     c                   exsr      skriv
     c                   eval      *in30 = *off
     c                   endif
      *
     c                   write     skriv5
      *
     c                   if        *in61 = *off
      *
      * Hent ny kostnadsbærertekst fra koderegisteret og skriv ny heading
      *
     c                   if        rhpsrt = 'B'
     c                   eval      sptext = *blank
     c                   move      nysps         wspes
     c                   move      nysps         p_Økod
      *
     c                   call      'RS728R'
     c                   parm                    p_stat
     c                   parm                    p_Økod
     c                   parm                    p_tx30
      *
     c                   move      p_tx30        sptext
     c                   endif
      *
     c                   exsr      skriv
      *
      * Nullstill arbeidsfelt på L5-nivå
      *
     c                   eval      l5bevp = *zeros
     c                   eval      l5salh = *zeros
     c                   eval      l5samp = *zeros
     c                   eval      l5samh = *zeros
      *
     c                   endif
      *
     c                   endsr
     c/eject
      *
      ****************************************************************
      *   Subrutine for 1.gang for å hente kostnadsbærer- eller
      *   avdelingstekst.
      ****************************************************************
      *
     c     sr_forst      begsr
      *
      * Hent avdelingstekst fra koderegisteret
      *
     c                   if        rhpsrt = 'A'
     c                   eval      avtext = *blank
     c                   move      gmavd         wavde
     c                   move      gmavd         p_gkod
     c                   call      'RS707R'
     c                   parm                    p_stat
     c                   parm                    p_gkod
     c                   parm                    p_tx30
      *
     c                   move      p_tx30        avtext
     c                   endif
      *
      * Hent kostnadsbærertekst fra koderegisteret
      *
     c                   if        rhpsrt = 'B'
     c                   eval      sptext = *blank
     c                   move      gmsps         wspes
     c                   move      gmsps         p_Økod
      *
     c                   call      'RS728R'
     c                   parm                    p_stat
     c                   parm                    p_Økod
     c                   parm                    p_tx30
      *
     c                   move      p_tx30        sptext
     c                   endif
      *
      * Skriv heading
      *
     c                   write     head2
     c                   eval      *in30 = *off
     c                   endsr
     c/eject
      *
      ****************************************************************
      *   Subrutine for beregning av brukt/oppnådd                   *
      ****************************************************************
      *
     c     brukt         begsr
      *
      * Denne periode
      *
     c                   eval      *in16 = *off
     c                   eval      *in17 = *off
     c                   if        samp <> *zero
      *
     c                   select
     c                   when      (difp / samp * 100) + 100 < -999999,9
     c                   eval      prop = -999999,9
     c                   when      (difp / samp * 100) + 100 >  999999,9
     c                   eval      prop =  999999,9
     c                   other
     c                   eval(h)   prop = (difp / samp * 100) + 100
     c                   endsl
      *
     c                   else
     c                   if        difp <> *zero
     c                   eval      *in16 = *on
     c                   else
     c                   eval      prop = 100
     c                   endif
     c                   endif
      *
      * Hittil
      *
     c                   if        samh <> *zero
      *
     c                   select
     c                   when      (difh / samh * 100) + 100 < -999999,9
     c                   eval      proh = -999999,9
     c                   when      (difh / samh * 100) + 100 >  999999,9
     c                   eval      proh =  999999,9
     c                   other
     c                   eval(h)   proh = (difh / samh * 100) + 100
     c                   endsl
      *
     c                   else
     c                   if        difh <> *zero
     c                   eval      *in17 = *on
     c                   else
     c                   eval      proh = 100
     c                   endif
     c                   endif
      *
     c                   endsr
     c/eject
      *
      ****************************************************************
      *   Subrutine for å skrive heading                             *
      ****************************************************************
      *
     c     skriv         begsr
      *
     c                   eval      totsid = totsid + 1
     c                   write     head1
     c                   write     head2
      *
     c                   endsr
     c/space 3
      *
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Finner spool som er generert av jobben
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     spoolnbr      begsr
pdf   /Free
pdf
pdf            QSPRILSP( SPRL0100
pdf                    : %size(SPRL0100)
pdf                    : 'SPRL0100'
pdf                    : errorcode );
pdf   /End-free
pdf  c*
pdf   *
pdf  c                   endsr
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Kaller eget program for generering av xml fil. Eget pgm         *
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     xml_create    begsr
pdf   *
pdf  c                   eval      splfname2 = *blanks
pdf  c                   eval      splfname3 = *blanks
pdf  c                   eval      jobname2 = *blanks
pdf  c                   eval      jobname3 = *blanks
pdf  c                   eval      username2 = *blanks
pdf  c                   eval      username3 = *blanks
pdf  c                   eval      jobnbr2 = *blanks
pdf  c                   eval      jobnbr3 = *blanks
pdf  c                   eval      splfnbr2 = *zeros
pdf  c                   eval      splfnbr3 = *zeros
6.11 c                   eval      p_btyp = 'SALDOBALANSE'
6.20 c                   eval      p_dspl = 'Saldobalanse, kjørt dato: '+
6.20 c                                      %char(udate)
6.20 c                   eval      p_refn = 'HSB-' + %char(udate)
6.20 c                   eval      p_tagg0 = *blanks
6.20 c                   eval      p_tagg0val = *blanks
6.20 c                   eval      p_tagg1 = *blanks
6.20 c                   eval      p_tagg1val = *blanks
6.20 c                   eval      p_tagg2 = *blanks
6.20 c                   eval      p_tagg2val = *blanks
6.20 c                   eval      p_tagg3 = *blanks
6.20 c                   eval      p_tagg3val = *blanks
6.20 c                   eval      p_tagg4 = *blanks
6.20 c                   eval      p_tagg4val = *blanks
6.20 c                   eval      p_tagg5 = *blanks
6.20 c                   eval      p_tagg5val = *blanks
6.20 c                   eval      p_tagg6 = *blanks
6.20 c                   eval      p_tagg6val = *blanks
6.20 c                   eval      p_tagg7 = *blanks
6.20 c                   eval      p_tagg7val = *blanks
6.20 c                   eval      p_tagg8 = *blanks
6.20 c                   eval      p_tagg8val = *blanks
6.20 c                   eval      p_tagg9 = *blanks
6.20 c                   eval      p_tagg9val = *blanks
pdf   *
6.20 c**                 call      'AP620R'
6.20 c                   call      'AP627R'
pdf  c                   PARM                    splfname
pdf  c                   PARM                    jobname
pdf  c                   PARM                    username
pdf  c                   PARM                    jobnbr
pdf  c                   PARM                    splfnbr
pdf  c                   PARM                    splfname2
pdf  c                   PARM                    jobname2
pdf  c                   PARM                    username2
pdf  c                   PARM                    jobnbr2
pdf  c                   PARM                    splfnbr2
pdf  c                   PARM                    splfname3
pdf  c                   PARM                    jobname3
pdf  c                   PARM                    username3
pdf  c                   PARM                    jobnbr3
pdf  c                   PARM                    splfnbr3
6.20 c                   parm                    p_tagg0
6.20 c                   parm                    p_tagg0val
6.20 c                   parm                    p_tagg1
6.20 c                   parm                    p_tagg1val
6.20 c                   parm                    p_tagg2
6.20 c                   parm                    p_tagg2val
6.20 c                   parm                    p_tagg3
6.20 c                   parm                    p_tagg3val
6.20 c                   parm                    p_tagg4
6.20 c                   parm                    p_tagg4val
6.20 c                   parm                    p_tagg5
6.20 c                   parm                    p_tagg5val
6.20 c                   parm                    p_tagg6
6.20 c                   parm                    p_tagg6val
6.20 c                   parm                    p_tagg7
6.20 c                   parm                    p_tagg7val
6.20 c                   parm                    p_tagg8
6.20 c                   parm                    p_tagg8val
6.20 c                   parm                    p_tagg9
6.20 c                   parm                    p_tagg9val
pdf  c                   PARM                    l_bach
6.11 c                   PARM                    p_btyp
6.20 c                   PARM                    p_refn
6.20 c                   PARM                    p_dspl
pdf   *
pdf  c                   endsr
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Subrutiner for XML/jasper integrasjon - Skjermvisning           *
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     pdf_preview   begsr
pdf  c                   if        l_skje = 1
pdf   * Vi kaller program for launch av PDF i browser
pdf  c                   call      'AP621R'
pdf  c                   parm                    l_bach
pdf   *
pdf  c                   endif
pdf   *
pdf  c                   endsr
pdf   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
pdf  c                   open      rh621p
      *
     c                   eval      totkto = *zeros
     c                   eval      risa01 = *zeros
      *
      * Behandle parameter
      *
     c                   eval      prfjo = rhprÅr - 1
      *
     c                   select
     c                   when      rhpsml = 'B'
     c                   eval      *in11 = *on
      *
     c                   when      rhpsml = 'A'
     c                   eval      *in21 = *on
      *
     c                   when      rhpsml = 'F'
     c                   eval      *in12 = *on
      *
     c                   when      rhpsml = 'G'
     c                   eval      *in13 = *on
     c                   endsl
      *
     c                   select
     c                   when      rhpavv = 'D'
     c                   eval      *in14 = *on
      *
     c                   when      rhpavv = 'P'
     c                   eval      *in15 = *on
     c                   endsl
      *
     c                   select
     c                   when      rhpsrt = 'A'
     c                   eval      *in18 = *on
      *
     c                   when      rhpsrt = 'B'
     c                   eval      *in20 = *on
      *
T5.50c                   when      rhpsrt = 'K' and
T5.50c                             rhpspa = 'J'
T5.50c                   eval      *in22 = *on
     c                   endsl
      *
     c                   if        rhppef <> rhppet
     c                   eval      *in19 = *on
     c                   endif
      *
      *  Flytt utvalgte avdelinger til tabell
      *
     c                   move      rhpa01        pav(01)
     c                   move      rhpa02        pav(02)
     c                   move      rhpa03        pav(03)
     c                   move      rhpa04        pav(04)
     c                   move      rhpa05        pav(05)
     c                   move      rhpa06        pav(06)
     c                   move      rhpa07        pav(07)
     c                   move      rhpa08        pav(08)
     c                   move      rhpa09        pav(09)
     c                   move      rhpa10        pav(10)
     c                   move      rhpa11        pav(11)
     c                   move      rhpa12        pav(12)
     c                   move      rhpa13        pav(13)
     c                   move      rhpa14        pav(14)
     c                   move      rhpa15        pav(15)
     c                   move      rhpa16        pav(16)
     c                   move      rhpa17        pav(17)
     c                   move      rhpa18        pav(18)
     c                   move      rhpa19        pav(19)
     c                   move      rhpa20        pav(20)
     c                   move      rhpa21        pav(21)
     c                   move      rhpa22        pav(22)
     c                   move      rhpa23        pav(23)
     c                   move      rhpa24        pav(24)
      *
      * Sjekk om avdelingsvalg utfylt
      *
     c                   xfoot     pav           tstav
      *
      * Sett startverdier
      *
     c                   eval      w_firm = l_firm
     c                   eval      nybrd = *zeros
     c                   eval      gmbrd = *zeros
     c                   eval      gmsps = *zeros
      *
      * Hent avdelingstekst fra koderegisteret
      *
     c                   if        rhpsrt = 'A'
      *
     c                   if        rhanul = 'J'
     c                   move      *zeros        p_gkod
     c                   else
     c                   move      rhpavf        p_gkod
     c                   endif
      *
     c                   eval      avtext = *blank
     c                   call      'RS707R'
     c                   parm                    p_stat
     c                   parm                    p_gkod
     c                   parm                    p_tx30
      *
     c                   move      p_gkod        wavde
     c                   move      p_tx30        avtext
      *
     c                   endif
      *
      * Hent kostnadsbærertekst fra koderegisteret
      *
     c                   if        rhpsrt = 'B'
      *
     c                   move      rhpbØf        p_Økod
     c                   move      rhpbØf        wspes
     c                   call      'RS728R'
     c                   parm                    p_stat
     c                   parm                    p_Økod
     c                   parm                    p_tx30
      *
     c                   if        p_stat = 'N'
     c                   eval      sptext = *blank
     c                   else
     c                   move      p_tx30        sptext
     c                   endif
      *
     c                   endif
      *
      * Skriv heading på første side
      *
     c                   write     head1
      *
     c                   eval      totsid = 1
     c                   eval      antkto = *zeros
     c                   eval      antsps = *zeros
      *
      * Key for saldoregister - konto
      *
     c     rhsal1_key    klist
     c                   kfld                    w_firm
      *
      * Key for saldoregister - avdeling
      *
     c     rhsal2_key    klist
     c                   kfld                    w_firm
      *
      * Key for saldoregister - Kostnadsbærer
      *
     c     rhsal3_key    klist
     c                   kfld                    w_firm
      *
      * Key for kontoplan
      *
     c     rhovlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhovlr_kont
     c                   kfld                    rhovlr_spes
     c                   kfld                    rhovlr_avde
      *
      * Key for kostnadsbærer
      *
     c     ra28lr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra28lr_Økod
      *
     c                   endsr
      *
**
K L A S S E S U M
G R U P P E S U M
