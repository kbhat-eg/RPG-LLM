     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LH708R
      * Beskrivelse . . : Finne ordresaldo pr. vare og dato
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       301100 ASK  Nytt program
      *       110806 bof  Lagt inn test på Lev.type = 1
      *       110806 bof  Tar hensyn til fortegn på linjetype/ordretype
6.10  *       131210 bof  Rettet feil vedr. å finne antall hvis retur
6.nu  *       230614 bof  sjekket mtp. nummerutvidelse
8.01  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffodtlv    if   e           k disk    rename(fodtpfr:fodtlvr)
     ffohelr    if   e           k disk    rename(fohepfr:fohelrr)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     fvltyl1    if   e           k disk    rename(vltypfr:vltyl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
      *
      * Parameter-inn
      *
     d                 ds
     d d_prec                        30
     d  d_vare                        8  0 overlay(d_prec)
     d  d_tdat                        6  0 overlay(d_prec:9)
     d  d_lage                        2  0 overlay(d_prec:15)
     d  d_anta                       11  3 overlay(d_prec:17)
      *
      * Local Data Area
      *
     d                uds
      *
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d fodtlv_vare     s                   like(fdvare)
     d fohelr_numm     s                   like(fonumm)
     d fohelr_suff     s                   like(fosuff)
     d votyl1_otyp     s                   like(vaotyp)
     d vltyl1_ltyp     s                   like(valtyp)
     d vvarl1_vare     s                   like(vvvare)
     d vvenl1_vare     s                   like(vevare)
      *
      * Definering av variable
      *
     d w_tdat          s               d   datfmt(*iso)
     d w_bdat          s               d   datfmt(*iso)
     d w_firm          s                   like(fofirm)
     d w_anta          s                   like(fdanta)
6.10 d w_anto          s                   like(fdanta)
     d w_bant          s                   like(fdanta)
     d w_omrl          s                   like(veomre)
     d w_omrs          s                   like(veomre)
     d w_parm          s             30
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram - leser poster fra ordrelinjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     fodtlv_key    reade     fodtlvr                                90
      *
     c                   dow       *in90 = *off
      *
      * Sjekk linjetype
      *
     c                   eval      vltyl1_ltyp = fdltyp
     c     vltyl1_key    chain     vltyl1                             91
     c                   if        vallag = 1
     c                   exsr      behandling
     c                   endif
      *
     c     fodtlv_key    reade     fodtlvr                                90
     c                   enddo
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Slutt- oppgaver
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      d_anta = w_anta
     c                   eval      w_parm = d_prec
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Behandler ordrelinjer - sjekker mot ordrehode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
6.10 c                   eval      w_anto = 0
      *
      *  Hent ordrehode informasjon
      *
     c                   eval      fohelr_numm = fdnumm
     c                   eval      fohelr_suff = fdsuff
     c     fohelr_key    chain     fohelr                             92
     c                   if        *in92 = *on
     c                   goto      end_behand
     c                   endif
      *
      * Sjekk riktig ordretype og lageroppdateringskode
      *
     c                   eval      votyl1_otyp = footyp
     c     votyl1_key    chain     votyl1                             93
     c                   if        vaosys <> 0 or
     c                             vaoakk <> 1 or
     c                             vaolag <> 1
     c                   goto      end_behand
     c                   endif
      *
      * Sjekk riktig lager
      *
     c                   if        fdlage <> d_lage
     c                   goto      end_behand
     c                   endif
      *
      * Sjekk hvilken leveringsdato som er registrert
      *
     c                   select
     c                   when      fdldat <> *loval
     c                   eval      w_bdat = fdldat
     c                   when      fopdat <> *loval
     c                   eval      w_bdat = fopdat
     c                   when      foldat <> *loval
     c                   eval      w_bdat = foldat
     c                   when      fobdat <> *loval
     c                   eval      w_bdat = fobdat
     c                   other
     c                   goto      end_behand
     c                   endsl
      *
      * Er ordre innenfor grensen?
      *
     c                   if        w_bdat > w_tdat
     c                   goto      end_behand
     c                   endif
      *
      * Er leveringstype =1 - skal ikke behandles
      *
     c                   if        fdlety = 1
     c                   goto      end_behand
     c                   endif
      *
      * Beregn ordre i lagerenhet
      *
     c                   eval      vvarl1_vare = fdvare
     c     vvarl1_key    chain     vvarl1r
      *
     c                   if        fdenh1 = vvenhl
6.10 c                   eval      w_anto = fdanta
     c                   goto      tst_linjet
     c                   endif
      *
      * Finner omregningsfaktor volum for lagerenhet og for ordre enhet
      * Antall regnes om etter forholdet mellom disse
      *
     c                   eval      vvenl1_vare = fdvare
     c     vvenl1_key    setll     vvenl1
     c     vvenl1_key    reade     vvenl1
     c                   dow       not %eof
     c                   if        fdenh1 = veenhe
     c                   eval      w_omrl = veomre
     c                   endif
     c                   if        vvenhl = veenhe
     c                   eval      w_omrs = veomre
     c                   endif
     c     vvenl1_key    reade     vvenl1
     c                   enddo
      *
     c                   if        w_omrs = 0
     c                   eval      w_omrs = 1
     c                   endif
      *
     c                   if        w_omrl <> 0
     c                   eval      w_bant = fdanta * w_omrl / w_omrs
     c                   else
     c                   eval      w_bant = 0
     c                   endif
6.10 c                   eval      w_anto = + w_bant
      *
     c     tst_linjet    tag
     c                   if        vaokre = '-'
6.10 c                   eval      w_anto = w_anto * -1
     c                   endif
      *
     c                   if        valkre = '-'
     c                   eval      w_anto = w_anto * -1
     c                   endif
6.10 c                   eval      w_anta = w_anta + w_anto
      *
     c     end_behand    endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for les av ORDRE-LINJE
      *
     c     fodtlv_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtlv_vare
      *
      *  Key les av ORDRE-HODE
      *
     c     fohelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelr_numm
     c                   kfld                    fohelr_suff
      *
      * Key for les av ordre-type
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      *  Key for les av LINJETYPE
      *
     c     vltyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltyl1_ltyp
      *
      *  Key for les av VARE
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for les av vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    w_parm
     c                   eval      d_prec = w_parm
      *
      *  Setter startverdier
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_anta = 0
      *
      *  Behandler parameterdato
      *
     c     *dmy          move      d_tdat        w_tdat
      *
      *  Legger inn startverdi for lesingen
      *
     c                   movel     d_vare        fodtlv_vare
      *
     c     fodtlv_key    setll     fodtlv
      *
     c                   endsr
