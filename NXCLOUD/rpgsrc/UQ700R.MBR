     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . :
      * Program . . . . : UQ700R
      * Beskrivelse . . : Brukere, oppretter brukere og selgere fra fil
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       090318 HKO  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjrvapf    if   e           k disk    rename(jrvapfr:jrvapfr)
     fra09l1    if   e           k disk    rename(ra09pfr:ra09l1r)
     fausrl1    if   e           k disk    rename(ausrpfr:ausrl1r)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
      *
     fra09lu    uf a e           k disk    rename(ra09pfr:ra09lur)
     fausrlu    uf a e           k disk    rename(ausrpfr:ausrlur)
     ffusrlu    uf a e           k disk    rename(fusrpfr:fusrlur)
     flusrlu    uf a e           k disk    rename(lusrpfr:lusrlur)
      *
      * Definering av LDA (Local Data Area)
      *
     d                uds
     d l_user                911    920
      *
      * Definering av datastruktur
      *
     d                 ds
     d d_urec                       240
     d  d_navn                       30    overlay(d_urec:1)
     d  d_vref                       20    overlay(d_urec:31)
     d  d_emai                       50    overlay(d_urec:51)
     d  d_mobi                       11    overlay(d_urec:101)
     d  d_selg                        4  0 overlay(d_urec:112)
     d  d_user                       10    overlay(d_urec:116)
     d  d_fgrp                        3    overlay(d_urec:126)
     d  d_firm                        3  0 overlay(d_urec:129)
     d  d_milj                        3    overlay(d_urec:132)
     d  d_avde                        4  0 overlay(d_urec:135)
     d  d_lage                        2  0 overlay(d_urec:139)
     d  d_bfgr                        1    overlay(d_urec:141)
     d  d_bfir                        1    overlay(d_urec:142)
     d  d_bavd                        1    overlay(d_urec:143)
     d  d_rlib                       10    overlay(d_urec:144)
     d  d_utko                       10    overlay(d_urec:154)
     d  d_meny                       10    overlay(d_urec:164)
     d  d_otyp                        2    overlay(d_urec:174)
     d  d_fkod                       19    overlay(d_urec:176)
     d  d_fk01                        1  0 overlay(d_urec:176)
     d  d_fk02                        1  0 overlay(d_urec:177)
     d  d_fk03                        1  0 overlay(d_urec:178)
     d  d_fk06                        1  0 overlay(d_urec:179)
     d  d_fk04                        1  0 overlay(d_urec:180)
     d  d_fk07                        1  0 overlay(d_urec:181)
     d  d_fk08                        1  0 overlay(d_urec:182)
     d  d_fk11                        1  0 overlay(d_urec:183)
     d  d_fk05                        1  0 overlay(d_urec:184)
     d  d_fk09                        1  0 overlay(d_urec:185)
     d  d_fk10                        1  0 overlay(d_urec:186)
     d  d_fk12                        1  0 overlay(d_urec:187)
     d  d_fk13                        1  0 overlay(d_urec:188)
     d  d_fk15                        1  0 overlay(d_urec:189)
     d  d_fk16                        1  0 overlay(d_urec:190)
     d  d_fk17                        1  0 overlay(d_urec:191)
     d  d_fk20                        1  0 overlay(d_urec:192)
     d  d_krab                        1  0 overlay(d_urec:193)
     d  d_fkdm                        1  0 overlay(d_urec:194)
     d  d_best                        1    overlay(d_urec:195)
     d  d_btyp                        2    overlay(d_urec:196)
     d  d_bkod                       10    overlay(d_urec:198)
     d  d_bk01                        1  0 overlay(d_urec:198)
     d  d_bk02                        1  0 overlay(d_urec:199)
     d  d_bk03                        1  0 overlay(d_urec:200)
     d  d_bk09                        1  0 overlay(d_urec:201)
     d  d_bk04                        1  0 overlay(d_urec:202)
     d  d_tans                        1  0 overlay(d_urec:203)
     d  d_bk05                        1  0 overlay(d_urec:204)
     d  d_bk06                        1  0 overlay(d_urec:205)
     d  d_bk10                        1  0 overlay(d_urec:206)
     d  d_bk11                        1  0 overlay(d_urec:207)
     d  d_bkdm                        1  0 overlay(d_urec:208)
      *
      * Definering av variable i nøkler
      *
     d ra09l1_ikod     s                   like(raikod)
     d ausrl1_user     s                   like(abuser)
     d afirl1_ffir     s                   like(aaffir)
      *
      * Definering av variable
      *
     d w_firm          s                   like(fbfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Utleder bruker og selgerinformasjon
      *
     c                   read      jrvapf
     c                   dow       not %eof
      *
     c                   eval      d_urec = jrvrad
      *
     c     X'46':'Æ'     xlate     d_navn        d_navn
     c     X'A0':'æ'     xlate     d_navn        d_navn
     c     X'77':'Ø'     xlate     d_navn        d_navn
     c     X'90':'ø'     xlate     d_navn        d_navn
     c     X'2A':'Å'     xlate     d_navn        d_navn
     c     X'EF':'å'     xlate     d_navn        d_navn
      *
     c     X'46':'Æ'     xlate     d_vref        d_vref
     c     X'A0':'æ'     xlate     d_vref        d_vref
     c     X'77':'Ø'     xlate     d_vref        d_vref
     c     X'90':'ø'     xlate     d_vref        d_vref
     c     X'2A':'Å'     xlate     d_vref        d_vref
     c     X'EF':'å'     xlate     d_vref        d_vref
      *
     c     ' ':'0'       xlate     d_fkod        d_fkod
     c     'x':'1'       xlate     d_fkod        d_fkod
     c     'X':'1'       xlate     d_fkod        d_fkod
      *
     c     ' ':'0'       xlate     d_bkod        d_bkod
     c     'x':'1'       xlate     d_bkod        d_bkod
     c     'X':'1'       xlate     d_bkod        d_bkod
      *
     c                   if        d_user = *blank
     c                   goto      neste_bruker
     c                   endif
      *
     c                   if        d_selg = 0
     c                   goto      neste_bruker
     c                   endif
      *
     c                   eval      w_firm = d_firm
      *
      * Oppretter selgere
      *
     c                   clear                   ra09lur
      *
     c                   eval      ra09l1_ikod = d_selg
     c     ra09l1_key    chain     ra09l1
     c                   if        not %found
     c                   eval      raifir = w_firm
     c                   eval      raikod = d_selg
     c                   eval      raitxt = %trim(d_vref)
     c                   eval      raiema = %trim(d_emai)
     c                   eval      raimob = %trim(d_mobi)
     c                   eval      raidat = %date
     c                   eval      raitim = %time
     c                   eval      raiusr = l_user
     c                   write     ra09lur
     c                   endif
      *
      * Oppretter brukere
      *
     c                   eval      ausrl1_user = d_user
     c     ausrl1_key    chain     ausrl1
     c                   if        %found
     c                   goto      neste_bruker
     c                   endif
      *
      *  AUSRPF
      *
     c     afirl1_key    chain     afirl1
      *
     c                   clear                   ausrlur
      *
     c                   eval      abuser = d_user
     c                   eval      abnavn = %trim(d_navn)
     c                   eval      abmeny = d_meny
     c                   eval      absfil = d_fgrp
     c                   eval      absbib = 'ADB' + d_fgrp
     c                   eval      absfir = w_firm
     c                   eval      absavd = d_avde
     c                   eval      ablagr = d_lage
     c                   eval      abfnav = aafnav
     c                   eval      abmilj = d_milj
     c                   eval      abbfil = d_bfgr
     c                   eval      abbfir = d_bfir
     c                   eval      abbavd = *blank
     c                   eval      abrlib = d_rlib
     c                   eval      abedat = %date
     c                   eval      abetim = %time
     c                   eval      abeusr = l_user
      *
     c                   write     ausrlur
      *
      *  FUSRPF
      *
     c                   clear                   fusrlur
      *
     c                   eval      fbfirm = w_firm
     c                   eval      fbuser = d_user
     c                   eval      fbvref = %trim(d_vref)
     c                   eval      fbselg = d_selg
     c                   if        d_otyp <> 'O '
     c                   eval      fbotyp = d_otyp
     c                   endif
     c                   eval      fbko01 = d_fk01
     c                   eval      fbko02 = d_fk02
     c                   eval      fbko03 = d_fk03
     c                   eval      fbko04 = d_fk04
     c                   eval      fbko05 = d_fk05
     c                   eval      fbko06 = d_fk06
     c                   eval      fbko07 = d_fk07
     c                   eval      fbko08 = d_fk08
     c                   eval      fbko09 = d_fk09
     c                   eval      fbko10 = d_fk10
     c                   eval      fbko11 = d_fk11
     c                   eval      fbko12 = d_fk12
     c                   eval      fbko13 = d_fk13
     c                   eval      fbko15 = d_fk15
     c                   eval      fbko16 = d_fk16
     c                   eval      fbko17 = d_fk17
     c                   eval      fbko20 = d_fk20
     c                   eval      fbodat = %date
     c                   eval      fbotim = %time
     c                   eval      fbousr = l_user
     c                   eval      fbedat = %date
     c                   eval      fbetim = %time
     c                   eval      fbeusr = l_user
      *
     c                   write     fusrlur
      *
      *  LUSRPF
      *
     c                   if        d_best <> ' '
      *
     c                   clear                   lusrlur
      *
     c                   eval      lbfirm = w_firm
     c                   eval      lbuser = d_user
     c                   eval      lbotyp = d_btyp
     c                   eval      lbvise = 2
     c                   eval      lbko01 = d_bk01
     c                   eval      lbko02 = d_bk02
     c                   eval      lbko03 = d_bk03
     c                   eval      lbko04 = d_bk04
     c                   eval      lbko05 = d_bk05
     c                   eval      lbko06 = d_bk06
     c                   eval      lbko09 = d_bk09
     c                   eval      lbko10 = d_bk10
     c                   eval      lbko11 = d_bk11
     c                   eval      lbtans = d_tans
     c                   eval      lbodat = %date
     c                   eval      lbotim = %time
     c                   eval      lbousr = l_user
     c                   eval      lbedat = %date
     c                   eval      lbetim = %time
     c                   eval      lbeusr = l_user
      *
     c                   write     lusrlur
      *
     c                   endif
      *
     c     neste_bruker  tag
      *
     c                   read      jrvapf
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på selger
      *
     c     ra09l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra09l1_ikod
      *
      * Key for oppslag på AUSRPF
      *
     c     ausrl1_key    klist
     c                   kfld                    ausrl1_user
      *
      * Key for oppslag på firma
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
     c                   endsr
