     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JR808R
      * Beskrivelse . . : Leser fil på recordformat LUNA_AVT (Luna avtale),
      *                   og oppretter/oppdaterer priser på eksisterende
      *                   varer i LVR.
      *
      *                   Forutsetter at vare med nobbnr finnes i VA
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       220410 bof  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjrvapf    if   e           k disk
      *
     fjrapl1    if   e           k disk    rename(jrappfr:jrapl1r)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fjvarl3    if   e           k disk    rename(jvarpfr:jvarl3r)
     fjvprl1    if   e           k disk    rename(jvprpfr:jvprl1r)
     fjvprlu    uf a e           k disk    rename(jvprpfr:jvprlur)
      *
      * Definering av parametre
      *
     d p_list          s             32
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
      *
      * Definering av parameterliste -inn
      *
     d                 ds
     d d_list                        32
     d  d_firm                        3  0 overlay(d_list)
     d  d_rapp                        2  0 overlay(d_list:4)
     d  d_dato                         d   overlay(d_list:6) datfmt(*iso)
      *
      * Definering av recordbeskrivelse - Luna
      *
     d                 ds
     d d_inpu                       262
     d  d_type                        3  0 overlay(d_inpu:1)
     d  d_vare                       24    overlay(d_inpu:4)
     d  d_eanx                       13    overlay(d_inpu:28)
     d  d_eann                       13  0 overlay(d_inpu:28)
     d  d_tek1                       30    overlay(d_inpu:41)
     d  d_sapr                        9  2 overlay(d_inpu:101)
     d  d_rabt                        4  2 overlay(d_inpu:132)
     d  d_raba                        4  2 overlay(d_inpu:137)
     d  d_rabg                        4  2 overlay(d_inpu:142)
     d  d_rabm                        4  2 overlay(d_inpu:147)
     d  d_enhe                        3    overlay(d_inpu:214)
     d  d_inpr                        9    overlay(d_inpu:229)
     d  d_inpr_kr                     6  0 overlay(d_inpr:1)
     d  d_inpr_ore                    2  2 overlay(d_inpr:8)
     d  d_nobb                        8    overlay(d_inpu:255)
      *
      * Definering av variable i nøkler
      *
     d jrapl1_rapp     s                   like(jrrapp)
     d jlevl1_ldor     s                   like(jlldor)
     d jvarl3_nobb     s                   like(jvnobb)
     d jvprlu_vare     s                   like(jxvare)
     d jvprlu_ldor     s                   like(jxldor)
     d jvprl1_vare     s                   like(jxvare)
     d jvprl1_ldor     s                   like(jxldor)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
      *
     d w_pris          s                   like(jxpris)
      *
     d b_inlr          s              3    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter rapportør-informasjon
      *
     c                   eval      jrapl1_rapp = d_rapp
     c     jrapl1_key    chain     jrapl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
      * Henter leverandørinformasjon
      *
     c                   eval      jlevl1_ldor = jrldor
     c     jlevl1_key    chain     jlevl1
      *
      * Behandling
      *
     c                   exsr      behandling
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av varer/priser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
     c                   read      jrvapf
     c                   dow       not %eof
      *
     c                   eval      d_inpu = jrvrad
      *
     c                   if        d_type <> 300
     c                   goto      les_neste
     c                   endif
      *
     c                   if        d_nobb = *blank
     c                   goto      les_neste
     c                   endif
      *
     c                   move      d_nobb        jvarl3_nobb
     c     jvarl3_key    chain     jvarl3
     c                   if        not %found
     c                   goto      les_neste
     c                   endif
      *
     c     ' ':'0'       xlate     d_inpr        d_inpr
     c     ' ':'0'       xlate     d_eanx        d_eanx
      *
     c                   eval      jvprl1_ldor = jrldor
     c                   eval      jvprl1_vare = jvvare
     c     jvprl1_key    chain     jvprl1
     c                   if        %found
     c                   exsr      endr_pris
     c                   else
     c                   exsr      oppr_pris
     c                   endif
      *
      * Oppdaterer søketekst
      *
     c                   if        jxvare <> *blank
     c                   eval      b_inlr = *on
     c                   call      'JV790R'
     c                   parm                    w_firm
     c                   parm                    jxvare
     c                   parm                    b_inlr
     c                   endif
      *
     c     les_neste     tag
      *
     c                   read      jrvapf
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting av pris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppr_pris     begsr
      *
      * Sletter eventuelt eksisterende priser for leverandør på varen
      *
     c                   eval      jvprlu_vare = jvvare
     c                   eval      jvprlu_ldor = jrldor
     c     jvprlu_key    setll     jvprlu
     c     jvprlu_key    reade     jvprlur
     c                   dow       not %eof
     c                   delete    jvprlur
     c     jvprlu_key    reade     jvprlur
     c                   enddo
      *
      * Oppretter pris på vare for leverandør
      *
     c                   clear                   jvprlur
      *
     c                   eval      jxvare = jvvare
     c                   eval      jxldor = jrldor
     c                   eval      jxlvar = d_vare
     c                   eval      jxpris = d_inpr_kr + d_inpr_ore
     c                   eval      jxgdat = d_dato
     c                   eval      jxtdat = *loval
     c                   eval      jxvalu = *blank
     c                   eval      jxeann = d_eann
     c                   eval      jxcrdo = 0
     c                   eval      jxrsts = 1
     c                   eval      jxeusr = l_user
      *
     c                   time                    jxodat
     c                   time                    jxedat
     c                   time                    jxetim
      *
     c                   write     jvprlur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for endring av pris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     endr_pris     begsr
      *
     c                   eval      w_pris = d_inpr_kr + d_inpr_ore
      *
     c                   if        w_pris = jxpris
     c                   leavesr
     c                   endif
      *
     c                   eval      jvprlu_vare = jxvare
     c                   eval      jvprlu_ldor = jrldor
     c     jvprlu_key    chain     jvprlur
      *
     c                   eval      jxlvar = d_vare
     c                   eval      jxpris = w_pris
     c                   eval      jxgdat = d_dato
     c                   eval      jxtdat = *loval
     c                   eval      jxeann = d_eann
     c                   eval      jxcrdo = 0
     c                   eval      jxeusr = l_user
      *
     c                   time                    jxedat
     c                   time                    jxetim
      *
     c                   update    jvprlur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_list
      *
      * Key for oppslag på rapportør
      *
     c     jrapl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jrapl1_rapp
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for oppslag på vare med NOBB-nummer
      *
     c     jvarl3_key    klist
     c                   kfld                    jvarl3_nobb
      *
      * Key for oppdatering av vare-pris
      *
     c     jvprlu_key    klist
     c                   kfld                    jvprlu_vare
     c                   kfld                    jvprlu_ldor
      *
      * Key for les av av vare-pris
      *
     c     jvprl1_key    klist
     c                   kfld                    jvprl1_vare
     c                   kfld                    jvprl1_ldor
      *
      *
      * Fyller datastruktur fra parameter, og henter firmakode
      *
     c                   eval      d_list = p_list
     c                   eval      w_firm = d_firm
      *
     c                   endsr
