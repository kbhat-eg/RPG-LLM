     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RK662R                                              *
      * Beskrivelse . . : Utskrift av renteberegning-kontrolliste             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
T5.20 * V5.30     060404  Lagt ut kjøredato på rapport                   KOB  *
T5.20 * V5.60     050808  Lagt inn test på bilagsdato                    KOB  *
      * V5.60     150908 Lagt inn utskrift av referanse på kontrollista  KOB  *
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frkwbl2    if   e           k disk    rename(rkwbpfr:rkwbl2r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     frk662p    o    e             printer oflind(*in30)
      *
     d sub             s              3  0 dim(13)
      *
     d                uds
     d  pfirm                300    302  0
     d  pnavn                303    332
     d  puser                333    342
     d  pkdat                343    348  0
     d  praar                349    352  0
     d  prper                353    354  0
     d  pavdf                355    358  0
     d  pavdt                359    362  0
     d  pkatf                363    366  0
     d  pkatt                367    370  0
     d  pself                371    374  0
     d  pselt                375    378  0
     d  pknrf                379    384  0
     d  pknrt                385    390  0
     d  kun                  391    450  0 dim(10)
     d  pknr1                391    396  0
     d  pknr2                397    402  0
     d  pknr3                403    408  0
     d  pknr4                409    414  0
     d  pknr5                415    420  0
     d  pknr6                421    426  0
     d  pknr7                427    432  0
     d  pknr8                433    438  0
     d  pknr9                439    444  0
     d  pknr0                445    450  0
     d  pfdat                451    456  0
     d  plope                457    459  0
     d  ppkod                460    460
     d  pgrns                464    465  0
     d  pgbyr                466    466
     d  pmini                467    468  0
     d  pbskr                469    469
     d  pegrn                470    470
     d  pajor                471    471
     d  ptext                472    472  0
     d  ppunr                473    473  0
     d  ppbeh                474    474
     d  palfa                475    475
     d  pdfgg                476    481  0
     d  pgeby                482    484  0
     d  ppros                485    488  2
     d  pparam               300    490
      *
     d  l_wsid               901    910
     d  l_user               911    920
     d  l_fgrp               931    933
     d  l_firm               944    946  0
     d  l_fnav               951    980
      *
     d w_bdat          s              6  0
     d wkjdat          s              6  0
     d w_firm          s                   like(rnfirm)
     d w_kund_gml      s                   like(rnkund)
     d w_biln_gml      s                   like(rnbiln)
     d w_bdat_gml      s                   like(rnbdat)
     d rkunl1_kund     s                   like(rnkund)
      *
     d w_forp          s             11  2
     d w_fort          s             11  2
     d n               s              3  0
      *
     c     dummy         tag
      *
     c                   read      rkwbl2r                                61
      *
     c                   dow       *in61 = *off
      *
      * Brudd på kundenummer
      *
     c                   if        rnkund <> w_kund_gml
      *
     c                   if        w_kund_gml > *zeros
      *
     c                   write     t1tot
      *
     c                   endif
      *
     c                   eval      w_forp = 0
      *
      * Les kunderegister
      *
     c                   eval      rkunl1_kund = rnkund
     c     rkunl1_key    chain     rkunl1                             10
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   eval      *in30 = *off
     c                   endif
      *
     c                   write     l1kun
      *
     c                   eval      w_kund_gml = rnkund
     c                   endif
      *
      * Behandle bilagsdato
      *
     c     *dmy          move      rnbdat        w_bdat
      *
      * Behandle rente fra dato
      *
     c                   if        rwrfda <> *loval
     c     *dmy          move      rwrfda        w_rfda
     c                   else
     c                   eval      w_rfda = 0
     c                   endif
      *
      * Behandle rente til dato
      *
     c                   if        rwrtda <> *loval
     c     *dmy          move      rwrtda        w_rtda
     c                   else
     c                   eval      w_rtda = 0
     c                   endif
      *
      * Sjekk om brudd i bilagsnummer eller bilagsdato
      *
     c                   if        rnbiln = w_biln_gml and
     c                             rnbdat = w_bdat_gml
     c                   eval      *in31 = *on
     c                   endif
      *
     c                   eval      w_biln_gml = rnbiln
     c                   eval      w_bdat_gml = rnbdat
      *
      * Akkumuler totalt forfalt
      *
     c                   eval      w_forp = w_forp + rwrent
     c                   eval      w_fort = w_fort + rwrent
      *
      * Ny side
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   eval      *in30 = *off
     c                   endif
      *
      * Skriv linje
      *
     c                   write     d1det
     c                   eval      *in31 = *off                                   FFÅR
      *
     c     neste         tag
      *
     c                   read      rkwbl2r                                61
     c                   enddo
      *
     c                   move      *on           *inlr
      *
     c                   write     t1tot
     c                   write     t2tot
      *
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
     c                   eval      n = *zeros
     c                   move      l_firm        w_firm
      *
     c                   eval      a1dato = pkdat
      *
     c                   write     a1hdr
      *
     c                   endsr
      *
