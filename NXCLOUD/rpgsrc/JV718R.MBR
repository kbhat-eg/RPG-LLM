     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JV718R
      * Beskrivelse . . : Oppdater fra ODA, oppr. varedetaljer på nye varer
      *
      *                   Varer som ikke finnes i vare-detalj-registeret,
      *                   opprettes med JVDHKO = 1 (Holdt)
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       060498 HKO  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjvdtl1    if   e           k disk    rename(jvdtpfr:jvdtl1r)
     fjvdtlu    uf a e           k disk    rename(jvdtpfr:jvdtlur)
      *
      * Definering av LDA
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d jvdtl1_dvar     s                   like(jvdvar)
      *
      * Definering av variable
      *
     d w_firm          s                   like(jvdfir)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser vareregister
      *
     c     *loval        setll     jvarl1
     c                   read      jvarl1                                 90
     c                   dow       *in90 = *off
      *
      * Oppdaterer vare-detalj
      *
     c                   clear                   jvdtlur
      *
     c                   eval      jvdtl1_dvar = jvvare
     c     jvdtl1_key    chain     jvdtl1                             91
     c                   if        *in91 = *on
     c                   eval      jvdfir = w_firm
     c                   eval      jvdvar = jvvare
     c                   eval      jvdpko = *blank
     c                   eval      jvdsko = 0
     c                   eval      jvdhko = 1
     c                   write     jvdtlur
     c                   endif
      *
     c                   read      jvarl1                                 90
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på vare-detalj
      *
     c     jvdtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jvdtl1_dvar
      *
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
