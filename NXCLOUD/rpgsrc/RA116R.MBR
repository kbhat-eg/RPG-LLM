     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RA116R
      * Beskrivelse . . : Valutakoder, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       260302 KOB  Nytt program
T5.00 *       170603 HFL  Rettet datofeil
7.xx  * 7.00  020316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 7.01  040321 ask  Oppslag mot historisk valuta register
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fra16l1    if   e           k disk    rename(ra16pfr:ra16l1r)
     fra16l2    if   e           k disk    rename(ra16pfr:ra16l2r)
     fra16lr    if   e           k disk    rename(ra16pfr:ra16lrr)
     fra16lu    uf a e           k disk    rename(ra16pfr:ra16lur)
      *
     fra116d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d ra16l1_pkod     s                   like(rapkod)
     d ra16l2_pmna     s                   like(rapmna)
     d ra16lr_pkod     s                   like(rapkod)
     d ra16lu_pkod     s                   like(rapkod)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(rapfir)
7.01 d w_pkod          s                   like(rapkod)
      *
     d s_pkod          s                   like(rapkod)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste recordnummer på siden
      * w_curn - Current recordnummer på siden
      * w_virn - Siden som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2pkod <> *blank or
     c                             b2pmna <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_sfrn <> 0
     c     w_sfrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      ra16l2_pmna = b1pmna
     c     ra16l2_key    setll     ra16l2
     c                   other
     c                   eval      ra16l1_pkod = b1pkod
     c     ra16l1_key    setll     ra16l1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Valuta
      *
     c                   if        b2pkod <> *blank
     c                   eval      w_seqe = *blank
     c                   eval      ra16l1_pkod = b2pkod
     c     ra16l1_key    setll     ra16l1
     c                   goto      end_pos
     c                   endif
      *
      * Beskrivelse
      *
     c                   if        b2pmna <> *blank
     c                   eval      w_seqe = 'B'
     c                   eval      ra16l2_pmna = b2pmna
     c     ra16l2_key    setll     ra16l2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2pkod = *blank
     c                   eval      b2pmna = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1pkod = b1pkod
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1pkod = b1pkod
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1pkod = b1pkod
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1pkod = b1pkod
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
7.01  *
7.01  * Vis historiske valutakurser
7.01  *
7.01 c                   if        b1valg = 7
7.01 c                   eval      w_pkod = b1pkod
7.01 c                   exsr      his_val
7.01 c                   eval      b1valg = 0
7.01 c                   update    b1sfl
7.01 c                   iter
7.01 c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1pkod = b2pkod
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1pkod = *blank
     c                   goto      end_c1bld
     c                   endif
      *
      * Validering
      *
     c***                if        c1pkod = *blank
     c***                eval      *in31  = *on
     c***                goto      c1taga
     c***                endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      ra16lr_pkod = c1pkod
     c     ra16lr_key    chain     ra16lr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1pkod = *blank
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2pkod = *blank
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      ra16lr_pkod = c1pkod
     c     ra16lr_key    chain     ra16lr
      *
     c                   if        %found
     c                   eval      c2pmna = rapmna
      *
     c                   if        rapkda = *loval
     c                   move      *zeros        c2pkda
     c                   else
     c     *dmy          move      rapkda        c2pkda
     c                   endif
      *
     c                   eval      c2pmfo = rapmfo                                           .
     c                   eval      c2pmku = rapmku                                           .
     c                   eval      c2pmkg = rapmkg                                           .
     c                   eval      c2pbkt = rapbkt                                           .
     c                   eval      c2pavd = rapavd                                           .
     c                   eval      c2pkto = rapkto                                           .
     c                   eval      c2psps = rapsps                                           .
     c                   eval      c2eusr = rapusr
     c     *dmy          move      rapdat        c2edat
     c                   else
     c                   eval      c2pmna = *blank
E5.00c                   eval      c2pmfo = *blank                                           .
T5.00c                   eval      c2pkda = *zeros                                           .
     c                   eval      c2pmku = *zeros                                           .
     c                   eval      c2pmkg = *zeros                                           .
     c                   eval      c2pbkt = *zeros                                           .
     c                   eval      c2pavd = *zeros                                           .
     c                   eval      c2pkto = *zeros                                           .
     c                   eval      c2psps = *zeros                                           .
     c                   eval      c2eusr = *blank
     c                   eval      c2edat = 0
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1pkod = k1pkod
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
7.01 c                   if        *inkg
7.01 c                   eval      w_pkod = rapkod
7.01 c                   exsr      his_val
7.01 c                   goto      c2taga
7.01 c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c2taga
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2pmna = *blank
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
T5.00c     *dmy          test(d)                 c2pkda                 37
T5.00c                   if        *in37 = *on
T5.00c                   goto      c2taga
T5.00c                   endif
      *
      * Oppdatering
      *
     c                   clear                   ra16lur
      *
     c                   eval      ra16lu_pkod = c1pkod
     c     ra16lu_key    chain     ra16lur
      *
     c                   eval      rapmna = c2pmna
     c                   eval      rapmfo = c2pmfo                                           .
     c                   eval      rapmku = c2pmku                                           .
     c***  *dmy          move      rapkda        c2pkda
T5.00c                   if        c2pkda = *zeros
T5.00c                   move      *loval        rapkda
T5.00c                   else
     c     *dmy          move      c2pkda        rapkda
T5.00c                   endif
      *
      * Beregn faktor til valuta
      *
     c                   if        c2pmku <> *zero
     c                   eval      rapmti = 1 / c2pmku
     c                   endif
      *
     c                   eval      rapmkg = c2pmkg                                           .
      *
      * Beregn faktor til valuta - gammel
      *
     c                   if        c2pmkg <> *zero
     c                   eval      rapmtg = 1 / c2pmkg
     c                   endif
      *
     c                   eval      rapbkt = c2pbkt                                           .
     c                   eval      rapavd = c2pavd                                           .
     c                   eval      rapkto = c2pkto                                           .
     c                   eval      rapsps = c2psps                                           .
      *
     c                   time                    rapdat
     c                   time                    raptim
     c                   eval      rapusr = l_user
      *
     c                   if        %found
     c                   update    ra16lur
     c                   else
     c                   eval      rapfir = w_firm
     c                   eval      rapkod = c1pkod
     c                   write     ra16lur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1pmna = rapmna
     c     *dmy          move      rapkda        b1pkda
     c                   eval      b1pmku = rapmku
     c                   eval      b1pmkg = rapmkg
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      ra16lr_pkod = d1pkod
     c     ra16lr_key    chain     ra16lr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      ra16lu_pkod = d1pkod
     c     ra16lu_key    chain     ra16lur
      *
     c                   delete    ra16lur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_pkod = k1pkod
      *
      * Fyller kopierings-bildet
      *
     c                   eval      ra16lr_pkod = k1pkod
     c     ra16lr_key    chain     ra16lr
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1pkod = *blank
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      ra16lr_pkod = k1pkod
     c     ra16lr_key    chain     ra16lr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1pkod = s_pkod
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      ra16l2
     c                   other
     c                   read      ra16l1
     c                   endsl
      *
     c                   if        %eof or
     c                             rapfir <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      ra16l2_pmna = rapmna
     c                   other
     c                   eval      ra16l1_pkod = rapkod
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1pkod = rapkod
     c                   eval      b1pmna = rapmna
     c     *dmy          move      rapkda        b1pkda
     c                   eval      b1pmku = rapmku
     c                   eval      b1pmkg = rapmkg
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'B'
     c     ra16l2_key    setgt     ra16l2
     c                   readp     ra16l2
     c                   other
     c     ra16l1_key    setgt     ra16l1
     c                   readp     ra16l1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   readp     ra16l2
     c                   other
     c                   readp     ra16l1
     c                   endsl
      *
     c                   if        %eof or
     c                             rapfir <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      ra16l2_pmna = rapmna
     c                   other
     c                   eval      ra16l1_pkod = rapkod
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'B'
     c     ra16l2_key    setll     ra16l2
     c                   other
     c     ra16l1_key    setll     ra16l1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
7.01  *
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  * Subrutine for visning av valutahistorikk
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *
7.01 c     his_val       begsr
7.01  *
7.01 c                   call      'RA161R'
7.01 c                   parm                    w_pkod
7.01  *
7.01 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for posisjonering på valuta
      *
     c     ra16l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra16l1_pkod
      *
      * Key for posisjonering på beskrivelse
      *
     c     ra16l2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra16l2_pmna
      *
      * Key for les på valuta
      *
     c     ra16lr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra16lr_pkod
      *
      * Key for oppdatering av valuta
      *
     c     ra16lu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra16lu_pkod
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      ra16l1_pkod = *blank
     c     ra16l1_key    setll     ra16l1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
