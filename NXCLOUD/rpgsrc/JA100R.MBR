     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JA100R
      * Beskrivelse . . : Kode/status, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       190905 HKO  Lagt inn dato som viser når VA sist ble oppdatert
      *       081105 GRO  Lagt inn bryter for NOBB-nr som eget varenr
6.20  *       040511 bof  Lagt inn bruk nobb enhetsrekkefølge +
      *                   test og sikkerhet på felter
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjstsl1    if   e           k disk    rename(jstspfr:jstsl1r)
     fjstslu    uf a e           k disk    rename(jstspfr:jstslur)
      *
     fja100d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variable
      *
     d w_firm          s                   like(jaafir)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     jstsl1_key    chain     jstsl1
     c                   if        %found
     c                   eval      c2avsi = jaavsi
     c                   eval      c2aini = jaaini
     c                   eval      c2anob = jaanob
6.20 c                   eval      c2aerf = jaaerf
     c                   if        jaaopd <> *loval
     c     *dmy          move      jaaopd        c2aopd
     c                   endif
6.20 c                   if        jaaeda <> *loval
6.20 c     *dmy          move      jaaeda        c2aeda
6.20 c                   else
6.20 c                   eval      c2aeda = 0
6.20 c                   endif
6.20 c                   if        jaaoda <> *loval
6.20 c     *dmy          move      jaaoda        c2aoda
6.20 c                   else
6.20 c                   eval      c2aoda = 0
6.20 c                   endif
6.20 c                   eval      c2aeus = jaaeus
     c                   else
     c                   eval      c2avsi = 0
     c                   eval      c2aini = 0
     c                   eval      c2anob = 0
     c                   eval      c2aopd = 0
6.20 c                   eval      c2aerf = 0
6.20 c                   eval      c2aeda = 0
6.20 c                   eval      c2aoda = 0
6.20 c                   eval      c2aeus = *blank
     c                   endif
      *
6.20 c     c2taga        tag
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl
     c                   goto      avslutt
     c                   endif
6.20  * valider
6.20 c                   if        c2avsi < 90000000
6.20 c                   eval      *in31 = *on
6.20 c                   goto      c2taga
6.20 c                   endif
      *
     c     jstslu_key    chain     jstslur
      *
     c                   eval      jaavsi = c2avsi
     c                   eval      jaaini = c2aini
     c                   eval      jaanob = c2anob
6.20 c                   eval      jaaerf = c2aerf
6.20 c                   time                    jaaeda
6.20 c                   time                    jaaeti
6.20 c                   eval      jaaeus = c2aeus
      *
     c                   if        %found
     c                   update    jstslur
     c                   else
     c                   eval      jaafir = w_firm
6.20 c                   time                    jaaoda
     c                   write     jstslur
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
      * Key for oppslag på kode/status
      *
     c     jstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppdatering av kode/status
      *
     c     jstslu_key    klist
     c                   kfld                    w_firm
      *
     c                   endsr
