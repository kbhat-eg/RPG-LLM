      /TITLE  ASLAGR Overføring av feltverdier til lagerregister
     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LX405R
      * Beskrivelse . . : Kopierer feltverdier til lagerregister
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       261008 KOB  Nytt program
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flx405d    cf   e             workstn
     fvvarl1    if a e           k disk    rename(vvarpfr:vvarl1r)
     fvlaglu    uf   e           k disk    rename(vlagpfr:vlaglur)
     fmdlolu    uf a e           k disk    rename(mdlopfr:mdlolur)
      *
      * Definering av Local data area (LDA)
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variable
      *
     d w_firm          s                   like(vvfirm)
      *
      * Definering av variable i nøkler
      *
     d vlaglu_lage     s                   like(vllage)
     d vlaglu_vare     s                   like(vvvare)
     d mdlolu_lage     s                   like(mdlage)
     d mdlolu_loka     s                   like(mdloka)
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      *
      * A2BLD  - A2 Bilde for valg av parametre
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * A2BLD Behandle bilde for valg av utskriftskriteria              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     a2taga        tag
      *
      * Bare data
      *
     c     a2tagb        tag
      *
     c                   exfmt     a2bld
      *
      * Gå ut
      *
     c                   if        *inkc = *on
     c                   goto      avslutt
     c                   endif
      *
      *  Kontroll valgt lager
      *
     c                   if        a2lagr = 0
     c                   eval      *in31 = *on
     c                   goto      a2taga
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Legger inn verdi for ønsket lager
      *
     c                   eval      vlaglu_lage = a2lagr
      *
      * Leser vareregister, finner ønsket lager og oppdaterer lagerregister
      *
     c                   read      vvarl1                                 90
     c                   dow       *in90 = *off
      *
     c                   eval      vlaglu_vare = vvvare
     c     vlaglu_key    chain     vlaglur                            91
      *
     c                   if        %found(vlaglu)
      *
      * Lagerplass
      *
     c                   if        a2plas = 'J' and
     c                             vlplas = *blank
     c                   eval      vlplas = vvplas
     c                   endif
      *
      * Minimumsbeholdning
      *
     c                   if        a2mini = 'J' and
     c                             vlmini = 0
     c                   eval      vlmini = vvmini
     c                   endif
      *
      * Maximumsbeholdning
      *
     c                   if        a2maks = 'J' and
     c                             vlmaks = 0
     c                   eval      vlmaks = vvmaks
     c                   endif
      *
      * Bestillingspunkt
      *
     c                   if        a2bpun = 'J' and
     c                             vlbpun = 0
     c                   eval      vlbpun = vvbpun
     c                   endif
      *
      * Bestillingsmengde
      *
     c                   if        a2bmen = 'J' and
     c                             vlbmen = 0
     c                   eval      vlbmen = vvbmen
     c                   endif
      *
      * Leveringstid
      *
     c                   if        a2ltid = 'J' and
     c                             vlltid = 0
     c                   eval      vlltid = vvltid
     c                   endif
      *
      * Sikkerhetslager
      *
     c                   if        a2sikl = 'J' and
     c                             vlsikl = 0
     c                   eval      vlsikl = vvsikl
     c                   endif
      *
     c                   update    vlaglur
      *
      * Legger ut lagerlokasjon
      *
     c                   if        vvplas <> *blank
      *
     c                   clear                   mdlolur
      *
5.20 c                   eval      mdlolu_lage = a2lagr
     c                   eval      mdlolu_loka = vvplas
     c     mdlolu_key    chain     mdlolur
      *
     c                   if        not%found
     c                   eval      mdfirm = w_firm
     c                   eval      mdlage = a2lagr
     c                   eval      mdloka = vvplas
     c                   eval      mdlbes = %trim('Lagerplass ') + ' ' +  vvplas
     c                   time                    mdodat
     c                   write     mdlolur
     c                   endif
      *
     c                   endif
      *
     c                   endif
      *
     c                   read      vvarl1                                 90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på lager
      *
     c     vlaglu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlaglu_vare
     c                   kfld                    vlaglu_lage
      *
      * Key for oppdatering av lagerlokasjon
      *
     c     mdlolu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mdlolu_lage
     c                   kfld                    mdlolu_loka
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
