     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : fo753R
      * Beskrivelse . . : Sjekker hvor mange ordre :
      *                   med utbetalt depositum
      *                   med innbetalt depositum som ikke er bestilt.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       140117 bof  Nytt program
6.xx  *       030517 bsa  Justert pga jacada
7.xx  * 7.00  040419 bsa  Mulighet for bredere skjerm
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *    31-79 = Varsle m/feilmeldinger/skvdrmindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     ffo753d    cf   e             workstn
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variable i nøkler
      *
      *
      * Definering av variable
      *
     d b_funn          s              1    inz(*off)
      *
     d w_firm          s              3  0
     d w_user          s             10
     d w_ubet          s              6  0
     d w_ubes          s              6  0
     d w_acli          s              2  0
     d w_acpo          s              3  0
     d w_selgx         s              4
      *
      * - - - - - - - - - - - - - - - - -
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * M1WIN  -
      *
6.3q C/Exec SQL
6.3q C+  Set Option DatFmt=*ISO
6.3q C/End-Exec
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   exsr      finn_antall
     c                   if        w_ubet > 0  or
     c                             w_ubes > 0
     c                   eval      m1ubet = w_ubet
     c                   eval      m1ubes = w_ubes
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * A1WIN Behandle bilde på startmeny
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
6.xx c                   z-add     7             w_acli
6.xx c                   z-add     5             w_acpo
      *
     c                   exfmt     m1win
     c                   endif
      *
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_antall   begsr
      *
     c                   eval      w_ubet = 0
     c                   eval      w_ubes = 0
      *
      * finn antall ordre med ubetalt depositum
      *
     c/exec sql
     c+                  declare ubet cursor for
     c+                  select count(*) as w_ubet
     c+                  from   sdeppf
     c+                  left outer join fohepf
     c+                  on skfirm = fofirm and
     c+                     sknumm = fonumm
     c+                  where  skfirm = :w_firm and
     c+                               footyp in (select vaotyp from votypf
     c+                                      where vaoakk = 1)   and
     c+                         skdepo <> 0  and
     c+                         sktell = 0   and
     c+                         skdepo >  skdepb and
     c+                         foselg <> 0      and
     c+                         foselg in (select fbselg from fusrpf
     c+                                   where fbuser = :w_user)
     c+                  for read only
     c/end-exec
      *
     c/exec sql
     c+ open ubet
     c/end-exec
      *
     c/exec sql
     c+ fetch ubet
     c+ into  :w_ubet
     c/end-exec
      *
     c/exec sql
     c+ close ubet
     c/end-exec
      *
      *
      * finn antall ordre med betalt depopsitum, men ikke bestilt.
      *
     c/exec sql
     c+                  declare ubes cursor for
     c+                  select count(*) as w_ubes
     c+                  from   sdeppf
     c+                  left outer join fohepf
     c+                  on skfirm = fofirm and
     c+                     sknumm = fonumm
     c+                  where  skfirm = :w_firm and
     c+                  footyp in (select vaotyp from votypf
     c+                             where  vaofir = :w_firm and
     c+                                    vaoakk = 1)   and
     c+                  fonumm not in (select funumm from flogpf
     c+                             where fufirm = :w_firm and
     c+                                   fukode = 2) and
     c+                         skdepo <> 0  and
     c+                         skdepo <= skdepb and
     c+                         foselg <> 0      and
     c+                         foselg in (select fbselg from fusrpf
     c+                                   where fbuser = :w_user)
     c+                  for read only
     c/end-exec
      *
     c/exec sql
     c+ open ubes
     c/end-exec
      *
     c/exec sql
     c+ fetch ubes
     c+ into  :w_ubes
     c/end-exec
      *
     c/exec sql
     c+ close ubes
     c/end-exec

     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Henter parametre, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_user = l_user
      *
     c                   endsr
