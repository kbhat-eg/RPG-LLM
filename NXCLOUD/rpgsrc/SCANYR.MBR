      /title  Akkumulering av statistikk-register
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : AsistT
      * Program . . . . : SCANYR
      * Beskrivelse . . : Akkumumlering av statistikk-register
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * V5.40 070405 KOB  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsistl1    if   e           k disk    rename(sistpfr:sistl1r)
      *
      * Definering av parameter (inn)
      *
     d                 ds
     d wsparm                        64
     d  wsfirm                        3  0 overlay(wsparm:01)
     d  wshhaa                        4  0 overlay(wsparm:04)
     d  wsvers                        2    overlay(wsparm:08)
     d  wsakod                       12    overlay(wsparm:10)
     d  wsaabb                        1    overlay(wsparm:22)
     d  wskode                       12    overlay(wsparm:23)
      *
      * Definering av parameter (opp)
      *
     d                 ds
     d wkparm                       128
     d  wkfirm                        3  0 overlay(wkparm:01)
     d  wkhhaa                        4  0 overlay(wkparm:04)
     d  wkkode                       12    overlay(wkparm:08)
     d  wkperi                        2  0 overlay(wkparm:20)
     d  wkavde                             like(sikavd) overlay(wkparm:31)
     d  wklage                             like(silage) overlay(wkparm:35)
     d  wkotyp                             like(siotyp) overlay(wkparm:37)
     d  wkland                        3                 overlay(wkparm:39)
     d  wkdist                             like(sidist) overlay(wkparm:42)
     d  wkselg                             like(siselg) overlay(wkparm:44)
     d  wklkat                             like(silkat) overlay(wkparm:48)
     d  wkldor                             like(sildor) overlay(wkparm:52)
     d  wkogrp                             like(siogrp) overlay(wkparm:58)
     d  wkhgrp                             like(sihgrp) overlay(wkparm:60)
     d  wkugrp                             like(siugrp) overlay(wkparm:62)
     d  wkvare                             like(sivare) overlay(wkparm:65)
      *
     d  wkanta                       11  3              overlay(wkparm:81)
     d  wksumb                       11  2              overlay(wkparm:92)
     d  wksumk                       11  2              overlay(wkparm:103)
     d  wkrabb                       11  2              overlay(wkparm:114)
      *
      * Definering av variable i n√∏kler
      *
     d w_firm          s                   like(sifirm)
     d w_hhaa          s                   like(sipaar)
      *
     d sistl1_binr     s                   like(sibinr)
     d sistl1_line     s                   like(siline)
     d sistl1_bida     s                   like(sibida)
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * XINIT  Initisiering av utskriften                               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c                   move      *blank        wkparm
     c                   move      wskode        wkkode
     c                   move      wsfirm        wkfirm
     c                   move      wshhaa        wkhhaa
      *
      * Kaller opp subprogram for sletting
      *
     c                   call      'SCADER'
     c                   parm                    wkparm
      *
      * Legger inn verdier for start lesing statistikk-register
      *
     c                   eval      sistl1_binr = *zero
     c                   eval      sistl1_line = *zero
     c                   eval      sistl1_bida = *loval
     c     sistl1_key    setll     sistl1
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * XSTART Behandler linjer fra statistikk                         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     xstart        tag
     c                   read      sistl1                                 10
     c     *in10         cabeq     *on           xslutt
      *
      * Sjekk, Hopp ut dersom ikke riktig
      *
     c     sifirm        cabne     w_firm        xslutt
     c     sipaar        cabne     w_hhaa        xslutt
      *
      * Legger inn verdier fra statistikk-record
      *
     c                   move      *blank        wkparm
     c                   move      wskode        wkkode
      *
     c                   move      sifirm        wkfirm
     c                   move      sipaar        wkhhaa
     c                   move      sipmnd        wkperi
      *
     c                   move      sikavd        wkavde
     c                   move      silage        wklage
     c                   move      siotyp        wkotyp
     c                   move      siland        wkland
     c                   move      sidist        wkdist
     c                   move      siselg        wkselg
     c                   move      silkat        wklkat
     c                   move      sildor        wkldor
     c                   move      siogrp        wkogrp
     c                   move      sihgrp        wkhgrp
     c                   move      siugrp        wkugrp
     c                   move      sivare        wkvare
      *
     c                   z-add     sianta        wkanta
     c                   z-add     sisumb        wksumb
     c                   z-add     sisumk        wksumk
     c                   z-add     sirab1        wkrabb
     c                   add       sirab2        wkrabb
     c                   add       sirabo        wkrabb
      *
      * Kaller opp subprogram for oppdatering
      *
     c                   call      'VS001R'
     c                   parm                    wkparm
      *
      * Les neste
      *
     c                   goto      xstart
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
      *
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     csr   *inzsr        begsr
      *
      * Key for lesing av statistikk-register
      *
     c     sistl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_hhaa
     c                   kfld                    sistl1_binr
     c                   kfld                    sistl1_line
     c                   kfld                    sistl1_bida
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     *entry        plist
     c                   parm                    dsparm           64
     c                   move      dsparm        wsparm
      *
     c                   move      wsfirm        w_firm
     c                   eval      w_hhaa      = wshhaa
      *
     csr                 endsr
