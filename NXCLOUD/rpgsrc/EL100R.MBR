     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : EL100R
      * Beskrivelse . . : Leverandør bonus, vedlikehold - parameter (liste)
      *
      *                   Velger leverandør fra liste
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       271005 HKO  Nytt program
      * 6.10  220609 BSA  Oppdatert med sporingsinformasjon
      *
8.01  * 8.xx  070422 bsa  Tilpasset cloud
8.02  * 8.xx  120224 bsa  Lagt inn F-tast for eksport av info til excel.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frlevlr    if   e           k disk    rename(rlevpfr:rlevlrr)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     frlevl2    if   e           k disk    rename(rlevpfr:rlevl2r)
     felboi1    if   e           k disk    rename(elbost:elboi1r)
     felbei1    if   e           k disk    rename(elbest:elbei1r)
     felboiu    uf a e           k disk    rename(elbost:elboiur)
     felbeiu    uf a e           k disk    rename(elbest:elbeiur)
      *
     fel100d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d rlevlr_levr     s                   like(rllevr)
     d rlevl1_levr     s                   like(rllevr)
     d rlevl2_alfa     s                   like(rlalfa)
     d elboi1_blev     s                   like(elblev)
     d elbei1_eble     s                   like(eleble)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
      *
     d w_udat          s               d   datfmt(*iso)
     d w_udat_8        s              8
     d w_firm_alf      s              3
     d w_ldor_alf      s              6
     d w_paar_alf      s              4
      *
     d w_firm          s                   like(elfirm)
     d w_navn          s                   like(rlnavn)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * K1WIN  - K1 Vindu for kopiere
      * O1WIN  - K1 Vindu for starte beregnign av årsbonus for leverandør
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile                                  DSP SFL
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
8.02 c                   when      *inkf
8.02 c                   exsr      excl_eksport
8.02 c                   goto      b2tagb
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile                                  CRT SFL
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * F23=Vis alle / kun med bonus
      *
     c                   if        *inkx = *on
     c                   if        *in80 = *on
     c                   eval      *in80 = *off
     c                   else
     c                   eval      *in80 = *on
     c                   endif
     c                   exsr      forny
     c                   goto      b2taga
     c                   endif
      *
      * Posisjonering
      *
     c                   if        b2alfa <> *blank or
     c                             b2ldor <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'L'
     c                   eval      rlevl1_levr = b1ldor
     c     rlevl1_key    setll     rlevl1
     c                   other
     c                   eval      rlevl2_alfa = b1alfa
     c     rlevl2_key    setll     rlevl2
     c                   endsl
      *
     c                   exsr      clr_subfile                                  TØM SFL
     c                   exsr      crt_subfile                                  CRT SFL
      *
     c                   endsr
      *
8.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.02  * Subrutine for eksport av informasjon
8.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.02  *
8.02 c     excl_eksport  begsr
8.02  *
8.02 c                   call      'EL600C'
8.02  *
8.02 c                   endsr
8.02  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på navn/alfa
      *
     c                   if        b2alfa <> *blank
     c                   eval      w_seqe = *blank
     c                   eval      rlevl2_alfa = b2alfa
     c     rlevl2_key    setll     rlevl2
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på nummer
      *
     c                   if        b2ldor <> 0
     c                   eval      w_seqe = 'L'
     c                   eval      rlevl1_levr = b2ldor
     c     rlevl1_key    setll     rlevl1
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile                                  TØM SFL
     c                   exsr      crt_subfile                                  CRT SFL
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2alfa = *blank
     c                   eval      b2ldor = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Kopier bonus
      *
     c                   if        b1valg = 3
     c                   if        b1bonu <> 0
     c                   exsr      kopier_bonus
     c                   eval      b_forn = *on
     c                   endif
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Bonus
      *
     c                   if        b1valg = 7
     c                   call      'EL101R'
     c                   parm                    w_firm
     c                   parm                    b1ldor
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kalkuler årsbonus
      *
     c                   if        b1valg = 9
     c                   if        b1bonu <> 0
     c                   exsr      kalk_bonus
     c                   endif
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil                     SFL END
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'L'
     c                   read      rlevl1
     c                   other
     c                   read      rlevl2
     c                   endsl
      *
     c                   if        %eof or                                      EOF
     c                             rlfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      elboi1_blev = rllevr
     c     elboi1_key    setll     elboi1
     c     elboi1_key    reade     elboi1
     c                   if        %eof and
     c                             *in80 = *off
     c                   iter
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'L'
     c                   eval      rlevl1_levr = rllevr
     c                   other
     c                   eval      rlevl2_alfa = rlalfa
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1alfa = rlalfa
     c                   eval      b1navn = rlnavn
     c                   eval      b1ldor = rllevr
     c                   eval      b1bonu = 0
     c                   eval      b1sbon = 0
     c                   eval      b1bdat = 0
      *
     c                   if        not %eof(elboi1)
     c                   eval      b1bonu = 1
     c                   endif
      *
     c                   dow       not %eof(elboi1)
     c                   eval      b1sbon = b1sbon + elsbon
     c                   if        elbdat <> *loval
     c     *dmy          move      elbdat        b1bdat
     c                   endif
     c     elboi1_key    reade     elboi1
     c                   enddo
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   if        w_seqe = 'L'
     c     rlevl1_key    setgt     rlevl1
     c                   readp     rlevl1
     c                   else
     c     rlevl2_key    setgt     rlevl2
     c                   readp     rlevl2
     c                   endif
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   if        w_seqe = 'L'
     c                   readp     rlevl1
     c                   else
     c                   readp     rlevl2
     c                   endif
      *
     c                   if        %eof or
     c                             rlfirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      elboi1_blev = rllevr
     c     elboi1_key    chain     elboi1
     c                   if        not %found and
     c                             *in80 = *off
     c                   iter
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   if        w_seqe = 'L'
     c                   eval      rlevl1_levr = rllevr
     c                   else
     c                   eval      rlevl2_alfa = rlalfa
     c                   endif
      *
     c                   enddo
      *
     c                   if        %eof
     c                   if        w_seqe = 'L'
     c     rlevl1_key    setll     rlevl1
     c                   else
     c     rlevl2_key    setll     rlevl2
     c                   endif
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kopiering av bonus til annen leverandør
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kopier_bonus  begsr
      *
      * Fyller kopierings-bildet
      *
     c                   eval      k1ldor = 0
     c                   eval      elblev = b1ldor
      *
     c     k1tagb        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd = *on
     c                   if        w_afld = 'K1LDOR'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    k1ldor
     c                   parm                    w_navn
     c                   endif
     c                   goto      k1tagb
     c                   endif
      *
      * Sjekker input
      *
     c                   if        k1ldor = 0
     c                   eval      *in31 = *on
     c                   goto      k1tagb
     c                   endif
      *
     c                   eval      rlevlr_levr = k1ldor
     c     rlevlr_key    chain     rlevlr
     c                   if        not %found
     c                   eval      *in32 = *on
     c                   goto      k1tagb
     c                   endif
      *
     c                   eval      elboi1_blev = k1ldor
     c     elboi1_key    chain     elboi1
     c                   if        %found
     c                   eval      *in33 = *on
     c                   goto      k1tagb
     c                   endif
      *
      * Kopierer bonus og bonuselementer
      *
     c                   eval      elboi1_blev = b1ldor
     c     elboi1_key    setll     elboi1
     c     elboi1_key    reade     elboi1
     c                   dow       not %eof
      *
     c                   eval      elblev = k1ldor
     c                   eval      eleusr = l_user
     c                   time                    elodat
6.10 c                   time                    elotim
6.10 c                   eval      elousr = l_user
     c                   time                    eledat
     c                   time                    eletim
     c                   write     elboiur
      *
     c     elboi1_key    reade     elboi1
     c                   enddo
      *
     c                   eval      elbei1_eble = b1ldor
     c     elbei1_key    setll     elbei1
     c     elbei1_key    reade     elbei1
     c                   dow       not %eof
      *
     c                   eval      eleble = k1ldor
     c                   eval      eleeus = l_user
     c                   time                    eleoda
6.10 c                   time                    eleoti
6.10 c                   eval      eleous = l_user
     c                   time                    eleeda
     c                   time                    eleeti
     c                   write     elbeiur
      *
     c     elbei1_key    reade     elbei1
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kalkulering av årsbonus for leverandør
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kalk_bonus    begsr
      *
      * Fyller bildet
      *
     c                   eval      o1ldor = b1ldor
     c                   eval      o1lnav = b1navn
      *
     c                   time                    w_udat
     c     *iso0         move      w_udat        w_udat_8
     c                   movel     w_udat_8      o1paar
      *
     c     o1tagb        tag
      *
     c                   exfmt     o1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * Sjekker input
      *
     c                   if        o1paar = 0
     c                   movel     w_udat_8      o1paar
     c                   endif
      *
     c                   if        o1paar < 2000
     c                   eval      *in31 = *on
     c                   goto      o1tagb
     c                   endif
      *
      * Kaller kalkulasjonsprogram
      *
     c                   movel     w_firm        w_firm_alf
     c                   movel     b1ldor        w_ldor_alf
     c                   movel     o1paar        w_paar_alf
      *
     c                   call      'EL710C'
     c                   parm                    w_firm_alf
     c                   parm                    w_ldor_alf
     c                   parm                    w_paar_alf
      *
      * Oppdaterer subfile
      *
     c                   eval      b1sbon = 0
     c                   eval      b1bdat = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på leverandør
      *
     c     rlevlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevlr_levr
      *
      * Key for posisjonering på lev.nr
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for posisjonering på navn/alfa
      *
     c     rlevl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl2_alfa
      *
      * Key for oppslag/les på bonus
      *
     c     elboi1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    elboi1_blev
      *
     c     elboi1_key2   klist
     c                   kfld                    w_firm
      *
      * Key for les på leverandør bonuselement
      *
     c     elbei1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    elbei1_eble
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
      * Initierer skjermbildet
      *
     c                   eval      *in22 = *on
     c                   eval      *in80 = *off
      *
     c     elboi1_key2   chain     elboi1
     c                   if        not %found
     c                   eval      *in80 = *on
     c                   endif
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      rlevl2_alfa = *blank
     c     rlevl2_key    setll     rlevl2
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
