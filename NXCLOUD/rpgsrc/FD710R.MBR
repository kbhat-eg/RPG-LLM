      /TITLE  ASOFAK: Tilbakestilling av ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FD710R
      * Beskrivelse . . : Tilbakestilling av ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * R4.00 100102 AMD  Skrevet om til ny standard
      * 6.10  240609 BSA  Oppdatert med sporingsinformasjon
6.nu  * R6.30 270514 bsa  Programmet gjennomgått mtp nummerutvidelser.
8.01  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     ffodtlu    uf   e           k disk    rename(fodtpfr:fodtlur)
      *
6.10  * Definering av Local data area
6.10  *
6.10 d                uds
6.10 d l_user                911    920
      *
      * Definering av parametere
      *
     d p_retu          s                   like(w_alf1)
     d p_firm          s                   like(fdfirm)
6.nu d p_numm          s                   like(fdnumm)
     d p_suff          s                   like(fdsuff)
      *
      * Definering av variable i nøkler
      *
     d w_firm          s                   like(fdfirm)
     d fodtl1_numm     s                   like(fdnumm)
     d fodtl1_suff     s                   like(fdsuff)
     d fodtlu_numm     s                   like(fdnumm)
     d fodtlu_suff     s                   like(fdsuff)
     d fodtlu_line     s                   like(fdline)
      *
      * Definering av variable
      *
     d w_alf1          s              1
     d w_antb          s                   like(fdantb)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av ordre-linjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     fodtl1_key    setll     fodtl1r
     c     fodtl1_key    reade     fodtl1r                                90
     c                   dow       *in90 = *off
      *
      * Oppdatering av ORDRE-LINJE-REGISTER med nye opplysninger
      *
     c                   eval      w_antb = *zero
     c                   if        fdsuff >= 50
     c                   eval      w_antb = fdanta
     c                   endif
      *
     c                   eval      fodtlu_line = fdline
     c                   eval      fodtlu_suff = *zero
     c     fodtlu_key    chain     fodtlur                            94
     c                   if        *in94 = *off
     c                   eval      fdantb = w_antb
6.10 c                   time                    fdedat
6.10 c                   time                    fdetim
6.10 c                   eval      fdeusr = l_user
     c                   update    fodtlur
     c                   endif
      *
     c     fodtl1_key    reade     fodtl1r                                90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for file : ORDRE-LINJE-REGISTER LES
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
      *
      * Key for file : ORDRE-LINJE-REGISTER UPDATE
      *
     c     fodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtlu_numm
     c                   kfld                    fodtlu_suff
     c                   kfld                    fodtlu_line
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_retu
     c                   parm                    p_firm
6.nu c                   parm                    p_numm
     c                   parm                    p_suff
      *
      * Legger inn firmanummer i nøkklene
      *
     c                   eval      w_firm = p_firm
      *
      * Legger inn ordrenummer og suffix inn i nøkklene
      *
     c                   eval      fodtl1_numm = p_numm
     c                   eval      fodtl1_suff = p_suff
     c                   eval      fodtlu_numm = p_numm
     c                   eval      fodtlu_suff = p_suff
     c                   eval      fodtlu_line = *zero
      *
     c                   endsr
