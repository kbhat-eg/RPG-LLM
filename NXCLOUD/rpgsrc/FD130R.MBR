     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FD130R
      * Beskrivelse . . : Ordre-linje, oppretting av varelinje fra stat.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       171005 HKO  Nytt program
5.61  * R5.61 290908 AMD  Programmet feilet når det ble lagt på et
      *                   prosjektnummer (økonomi) på varelinja.
      *                   Parameterlista var ikke riktig.
5.70  * PRGR  290908 sst  Prisgruppe
      * 6.10  240609 BSA  Oppdatert med sporingsinformasjon
6.11  * 6.10  290609 bof  Utivdet eannr til 14 og brukt std. oppslag
6.11  * 6.10  220909 ASK  Nye parametre til VL001R
6.1a  * R6.10 070513 bof  EHF krever avrunding av FDSUMS
6.20  * 6.20  021110 bof  Utvidet med utg.dato pr. lager
6.20  * 6.20  221110 NHO  Proj er nå alfa
6.21  * 6.20  190511 bof  Utvidet VL710R med utg.dato og l.dor
6.22  * 6.20  080312 bof  Utvidet kall til VP715r
6.31  * 6.30  260114 bof  Utvidet nøkkel i JVPKPF
6.32  *       140314 bof  Lagt inn test på fra /til dato på pakning
6.nu  *       160614 bof  Endret ihht. nummerutvidelse
6.33  * 6.30  040615 bkv  Ordre økt fra 6 til 8      *
6.34  * 6.30  221015 BOF  Justert vp900r parm, pga. nummer.utv.
6.35  * 6.30  250117 BKV  Endre salgspris og kostpris ved endring av enhet
      *                   når skaffevare
6.36  * 6.30  170918 bkv  Utvidet med trelast sortiment
7.01  * 7.00  130919 bkv  Rettet feil i visning av kostpris < 1
8.01  *PRG2   080622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     ffusrl1    if   e           k disk    rename(fusrpfr:fusrl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjvpkl1    if   e           k disk    rename(jvpkpfr:jvpkl1r)
6.31 fjlevl3    if   e           k disk    rename(jlevpfr:jlevl3r)
     fvltpl1    if   e           k disk    rename(vltppfr:vltpl1r)
     fvltyl1    if   e           k disk    rename(vltypfr:vltyl1r)
     fvenhl1    if   e           k disk    rename(venhpfr:venhl1r)
     ffpril1    if   e           k disk    rename(fpripfr:fpril1r)
     ffprnl1    if   e           k disk    rename(fprnpfr:fprnl1r)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffodtlu    uf a e           k disk    rename(fodtpfr:fodtlur)
6.36 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
     ffd130d    cf   e             workstn
      *
      * Definering av parametre
      *
     d p_kode          s              1
     d p_firm          s                   like(fdfirm)
     d p_numm          s                   like(fdnumm)
     d p_suff          s                   like(fdsuff)
     d p_line          s                   like(fdline)
     d p_ltyp          s                   like(fdltyp)
     d p_lety          s                   like(fdlety)
     d p_vare          s                   like(fdvare)
     d p_anta          s                   like(fdanta)
     d p_enhe          s                   like(fdenh1)
     d p_sapr          s                   like(fdsapr)
     d p_kopr          s                   like(fdkopr)
     d p_ldor          s                   like(fdldor)
     d p_lvar          s                   like(fdlvar)
     d p_kost          s              1
5.70 d p_vtyp          s                   like(vvvtyp)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
      *
      * Definering av datastrukturer
      *
      * Parametre for henting av pris -inn
      *
     d                 ds
     d hirec                        120
     d  hifirm                        3  0 overlay(hirec)
     d  hirkat                        3  0 overlay(hirec:4)
     d  hikund                        6  0 overlay(hirec:7)
     d  hikpro                        6  0 overlay(hirec:13)
     d  hivare                       15    overlay(hirec:19)
     d  hilety                        1  0 overlay(hirec:34)
     d  hienhe                        3    overlay(hirec:35)
     d  hidato                         d   overlay(hirec:38) datfmt(*iso)
     d  hitime                         t   overlay(hirec:48) timfmt(*iso)
6.nu d  hinumm                        8  0 overlay(hirec:56)
6.nu d  hikode                        1    overlay(hirec:64)
6.nu d  hidekn                        5  2 overlay(hirec:65)
6.nu d  hiavgf                        1  0 overlay(hirec:70)
6.nu d  hipriv                        1  0 overlay(hirec:71)
6.nu d  hitilb                        1    overlay(hirec:72)
6.nu d  hiskaf                        1    overlay(hirec:73)
6.nu d  hildor                        6  0 overlay(hirec:74)
6.34 d  hisapr                        9  2 overlay(hirec:80)
6.34 d  hikopr                        9  2 overlay(hirec:89)
8.01 d  hiprgr                        2    overlay(hirec:98)
     d  hiinlr                        1    overlay(hirec:120)
      *
      * Parametre for henting av pris -ut
      *
     d                 ds
     d horec                         80
     d  holety                        1  0 overlay(horec)
     d  hokpri                        1    overlay(horec:2)
     d  hooppr                        9  2 overlay(horec:3)
     d  hosapr                        9  2 overlay(horec:12)
     d  hokopr                        9  2 overlay(horec:21)
     d  horab1                        5  2 overlay(horec:30)
     d  horab2                        5  2 overlay(horec:35)
     d  hodekn                        5  2 overlay(horec:40)
     d  hopros                        5  2 overlay(horec:45)
     d  hokamp                        5    overlay(horec:50)
     d  hoalme                        4  0 overlay(horec:55)
     d  hobrty                        1  0 overlay(horec:59)
     d  hobrr1                        5  2 overlay(horec:60)
     d  hobrr2                        5  2 overlay(horec:65)
     d  hoomva                        1    overlay(horec:70)
8.01 d  hoprgr                        2    overlay(horec:71)
      *
      * Lageroppdatering
      *
     d                 ds
6.nu d hlrec                        128
     d  hlvare                       15    overlay(hlrec)
     d  hllage                        2  0 overlay(hlrec:16)
     d  hldato                         d   overlay(hlrec:18) datfmt(*iso)
     d  hltime                         t   overlay(hlrec:28) timfmt(*iso)
     d  hlotyp                        2    overlay(hlrec:36)
     d  hlltyp                        2    overlay(hlrec:38)
     d  hlanta                       11  3 overlay(hlrec:40)
     d  hlkopr                        9  2 overlay(hlrec:51)
     d  hlrefr                       20    overlay(hlrec:60)
     d  hlsyst                        1    overlay(hlrec:80)
     d  hlaltk                        2    overlay(hlrec:81)
     d  hlrest                        1    overlay(hlrec:83)
     d  hltype                        1    overlay(hlrec:84)
     d  hlenhe                        3    overlay(hlrec:85)
     d  hlinlr                        1    overlay(hlrec:88)
     d  hllety                        1    overlay(hlrec:89)
6.nu d  hlonum                        8  0 overlay(hlrec:90)
6.nu d  hlolin                        4  0 overlay(hlrec:98)
      *
      * Definering av variabler i nøkler
      *
     d fusrl1_user     s                   like(fbuser)
     d vvarl1_vare     s                   like(vvvare)
     d jvarl1_vare     s                   like(jvvare)
     d jvpkl1_vare     s                   like(jwvare)
6.31 d jvpkl1_ldor     s                   like(jwldor)
     d jlevl3_enum     s                   like(jlenum)
     d vltpl1_lety     s                   like(vylety)
     d vltyl1_ltyp     s                   like(valtyp)
     d venhl1_eenh     s                   like(vaeenh)
5.70 d fpril1_prgr     s                   like(fpprgr)
     d fpril1_rkat     s                   like(fprkat)
     d fpril1_kkat     s                   like(fpkkat)
     d fpril1_kund     s                   like(fpkund)
     d fpril1_kpro     s                   like(fpkpro)
     d fpril1_vare     s                   like(fpvare)
     d fpril1_enhe     s                   like(fpenhe)
     d fpril1_lety     s                   like(fplety)
     d fprnl1_nkun     s                   like(fpnkun)
     d fprnl1_nkpr     s                   like(fpnkpr)
     d fprnl1_nvar     s                   like(fpnvar)
      *
      * Definering av variable
      *
     d b_skaf          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
5.31 d b_feil          s              1    inz(*off)
5.31 d b_inlr          s              1    inz(*off)
6.32 d b_pakn          s              1    inz(*off)
      *
6.20 d w_ddat          s               d   datfmt(*iso)
     d w_mvap          s              6  2
     d w_mvat          s              5  4
     d w_mvaf          s             10  9
     d w_omr2          s             12  6
     d w_omr3          s             12  6
     d w_stat          s              1
     d w_sald          s             11  2
     d w_retk          s              1
     d w_kont          s              5  0
     d w_spes          s              4  0
     d w_txt1          s             30
     d w_txt2          s             30
     d w_ldat          s               d   datfmt(*iso)
5.61 d w_utxt          s             30
6.32 d w_dato          s               d   datfmt(*iso)
      *
     d w_firm          s                   like(vvfirm)
     d w_numm          s                   like(fonumm)
     d w_suff          s                   like(fosuff)
     d w_line          s                   like(fdline)
6.33 d w_numa          s              8
5.02 d w_lina          s              4
      *
     d w_raba          s                   like(fdsums)
     d w_sumr          s                   like(fdsums)
     d w_sums          s                   like(fdsums)
     d w_enh1          s                   like(fdenh1)
     d w_sap1          s                   like(fdsapr)
     d w_bup1          s                   like(fdsapr)
     d w_enh2          s                   like(fdenh1)
     d w_sap2          s                   like(fdsapr)
     d w_bup2          s                   like(fdsapr)
     d w_enh3          s                   like(fdenh1)
     d w_sap3          s                   like(fdsapr)
     d w_bup3          s                   like(fdsapr)
     d w_bupr          s                   like(fdsapr)
     d w_sapr          s                   like(fdsapr)
     d w_kopr          s                   like(fdkopr)
     d w_ltxt          s                   like(vybesk)
     d w_rab1          s                   like(fdrab1)
6.35 d w_saps          s                   like(fdsapr)
6.35 d w_kops          s                   like(fdkopr)
5.33 d w_totm          s                   like(fdanta)
5.31 d w_jtxt          s             30
5.31 d w_jadr          s             30
5.31 d w_jpnr          s              4  0
5.31 d w_jste          s             30
5.31 d w_javd          s              4  0
      *
5.70 d w_pgla          s                   like(folage)
5.70 d w_pgav          s                   like(foavde)
5.70 d w_prgr          s                   like(fdprgr)
6.11 d w_east          s              1
6.11 d w_eann          s                   like(fdeann)
6.21 d w_udat          s               d   datfmt(*iso)
6.21 d w_ldor          s                   like(vvldor)
6.36 d w_lv_nobb       s                   like(vvlnob)
6.36 d w_lv_modn       s                   like(vvlmod)
6.36 d w_lv_lldo       s                   like(vvlnle)
6.36 d w_lv_llva       s                   like(vvllva)
      *
     d s_ant1          s                   like(fdanta)
     d s_ant2          s                   like(fdanta)
     d s_ant3          s                   like(fdanta)
     d s_anta          s                   like(fdanta)
     d s_enhe          s                   like(fdenh1)
     d s_sapr          s                   like(fdsapr)
     d s_rab1          s                   like(fdrab1)
     d s_rab2          s                   like(fdrab2)
     d s_lety          s                   like(fdlety)
     d s_kpri          s                   like(fdkpri)
     d s_kopr          s                   like(fdkopr)
     d s_dekn          s                   like(fdpasl)
     d s_priv          s                   like(fdpriv)
      *
      * Definering av konstanter
      *
     d lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter vareinformasjon
      *
     c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   eval      jvarl1_vare = p_vare
     c     jvarl1_key    chain     jvarl1
     c                   if        not %found
     c                   goto      avslutt
     c                   else
     c                   eval      b_skaf = *on
     c                   eval      vvvare = jvvare
     c                   eval      vvkord = 0
     c                   eval      vvnobb = jvnobb
     c                   eval      vvogrp = jvogrp
     c                   eval      vvhgrp = jvhgrp
     c                   eval      vvugrp = jvugrp
6.20 c                   eval      w_udat = jvudat
     c                   eval      vvnmod = jvmodn
5.70 c                   eval      p_vtyp = 'S'
     c                   if        jvete1 <> *blank
     c                   eval      vvtek1 = jvete1
     c                   eval      vvtek2 = jvete2
     c                   else
     c                   eval      vvtek1 = jvtek1
     c                   eval      vvtek2 = jvtek2
     c                   endif
     c                   endif
     c                   endif
6.11  *
6.11  * Kaller program for henting av ean-nr
6.11  *
6.11 c                   call      'VE711R'
6.11 c                   parm                    w_firm
6.11 c                   parm                    p_vare
6.11 c                   parm                    p_enhe
6.11 c                   parm                    w_eann
6.11 c                   parm                    w_east
5.70  *
5.70  * Kaller program for henting av varetype
5.70  *
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    vvvare
5.70 c                   parm                    folage
5.70 c                   parm                    p_vtyp
6.21 c                   parm                    w_udat
6.21 c                   parm                    w_ldor
6.21 c                   parm                    b_inlr
6.36 c                   parm                    w_lv_nobb
6.36 c                   parm                    w_lv_modn
6.36 c                   parm                    w_lv_lldo
6.36 c                   parm                    w_lv_llva
5.70  *
5.70 c                   eval      w_pgla = folage
5.70 c                   eval      w_pgav = foavde
5.70 c                   exsr      hent_prisgr
      *
      * Kaller program for henting av pris-informasjon på tre enheter
      *
     c                   if        b_skaf = *off
     c                   call      'VP715R'
     c                   parm                    w_firm
     c                   parm                    vvvare
5.70 c                   parm                    w_prgr
6.22 c                   parm                    folage
     c                   parm                    p_lety
     c                   parm                    fobdat
     c                   parm                    w_enh1
     c                   parm                    w_sap1
     c                   parm                    w_bup1
     c                   parm                    w_enh2
     c                   parm                    w_sap2
     c                   parm                    w_bup2
     c                   parm                    w_omr2
     c                   parm                    w_enh3
     c                   parm                    w_sap3
     c                   parm                    w_bup3
     c                   parm                    w_omr3
     c                   else
      *
     c                   eval      jvpkl1_vare = p_vare
6.31 c                   eval      jvpkl1_ldor = jlldor
     c     jvpkl1_key    setll     jvpkl1
     c     jvpkl1_key    reade     jvpkl1
     c                   dow       not %eof
6.32  *
6.32  * sjekker fra / tildato
6.32 c                   exsr      sjekk_pkdato
6.32 c                   if        b_pakn = *on
     c                   select
     c                   when      w_enh1 = *blank
     c                   eval      w_enh1 = jwenhe
     c                   when      w_enh2 = *blank and
     c                             jwenhe <> w_enh1
     c                   eval      w_enh2 = jwenhe
     c                   eval      w_omr2 = jwofak
     c                   when      w_enh3 = *blank and
     c                             jwenhe <> w_enh1 and
     c                             jwenhe <> w_enh2
     c                   eval      w_enh3 = jwenhe
     c                   eval      w_omr3 = jwofak
     c                   leave
     c                   endsl
6.32 c                   endif
     c     jvpkl1_key    reade     jvpkl1
     c                   enddo
      *
     c                   endif
      *
     c                   eval      hopros = 0
      *
      * Viser registreringbildet
      *
     c     tc1win        tag
      *
     c                   exsr      xc1win
     c                   if        *inkc or *inkl
     c                   goto      avslutt
     c                   endif
      *
      * Oppretter ordre-linje
      *
     c                   clear                   fodtlur
      *
     c                   eval      fdfirm = w_firm
     c                   eval      fdnumm = w_numm
     c                   eval      fdsuff = w_suff
     c                   eval      fdline = w_line
      *
      * Henter verdier fra skjermbildet
      *
     c                   eval      fdvare = p_vare
     c                   eval      fdanta = c1anta
     c                   eval      fdenh1 = c1enhe
     c                   eval      fdsapr = c1sapr
     c                   eval      fdrab1 = c1rab1
     c                   eval      fdrab2 = c1rab2
     c                   eval      fdlety = c1lety
5.32 c                   eval      fdltyp = c1ltyp
     c                   eval      fdkpri = c1kpri
     c                   eval      fdkopr = c1kopr
     c                   eval      fdtek1 = c1tek1
     c                   eval      fdtek2 = c1tek2
     c                   eval      fdpasl = c1dekn
     c                   eval      fdlass = c1lass
     c                   eval      fdproj = c1proj
     c                   eval      fdakti = c1akti
     c                   eval      fdldat = *loval
     c                   eval      fdalme = c1alme
     c                   eval      fdomva = c1omva
     c                   eval      fdbrty = c1brty
     c                   eval      fdbrr1 = c1brr1
     c                   eval      fdbrr2 = c1brr2
     c                   eval      fdpriv = c1priv
5.31 c                   eval      fdlage = c1lage
     c                   eval      fdoppr = hooppr
     c                   eval      fdkamp = hokamp
5.70 c                   eval      fdprgr = hoprgr
      *
     c                   if        b_skaf = *on
     c                   eval      fdlvre = 1
     c                   else
     c                   eval      fdlvre = 0
     c                   endif
      *
     c                   if        c1ldat <> 0
     c     *dmy          move      c1ldat        fdldat
     c                   endif
      *
      * Beregning av totaler
      *
6.1a c*                  eval      w_sums = fdsapr * fdanta
6.1a c                   eval(h)   w_sums = fdsapr * fdanta
     c                   eval      w_sumr = 0
      *
      * Rabatt
      *
     c                   if        vvkord = 0
     c                   eval(h)   w_raba = (w_sums * forabp) / 100
     c                   eval      w_sumr = w_raba
     c                   endif
      *
     c                   if        fdrab1 <> 0
     c                   eval(h)   w_raba = (w_sums - w_sumr) * fdrab1 / 100
     c                   eval      w_sumr = w_sumr + w_raba
     c                   endif
      *
     c                   if        fdrab2 <> 0
     c                   eval(h)   w_raba = (w_sums - w_sumr) * fdrab2 / 100
     c                   eval      w_sumr = w_sumr + w_raba
     c                   endif
      *
     c                   if        fdbrr1 <> 0
     c                   eval(h)   w_raba = (w_sums - w_sumr) * fdbrr1 / 100
     c                   eval      w_sumr = w_sumr + w_raba
     c                   endif
      *
     c                   if        fdbrr2 <> 0
     c                   eval(h)   w_raba = (w_sums - w_sumr) * fdbrr2 / 100
     c                   eval      w_sumr = w_sumr + w_raba
     c                   endif
      *
      * Kostpris  (*** ?)
      *
     c                   if        fdkopr = 0
     c                   eval      fdkopr = fdsapr * hopros
     c                   endif
      *
      * Legger inn totaler
      *
     c                   eval      fdsums = w_sums
     c                   eval      fdsumr = w_sumr
     c                   eval      fdsumk = fdkopr * fdanta
      *
      * Dersom kredit-linjetype må summene korrigeres
      *
     c                   eval      vltyl1_ltyp = fdltyp
     c     vltyl1_key    chain     vltyl1
     c                   if        valkre = '-'
     c                   eval      fdsums = fdsums * -1
     c                   eval      fdsumr = fdsumr * -1
     c                   eval      fdsumk = fdsumk * -1
     c                   endif
      *
      * Legger inn faste opplysninger fra vare og ordre
      *
     c                   eval      fdotyp = footyp
     c                   eval      fdkakk = 0
     c                   eval      fdkgeb = 0
      *
     c                   eval      fdkund = fokund
     c                   if        folkun <> 0
     c                   eval      fdkund = folkun
     c                   endif
      *
     c                   eval      fdbenr = 0
     c                   eval      fdavde = 0
     c                   eval      fdkont = 0
     c                   eval      fdspes = 0
     c                   eval      fdldor = p_ldor
     c                   eval      fdlvar = p_lvar
6.36 c                   if        w_lv_nobb > 0
6.36 c                   eval      fdnobb = w_lv_nobb
6.36 c                   else
     c                   eval      fdnobb = vvnobb
6.36 c                   endif
     c                   eval      fdogrp = vvogrp
     c                   eval      fdhgrp = vvhgrp
     c                   eval      fdugrp = vvugrp
      *
      * Oversetter varetekst til store bokstaver dersom dette er valgt
      *
     c                   if        faahoy = 1
     c     lo:up         xlate     fdtek1        fdtek1
     c     lo:up         xlate     fdtek2        fdtek2
     c                   endif
      *
      * Oppretter ordre-linje
      *
6.10 c                   time                    fdodat
6.10 c                   time                    fdotim
6.10 c                   eval      fdoous = l_user
6.10 c                   time                    fdedat
6.10 c                   time                    fdetim
6.10 c                   eval      fdeusr = l_user
     c                   write     fodtlur
      *
      * Oppdatering av lager
      *
     c                   exsr      oppdat_lager
      *
      * Legger inn kode som sier at minst en linje er registrert, og
      * returnerer om kostpris skal vises eller ikke
      *
     c                   eval      p_kode = *on
     c                   eval      p_kost = *in86
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avtalenotat
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     notat         begsr
      *
      * Finner nivået som spesialprisen er hentet fra:
      *
      * Kunde/kundeprosjekt
      *
5.70 c                   eval      fpril1_prgr = hoprgr
     c                   eval      fpril1_rkat = 0
     c                   eval      fpril1_kkat = 0
     c                   eval      fpril1_kund = fokund
     c                   eval      fpril1_kpro = fokpro
     c                   eval      fpril1_vare = c1vare
     c                   eval      fpril1_enhe = c1enhe
     c                   eval      fpril1_lety = c1lety
      *
     c                   if        folkun <> 0
     c                   eval      fpril1_kund = folkun
     c                   endif
      *
     c     fpril1_key    chain     fpril1
      *
     c                   if        not %found and
     c                             c1lety <> 0
     c                   eval      fpril1_lety = 0
     c     fpril1_key    chain     fpril1
     c                   endif
      *
     c                   if        not %found
     c                   eval      fpril1_enhe = *blank
     c                   eval      fpril1_lety = c1lety
     c     fpril1_key    chain     fpril1
     c                   if        not %found and
     c                             c1lety <> 0
     c                   eval      fpril1_lety = 0
     c     fpril1_key    chain     fpril1
     c                   endif
     c                   endif
      *
      * Kunde
      *
     c                   if        not %found and
     c                             fokpro <> 0
      *
     c                   eval      fpril1_kpro = 0
     c                   eval      fpril1_enhe = c1enhe
     c                   eval      fpril1_lety = c1lety
      *
     c     fpril1_key    chain     fpril1
      *
     c                   if        not %found and
     c                             c1lety <> 0
     c                   eval      fpril1_lety = 0
     c     fpril1_key    chain     fpril1
     c                   endif
      *
     c                   if        not %found
     c                   eval      fpril1_enhe = *blank
     c                   eval      fpril1_lety = c1lety
     c     fpril1_key    chain     fpril1
     c                   if        not %found and
     c                             c1lety <> 0
     c                   eval      fpril1_lety = 0
     c     fpril1_key    chain     fpril1
     c                   endif
     c                   endif
      *
     c                   endif
      *
      * Avbryter dersom spesialpris ikke ble funnet
      *
     c                   if        not %found
     c                   leavesr
     c                   endif
      *
     c                   eval      fprnl1_nkun = fpril1_kund
     c                   eval      fprnl1_nkpr = fpril1_kpro
     c                   eval      fprnl1_nvar = fpril1_vare
      *
     c     fprnl1_key    chain     fprnl1
     c                   if        %found
     c                   call      'FP550R'
     c                   parm                    w_firm
     c                   parm                    fpril1_kund
     c                   parm                    fpril1_kpro
     c                   parm                    fpril1_vare
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * C1 Behandle bilde for registrering av varelinje med antall
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1win        begsr
      *
      * Henter inn priser og rabatter
      *
     c                   eval      hienhe = p_enhe
     c                   eval      hilety = p_lety
     c                   eval      hidekn = 0
     c                   eval      hiavgf = fomkod
     c                   eval      hipriv = 0
5.70 c                   eval      hiprgr = w_prgr
      *
     c                   exsr      hent_pris
      *
      * Initierer skjermbildet
      *
     c                   eval      *in81 = w_enh1 = *blank
      *
     c                   eval      *in82 = w_enh2 = *blank
     c                   eval      *in83 = w_enh3 = *blank
      *
6.20 c                   eval      *in84 = w_udat <> *loval
     c                   eval      *in85 = *off
     c                   eval      *in88 = *off
      *
     c                   eval      c1anta = p_anta
     c                   eval      c1enhe = p_enhe
     c                   eval      c1sapr = p_sapr
     c                   eval      c1rab1 = 0
     c                   eval      c1rab2 = 0
     c                   eval      c1lety = p_lety
     c                   eval      c1kpri = 'F'
     c                   eval      c1kopr = hokopr
     c                   eval      c1ltyp = p_ltyp
     c                   eval      c1tek1 = vvtek1
     c                   eval      c1tek2 = vvtek2
     c                   eval      c1enh1 = w_enh1
     c                   eval      c1enh2 = w_enh2
     c                   eval      c1enh3 = w_enh3
     c                   eval      c1vare = p_vare
6.36 c                   if        w_lv_nobb > 0
6.36 c                   eval      c1nobb = w_lv_nobb
6.36 c                   eval      c1nmod = w_lv_modn
6.36 c                   else
     c                   eval      c1nobb = vvnobb
     c                   eval      c1nmod = vvnmod
6.36 c                   endif
6.11 c                   eval      c1eann = w_eann
5.70 c                   eval      c1vtyp = p_vtyp
     c                   eval      c1saim = 0
     c                   eval      c1dekn = 0
     c                   eval      c1pasl = 0
     c                   eval      c1lass = 0
     c                   eval      c1ldat = 0
6.20 c**                 eval      c1proj = 0
6.20 c                   eval      c1proj = *blank
     c                   eval      c1akti = *blank
     c                   eval      c1ant1 = 0
     c                   eval      c1ant2 = 0
     c                   eval      c1ant3 = 0
     c                   eval      c1alme = hoalme
     c                   eval      c1omva = hoomva
     c                   eval      c1brty = hobrty
     c                   eval      c1brr1 = hobrr1
     c                   eval      c1brr2 = hobrr2
     c                   eval      c1priv = 0
5.31 c                   eval      c1lage = folage
     c                   eval      c1udat = 0
      *
6.20 c                   if        w_udat <> *loval
6.20 c     *dmy          move      w_udat        c1udat
     c                   endif
      *
      * Kaller subrutine for å vise antall i andre enheter
      *
     c                   exsr      kalkulator
      *
     c                   if        w_enh1 = *blank
     c                   eval      c1enh1 = p_enhe
     c                   eval      c1ant1 = p_anta
     c                   endif
      *
      * Henter lagerbeholdning
      *
     c                   exsr      lagerbeholdn
      *
      * Tar vare på eksisterende verdier
      *
     c                   eval      hosapr = c1sapr
      *
     c                   eval      s_ant1 = c1ant1
     c                   eval      s_ant2 = c1ant2
     c                   eval      s_ant3 = c1ant3
     c                   eval      s_anta = c1anta
     c                   eval      s_enhe = c1enhe
     c                   eval      s_sapr = c1sapr
     c                   eval      s_lety = c1lety
     c                   eval      s_kpri = c1kpri
     c                   eval      s_kopr = c1kopr
     c                   eval      s_dekn = c1dekn
     c                   eval      s_priv = c1priv
      *
      * Viser avtalenotat dersom avtalt pris og ny linje
      *
     c                   if        c1kpri = 'A'
     c                   exsr      notat
     c                   endif
      *
      * Viser bildet
      *
     c     c1tagb        tag
      *
     c                   eval      s_rab1 = c1rab1
     c                   eval      s_rab2 = c1rab2
      *
     c                   eval      c1ntpr = c1sapr
     c                   if        c1rab1 > 0
     c                   eval(h)   c1ntpr = c1ntpr - (c1ntpr * c1rab1 / 100)
     c                   endif
     c                   if        c1rab2 > 0
     c                   eval(h)   c1ntpr = c1ntpr - (c1ntpr * c1rab2 / 100)
     c                   endif
     c                   if        c1brr1 > 0
     c                   eval(h)   c1ntpr = c1ntpr - (c1ntpr * c1brr1 / 100)
     c                   endif
     c                   if        c1brr2 > 0
     c                   eval(h)   c1ntpr = c1ntpr - (c1ntpr * c1brr2 / 100)
     c                   endif
      *
     c                   eval      c1ntim = c1ntpr * w_mvat
      *
     c                   eval      c1odek = 0
     c                   if        c1kopr <> 0 and
     c                             c1ntpr <> 0
     c                   select
     c                   when      (100 - (c1kopr * 100 / c1ntpr)) > 999,99
     c                   eval      c1odek = 999,99
     c                   when      (100 - (c1kopr * 100 / c1ntpr)) < -999,99
     c                   eval      c1odek = -999,99
     c                   other
     c                   eval(h)   c1odek = 100 - (c1kopr * 100 / c1ntpr)
     c                   endsl
     c                   endif
      *
     c                   eval      c1ntsu = c1ntpr * c1anta
     c                   eval      c1nisu = c1ntsu * w_mvat
      *
     c                   exfmt     c1win
      *
     c                   eval      b_forn = *off
      *
      * F2=Manuell beregning av kostpris
      *
     c                   if        *inkb = *on
     c                   call      'FO524R'
     c                   parm                    w_kopr
     c                   if        w_kopr <> 0
     c                   eval      c1kopr = w_kopr
     c                   eval      s_kopr = w_kopr
     c                   if        c1dekn <> 0
     c                   exsr      dekning
     c                   endif
     c                   endif
     c                   goto      c1tagb
     c                   endif
      *
      * F3=Gå tilbake
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * F4=Spørring
      *
     c                   if        *inkd  = *on
      *
     c                   if        w_afld = 'C1LETY'
     c                   call      'VY500R'
     c                   parm                    w_firm
     c                   parm                    c1lety
     c                   parm                    w_ltxt
     c                   goto      c1tagb
     c                   endif
      *
     c                   if        w_afld = 'C1PROJ'
5.61 c                   eval      w_kont = 0
5.61 c                   eval      w_spes = 0
     c                   call      'RP500R'
     c                   parm                    w_firm
     c                   parm                    c1proj
5.61 c                   parm                    c1akti
5.61 c                   parm                    w_kont
5.61 c                   parm                    w_spes
     c                   parm                    w_txt1
     c                   goto      c1tagb
     c                   endif
      *
5.31 c                   if        w_afld = 'C1LAGE'
5.31 c                   call      'RA510R'
5.31 c                   parm                    w_firm
5.31 c                   parm                    c1lage
5.31 c                   parm                    w_txt1
5.31 c                   goto      c1tagb
5.31 c                   endif
      *
     c                   endif
      *
      * F8=Produktinformasjon
      *
     c                   if        *inkh = *on
     c                   call      'FO704C'
     c                   parm                    w_firm
     c                   parm                    p_vare
     c                   goto      c1tagb
     c                   endif
      *
      * F9=Lagerinformasjon
      *
     c                   if        *inki = *on
     c                   call      'LL502R'
     c                   parm                    w_firm
     c                   parm                    p_vare
     c                   goto      c1tagb
     c                   endif
      *
      * F10=Notat
      *
     c                   if        *inkj = *on
     c                   exsr      notat
     c                   goto      c1tagb
     c                   endif
5.33  *
5.33  * F06=Tilleggsspesif./linjer
5.33  *
5.33 c                   if        *inkf = *on
5.33 c                   call      'FD115R'
5.33 c                   parm                    w_firm
5.33 c                   parm                    p_numm
5.33 c                   parm                    p_suff
5.33 c                   parm                    p_line
5.33 c                   parm                    p_vare
5.33 c                   parm                    w_totm
5.33 c                   eval      c1anta = w_totm
5.33 c                   eval      c1ant1 = w_totm
5.33 c                   goto      c1tagb
5.33 c                   endif
5.33  *
5.33  * F07=Pakketre
5.33  *
5.33 c                   if        *inkg = *on
5.33 c                   call      'FD117R'
5.33 c                   parm                    w_firm
5.33 c                   parm                    p_numm
5.33 c                   parm                    p_suff
5.33 c                   parm                    p_line
5.33 c                   parm                    p_vare
5.33 c                   parm                    w_totm
5.33 c                   eval      c1anta = w_totm
5.33 c                   eval      c1ant1 = w_totm
5.33 c                   goto      c1tagb
5.33 c                   endif
      *
      * F23=Skifte mellom skjul/vis kostpris (dersom tillatt på bruker)
      *
     c                   if        *inkx = *on and
     c                             fbko01 = 1
     c                   if        *in86 = *off
     c                   eval      *in86 = *on
     c                   else
     c                   eval      *in86 = *off
     c                   endif
     c                   goto      c1tagb
     c                   endif
      *
      * Enhet må oppgis dersom varen ikke har enhet
      *
     c                   if        w_enh1 = *blank
      *
     c                   if        c1enh1 = *blank
     c                   eval      *in31 = *on
     c                   goto      c1tagb
     c                   endif
      *
     c                   eval      venhl1_eenh = c1enh1
     c     venhl1_key    chain     venhl1
     c                   if        not %found
     c                   eval      *in32 = *on
     c                   goto      c1tagb
     c                   endif
      *
     c                   endif
      *
      * Sjekker og behandler antall
      *
     c                   if        c1ant1 = 0 and
     c                             c1ant2 = 0 and
     c                             c1ant3 = 0 and
     c                             c1anta = 0
     c                   eval      *in33 = *on
     c                   goto      c1tagb
     c                   endif
      *
     c                   select
     c                   when      c1ant1 <> s_ant1 and
     c                             c1ant1 <> 0 or
     c                             w_enh1 = *blank and
     c                             c1ant1 <> 0
     c                   eval      s_ant1 = c1ant1
     c                   eval      c1anta = c1ant1
     c                   eval      c1enhe = c1enh1
     c                   when      c1ant2 <> s_ant2 and
     c                             c1ant2 <> 0
     c                   eval      s_ant2 = c1ant2
     c                   eval      c1anta = c1ant2
     c                   eval      c1enhe = c1enh2
     c                   when      c1ant3 <> s_ant3 and
     c                             c1ant3 <> 0
     c                   eval      s_ant3 = c1ant3
     c                   eval      c1anta = c1ant3
     c                   eval      c1enhe = c1enh3
     c                   endsl
5.31  *
5.31  * Sjekk gyldig lager
5.31  *
5.31 c                   exsr      sjekk_lager
5.31 c                   if        b_feil = *on
5.31 c                   eval      *in38 = *on
5.31 c                   goto      c1tagb
5.31 c                   endif
      *
      * Sjekk leveringstype
      *
     c                   eval      vltpl1_lety = c1lety
     c     vltpl1_key    chain     vltpl1
     c                   if        not %found
     c                   eval      *in34 = *on
     c                   goto      c1tagb
     c                   endif
5.32  *
5.32  * Sjekk linjetype
5.32  *
5.32 c                   eval      vltyl1_ltyp = c1ltyp
5.32 c     vltyl1_key    chain     vltyl1
5.32 c                   if        not %found
5.32 c                   eval      b_feil = *on
5.32 c                   eval      *in42 = *on
5.32 c                   goto      c1tagb
5.32 c                   endif
5.32  *
5.32  * Tekst-linjetype og vare kan ikke oppgis samtidig
5.32  *
5.32 c                   if        valktx = 1 and
5.32 c                             c1vare <> *blank
5.32 c                   eval      b_feil = *on
5.32 c                   eval      *in43 = *on
5.32 c                   goto      c1tagb
5.32 c                   endif
5.32  *
5.32  * Vare må oppgis sammen med vare-linjetype
5.32  *
5.32 c                   if        valktx = 0 and
5.32 c                             c1vare = *blank
5.32 c                   eval      b_feil = *on
5.32 c                   eval      *in44 = *on
5.32 c                   goto      c1tagb
5.32 c                   endif
      *
      * Sjekk varetekst
      *
     c                   if        c1tek1 = *blank
     c                   eval      *in35 = *on
     c                   goto      c1tagb
     c                   endif
      *
      * Salgspris inkl.mva, DG og påslag kan ikke endres samtidig
      *
     c                   select
     c                   when      c1saim <> 0
     c                   if        c1dekn <> s_dekn and
     c                             c1dekn <> 0 or
     c                             c1pasl <> 0
     c                   eval      *in36 = *on
     c                   goto      c1tagb
     c                   endif
     c                   when      c1dekn <> s_dekn and
     c                             c1dekn <> 0
     c                   if        c1saim <> 0 or
     c                             c1pasl <> 0
     c                   eval      *in36 = *on
     c                   goto      c1tagb
     c                   endif
     c                   when      c1pasl <> 0
     c                   if        c1saim <> 0 or
     c                             c1dekn <> s_dekn and
     c                             c1dekn <> 0
     c                   eval      *in36 = *on
     c                   goto      c1tagb
     c                   endif
     c                   endsl
      *
      * DG og påslag kan ikke oppgis når kostpris = 0
      *
     c                   if        c1kopr = 0
     c                   if        c1dekn <> 0 or
     c                             c1pasl <> 0
     c                   eval      *in37 = *on
     c                   goto      c1tagb
     c                   endif
     c                   endif
      *
      * Sjekk leveringsdato
      *
     c                   if        c1ldat <> 0
     c     *dmy          test(d)                 c1ldat                 40
     c                   if        *in40 = *on
     c                   goto      c1tagb
     c                   endif
     c                   endif
      *
      * Sjekk om leveringsdato er større enn ordredato
      *
     c                   if        c1ldat <> 0
     c     *dmy          move      c1ldat        w_ldat
     c                   if        w_ldat < fobdat
     c                   eval      *in41 = *on
     c                   goto      c1tagb
     c                   endif
     c                   endif
      *
      * Sjekk prosjekt
      *
6.20 c**                 if        c1proj <> 0
6.20 c                   if        c1proj <> *blank
     c                   eval      w_kont = 0
     c                   eval      w_spes = 0
     c                   call      'RS770R'
     c                   parm                    w_retk
     c                   parm                    c1proj
     c                   parm                    c1akti
     c                   parm                    w_kont
     c                   parm                    w_spes
     c                   parm                    w_txt1
     c                   parm                    w_txt2
     c                   if        w_retk <> *blank
     c                   eval      *in45 = *on
     c                   goto      c1tagb
     c                   endif
     c                   endif
      *
      * Sjekk aktivitet
      *
     c                   if        c1akti <> *blank
     c                   call      'RS721R'
     c                   parm                    w_retk
     c                   parm                    c1akti
     c                   parm                    w_txt1
     c                   if        w_retk <> *blank
     c                   eval      *in46 = *on
     c                   goto      c1tagb
     c                   endif
     c                   endif
      *
      * Kaller subrutine for å vise antall i andre enheter
      *
     c                   if        w_enh2 <> *blank
     c                   exsr      kalkulator
     c                   eval      s_ant1 = c1ant1
     c                   eval      s_ant2 = c1ant2
     c                   eval      s_ant3 = c1ant3
     c                   endif
      *
      * Henter ny lagerbeholdning dersom enhet er endret
      *
     c                   if        c1enhe <> s_enhe
     c                   exsr      lagerbeholdn
     c                   endif
      *
      * Dersom enhet, leveringstype eller privat er endret, eller dersom
      * priskode er endret fra 'F', hentes ny pris og rabatt.
      *
     c                   if        c1enhe <> s_enhe or
     c                             c1lety <> s_lety or
     c                             c1priv <> s_priv or
     c                             (c1kpri <> 'F' and
     c                              s_kpri = 'F')
      *
     c                   eval      s_enhe = c1enhe
     c                   eval      s_lety = c1lety
     c                   eval      s_kpri = c1kpri
     c                   eval      s_dekn = c1dekn
     c                   eval      s_priv = c1priv
      *
     c                   eval      hienhe = c1enhe
     c                   eval      hilety = c1lety
     c                   eval      hidekn = c1dekn
     c                   eval      hiavgf = fomkod
     c                   eval      hipriv = c1priv
     c                   exsr      hent_pris
      *
     c                   eval      c1alme = hoalme
     c                   eval      c1omva = hoomva
     c                   eval      c1brty = hobrty
     c                   eval      c1brr1 = hobrr1
     c                   eval      c1brr2 = hobrr2
      *
      * Oppdater ny pris bare dersom pris ikke er overstyrt og dersom
      * pris ikke er forsjellig fra null
      *
     c                   if        hosapr <> 0
     c                   if        c1sapr = s_sapr or
     c
     c                             c1sapr = 0
     c                   eval      c1sapr = hosapr
     c                   eval      c1kopr = hokopr
     c                   eval      c1kpri = hokpri
     c                   eval      c1rab1 = horab1
     c                   eval      c1rab2 = horab2
     c                   eval      s_anta = c1anta
     c                   eval      s_sapr = c1sapr
     c                   eval      s_rab1 = c1rab1
     c                   eval      s_rab2 = c1rab2
     c                   eval      b_forn = *on
     c                   endif
     c                   endif
      *
     c                   endif
      *
      * Behandling av salgspris inkl.mva.
      *
     c                   if        c1saim <> 0
     c                   eval(h)   c1sapr = c1saim * w_mvaf
     c                   eval      c1dekn = 0
     c                   eval      c1saim = 0
     c                   goto      c1tagb
     c                   endif
      *
      * Behandling av kostpris
      *
     c                   if        c1kopr <> s_kopr
     c                   eval      s_kopr = c1kopr
     c                   eval      s_dekn = c1dekn
     c                   if        c1dekn <> 0 and
     c                             c1pasl = 0
     c                   exsr      dekning
     c                   goto      c1tagb
     c                   endif
     c                   endif
      *
      * Behandling av DG
      *
     c                   if        c1dekn = s_dekn and
     c                             c1dekn <> 0
     c                   if        c1rab1 <> s_rab1 or
     c                             c1rab2 <> s_rab2
     c                   eval      c1dekn = 0
     c                   eval      s_dekn = 0
     c                   eval      s_rab1 = c1rab1
     c                   eval      s_rab2 = c1rab2
     c                   endif
     c                   endif
      *
     c                   if        c1dekn <> s_dekn
     c                   eval      s_dekn = c1dekn
     c                   if        c1dekn <> 0
     c                   exsr      dekning
     c                   goto      c1tagb
     c                   endif
     c                   endif
      *
      * Behandling av påslag
      *
     c                   if        c1pasl <> 0
     c                   eval(h)   w_sapr = c1kopr + (c1kopr * c1pasl / 100)
     c                   eval(h)   c1dekn = 100 - (c1kopr * 100 / w_sapr)
     c                   eval      c1pasl = 0
     c                   eval      s_dekn = c1dekn
     c                   if        c1dekn <> 0
     c                   exsr      dekning
     c                   endif
     c                   goto      c1tagb
     c                   endif
      *
      * Viser bildet på nytt dersom dette er angitt
      *
     c                   if        b_forn = *on
     c                   goto      c1tagb
     c                   endif
      *
      * Viser bildet på nytt dersom varen har flere enheter, antall er
      * endret og bildet ikke allerede er vist
      *
     c                   if        w_enh2 <> *blank and
     c                             c1anta <> s_anta
     c                   eval      s_anta = c1anta
     c                   goto      c1tagb
     c                   endif
      *
      * Sjekk pris
      *
     c                   if        c1sapr <= 0,01
     c                   eval      *in39 = *on
     c                   goto      c1tagb
     c                   endif
      *
      * Sender advarsel dersom netto salgspris < kostpris
      *
     c                   eval      w_sapr = c1sapr
     c                   if        c1rab1 > 0
     c                   eval(h)   w_sapr = w_sapr - (w_sapr * c1rab1 / 100)
     c                   endif
     c                   if        c1rab2 > 0
     c                   eval(h)   w_sapr = w_sapr - (w_sapr * c1rab2 / 100)
     c                   endif
      *
     c                   if        c1kopr > w_sapr
     c                   exfmt     c1msg
     c                   if        *inkc or *inkl
     c                   goto      c1tagb
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter kalkulert pris på varen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_pris     begsr
      *
     c                   eval      hifirm = w_firm
     c                   eval      hirkat = 0
      *
     c                   eval      hikund = fokund
     c                   if        folkun <> 0
     c                   eval      hikund = folkun
     c                   endif
      *
     c                   eval      hikpro = fokpro
     c                   eval      hivare = vvvare
     c                   eval      hidato = fobdat
     c                   eval      hitime = *loval
     c                   eval      hinumm = 0
     c                   eval      hikode = fopask
     c                   eval      hiskaf = b_skaf
      *
     c                   if        b_skaf = *off
     c                   eval      hildor = 0
     c                   eval      hisapr = 0
     c                   eval      hikopr = 0
     c                   else
     c                   eval      hildor = p_ldor
6.35 c                   if        w_saps <> 0 and w_saps <> c1sapr
6.35 c                   eval      hisapr = w_saps
     c                   elseif    c1sapr <> 0
     c                   eval      hisapr = c1sapr
     c                   else
     c                   eval      hisapr = p_sapr
     c                   endif
6.35 c                   if        w_kops <> 0 and w_kops <> c1kopr
6.35 c                   eval      hikopr = w_kops
     c                   elseif    c1kopr <> 0
     c                   eval      hikopr = c1kopr
     c                   else
     c                   eval      hikopr = p_kopr
     c                   endif
     c                   endif
      *
     c                   call      'VP900R'
     c                   parm                    hirec
     c                   parm                    horec
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Kalkulator for omregning mellom enheter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kalkulator    begsr
      *
     c                   if        c1enhe <> *blank
      *
     c                   eval      c1ant1 = 0
     c                   eval      c1ant2 = 0
     c                   eval      c1ant3 = 0
6.35 c                   eval      w_saps = 0
6.35 c                   eval      w_kops = 0
      *
     c                   select
     c                   when      c1enhe = w_enh1
     c                   eval      c1ant1 = c1anta
     c                   if        w_omr2 <> 0
     c                   eval(h)   c1ant2 = c1anta / w_omr2
     c                   endif
     c                   if        w_omr3 <> 0
     c                   eval(h)   c1ant3 = c1anta / w_omr3
     c                   endif
     c                   when      c1enhe = w_enh2
     c                   eval(h)   c1ant1 = c1anta * w_omr2
     c                   eval      c1ant2 = c1anta
     c                   if        w_omr3 <> 0
     c                   eval(h)   c1ant3 = c1ant1 / w_omr3
     c                   endif
     c                   when      c1enhe = w_enh3
     c                   eval(h)   c1ant1 = c1anta * w_omr3
     c                   if        w_omr2 <> 0
     c                   eval(h)   c1ant2 = c1ant1 / w_omr2
     c                   endif
     c                   eval      c1ant3 = c1anta
     c                   endsl
      *
6.35 c                   if        b_skaf = *on
6.35  *                  sjekk om enhet byttet, hvis det, regn ny pris pr enhet
6.35 c                   if        c1enhe <> ''
6.35 c                             and s_enhe <> ''
6.35 c                             and c1enhe <> s_enhe
6.35 c
6.35 c                   select
6.35 c                   when      s_enhe = w_enh1
6.35 c                   if        c1enhe = w_enh2
6.35 c                   eval(h)   w_saps = c1sapr * w_omr2
6.35 c                   eval(h)   w_kops = c1kopr * w_omr2
6.35 c                   endif
6.35 c                   if        c1enhe = w_enh3
6.35 c                   eval(h)   w_saps = c1sapr * w_omr3
6.35 c                   eval(h)   w_kops = c1kopr * w_omr3
6.35 c                   endif
6.35 c                   when      s_enhe = w_enh2
6.35 c                   if        c1enhe = w_enh1
6.35 c                   eval(h)   w_saps = c1sapr / w_omr2
6.35 c                   eval(h)   w_kops = c1kopr / w_omr2
6.35 c                   endif
6.35 c                   if        c1enhe = w_enh3
6.35 c                   eval(h)   w_saps = c1sapr * (w_omr3/w_omr2)
6.35 c                   eval(h)   w_kops = c1kopr * (w_omr3/w_omr2)
6.35 c                   endif
6.35 c                   when      s_enhe = w_enh3
6.35 c                   if        c1enhe = w_enh1
6.35 c                   eval(h)   w_saps = c1sapr / w_omr3
6.35 c                   eval(h)   w_kops = c1kopr / w_omr3
6.35 c                   endif
6.35 c                   if        c1enhe = w_enh2
6.35 c                   eval(h)   w_saps = c1sapr * (w_omr2/w_omr3)
6.35 c                   eval(h)   w_kops = c1kopr * (w_omr2/w_omr3)
6.35 c                   endif
6.35 c                   endsl
6.35 c
6.35 c                   endif
6.35 c                   endif
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter beholdingstall dersom lagervare og lager kjøres
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lagerbeholdn  begsr
      *
     c                   eval      *in87  = *off
     c                   eval      c1behl = 0
     c                   eval      c1bbes = 0
     c                   eval      c1pakk = 0
     c                   eval      c1disp = 0
      *
     c                   if        faalag = 1 and
5.70 c                             p_vtyp = 'L'
     c                   call      'VL700R'
     c                   parm                    p_firm
     c                   parm                    fdvare
5.31 c                   parm                    fdlage
     c                   parm                    c1enhe
     c                   parm                    c1behl
     c                   parm                    c1bbes
     c                   parm                    c1pakk
     c                   parm                    c1disp
     c                   eval      *in87 = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Beregner salgspris og rabatt i.h.t. dekningsgrad (DG)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dekning       begsr
      *
     c                   eval(h)   c1sapr = (100 * c1kopr) / (100 - c1dekn)
     c                   eval      c1rab1 = 0
     c                   eval      c1rab2 = 0
      *
     c                   if        fopask = *blank
     c                   if        hosapr > 0,01
     c                   eval(h)   w_rab1 = (hosapr - c1sapr) * 100 / hosapr
     c                   if        w_rab1 >= 0
     c                   eval      c1sapr = hosapr
     c                   eval      c1rab1 = w_rab1
     c                   endif
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdat_lager  begsr
      *
     c                   if        faalag = 1 and
5.70 c                             p_vtyp = 'L'
     c                   else
     c                   leavesr
     c                   endif
      *
      * Legger over verdier i overførings-felter
      *
     c                   eval      hlvare = fdvare
5.31 c                   eval      hllage = fdlage
     c                   eval      hlenhe = fdenh1
     c                   eval      hldato = *loval
     c                   eval      hltime = *loval
     c                   eval      hlotyp = footyp
     c                   eval      hlltyp = fdltyp
      *
     c                   eval      hlanta = fdanta
     c                   eval      hlkopr = fdkopr
      *
     c                   eval      hlrefr = *blank
     c                   eval      hlsyst = 'F'
     c                   eval      hlaltk = *blank
     c                   eval      hlrest = *blank
     c                   move      fdnumm        w_numa
     c                   move      fdline        w_lina
     c                   eval      hlrefr = 'O:' + w_numa +
     c                                      ' L:' + w_lina
     c                   eval      hlonum = fdnumm
     c                   eval      hlolin = fdline
3.12 c                   move      fdlety        hllety
      *
      * Kaller lageroppdaterings-program
      *
     c                   eval      w_stat = *blank
      *
     c                   call      'VL001R'
     c                   parm                    w_stat
     c                   parm                    hlrec
      *
     c                   endsr
5.31  *
5.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.31  * Sjekker gyldig lager
5.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.31  *
5.31 c     sjekk_lager   begsr
5.31  *
5.31 c                   eval      b_feil = *off
5.31  *
5.31 c                   call      'FÅ720R'
5.31 c                   parm                    p_firm
5.31 c                   parm                    c1lage
5.31 c                   parm                    w_jtxt
5.31 c                   parm                    w_jadr
5.31 c                   parm                    w_jpnr
5.31 c                   parm                    w_jste
5.31 c                   parm                    w_javd
5.31 c                   parm                    w_retk
5.31  *
5.31 c                   if        w_retk <> '0'
5.31 c                   eval      b_feil = *on
5.31 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ERROR-rutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *pssr         begsr
      *
     c                   eval      *in49 = *on
     c                   goto      tc1win
      *
     c                   endsr
      *
5.70  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.70  * Subrutine for å hente prisgruppe
5.70  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.70  *
5.70 c     hent_prisgr   begsr
5.70  *
5.70  * Kaller program for henting av prisgruppe fra lager/avdeling
5.70  *
5.70 c                   call      'VL712R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    w_pgla
5.70 c                   parm                    w_pgav
5.70 c                   parm                    w_prgr
5.70  *
5.70 c                   endsr
      *
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  * Subrutine for å sjekke om dato er innenfor fra /til dato
6.32  * hvis *loval i begge datoer = ok
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  *
6.32 c     sjekk_pkdato  begsr
6.32  *
6.32 c                   eval      b_pakn = *on
6.32 c                   time                    w_dato
6.32  *
6.32 c                   if        jwfdat = *loval and
6.32 c                             jwtdat = *loval
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   if        jwfdat <= w_dato and
6.32 c                             jwtdat >  w_dato
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   if        jwfdat = *loval  and
6.32 c                             jwtdat >  w_dato
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   if        jwfdat <= w_dato and
6.32 c                             jwtdat =  *loval
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   eval      b_pakn = *off
6.32  *
6.32 c                   endsr
6.32  *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_kode
     c                   parm                    p_firm
     c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_line
     c                   parm                    p_ltyp
     c                   parm                    p_lety
     c                   parm                    p_vare
     c                   parm                    p_anta
     c                   parm                    p_enhe
     c                   parm                    p_sapr
     c                   parm                    p_kopr
     c                   parm                    p_ldor
     c                   parm                    p_lvar
     c                   parm                    p_kost
      *
      * Key for oppslag på status
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på bruker
      *
     c     fusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fusrl1_user
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på vare (VA)
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for les på vare-pakning (VA)
      *
     c     jvpkl1_key    klist
     c                   kfld                    jvpkl1_vare
6.31 c                   kfld                    jvpkl1_ldor
6.31  *
6.31  * Key for les på leverandør (VA)
6.31  *
6.31 c     jlevl3_key    klist
6.31 c                   kfld                    jlevl3_enum
      *
      * Key for oppslag på leveringstype
      *
     c     vltpl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltpl1_lety
      *
      * Key for oppslag på linjetype
      *
     c     vltyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltyl1_ltyp
      *
      * Key for oppslag på enhet
      *
     c     venhl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    venhl1_eenh
      *
      * Key for oppslag på spesialpris
      *
     c     fpril1_key    klist
     c                   kfld                    w_firm
5.70 c                   kfld                    fpril1_prgr
     c                   kfld                    fpril1_rkat
     c                   kfld                    fpril1_kkat
     c                   kfld                    fpril1_kund
     c                   kfld                    fpril1_kpro
     c                   kfld                    fpril1_vare
     c                   kfld                    fpril1_enhe
     c                   kfld                    fpril1_lety
      *
      * Key for oppslag på spesialpris notat
      *
     c     fprnl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fprnl1_nkun
     c                   kfld                    fprnl1_nkpr
     c                   kfld                    fprnl1_nvar
      *
      * Key for oppslag på ordre-hode
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
      *
      * Key for oppdatering av ordre-linje
      *
     c     fodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
     c                   kfld                    w_line
      *
      *
      * Henter parametre
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_numm = p_numm
     c                   eval      w_suff = p_suff
     c                   eval      w_line = p_line
      *
      * Henter dagens dato
      *
6.20 c                   time                    w_ddat
      *
      * Legger inn type for oppdatering av lager (R=Registrering)
      *
5.01 c                   eval      hltype = ' '
      *
      * Hent opplysninger fra ofak koderegister
      *
     c     fstsl1_key    chain     fstsl1r
      *
      * Henter opplysninger fra ofak bruker
      *
     c                   eval      fusrl1_user = l_user
     c     fusrl1_key    chain     fusrl1
6.31  *
6.31  * Leser VA leverandør
6.31  *
6.31 c                   eval      jlevl3_enum = p_ldor
6.31 c     jlevl3_key    chain     jlevl3
      *
      * Sett indikator som styrer kostpris i henhold til parameter
      *
     c                   eval      *in86 = p_kost
      *
      * Hent opplysninger fra ordre-hode
      *
     c     fohel1_key    chain     fohel1r
      *
      * Henter mva-sats
      *
     c                   call      'FÅ705R'
     c                   parm                    p_firm
     c                   parm                    fobdat
     c                   parm                    w_mvap
     c                   parm                    w_mvat
     c                   parm                    w_mvaf
      *
      * Initierer skjermbildet
      *
     c                   eval      c1vare = p_vare
      *
     c                   endsr
