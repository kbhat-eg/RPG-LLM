     h datedit(*dmy.) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO660R
      * Beskrivelse . . : Utskrift av Faktura/Kreditnota
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * R5.21 200803 AMD  Lagt om til AP600R i stedet for AP100R
6.nu  * R6.30 280514 bsa  Programmet gjennomgått mtp nummerutvidelser.
7.xx  * 7.00  290116 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     FAFORL1    IF   E           K DISK    RENAME(AFORPFR:AFORL1R)
     FVOTYL1    IF   E           K DISK    RENAME(VOTYPFR:VOTYL1R)
      *
     FFO660D    CF   E             WORKSTN
      *
     D                 DS
      *                                                      Parameter-inn
6.nu D  LDPREC                 1     35
     D  LDAARR                 1      4  0
     D  LDOTYP                 5      6
6.nu D  LDFONR                 7     14  0
6.nu D  LDTONR                15     22  0
6.nu D  LDPROG                23     32
6.nu D  LDKOPI                33     33
      *
      * Definering av Local Data Area
      *
     d lda            uds
     d  l_ruti               800    809
     d  l_chgv               844    844
     d  l_sfir               944    946  0
     d  l_fnav               951    980
      *
5.21  * Definering av variable
5.21  *
5.21 d w_in03          s              1
5.21 d w_tvbt          s              1
5.21 d w_btch          s              1
5.21  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandle bilde for registrering av parametre                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     B1TAG         TAG
      *
      *  Nullstiller variable som skal vises i skjermbildet
      *
     C                   MOVE      *BLANK        B1OTYP
     C                   MOVE      *ZERO         B1FONR
     C                   MOVE      *ZERO         B1TONR
      *
     C                   MOVE      *ZERO         B1AARR
     C                   MOVE      UYEAR         B1AARR
      *
     C     B1AARR        IFGT      80
     C                   ADD       1900          B1AARR
     C                   ELSE
     C                   ADD       2000          B1AARR
     C                   ENDIF
      *
     C     B1TAGA        TAG
     C                   SETON                                        30
      *
     C     B1TAGB        TAG
     C                   EXFMT     B1BLD
      *
      *  F3=Gå tilbake  (Avslutt)
      *
     C     *INKC         CABEQ     *ON           XSLUTT
      *
      *  F4=Spørring
      *
     C     *INKD         IFEQ      *ON
     C                   CALL      'VA550R'
     C                   PARM                    OTYFIR
     C                   PARM                    B1OTYP
     C                   GOTO      B1TAGB
     C                   END
      *
      *  Sjekk   Finnes ordretype i ORDRE-TYPE-REGISTER
      *
     C                   MOVE      B1OTYP        OTYTYP
     C     OTYKEY        CHAIN     VOTYL1R                            90
     C     *IN90         IFEQ      *ON
     C                   MOVE      *ON           *IN31
     C                   GOTO      B1TAGB
     C                   END
      *
      *  Sjekk   Er Ordretypen enn faktura/kreditnota-type  akk.=3
      *
     C     VAOAKK        IFNE      3
     C                   MOVE      *ON           *IN32
     C                   GOTO      B1TAGB
     C                   END
      *
      *  Sjekk   Er fakturanummer fra større enn til
      *
     C     B1FONR        IFGT      B1TONR
     C                   MOVE      *ON           *IN37
     C                   GOTO      B1TAGB
     C                   END
      *
      *  Legger inntastede opplysninger i Hjelpe-felter for Parameter
      *
     C                   MOVE      B1AARR        LDAARR
     C                   MOVE      B1OTYP        LDOTYP
     C                   MOVE      B1FONR        LDFONR
     C                   MOVE      B1TONR        LDTONR
     C                   MOVE      ' '           LDKOPI
      *
      *  Dersom ordrenummer til=0 skal t.o.m. alle ordrenmmer velges
      *
     C     LDTONR        IFEQ      *ZERO
6.nu C                   MOVE      99999999      LDTONR
     C                   END
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Inntasting er ferdig !  Oppstart av utvalg/utskriftsprogrammer *
      *  Kaller opp neste 'CL'-program                                  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      *  Legg inn verdi for bare henting av opplysninger
      *
     c                   eval      l_chgv = *blank                               KODE VISE BILDE
     c                   eval      l_ruti = vaorut                               RUTINE
     c                   out       *dtaara
      *
      *  Kaller opp program før utskrift
      *
5.21 c                   call      'AP600R'
5.21 c                   parm                    l_ruti
5.21 c                   parm                    l_chgv
5.21 c                   parm                    w_tvbt
5.21 c                   parm                    w_in03
5.21 c                   parm                    w_btch
      *
      *  Hent inn opplysninger om formularet
      *
     C                   MOVE      VAORUT        RUTNUM
     C     RUTKEY        CHAIN     AFORL1R                            95
     C                   MOVE      AFPROG        LDPROG
      *
      *  F19=Legge jobb på jobbkø
      *
     C     *INKT         IFEQ      *ON
     C                   MOVE      '1'           WWVALG
     C                   END
      *
      *  Kaller opp subprogram
      *
     C                   CALL      'FO662C'
     C                   PARM                    WWVALG
6.nu C                   PARM                    LDPREC
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     XSLUTT        TAG
      *
     C                   SETON                                        LR
     C                   RETURN
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     *INZSR        BEGSR
      *
      *  Definisjon av local data area for output
      *
     C     *DTAARA       DEFINE    *LDA          LDA
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     C                   MOVE      *BLANK        WWVALG            1
      *
      *  Redefinering av nøkler
      *
     C     *LIKE         DEFINE    AFRUTI        RUTNUM
      *
     C     *LIKE         DEFINE    VAOFIR        OTYFIR
     C     *LIKE         DEFINE    VAOTYP        OTYTYP
      *
      *  Key for file : FORMULAR-RUTINE-KODE
      *
     C     RUTKEY        KLIST
     C                   KFLD                    RUTNUM
      *
      *  Key for file : ORDRE-TYPE-REGISTER
      *
     C     OTYKEY        KLIST
     C                   KFLD                    OTYFIR
     C                   KFLD                    OTYTYP
      *
      *  Legge inn firmanummer i nøkklene
      *
     c                   eval      otyfir = l_sfir
      *
     csr                 endsr
