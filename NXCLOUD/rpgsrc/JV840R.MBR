     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JV840R
      * Beskrivelse . . : Oppdater fra NIB, leverandør
      *
      *                   Oppdaterer/oppretter leverandør
      *                   Norgros/NIB (XML-format)
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       151105 GRo  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fnxdepf    uf   e           k disk
     fjlevlu    uf a e           k disk    rename(jlevpfr:jlevlur)
     fjstsl1    if   e           k disk    rename(jstspfr:jstsl1r)
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
      *
      * Definering av Local Data Area
      *
     d                uds
     d l_user                911    920
      *
      *  Dataelementer leverandør
      *
     d                 ds
     d d_drec                       600
     d  d_stat                        1    overlay(d_drec:1)
     d   d_stat_num                   1  0 overlay(d_stat:1)
     d  d_dtnr                        6    overlay(d_drec:2)
     d   d_dtnr_num                   6  0 overlay(d_dtnr:1)
     d  d_dtyp                        1    overlay(d_drec:8)
     d   d_dtyp_num                   1  0 overlay(d_dtyp:1)
     d  d_navn                       35    overlay(d_drec:9)
     d  d_adr1                       35    overlay(d_drec:44)
     d  d_adr2                       35    overlay(d_drec:79)
     d  d_ponr                       15    overlay(d_drec:114)
     d  d_sted                       35    overlay(d_drec:129)
     d  d_land                       20    overlay(d_drec:164)
     d  d_mail                       50    overlay(d_drec:184)
     d  d_urla                       50    overlay(d_drec:234)
     d  d_tlfn                       15    overlay(d_drec:284)
     d  d_faxn                       15    overlay(d_drec:299)
     d  d_faxo                       15    overlay(d_drec:314)
     d  d_valu                        3    overlay(d_drec:329)
     d  d_orgn                       15    overlay(d_drec:332)
     d   d_orgn_num                   9  0 overlay(d_orgn:7)
     d  d_eanl                       15    overlay(d_drec:347)
     d   d_eanl_num                  13  0 overlay(d_eanl:3)
     d  d_ansv                       35    overlay(d_drec:362)
     d  d_bruk                       35    overlay(d_drec:397)
     d  d_btlf                       15    overlay(d_drec:432)
     d  d_bfax                       15    overlay(d_drec:447)
     d  d_bmai                       50    overlay(d_drec:462)
     d  d_noda                       10    overlay(d_drec:512)
     d   d_noda_iso                    d   overlay(d_noda:1) datfmt(*iso)
     d  d_neda                       10    overlay(d_drec:522)
     d  d_odat                       10    overlay(d_drec:532)
     d  d_edat                       10    overlay(d_drec:542)
     d  d_etim                       10    overlay(d_drec:552)
      *
      * Definering av variable i nøkler
      *
     d jlevlu_ldor     s                   like(jlldor)
      *
      * Definering av variable
      *
     d b_inlr          s              1
     d b_kild          s              1    inz(*off)
     d b_delt          s              1    inz(*off)
     d b_dfel          s              1    inz(*off)
     d b_blnk          s              1    inz(*off)
      *
     d w_stda          s              3  0
     d w_slda          s              3  0
     d w_welm          s            400
     d w_wlen          s              3  0
     d w_decp          s              3  0
      *
     d w_str_i         s            400
     d w_str_u         s            400
     d w_posi          s              4  0
     d w_posu          s              4  0
     d w_str_x         s              1
      *
     d w_firm          s              3  0
      *
      *  Konstanter
      *
     d c_apos          c                   x'7D'
     d c_digits        c                   ' 0123456789'
     d c_nsep          c                   ','
      *
     d c_NIBk          c                   '0'
     d c_NVk           c                   '3'
      *
      * XML string konstanter
      *
     d c_xsle          c                   '</'
     d c_xend          c                   '>'
      *
      * XML feltnavn - meldingsspesifikke
      *
      * Hovedstruktur - Avsender
      *
     d c_xstr          c                   '<Avsender>'
     d c_kild          c                   '<Kilde>'
      *
      * Substruktur - Deltaker/leverandør
      *
     d c_delt_s        c                   '<Deltaker>'
     d c_delt_e        c                   '</Deltaker>'
     d c_dtnr          c                   '<Deltakernr>'
     d c_stat          c                   '<Status>'
     d c_dttp          c                   '<Deltakertype>'
     d c_navn          c                   '<Navn>'
     d c_adr1          c                   '<Adr1>'
     d c_adr2          c                   '<Adr2>'
     d c_psnr          c                   '<Postnr>'
     d c_pste          c                   '<Poststed>'
     d c_land          c                   '<Land>'
     d c_mail          c                   '<Mail>'
     d c_wadr          c                   '<URL>'
     d c_tlfn          c                   '<Tlf>'
     d c_faxn          c                   '<Faks>'
     d c_faxo          c                   '<Faks_ordre>'
     d c_valu          c                   '<Valuta>'
     d c_orgn          c                   '<Orgnr>'
     d c_eanl          c                   '<EAN_lokasjonsnr>'
     d c_ansv          c                   '<Ansvarlig>'
     d c_user          c                   '<Bruker>'
     d c_utlf          c                   '<Bruker_tlf>'
     d c_ufax          c                   '<Bruker_faks>'
     d c_umal          c                   '<Bruker_mail>'
     d c_nbdo          c                   '<Nobb_oppr_dato>'
     d c_nbde          c                   '<Nobb_endr_dato>'
     d c_opda          c                   '<Oppr_dato>'
     d c_enda          c                   '<Endr_dato>'
     d c_entd          c                   '<Endr_tidspunkt>'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      b_inlr = *on
      *
      * Leser XML-fil deltaker/levr.
      *
     c                   read      nxdepfr
     c                   dow       not %eof(nxdepf)
      *
     c                   if        b_kild = *on
     c                   if        %subst(nxdelt:1:10) = c_delt_s
     c                   eval      b_delt = *on
     c                   exsr      beh_delt
     c                   endif
     c                   endif
      *
      *  Finn gyldig avsender/kilde
      *
     c                   eval      w_stda = %scan(c_xend:nxdelt:1)
     c                   eval      w_slda = %scan(c_xsle:nxdelt:w_stda)
     c                   eval      w_stda = w_stda + 1
      *
     c                   eval      w_wlen = *zero
     c                   eval      w_welm = *blank
     c                   if        w_slda > w_stda
     c                   eval      w_wlen = w_slda - w_stda
     c                   eval      w_welm = %subst(nxdelt:w_stda:w_wlen)
     c                   endif
      *
     c                   select
     c                   when      %subst(nxdelt:1:7) = c_kild
     c                   exsr      uttr_kild
     c                   endsl
      *
     c                   delete    nxdepfr
      *
     c                   read      nxdepfr
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av avsenderelement : Kilde
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_kild     begsr
      *
     c                   eval      b_kild = *off
      *
     c                   if        %trim(w_welm) = c_NIBk
     c                   eval      b_kild = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av deltaker/lev. informasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     beh_delt      begsr
      *
     c                   eval      b_dfel = *off
     c                   exsr      delt_init
      *
     c                   dow       not %eof(nxdepf)
      *
      *  Slutt på deltakerelement
      *
     c                   if        %subst(nxdelt:1:11) = c_delt_e
     c                   exsr      skr_delt
     c                   leave
     c                   endif
      *
      *  Enkeltelement deltakernivå herfra
      *
     c                   eval      w_stda = %scan(c_xend:nxdelt:1)
     c                   eval      w_slda = %scan(c_xsle:nxdelt:w_stda)
     c                   eval      w_stda = w_stda + 1
      *
     c                   eval      w_wlen = *zero
     c                   eval      w_welm = *blank
     c                   if        w_slda > w_stda
     c                   eval      w_wlen = w_slda - w_stda
     c                   eval      w_welm = %subst(nxdelt:w_stda:w_wlen)
     c                   endif
      *
     c                   select
     c                   when      %subst(nxdelt:1:12) = c_dtnr
     c                   exsr      uttr_dtnr
     c                   when      %subst(nxdelt:1:8) = c_stat
     c                   exsr      uttr_stat
     c                   when      %subst(nxdelt:1:14) = c_dttp
     c                   exsr      uttr_dtyp
     c                   when      %subst(nxdelt:1:6) = c_navn
     c                   exsr      uttr_navn
     c                   when      %subst(nxdelt:1:6) = c_adr1
     c                   exsr      uttr_adr1
     c                   when      %subst(nxdelt:1:6) = c_adr2
     c                   exsr      uttr_adr2
     c                   when      %subst(nxdelt:1:8) = c_psnr
     c                   exsr      uttr_psnr
     c                   when      %subst(nxdelt:1:10) = c_pste
     c                   exsr      uttr_pste
     c                   when      %subst(nxdelt:1:6) = c_land
     c                   exsr      uttr_land
     c                   when      %subst(nxdelt:1:6) = c_mail
     c                   exsr      uttr_mail
     c                   when      %subst(nxdelt:1:5) = c_wadr
     c                   exsr      uttr_wadr
     c                   when      %subst(nxdelt:1:5) = c_tlfn
     c                   exsr      uttr_tlfn
     c                   when      %subst(nxdelt:1:6) = c_faxn
     c                   exsr      uttr_faxn
     c                   when      %subst(nxdelt:1:12) = c_faxo
     c                   exsr      uttr_faxo
     c                   when      %subst(nxdelt:1:8) = c_valu
     c                   exsr      uttr_valu
     c                   when      %subst(nxdelt:1:7) = c_orgn
     c                   exsr      uttr_orgn
     c                   when      %subst(nxdelt:1:17) = c_eanl
     c                   exsr      uttr_eanl
     c                   when      %subst(nxdelt:1:11) = c_ansv
     c                   exsr      uttr_ansv
     c                   when      %subst(nxdelt:1:8) = c_user
     c                   exsr      uttr_user
     c                   when      %subst(nxdelt:1:12) = c_utlf
     c                   exsr      uttr_utlf
     c                   when      %subst(nxdelt:1:13) = c_ufax
     c                   exsr      uttr_ufax
     c                   when      %subst(nxdelt:1:13) = c_umal
     c                   exsr      uttr_umal
     c                   when      %subst(nxdelt:1:16) = c_nbdo
     c                   exsr      uttr_nbdo
     c                   when      %subst(nxdelt:1:16) = c_nbde
     c                   exsr      uttr_nbde
     c                   when      %subst(nxdelt:1:11) = c_opda
     c                   exsr      uttr_opda
     c                   when      %subst(nxdelt:1:11) = c_enda
     c                   exsr      uttr_enda
     c                   when      %subst(nxdelt:1:16) = c_entd
     c                   exsr      uttr_entd
     c                   endsl
      *
     c                   delete    nxdepfr
      *
     c                   read      nxdepfr
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av deltakerinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     delt_init     begsr
      *
     c                   eval      d_drec = *blank
     c                   eval      d_stat_num = *zero
     c                   eval      d_dtnr_num = *zero
     c                   eval      d_dtyp_num = *zero
     c                   eval      d_orgn_num = *zero
     c                   eval      d_eanl_num = *zero
     c                   eval      d_noda_iso = *loval
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av delt.element : Deltakernr.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_dtnr     begsr
      *
     c                   if        %check(c_digits : w_welm) = 0
     c                   evalr     d_dtnr = %trim(w_welm)
     c                   eval      d_dtnr = %xlate(' ':'0':d_dtnr)
     c                   else
     c                   eval      b_dfel = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av delt.element : Status
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_stat     begsr
      *
     c                   eval      d_stat_num = *zero
      *
     c                   if        w_wlen = 1
     c                   if        %check(c_digits : w_welm) = 0
     c                   eval      d_stat = %trim(w_welm)
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av delt.element : Deltakertype
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_dtyp     begsr
      *
     c                   eval      d_dtyp_num = *zero
      *
     c                   if        w_wlen = 1
     c                   if        %check(c_digits : w_welm) = 0
     c                   eval      d_dtyp = %trim(w_welm)
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av delt.element : Navn
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_navn     begsr
      *
     c                   eval      w_str_i = *blank
     c                   eval      w_str_i = %trim(w_welm)
     c                   exsr      konv_XML_esc
     c                   eval      d_navn = %trim(w_str_u)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av delt.element : Adresse-1
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_adr1     begsr
      *
     c                   eval      w_str_i = *blank
     c                   eval      w_str_i = %trim(w_welm)
     c                   exsr      konv_XML_esc
     c                   eval      d_adr1 = %trim(w_str_u)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av delt.element : Adresse-2
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_adr2     begsr
      *
     c                   eval      w_str_i = *blank
     c                   eval      w_str_i = %trim(w_welm)
     c                   exsr      konv_XML_esc
     c                   eval      d_adr2 = %trim(w_str_u)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av delt.element : Postnr.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_psnr     begsr
      *
     c                   eval      w_str_i = *blank
     c                   eval      w_str_i = %trim(w_welm)
     c                   exsr      konv_XML_esc
     c                   eval      d_ponr = %trim(w_str_u)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av delt.element : Poststed
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_pste     begsr
      *
     c                   eval      w_str_i = *blank
     c                   eval      w_str_i = %trim(w_welm)
     c                   exsr      konv_XML_esc
     c                   eval      d_sted = %trim(w_str_u)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av delt.element : Land
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_land     begsr
      *
     c                   eval      w_str_i = *blank
     c                   eval      w_str_i = %trim(w_welm)
     c                   exsr      konv_XML_esc
     c                   eval      d_land = %trim(w_str_u)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av delt.element : Mail
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_mail     begsr
      *
     c                   eval      w_str_i = *blank
     c                   eval      w_str_i = %trim(w_welm)
     c                   exsr      konv_XML_esc
     c                   eval      d_mail = %trim(w_str_u)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av delt.element : URL
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_wadr     begsr
      *
     c                   eval      w_str_i = *blank
     c                   eval      w_str_i = %trim(w_welm)
     c                   exsr      konv_XML_esc
     c                   eval      d_urla = %trim(w_str_u)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av delt.element : Telefon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_tlfn     begsr
      *
     c                   eval      w_str_i = *blank
     c                   eval      w_str_i = %trim(w_welm)
     c                   exsr      konv_XML_esc
     c                   eval      d_tlfn = %trim(w_str_u)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av delt.element : Fax
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_faxn     begsr
      *
     c                   eval      w_str_i = *blank
     c                   eval      w_str_i = %trim(w_welm)
     c                   exsr      konv_XML_esc
     c                   eval      d_faxn = %trim(w_str_u)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av delt.element : Fax/ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_faxo     begsr
      *
     c                   eval      w_str_i = *blank
     c                   eval      w_str_i = %trim(w_welm)
     c                   exsr      konv_XML_esc
     c                   eval      d_faxo = %trim(w_str_u)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av delt.element : Valuta
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_valu     begsr
      *
     c                   eval      w_str_i = *blank
     c                   eval      w_str_i = %trim(w_welm)
     c                   exsr      konv_XML_esc
     c                   eval      d_valu = %trim(w_str_u)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av delt.element : Mail
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_orgn     begsr
      *
     c                   eval      d_orgn_num = *zero
      *
     c                   if        %check(c_digits : w_welm) = 0
     c                   evalr     d_orgn = %trim(w_welm)
     c                   eval      d_orgn = %xlate(' ':'0':d_orgn)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av delt.element : EAN/lok.nr.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_eanl     begsr
      *
     c                   eval      d_eanl_num = *zero
      *
     c                   if        %check(c_digits : w_welm) = 0
     c                   evalr     d_eanl = %trim(w_welm)
     c                   eval      d_eanl = %xlate(' ':'0':d_eanl)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av delt.element : Ansvarlig
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_ansv     begsr
      *
     c                   eval      w_str_i = *blank
     c                   eval      w_str_i = %trim(w_welm)
     c                   exsr      konv_XML_esc
     c                   eval      d_ansv = %trim(w_str_u)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av delt.element : Bruker
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_user     begsr
      *
     c                   eval      w_str_i = *blank
     c                   eval      w_str_i = %trim(w_welm)
     c                   exsr      konv_XML_esc
     c                   eval      d_bruk = %trim(w_str_u)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av delt.element : Bruker/tlf
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_utlf     begsr
      *
     c                   eval      w_str_i = *blank
     c                   eval      w_str_i = %trim(w_welm)
     c                   exsr      konv_XML_esc
     c                   eval      d_btlf = %trim(w_str_u)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av delt.element : Bruker/fax
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_ufax     begsr
      *
     c                   eval      w_str_i = *blank
     c                   eval      w_str_i = %trim(w_welm)
     c                   exsr      konv_XML_esc
     c                   eval      d_bfax = %trim(w_str_u)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av delt.element : Bruker/mail
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_umal     begsr
      *
     c                   eval      w_str_i = *blank
     c                   eval      w_str_i = %trim(w_welm)
     c                   exsr      konv_XML_esc
     c                   eval      d_bmai = %trim(w_str_u)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av delt.element : NOBB oppr.dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_nbdo     begsr
      *
     c                   if        w_wlen = 10
     c                   eval      d_noda = %trim(w_welm)
     c                   else
     c                   eval      d_noda_iso = *loval
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av delt.element : NOBB endr.dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_nbde     begsr
      *
     c                   if        w_wlen = 10
     c                   eval      d_neda = %trim(w_welm)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av delt.element : Opprettet dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_opda     begsr
      *
     c                   if        w_wlen = 10
     c                   eval      d_odat = %trim(w_welm)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av delt.element : Endret dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_enda     begsr
      *
     c                   if        w_wlen = 10
     c                   eval      d_edat = %trim(w_welm)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av delt.element : Endringstidspunkt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_entd     begsr
      *
     c                   if        w_wlen = 8
     c                   eval      d_etim = %trim(w_welm)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer deltaker/levr.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_delt      begsr
      *
     c                   if        b_dfel = *on
     c                   leavesr
     c                   endif
      *
     c                   clear                   jlevlur
      *
     c                   eval      jlevlu_ldor = d_dtnr_num
     c     jlevlu_key    chain     jlevlur
      *
     c*                  if        d_stat = '4'
     c*                  if        %found
     c*                  delete    jlevlur
     c*                  endif
     c*                  leavesr
     c*                  endif
      *
     c                   eval      jlnavn = %trim(d_navn)
     c                   eval      jladr1 = %trim(d_adr1)
     c                   eval      jladr2 = %trim(d_adr2)
     c                   eval      jlponr = %trim(d_ponr)
     c                   eval      jlsted = %trim(d_sted)
     c                   eval      jltlfn = %trim(d_tlfn)
     c                   eval      jlmobn = *blank
     c                   eval      jlfaxn = %trim(d_faxn)
     c                   eval      jlfaxo = %trim(d_faxo)
     c                   eval      jlmail = %trim(d_mail)
     c                   eval      jlurla = %trim(d_urla)
     c                   eval      jlkprs = %trim(d_bruk)
     c                   eval      jlktlf = %trim(d_btlf)
     c                   eval      jlkmob = *blank
     c                   eval      jlkfax = %trim(d_bfax)
     c                   eval      jlkmai = %trim(d_bmai)
     c                   eval      jlorgn = d_orgn_num
     c                   eval      jleanl = d_eanl_num
     c                   eval      jlvalu = %trim(d_valu)
     c                   eval      jlland = %trim(d_land)
     c                   eval      jldtyp = d_dtyp_num
     c                   eval      jleusr = l_user
      *
     c                   if        jlfaxo <> *blank and
     c                             jlfaxn = *blank
     c                   eval      jlfaxn = jlfaxo
     c                   endif
      *
     c                   time                    jledat
     c                   time                    jletim
      *
     c                   if        %found
     c                   update    jlevlur
     c                   else
     c                   eval      jlldor = d_dtnr_num
     c                   time                    jlodat
     c                   write     jlevlur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tilbakestilling av XML-escape tegn i tekstfelt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     konv_XML_esc  begsr
      *
     c                   eval      w_posi = 0
     c                   eval      w_posu = 0
     c                   eval      w_str_u = *blank
     c                   eval      b_blnk = *off
      *
     c                   dow       w_posi < 395 and
     c                             b_blnk = *off
     c                   eval      w_posi = w_posi + 1
     c                   if        %subst(w_str_i:w_posi) = *blank
     c                   eval      b_blnk = *on
     c                   else
     c                   eval      w_str_x = %subst(w_str_i:w_posi:1)
     c                   if        w_posu < 395
     c                   select
     c                   when      %subst(w_str_i:w_posi:5) = '&amp;'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:1) = '&'
     c                   eval      w_posi = w_posi + 4
      *
     c                   when      %subst(w_str_i:w_posi:4) = '&lt;'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:1) = '<'
     c                   eval      w_posi = w_posi + 3
      *
     c                   when      %subst(w_str_i:w_posi:4) = '&gt;'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:1) = '>'
     c                   eval      w_posi = w_posi + 3
      *
     c                   when      %subst(w_str_i:w_posi:6) = '&apos;'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:1) = c_apos
     c                   eval      w_posi = w_posi + 5
      *
     c                   when      %subst(w_str_i:w_posi:6) = '&quot;'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:1) = '"'
     c                   eval      w_posi = w_posi + 5
      *
     c                   other
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:1) = w_str_x
      *
     c                   endsl
     c                   endif
     c                   endif
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
      *
      * Key for oppdatering av leverandør
      *
     c     jlevlu_key    klist
     c                   kfld                    jlevlu_ldor
      *
      * Key for oppslag på status
      *
     c     jstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c     jstsl1_key    chain     jstsl1
      *
     c                   endsr
