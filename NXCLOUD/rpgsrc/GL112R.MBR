      /TITLE  ASOKON Oppdaterer returfiler:
     h datedit(*dmy.) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * System  . . . . : ASOKON                                        *
      * Program . . . . : GL112R                                        *
      * Beskrivelse . . : Oppdaterer returfiler:                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
      *           291294  Nytt program desember-1994               RH   *
T5.00 * V5.00     171002  Ikke lenger key i GUBFPF, bruker L8      HFL  *
T5.00 * V5.40     230505  Lagt inn oppdatering av lev.post         KOB  *
      *                                                                 *
      * 13105     131005  Dersom forslag IKKE finnes, så blir      RHO  *
      *                   lev.nummer tatt ut i fra BETFOR-post.    RHO  *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KC = F3  = Exit                                           *
      *       KL = F12 = Annuller                                       *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *    60-69 = Fileindikatorer chain etc.                           *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
E5.00fgubfl8    if   e           k disk
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
T5.40frltrl8    uf   e           k disk    rename(rltrpfr:rltrl8r)
     fgur1pf    o  a e             disk
     fgur2pf    o  a e             disk
     fgurfpf    o  a e             disk
     fgul1pf    o  a e             disk
     fgul2pf    o  a e             disk
     fgulfpf    o  a e             disk
T5.40 *
T5.40 * Definering av Local data area
T5.40 *
T5.40d                uds
T5.40d l_user                911    920
T5.40d l_firm                944    946  0
T5.40d l_fnav                951    980
      *
     d wpbefe          s              2  0
     d wpbelØ          s             15  2
     d wpbetd          s              8  0
     d wpbetf          s              8
     d wpbe00          s              1
     d wpbfli          s              5  0
13105d wplevr          s              6  0
     d wpbfnr          s              3  0
     d wpfeil          s              2  0
     d wpfirm          s              3  0
     d wpkidi          s             27
     d wpktoi          s             11  0
     d wpktou          s             35
     d wplØre          s              1
     d wpnavn          s             30
     d wpokno          s              8  4
     d wpoppd          s              1  0
     d wpprov          s              9  2
     d wprefe          s              6
     d wpretu          s              1
     d wpvalb          s             15  2
     d wpvkod          s              3
      *
T5.40d rltrl8_levr     s                   like(rolevr)
T5.40d rltrl8_biln     s                   like(robiln)
      /eject
      *
      * Tester om godkjent eller avvist transaksjon:
      *
     c                   if        wpretu <> 'F'
     c                   exsr      xokret
     c                   else
     c                   exsr      xerret
     c                   endif
      *
      /space 3
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Behandling av godkjent retur:                                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     c     xokret        begsr
      *
      * WPOPPD flagges til "1" dersom ikke tilslag ved søk
      * mot utbetalingsforslagsfilen:
      *
     c                   eval      wpoppd = 0
      *
     c                   eval      ganavn = *blanks
      *
E5.00c     gubfl8_key    chain     gubfl8                             60
      *
     c                   if        *in60 = *on
     c                   eval      wpoppd = 1
     c                   endif
      *
     c                   if        wpbetf <> 'BETFOR22' and
     c                             *in60 = *off and
     c                             wplØre = 'R'
      * Leser RLEVPF for å hente ev. factoringnr.:
     c     rlevl1_key    chain     rlevl1                             60
     c                   endif
      *
      * Leser RLEVPF med annen nøkkel:
      *
13105c                   if        wpoppd =1
13105c     rlevl2_key    chain     rlevl1                             60
13105c                   endif
      *
     c                   exsr      xoverf
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Behandling av feilretur:                                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xerret        begsr
      *
      * WPOPPD flagges til "1" dersom ikke tilslag ved søk
      * mot utbetalingsforslagsfilen:
      *
     c                   eval      wpoppd = 1
     c                   eval      rllevr = 0
     c                   eval      rlfact = 0
     c                   eval      gabiln = 0
      *
     c                   eval      ganavn = *blanks
      *
     c                   if        wpbetf = 'BETFOR04' or
     c                             wpbetf = 'BETFOR22' or
     c                             wpbetf = 'BETFOR23'
      *
E5.00c     gubfl8_key    chain     gubfl8
      *
     c                   if        %found and
     c                             wpbetf <> 'BETFOR22'and
     c                             wplØre = 'R'
      *
      * Leser RLEVL1 for å hente ev. factoringnr.:
      *
     c     rlevl1_key    chain     rlevl1                             60
     c                   endif
      *
     c                   endif
      *
     c                   exsr      xoverf
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for overføring av data til returfiler:                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xoverf        begsr
      *
      * "Flagger" at det er mottatt feilretur på BETFOR00, og
      * mellomlagrer returkoden.
      * "Flagget" blir senere brukt til å legge ut en ev.
      * feilretur for BETFOR00-posten.  Dette gjøres først
      * når en senere BETFOR-transaksjon er lest, som kan
      * identifisere vilket forslag det er feil på.
      * BETFOR00 har inneholder IKKE info om forslagsnummer!
      *
     c                   if        wpbetf = 'BETFOR00' and
     c                             wpfeil <> 0
     c                   eval      wpbefe = wpfeil
     c                   movel     'J'           wpbe00
     c                   endif
      *
      * Legger ut på 1. gangs retur - regnskap:
      *
     c                   if        wpretu = '1' and
     c                             wplØre = 'R'
      *
     c                   eval      gdnfir = wpfirm
     c                   eval      gdnfor = wpbetd
     c                   eval      gdnlev = rllevr
     c                   eval      gdnfac = rlfact
     c                   movel     wpvkod        gdnval
     c                   movel     wprefe        gdnref
     c                   eval      gdnbnr = gabiln
     c                   eval      gdnbel = wpbelØ
     c                   eval      gdnvbe = wpvalb
     c                   eval      gdntki = wpktoi
     c                   movel     wpktou        gdntku
     c                   movel     wpkidi        gdnkid
     c                   eval      gdnubf = wpbfnr
     c                   eval      gdnbfl = wpbfli
     c                   eval      gdnopp = wpoppd
      *
     c                   write     gur1pfr
     c                   endif
      *
      * Legger ut på 2. gangs retur - regnskap:
      *
     c                   if        wpretu = '2' and
     c                             wplØre = 'R'
      *
     c                   eval      gdofir = wpfirm
     c                   eval      gdofor = wpbetd
     c                   eval      gdolev = rllevr
     c                   eval      gdofac = rlfact
     c                   movel     wpvkod        gdoval
     c                   movel     wprefe        gdoref
     c                   eval      gdobnr = gabiln
     c                   eval      gdobel = wpbelØ
     c                   eval      gdovbe = wpvalb
     c                   eval      gdotki = wpktoi
     c                   movel     wpktou        gdotku
     c                   movel     wpkidi        gdokid
     c                   eval      gdoubf = wpbfnr
     c                   eval      gdobfl = wpbfli
     c                   eval      gdokno = wpokno
     c                   eval      gdoopp = wpoppd
     c                   eval      gdopro = wpprov
      *
     c                   write     gur2pfr
T5.40 *
T5.40 * Oppdater reskontropost
T5.40 *
T5.40c                   eval      rltrl8_levr = galevr
T5.40c                   eval      rltrl8_biln = gabiln
T5.40c     rltrl8_key    chain     rltrl8
T5.40c                   if        %found
      *
     c                   if        wpktou <> *blank
T5.40c                   move      wpktou        rotkto
T5.40c                   else
T5.40c                   move      wpktoi        rotkto
T5.40c                   endif
      *
T5.40c                   eval      rorsig = l_user
T5.40c                   time                    rordat
T5.40c                   time                    rortim
T5.40c                   update    rltrl8r
      *
T5.40c                   endif
      *
     c                   endif
      *
      * Legger ut på feilretur:
      *
     c                   if        wpretu = 'F' and
     c                             wplØre = 'R'
      *
      * Legger ev. ut feilretur for BETFOR00 dersom denne
      * hadde feilstatus:
      *
     c                   if        wpbe00 = 'J' and
     c                             wpbetf <> 'BETFOR00'
      *
     c                   eval      gdpfir = wpfirm
     c                   eval      gdpfor = wpbetd
     c                   eval      gdpret = wpbefe
     c                   movel     'BETFOR00'    gdptrk
     c                   eval      gdpubf = wpbfnr
     c                   eval      gdpbfl = wpbfli
      *
     c                   write     gurfpfr
     c                   eval      wpbe00 = *blanks
     c                   eval      wpbefe = 0
     c                   endif
      *
     c                   eval      gdpfir = wpfirm
     c                   eval      gdpfor = wpbetd
     c                   eval      gdplev = rllevr
     c                   eval      gdpbnr = gabiln
     c                   eval      gdpret = wpfeil
     c                   movel     wpbetf        gdptrk
     c                   eval      gdpubf = wpbfnr
     c                   eval      gdpbfl = wpbfli
     c                   eval      gdpopp = wpoppd
      *
     c                   write     gurfpfr
     c                   endif
      *
      * Legger ut på 1. gangs retur - Lønn:
      *
     c                   if        wpretu = '1' and
     c                             wplØre = 'L'
      *
     c                   eval      gdsfir = wpfirm
     c                   eval      gdsfor = wpbetd
     c                   eval      gdslon = galevr
     c                   movel     wpnavn        gdsnav
     c                   movel     wprefe        gdsref
     c                   eval      gdsbel = wpbelØ
     c                   eval      gdstki = wpktoi
     c                   eval      gdsubf = wpbfnr
     c                   eval      gdsbfl = wpbfli
     c                   eval      gdsopp = wpoppd
      *
     c                   write     gul1pfr
     c                   endif
      *
      * Legger ut på 2. gangs retur - Lønn:
      *
     c                   if        wpretu = '2' and
     c                             wplØre = 'L'
      *
     c                   eval      gdtfir = wpfirm
     c                   eval      gdtfor = wpbetd
     c                   eval      gdtlon = galevr
     c                   movel     wpnavn        gdtnav
     c                   movel     wprefe        gdtref
     c                   eval      gdtbel = wpbelØ
     c                   eval      gdttki = wpktoi
     c                   eval      gdtubf = wpbfnr
     c                   eval      gdtbfl = wpbfli
     c                   eval      gdtopp = wpoppd
      *
     c                   write     gul2pfr
     c                   endif
      *
      * Legger ut på feilretur - Lønn:
      *
     c                   if        wpretu = 'F' and
     c                             wplØre = 'L'
      *
      * Legger ev. ut feilretur for BETFOR00 dersom denne
      * hadde feilstatus:
      *
     c                   if        wpbe00 = 'J' and
     c                             wpbetf <> 'BETFOR00'
      *
     c                   eval      gdvfir = wpfirm
     c                   eval      gdvfor = wpbetd
     c                   eval      gdvret = wpbefe
     c                   movel     'BETFOR00'    gdvtrk
     c                   eval      gdvubf = wpbfnr
     c                   eval      gdvbfl = wpbfli
      *
     c                   write     gulfpfr
     c                   eval      wpbe00 = *blanks
     c                   eval      wpbefe = 0
     c                   endif
      *
      *
     c                   eval      gdvfir = wpfirm
     c                   eval      gdvfor = wpbetd
     c                   eval      gdvlon = galevr
     c                   movel     wpnavn        gdvnav
     c                   eval      gdvret = wpfeil
     c                   movel     wpbetf        gdvtrk
     c                   eval      gdvubf = wpbfnr
     c                   eval      gdvbfl = wpbfli
     c                   eval      gdvopp = wpoppd
      *
     c                   write     gulfpfr
     c                   endif
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     c     *inzsr        begsr
      *
      * Parameterliste:
      *
     c     *entry        plist
     c                   parm                    wpfirm
     c                   parm                    wpbfnr
     c                   parm                    wpbfli
     c                   parm                    wpretu
     c                   parm                    wplØre
     c                   parm                    wpbetd
     c                   parm                    wprefe
     c                   parm                    wpbelØ
     c                   parm                    wpvalb
     c                   parm                    wpvkod
     c                   parm                    wpktoi
     c                   parm                    wpktou
     c                   parm                    wpkidi
     c                   parm                    wpfeil
     c                   parm                    wpbetf
     c                   parm                    wpnavn
     c                   parm                    wpbe00
     c                   parm                    wpbefe
     c                   parm                    wpokno
     c                   parm                    wpprov
13105c                   parm                    wplevr
      *
      * Definering av variable:
      *
     c                   eval      wpoppd = 0
      *
      * Key for forslagsfile:
      *
E5.40c     gubfl8_key    klist
     c                   kfld                    wpfirm
     c                   kfld                    wpbfnr
     c                   kfld                    wpbfli
      *
      * Key for leverandørfile:
      *
E5.40c     rlevl1_key    klist
     c                   kfld                    wpfirm
     c                   kfld                    galevr
      *
      * Key for leverandørfile:
      *
13105c     rlevl2_key    klist
13105c                   kfld                    wpfirm
13105c                   kfld                    wplevr
      *
T5.40 * Key for leverandørposter:
T5.40 *
T5.40c     rltrl8_key    klist
T5.40c                   kfld                    wpfirm
T5.40c                   kfld                    rltrl8_levr
T5.40c                   kfld                    rltrl8_biln
      *
     c                   endsr
      *
