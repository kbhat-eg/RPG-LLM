     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : NG740R
      * Beskrivelse . . : Styreprogram - sende hva/hvor
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.
      * --------- ------  --------------------------------------------------
      *     8.xx  040522  Nytt program                              bsa
8.01  *K000 8.xx  100822  Mangler overføring av statusfelt.         bsa
8.02  *K000 8.xx  120822  Feil sjekk.                               bsa
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flldilr    if   e           k disk    rename(lldipfr:lldilrr)
     flohelr    if   e           k disk    rename(lohepfr:lohelrr)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
      *
      *
      * Datastruktur
      *
     d                 ds
     d d_urec                        80
     d  d_firm                        3  0 overlay(d_urec)
6.nu d  d_numm                        8  0 overlay(d_urec:4)
6.nu d  d_suff                        2  0 overlay(d_urec:12)
6.nu d  d_kode                        1    overlay(d_urec:14)
6.nu d  d_date                         d   overlay(d_urec:15) datfmt(*iso)
6.nu d  d_time                         t   overlay(d_urec:25) timfmt(*iso)
6.nu d  d_user                       10    overlay(d_urec:33)
6.nu d  d_wsid                       10    overlay(d_urec:43)
      *
      * Local Data Area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d lohelr_numm     s                   like(lonumm)
     d lohelr_suff     s                   like(losuff)
     d lldilr_ldor     s                   like(lpldor)
     d lldilr_lage     s                   like(lplage)
     d rlevl1_levr     s                   like(rllevr)
      *
      * Definering av variable i parametre
      *
     d p_numm          s                   like(lonumm)
     d p_suff          s                   like(losuff)
6.20 d p_stat          s              1
     d p_otyp          s              4
     d p_akod          s              2  0
     d p_aktk          s              2
     d p_luke          s              4  0
     d p_lknr          s             10
5.53 d p_text          s             35
      *
      * Definering av øvrige variable
      *
     d w_firm          s                   like(lofirm)
     d w_mld1          s             50    inz(*blank)
     d w_stmp          s               z
     d w_stmp_alf      s             26
     d w_memb          s             10
     d w_lknr          s             10
     d w_numm_alf      s              6
     d w_leng          s              2  0
     d w_stat          s              1
     d w_parm          s             80
6.11 d w_bsts          s              1
6.11 d w_numm          s                   like(lonumm)
6.11 d w_suff          s                   like(losuff)
6.30 d w_navn          s             10
6.30 d w_osta          s              1
6.25 d r_prog          s             10
6.25 d r_stat          s              1
      *
     d w_feil          s              1    inz(*off)
      *
      * Definering av konstanter
      *
     d c_null          s              6    inz('000000')
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram - legg ut informasjon og start overføring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Finn ordre
      *
     c                   eval      lohelr_numm = p_numm
     c                   eval      lohelr_suff = p_suff
     c     lohelr_key    chain     lohelrr
     c                   if        not %found
     c                   eval      w_stat = 'B'
     c                   goto      pgm_end
     c                   endif
      *
     c                   if        lobsen = 1
     c**                 if        lobsda <> *loval
     c**   *dmy          move      lobsda        a1sida
     c**                 endif
     c**                 eval      a1sius = lobsav
     c**                 eval      a1stat = 'BEST. SENDT FØR - F10=OMKJØRING'
     c                   endif
      *
      * Finn leverandør på ordre
      *
     c                   eval      rlevl1_levr = loldor
     c     rlevl1_key    chain     rlevl1
     c                   if        not %found
     c                   eval      w_stat = 'L'
     c                   goto      pgm_end
     c                   endif
      *
      * Finn forsendelsesinfo
      *
     c                   eval      lldilr_ldor = loldor
     c                   move      lolage        lldilr_lage
     c     lldilr_key    chain     lldilr
     c                   if        not %found
     c                   eval      lldilr_lage = *blank
     c     lldilr_key    chain     lldilr
     c                   if        not %found
     c                   eval      w_stat = 'F'
     c                   goto      pgm_end
     c                   endif
     c                   endif
      *
      * Sjekk om det skal sjekkes etter NEB standard
      *
     c**                 if        lpnebg <> '1'
     c**                 eval      *in42 = *off
     c**                 endif
      *
     c**                 if        lobsen = 1 and
     c**                           *inkj = *off
     c**                 goto      pgm_end
     c**                 endif
      * Sjekk om det leverandøren skal sende på EDI
8.02 c                   if        %trim(lpform) <> 'EDI'
     c                   eval      w_stat = 'E'
     c                   goto      pgm_end
     c                   endif
      *
      * Er det GLN nummer på leverandøren
      *
     c                   if        rleanl = 0
     c                   eval      w_stat = 'G'
     c                   goto      pgm_end
     c                   endif
      *
      * Sjekk kvaliteten på varelinjene hvis NEB standard
      *
     c                   eval      w_bsts = *blank
     c                   eval      w_numm = p_numm
     c                   eval      w_suff = p_suff
     c                   call      'LI653R'
     c                   parm                    w_numm
     c                   parm                    w_suff
     c                   parm                    w_bsts
      * Hvis feilkode kommer i retur, send feilmelding
     c                   if        w_bsts = '1'
     c                   eval      w_stat = 'V'
     c                   goto      pgm_end
     c                   endif
      *
      * Hvis feilkode kommer i retur, send feilmelding (leverandørinfo)
      *
     c                   if        w_bsts = '2'
     c                   eval      w_stat = 'L'
     c                   goto      pgm_end
     c                   endif
      *
      * Sjekk om overfølring allerede pågår.
      *
      * NBS
     c                   if        %trim(lpform) = 'EDI' and
     c                             %trim(lptype) = 'FTP' and
     c                             %trim(lpmott) = 'NBS'
     c                   eval      w_navn = 'NBSEDISND'
     c                   else
     c                   eval      w_stat = 'T'
     c                   goto      pgm_end
     c                   endif
      *
     c                   if        w_navn = *blanks
     c                   eval      w_stat = 'T'
     c                   goto      pgm_end
     c                   endif
      *
      * Sjekk om overføring finnes eller pågår
      *
     c                   call      'AF712R'
     c                   parm                    w_navn
     c                   parm                    w_osta
      *
     c                   if        w_osta <> *blanks
     c                   eval      w_stat = 'T'
     c                   goto      pgm_end
     c                   endif
      *
      * Overføring pågår alikevel - sjekk mot LWEDUT
      *
     c                   call      'AF713C'
     c                   parm                    w_osta
      *
     c                   if        w_osta <> *blanks
     c                   eval      w_stat = 'T'
     c                   goto      pgm_end
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     pgm_end       tag
      *
8.01 c                   eval      p_stat = w_stat
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på leverandør distribusjon
      *
     c     lldilr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lldilr_ldor
     c                   kfld                    lldilr_lage
      *
      * Key for oppslag på ordre/suffix
      *
     c     lohelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelr_numm
     c                   kfld                    lohelr_suff
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_stat
      *
     c                   eval      w_firm = l_firm
     c                   eval      p_stat = *blank
8.01 c                   eval      w_stat = *blank
      *
     c                   endsr
