     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : RF225R
      * Beskrivelse . . : Kortregister, vedlikehold
      *
      * Spesilatilpasning:
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       070910 BSA  Nytt program
6.10  *       171011 BSA  Utvidet med oppgjørsform
6.11  *       301312 BSA  Utvidet med kort for GE Moneybank
6.12  *       060812 BSA  Skiller mellom fordel/kredit kort på feilmeldinger
6.30  *       020514 BSA  Legger inn ordreinfokode for å holde ordre
6.30  *                   fra fakturering.
      *
6.31  *       250914 ASK  Lagt inn bankterminal korttype og spesifiaksjon
8.00  * 8.00  250522 sst  Endret fra 'GE Kort' til Santander
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frukslr    if   e           k disk    rename(rukspfr:rukslrr)
     fruksl1    if   e           k disk    rename(rukspfr:ruksl1r)
     frukslu    uf a e           k disk    rename(rukspfr:rukslur)
     fra01l1    if   e           k disk    rename(ra01pfr:ra01l1r)
     frhovl1    if   e           k disk    rename(rhovpfr:rhovl1r)
6.30 fvinfl1    if   e           k disk    rename(vinfpfr:vinfl1r)
      *
     frf225d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(rusfir)
      *
     d p_ofir          s              3  0
     d p_okun          s              6  0
     d p_opro          s              6  0
     d p_okpn          s              6  0
     d p_otyp          s              2  0
     d p_otsq          s              4  0
     d p_oper          s              1
     d p_kont          s              5  0
     d p_spes          s              4  0
     d p_akti          s              3
     d p_txt1          s             30
     d p_ktyp          s              2  0
     d p_in03          s              1
     d p_akod          s              2  0
     d p_atxt          s             20
      *
      * Definering av local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d rukslr_skty     s                   like(ruskty)
     d rukslr_slag     s                   like(ruslag)
     d ruksl1_skty     s                   like(ruskty)
     d ruksl1_slag     s                   like(ruslag)
     d rukslu_skty     s                   like(ruskty)
     d rukslu_slag     s                   like(ruslag)
     d ra01l1_akod     s                   like(raakod)
     d rhovl1_kont     s                   like(rhkont)
     d rhovl1_spes     s                   like(rhspes)
     d rhovl1_avde     s                   like(rhavde)
6.30 d vinfl1_sity     s                   like(rusity)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
     d w_ktyp          s              2  0
     d w_ktpn          s             25
      *
     d w_seqe          s              1
     d b_valg_ok       s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_tlgx          s              1    inz(*off)
      *
     d w_firm          s                   like(rusfir)
     d w_kort          s             16
     d w_ddat          s               d   datfmt(*iso)
     d w_adr1          s             30
     d w_adr2          s             30
     d w_sted          s             30
     d w_kprs          s             30
     d w_tlfa          s             15
     d w_tlfp          s             15
     d w_tlfm          s             15
     d w_mail          s             50
     d w_orka          s              3  0
     d w_okun          s              6  0
     d w_okpr          s              6  0
     d w_opda          s               d
     d w_proj          s              6  0
     d w_posi          s              2  0
     d w_modu          s             10
     d w_disp          s              1
     d w_stat          s              1
     d w_ltek          s             30
     d w_lage          s                   like(ruslag)
     d w_latx          s             30
6.30 d w_sity          s              2
6.30 d w_stxt          s             35
      *
      * Definering av konstanter
      *
     d c_modu          c                   'KORT      '
     d c_sfil          s              2  0 inz(13)
     d c_digi          c                   '0123456789 '
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * F1WIN  - F1 Vindu for spørring på valgalternativer (Hardkodet)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   if        b_tlgx = *off
     c                   goto      avslutt
     c                   endif
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      bc2bld
     c                   exsr      xc2bld
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
     c                   if        b_valg_ok = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      ruksl1_skty = *zero
     c     ruksl1_key    setll     ruksl1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      b_forn = *off
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2 or
     c                             b1valg = 5
     c                   eval      c2skty = b1skty
     c                   eval      c2slag = b1slag
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1skty = b1skty
     c                   eval      d1type = b1type
     c                   eval      d1slag = b1slag
     c                   eval      d1sbes = b1sbes
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Start overføring av kortinfo
      *
     c                   if        b1valg = 7
     c                   eval      p_ktyp = b1skty
     c                   eval      p_in03 = *blank
     c                   call      'RF810R'
     c                   parm                    p_ktyp
     c                   parm                    p_in03
      *
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
      * Fyller opp subfile
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      ruksl1
      *
     c                   if        %eof
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   if        rusfir <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
      * Skriver til subfile
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1skty = ruskty
     c                   eval      b1slag = ruslag
     c                   eval      b1sbes = rusbes
     c                   eval      b1sfra = rusfra
     c                   eval      b1sdat = rusdat
      *
     c                   exsr      avt_tp_sh
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle skjermbilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
     c                   eval      *in40 = *off
      *
     c                   eval      rukslr_skty = c2skty
     c                   eval      rukslr_slag = c2slag
     c     rukslr_key    chain     rukslr
      *
     c                   if        %found(rukslr)
     c                   eval      c2skid = ruskid
     c                   eval      c2sbes = rusbes
     c                   eval      c2sfrk = rusfrk
     c                   eval      c2sdg1 = rusdg1
     c                   eval      c2sdg2 = rusdg2
     c                   eval      c2sdg3 = rusdg3
     c                   eval      c2sdg4 = rusdg4
     c                   eval      c2sdg5 = rusdg5
     c                   eval      c2sdg6 = rusdg6
     c                   eval      c2sdg7 = rusdg7
     c                   eval      c2sktf = rusktf
     c                   eval      c2sktt = rusktt
     c                   eval      c2skto = ruskto
6.31 c                   eval      c2spes = russpe
     c                   eval      c2styp = rustyp
6.31 c                   eval      c2kbty = rusbty
     c                   eval      c2sbkd = rusbkd
     c                   eval      c2skkd = ruskkd
6.10 c                   eval      c2some = rusome
6.30 c                   eval      c2sity = rusity
     c                   eval      *in40 = *on
     c     *dmy          move      rusfra        c2sfra
     c                   eval      c2shld = rushld
     c                   if        rusdat <> *loval
     c     *dmy          move      rusdat        c2sdat
     c                   endif
      *
     c                   exsr      avt_tp_fu
      *
     c                   eval      c2seus = ruseus
      *
     c                   if        ruseda <> *loval
     c     *dmy          move      ruseda        c2seda
     c                   endif
      *
     c                   else
      *
     c                   eval      c2skty = *zero
6.31 c                   eval      c2kbty = *blank
     c                   eval      c2ktpn = *blank
     c                   eval      c2slag = *zero
     c                   eval      c2ltxt = *blank
     c                   eval      c2skid = *blank
     c                   eval      c2sbes = *blank
     c                   eval      c2shld = *blank
     c                   eval      c2sfra = *zero
     c                   eval      c2sdat = *zero
     c                   eval      c2seus = *blank
     c                   eval      c2seda = *zero
     c                   eval      c2sfrk = *blank
     c                   eval      c2sdg1 = *blank
     c                   eval      c2sdg2 = *blank
     c                   eval      c2sdg3 = *blank
     c                   eval      c2sdg4 = *blank
     c                   eval      c2sdg5 = *blank
     c                   eval      c2sdg6 = *blank
     c                   eval      c2sdg7 = *blank
     c                   eval      c2sktf = *zero
     c                   eval      c2sktt = *zero
     c                   eval      c2styp = *blank
     c                   eval      c2skto = *zero
6.31 c                   eval      c2spes = *zero
     c                   eval      c2sbkd = *zero
     c                   eval      c2skkd = *zero
6.10 c                   eval      c2some = *blank
6.30 c                   eval      c2sity = *blank
      *
     c                   endif
      *
      * Dersom 'vise' settes protect-indikatorer
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   eval      *in40 = *on
     c                   endif
      *
      * Behandler bildet
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
     c                   eval      *in31 = *off
     c                   eval      *in32 = *off
     c                   eval      *in33 = *off
     c                   eval      *in34 = *off
     c                   eval      *in35 = *off
     c                   eval      *in36 = *off
     c                   eval      *in37 = *off
     c                   eval      *in39 = *off
6.30 c                   eval      *in41 = *off
      *
      * Spørring
      *
     c                   if        *inkd
      *
     c                   if        w_afld = 'C2SKTY'
     c                   if        *in40 = *off
     c                   eval      c2skty = *zeros
     c                   eval      f1head = 'kort/avt.type'
     c                   eval      f1alth = 'Kort/avt.type'
     c                   exsr      xf1win1
     c                   eval      c2skty = w_ktyp
     c                   eval      c2ktpn = w_ktpn
     c                   goto      c2tagb
     c                   endif
     c                   endif
      * Lager
     c                   if        *in40 = *off
     c                   if        w_afld = 'C2SLAG'
     c                   eval      c2slag = *zeros
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    w_lage
     c                   parm                    w_ltek
     c                   move      w_lage        c2slag
     c                   movel     w_ltek        c2ltxt
     c                   goto      c2tagb
     c                   endif
     c                   endif
      *                                                                 .
      * Konto
      *                                                                 .
     c                   if        w_afld = 'C2SKTO'                                         .
      *                                                                 .
     c                   call      'RH500R'                                      konto       .
     c                   parm                    w_firm
     c                   parm                    p_kont
     c                   parm                    p_spes
     c                   parm                    p_akti
     c                   parm                    p_txt1
      *
     c                   if        p_kont <> *zero
     c                   move      p_kont        c2skto
     c                   goto      c2tagb
     c                   endif
      *
     c                   endif
      *                                                                 .
      * Bilagskode (debet)
      *                                                                 .
     c                   if        w_afld = 'C2SBKD'                                         .
      *                                                                 .
     c                   call      'RA501R'                                      konto       .
     c                   parm                    w_firm
     c                   parm                    p_akod
     c                   parm                    p_atxt
      *
     c                   if        p_akod <> *zero
     c                   eval      c2sbkd = p_akod
     c                   goto      c2tagb
     c                   endif
      *
     c                   endif
6.30  *                                                                 .
6.30  * Ordreinfo kode
6.30  *                                                                 .
6.30 c                   if        w_afld = 'C2SITY'                                         .
6.30  *                                                                 .
6.30 c                   call      'VA540R'                                      konto       .
6.30 c                   parm                    w_firm
6.30 c                   parm                    w_sity
6.30 c                   parm                    w_stxt
6.30  *
6.30 c                   if        w_sity <> *blanks
6.30 c                   eval      c2sity = w_sity
6.30 c                   goto      c2tagb
6.30 c                   endif
6.30  *
6.30 c                   endif
      *                                                                 .
      * Bilagskode (kredit)
      *                                                                 .
     c                   if        w_afld = 'C2SKKD'                                         .
      *                                                                 .
     c                   call      'RA501R'                                      konto       .
     c                   parm                    w_firm
     c                   parm                    p_akod
     c                   parm                    p_atxt
      *
     c                   if        p_akod <> *zero
     c                   eval      c2skkd = p_akod
     c                   goto      c2tagb
     c                   endif
      *
     c                   endif
      *
     c                   endif
      *
      * Validering
      *
     c                   if        c2skty = 0
     c                   eval      *in31  = *on
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        c2sfrk <> 'DG' and
     c                             c2sfrk <> '14' and
     c                             c2sfrk <> 'UK' and
     c                             c2sfrk <> 'MD'
     c                   eval      *in39 = *on
     c                   goto      c2tagb
     c                   endif
      * Sjekk lager
     c                   if        c2slag <> *zero
     c                   eval      w_lage = c2slag
     c                   call      'RS710R'
     c                   parm                    w_stat
     c                   parm                    w_lage
     c                   parm                    w_latx
      *
     c                   if        w_stat <> *blank
     c                   eval      *in34  = *on
     c                   goto      c2tagb
     c                   else
     c                   eval      c2ltxt = w_latx
     c                   endif
     c                   endif
      *
      * Gyldig fra må legges inn
     c                   if        c2sfra = *zero
     c                   eval      *in32  = *on
     c                   goto      c2tagb
     c                   endif
      *
      * Aktiv/passiv kode må legges inn
     c                   if        c2shld <> 'A' and
     c                             c2shld <> 'P'
     c                   eval      *in33  = *on
     c                   goto      c2tagb
     c                   endif
      * Korttype må legges inn (F eller K)
     c                   if        c2styp <> 'F' and
     c                             c2styp <> 'K'
     c                   eval      *in35  = *on
     c                   goto      c2tagb
     c                   endif
6.12  * Oppfølgingmetode må være gyldig (sekvensen er flyttet)
6.12  * Sjekker kun hvis kreditkort
6.12 c                   if        c2some = *blank and
6.12 c                             c2styp = 'K'
6.10 c                   eval      *in38  = *on
6.10 c                   goto      c2tagb
6.10 c                   endif
6.12  *
6.12 c                   if        c2styp = 'K'
6.10 c                   if        c2some <> 'R' and
6.10 c                             c2some <> 'H'
6.10 c                   eval      *in38  = *on
6.10 c                   goto      c2tagb
6.10 c                   endif
6.12 c                   endif
      * Konto må være gyldig
6.12 c                   if        c2styp = 'K' and
6.12 c                             c2some = 'H' and
6.12 c                             c2skto = 0
     c                   eval      *in36  = *on
     c                   goto      c2tagb
     c                   endif
      *
6.12 c                   if        c2styp = 'K' and
6.12 c                             c2some = 'H'
     c                   eval      rhovl1_kont = c2skto
     c                   eval      rhovl1_spes = *zero
     c                   eval      rhovl1_avde = *zero
     c     rhovl1_key    chain     rhovl1
     c                   if        not %found
     c                   eval      *in36 = *on
     c                   goto      c2tagb
     c                   endif
6.12 c                   endif
6.12  * Bilagskode må være gyldig hvis kreditkort
6.12 c                   if        c2styp = 'K' and
6.12 c                             c2some = 'H'
      * Debet
     c                   if        c2sbkd = 0
     c                   eval      *in37  = *on
     c                   goto      c2tagb
     c                   endif
      *
     c                   eval      ra01l1_akod = c2sbkd
     c     ra01l1_key    chain     ra01l1
     c                   if        not %found
     c                   eval      *in37 = *on
     c                   goto      c2tagb
     c                   endif
      * Kredit
     c                   if        c2skkd = 0
     c                   eval      *in37  = *on
     c                   goto      c2tagb
     c                   endif
      *
     c                   eval      ra01l1_akod = c2skkd
     c     ra01l1_key    chain     ra01l1
     c                   if        not %found
     c                   eval      *in37 = *on
     c                   goto      c2tagb
     c                   endif
      *
     c                   endif
6.30  *
6.30  * Sjekk oredreinfokode
6.30  *
6.30 c                   if        c2sity <> *blanks
6.30 c                   eval      vinfl1_sity = c2sity
6.30 c     vinfl1_key    chain     vinfl1
6.30 c                   if        not %found
6.30 c                   eval      *in41 = *on
6.30 c                   goto      c2tagb
6.30 c                   endif
6.30 c                   endif
      *
      * Oppdaterer tabell
      *
     c                   clear                   rukslur
      *
     c                   eval      rukslu_skty = c2skty
     c                   eval      rukslu_slag = c2slag
     c     rukslu_key    chain     rukslur
      *
     c                   eval      ruskid = c2skid
     c                   eval      rusbes = c2sbes
     c                   eval      rusfrk = c2sfrk
     c                   eval      rusdg1 = c2sdg1
     c                   eval      rusdg2 = c2sdg2
     c                   eval      rusdg3 = c2sdg3
     c                   eval      rusdg4 = c2sdg4
     c                   eval      rusdg5 = c2sdg5
     c                   eval      rusdg6 = c2sdg6
     c                   eval      rusdg7 = c2sdg7
     c                   eval      rusktf = c2sktf
     c                   eval      rusktt = c2sktt
     c                   eval      rustyp = c2styp
6.31 c                   eval      rusbty = c2kbty
     c                   eval      ruskto = c2skto
6.31 c                   eval      russpe = c2spes
     c                   eval      rusbkd = c2sbkd
     c                   eval      ruskkd = c2skkd
6.10 c                   eval      rusome = c2some
6.30 c                   eval      rusity = c2sity
     c     *dmy          move      c2sfra        rusfra
     c                   eval      rushld = c2shld
     c                   eval      ruseus = l_user
     c                   time                    ruseda
     c                   time                    ruseti
      *
     c                   if        %found(rukslu)
     c                   update    rukslur
     c                   else
     c                   eval      rusfir = w_firm
     c                   eval      ruskty = c2skty
     c                   eval      ruslag = c2slag
     c                   eval      rusous = l_user
     c                   time                    rusoti
     c                   time                    rusoda
     c                   write     rukslur
      *
     c                   eval      b1skty = c2skty
     c                   exsr      forny
     c                   endif
      *
     c                   eval      b_forn = *on
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30 = *off
     c                   eval      *in40 = *off
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å init. av skjermbilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bc2bld        begsr
      *
     c                   eval      c2skty = *zero
6.31 c                   eval      c2kbty = *blank
     c                   eval      c2ktpn = *blank
     c                   eval      c2slag = *zero
     c                   eval      c2ltxt = *blank
     c                   eval      c2skid = *blank
     c                   eval      c2sbes = *blank
     c                   eval      c2sfrk = *blank
     c                   eval      c2sdg1 = *blank
     c                   eval      c2sdg2 = *blank
     c                   eval      c2sdg3 = *blank
     c                   eval      c2sdg4 = *blank
     c                   eval      c2sdg5 = *blank
     c                   eval      c2sdg6 = *blank
     c                   eval      c2sdg7 = *blank
     c                   eval      c2sktf = *zero
     c                   eval      c2sktt = *zero
     c                   eval      c2skto = *zero
6.31 c                   eval      c2spes = *zero
     c                   eval      c2styp = *blank
     c                   eval      c2sbkd = *zero
     c                   eval      c2skkd = *zero
6.10 c                   eval      c2some = *blank
6.30 c                   eval      c2sity = *blank
     c     *dmy          move      w_ddat        c2sfra
     c**                 eval      c2sfra = w_ddat
     c                   eval      c2shld = *blank
     c                   eval      c2sdat = *zero
     c                   eval      c2seus = *blank
     c                   eval      c2seda = *zero
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     ruksl1_key    setgt     ruksl1
     c                   readp     ruksl1
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     ruksl1
      *
     c                   if        %eof or
     c                             rusfir <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   enddo
      *
     c                   if        %eof
     c     ruksl1_key    setll     ruksl1
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_bck       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av hardkodet søk  (F1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xf1win1       begsr
      *
     c                   exsr      xf1nul
     c                   eval      f1alt1 = 'VISA Purchasing          '
     c                   eval      f1alt2 = 'Europay Purchasing       '
     c                   eval      f1alt3 = 'Maxbo Kort (DNB)         '
8.00 c                   eval      f1alt4 = 'Santander                '
     c                   eval      f1alt5 = 'BM Kort (DNB)            '
     c                   eval      f1alt6 = 'NBBL Fordelskort         '
     c                   eval      f1alt7 = '                         '
     c                   eval      f1alt8 = '                         '
      *
     c                   exfmt     f1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_f1win1
     c                   endif
      *
     c                   select
     c                   when      f1val1 = '1'
     c                   eval      w_ktyp = 01
     c                   eval      w_ktpn = f1alt1
     c                   when      f1val2 = '1'
     c                   eval      w_ktyp = 02
     c                   eval      w_ktpn = f1alt2
     c                   when      f1val3 = '1'
     c                   eval      w_ktyp = 03
     c                   eval      w_ktpn = f1alt3
     c                   when      f1val4 = '1'
     c                   eval      w_ktyp = 04
     c                   eval      w_ktpn = f1alt4
     c                   when      f1val5 = '1'
     c                   eval      w_ktyp = 05
     c                   eval      w_ktpn = f1alt5
     c                   when      f1val6 = '1'
     c                   eval      w_ktyp = 06
     c                   eval      w_ktpn = f1alt6
     c                   when      f1val7 = '1'
     c                   eval      w_ktyp = 07
     c                   eval      w_ktpn = f1alt7
     c                   when      f1val8 = '1'
     c                   eval      w_ktyp = 08
     c                   eval      w_ktpn = f1alt8
     c                   endsl
      *
     c     end_f1win1    endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å blanke ut hardkodet spørrebilde
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xf1nul        begsr
      *
     c                   eval      f1alt1 = *blank
     c                   eval      f1alt2 = *blank
     c                   eval      f1alt3 = *blank
     c                   eval      f1alt4 = *blank
     c                   eval      f1alt5 = *blank
     c                   eval      f1alt6 = *blank
     c                   eval      f1alt7 = *blank
     c                   eval      f1alt8 = *blank
     c                   eval      f1val1 = *blank
     c                   eval      f1val2 = *blank
     c                   eval      f1val3 = *blank
     c                   eval      f1val4 = *blank
     c                   eval      f1val5 = *blank
     c                   eval      f1val6 = *blank
     c                   eval      f1val7 = *blank
     c                   eval      f1val8 = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sette kort/avtaletype i klartekst (kort)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avt_tp_sh     begsr
      *
     c                   select
     c                   when      ruskty = 1
     c                   eval      b1type = 'VISA Purchasing'
     c                   when      ruskty = 2
     c                   eval      b1type = 'Europay Purchasing'
     c                   when      ruskty = 3
     c                   eval      b1type = 'Maxbo Kort (DNB)'
     c                   when      ruskty = 4
8.00 c                   eval      b1type = 'Santander   '
     c                   when      ruskty = 5
     c                   eval      b1type = 'BM Kort (DNB)'
     c                   when      ruskty = 6
     c                   eval      b1type = 'NBBL Fordelskort'
     c                   other
     c                   eval      b1type = 'Udefinert....'
     c                   endsl
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sette kort/avtaletype i klartekst (full)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avt_tp_fu     begsr
      *
     c                   select
     c                   when      ruskty = 1
     c                   eval      c2ktpn = 'VISA Purchasing'
     c                   when      ruskty = 2
     c                   eval      c2ktpn = 'Europay Purchasing'
     c                   when      ruskty = 3
     c                   eval      c2ktpn = 'Maxbo Kort (DNB)'
     c                   when      ruskty = 4
8.00 c                   eval      c2ktpn = 'Santander   '
     c                   when      ruskty = 5
     c                   eval      c2ktpn = 'BM Kort (DNB)'
     c                   when      ruskty = 6
     c                   eval      c2ktpn = 'NBBL Fordelskort'
     c                   other
     c                   eval      b1type = 'Udefinert....'
     c                   endsl
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      rukslr_skty = d1skty
     c                   eval      rukslr_slag = d1slag
     c     rukslr_key    chain     rukslr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
      * Sletter rad
      *
     c                   eval      rukslu_skty = d1skty
     c                   eval      rukslu_slag = d1slag
     c     rukslu_key    chain     rukslur
      *
     c                   delete    rukslur
      *
     c                   eval      b_forn = *on
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for oppslag på kort/fakt.avtale-register
      *
     c     rukslr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rukslr_skty
     c                   kfld                    rukslr_slag
      *
      * Key for sjekk av bilagskode
      *
     c     ra01l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra01l1_akod
      *
      * Key for sjekk av kunde
      *
     c     ruksl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ruksl1_skty
      *
      * Key for oppdat. av kort/fakt.avtale-register
      *
     c     rukslu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rukslu_skty
     c                   kfld                    rukslu_slag
      *
      * Key for sjekk konto
      *
     c     rhovl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhovl1_kont
     c                   kfld                    rhovl1_spes
     c                   kfld                    rhovl1_avde
6.30  *
6.30  * Key for sjekk av ordreinfotype
6.30  *
6.30 c     vinfl1_key    klist
6.30 c                   kfld                    w_firm
6.30 c                   kfld                    vinfl1_sity
      *
      * Henter parameter, og initierer skjermbildet
      *
     c                   time                    w_ddat
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
      *
      * Sjekker om tilleggsmodul er installert
      *
     c                   eval      w_modu = c_modu
     c                   eval      w_disp = '1'
     c                   eval      w_stat = '0'
     c                   eval      b_tlgx = *on
      *
     c                   call      'AX700R'
     c                   parm                    w_modu
     c                   parm                    w_disp
     c                   parm                    w_stat
      *
     c                   if        w_stat = '1'
     c                   eval      b_tlgx = *off
     c                   endif
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      ruksl1_skty = *zero
     c                   eval      ruksl1_slag = *zero
     c     ruksl1_key    setll     ruksl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
