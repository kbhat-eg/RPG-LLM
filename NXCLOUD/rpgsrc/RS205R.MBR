     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RS205R
      * Beskrivelse . . : Kontroll av gyldig MVA-koder - returnerer satser
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       020100 KOB  Nytt program                                        *
      *       180902 HKO  Skrevet om til ny standard
      *       050104 KOB  Legger ut %-sats fordeling i parameter
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     fra05l1    if   e           k disk    rename(ra05pfr:ra05l1r)
      *
      * Definering av parametre
      *
     d p_stat          s              1
     d p_ekod          s                   like(raekod)
     d p_etxt          s                   like(raetxt)
     d p_dato          s                   like(raemda)
     d p_bpro          s                   like(raebpr)
     d p_mvas          s              6  2
     d p_mvat          s              5  4
     d p_mvaf          s             10  9
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variable i nøkler
      *
     d ra05l1_ekod     s                   like(raekod)
      *
      * Definering av variable
      *
     d w_dato          s               d   datfmt(*iso)
      *
     d w_firm          s                   like(raefir)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Sjekke kode
      *
     c                   eval      ra05l1_ekod = p_ekod                                      .
     c     ra05l1_key    chain     ra05l1r
      *
     c                   if        not %found                                            .....
     c                   eval      p_stat = 'N'                                              .
     c                   eval      p_etxt = 'Koden er ugyldig  '                             .
     c                   goto      avslutt
     c                   endif                                                           .....
      *
      * Finner dagens dato, dersom dato ikke er utfylt
      *
     c                   if        p_dato = *loval
     c                   time                    w_dato
     c                   else
     c                   eval      w_dato = p_dato
     c                   endif
      *
      * Finner hvilken mva-sats som skal benyttes utfra dato
      *
     c                   if        w_dato >= raemda
     c                   eval      p_mvas = raepro
     c                   else
     c                   eval      p_mvas = raegpr
     c                   endif
      *
      * Beregner MVA-faktorer
      *
     c                   eval      p_mvat = (p_mvas + 100) / 100
     c                   eval(h)   p_mvaf = 100 / (p_mvas + 100)
      *
      * Finner hvilken beregnings-% som skal benyttes utfra dato
      *
     c                   if        w_dato >= raepda
     c                   eval      p_bpro = raebpr
     c                   else
     c                   eval      p_bpro = raebgp
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameter
      *
     c     *entry        plist
     c                   parm                    p_stat
     c                   parm                    p_ekod
     c                   parm                    p_etxt
     c                   parm                    p_dato
     c                   parm                    p_mvas
     c                   parm                    p_mvat
     c                   parm                    p_mvaf
     c                   parm                    p_bpro
      *
      * Key for oppslag på kode
      *
     c     ra05l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra05l1_ekod
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
      * Initierer parametre
      *
     c                   eval      p_stat = *blank                                           .
     c                   eval      p_etxt = raetxt                                           .
     c                   eval      p_mvas = 0                                                .
     c                   eval      p_mvat = 1                                                .
     c                   eval      p_mvaf = 1                                                .
      *
     c                   endsr
