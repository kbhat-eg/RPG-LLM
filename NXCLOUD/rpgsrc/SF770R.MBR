     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASSTAT
      * Program . . . . : SF770R
      * Beskrivelse . . : Enhet, Omregning av antall til ny enhet
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       100204 AMD  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vefirm)
     d p_vare          s                   like(vevare)
     d p_enh1          s                   like(veenhe)
     d p_enh2          s                   like(veenhe)
     d p_ant1          s             11  3
     d p_ant2          s             11  3
     d p_ant3          s             11  3
      *
      * Definering av variable i nøkler
      *
     d vvenl1_vare     s                   like(vevare)
     d vvarl1_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vefirm)
     d w_omr1          s                   like(veomre)
     d w_omr2          s                   like(veomre)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Omregning fra enhet 1 til enhet 2
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer ny verdi for antall
      *
     c                   eval      p_ant2 = 0
      *
      * Omregner antall til valgt enhet
      *
     c                   eval      vvenl1_vare = p_vare
     c     vvenl1_key    setll     vvenl1
     c     vvenl1_key    reade     vvenl1
     c                   dow       not %eof
      *
     c                   if        veenhe = p_enh1
     c                   eval      w_omr1 = veomre
     c                   endif
      *
     c                   if        veenhe = p_enh2
     c                   eval      w_omr2 = veomre
     c                   endif
      *
     c     vvenl1_key    reade     vvenl1
     c                   enddo
      *
     c                   if        w_omr2 <> 0
     c                   eval(h)   p_ant2 = p_ant1 * w_omr1 / w_omr2
     c                   endif
      *
      * Omregner antall til nominell m3
      *
     c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   eval(h)   p_ant3 = p_ant2 * vvomrn
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_enh1
     c                   parm                    p_ant1
     c                   parm                    p_enh2
     c                   parm                    p_ant2
     c                   parm                    p_ant3
      *
      * Key for oppslag på vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
      *
      * Key for oppslag på vareregister
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
