      /TITLE  Utplukk til lagerverdiliste
     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : LL608R
      * Beskrivelse . . : Utplukk til lagerverdiliste
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.
      * --------- ------  --------------------------------------------------
      *           010200  Nytt program                             KOB
      *           040102  Lagt inn dummy-parameter bakerst i       ASK
      *                   parameterliste pga. OS/400 feil.
5.20  * R5.20 171003 AMD  Utvidet med mulighet for å kunne kontere på
      *                   leveringstype
5.70  *  PRGR 021208 bof  Utvidet med prisgruppe
6.20  *       271114 bof  Hindre at ikke lagerstyrte kommer på listen
6.30  * 6.30  030117 bsa  Sender med lager til prisprogram.
7.01  *  6.30 130117 bsa  Legger ut varetekst i tilleggsregister. Legger til
7.01  *                   løpenummer for unik nøkkel.
8.01  *PRG2   150622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
8.02  * 800  081124  ask  Lagt inn mulighet for å kalle opp pgm med batchid
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  BRUK AV INDIKATORER
      *
      *       LR = Avslutt program
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer
      *    70-79 = Arbeidsindikatorer i sub-rutiner
      *    80-89 = Arbeidsindikatorer ordinære
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fltell1    if   e           k disk    rename(ltelpfr:ltell1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     flwa1lu    o    e             disk    rename(lwa1pfr:lwa1lur)
7.01 flwa4lu    o    e             disk    rename(lwa4pfr:lwa4lur)
      *
      * Local Data Area
      *
8.02 d lda            uds
7.01 d l_user                911    920
     d l_firm                944    946  0
8.02 d l_batc               1006   1015
      *
      *  Parameter-rec
      *
     d                 ds
     d p_prec                       128
     d  p_fvar                        8  0 overlay(p_prec)
     d  p_tvar                        8  0 overlay(p_prec:9)
     d  p_flag                        2  0 overlay(p_prec:17)
     d  p_tlag                        2  0 overlay(p_prec:19)
     d  p_fogr                        2  0 overlay(p_prec:21)
     d  p_togr                        2  0 overlay(p_prec:23)
     d  p_fhgr                        2  0 overlay(p_prec:25)
     d  p_thgr                        2  0 overlay(p_prec:27)
     d  p_fugr                        3  0 overlay(p_prec:29)
     d  p_tugr                        3  0 overlay(p_prec:32)
     d  p_levn                        6  0 overlay(p_prec:35)
     d  p_sakb                        4    overlay(p_prec:41)
     d  p_sort                        1  0 overlay(p_prec:45)
     d  p_type                        1  0 overlay(p_prec:46)
     d  p_vlin                        1  0 overlay(p_prec:47)
     d  p_batc                       10    overlay(p_prec:48)
5.40 d  p_dath                        6  0 overlay(p_prec:58)
5.40 d  p_gjko                        1  0 overlay(p_prec:64)
5.40 d  p_dumm                       10    overlay(p_prec:65)
      *
      * Parametere for kontohenvisning
      *
     d p_stat          s              1
     d p_otyp          s              2
     d p_ogrp          s              2  0
     d p_hgrp          s              2  0
     d p_ugrp          s              3  0
     d p_vare          s             15
5.20 d p_hele          s            128
6.30 d p_lage          s              2  0
      *
      * Definering av datastruktur
      *
     d                 ds
     d d_dato_8                       8  0
     d  d_aar                         4  0 overlay(d_dato_8)
     d  d_mnd                         2  0 overlay(d_dato_8:5)
     d  d_dag                         2  0 overlay(d_dato_8:7)
      *
     d                 ds
5.20 d d_hele                       128
     d  d_kav1                        4  0 overlay(d_hele)
     d  d_kko1                        6  0 overlay(d_hele:5)
     d  d_ksp1                        4  0 overlay(d_hele:11)
     d  d_kav2                        4  0 overlay(d_hele:15)
     d  d_kko2                        6  0 overlay(d_hele:19)
     d  d_ksp2                        4  0 overlay(d_hele:25)
     d  d_kav3                        4  0 overlay(d_hele:29)
     d  d_kko3                        6  0 overlay(d_hele:33)
     d  d_ksp3                        4  0 overlay(d_hele:39)
     d  d_kav4                        4  0 overlay(d_hele:43)
     d  d_kko4                        6  0 overlay(d_hele:47)
     d  d_ksp4                        4  0 overlay(d_hele:53)
     d  d_kav5                        4  0 overlay(d_hele:57)
     d  d_kko5                        6  0 overlay(d_hele:61)
     d  d_ksp5                        4  0 overlay(d_hele:67)
     d  d_kav6                        4  0 overlay(d_hele:71)
     d  d_kko6                        6  0 overlay(d_hele:75)
     d  d_ksp6                        4  0 overlay(d_hele:81)
     d  d_kav7                        4  0 overlay(d_hele:85)
     d  d_kko7                        6  0 overlay(d_hele:89)
     d  d_ksp7                        4  0 overlay(d_hele:95)
     d  d_khev                        4    overlay(d_hele:99)
5.20 d  d_nivo                        1    overlay(d_hele:103)
      *
      * Key på arbeidsfil
      *
     d                 ds
     d d_mkey                        65
     d  d_mk01                        8  0 overlay(d_mkey)
     d  d_mk01a                       8    overlay(d_mkey)
     d  d_mk02                        8  0 overlay(d_mkey:9)
     d  d_mk02a                       8    overlay(d_mkey:9)
     d  d_mk03                        8  0 overlay(d_mkey:17)
     d  d_mk04                        8  0 overlay(d_mkey:25)
     d  d_mk05                        8  0 overlay(d_mkey:33)
7.01 d  d_mk06                        8  0 overlay(d_mkey:41)
      *
      * Datastrukturer for testing på varegrupper
      *
     d                 ds
     d f_vgrp                         7
     d  f_ogrp                        2  0 overlay(f_vgrp)
     d  f_hgrp                        2  0 overlay(f_vgrp:3)
     d  f_ugrp                        3  0 overlay(f_vgrp:5)
      *
     d                 ds
     d t_vgrp                         7
     d  t_ogrp                        2  0 overlay(t_vgrp)
     d  t_hgrp                        2  0 overlay(t_vgrp:3)
     d  t_ugrp                        3  0 overlay(t_vgrp:5)
      *
     d                 ds
     d v_vgrp                         7
     d  v_ogrp                        2  0 overlay(v_vgrp)
     d  v_hgrp                        2  0 overlay(v_vgrp:3)
     d  v_ugrp                        3  0 overlay(v_vgrp:5)
      *
      * Definering av variable i nøkler
      *
     d ltell1_batc     s                   like(lebatc)
     d ltell1_line     s                   like(leline)
     d ltell1_numm     s                   like(lenumm)
     d ltell1_suff     s                   like(lesuff)
     d vvarl1_vare     s                   like(vvvare)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vogrl1_oogr     s                   like(vgoogr)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vvfirm)
     d w_slbe          s                   like(lenumm)
     d w_parm          s            128
     d w_vare          s              8  0
      *
     d w_dato          s               d   datfmt(*iso)
     d w_mvaf          s             10  9
     d w_mvas          s              6  2
     d w_mvat          s              5  4
     d w_kpny          s              9  2
     d w_spny          s              9  2
     d w_khev          s              5
5.70 d w_avde          s              4  0
8.01 d w_prgr          s              2
7.01 d w_mk06          s              8  0
      *
     d p_firm          s                   like(lefirm)
     d p_lety          s              1  0
     d p_dato          s               d   datfmt(*iso)
     d p_enhe          s              3
     d p_sapr          s              9  2
     d p_gmpr          s              9  2
     d p_kopr          s              9  2
     d p_inpr          s              9  2
      *
      * Definering av midlertidige felt
      *
     d vgopra          s                   like(vgupra)
     d vghpra          s                   like(vgupra)
     d vgolag          s                   like(vgulag)
     d vghlag          s                   like(vgulag)
      *
      *
      *---------------------------------------------------------------
      *  Behandle lagertelling
      *---------------------------------------------------------------
      *
     c                   eval      ltell1_batc = p_batc
     c                   eval      ltell1_numm = *zero
     c                   eval      w_slbe = 999999
      *
     c     ltell1_key    setll     ltell1r
      *
     c     les_lager     tag
      *
     c                   read      ltell1                                 90
     c                   if        *in90 = *on
     c                   goto      end_pgm
     c                   endif
     c                   movel     levare        w_vare
     c                   move      levare        p_vare
      *
      * Sjekk riktig firma
      *
     c                   if        lefirm <> l_firm
     c                   goto      les_lager
     c                   endif
      *
      *  Sjekk batchnummer
      *
     c                   if        lebatc <> ltell1_batc
     c                   goto      end_pgm
     c                   endif
      *
      *  Sjekk listenummer
      *
     c                   if        lenumm > w_slbe
     c                   goto      end_pgm
     c                   endif
      *
      *  Sjekk om listenummer en subliste
      *
     c                   if        lesuff <> *zero
     c                   goto      les_lager
     c                   endif
      *
      * Sjekk varenr. fra - til
      *
     c                   if        w_vare < p_fvar
     c                             or w_vare > p_tvar
     c                   goto      les_lager
     c                   endif
      *
      * Sjekk lagernr. fra - til
      *
     c                   if        lelage < p_flag
     c                             or lelage > p_tlag
     c                   goto      les_lager
     c                   endif
      *
      * Sjekk om varenr. i registeret
      *
     c                   eval      vvarl1_vare = levare
     c     vvarl1_key    chain     vvarl1                             91
     c                   if        *in91 = *on
     c                   goto      les_lager
     c                   endif
      *
      * Sjekk om varegruppe fra - til
      *
     c                   eval      v_ogrp = vvogrp
     c                   eval      v_hgrp = vvhgrp
     c                   eval      v_ugrp = vvugrp
      *
     c                   if        v_vgrp < f_vgrp
     c                             or v_vgrp > t_vgrp
     c                   goto      les_lager
     c                   endif
6.20  *
6.20  * Sjekk om undergruppe skal lagerstyres
6.20  *
6.20 c                   eval      vugrl1_uogr = vvogrp
6.20 c                   eval      vugrl1_uhgr = vvhgrp
6.20 c                   eval      vugrl1_uugr = vvugrp
6.20 c     vugrl1_key    chain     vugrl1                             92
6.20 c                   if        *in92 = *off and
6.20 c                             vgulag = 1
6.20 c                   goto      les_lager
6.20 c                   endif
      *
      * Sjekk om riktig leverandør - hvis leverandør er valgt
      *
     c                   if        p_levn <> *zero
     c                             and p_levn <> vvldor
     c                   goto      les_lager
     c                   endif
      *
      * Sjekk om riktig saksbehandler - hvis saksbehandler er valgt
      *
     c                   if        p_sakb <> *blank
      *
      * Hent saksbehandler fra varegruppe
      *
     c                   eval      vugrl1_uogr = vvogrp
     c                   eval      vugrl1_uhgr = vvhgrp
     c                   eval      vugrl1_uugr = vvugrp
     c     vugrl1_key    chain     vugrl1                             92
     c                   if        *in92 = *off
     c                   endif
      *
     c                   eval      vhgrl1_hogr = vvogrp
     c                   eval      vhgrl1_hhgr = vvhgrp
     c     vhgrl1_key    chain     vhgrl1                             92
     c                   if        *in92 = *off
     c                   endif
      *
     c                   eval      vogrl1_oogr = vvogrp
     c     vogrl1_key    chain     vogrl1                             92
     c                   if        *in92 = *off
     c                   endif
      *
     c                   if        p_sakb <> vgopra and
     c                             p_sakb <> vghpra and
     c                             p_sakb <> vgupra
     c                   goto      les_lager
     c                   endif
      *
     c                   endif
      *
      * Sjekk om antall tellet = 0
      *
     c                   if        leantt = *zeros
     c                   goto      les_lager
     c                   endif
      *
      * Bygg opp sorteringsbegrep
      *
     c                   eval      d_mkey = *blank
      *
     c                   select
     c                   when      p_sort = 1
     c                   eval      d_mk01 = w_vare
     c                   eval      d_mk02 = lelage
     c                   when      p_sort = 2
     c                   eval      d_mk01 = vvogrp
     c                   eval      d_mk02 = vvhgrp
     c                   eval      d_mk03 = vvugrp
     c                   eval      d_mk04 = w_vare
     c                   eval      d_mk05 = lelage
     c                   when      p_sort = 3
     c                   exsr      hnt_konto
     c                   movel     w_khev        d_mk01a
     c                   eval      d_mk02 = lelage
     c                   eval      d_mk03 = w_vare
     c                   when      p_sort = 4
     c                   eval      d_mk01 = lelage
     c                   eval      d_mk02 = w_vare
     c                   when      p_sort = 5
     c                   eval      d_mk01 = lelage
     c                   eval      d_mk02 = vvogrp
     c                   eval      d_mk03 = vvhgrp
     c                   eval      d_mk04 = vvugrp
     c                   eval      d_mk05 = w_vare
     c                   when      p_sort = 6
     c                   exsr      hnt_konto
     c                   eval      d_mk01 = lelage
     c                   movel     w_khev        d_mk02a
     c                   eval      d_mk03 = w_vare
     c                   endsl
7.01  * Legg inn i løpenummer
7.01 c                   eval      w_mk06 = w_mk06 + 1
7.01 c                   eval      d_mk06 = w_mk06
     c                   eval      mlakey = d_mkey
      *
      * Finn riktige priser
      *
5.70 c                   exsr      hent_prisgr
     c                   eval      p_lety = 0
6.30 c                   eval      p_lage = lelage
      *
     c                   call      'VP730R'
     c                   parm                    w_firm
     c                   parm                    p_vare
5.70 c                   parm                    w_prgr
     c                   parm                    p_lety
     c                   parm                    p_dato
     c                   parm                    p_enhe
     c                   parm                    p_sapr
     c                   parm                    p_gmpr
     c                   parm                    p_kopr
     c                   parm                    p_inpr
6.30 c                   parm                    p_lage
      *
      * Oppdater fra vare/pris hvis ikke utfylt
      *
     c                   eval      mlenhl = leenh1
     c                   if        leenh1 = *blank
     c                   eval      mlenhl = vvenhl
     c                   endif
      *
     c                   eval      mlspri = lespny
     c                   if        lespny = *zeros
     c                   eval      mlspri = p_sapr
     c                   endif
      *
     c                   eval      mlkpri = lekpny
     c                   if        lekpny = *zeros
     c                   eval      mlkpri = p_kopr
     c                   endif
      *
     c                   eval      mlipri = p_inpr
      *
      * Beregn lagerverdi
      *
     c****               if        p_type = 0
     c                   eval      mlomst = mlspri * leantt
     c                   eval      mlkost = mlkpri * leantt
     c                   eval      mlinkt = mlipri * leantt
     c****               endif
      *
      * Flytt til arbeidsfil
      *
     c                   eval      mlfirm = lefirm
     c                   movel     levare        mlvare
     c                   eval      mllage = lelage
     c                   eval      mlplas = leplas
     c                   eval      mlpakk = *zeros
     c                   eval      mloord = *zeros
     c                   eval      mlores = *zeros
     c                   eval      mlbbes = *zeros
     c                   eval      mlbres = *zeros
     c                   eval      mlinng = *zeros
     c                   eval      mlutta = *zeros
     c                   eval      mljust = *zeros
     c                   eval      mltell = leantt
     c                   eval      mlsalg = *zeros
     c                   eval      mlretu = *zeros
     c                   eval      mlb101 = *zeros
     c                   eval      mlbehl = leantt
     c                   eval      mlbeve = *zeros
     c                   eval      mlkpny = lekpny
     c                   eval      mlmini = *zeros
     c                   eval      mlmaks = *zeros
     c                   eval      mlltid = *zeros
     c                   eval      mlbpun = *zeros
     c                   eval      mlbmen = *zeros
     c                   eval      mlsikl = *zeros
     c****               eval      mltrda = vltrda
     c****               eval      mlopda = vlopda
     c****               eval      mlsdat = vlsdat
     c                   eval      mlogrp = vvogrp
     c                   eval      mlhgrp = vvhgrp
     c                   eval      mlugrp = vvugrp
     c                   eval      mlkhev = w_khev
      *
     c                   write     lwa1lur
7.01  *
7.01  * Legg ut varetekst - spesielt viktig da diversevarer har
7.01  * overstyrt varetekst.
7.01  *
7.01 c                   eval      l4firm = vvfirm
7.01 c                   eval      l4akey = d_mkey
7.01 c                   eval      l4tek1 = letek1
7.01  *
7.01 c                   eval      l4ousr = l_user
7.01 c                   time                    l4odat
7.01 c                   time                    l4otim
7.01  *
7.01 c                   eval      l4eusr = l_user
7.01 c                   time                    l4edat
7.01 c                   time                    l4etim
7.01 c                   write     lwa4lur
      *
     c                   goto      les_lager
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
      *
     c/eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av kontohenvisning
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hnt_konto     begsr
      *
     c                   eval      p_stat = *blank
     c                   eval      p_otyp = *blank
     c                   eval      p_ogrp = vvogrp
     c                   eval      p_hgrp = vvhgrp
     c                   eval      p_ugrp = vvugrp
     c                   movel     levare        p_vare
      *
     c                   call      'VA720R'
     c                   parm                    p_stat
5.20 c                   parm                    p_lety
     c                   parm                    p_otyp
     c                   parm                    p_ogrp
     c                   parm                    p_hgrp
     c                   parm                    p_ugrp
     c                   parm                    p_vare
     c                   parm                    p_hele
      *
     c                   eval      d_hele = p_hele
     c                   eval      w_khev = d_khev
      *
     c                   endsr
     c/eject
5.70  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5 70  * Subrutine for å hente prisgruppe
5.70  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.70  *
5.70 c     hent_prisgr   begsr
5.70  *
5.70 c                   eval      w_avde = 0
5.70 c                   call      'VL712R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    lelage
5.70 c                   parm                    w_avde
5.70 c                   parm                    w_prgr
5.70  *
5.70 c                   endsr
8.02  *
8.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8 02  * Subrutine for å sette parameterliste ved direkte tilgang
8.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.02  *
8.02 c     bygg_batc     begsr
8.02  *
8.02 c                   eval      p_fogr = 0
8.02 c                   eval      p_fhgr = 0
8.02 c                   eval      p_fugr = 0
8.02 c                   eval      p_togr = 99
8.02 c                   eval      p_thgr = 99
8.02 c                   eval      p_tugr = 999
8.02 c                   eval      p_fvar = 0
8.02 c                   eval      p_tvar = 99999999
8.02 c                   eval      p_flag = 0
8.02 c                   eval      p_tlag = 99
8.02 c                   eval      p_levn = 0
8.02 c                   eval      p_sakb = ' '
8.02 c                   eval      p_sort = 1
8.02 c                   eval      p_vlin = 0
8.02 c                   eval      p_gjko = 0
8.02 c                   eval      p_type = 2
8.02 c                   eval      p_batc = l_batc
8.02 c                   eval      p_dath = 0
8.02 c                   eval      w_parm = p_prec
8.02  *
8.02 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
8.01  *
8.01  * Definisjon av local data area for output
8.01  *
8.01 c     *dtaara       define    *lda          lda
      *
      * Key les av varer
      *
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for les av lagertelling
      *
     c     ltell1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ltell1_batc
     c                   kfld                    ltell1_numm
     c                   kfld                    ltell1_suff
     c                   kfld                    ltell1_line
      *
      * Key for les av undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for laes av hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for les av overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    w_parm
     c                   eval      p_prec = w_parm
      *
      * Legge inn firmanummer
      *
     c                   move      l_firm        w_firm
      *
      * Bygge parameter ved direkte tilgang
      *
8.01 c                   if        l_batc <> ' '
8.01 c                   exsr      bygg_batc
8.01 c                   endif
8.01  *
      *
      * Bygg opp varegryppe fra
      *
     c                   eval      f_ogrp = p_fogr
     c                   eval      f_hgrp = p_fhgr
     c                   eval      f_ugrp = p_fugr
      *
      * Bygg opp varegryppe til
      *
     c                   eval      t_ogrp = p_togr
     c                   eval      t_hgrp = p_thgr
     c                   eval      t_ugrp = p_tugr
      *
      * Sett startpunkt for les
      *
     c***                movel     p_fvar        ltell1_vare
     c***                move      p_flag        ltell1_lage
     c***  ltell1_key    setll     ltell1
      *
      * Sett prisberegningsdato
      *
     c                   if        p_type = 0
     c                   time                    p_dato
     c                   else
     c                   eval      p_dato = *loval
     c                   extrct    p_dato:*d     d_dag
     c                   extrct    p_dato:*m     d_mnd
     c                   time                    p_dato
     c                   extrct    p_dato:*y     d_aar
     c     *ymd          move      d_dato_8      p_dato
     c                   endif
7.01  * Legger inn en startverdi, så det ikke kræsjer med utlegg i LL607
7.01 c                   eval      w_mk06 = 0
      *
     c                   endsr
