     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RR612R                                              *
      * Beskrivelse . . : Beregning av sammenligningstall                     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frrf9l1    if   e           k disk    rename(rrf9pfr:rrf9l1r)
     frrf9l2    if   e           k disk    rename(rrf9pfr:rrf9l2r)
      *
      *+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      *               T   A   B   E   L   L   E   R                      +
      *+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      *
     d sal             s             11  2 dim(14)
      *
      /eject
      *
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      * LOCAL DATA
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     d                uds
     d rhprap                300    301  0
     d rhpkda                         6  0
     d rhprÅr                         4  0
     d rhpper                         2  0
     d rhppef                         2  0
     d rhppet                         2  0
     d rhpavf                         4  0
     d rhpavt                         4  0
     d rhpsrt                         1
     d rhpsml                         1
     d rhpavv                         1
     d rhphel                         1
     d rhplin                         1
     d rhpnul                         1
      *
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    970
      *
     d bev             s                   like(rr9hia)
     d bevp            s                   like(rr9hia)
     d salh            s                   like(rr9hia)
     d sum             s                   like(rr9hia)
     d w_firm          s                   like(rr9fir)
     d w_9avd          s                   like(rr9avd)
     d w_9sln          s                   like(rr9sln)
     d n               s              2  0
      *
     d rrf9l1_9rap     s                   like(rr9rap)
     d rrf9l1_9avd     s                   like(rr9avd)
     d rrf9l1_9lin     s                   like(rr9sln)
     d rrf9l2_9rap     s                   like(rr9rap)
     d rrf9l2_9avd     s                   like(rr9avd)
     d rrf9l2_9lin     s                   like(rr9sln)
      *
     irrf9l1r
      *
      *  Resultatregister - Linjerekkefølge
      *
     i              rr9s01                      sal(01)
     i              rr9s02                      sal(02)
     i              rr9s03                      sal(03)
     i              rr9s04                      sal(04)
     i              rr9s05                      sal(05)
     i              rr9s06                      sal(06)
     i              rr9s07                      sal(07)
     i              rr9s08                      sal(08)
     i              rr9s09                      sal(09)
     i              rr9s10                      sal(10)
     i              rr9s11                      sal(11)
     i              rr9s12                      sal(12)
     i              rr9s13                      sal(13)
     i              rr9s00                      sal(14)
     irrf9l2r
      *
      *  Resultatregister - Avdelingsrekkefølge
      *
     i              rr9s01                      sal(01)
     i              rr9s02                      sal(02)
     i              rr9s03                      sal(03)
     i              rr9s04                      sal(04)
     i              rr9s05                      sal(05)
     i              rr9s06                      sal(06)
     i              rr9s07                      sal(07)
     i              rr9s08                      sal(08)
     i              rr9s09                      sal(09)
     i              rr9s10                      sal(10)
     i              rr9s11                      sal(11)
     i              rr9s12                      sal(12)
     i              rr9s13                      sal(13)
     i              rr9s00                      sal(14)
      *
      /eject
      *
      * Les saldo-records
      *
     c                   select
     c                   when      rhpsrt = ' '
     c                   eval      rrf9l1_9rap = rhprap
     c                   eval      rrf9l1_9lin = w_9sln
     c     rrf9l1_key    setll     rrf9l1r                                                   .
     c     rrf9l1_key    reade     rrf9l1r                                60
     c                   other
     c                   eval      rrf9l2_9rap = rhprap
     c                   eval      rrf9l2_9lin = w_9sln
     c                   eval      rrf9l2_9avd = w_9avd
     c     rrf9l2_key    setll     rrf9l2r                                                   .
     c     rrf9l2_key    reade     rrf9l2r                                60
     c                   endsl
      *
     c                   dow       *in60 = *off
      *
      *  Sjekk type
      *
     c                   if        rr9typ <> 1                                           .....
     c                   goto      neste
     c                   endif
      *
      *  Sjekk regnskapsår
      *
     c                   if        rr9aar <> rhprÅr
     c                   goto      neste
     c                   endif
      *
      * Summer til denne periode og hittil i år
      *
     c                   eval      bev = 0
     c                   eval      sum = 0
      *
     c                   eval      n = 1
      *
     c                   eval      sum = sum + sal(14)                          IB
      *
      * Summer periode fra 1 - til denne periode
      *
     c                   dou       n > rhpper                                            .....
      *                                                                 .
     c                   eval      sum = sum + sal(n)                                        .
      *                                                                 .
      * Summer bevegelse for periode fra  - til                         .
      *                                                                 .
     c                   if        n >= rhppef and                                    .....  .
     c                             n <= rhppet                                            .  .
     c                   eval      bev = bev + sal(n)                                     .  .
     c                   endif                                                        .....  .
      *                                                                 .
     c                   eval      n = n + 1                                                 .
     c                   enddo                                                           .....
      *
      * Summer
      *
     c                   eval      bevp = bevp + bev                                      .  .
     c                   eval      salh = salh + sum                                      .  .
      *
     c     neste         tag
      *
     c                   select
     c                   when      rhpsrt = ' '
     c     rrf9l1_key    reade     rrf9l1r                                60
     c                   other
     c     rrf9l2_key    reade     rrf9l2r                                60
     c                   endsl
      *
     c                   enddo
      *
     c     slutt         tag
      *
     c                   eval      *inlr = *on                                               .
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    w_9avd
     c                   parm                    w_9sln
     c                   parm                    bevp
     c                   parm                    salh
      *
      * Sett startverdier
      *
     c                   move      l_firm        w_firm
      *
      * Key for resultatregister - linje
      *
     c     rrf9l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rrf9l1_9rap
     c                   kfld                    rrf9l1_9lin
      *
      * Key for resultatregister - avdeling
      *
     c     rrf9l2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rrf9l2_9rap
     c                   kfld                    rrf9l2_9avd
     c                   kfld                    rrf9l2_9lin
      *
     c                   endsr
