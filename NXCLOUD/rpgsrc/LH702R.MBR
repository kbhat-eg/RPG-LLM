     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LH702R
      * Beskrivelse . . : Generering av forslag - utplukk og generering
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       140200 ASK  Nytt program
      *       250504 bø   Justert mht. Logistikkprosjekt
      *       010904 bø   Korrigere for anbrekk.
      *       100305 bø   Teste mot enhetsreg. vedr vekt.
      *       150305 bø   Parameter for styre om status brukes
      *                   Midlertidig hardkodet, legge kode på kodereg. v.540
      *       080805 HKO  Henter vekt fra vare-enhet
      *       240805 bø   Kode for å styre status på plass
      *       090307 bø   Utvidet med sats fra best.forsl.parm
      *       150307 bø   Tar aldri best.pkt,min,mm. fra varereg. kun lager
      *       270307 bø   Nytt valg 3 - best.m +/- ordre/best. i ledetid
      *       120607 bø   Legger ut i parm. at best.forslag er dannet
      *       110907 bø   Korrigering av valg 3
5.70  * PRGR  021208 bof  Utvidet med prisgruppe /varetype
      *       300108 bø   Nullstiller best.mengde for sikkerh.skyld
      * 6.10  170609 BSA  Oppdatert med sporingsinformasjon
6.11  * 6.10  040409 bof  Hvis kjedesortiment kjøres, så ny status
6.12  *       260309 bø   Bruker legger selger ut i ref.info hvis det finnes
6.13  *       260309 bø   Det er kostpris på lagerenhet som skal hentes.
6.14  *       190810 bø   Tester <= istf. < på bpunkt - gjør stor forsk
6.14  *       190810 bø   Ved valg 3, gjort om på best.mengde
6.14  *       190810 bø   Ved valg 3, bestm. = bpkt + bmen - disp.beh.
6.15  *       041110 bø   Justert vedr. ledetid og valg 3
6.16  *       010411 bø   Utvidet en beregning for å unngå value too small
6.17  *       150411 bø   Utvidet beregneing av tot.vekt unngpå value too small
6.18  *       150411 bø   Sjekk mot riktig lager ved status 6
6.20  * 6.20  021110 bof  Utvidet med utg.dato pr. lager
6.21  * 6.20  101110 bof  Utvidet med leverandør på lager
6.22  * 6.20  121110 bof  Sjekke om vare er periodevare utenfor periode
6.22  *                   skal ikke på best.forslag da.
6.23  * 6.20  261110 bof  Ordretype hentes fra lev. hvis denne er spesifisert
6.23  *                   Feltet ligger på lev.distribsjon pr. lager
6.24  * 6.20  010411 bsa  Programmet brukes også for test om varer burde
6.24  *                   vært bestilt. Da lages ikke selve bestillings-
6.24  *                   forslaget, men lages ei liste som sendes til
6.24  *                   person på bestillingsparametre som en pdf.
6.24  *                   Parameter om hvilken type kjøring, ligger i data-
6.24  *                   strukturen.
6.25  * 6.20  190511 bø   Utvidet vl710r med div. info
6.26  * 6.20  060312 bsa  Nullstille leverandør ved innhenting av info fra
6.26  *                   leverandørregister.
6.27  * 6.20  060312 bsa  Skal ikke bruke ABC kode telling.
6.28  * 6.20  151113 bsa  Bruker innkjøpspris og ikke kostpris ved ut-
6.28  *                   legg av verdi på bestillingsforslag.
6.29  * 6.20  070415 bof  feilrett.vedr. lev.type, og litt omskr. reade
6.30  * 6.30  050214 bsa  Tar hensyn til erstatningsvarer ved beregning
6.30  *                   av bestillingsforslag.
6.31  * 6.30  270314 bsa  Hvis varen er en erstatningsvare, skal ikke
6.31  *                   denne behandles, hvis den ikke er innenfor
6.31  *                   gyldighetsintervall. Da skal varen den er-
6.31  *                   statter brukes.
6.32  * 6.30  270314 bsa  Statuskode settes hvis erstatningsvare. Den
6.32  *                   skal overstyre alle andre statuser.
6.33  * 6.30  280314 bsa  Hvis "gammel varenummer" ta med behov fra
6.33  *                   "nytt varenummer" hvis utenfor datointervall
6.34  * 6.30  010414 bsa  Hvis nytt varenummer ersatter et annet, og vi
6.34  *                   er i gyldighetsintervallet, ta med beholdning
6.34  *                   til gammelt varenummer også.
6.35  * 6.30  010414 bsa  Hvis "gammelt varenummer", ta med beholdning
6.35  *                   fra nyrr varenummer uansett (Justert 6.33)
6.nu  * 6.30  060614 bof  Utvidelser iforb. med nummerserieutvidelse
6.36  * 6.30  290515 bsa  Erstatningsvare gjelder kun for de med BM_MODUL.
6.37  * 6.30  280415 bof  Henter evt. innk. pris fra kampanje  (6.36 i BM)
6.38  * 6.30  030117 bsa  Sender med lager til prisprogram.
6.39  * 6.30  180618 nho  Regnet ikke anbrekk dersom antall = 0
6.40  * 6.30  050918 nho  Endring av 6.39
6.3b  * 6.30  170918 bkv  Utvidet med trelast sortiment
6.3c  * 6.30  270519 bof  Henter innkj.pris fra evr kampanje. (BM tilp.)
6.3d  * 6.30  170719 bsa  Omrokkere rekkefølge på selektering.
7.01  *K00    110920 bof  Hvis ikke status kjøres, så ikke sett på heading
7.02  *K00    180121 bsa  Sikrer at avrunding til anbrekksmengde ikke
7.02  *K00                medfører bestilling over maks lager
7.03  *       160210 bof  Hvis det ikke finnes parametre,så ikke dann
7.03  *                   bestillingsforslag
8.01  * 800   080621 ask  Lagt inn fjerning av statuskode 6 om bestilling er til ordre
8.01  *                   Styrt av switch 'BESTF_STATUS6'
8.02  *       100221 bof  Switch-styre endr.7.02. Skattum vil ikke ha det lenger
8.03  * 800   100322 ask  Endrer statuskode fra " " til "0" på 8.01
      *                   Legger inn statuskode "0" dersom " " før skriv til LFDTPF.
8.04  *PRG2   150622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
     flodtlv    if   e           k disk    rename(lodtpfr:lodtlvr)
     flohelr    if   e           k disk    rename(lohepfr:lohelrr)
     fvotylr    if   e           k disk    rename(votypfr:votylrr)
     flfhelu    uf a e           k disk    rename(lfhepfr:lfhelur)
     flfdtlu    uf a e           k disk    rename(lfdtpfr:lfdtlur)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
6.23 flldil1    if   e           k disk    rename(lldipfr:lldil1r)
6.24 fmppal1    if   e           k disk    rename(mppapfr:mppal1r)
6.24 fra10l1    if   e           k disk    rename(ra10pfr:ra10l1r)
6.30 fvverl2    if   e           k disk    rename(vverpfr:vverl2r)
6.31 fvverl4    if   e           k disk    rename(vverpfr:vverl4r)
6.30 fvverl1    if   e           k disk    rename(vverpfr:vverl1r)
6.3b fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
6.24 Flh702P    o    e             printer
6.24 f                                     usropn
      *
      *  Definering av datastrukturer
      *
      * Parameter-inn
     d                 ds
     d d_prec                       256
     d  d_flev                        6  0 overlay(d_prec)
     d  d_tlev                        6  0 overlay(d_prec:7)
     d  d_fogr                        2  0 overlay(d_prec:13)
     d  d_fhgr                        2  0 overlay(d_prec:15)
     d  d_fugr                        3  0 overlay(d_prec:17)
     d  d_fvar                       15    overlay(d_prec:20)
     d  d_togr                        2  0 overlay(d_prec:35)
     d  d_thgr                        2  0 overlay(d_prec:37)
     d  d_tugr                        3  0 overlay(d_prec:39)
     d  d_tvar                       15    overlay(d_prec:42)
     d  d_sakb                        4    overlay(d_prec:57)
     d  d_lage                        2  0 overlay(d_prec:61)
     d  d_vval                        1  0 overlay(d_prec:63)
     d  d_sats                        3  0 overlay(d_prec:64)
     d  d_aval                        1  0 overlay(d_prec:67)
     d  d_tdat                        6  0 overlay(d_prec:68)
     d  d_selg                        4  0 overlay(d_prec:74)
     d  d_gvek                       11  3 overlay(d_prec:78)
     d  d_gbel                       11  2 overlay(d_prec:89)
     d  d_numm                        6  0 overlay(d_prec:100)
     d  d_lnum                        3  0 overlay(d_prec:106)
     d  d_kora                        1    overlay(d_prec:109)
6.24 d  d_ndat                       10    overlay(d_prec:245)
6.24 d  d_bgen                        1    overlay(d_prec:256)
      *
      * Parameter-rec utskrift
      *
     d                 ds
     d d_srec                        13
     d  d_sfbes                       6  0 overlay(d_srec)
     d  d_stbes                       6  0 overlay(d_srec:7)
     d  d_ssort                       1  0 overlay(d_srec:13)
      *
      * Parameter-rec batch-programmer
      *
     d                 ds
     d d_brec                        30
     d  d_bvare                       8  0 overlay(d_brec)
     d  d_btdat                       6  0 overlay(d_brec:9)
     d  d_blage                       2  0 overlay(d_brec:15)
     d  d_banta                      11  3 overlay(d_brec:17)
      *
      * Local Data Area
      *
     d                uds
     d l_wsid                901    910
     d l_user                        10
6.3c d l_filg                931    933
     d l_firm                944    946  0
6.24 d l_fnav                951    980
      *
      * Definering av variable i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vvenl1_vare     s                   like(vevare)
     d vvenl1_enhe     s                   like(veenhe)
     d vlagl1_vare     s                   like(vlvare)
6.24 d vlaglr_vare     s                   like(vlvare)
     d vlagl1_lage     s                   like(vllage)
     d lfhelu_numm     s                   like(lhnumm)
     d lfdtlu_numm     s                   like(lfnumm)
     d lfdtlu_line     s                   like(lfline)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vogrl1_oogr     s                   like(vgoogr)
     d rlevl1_levr     s                   like(rllevr)
     d lodtlv_vare     s                   like(ldvare)
     d lohelr_numm     s                   like(lonumm)
     d lohelr_suff     s                   like(losuff)
     d votylr_otyp     s                   like(vaotyp)
6.23 d lldil1_ldor     s                   like(lpldor)
6.23 d lldil1_lage     s                   like(lplage)
6.24 d ra10l1_jkod     s                   like(rajkod)
6.24 d mppal1_ldor     s                   like(mpldor)
6.24 d mppal1_lnum     s                   like(mplnum)
6.30 d vverl2_egvr     s                   like(vvegvr)
6.30 d vverl1_envr     s                   like(vvenvr)
6.30 d vverl1_egvr     s                   like(vvegvr)
6.31 d vverl4_envr     s                   like(vvenvr)
      *
      * Definering av øvrige variable
      *
     d w_firm          s                   like(vvfirm)
     d w_sakb          s                   like(vgupra)
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
6.20 d w_udat          s               d   datfmt(*iso)
     d w_behl          s             11  3
     d w_maks          s             11  3
     d w_mini          s             11  3
     d w_bpun          s             11  3
     d w_bmen          s             11  3
     d w_bmin          s             11  3
     d w_antb          s             11  3
     d w_anto          s             11  3
     d w_anta          s             11  3
     d w_fast          s             10
     d w_fell          s              6
     d w_kode          s              1
6.nu d w_numm          s              8  0
     d w_pnum          s              6
     d w_type          s              2
     d w_parm          s            256
     d w_pars          s             13
     d w_parb          s             30
     d w_ldor          s                   like(lhldor)
6.21 d w_ldorx         s                   like(lhldor)
6.3b d w_lv_nobb       s                   like(vvlnob)
6.3b d w_lv_modn       s                   like(vvlmod)
6.3b d w_lv_lldo       s                   like(vvlnle)
6.3b d w_lv_llva       s                   like(vvllva)
     d w_lsor          s                   like(rlalfa)
     d w_lety          s              1  0
     d w_dato          s               d   datfmt(*iso)
     d w_dat1          s               d   datfmt(*iso)
     d w_enhe          s                   like(lfenh1)
     d w_kopr          s              9  2
6.13 d w_sapr          s              9  2
6.13 d w_gmpr          s              9  2
     d w_totb          s             11  2
6.17 d w_totv          s             15  3
     d w_dbel          s             11  2
6.17 d w_dvek          s             15  3
     d w_110           s             11  0
     d w_112           s             11  2
6.16 d w_152           s             15  2
     d w_inpr          s              9  2
     d w_ltid          s              3  0
     d w_stat          s                   like(lfstat)
5.70 d w_avde          s              4  0
5.70 d w_lage          s                   like(vllage)
8.04 d w_prgr          s              2
5.70 d w_vtyp          s                   like(vvvtyp)
6.12 d w_ikod          s                   like(lhselg)
6.12 d w_itxt          s             30
6.12 d w_ista          s              1
6.24 d w_tell          s              5  0
6.24 d w_flev          s              6  0
6.24 d w_lnum          s              3  0
6.24 d w_ibes          s              9  0
6.24 d w_totl          s             12  3
6.30 d w_erbh          s             11  3
6.36 d w_disp          s              1
6.36 d w_msts          s              1
6.36 d w_modu          s             10
      *
6.24 d p_anti          s             11  3
6.24 d p_antu          s             11  3
6.37 d w_vvar          s                   like(lfvare)
6.37 d w_vldo          s                   like(lhldor)
6.37 d w_vkai          s              9  2
6.38 d p_lage          s              2  0
      *
     d b_feil          s              1    inz(*off)
     d b_sts0          s              1    inz(*off)
     d b_sts1          s              1    inz(*off)
     d b_sts2          s              1    inz(*off)
     d b_sts3          s              1    inz(*off)
     d b_sts4          s              1    inz(*off)
     d b_sts5          s              1    inz(*off)
     d b_sts6          s              1    inz(*off)
     d b_sts7          s              1    inz(*off)
6.11 d b_sts9          s              1    inz(*off)
     d b_dann          s              1
6.11 d b_ksor          s              1    inz(*off)
6.20 d b_inlr          s              1    inz(*off)
6.24 d b_bfor          s              1    inz(*off)
6.24 d b_prnt          s              1    inz(*off)
6.24 d b_abck          s              1    inz(*off)
6.30 d b_repl          s              1    inz(*off)
6.33 d b_grep          s              1    inz(*off)
6.36 d b_bmpp          s              1    inz(*off)
      *
6.24 d qsprilsp        pr                  extpgm('QSPRILSP')
6.24 d rcvvar                     32767a   options(*varsize)
6.24 d rcvvarlen                     10i 0 const
6.24 d format                         8a   const
6.24 d errorcode                  32767a   options(*varsize)
6.24  *
6.24 d sprl0100        ds
6.24 d  bytesrtn                     10i 0
6.24 d  bytesavl                     10i 0
6.24 d  splfname                     10a
6.24 d  jobname                      10a
6.24 d  username                     10a
6.24 d  jobnbr                        6a
6.24 d  splfnbr                      10i 0
6.24 d  systemname                    8a
6.24 d  createdate                    7a
6.24 d                                1a
6.24 d  createtime                    6a
6.24  *
6.24 d errorcode       ds
6.24 d  bytesprov                    10i 0 inz(0)
6.24 d  byteavail                    10i 0 inz(0)
      *
6.3c d u_63c           s              1    inz(*off)                            Endring 6.3c
8.01 d u_801           s              1    inz(*off)
8.02 d u_702           s              1    inz(*off)
6.3c  * Definering av eksterne prg
6.3c  *
6.3c   dcl-s co402_verdi1  char(1);
6.3c   dcl-s co402_verdi2  char(2048);
6.3c   DCL-PR CO402R EXTPGM('CO402R');
6.3c     p_filgrp            char(3)     const;
6.3c     p_firm              packed(3:0) const;
6.3c     p_lager             packed(2:0) const;
6.3c     p_nokkel            char(50)    const;
6.3c     p_verdi1            char(1);
6.3c     p_verdi2            char(2048);
6.3c   END-PR;
      *
6.36 d c_modu          c                   'BM_MODUL  '
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram - leser rader fra vare for utplukks-test
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   read      vvarl1r                                91
      *
     c                   dow       *in91 = *off
      *
      *
     c                   if        vvvare > d_tvar
     c                   goto      les_ferdig
     c                   endif
      *
     c                   exsr      sjekk_utvalg
     c                   if        b_feil = *on
     c                   goto      ny_les
     c                   endif
      *
     c                   exsr      sjekk_lager
     c                   if        b_feil = *on
     c                   goto      ny_les
     c                   endif
      *
     c                   exsr      sjekk_bestill
     c                   if        b_feil = *on
     c                   goto      ny_les
     c                   endif
      *
     c                   exsr      skriv_forslag
      *
     c     ny_les        tag
     c                   read      vvarl1r                                91
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Utlegg av forslagshode og utskrift dersom detaljer er lagt ut
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_ferdig    tag
      *
     c                   if        lfdtlu_line > 0
6.23  *
6.23  * finner evt. overstyrt ordretype
6.23  *
6.23 c                   eval      lpotyp = *blank
6.23 c                   eval      lldil1_ldor = d_flev
     c                   move      d_lage        lldil1_lage
6.23 c     lldil1_key    chain     lldil1
      *
     c     lfhelu_key    chain     lfhelur                            94
6.12  *
6.12  * finner selger info
6.12 c                   eval      w_itxt = *blank
6.12 c                   if        d_selg <> 0
6.12 c                   eval      w_ista = *blank
6.12 c                   eval      w_ikod = d_selg
6.12 c                   call      'RS709R  '
6.12 c                   parm                    w_ista
6.12 c                   parm                    w_ikod
6.12 c                   parm                    w_itxt
6.12 c                   endif
      *
6.12 c                   if        w_itxt <> *blank
6.12 c                   movel     w_itxt        lhuser
6.12 c                   else
     c                   eval      lhuser = l_user
6.12 c                   endif
      *
     c                   eval      lhwsid = l_wsid
     c                   eval      lhdate = w_date
     c                   eval      lhtime = w_time
6.23 c                   if        lpotyp <> *blank
6.23 c                   eval      lhotyp = lpotyp
6.23 c                   else
     c                   eval      lhotyp = laaoty
6.23 c                   endif
     c                   eval      lhbdat = w_date
     c                   eval      lhlage = d_lage
     c                   eval      lhldor = d_flev
     c                   eval      lhselg = d_selg
     c                   eval      lhlnum = d_lnum
     c                   eval      lhsats = d_sats
6.17 c                   if        w_totv < 99999999,999
     c                   eval      lhvekt = w_totv
6.17 c                   else
6.17 c                   eval      lhvekt = 99999999,999
6.17 c                   endif
     c                   eval      lhtbel = w_totb
     c                   eval      lhoppr = 'A'
     c                   eval      w_ldor = lhldor
     c                   exsr      les_ldor
     c                   movel     w_lsor        lhlsor
      *
     c                   exsr      finn_stsh
      *
     c                   if        *in94 = *on
     c                   eval      lhfirm = w_firm
     c                   eval      lhnumm = lfhelu_numm
6.10 c                   time                    lhdate
6.10 c                   time                    lhtime
6.10 c                   eval      lhousr = l_user
6.10 c                   time                    lhedat
6.10 c                   time                    lhetim
6.10 c                   eval      lheusr = l_user
6.24 c                   if        b_bfor = *on
     c                   write     lfhelur
6.24 c                   endif
     c                   endif
     c                   move      lhnumm        w_pnum
      *
      * skal ikke ha utskrift hver gang
     c*                  eval      d_numm = lfhelu_numm
     c*                  eval      d_sfbes= w_numm
     c*                  eval      d_stbes= w_numm
     c*                  eval      d_ssort= 0
     c*                  eval      w_pars = d_srec
     c*                  call      'LH601C'
     c*                  parm                    w_pars
      *
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xslutt        tag
6.20  *   setter lr i subprogrammer
6.26 c                   eval      w_ldorx = 0
6.20 c                   eval      b_inlr = *off
6.25 c                   call      'VL710R'
5.25 c                   parm                    w_firm
5.25 c                   parm                    vvvare
5.25 c                   parm                    w_lage
5.25 c                   parm                    w_vtyp
6.25 c                   parm                    w_udat
6.25 c                   parm                    w_ldorx
6.25 c                   parm                    b_inlr
6.3b c                   parm                    w_lv_nobb
6.3b c                   parm                    w_lv_modn
6.3b c                   parm                    w_lv_lldo
6.3b c                   parm                    w_lv_llva
6.21 c                   eval      w_ista = *blank
6.21 c                   call      'VD772R  '
6.21 c                   parm                    w_firm
6.21 c                   parm                    w_lage
6.21 c                   parm                    vvvare
6.21 c                   parm                    w_ista
6.21 c                   parm                    b_inlr
6.3c c                   if        u_63c = *on
6.3c c                   call      'VT592R  '
6.3c c                   parm                    w_firm
6.3c c                   parm                    vvvare
6.3c c                   parm                    w_enhe
6.3c c                   parm                    w_vkai
6.3c c                   parm                    b_inlr
6.3c c                   else
6.37 c                   call      'JM592R  '
6.37 c                   parm                    w_firm
6.37 c                   parm                    vvvare
6.37 c                   parm                    w_vldo
6.37 c                   parm                    w_vkai
6.37 c                   parm                    w_enhe
6.37 c                   parm                    b_inlr
     c                   endif

      *
6.24  * Generer xml for videre utskrift
6.24 c                   if        b_prnt = *on
6.24 c                   eval      t1post = w_tell
6.24 c                   write     t1tot
6.24  *
6.24 c                   exsr      spoolnbr
6.24 c                   exsr      xml_create
6.24 c                   close     lh702p
6.24 c                   endif
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sjekker om varenr er innenfor utvalg
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_utvalg  begsr
      *
     c                   eval      b_feil = *off
      *
      *  Sjekk   Er dette riktig firma ?                                    ost
      *
     c                   if        vvfirm <> l_firm
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      *  Sjekk   Er dette en lagervare ?                                    ost
      *
5.70  *
6.26 c                   eval      w_ldorx = 0
6.25 c                   eval      b_inlr = *on
6.25 c                   call      'VL710R'
6.25 c                   parm                    w_firm
6.25 c                   parm                    vvvare
6.25 c                   parm                    w_lage
6.25 c                   parm                    w_vtyp
6.25 c                   parm                    w_udat
6.25 c                   parm                    w_ldorx
6.25 c                   parm                    b_inlr
6.3b c                   parm                    w_lv_nobb
6.3b c                   parm                    w_lv_modn
6.3b c                   parm                    w_lv_lldo
6.3b c                   parm                    w_lv_llva
      *
5.70 c*                  if        w_vtyp <> 'L'
     c*                  eval      b_feil = *on
     c*                  leavesr
     c*                  endif
      *
      *  Sjekk   Er dette en vare som kan bestilles
      *
     c                   if        vvgodj = 1
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      *  Sjekk   Er dette en vare som er utgått
6.20  *
6.20 c                   if        w_udat <> *loval and
6.20 c                             w_udat <= w_date
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      * Er leverandørnummer innenfor grensene ?                             ost
      *
6.21 c                   if        w_ldorx < d_flev or
6.12 c                             w_ldorx > d_tlev
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      * Er varegrupper innenfor grensene ?
      *
     c                   if        vvogrp < d_fogr or
     c                             vvogrp > d_togr
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   if        vvhgrp < d_fhgr or
     c                             vvhgrp > d_thgr
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   if        vvugrp < d_fugr or
     c                             vvugrp > d_tugr
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      * Seleksjon på innkjøper/saksbehandler?
      *
     c                   eval      w_sakb = *blank
     c                   if        d_sakb <> *blank
     c                   eval      vogrl1_oogr = vvogrp
     c     vogrl1_key    chain     vogrl1                             95
      *
     c                   if        *in95 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   else
     c                   if        vgopra <> *blank
     c                   eval      w_sakb = vgopra
     c                   endif
     c                   endif
      *
     c                   eval      vhgrl1_hogr = vvogrp
     c                   eval      vhgrl1_hhgr = vvhgrp
     c     vhgrl1_key    chain     vhgrl1                             95
      *
     c                   if        *in95 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   else
     c                   if        vghpra <> *blank
     c                   eval      w_sakb = vghpra
     c                   endif
     c                   endif
      *
     c                   eval      vugrl1_uogr = vvogrp
     c                   eval      vugrl1_uhgr = vvhgrp
     c                   eval      vugrl1_uugr = vvugrp
     c     vugrl1_key    chain     vugrl1                             95
      *
     c                   if        *in95 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   else
     c                   if        vgupra <> *blank
     c                   eval      w_sakb = vgupra
     c                   endif
     c                   endif
      *
     c                   if        w_sakb <> d_sakb
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
6.21  *
6.38  * Flyttet som siste sjekk - start
6.21  * Sjekk   Er vare en periodevare utenfor periode
6.21  *
6.20 c                   eval      b_inlr = *on
6.21 c                   eval      w_ista = *blank
6.21 c                   call      'VD772R  '
6.21 c                   parm                    w_firm
6.21 c                   parm                    w_lage
6.21 c                   parm                    vvvare
6.21 c                   parm                    w_ista
6.21 c                   parm                    b_inlr
6.21 c                   if        w_ista = '1'
6.21 c                   eval      b_feil = *on
6.21 c                   leavesr
6.21 c                   endif
6.38  * Flyttet som siste sjekk - slutt
6.36  * Skal ikke sjekke mot erstatningsvarer hvis ikke BM modul
6.36 c                   if        b_bmpp = *on
6.30  * Hvis vare er erstattet av andre varer, skal det ikke dannes
6.30  * bestillingsforslag på denne.
6.30 c                   eval      vverl2_egvr = vvvare
6.30 c     vverl2_key    chain     vverl2
6.30 c                   if        %found           and
6.30 c                             w_date >= vvfidt and
6.30 c                             w_date <= vvsidt
6.30 c                   eval      b_feil = *on
6.30 c                   leavesr
6.30 c                   endif
6.31  *
6.31  * Hvis vare er erstatningsvare for en annen, og vi er utenfor
6.31  * gyldighetsperioden, skal den ikke bestilles. Da skal "gammel"
6.31  * varenummer bestilles. Vi forutsetter at dette skjer gjennom
6.31  * bestillingsforslag på denne.
6.31  *
6.31 c                   eval      vverl4_envr = vvvare
6.31 c     vverl4_key    chain     vverl4
6.31 c                   if        %found
6.31 c                   if        w_date < vvfidt or
6.31 c                             w_date > vvsidt
6.31 c                   eval      b_feil = *on
6.31 c                   leavesr
6.31 c                   endif
6.31 c                   endif
      *
6.36 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sjekker og henter lagerinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_lager   begsr
      *
     c                   eval      b_feil = *off
      *
     c                   eval      vlagl1_vare = vvvare
     c                   eval      vlagl1_lage = d_lage
     c     vlagl1_key    chain     vlagl1r                            92
     c                   if        *in92 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
5.70 c                   if        vlvtyp <> 'L'
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
7.03  *
7.03  * Hvis det ikke finnes innkjøpsparametre - går den videre
7.03  *
7.03 c                   if        vlmini = 0 and
7.03 c                             vlmaks = 0 and
7.03 c                             vlbmen = 0 and
7.03 c                             vlbpun = 0
7.03 c                   eval      b_feil = *on
7.03 c                   leavesr
7.03 c                   endif
6.24  * Er det riktig ABC kode som skal behandles
6.24 c                   if        b_bfor = *off
6.24 c                   if        b_abck = *on
6.24 c                   exsr      sjekk_abc
6.24 c                   if        b_feil = *on
6.24 c                   leavesr
6.24 c                   endif
6.24 c                   endif
6.24 c                   endif
      *
      * Dato avgrensing valgt?
      * Hvis aval = 3 - så bestemmes tildato ut fra ledetid
      * ellers evt. tidshorisont.
      *
6.15  * Denne er fjernet i forb.med valg 3 i std. som
6.15  * hos Herføll
6.15 c*                  if        d_aval = 3 and
6.15 c*                            vlltid > 0
6.15 c*                  eval      w_ltid = vlltid
6.15 c*                  time                    w_dat1
6.15 c*                  adddur    w_ltid:*D     w_dat1
6.15 c*    *dmy          move      w_dat1        d_tdat
6.15 c*                  eval      d_btdat = d_tdat
6.15 c*                  endif
6.30  * Hvis dette er en erstatninsgvare, ta høyde for beholdning
6.30  * for vare den erstatter, før behovet beregnes. NB I teorien
6.30  * kan dette være flere varer.
6.30 c                   eval      b_repl = *off
6.30 c                   eval      w_erbh = 0
6.36  *
6.36  * Kun sjekkes hvis BM modul
6.36  *
6.36 c                   if        b_bmpp = *on
6.36  *
6.30 c                   eval      vverl1_envr = vvvare
6.30 c                   eval      vverl1_egvr = *blanks
6.30 c     vverl1_key    setll     vverl1
6.30 c     vverl1_key2   reade     vverl1
6.30 c                   dow       not %eof
6.30 c                   if        w_date >= vvfidt and
6.30 c                             w_date <= vvsidt
6.30 c                   exsr      finn_beh
6.30 c                   endif
6.30 c     vverl1_key2   reade     vverl1
6.30 c                   enddo
6.33  *
6.33  * Hvis dette er en "gammel vare", og vi er utenfor datointervall
6.33  * for at "nytt varenummer" beregnes, ta med beholdning for "nytt
6.33  * varenummer" i beregning av behov.
6.33  *
6.33 c                   eval      b_grep = *off
6.33 c                   eval      w_erbh = 0
6.33 c                   eval      vverl2_egvr = vvvare
6.33 c     vverl2_key    chain     vverl2
6.33 c                   if        %found
6.35 c**                 if        w_date < vvfidt or
6.35 c**                           w_date > vvsidt
6.33 c                   exsr      finn_beh2
6.35 c**                 endif
6.33 c                   endif
6.34  *
6.34  * Hvis dette er en "nytt varenummer, og vi er innenfor intervall
6.34  * for at "nytt varenummer" er erstatningsvare, ta med beholdning
6.34  * for gammelt varenummer" i beregning av behov.
6.34  *
6.35 c                   if        b_grep = *off
6.35 c**                 eval      b_grep = *off
6.34 c                   eval      w_erbh = 0
6.34 c                   eval      vverl4_envr = vvvare
6.34 c     vverl4_key    chain     vverl4
6.34 c                   if        %found
6.34 c                   if        w_date >= vvfidt and
6.34 c                             w_date <= vvsidt
6.34 c                   exsr      finn_beh3
6.34 c                   endif
6.34 c                   endif
6.35 c                   endif
6.30  *
6.30  * "Legger tilbake" vare og lager info, da vi er innom lager i
6.30  * loopen over.
6.30  *
6.33 c                   if        b_repl = *on or
6.33 c                             b_grep = *on
6.30 c                   eval      vlagl1_vare = vvvare
6.30 c                   eval      vlagl1_lage = d_lage
6.30 c     vlagl1_key    chain     vlagl1r                            92
6.30 c                   endif
6.36  *
6.36 c                   endif
      *
     c                   if        d_tdat > 0
6.15 c                   eval      d_btdat = d_tdat
     c                   movel     vvvare        d_bvare
      *
     c                   eval      d_banta = 0
     c                   eval      w_parb = d_brec
     c                   call      'LH708R'
     c                   parm                    w_parb
     c                   eval      d_brec = w_parb
     c                   eval      w_antb = d_banta
      *
     c                   eval      d_banta = 0
     c                   eval      w_parb = d_brec
     c                   call      'LH709R'
     c                   parm                    w_parb
     c                   eval      d_brec = w_parb
     c                   eval      w_anto = d_banta
      *
     c                   eval(h)   w_behl = vlbehl + w_antb -
     c                                      vlpakk - w_anto
      *
     c                   else
     c                   eval(h)   w_behl = vlbehl + vlbbes -
     c                                      vlpakk - vloord
     c                   endif
6.30  * Hvis erstatningsvare, husk å ta med beholdning fra "gamle"
6.30  * varer.
6.36  * Sjekkes kun hvisa BM modul
6.36 c                   if        b_bmpp = *on
6.33 c                   if        b_repl = *on or
6.33 c                             b_grep = *on
6.30 c                   eval      w_behl = w_behl + w_erbh
6.30 c                   endif
6.36 c                   endif
      *
      * Verdier fra lager eller vare?
      * fom 5.50 tas kun verdier fra lager
      *
     c                   eval      w_mini = vlmini
      *
     c                   eval      w_maks = vlmaks
      *
     c                   eval      w_bpun = vlbpun
     c                   eval      w_bmen = 0
      *
     c                   if        d_aval = 3
6.14  * beregner bestmengde + ant.ordre - ant. best. - lagerbeh.
6.14 c                   eval      w_bmen = vlbpun + vlbmen
6.14 c                                             - w_behl
     c                   else
     c                   eval      w_bmen = vlbmen
     c                   endif
      *
     c                   eval      w_bmin = vlbmin
      *
     c
      *
      * Skal verdier økes med prosentsats?
      *
     c                   if        d_sats > 0
     c                   eval(h)   w_mini = w_mini +
     c                                     %inth(((w_mini * d_sats)/100))
     c                   eval(h)   w_bpun = w_bpun +
     c                                     %inth(((w_bpun * d_sats)/100))
     c                   endif
      *
     c                   endsr
      *
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30  *  Sjekker behov og beregner antall i bestilling
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30  *
6.30 c     finn_beh      begsr
6.30  *
6.30 c                   eval      b_repl = *on
6.30  *
6.30 c                   eval      vlagl1_vare = vvegvr
6.30 c                   eval      vlagl1_lage = d_lage
6.30 c     vlagl1_key    chain     vlagl1r
6.30 c                   if        %found
6.30 c                   eval(h)   w_behl = vlbehl + vlbbes -
6.30 c                                      vlpakk - vloord
6.30 c                   eval      w_behl = w_behl * vvomre
6.30 c                   endif
6.30  *
6.30 c                   eval      w_erbh = w_erbh + w_behl
6.30  *
6.30 c                   endsr
6.33  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.33  *  Sjekker behov og beregner antall i bestilling
6.33  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.33  *
6.33 c     finn_beh2     begsr
6.33  *
6.33 c                   eval      b_grep = *on
6.33  *
6.33 c                   eval      vlagl1_vare = vvenvr
6.33 c                   eval      vlagl1_lage = d_lage
6.33 c     vlagl1_key    chain     vlagl1r
6.33 c                   if        %found
6.33 c                   eval(h)   w_behl = vlbehl + vlbbes -
6.33 c                                      vlpakk - vloord
6.33 c                   eval      w_behl = w_behl * vvomre
6.33 c                   endif
6.33  *
6.33 c                   eval      w_erbh = w_erbh + w_behl
6.33  *
6.33 c                   endsr
6.34  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.34  *  Sjekker behov og beregner antall i bestilling
6.34  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.34  *
6.34 c     finn_beh3     begsr
6.34  *
6.34 c                   eval      b_grep = *on
6.34  *
6.34 c                   eval      vlagl1_vare = vvegvr
6.34 c                   eval      vlagl1_lage = d_lage
6.34 c     vlagl1_key    chain     vlagl1r
6.34 c                   if        %found
6.34 c                   eval(h)   w_behl = vlbehl + vlbbes -
6.34 c                                      vlpakk - vloord
6.34 c                   eval      w_behl = w_behl * vvomre
6.34 c                   endif
6.34  *
6.34 c                   eval      w_erbh = w_erbh + w_behl
6.34  *
6.34 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sjekker behov og beregner antall i bestilling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_bestill begsr
      *
     c                   eval      b_feil = *on
     c                   eval      w_anta = 0
      *
      * Sjekk bestillingsbehov etter metode
      *
     c                   eval      w_stat = *blank
      *
     c                   select
     c                   when      d_vval = 3
     c                   eval      b_feil = *off
     c                   when      d_vval = 2 and
     c                             w_bpun > 0 and
6.14 c                             w_behl <=w_bpun
     c                   eval      w_stat = '2'
     c                   eval      b_feil = *off
     c                   eval      b_sts2 = *on
     c                   when      d_vval = 1 and
     c                             w_behl < w_mini
     c                   eval      w_stat = '1'
     c                   eval      b_feil = *off
     c                   eval      b_sts1 = *on
     c                   when      d_vval = 0 and
     c                             w_behl <= 0
     c                   eval      w_stat = '0'
     c                   eval      b_feil = *off
     c                   eval      b_sts0 = *on
     c                   endsl
      *
      * hvis cross docking vare - så settes egen status for det, overstyrer alt
      *
     c                   if        vvcrdo = 1
     c                   eval      w_stat = '7'
     c                   eval      b_sts7 = *on
     c                   eval      b_feil = *on
     c                   endif
      *
      * Hvis status ikke er bestemt allerede, så bestemmes evt. 1 - 3
      *
     c                   if        b_feil = *off   and
     c                             w_stat = *blank
     c                   select
     c                   when      w_behl <= 0
     c                   eval      w_stat = '0'
     c                   eval      b_sts0 = *on
     c                   when      w_behl < w_mini
     c                   eval      w_stat = '1'
     c                   eval      b_sts1 = *on
     c                   when      w_behl < w_bpun
     c                   eval      w_stat = '2'
     c                   eval      b_sts2 = *on
     c                   endsl
     c                   endif
      *
      * Fast best.kvant eller til maks.beholdning?
      *
     c                   if        b_feil = *off
     c                   if        d_aval = 1 or
     c                             d_aval = 3
     c                   eval      w_anta = w_bmen
     c                   endif
     c                   if        d_aval = 2
     c                   eval      w_anta = w_maks - w_behl
     c                   endif
      * korrigere for anbrekk
     c                   if        d_kora = '1' and
     c                             w_bmin <> 0  and
     c                             w_anta <> 0
     c                   eval      w_112  = w_anta / w_bmin
     c                   move      w_112         w_20              2 0
6.40 c                   move      w_112         w_42              4 2
6.39 c                   if        w_anta > 0 and
6.39 c**6.40                       w_20 = 0
6.40 c                             w_42 = 0
6.39 c                   z-add     1             w_20
6.39 c                   z-add     1             w_anta
6.39 c                   endif
     c                   if        w_20 > 0
     c                   eval      w_110  = w_anta / w_bmin
7.02  *
7.02  * Skattum spes - plusser ikke på en i bestillingsmengde beregning
7.02  *
7.02 c*                  eval      w_anta = w_bmin * (w_110 + 1)
8.02 c                   if        u_702 = *on
7.02 c                   if        w_110 = 0
7.02 c                   eval      w_anta = w_bmin * (w_110 + 1)
7.02 c                   else
7.02 c                   eval      w_anta = w_bmin * (w_110)
7.02 c                   endif
8.02 c                   else
8.02 c                   eval      w_anta = w_bmin * (w_110 + 1)
8.02 c                   endif
     c                   endif
     c                   endif
     c                   endif
     c                   if        w_anta <= 0
     c                   eval      b_feil = *on
     c                   endif
      *
      * Her sjekkes det for status 4 - 6.
      *
     c                   if        b_feil = *off
     c                   if        w_bmen <> 0 and
     c                             w_anta <> w_bmen
     c                   eval      w_stat = '4'
     c                   eval      b_sts4 = *on
     c                   endif
     c                   if        w_bmin <> 0
     c                   eval      w_112  = w_anta / w_bmin
     c                   move      w_112         w_20              2 0
     c                   if        w_20 > 0
     c                   eval      w_stat = '5'
     c                   eval      b_sts5 = *on
     c                   endif
     c                   endif
6.11  *
6.11  * Her sjekkes det for status9
6.11  *
6.11 c                   if        b_feil = *off and
6.11 c                             b_ksor = *on  and
6.11 c                             vvsort = *blank
6.11 c                   eval      w_stat = '9'
6.11 c                   eval      b_sts9 = *on
6.11 c                   endif
      *
      * sjekk mot bestilling /restleveranse
     c                   exsr      sjk_best
     c                   endif
6.30  * Sett status hvis erstaningsvare, og ingen andre koder
6.36  * Kun sjekk vhsi BM modul
6.36 c                   if        b_bmpp = *on
6.30 c                   if        b_repl = *on
6.32 c**                 if        w_stat = ' ' or
6.32 c**                           w_stat = '0'
6.30 c                   eval      w_stat = 'E'
6.32 c**                 endif
6.30 c                   endif
6.36 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Legg ut linje i bestillingsforslag
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_forslag begsr
      *
     c                   exsr      finn_pris
      *
     c                   eval      lfdtlu_line = lfdtlu_line + 2
     c     lfdtlu_key    chain     lfdtlur                            93
      *
     c                   eval      lflage = d_lage
     c                   eval      lfldor = d_flev
6.21 c                   eval      lfvlev = w_ldorx
     c                   eval      lfogrp = vvogrp
     c                   eval      lfhgrp = vvhgrp
     c                   eval      lfugrp = vvugrp
     c                   eval      lfvare = vvvare
6.3b c                   if        w_lv_nobb > 0
6.3b c                   eval      lflvar = w_lv_llva
6.3b c                   else
     c                   eval      lflvar = vvlvar
6.3b c                   endif
      *
     c                   eval      lfenh1 = vvenhl
     c                   eval      lftek1 = vvtek1
     c                   eval      lftek2 = vvtek2
      *
     c                   eval      lfanta = w_anta
     c                   eval      lflbel = w_kopr
     c                   if        laabst = 1
     c                   eval      lfstat = w_stat
     c                   else
     c                   eval      lfstat = '0'
     c                   endif
6.16 c                   eval(h)   w_152  = w_kopr * w_anta
6.16 c                   if        w_152 < 99999999,99
6.16 c                   eval      w_totb = w_totb + w_152
6.16 c                   else
6.16 c                   eval      w_totb = 99999999,99
6.16 c                   endif
      *
      * Henter vekt på enhet
      *
     c                   eval      vevekt = 0
      *
     c                   eval      vvenl1_vare = vvvare
     c                   eval      vvenl1_enhe = vvenhl
     c     vvenl1_key    chain     vvenl1
      *
     c                   eval      lfvekt = vevekt
     c                   eval(h)   w_totv = w_totv + (vevekt * w_anta)
8.03  *
8.03  * Sjekker om status er blank - legger inn 0
8.03  *
8.03 c                   if        lfstat = ' '
8.03 c                   eval      lfstat = '0'
8.03 c                   endif
      *
      * Skriver til bestillingsforslag-linje
      *
     c                   if        *in93 = *on
     c                   eval      lffirm = w_firm
     c                   eval      lfnumm = lfdtlu_numm
     c                   eval      lfline = lfdtlu_line
6.10 c                   time                    lfodat
6.10 c                   time                    lfotim
6.10 c                   eval      lfousr = l_user
6.10 c                   time                    lfedat
6.10 c                   time                    lfetim
6.10 c                   eval      lfeusr = l_user
6.24 c                   if        b_bfor = *on
     c                   write     lfdtlur
6.24 c                   endif
     c                   endif
      *
     c                   eval      b_dann = *on
6.24  * Vi har funnet ei linje som burde vært på bestillingsforslag. Den
6.24  * må skrives.
6.24 c                   if        b_bfor = *off
6.24 c                   exsr      prt_line
6.24 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Legg ut linje i bestillingsforslag
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_pris     begsr
      *
      *
     c                   eval      w_lety = 0
     c                   if        d_tdat <>0
     c     *dmy          move      d_tdat        w_dato
     c                   else
     c                   time                    w_dato
     c                   endif
      *
6.38 c                   eval      p_lage = d_lage
      *
6.13 c                   call      'VP730R'
     c                   parm                    w_firm
     c                   parm                    vvvare
5.70 c                   parm                    w_prgr
     c                   parm                    w_lety
     c                   parm                    w_dato
     c                   parm                    w_enhe
6.13 c                   parm                    w_sapr
6.13 c                   parm                    w_gmpr
     c                   parm                    w_kopr
     c                   parm                    w_inpr
6.38 c                   parm                    p_lage
6.28  * Bruk innkjøpspris, hvis ikke finnes bruk kost
6.28  *
6.28 c                   if        w_inpr <> 0
6.28 c                   eval      w_kopr = w_inpr
6.28 c                   endif
      *
6.37  * sjekker om varen har kampanje og velger evt. kampanjens innk.pris
6.37  *
6.37 c                   eval      w_vkai = 0
6.37 c                   eval      w_vvar = vvvare
6.3c c                   if        u_63c = *on
6.3c c                   eval      b_inlr = *on
6.3c c                   call      'VT592R  '
6.3c c                   parm                    w_firm
6.3c c                   parm                    w_vvar
6.3c c                   parm                    w_enhe
6.3c c                   parm                    w_vkai
6.3c c                   parm                    b_inlr
6.3c c                   eval      b_inlr = *off
6.3c c                   else
6.37 c                   eval      w_vldo = d_flev
6.37 c                   eval      w_enhe = *blank
6.37 c                   call      'JM592R  '
6.37 c                   parm                    w_firm
6.37 c                   parm                    w_vvar
6.37 c                   parm                    w_vldo
6.37 c                   parm                    w_vkai
6.37 c                   parm                    w_enhe
6.37 c                   parm                    b_inlr
6.3c c                   endif
6.37 c                   if        vvenhl <> w_enhe
6.37 c                   eval      w_vkai = 0
6.37 c                   endif
6.37 c                   if        w_vkai <> 0
6.37 c                   eval      w_kopr = w_vkai
6.37 c                   endif
6.37  *
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Henter/Oppdatering av NUMMER-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hntnum        begsr
      *
     c                   eval      w_numm = 0
     c                   eval      w_kode = '0'
     c                   eval      w_fell = laafor
     c                   eval      w_type = *blank
     c                   eval      w_fast = *blank
      *
      *  Kaller opp subprogram for henting av nummer fra NUMMER-REGISTER
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
     c                   parm                    w_numm
      *
     c                   eval      lfhelu_numm = w_numm
     c                   eval      lfdtlu_numm = w_numm
     c                   eval      lfdtlu_line = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for lesing av leverandør
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_ldor      begsr
      *
     c                   eval      b_feil = *off
     c                   eval      rlevl1_levr = w_ldor
     c     rlevl1_key    chain     rlevl1                             91
     c                   if        *in91 = *off
     c                   eval      w_lsor = rlalfa
     c                   else
     c                   eval      w_lsor = *blank
     c                   endif
      *
     c                   endsr
      *
6.24  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.24  * Subrutine for lesing av lager
6.24  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.24  *
6.24 c     les_lager     begsr
6.24  *
6.24 c                   eval      ra10l1_jkod = a1lage
6.24 c     ra10l1_key    chain     ra10l1                             91
6.24 c                   if        *in91 = *off
6.24 c                   eval      a1jtxt = rajtxt
6.24 c                   else
6.24 c                   eval      a1jtxt = *blank
6.24 c                   endif
6.24  *
6.24 c                   endsr
6.24  *
6.24  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.24  * Subrutine for sjekk av abc kode
6.24  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.24  *
6.24 c     sjekk_abc     begsr
6.24  * Sjekk abc kodene
6.24 c                   eval      b_feil = *on
6.27 c                   if        vlkabc = *blank
6.24 c                   leavesr
6.24 c                   endif
6.24  *
6.24 c                   if        mpab01 <> *blank and
6.27 c                             mpab01 = vlkabc
6.24 c                   eval      b_feil = *off
6.24 c                   leavesr
6.24 c                   endif
6.24  *
6.24 c                   if        mpab02 <> *blank and
6.27 c                             mpab02 = vlkabc
6.24 c                   eval      b_feil = *off
6.24 c                   leavesr
6.24 c                   endif
6.24  *
6.24 c                   if        mpab03 <> *blank and
6.27 c                             mpab03 = vlkabc
6.24 c                   eval      b_feil = *off
6.24 c                   leavesr
6.24 c                   endif
6.24  *
6.24 c                   if        mpab04 <> *blank and
6.27 c                             mpab04 = vlkabc
6.24 c                   eval      b_feil = *off
6.24 c                   leavesr
6.24 c                   endif
6.24  *
6.24 c                   if        mpab05 <> *blank and
6.27 c                             mpab05 = vlkabc
6.24 c                   eval      b_feil = *off
6.24 c                   leavesr
6.24 c                   endif
6.24  *
6.24 c                   if        mpab06 <> *blank and
6.27 c                             mpab06 = vlkabc
6.24 c                   eval      b_feil = *off
6.24 c                   leavesr
6.24 c                   endif
6.24  *
6.24 c                   if        mpab07 <> *blank and
6.27 c                             mpab07 = vlkabc
6.24 c                   eval      b_feil = *off
6.24 c                   leavesr
6.24 c                   endif
6.24  *
6.24 c                   if        mpab08 <> *blank and
6.27 c                             mpab08 = vlkabc
6.24 c                   eval      b_feil = *off
6.24 c                   leavesr
6.24 c                   endif
6.24  *
6.24 c                   if        mpab09 <> *blank and
6.27 c                             mpab09 = vlkabc
6.24 c                   eval      b_feil = *off
6.24 c                   leavesr
6.24 c                   endif
6.24  *
6.24 c                   endsr
6.24  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne status på heading
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_stsh     begsr
      *
7.01 c                   if        laabst = 0
7.01 c                   eval      lhstat = *blank
7.01 c                   leavesr
7.01 c                   endif
      * finner evt. differanse for mellom vekt/Beløp grense og best.forslag
     c                   if        d_gvek > 0
     c                   eval      w_dvek = w_totv - d_gvek
     c                   else
     c                   eval      w_dvek = 0
     c                   endif
     c                   if        d_gbel > 0
     c                   eval      w_dbel = w_totb - d_gbel
     c                   else
     c                   eval      w_dbel = 0
     c                   endif
      *
     c                   eval      lhstat = *blank
      *
     c                   select
      * varestatus 7
     c                   when      b_sts7 = *on
     c                   eval      lhstat = '5'
      * varestatus 4 - 6
     c                   when      b_sts6 = *on  or
     c                             b_sts5 = *on  or
     c                             b_sts4 = *on
     c                   eval      lhstat = '4'
      * varestatus 3
     c                   when      b_sts3 = *on
     c                   eval      lhstat = '3'
      * varestatus 2
     c                   when      b_sts2 = *on
     c                   eval      lhstat = '1'
6.11  * varestatus 9
6.11 c                   when      b_sts9 = *on
6.11 c                   eval      lhstat = '7'
      * varestatus 0 eller 1
     c                   when      b_sts0 = *on or
     c                             b_sts1 = *on
      * over min grenser ?
     c                   if        (d_gbel > 0 or
     c                             d_gvek > 0) and
     c                             (w_dvek < 0 or
     c                             w_dbel < 0)
     c                   eval      lhstat = '2'
     c                   else
     c                   eval      lhstat = '0'
     c                   endif
     c                   endsl
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_best      begsr
readec                   eval      lodtlv_vare = vvvare
readec     lodtlv_key    setll     lodtlv
readec     lodtlv_key    reade     lodtlv
6.29 c                   dow       not %eof(lodtlv)
6.29 c                   if        ldlety = 0 and
6.29 c                             ldlage = d_lage and
6.18 c                             ldanta > 0
      *
     c                   eval      lohelr_numm = ldnumm
     c                   eval      lohelr_suff = ldsuff
     c     lohelr_key    chain     lohelr
     c                   if        %found(lohelr)
     c                   eval      votylr_otyp = lootyp
     c     votylr_key    chain     votylr
     c                   if        %found(votylr) and
     c                             vaolag = 1  and
     c                             vaoakk = 1
     c                   eval      w_stat = '6'
     c                   eval      b_sts6 = *on
8.01 c                   goto      sjk_u801
readec                   endif
readec                   endif
     c                   endif
6.29 c     lodtlv_key    reade     lodtlv
6.29 c                   enddo
8.01  *
8.01  * Sjekk om switch for overstyring av kode 6 er satt
8.01  *
8.01 c     sjk_u801      tag
8.01 c                   if        u_801 = *on and
8.01 c                             ldornu <> 0
8.03 c                   eval      w_stat = '0'
8.01 c                   eval      b_sts6 = *off
8.01 c                   endif
      *
     c                   endsr
      *
      *
6.24  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.24  * Subrutine for innhenting av diverse tall til linje
6.24  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.24  *
6.24 c     hnt_antall    begsr
6.24  *
6.24 c                   eval      w_ibes = 0
6.24  *
6.24 c                   eval      lodtlv_vare = vvvare
6.24 c     lodtlv_key    setll     lodtlv
6.24 c     lodtlv_key    reade     lodtlv
6.24 c                   dow       not %eof(lodtlv)
6.24  * Regn om til lagerenhet hvis det ikke vlenhl
6.24 c                   if        ldenh1 <> vlenhl
6.24 c                   eval      p_anti = ldanta
6.24 c                   call      'VA770R'
6.24 c                   parm                    w_firm
6.24 c                   parm                    vvvare
6.24 c                   parm                    ldenh1
6.24 c                   parm                    p_anti
6.24 c                   parm                    vlenhl
6.24 c                   parm                    p_antu
6.24 c                   else
6.24 c                   eval      p_antu = ldanta
6.24 c                   endif
6.24  *
6.24 c                   eval      w_ibes = w_ibes + p_antu
6.24 c     lodtlv_key    reade     lodtlv
6.24 c                   enddo
6.24  *
6.24 c                   eval      vlaglr_vare = vvvare
6.24 c     vlaglr_key    setll     vlagl1
6.24 c     vlaglr_key    reade     vlagl1
6.24 c                   dow       not %eof(vlagl1)
6.24 c                   eval      w_totl = 0
6.24 c                   eval      w_totl = vlbehl + vlbbes -
6.24 c                                      vlpakk - vloord
6.24 c                   eval      d1totl = d1totl + w_totl
6.24 c     vlaglr_key    reade     vlagl1
6.24 c                   enddo
6.24  *
6.24 c                   endsr
6.24  *
6.24  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.24  *  Skriv linja som "burde" vært laget
6.24  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.24 c     prt_line      begsr
6.24  * Første gang skrives heading, deretter etter behov
6.24 c                   if        b_prnt = *off
6.24 c                   eval      a1firm = l_firm
6.24 c                   eval      a1fnav = l_fnav
6.24 c                   eval      w_ldor = d_flev
6.24 c                   exsr      les_ldor
6.24 c                   eval      a1ldor = rllevr
6.24 c                   eval      a1lnav = rlnavn
6.24 c                   eval      a1lage = d_lage
6.24 c                   exsr      les_lager
6.24 c                   eval      a1ndat = %date(d_ndat)
6.24 c                   write     a1hdr
6.24 c                   eval      b_prnt = *on
6.24 c                   endif
6.24  * Klargjør linjene
6.24 c                   eval      d1disp = vlbehl + vlbbes -
6.24 c                                      vlpakk - vloord
6.24 c                   eval      d1vare = vvvare
6.24 c                   eval      d1lvar = vvlvar
6.24 c                   eval      d1tek1 = vvtek1
6.24 c                   eval      d1anta = w_anta
6.24 c                   eval      d1enh1 = vvenhl
6.24 c                   eval      d1abcc = vlkabc
6.24 c                   eval      d1mini = vlmini
6.24 c                   eval      d1maks = vlmaks
6.24 c                   eval      d1behl = vlbehl
6.24 c                   eval      d1totl = 0
6.24 c                   exsr      hnt_antall
6.24 c                   eval      d1ibes = w_ibes
6.24  *
6.24 c                   write     d1det                                30
6.24 c                   eval      w_tell = w_tell +1
6.24  *
6.24  * Er det overflow ?
6.24  *
6.24 c                   if        *in30 = *on
6.24 c                   write     a1hdr
6.24 c                   endif
6.24  *
6.24 c                   endsr
6.24  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.24  * Finner spool som er generert av jobben
6.24  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.24 c     spoolnbr      begsr
6.24  /Free
6.24
6.24           QSPRILSP( SPRL0100
6.24                   : %size(SPRL0100)
6.24                   : 'SPRL0100'
6.24                   : errorcode );
6.24  /End-free
6.24 c*
6.24  *
6.24 c                   endsr
6.24  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.24  * Kaller eget program for generering av xml fil. Eget pgm         *
6.24  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.24 c     xml_create    begsr
6.24  *
6.24 c                   eval      w_flev = d_flev
6.24 c                   eval      w_lnum = d_lnum
6.24  *
6.24 c                   call      'MP714R'
6.24 c                   parm                    splfname
6.24 c                   parm                    jobname
6.24 c                   parm                    username
6.24 c                   parm                    jobnbr
6.24 c                   parm                    splfnbr
6.24 c                   parm                    w_flev
6.24 c                   parm                    w_lnum
6.24  *
6.24 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for les av VARE
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *  Key for les av enhetsregister
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
     c                   kfld                    vvenl1_enhe
      *
      *  Key for les av LAGER
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl1_vare
     c                   kfld                    vlagl1_lage
6.24  *
6.24  *  Key for les av LAGER
6.24  *
6.24 c     vlaglr_key    klist
6.24 c                   kfld                    w_firm
6.24 c                   kfld                    vlaglr_vare
      *
      * Key for les av STATUS
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      *  Key for oppdatering av BESTILLINGS-FORSLAG-HODE
      *
     c     lfhelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lfhelu_numm
      *
      *  Key for oppdatering av BESTILLINGS-FORSLAG-LINJE
      *
     c     lfdtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lfdtlu_numm
     c                   kfld                    lfdtlu_line
      *
      *  Key for les av bestillinger
      *
     c     lodtlv_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlv_vare
      *
      *  Key for les av bestilling hode
      *
     c     lohelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelr_numm
     c                   kfld                    lohelr_suff
      *
      *  Key for les av ordretype
      *
     c     votylr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votylr_otyp
      *
      *  Key for les av undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for les av hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for les av overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for les av leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
6.23  *
6.23  * Key for les av leverandør distribusjon
6.23  *
6.23 c     lldil1_key    klist
6.23 c                   kfld                    w_firm
6.23 c                   kfld                    lldil1_ldor
6.23 c                   kfld                    lldil1_lage
6.24  *
6.24  * Key for les av lager
6.24  *
6.24 c     ra10l1_key    klist
6.24 c                   kfld                    w_firm
6.24 c                   kfld                    ra10l1_jkod
6.24  *
6.24  * Key for les av parameterfile
6.24  *
6.24 c     mppal1_key    klist
6.24 c                   kfld                    w_firm
6.24 c                   kfld                    mppal1_ldor
6.24 c                   kfld                    mppal1_lnum
6.30  *
6.30  *  Key for les av erstatningsvare VVER
6.30  *
6.30 c     vverl2_key    klist
6.30 c                   kfld                    w_firm
6.30 c                   kfld                    vverl2_egvr
6.30  *
6.30  *  Key for les av erstatningsvare VVER
6.30  *
6.30 c     vverl1_key    klist
6.30 c                   kfld                    w_firm
6.30 c                   kfld                    vverl1_envr
6.30 c                   kfld                    vverl1_egvr
6.30  *
6.30  *  Key for les av erstatningsvare VVER
6.30  *
6.30 c     vverl1_key2   klist
6.30 c                   kfld                    w_firm
6.30 c                   kfld                    vverl1_envr
6.31  *
6.31  *  Key for les av erstatningsvare VVER
6.31  *
6.31 c     vverl4_key    klist
6.31 c                   kfld                    w_firm
6.31 c                   kfld                    vverl4_envr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    w_parm
     c                   parm                    w_pnum
     c                   parm                    b_dann
     c                   eval      d_prec = w_parm
      *
      *  Legge inn startverdier
      *
     c                   eval      w_firm = l_firm
     c                   time                    w_date
     c                   time                    w_time
     c                   eval      d_btdat = d_tdat
     c                   eval      d_blage = d_lage
     c                   eval      w_totb  = 0
     c                   eval      w_totv  = 0
     c                   eval      b_sts0  = *off
     c                   eval      b_sts1  = *off
     c                   eval      b_sts2  = *off
     c                   eval      b_sts3  = *off
     c                   eval      b_sts4  = *off
     c                   eval      b_sts5  = *off
     c                   eval      b_sts6  = *off
     c                   eval      b_dann  = *off
5.70  *
5.70  * Hent prisgruppe
5.70  *
5.70 c                   eval      w_avde = 0
5.70 c                   eval      w_lage = d_lage
5.70 c                   call      'VL712R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    w_lage
5.70 c                   parm                    w_avde
5.70 c                   parm                    w_prgr
5.70  *
      *
      * Hent opplysninger fra LAGER-STATUS-KODER
      *
     c     lstsl1_key    chain     lstsl1r                            90
      *
6.11 c                   if        laakso <> *blank
6.11 c                   eval      b_ksor = *on
6.11 c                   endif
      *
6.24 c                   eval      b_bfor = *on
6.24 c                   if        d_bgen = '1'
6.24 c                   eval      b_bfor = *off
6.24 c                   eval      w_tell = 0
6.24 c                   open      lh702p
6.24  * Slå opp i parameterfile for å hente inn om varene skal være med
6.24 c                   eval      b_abck = *on
6.24 c                   eval      mppal1_ldor = d_flev
6.24 c                   eval      mppal1_lnum = d_lnum
6.24 c     mppal1_key    chain     mppal1r
6.24 c                   if        %found(mppal1)
6.24 c                   if        mpab01 = *blank and
6.24 c                             mpab02 = *blank and
6.24 c                             mpab03 = *blank and
6.24 c                             mpab04 = *blank and
6.24 c                             mpab05 = *blank and
6.24 c                             mpab06 = *blank and
6.24 c                             mpab07 = *blank and
6.24 c                             mpab08 = *blank and
6.24 c                             mpab09 = *blank
6.24 c                   eval      b_abck = *off
6.24 c                   endif
6.24 c                   endif
6.24 c                   endif
      *
      *  Henter neste ledige FORSLAGS-nummer fra Nummer-register
      *
6.24 c                   if        b_bfor = *on
     c                   exsr      hntnum
6.24 c                   endif
6.36  * Sjekk om du har tilgang til BM modulen.
6.36 c                   eval      b_bmpp = *on
6.36 c                   eval      w_disp = '0'
6.36 c                   eval      w_msts = '0'
6.36 c                   eval      w_modu = c_modu
6.36 c                   call      'AX700R'
6.36 c                   parm                    w_modu
6.36 c                   parm                    w_disp
6.36 c                   parm                    w_msts
6.36  *
6.36 c                   if        w_msts = '1'
6.36 c                   eval      b_bmpp = *off
6.36 c                   endif
6.3c  *
6.3c  * Endring 6.3c
6.3c  *
6.3c   CallP CO402R(l_filg:w_firm:0:'TILB_INNKJ_PERIOD':
6.3c                    co402_verdi1:co402_verdi2);
6.3c   if co402_verdi1 = '1';
6.3c     u_63c = *on;
6.3c     *in50 = *on;
6.3c   endif;
8.01  *
8.01  * Endring 8.01
8.01  *
8.01   CallP CO402R(l_filg:w_firm:0:'BESTF_STATUS6':
8.01                    co402_verdi1:co402_verdi2);
8.01   if co402_verdi1 = '1';
8.01     u_801 = *on;
8.01   endif;
8.02  *
8.02  * Endring 7.05
8.02  *
8.02   CallP CO402R(l_filg:w_firm:0:'LH702R_702':
8.02                    co402_verdi1:co402_verdi2);
8.02   if co402_verdi1 = '1';
8.02     u_702 = *on;
8.02   endif;

      *
      *  Legger inn startverdi for lesingen
      *
     c                   movel     d_fvar        vvarl1_vare
     c     vvarl1_key    setll     vvarl1r
      *
     c                   endsr
