     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VP700R
      * Beskrivelse . . : Vare-pris, henter priser for enhet
      *
      *                   Input:  Firma, vare, enhet, leveringstype,
      *                           dato, tidspunkt og LR-flagg
      *                   Output: Salgspris, butikkpris, tilbudspris og
      *                           kostpris
      *
      *                   LR-flagg må være lik '1' for at *inLR ikke skal
      *                   settes
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       050599 HKO  Nytt program
5.70  * PRGR  231008 BOF  Utvidet med prisgruppe
6.20  *       220311 BOF  Utvidet med prisgiver på vare-pris
6.21  *       240411 BOF  Utvidet med tilbudspris med varegruppe og leverandør
6.24  *       070312 BOF  Justering på hente tilbudspris vareruppe og leverandør
      *
8.01  *PRG2   150622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvprl1    if   e           k disk    rename(vvprpfr:vvprl1r)
     fvtill1    if   e           k disk    rename(vtilpfr:vtill1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
      *
      * Definering av pamametre
      *
     d p_firm          s                   like(vpfirm)
     d p_vare          s                   like(vpvare)
5.70 d p_prgr          s                   like(vpprgr)
     d p_enhe          s                   like(vpenhe)
     d p_lety          s                   like(vplety)
     d p_dato          s                   like(vppdat)
     d p_time          s                   like(vtftim)
     d p_sapr          s                   like(vpsapr)
     d p_bupr          s                   like(vpbupr)
     d p_tipr          s                   like(vttip1)
     d p_kopr          s                   like(vpkopr)
      *
     d p_inlr          s              1
      *
      * Definering av variable i nøkler
      *
     d vvprl1_vare     s                   like(vpvare)
6.20 d vvprl1_ldor     s                   like(vpldor)
5.70 d vvprl1_prgr     s                   like(vpprgr)
     d vvprl1_enhe     s                   like(vpenhe)
     d vvprl1_lety     s                   like(vplety)
6.21 d vtill1_ogrp     s                   like(vtogrp)
6.21 d vtill1_hgrp     s                   like(vthgrp)
6.21 d vtill1_ugrp     s                   like(vtugrp)
6.21 d vtill1_ldor     s                   like(vtldor)
     d vtill1_vare     s                   like(vtvare)
5.70 d vtill1_prgr     s                   like(vtprgr)
     d vtill1_lety     s                   like(vtlety)
     d vvarl1_vare     s                   like(vvvare)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
      *
      * Definering av variable
      *
     d b_pris          s              1    inz(*off)
6.20 d b_inlr          s              1    inz(*off)
      *
     d w_firm          s                   like(vpfirm)
     d w_dato          s                   like(vppdat)
     d w_time          s                   like(vtftim)
6.20 d w_ldor          s                   like(vvldor)
6.20 d w_lage          s              2  0
6.21 d w_lety          s                   like(vplety)
6.21 d w_prgr          s                   like(vpprgr)
6.21 d w_pldo          s                   like(vpldor)
      *
      *
6.20 d                uds
6.20 d l_lage               1001   1002  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
6.20 c                   eval      w_lage = l_lage
6.20  *
6.20  * Henter leverandør for vare og lager
6.20  *
6.20 c                   call      'VL721R  '
6.20 c                   parm                    w_firm
6.20 c                   parm                    p_vare
6.20 c                   parm                    w_lage
6.20 c                   parm                    w_ldor
6.20 c                   parm                    b_inlr
      *
     c                   eval      w_firm = p_firm
      *
6.20 c                   eval      vvarl1_vare = p_vare
6.20 c     vvarl1_key    chain     vvarl1
      *
      * Initierer dato og tidspunkt
      *
     c                   eval      w_dato = p_dato
     c                   eval      w_time = p_time
      *
     c                   if        w_dato = *loval
     c                   time                    w_dato
     c                   endif
      *
     c                   if        w_time = *loval
     c                   time                    w_time
     c                   endif
      *
      * Initierer output-parametre
      *
     c                   eval      p_sapr = 0
     c                   eval      p_bupr = 0
     c                   eval      p_tipr = 0
     c                   eval      p_kopr = 0
      *
      * Henter pris for vare, prisgrp.,leveringstype og enhet
      *
6.20  * 1. leverandør for lageret
6.20 c                   eval      vvprl1_ldor = w_ldor
6.20 c                   exsr      hent_pris
6.20 c                   if        b_pris = *off  and
6.20 c                             w_ldor <> vvldor
6.20  * 2. hovedleverandør
6.20 c                   eval      vvprl1_ldor = vvldor
6.20 c                   exsr      hent_pris
6.20 c                   if        b_pris = *off
6.20  * siste mulighet...
6.20 c                   eval      vvprl1_ldor = 0
6.20 c                   exsr      hent_pris
6.20 c                   endif
6.20 c                   endif
      *
      * Avbryter dersom pris ikke ble funnet
      *
     c                   if        b_pris = *off
     c                   goto      avslutt
     c                   endif
      *
      * Henter eventuell tilbudspris
      *
6.21 c                   eval      vtill1_ogrp = 0
6.21 c                   eval      vtill1_hgrp = 0
6.21 c                   eval      vtill1_ugrp = 0
6.21 c                   eval      vtill1_ldor = 0
     c                   eval      vtill1_vare = p_vare
5.70 c                   eval      vtill1_prgr = p_prgr
     c                   eval      vtill1_lety = p_lety
     c     vtill1_key    setll     vtill1
     c     vtill1_key    reade     vtill1                                 90
     c                   dow       *in90 = *off
     c                   if        w_dato >= vtfdat and
     c                             w_dato <= vttdat
     c                   if        w_dato <> vtfdat or
     c                             w_dato = vtfdat and
     c                             w_time >= vtftim
     c                   if        w_dato <> vttdat or
     c                             w_dato = vttdat and
     c                             w_time <= vtttim
      *
     c                   select
     c                   when      p_enhe = vtenh1
     c                   eval      p_tipr = vttip1
     c                   if        vtkop1 <> 0
     c                   eval      p_kopr = vtkop1
     c                   endif
     c                   when      p_enhe = vtenh2
     c                   eval      p_tipr = vttip2
     c                   if        vtkop2 <> 0
     c                   eval      p_kopr = vtkop2
     c                   endif
     c                   when      p_enhe = vtenh3
     c                   eval      p_tipr = vttip3
     c                   if        vtkop3 <> 0
     c                   eval      p_kopr = vtkop3
     c                   endif
     c                   endsl
      *
     c                   leave
      *
     c                   endif
     c                   endif
     c                   endif
     c     vtill1_key    reade     vtill1                                 90
     c                   enddo
      *
      * Dersom tilbud ikke ble funnet på oppgitt leveringstype, letes det
      * etter tilbud på leveringstype 0
      *
     c                   if        p_tipr = 0 and
     c                             p_lety <> 0
      *
     c                   eval      vtill1_vare = p_vare
5.70 c                   eval      vtill1_prgr = p_prgr
     c                   eval      vtill1_lety = 0
     c     vtill1_key    setll     vtill1
     c     vtill1_key    reade     vtill1                                 90
     c                   dow       *in90 = *off
     c                   if        w_dato >= vtfdat and
     c                             w_dato <= vttdat
     c                   if        w_dato <> vtfdat or
     c                             w_dato = vtfdat and
     c                             w_time >= vtftim
     c                   if        w_dato <> vttdat or
     c                             w_dato = vttdat and
     c                             w_time <= vtttim
      *
     c                   select
     c                   when      p_enhe = vtenh1
     c                   eval      p_tipr = vttip1
     c                   if        vtkop1 <> 0
     c                   eval      p_kopr = vtkop1
     c                   endif
     c                   when      p_enhe = vtenh2
     c                   eval      p_tipr = vttip2
     c                   if        vtkop2 <> 0
     c                   eval      p_kopr = vtkop2
     c                   endif
     c                   when      p_enhe = vtenh3
     c                   eval      p_tipr = vttip3
     c                   if        vtkop3 <> 0
     c                   eval      p_kopr = vtkop3
     c                   endif
     c                   endsl
      *
     c                   leave
      *
     c                   endif
     c                   endif
     c                   endif
     c     vtill1_key    reade     vtill1                                 90
     c                   enddo
      *
     c                   endif
5.70  *
5.70  * Dersom tilbud ikke ble funnet på oppgitt prisgruppe, letes det
5.70  * etter tilbud på prisgruppe = blank
5.70  *
5.70 c                   if        p_tipr = 0 and
5.70 c                             p_prgr <> *blank
5.70 c                   eval      vtill1_vare = p_vare
5.70 c                   eval      vtill1_prgr = *blank
5.70 c                   eval      vtill1_lety = p_lety
5.70 c     vtill1_key    setll     vtill1
5.70 c     vtill1_key    reade     vtill1                                 90
5.70 c                   dow       *in90 = *off
5.70 c                   if        w_dato >= vtfdat and
5.70 c                             w_dato <= vttdat
5.70 c                   if        w_dato <> vtfdat or
5.70 c                             w_dato = vtfdat and
5.70 c                             w_time >= vtftim
5.70 c                   if        w_dato <> vttdat or
5.70 c                             w_dato = vttdat and
5.70 c                             w_time <= vtttim
5.70  *
5.70 c                   select
5.70 c                   when      p_enhe = vtenh1
5.70 c                   eval      p_tipr = vttip1
5.70 c                   if        vtkop1 <> 0
5.70 c                   eval      p_kopr = vtkop1
5.70 c                   endif
5.70 c                   when      p_enhe = vtenh2
5.70 c                   eval      p_tipr = vttip2
5.70 c                   if        vtkop2 <> 0
5.70 c                   eval      p_kopr = vtkop2
5.70 c                   endif
5.70 c                   when      p_enhe = vtenh3
5.70 c                   eval      p_tipr = vttip3
5.70 c                   if        vtkop3 <> 0
5.70 c                   eval      p_kopr = vtkop3
5.70 c                   endif
5.70 c                   endsl
5.70  *
5.70 c                   leave
5.70  *
5.70 c                   endif
5.70 c                   endif
5.70 c                   endif
5.70 c     vtill1_key    reade     vtill1                                 90
5.70 c                   enddo
5.70  *
5.70  * Dersom tilbud ikke ble funnet på oppgitt leveringstype, letes det
5.70  * etter tilbud på leveringstype 0
5.70  *
5.70 c                   if        p_tipr = 0 and
5.70 c                             p_lety <> 0
5.70  *
5.70 c                   eval      vtill1_vare = p_vare
5.70 c                   eval      vtill1_prgr = *blank
5.70 c                   eval      vtill1_lety = 0
5.70 c     vtill1_key    setll     vtill1
5.70 c     vtill1_key    reade     vtill1                                 90
5.70 c                   dow       *in90 = *off
5.70 c                   if        w_dato >= vtfdat and
5.70 c                             w_dato <= vttdat
5.70 c                   if        w_dato <> vtfdat or
5.70 c                             w_dato = vtfdat and
5.70 c                             w_time >= vtftim
5.70 c                   if        w_dato <> vttdat or
5.70 c                             w_dato = vttdat and
5.70 c                             w_time <= vtttim
5.70  *
5.70 c                   select
5.70 c                   when      p_enhe = vtenh1
5.70 c                   eval      p_tipr = vttip1
5.70 c                   if        vtkop1 <> 0
5.70 c                   eval      p_kopr = vtkop1
5.70 c                   endif
5.70 c                   when      p_enhe = vtenh2
5.70 c                   eval      p_tipr = vttip2
5.70 c                   if        vtkop2 <> 0
5.70 c                   eval      p_kopr = vtkop2
5.70 c                   endif
5.70 c                   when      p_enhe = vtenh3
5.70 c                   eval      p_tipr = vttip3
5.70 c                   if        vtkop3 <> 0
5.70 c                   eval      p_kopr = vtkop3
5.70 c                   endif
5.70 c                   endsl
5.70  *
5.70 c                   leave
5.70  *
5.70 c                   endif
5.70 c                   endif
5.70 c                   endif
5.70 c     vtill1_key    reade     vtill1                                 90
5.70 c                   enddo
5.70  *
5.70 c                   endif
5.70  *
5.70 c                   endif
6.21  *
6.21  * Dersom tilbud ikke ble funnet på oppgitt vare/prisgruppe/leveringstupe
6.21  * sjekkes varegruppe / leverandør
6.21  * overgruppe,hovedgruppe,undergruppe
6.21  *
6.24  *
6.24 c                   if        p_tipr = 0
6.24 c                   eval      vtill1_prgr = p_prgr
6.24 c                   exsr      tilb_sjekk
6.24 c                   if        p_tipr = 0 and
6.24 c                             p_prgr <> *blank
6.24 c                   eval      vtill1_prgr = *blank
6.24 c                   exsr      tilb_sjekk
6.24 c                   endif
6.24 c                   endif
      *
      *
      * Dersom kostpris er 0 hentes kostpris på bakgrunn av DG-% på vare
      * eller undergruppe
      *
     c                   if        p_kopr = 0
      *
     c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1                             90
     c                   if        *in90 = *on or
     c                             vvdekn = 0
     c                   eval      vugrl1_uogr = vvogrp
     c                   eval      vugrl1_uhgr = vvhgrp
     c                   eval      vugrl1_uugr = vvugrp
     c     vugrl1_key    chain     vugrl1r                            90
     c                   if        *in90 = *off
     c                   eval      vvdekn = vgudek
     c                   endif
     c                   endif
      *
     c                   eval(h)   p_kopr = p_sapr * vvdekn / 100
     c                   if        p_kopr <> 0
     c                   eval      p_kopr = p_sapr - p_kopr
     c                   endif
      *
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   if        p_inlr <> *on
     c                   eval      *inlr = *on
     c                   endif
      *
     c                   return
      *
6.20  * -----------------------------------------------------------------------------
6.20  * Henter pris - prøver først med valgt prisgr, så prisgr.blank
6.20  * -----------------------------------------------------------------------------
6.20  *
6.20 c     hent_pris     begsr
6.20  *
6.20 c                   eval      b_pris = *off
6.20  *
6.20 c                   eval      vvprl1_vare = p_vare
6.20 c                   eval      vvprl1_prgr = p_prgr
6.20 c                   eval      vvprl1_enhe = p_enhe
6.20 c                   eval      vvprl1_lety = p_lety
6.20 c     vvprl1_key    setll     vvprl1
6.20 c     vvprl1_key    reade     vvprl1                                 90
6.20 c                   dow       *in90 = *off
6.20  *
6.20 c                   eval      b_pris = *on
6.20 c                   eval      p_sapr = vpsapr
6.20 c                   eval      p_bupr = vpbupr
6.20 c                   eval      p_kopr = vpkopr
6.20  *
6.20 c                   if        w_dato >= vppdat
6.20 c                   leave
6.20 c                   endif
6.20  *
6.20 c     vvprl1_key    reade     vvprl1                                 90
6.20 c                   enddo
6.20  *
6.20  * Dersom pris ikke finnes for oppgitt leveringstype, hentes pris fra
6.20  * leveringstype 0
6.20  *
6.20 c                   if        b_pris = *off and
6.20 c                             p_lety <> 0
6.20  *
6.20 c                   eval      vvprl1_vare = p_vare
6.20 c                   eval      vvprl1_prgr = p_prgr
6.20 c                   eval      vvprl1_enhe = p_enhe
6.20 c                   eval      vvprl1_lety = 0
6.20 c     vvprl1_key    setll     vvprl1
6.20 c     vvprl1_key    reade     vvprl1                                 90
6.20 c                   dow       *in90 = *off
6.20  *
6.20 c                   eval      b_pris = *on
6.20 c                   eval      p_sapr = vpsapr
6.20 c                   eval      p_bupr = vpbupr
6.20 c                   eval      p_kopr = vpkopr
6.20  *
6.20 c                   if        w_dato >= vppdat
6.20 c                   leave
6.20 c                   endif
6.20  *
6.20 c     vvprl1_key    reade     vvprl1                                 90
6.20 c                   enddo
6.20  *
6.20 c                   endif
6.20  *
6.20  * Dersom pris ikke finnes for oppgitt prisgruppe, hentes pris fra
6.20  * prisgruppe blank
6.20  *
6.20 c                   if        b_pris = *off and
6.20 c                             p_prgr <> *blank
6.20  *
6.20 c                   eval      vvprl1_vare = p_vare
6.20 c                   eval      vvprl1_prgr = *blank
6.20 c                   eval      vvprl1_enhe = p_enhe
6.20 c                   eval      vvprl1_lety = p_lety
6.20 c     vvprl1_key    setll     vvprl1
6.20 c     vvprl1_key    reade     vvprl1                                 90
6.20 c                   dow       *in90 = *off
6.20  *
6.20 c                   eval      b_pris = *on
6.20 c                   eval      p_sapr = vpsapr
6.20 c                   eval      p_bupr = vpbupr
6.20 c                   eval      p_kopr = vpkopr
6.20  *
6.20 c                   if        w_dato >= vppdat
6.20 c                   leave
6.20 c                   endif
6.20  *
6.20 c     vvprl1_key    reade     vvprl1                                 90
6.20 c                   enddo
6.20  *
6.20  * Dersom pris ikke finnes for oppgitt leveringstype, hentes pris fra
6.20  * leveringstype 0
6.20  *
6.20 c                   if        b_pris = *off and
6.20 c                             p_lety <> 0
6.20  *
6.20 c                   eval      vvprl1_vare = p_vare
6.20 c                   eval      vvprl1_prgr = *blank
6.20 c                   eval      vvprl1_enhe = p_enhe
6.20 c                   eval      vvprl1_lety = 0
6.20 c     vvprl1_key    setll     vvprl1
6.20 c     vvprl1_key    reade     vvprl1                                 90
6.20 c                   dow       *in90 = *off
6.20  *
6.20 c                   eval      b_pris = *on
6.20 c                   eval      p_sapr = vpsapr
6.20 c                   eval      p_bupr = vpbupr
6.20 c                   eval      p_kopr = vpkopr
6.20  *
6.20 c                   if        w_dato >= vppdat
6.20 c                   leave
6.20 c                   endif
6.20  *
6.20 c     vvprl1_key    reade     vvprl1                                 90
6.20 c                   enddo
6.20  *
6.20 c                   endif
6.20  *
6.20 c                   endif
6.21  *
6.21 c                   eval      w_lety = vplety
6.21 c                   eval      w_prgr = vpprgr
6.21 c                   eval      w_pldo = vpldor
6.20  *
6.20 c                   endsr
6.20  *
6.24  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.24  * Subrutine for å hente sjekke om det finnes tilbud på varegr.nivå
6.24  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.24  *
6.24 c     tilb_sjekk    begsr
6.24  *
6.24 c                   if        p_tipr = 0
6.24  * 1. lety = p_lety, prgr = p_prgr
6.24 c                   eval      vtill1_ogrp = vvogrp
6.24 c                   eval      vtill1_hgrp = vvhgrp
6.24 c                   eval      vtill1_ugrp = vvugrp
6.24 c                   eval      vtill1_ldor = 0
6.24 c                   eval      vtill1_lety = p_lety
6.24 c                   eval      vtill1_vare = *blank
6.24 c                   exsr      hent_tilb
6.24 c                   endif
6.24 c                   if        p_tipr = 0  and
6.24 c                             p_lety <> 0
6.24  * 2. lety = 0       prgr = p_prgr
6.24 c                   eval      vtill1_lety = 0
6.24 c                   exsr      hent_tilb
6.24 c                   endif
6.24  *  overgruppe,hovedgruppe
6.24 c                   if        p_tipr = 0
c.24  * 1. lety = p_lety, prgr = p_prgr
6.24 c                   eval      vtill1_ugrp = 0
6.24 c                   eval      vtill1_lety = p_lety
6.24 c                   exsr      hent_tilb
6.24 c                   endif
6.24 c                   if        p_tipr = 0  and
6.24 c                             p_lety <> 0
6.24  * 2. lety = 0       prgr = p_prgr
6.24 c                   eval      vtill1_lety = 0
6.24 c                   exsr      hent_tilb
6.24 c                   endif
6.24  *  overgruppe
6.24 c                   if        p_tipr = 0
c.24  * 1. lety = p_lety, prgr = p_prgr
6.24 c                   eval      vtill1_hgrp = 0
6.24 c                   eval      vtill1_lety = p_lety
6.24 c                   exsr      hent_tilb
6.24 c                   endif
6.24 c                   if        p_tipr = 0  and
6.24 c                             p_lety <> 0
6.24  * 2. lety = 0       prgr = p_prgr
6.24 c                   eval      vtill1_lety = 0
6.24 c                   exsr      hent_tilb
6.24 c                   endif
6.24  *  overgruppe,hovedgruppe,undergruppe, leverandør
6.24 c                   if        p_tipr = 0
c.24  * 1. lety = p_lety, prgr = p_prgr
6.24 c                   eval      vtill1_ogrp = vvogrp
6.24 c                   eval      vtill1_hgrp = vvhgrp
6.24 c                   eval      vtill1_ugrp = vvugrp
6.24 c                   eval      vtill1_ldor = w_pldo
6.24 c                   eval      vtill1_lety = p_lety
6.24 c                   exsr      hent_tilb
6.24 c                   endif
6.24 c                   if        p_tipr = 0  and
6.24 c                             p_lety <> 0
6.24  * 2. lety = 0       prgr = p_prgr
6.24 c                   eval      vtill1_lety = 0
6.24 c                   exsr      hent_tilb
6.24 c                   endif
6.24  *  overgruppe,hovedgruppe              leverandør
6.24 c                   if        p_tipr = 0
c.24  * 1. lety = p_lety, prgr = p_prgr
6.24 c                   eval      vtill1_ugrp = 0
6.24 c                   eval      vtill1_lety = p_lety
6.24 c                   exsr      hent_tilb
6.24 c                   endif
6.24 c                   if        p_tipr = 0  and
6.24 c                             p_lety <> 0
6.24  * 2. lety = 0       prgr = p_prgr
6.24 c                   eval      vtill1_lety = 0
6.24 c                   exsr      hent_tilb
6.24 c                   endif
6.24  *  overgruppe                          leverandør
6.24 c                   if        p_tipr = 0
c.24  * 1. lety = p_lety, prgr = p_prgr
6.24 c                   eval      vtill1_hgrp = 0
6.24 c                   eval      vtill1_lety = p_lety
6.24 c                   exsr      hent_tilb
6.24 c                   endif
6.24 c                   if        p_tipr = 0   and
6.24 c                             p_lety <> 0
6.24  * 2. lety = 0       prgr = p_prgr
6.24 c                   eval      vtill1_lety = 0
6.24 c                   exsr      hent_tilb
6.24 c                   endif
6.24  *  leverandør
6.24 c                   if        p_tipr = 0
c.24  * 1. lety = p_lety, prgr = p_prgr
6.24 c                   eval      vtill1_ogrp = 0
6.24 c                   eval      vtill1_lety = p_lety
6.24 c                   exsr      hent_tilb
6.24 c                   endif
6.24 c                   if        p_tipr = 0   and
6.24 c                             p_lety <> 0
6.24  * 2. lety = 0       prgr = p_prgr
6.24 c                   eval      vtill1_lety = 0
6.24 c                   exsr      hent_tilb
6.24 c                   endif
6.24  *
6.24 c                   endsr
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  * Subrutine for å hente tilbudspris på varegruppe/leverandørnivå
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  *
6.20 c     hent_tilb     begsr
6.20  *
6.21 c     vtill1_key    setll     vtill1
6.21 c     vtill1_key    reade     vtill1                                 90
6.21 c                   dow       *in90 = *off
6.21 c                   if        w_dato >= vtfdat and
6.21 c                             w_dato <= vttdat
6.21 c                   if        w_dato <> vtfdat or
6.21 c                             w_dato = vtfdat and
6.21 c                             w_time >= vtftim
6.21 c                   if        w_dato <> vttdat or
6.21 c                             w_dato = vttdat and
6.21 c                             w_time <= vtttim
6.21  *
6.21 c                   eval(h)   p_tipr = p_sapr -
6.21 c                                      (p_sapr * vtpro1)/100
6.21  *
6.21 c                   leave
6.21  *
6.21 c                   endif
6.21 c                   endif
6.21 c                   endif
6.21 c     vtill1_key    reade     vtill1                                 90
6.21 c                   enddo
6.21  *
6.21 c                   endsr
6.21  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
5.70 c                   parm                    p_prgr
     c                   parm                    p_enhe
     c                   parm                    p_lety
     c                   parm                    p_dato
     c                   parm                    p_time
     c                   parm                    p_inlr
     c                   parm                    p_sapr
     c                   parm                    p_bupr
     c                   parm                    p_tipr
     c                   parm                    p_kopr
      *
      * Key for les på vare-pris-register
      *
     c     vvprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvprl1_vare
6.20 c                   kfld                    vvprl1_ldor
5.70 c                   kfld                    vvprl1_prgr
     c                   kfld                    vvprl1_enhe
     c                   kfld                    vvprl1_lety
      *
      * Key for les på tilbuds-register
      *
     c     vtill1_key    klist
     c                   kfld                    w_firm
6.21 c                   kfld                    vtill1_ogrp
6.21 c                   kfld                    vtill1_hgrp
6.21 c                   kfld                    vtill1_ugrp
6.21 c                   kfld                    vtill1_ldor
     c                   kfld                    vtill1_vare
5.70 c                   kfld                    vtill1_prgr
     c                   kfld                    vtill1_lety
      *
      * Key for oppslag på vare-register
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på undergruppe-register
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      *
     c                   endsr
