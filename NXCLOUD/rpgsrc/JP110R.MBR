     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JP110R
      * Beskrivelse . . : Vedlikehold av rabattelement
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       180598 HKO  Nytt program
      *       060198 HKO  Fjernet test på at verdi må oppgis
      *       110202 HKO  Lagt inn aktivisering/passivisering av element
      *       130605 HKO  Utvidet nivå med varetype og leveringstype
6.31  * v6.30 040714 bof  Overføre til fraktbetingelse
6.32  * v6.30 071014 bof  Justeringer vedr. fraktbetingelse
6.3x  * 6.30  310515 bof  Utvidet med prisgruppe
8.01  *PRG2   130622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjrael1    if   e           k disk    rename(jraepfr:jraelr1)
     fjraelr    if   e           k disk    rename(jraepfr:jraelrr)
     fjraelu    uf a e           k disk    rename(jraepfr:jraelur)
     fjbetl1    if   e           k disk    rename(jbetpfr:jbetl1r)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
6.3x fra30l1    if   e           k disk    rename(ra30pfr:ra30l1r)
     fjmodl1    if   e           k disk    rename(jmodpfr:jmodl1r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
6.31 fjfbelu    uf a e           k disk    rename(jfbepfr:jfbelur)
      *
     fjp110d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(jpefir)
     d p_ldor          s                   like(jpeldo)
6.3x d p_prgr          s                   like(jpeprg)
     d p_ogrp          s                   like(jpeogr)
     d p_hgrp          s                   like(jpehgr)
     d p_ugrp          s                   like(jpeugr)
     d p_modn          s                   like(jpemod)
     d p_vare          s                   like(jpevar)
     d p_vtyp          s                   like(jpevty)
     d p_lety          s                   like(jpelet)
     d p_rsts          s                   like(jperst)
     d p_line          s                   like(jpelin)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d jraelu_erst     s                   like(jperst)
     d jraelu_elin     s                   like(jpelin)
     d jraelr_erst     s                   like(jperst)
     d jraelr_elin     s                   like(jpelin)
     d jlevl1_ldor     s                   like(jlldor)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
6.3x d ra30l1_ekod     s                   like(redkod)
     d jmodl1_modn     s                   like(jumodn)
     d jvarl1_vare     s                   like(jvvare)
6.3x d jfbelu_fprg     s                   like(jpfprg)
6.31 d jfbelu_fogr     s                   like(jpfogr)
6.31 d jfbelu_fhgr     s                   like(jpfhgr)
6.31 d jfbelu_fugr     s                   like(jpfugr)
6.31 d jfbelu_fmod     s                   like(jpfmod)
6.31 d jfbelu_fvar     s                   like(jpfvar)
6.31 d jfbelu_fvty     s                   like(jpfvty)
6.31 d jfbelu_flet     s                   like(jpflet)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
6.32 d b_frak          s              1    inz(*off)
      *
     d w_efda          s               d   datfmt(*iso)
     d w_etda          s               d   datfmt(*iso)
      *
     d w_firm          s                   like(jpefir)
     d w_ldor          s                   like(jpeldo)
6.3x d w_prgr          s                   like(jpeprg)
     d w_ogrp          s                   like(jpeogr)
     d w_hgrp          s                   like(jpehgr)
     d w_ugrp          s                   like(jpeugr)
     d w_modn          s                   like(jpemod)
     d w_vare          s                   like(jpevar)
     d w_vtyp          s                   like(jpevty)
     d w_lety          s                   like(jpelet)
     d w_rsts          s                   like(jpebrs)
     d w_line          s                   like(jpebli)
6.31 d w_fkrt          s                   like(jpfkrt)
6.31 d w_fprt          s                   like(jpfprt)
      *
     d s_erst          s                   like(jperst)
     d s_elin          s                   like(jpelin)
     d s_erae          s                   like(jperae)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(7)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile                                  DSP SFL
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2taga
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile                                  CRT SFL
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c     jrael1_key    setll     jrael1
      *
     c                   exsr      clr_subfile                                  TØM SFL
     c                   exsr      crt_subfile                                  CRT SFL
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1erst = b1erst
     c                   eval      c1elin = b1elin
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1erst = b1erst
     c                   eval      k1elin = b1elin
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1erst = b1erst
     c                   eval      d1elin = b1elin
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1erst = b1erst
     c                   eval      c1elin = b1elin
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Aktiviser/passiviser
      *
     c                   if        b1valg = 9
     c                   exsr      aktiviser
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
6.31  *
6.31  * Overføre til frakt betingelse
6.31  *
6.31 c                   if        b1valg = 16
6.31 c                   exsr      xc4win
6.31 c                   eval      b1valg = 0
6.31 c                   update    b1sfl
6.31 c                   iter
6.31 c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1erae = *blank
     c                   eval      c1erst = 1
     c                   eval      c1elin = 0
      *
     c     c1taga        tag
      *
     c                   eval      s_erae = c1erae
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      c1taga
     c                   endsl
      *
     c                   if        c1erae = *blank
     c                   goto      end_c1bld
     c                   endif
      *
      * Dersom element-tekst er endret nullstilles reg.status og linjenr
      *
     c                   if        c1erae <> s_erae
     c                   eval      c1erst = 1
     c                   eval      c1elin = 0
     c                   eval      s_erae = c1erae
     c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   if        c1elin <> 0
     c                   eval      jraelr_erst = c1erst
     c                   eval      jraelr_elin = c1elin
     c     jraelr_key    chain     jraelr                             90
     c                   if        *in90 = *off
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1erae = *blank
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbilde legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle skjermbilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      *in90 = *on
      *
     c                   if        c1elin <> 0
     c                   eval      jraelr_erst = c1erst
     c                   eval      jraelr_elin = c1elin
     c     jraelr_key    chain     jraelr                             90
     c                   endif
      *
     c                   if        *in90 = *off
     c                   eval      c1erae = jperae
     c                   eval      c2ereg = jpereg
     c                   eval      c2ever = jpever
     c                   eval      c2eeus = jpeeus
     c                   eval      c2efda = 0
     c                   eval      c2etda = 0
     c     *dmy          move      jpeoda        c2eoda
     c     *dmy          move      jpeeda        c2eeda
     c                   if        jpefda <> *loval
     c     *dmy          move      jpefda        c2efda
     c                   endif
     c                   if        jpetda <> *loval
     c     *dmy          move      jpetda        c2etda
     c                   endif
     c                   else
     c                   eval      c2ereg = '1'
     c                   eval      c2ever = 0
     c                   eval      c2efda = 0
     c                   eval      c2etda = 0
     c                   eval      c2eoda = 0
     c                   eval      c2eeda = 0
     c                   eval      c2eeus = *blank
     c                   endif
      *
      * Kopiering
      *
     c                   if        b1valg = 3
     c                   eval      c1erst = 1
     c                   eval      c1elin = 0
     c                   eval      c1erae = k1erae
     c                   eval      jraelr_erst = 1
     c                   eval      jraelr_elin = 0
     c                   endif
      *
      * Sperrer felter dersom 'vise'
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Validering av input
      *
     c                   if        c1erae = *blank
     c                   eval      *in31  = *on
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        c2ereg = *blank
     c                   eval      *in32  = *on
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        c2ereg <> '1' and
     c                             c2ereg <> '2' and
     c                             c2ereg <> 'N' and
     c                             c2ereg <> 'K'
     c                   eval      *in33 = *on
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        c2ereg = '1' or
     c                             c2ereg = '2'
     c                   if        %abs(c2ever) > 100
     c                   eval      *in35  = *on
     c                   goto      c2tagb
     c                   endif
     c                   endif
      *
     c                   if        c2efda <> 0
     c     *dmy          test (d)                c2efda                 36
     c                   if        *in36
     c                   goto      c2tagb                                                    .
     c                   endif
     c                   endif
      *
     c                   if        c2etda <> 0
     c     *dmy          test (d)                c2etda                 37
     c                   if        *in37
     c                   goto      c2tagb                                                    .
     c                   endif
     c                   endif
      *
     c                   if        c2efda <> 0 and
     c                             c2etda <> 0
     c     *dmy          move      c2efda        w_efda
     c     *dmy          move      c2etda        w_etda
     c                   if        w_efda > w_etda
     c                   eval      *in38 = *on
     c                   goto      c2tagb                                                    .
     c                   endif
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   jraelur
      *
     c                   eval      *in90 = *on
      *
     c                   if        c1elin <> 0
     c                   eval      jraelu_erst = c1erst
     c                   eval      jraelu_elin = c1elin
     c     jraelu_key    chain     jraelur                            90
     c                   endif
      *
     c                   eval      jperae = c1erae
     c                   eval      jpereg = c2ereg
     c                   eval      jpever = c2ever
     c                   eval      jpeeus = l_user
      *
     c                   eval      jpefda = *loval
     c                   if        c2efda <> 0
     c     *dmy          move      c2efda        jpefda
     c                   endif
      *
     c                   eval      jpetda = *loval
     c                   if        c2etda <> 0
     c     *dmy          move      c2etda        jpetda
     c                   endif
      *
     c                   time                    jpeeda
     c                   time                    jpeeti
      *
      * Endrer/oppretter rad.
      * Dersom ny rad, kalles program for å hente neste ledige linjenummer
      * og c1erst og c1elin tildeles verdi
      *
     c                   if        *in90 = *off
     c                   update    jraelur
     c                   else
      *
     c                   eval      jpefir = w_firm
     c                   eval      jpeldo = w_ldor
6.3x c                   eval      jpeprg = w_prgr
     c                   eval      jpeogr = w_ogrp
     c                   eval      jpehgr = w_hgrp
     c                   eval      jpeugr = w_ugrp
     c                   eval      jpemod = w_modn
     c                   eval      jpevar = w_vare
     c                   eval      jpevty = w_vtyp
     c                   eval      jpelet = w_lety
     c                   eval      jpebrs = w_rsts
     c                   eval      jpebli = w_line
     c                   eval      jperst = 1
     c                   eval      jpeakt = 0
      *
     c                   time                    jpeoda
      *
     c                   call      'JP711R'
     c                   parm                    w_firm
     c                   parm                    w_ldor
6.3x c                   parm                    w_prgr
     c                   parm                    w_ogrp
     c                   parm                    w_hgrp
     c                   parm                    w_ugrp
     c                   parm                    w_modn
     c                   parm                    w_vare
     c                   parm                    w_vtyp
     c                   parm                    w_lety
     c                   parm                    w_rsts
     c                   parm                    w_line
     c                   parm                    jperst
     c                   parm                    jpelin
      *
     c                   write     jraelur
      *
     c                   eval      c1erst = jperst
     c                   eval      c1elin = jpelin
      *
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1erae = jperae
     c                   eval      b1ereg = jpereg
     c                   eval      b1ever = jpever
     c                   eval      b1efda = 0
     c                   eval      b1etda = 0
     c                   if        jpefda <> *loval
     c     *dmy          move      jpefda        b1efda
     c                   endif
     c                   if        jpetda <> *loval
     c     *dmy          move      jpetda        b1etda
     c                   endif
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  * Subrutine for å overføre valgt linje til frakttabell
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  *
6.31 c     xc4win        begsr
6.32  *
6.32  * Valider kun om betingelser er på varenivå
6.32  *
6.32 c                   if        p_vare = *blank
6.32 c                   exfmt     c4wina
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32  * Valider kun kronetillegg
6.32  *
6.32 c                   eval      b_frak = *off
6.32  *
6.31 c                   eval      jraelr_erst = b1erst
6.31 c                   eval      jraelr_elin = b1elin
6.32 c     jrael1_key    setll     jrael1
6.32 c     jrael1_key    reade     jrael1
6.32 c                   dow       not %eof(jrael1)
6.32 c                   if        jpereg = 'K'
6.32 c                   eval      b_frak = *on
6.32 c                   endif
6.32 c     jrael1_key    reade     jrael1                                 91
6.32 c                   enddo
6.32  *
6.32 c                   if        b_frak = *off
6.32 c                   exfmt     c4winb
6.32 c                   leavesr
6.32 c                   endif
6.31  *
6.31 c                   eval      b_anul = *off
6.31  *
6.31 c                   exfmt     c4win
6.31  *
6.31  * Behandling av funksjonstaster
6.31  *
6.31 c                   if        *inkc or *inkl
6.31 c                   eval      b_anul = *on
6.31 c                   leavesr
6.31 c                   endif
6.31  *
6.31  * overfører betingelse til frakt
6.31 c                   eval      w_fkrt = 0
6.31 c                   eval      w_fprt = 0
6.31  *
6.31 c                   eval      jraelr_erst = b1erst
6.31 c                   eval      jraelr_elin = b1elin
6.31 c     jraelr_key    chain     jraelr
6.31 c                   if        %found(jraelr)
6.31 c                   select
6.31 c                   when      jpereg = '1'
6.31 c                   eval(h)   w_fprt = jpever
6.31 c                   when      jpereg = '2'
6.31 c                   eval(h)   w_fprt = jpever
6.31 c                   when      jpereg = 'K'
6.31 c                   eval      w_fkrt = jpever
6.31 c                   endsl
6.31 c                   endif
6.31  *
6.31  *  Danner linje på betingelse
6.31  *
5.31 c                   if        w_fprt <> 0 or
5.31 c                             w_fkrt <> 0
6.31 c                   clear                   jfbelur
6.31  *
6.3x c                   eval      jfbelu_fprg = w_prgr
6.31 c                   eval      jfbelu_fogr = w_ogrp
6.31 c                   eval      jfbelu_fhgr = w_hgrp
6.31 c                   eval      jfbelu_fugr = w_ugrp
6.31 c                   eval      jfbelu_fmod = w_modn
6.31 c                   eval      jfbelu_fvar = w_vare
6.31 c                   eval      jfbelu_fvty = w_vtyp
6.31 c                   eval      jfbelu_flet = w_lety
6.31 c     jfbelu_key    chain     jfbelur                            90
6.31 c                   if        w_fprt <> 0
6.31 c                   eval      jpfprt = w_fprt
6.31 c                   eval      jpfkrt = 0
6.31 c                   else
6.31 c                   eval      jpfkrt = w_fkrt
     c                   eval      jpfprt = 0
6.31 c                   endif
6.31 c                   time                    jpfeda
6.31 c                   time                    jpfeti
6.31 c                   eval      jpfeus = l_user
6.31 c
6.31 c                   eval      jpfbfi = w_firm
6.31 c                   eval      jpfblo = w_ldor
6.3x c                   eval      jpfbpg = w_prgr
6.31 c                   eval      jpfbog = w_ogrp
6.31 c                   eval      jpfbhg = w_hgrp
6.31 c                   eval      jpfbug = w_ugrp
6.31 c                   eval      jpfbmo = w_modn
6.31 c                   eval      jpfbva = w_vare
6.31 c                   eval      jpfbvt = w_vtyp
6.31 c                   eval      jpfbke = w_lety
6.31 c                   eval      jpfbrs = w_rsts
6.31 c                   eval      jpfbli = w_line
6.31 c                   eval      jpfbbe = b2beti
6.31  *
6.31 c                   if        %found(jfbelu)
6.31 c                   update    jfbelur
6.31 c                   else
6.31 c                   eval      jpffir = w_firm
6.31 c                   eval      jpfldo = w_ldor
6.3x c                   eval      jpfprg = jfbelu_fprg
6.31 c                   eval      jpfogr = jfbelu_fogr
6.31 c                   eval      jpfhgr = jfbelu_fhgr
6.31 c                   eval      jpfugr = jfbelu_fugr
6.31 c                   eval      jpfmod = jfbelu_fmod
6.31 c                   eval      jpfvar = jfbelu_fvar
6.31 c                   eval      jpfvty = jfbelu_fvty
6.31 c                   eval      jpflet = jfbelu_flet
6.31 c                   time                    jpfoda
6.31 c                   write     jfbelur
6.31 c                   endif
6.31 c                   endif
6.31 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      jraelr_erst = d1erst
     c                   eval      jraelr_elin = d1elin
     c     jraelr_key    chain     jraelr                             90
      *
     c                   eval      d1efda = 0
     c                   eval      d1etda = 0
      *
     c                   if        jpefda <> *loval
     c     *dmy          move      jpefda        d1efda
     c                   endif
      *
     c                   if        jpetda <> *loval
     c     *dmy          move      jpetda        d1etda
     c                   endif
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
     c                   eval      jraelu_erst = d1erst
     c                   eval      jraelu_elin = d1elin
     c     jraelu_key    chain     jraelur                            90
     c                   delete    jraelur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_erst = k1erst
     c                   eval      s_elin = k1elin
      *
      * Fyller kopierings-bildet
      *
     c                   eval      jraelr_erst = k1erst
     c                   eval      jraelr_elin = k1elin
     c     jraelr_key    chain     jraelr                             90
      *
     c                   eval      k1erae = jperae
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   exsr      spØrring
     c                   goto      k1taga
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1erae = *blank
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1erst = s_erst
     c                   eval      c1elin = s_elin
      *
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil                     SFL END
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      jrael1                                 15
      *
     c                   if        *in15                                        EOF
     c                   leave
     c                   endif
      *
     c                   if        jpefir <> p_firm or
     c                             jpeldo <> p_ldor or
6.3x c                             jpeprg <> p_prgr or
     c                             jpeogr <> p_ogrp or
     c                             jpehgr <> p_hgrp or
     c                             jpeugr <> p_ugrp or
     c                             jpemod <> p_modn or
     c                             jpevar <> p_vare or
     c                             jpevty <> p_vtyp or
     c                             jpelet <> p_lety or
     c                             jpebrs <> p_rsts or
     c                             jpebli <> p_line
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1erst = jperst
     c                   eval      b1elin = jpelin
     c                   eval      b1erae = jperae
     c                   eval      b1ereg = jpereg
     c                   eval      b1ever = jpever
     c                   eval      b1efda = 0
     c                   eval      b1etda = 0
     c                   eval      b1akti = *blank
     c                   eval      b1rkod = *blank
      *
     c                   if        jpefda <> *loval
     c     *dmy          move      jpefda        b1efda
     c                   endif
      *
     c                   if        jpetda <> *loval
     c     *dmy          move      jpetda        b1etda
     c                   endif
      *
     c                   if        jpeakt = 1
     c                   eval      b1akti = 'A'
     c                   endif
      *
     c                   if        jperst = 1
     c                   eval      b1rkod = 'L'
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for aktivisering/passivisering av element
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     aktiviser     begsr
      *
     c                   eval      jraelu_erst =  b1erst
     c                   eval      jraelu_elin =  b1elin
     c     jraelu_key    chain     jraelur                            91
     c                   if        *in91 = *on
     c                   goto      end_aktiviser
     c                   endif
      *
      * Oppdaterer element med valg
      *
     c                   if        jpeakt = 1
     c                   eval      jpeakt = 0
     c                   else
     c                   eval      jpeakt = 1
     c                   endif
      *
     c                   update    jraelur
      *
      * Oppdater subfile
      *
     c                   if        b1valg <> 0
     c                   if        jpeakt = 1
     c                   eval      b1akti = 'A'
     c                   else
     c                   eval      b1akti = *blank
     c                   endif
     c                   endif
      *
     c     end_aktiviser endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ldor
6.3x c                   parm                    p_prgr
     c                   parm                    p_ogrp
     c                   parm                    p_hgrp
     c                   parm                    p_ugrp
     c                   parm                    p_modn
     c                   parm                    p_vare
     c                   parm                    p_vtyp
     c                   parm                    p_lety
     c                   parm                    p_rsts
     c                   parm                    p_line
      *
      * Key for les på rabattelement
      *
     c     jrael1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
6.3x c                   kfld                    w_prgr
     c                   kfld                    w_ogrp
     c                   kfld                    w_hgrp
     c                   kfld                    w_ugrp
     c                   kfld                    w_modn
     c                   kfld                    w_vare
     c                   kfld                    w_vtyp
     c                   kfld                    w_lety
     c                   kfld                    w_rsts
     c                   kfld                    w_line
      *
      * Key for oppslag på rabattelement
      *
     c     jraelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
6.3x c                   kfld                    w_prgr
     c                   kfld                    w_ogrp
     c                   kfld                    w_hgrp
     c                   kfld                    w_ugrp
     c                   kfld                    w_modn
     c                   kfld                    w_vare
     c                   kfld                    w_vtyp
     c                   kfld                    w_lety
     c                   kfld                    w_rsts
     c                   kfld                    w_line
     c                   kfld                    jraelr_erst
     c                   kfld                    jraelr_elin
      *
      * Key for oppdatering av rabattelemet
      *
     c     jraelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
6.3x c                   kfld                    w_prgr
     c                   kfld                    w_ogrp
     c                   kfld                    w_hgrp
     c                   kfld                    w_ugrp
     c                   kfld                    w_modn
     c                   kfld                    w_vare
     c                   kfld                    w_vtyp
     c                   kfld                    w_lety
     c                   kfld                    w_rsts
     c                   kfld                    w_line
     c                   kfld                    jraelu_erst
     c                   kfld                    jraelu_elin
      *
      * Key for oppslag på betingelse
      *
     c     jbetl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
6.3x c                   kfld                    w_prgr
     c                   kfld                    w_ogrp
     c                   kfld                    w_hgrp
     c                   kfld                    w_ugrp
     c                   kfld                    w_modn
     c                   kfld                    w_vare
     c                   kfld                    w_vtyp
     c                   kfld                    w_lety
     c                   kfld                    w_rsts
     c                   kfld                    w_line
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
6.3x  *
6.3x  * Key for oppslag på prisgruppe
6.3x  *
6.3x c     ra30l1_key    klist
6.3x c                   kfld                    w_firm
6.3x c                   kfld                    ra30l1_ekod
      *
      * Key for oppslag på modul
      *
     c     jmodl1_key    klist
     c                   kfld                    jmodl1_modn
      *
      * Key for oppslag på vare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
6.31  *
6.31  * Key for oppdatering av frakt betingelse
6.31  *
6.31 c     jfbelu_key    klist
6.31 c                   kfld                    w_firm
6.31 c                   kfld                    w_ldor
6.3x c                   kfld                    jfbelu_fprg
6.31 c                   kfld                    jfbelu_fogr
6.31 c                   kfld                    jfbelu_fhgr
6.31 c                   kfld                    jfbelu_fugr
6.31 c                   kfld                    jfbelu_fmod
6.31 c                   kfld                    jfbelu_fvar
6.31 c                   kfld                    jfbelu_fvty
6.31 c                   kfld                    jfbelu_flet
      *
      *
      * Henter parametre, og fyller opp skjermbilde og subfile
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_ldor = p_ldor
6.3x c                   eval      w_prgr = p_prgr
     c                   eval      w_ogrp = p_ogrp
     c                   eval      w_hgrp = p_hgrp
     c                   eval      w_ugrp = p_ugrp
     c                   eval      w_modn = p_modn
     c                   eval      w_vare = p_vare
     c                   eval      w_vtyp = p_vtyp
     c                   eval      w_lety = p_lety
     c                   eval      w_rsts = p_rsts
     c                   eval      w_line = p_line
      *
     c                   eval      b2eldo = p_ldor
6.3x c                   eval      b2eprg = p_prgr
     c                   eval      b2eogr = p_ogrp
     c                   eval      b2ehgr = p_hgrp
     c                   eval      b2eugr = p_ugrp
     c                   eval      b2emod = p_modn
     c                   eval      b2evar = p_vare
     c                   eval      b2evty = p_vtyp
     c                   eval      b2elet = p_lety
      *
     c     jbetl1_key    chain     jbetl1
     c                   eval      b2beti = jpbeti
      *
     c                   eval      jlevl1_ldor = b2eldo
     c     jlevl1_key    chain     jlevl1
     c                   eval      b2lnav = jlnavn
      *
     c                   if        b2eogr <> 0 and
     c                             b2ehgr =  0 and
     c                             b2eugr =  0
     c                   eval      vogrl1_oogr = b2eogr
     c     vogrl1_key    chain     vogrl1
     c                   eval      b2utxt = vgotxt
     c                   endif
      *
     c                   if        b2ehgr <> 0 and
     c                             b2eugr =  0
     c                   eval      vhgrl1_hogr = b2eogr
     c                   eval      vhgrl1_hhgr = b2ehgr
     c     vhgrl1_key    chain     vhgrl1
     c                   eval      b2utxt = vghtxt
     c                   endif
      *
     c                   if        b2eugr <> 0
     c                   eval      vugrl1_uogr = b2eogr
     c                   eval      vugrl1_uhgr = b2ehgr
     c                   eval      vugrl1_uugr = b2eugr
     c     vugrl1_key    chain     vugrl1
     c                   eval      b2utxt = vgutxt
     c                   endif
6.3x  *
6.3x c                   if        b2eprg <> *blank
6.3x c                   eval      ra30l1_ekod = b2eprg
6.3x c     ra30l1_key    chain     ra30l1
6.3x c                   eval      b2ptxt = redtxt
6.3x c                   endif
      *
     c                   if        b2emod <> 0
     c                   eval      jmodl1_modn = b2emod
     c     jmodl1_key    chain     jmodl1
     c                   eval      b2mte1 = jumte1
     c                   endif
      *
     c                   if        b2evar <> *blank
     c                   eval      jvarl1_vare = b2evar
     c     jvarl1_key    chain     jvarl1
     c                   eval      b2tek1 = jvtek1
     c                   eval      b2nobb = jvnobb
     c                   endif
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c     jrael1_key    setll     jrael1
      *
     c                   eval      *in22 = *on
     c                   exsr      crt_subfile
      *
     c                   endsr
