     H datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASBYGG
      * Program . . . . : RY132R
      * Beskrivelse . . : Fjerner priser for Jernia hvor Jernia er prisg.
      *
      *                   Fjerner Jernia som prisgiver hvor Jernia ikke
      *                   er vareeier. Bygger nye søketekster
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       210103 HKO  Nytt program
      *       140905 HKO  Byttet program for oppdatering av søktekster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjvprlu    uf   e           k disk    rename(jvprpfr:jvprlur)
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variable i nøkkler
      *
     d jvprlu_vare     s                   like(jxvare)
     d jvprlu_ldor     s                   like(jxldor)
      *
      * Definering av variable
      *
     d b_inlr          s              1
      *
     d w_firm          s              3  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      b_inlr = *on
      *
     c     *loval        setll     jvarl1
     c                   read      jvarl1
     c                   dow       not %eof
      *
     c                   if        jvldor <> 206655
      *
     c                   eval      jvprlu_vare = jvvare
     c                   eval      jvprlu_ldor = 206655
     c     jvprlu_key    chain     jvprlur
     c                   if        %found
     c                   delete    jvprlur
     c                   call      'JV790R'
     c                   parm                    w_firm
     c                   parm                    jvvare
     c                   parm                    b_inlr
     c                   endif
      *
     c                   endif
      *
     c                   read      jvarl1
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppdatering av vare-pris-register
      *
     c     jvprlu_key    klist
     c                   kfld                    jvprlu_vare
     c                   kfld                    jvprlu_ldor
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
