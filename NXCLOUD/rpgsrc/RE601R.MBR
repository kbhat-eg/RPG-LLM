     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
      *                                                                       *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RE601R                                              *
      * Beskrivelse . . : Saldobalanse til PC - utplukk                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      * V5.20     280104  Skrevet om.                                    HFL  *
7.xx  * K000      071020  Utvidet saldofelter                                 *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frhovl1    if   e           k disk    rename(rhovpfr:rhovl1r)
     frhsal1    if   e           k disk    rename(rhsapfr:rhsal1r)
     fre03lu    uf a e           k disk    rename(re03pfr:re03lur)
      *
7.xx d sal             s             15  2 dim(14)                              Saldo
     d saÅ             s             11  2 dim(14)                              Saldo i år
     d saf             s             11  2 dim(14)                              Saldo i fjor
     d sab             s             11  2 dim(14)                              Saldo budsjett
      *
     d                uds
     d pfirm                 300    302  0                                      Firma
     d pnavn                         30                                         Navn
     d puser                         10                                         BruerI
     d pdato                          6  0                                      Pr. da
     d praar                          4  0                                      Pr. år
     d pperf                          2  0                                      Period
     d ppert                          2  0                                      Period
     d pavdf                          4  0                                      Avd. f
     d pavdt                          4  0                                      Avd. t
     d prapp                          2  0                                      Rappor
     d psumm                          1                                         Avd.sum
     d psums                          1                                         Spes.sum
      *
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    970
      *
     d wfkont          s                   like(rhkont)
     d whtext          s                   like(rhtext)
     d w_raar          s                   like(riraar)
     d w_firm          s                   like(rhfirm)
     d w_funn          s              1
     d x               s              3  0
      *
     d rhovl1_kont     s                   like(rhkont)
     d rhsal1_kont     s                   like(rhkont)
      *
      *  Saldo-register - Kontonr.rekkefølge
      *
     irhsal1r       01
      *
     i              risa01                      sal(01)
     i              risa02                      sal(02)
     i              risa03                      sal(03)
     i              risa04                      sal(04)
     i              risa05                      sal(05)
     i              risa06                      sal(06)
     i              risa07                      sal(07)
     i              risa08                      sal(08)
     i              risa09                      sal(09)
     i              risa10                      sal(10)
     i              risa11                      sal(11)
     i              risa12                      sal(12)
     i              risa13                      sal(13)
     i              risa00                      sal(14)
      *
     c/eject
      *
      * Les kontplan
      *
     c     rhovl1_key    setll     rhovl1r                                60
     c     rhovl1_key    reade     rhovl1r                                60
      *
     c                   dow       *in60 = *off
      *
      * Konto behandles bare en gang
      *
     c                   if        rhkont = wfkont
     c                   goto      neste_kto
     c                   endif
      *
     c                   eval      wfkont = rhkont
      *
      * Nullstill tabeller
      *
     c                   eval      saÅ = 0
     c                   eval      saf = 0
     c                   eval      sab = 0
      *
     c                   eval      w_funn = ' '
      *
      * Les saldomatrise
      *
     c                   eval      rhsal1_kont = rhkont
     c     rhsal1_key    setll     rhsal1r                                61
     c     rhsal1_key    reade     rhsal1r                                61
      *
     c                   dow       *in61 = *off
      *
     c                   eval      saÅ = 0
     c                   eval      saf = 0
     c                   eval      sab = 0
      *
     c                   if        riraar < w_raar                              Pr. kto+spes?
     c                   goto      neste_sal
     c                   endif
      *
     c                   if        riavde < pavdf or                            Pr. avdeling?
     c                             riavde > pavdt
     c                   goto      neste_sal
     c                   endif
      *
      * Saldo i år
      *
     c                   if        riraar = praar and
     c                             ritype = 1
     c                   eval      w_funn = '1'
     c                   z-add     sal           saÅ
     c                   endif
      *
      * Saldo i fjor
      *
     c                   if        riraar = w_raar and
     c                             ritype = 1
     c                   eval      w_funn = '1'
     c                   z-add     sal           saf
     c                   endif
      *
      * Budsjett i år
      *
     c                   if        riraar = praar and
     c                             ritype = 3
     c                   eval      w_funn = '1'
     c                   z-add     sal           sab
     c                   endif
      *
      * Skriv/oppdater post hvis saldorecord funnet
      *
     c                   if        w_funn = '1'                                 Pr. kto+spes?
     c                   exsr      summer                                         pr. spes
     c                   eval      w_funn = ' '
     c                   endif
      *
     c     neste_sal     tag
      *
     c     rhsal1_key    reade     rhsal1r                                61
     c                   enddo
      *
     c     neste_kto     tag
      *
     c     rhovl1_key    reade     rhovl1r                                60
     c                   enddo
      *
     c     slutt         tag
      *
     c                   seton                                        lr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for summering                                         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     summer        begsr
      *
     c                   if        psums = 'J'                                  Pr. kto+spes?
     c                   eval      rhspes = *zeros
     c                   else
     c                   eval      rhspes = rispes
     c                   endif
      *
     c                   if        psumm = 'J'                                  Pr. kto+spes?
     c                   eval      rhavde = *zeros
     c                   else
     c                   eval      rhavde = riavde
     c                   endif
      *
     c     re03lu_key    chain     re03lu
     c                   if        %found
      * Akkumulerer hvis funnet
      * Saldo i år
      *
     c                   add       saÅ(14)       rrftÅr
     c                   z-add     ppert         x
     c                   add       saÅ(x)        rrfpÅr
     c                   z-add     1             x
      *
     c                   dou       x > ppert
     c                   add       saÅ(x)        rrftÅr
     c                   add       1             x
     c                   enddo
      *
      * Budsjett i år
      *
     c                   add       sab(14)       rrbtÅr
     c                   z-add     ppert         x
     c                   add       sab(x)        rrbpÅr
     c                   z-add     1             x
      *
     c                   dou       x > ppert
     c                   add       sab(x)        rrbtÅr
     c                   add       1             x
     c                   enddo
      *
      * Saldo i fjor
      *
     c                   add       saf(14)       rrftfj
     c                   z-add     ppert         x
     c                   add       saf(x)        rrfpfj
     c                   z-add     1             x
      *
     c                   dou       x > ppert
     c                   add       saf(x)        rrftfj
     c                   add       1             x
     c                   enddo
     c                   update    re03lur
      *
     c                   else
      * Ny post hvis ikke funnet
      * Saldo i år
      *
     c                   z-add     saÅ(14)       rrftÅr
     c                   z-add     ppert         x
     c                   z-add     saÅ(x)        rrfpÅr
     c                   z-add     1             x
      *
     c                   dou       x > ppert
     c                   add       saÅ(x)        rrftÅr
     c                   add       1             x
     c                   enddo
      *
      * Budsjett i år
      *
     c                   z-add     sab(14)       rrbtÅr
     c                   z-add     ppert         x
     c                   z-add     sab(x)        rrbpÅr
     c                   z-add     1             x
      *
     c                   dou       x > ppert
     c                   add       sab(x)        rrbtÅr
     c                   add       1             x
     c                   enddo
      *
      * Saldo i fjor
      *
     c                   z-add     saf(14)       rrftfj
     c                   z-add     ppert         x
     c                   z-add     saf(x)        rrfpfj
     c                   z-add     1             x
      *
     c                   dou       x > ppert
     c                   add       saf(x)        rrftfj
     c                   add       1             x
     c                   enddo
      *
      * Legg ut record
      *
     c                   if        rrfpÅr <> *zeros or                          Ikke skriv
     c                             rrftÅr <> *zeros or                           post dersom
     c                             rrbpÅr <> *zeros or                            alle beløp
     c                             rrbtÅr <> *zeros or                             er null.
     c                             rrfpfj <> *zeros or
     c                             rrftfj <> *zeros
     c                   write     re03lur                                       SKRIV POST
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c                   eval      x = 0
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_raar = praar - 1
     c                   eval      rr9per = ppert
     c                   eval      rr9aar = praar
      *
     c                   eval      rrfpÅr = *zeros
     c                   eval      rrftÅr = *zeros
     c                   eval      rrbpÅr = *zeros
     c                   eval      rrbtÅr = *zeros
     c                   eval      rrfpfj = *zeros
     c                   eval      rrftfj = *zeros
     c                   eval      wfkont = *zeros
      *
      * Key for konto
      *
     c     rhovl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for saldomatrise
      *
     c     rhsal1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhsal1_kont
      *
      * Key for RE03LU overføring til PC
      *
     c     re03lu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhkont
     c                   kfld                    rhspes
     c                   kfld                    rhavde
      *
     c                   endsr
