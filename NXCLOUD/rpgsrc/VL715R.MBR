     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VL715R
      * Beskrivelse . . : Henter varetype,utg.dato, lev.nr og beholdning
      *                   fra lager
      *
      *                   beholdning = lagerbeholdning - i ordre
      *                   Hvis lagerenhet <> salgsenhet 1, så omberegnes beholdn
      *
      *                   Input : firma, vare, lager
      *                   Output: varetype, utg.dato, lev.nr, beholdning
      *
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       060114 bof  Nytt program
6.30  *       260215 bof  Må lese vvarpf for å finne varetype hvis ikke lager
6.31  *       180815 bof  Feilretting for å finne enheter
6.32  *       090915 bof  Feilretting omregningsfaktore
6.33  *       131015 nho  Utelate ' i ordre' dersom 'D' eller 'T' vare
6.34  *       101115 bof  Feilretting vedr. gyldig salgsenhet.
6.35  *       310516 bof  Skal ikke vise beholdning på skaffevare.
6.36  *       310516 bof  Feilretting vedr. omregning
6.37  *       051216 bof  Feilretting vedr. omregning   II
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     fvvenl2    if   e           k disk    rename(vvenpfr:vvenl2r)
6.36 fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
      *
      * Definering av variable i nøkler
      *
     d vlagl1_firm     s                   like(vlfirm)
     d vlagl1_vare     s                   like(vlvare)
     d vlagl1_lage     s                   like(vllage)
     d vvarl1_firm     s                   like(vvfirm)
     d vvarl1_vare     s                   like(vvvare)
     d fstsl1_firm     s                   like(faafir)
     d vvenl2_vare     s                   like(vevare)
     d vvenl2_saen     s                   like(vesaen)
6.36 d vvenl1_vare     s                   like(vevare)
6.36 d vvenl1_enhe     s                   like(veenhe)
      *
      * Definering av pamametre
      *
     d p_firm          s                   like(vlfirm)
     d p_vare          s                   like(vlvare)
     d p_lage          s                   like(vllage)
     d p_vtyp          s                   like(vlvtyp)
     d p_udat          s                   like(vludat)
     d p_ldor          s                   like(vlbldo)
     d p_behl          s                   like(vlbehl)
     d p_inlr          s              1
      *
      * Definering av variable
      *
     d w_firm          s                   like(vefirm)
6.36 d w_enhe          s                   like(veenhe)
6.36 d w_omre          s                   like(veomre)
     d w_enh1          s                   like(veenhe)
     d w_enh2          s                   like(veenhe)
     d w_enh3          s                   like(veenhe)
6.36 d w_enh4          s                   like(veenhe)
     d w_omr1          s                   like(veomre)
     d w_omr2          s                   like(veomre)
     d w_omr3          s                   like(veomre)
6.36 d w_omr4          s                   like(veomre)
      *
     d                uds
     d l_user                911    920
     d l_sfir                944    946  0
     d l_savd                947    950  0
     d l_lagr               1001   1002  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      p_behl = 0
      *
      * Initierer output-parametre
      *
     c                   eval      p_vtyp = *blank
     c                   eval      p_udat = *loval
     c                   eval      p_ldor = 0
      *
     c                   if        faalag = 1
      *
6.36 c                   eval      vvarl1_firm = p_firm
6.36 c                   eval      vvarl1_vare = p_vare
6.36 c     vvarl1_key    chain     vvarl1
      *
     c                   exsr      hent_enhe
      *
      * Henter prisgruppe
      *
     c                   eval      vlagl1_firm = p_firm
     c                   eval      vlagl1_vare = p_vare
     c                   eval      vlagl1_lage = p_lage
     c     vlagl1_key    chain     vlagl1
     c                   if        %found(vlagl1)
     c                   if        vlvtyp <> *blank
     c                   eval      p_vtyp = vlvtyp
     c                   endif
     c                   if        vludat <> *loval
     c                   eval      p_udat = vludat
     c                   endif
     c                   if        vlbldo <> 0
     c                   eval      p_ldor = vlbldo
     c                   endif
6.33  *
6.33 c                   if        vlvtyp <> 'D' and
6.35 c                             vlvtyp <> 'T' and
6.35 c                             vlvtyp <> 'S'
     c                   eval      p_behl = vlbehl - vloord
6.33 c                   endif
      *
     c                   endif
      *
     c                   eval      vvarl1_firm = p_firm
     c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found(vvarl1)
      *
6.36 c                   if        vvenhl <> w_enhe
     c                   select
6.37 c                   when      w_enhe = w_enh1
6.36 c                   eval      p_behl = p_behl *  w_omr1
6.37 c                   when      w_enhe = w_enh2
6.36 c                   eval      p_behl = p_behl *  w_omr2
6.37 c                   when      w_enhe = w_enh3
6.36 c                   eval      p_behl = p_behl *  w_omr3
6.37 c                   when      w_enhe = w_enh4
6.36 c                   eval      p_behl = p_behl *  w_omr4
     c                   other
     c                   eval      p_behl = 0
     c                   endsl
     c                   endif
      *
     c                   if        p_vtyp = *blank
     c                   eval      p_vtyp = vvvtyp
     c                   endif
      *
     c                   if        p_udat = *loval
     c                   eval      p_udat = vvudat
     c                   endif
      *
     c                   if        p_ldor = 0
     c                   eval      p_ldor = vvldor
     c                   endif
      *
     c                   endif
      *
6.30 c                   else
6.30  *
6.30 c                   if        p_vtyp = *blank
6.30 c                   eval      vvarl1_firm = p_firm
6.30 c                   eval      vvarl1_vare = p_vare
6.30 c     vvarl1_key    chain     vvarl1
6.30 c                   if        %found(vvarl1)
6.30 c                   eval      p_vtyp = vvvtyp
6.30 c                   endif
6.30 c                   endif
6.30  *
6.30 c                   if        p_udat = *loval
6.30 c                   eval      vvarl1_firm = p_firm
6.30 c                   eval      vvarl1_vare = p_vare
6.30 c     vvarl1_key    chain     vvarl1
6.30 c                   if        %found(vvarl1)
6.30 c                   eval      p_udat = vvudat
6.30 c                   endif
6.30 c                   endif
6.30  *
6.30 c                   if        p_ldor = 0
6.30 c                   eval      vvarl1_firm = p_firm
6.30 c                   eval      vvarl1_vare = p_vare
6.30 c     vvarl1_key    chain     vvarl1
6.30 c                   if        %found(vvarl1)
6.30 c                   eval      p_ldor = vvldor
6.30 c                   endif
6.30 c                   endif
      *
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
      *
     c                   if        p_inlr <> *on
     c                   eval      *inlr = *on
     c                   endif
      *
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_enhe     begsr
      *
      * Henter tre salgsenheter fra varen
      *
     c                   eval      w_enh1 = *blank
     c                   eval      w_enh2 = *blank
     c                   eval      w_enh3 = *blank
6.36 c                   eval      w_enh4 = *blank
     c                   eval      w_omr1 = 0
     c                   eval      w_omr2 = 0
     c                   eval      w_omr3 = 0
6.36 c                   eval      w_omr4 = 0
6.31  *
6.31  * finner basis enhet
6.36 c                   eval      w_omre = 1
6.36 c                   eval      vvenl1_vare = p_vare
6.36 c                   eval      vvenl1_enhe = vvenhe
6.36 c     vvenl1_key    chain     vvenl1
6.36 c                   if        %found(vvenl1)
6.36 c                   eval      w_enhe = veenhe
6.36 c                   eval      w_omre = veomre
6.36 c                   endif
      *
     c                   eval      vvenl2_vare = p_vare
6.31 c*                  eval      vvenl2_saen = 0
6.31 c     vvenl2_key2   setll     vvenl2
     c     vvenl2_key2   reade     vvenl2
     c                   dow       not %eof
6.36  *
6.36 c*                  if        vesaen <> 0
      *
     c                   select
     c                   when      w_enh1 = *blank
     c                   eval      w_enh1 = veenhe
     c                   eval      w_omr1 = veomre
     c                   when      w_enh1 <> *blank and
     c                             w_enh2 = *blank
     c                   eval      w_enh2 = veenhe
     c                   eval      w_omr2 = veomre
     c                   when      w_enh1 <> *blank and
     c                             w_enh2 <> *blank and
     c                             w_enh3 = *blank
     c                   eval      w_enh3 = veenhe
     c                   eval      w_omr3 = veomre
6.36 c                   when      w_enh1 <> *blank and
6.36 c                             w_enh2 <> *blank and
6.36 c                             w_enh3 <> *blank and
6.36 c                             w_enh4 = *blank
6.36 c                   eval      w_enh4 = veenhe
6.36 c                   eval      w_omr4 = veomre
     c                   leave
     c                   endsl
6.36  *
6.36 c*                  endif
      *
     c     vvenl2_key2   reade     vvenl2
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_lage
     c                   parm                    p_vtyp
     c                   parm                    p_udat
     c                   parm                    p_ldor
     c                   parm                    p_behl
     c                   parm                    p_inlr
      *
      * Key for les på lager-register
      *
     c     vlagl1_key    klist
     c                   kfld                    vlagl1_firm
     c                   kfld                    vlagl1_vare
     c                   kfld                    vlagl1_lage
      *
      * Key for les på vare-register
      *
     c     vvarl1_key    klist
     c                   kfld                    vvarl1_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på OFAK-kode (til)
      *
     c     fstsl1_key    klist
     c                   kfld                    fstsl1_firm
      *
      * Key for les på vare-enhet
      *
     c     vvenl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
     c                   kfld                    vvenl2_saen
      *
     c     vvenl2_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
6.36  *
6.36 c     vvenl1_key    klist
6.36 c                   kfld                    w_firm
6.36 c                   kfld                    vvenl1_vare
6.36 c                   kfld                    vvenl1_enhe
      *
     c                   eval      w_firm = p_firm
      *
     c                   eval      fstsl1_firm = p_firm
     c     fstsl1_key    chain     fstsl1                             91
      *
     c                   endsr
