     h option(*nodebugio) datedit(*dmy)
     h decedit('0,')
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VP611R
      * Beskrivelse . . : Leser csv fil - kolonne
      *                   A- varenr
      *                   E-enhet
      *                   F-Evr. leverandør
      *                   I-prisgruppe
      *                   M-prisdato
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       240222 bof  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     flwexpf    uf   e           k disk
     fvvprlu    uf   e           k disk    rename(vvprpfr:vvprlur)
     f*lwexut    uf a e           k disk    rename(lexcpfr:lwexutr)


      *
5.70 d vvprlu_vare     s                   like(vpvare)
5.70 d vvprlu_prgr     s                   like(vpprgr)
5.70 d vvprlu_ldor     s                   like(vpldor)
     d vvprlu_enhe     s                   like(vpenhe)
     d vvprlu_lety     s                   like(vplety)
     d vvprlu_pdat     s                   like(vppdat)

      *
      * Definering av datastruktur
      *
     d                 ds
     d d_inpu                       300
     d  d_ldor                        6    overlay(d_inpu:1)
     d  d_enhe                        3    overlay(d_inpu:7)
     d  d_vare                       15    overlay(d_inpu:10)
6.32 d  d_prgr                        1    overlay(d_inpu:25)
6.32 d  d_pdat                       10    overlay(d_inpu:26)
      *
      * Definering av parametre
      *
     d w_jkey          s             41
      *
      * Definering av variable i nøkler
      *
      * Definering av variable
      *
     d b_ok            s              1    inz(*off)
     d w_posi          s              4  0
      *
     d w_6x            s              6
     d len             s              3  0
     d st              s              3  0


     d w_firm          s                   like(vpfirm)
     d w_vare          s                   like(vpvare)
     d w_ldor          s                   like(vpldor)
     d w_pdat          s                   like(vppdat)
     d w_retk          s              1
     d w_jtxt          s            200
     d w_jnav          s             15
     d w_jsta          s              2  0
     d w_rad           s            512
     d w_text          s             50
     d w_fldr          s             20
     d w_ffil          s             10
     d w_fmbr          s             10
     d w_fnav          s             25
     d w_ftyp          s              3
      *
      * Definering av konstanter
      *
     d Lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d Up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
     d digits          c                   ' 0123456789'
     d c_null          s             15    inz('000000000000000')
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
6.3q C/Exec SQL
6.3q C+  Set Option DatFmt=*ISO , commit=*none
6.3q C/End-Exec
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.40 c                   eval      w_jnav = 'SLETTE_VP411R'
6.40 c                   call      'AB700R  '
6.40 c                   parm                    w_jnav
6.40 c                   parm                    w_jkey
6.40 c                   parm                    w_jtxt

6.31
     c                   eval      w_jtxt = 'Starter sletting  '
     c                   eval      w_jsta = 10
     c                   call      'AB705R  '
     c                   parm                    w_jkey
     c                   parm                    w_jsta
     c                   parm                    w_jtxt
      *
9.02  *
      * Leser input-fil, og sletter varer fra sortiment
      *
     c                   read      lwexpf
     c                   dow       not %eof
      *
     c                   eval      b_ok = *off
     c                   eval      d_inpu = *blank
     c                   exsr      utled_ds
      *
     c                   if        b_ok = *on
     c                   exsr      slett_pris
     c                   endif
      *
     c     neste_vare    tag
      *
     c                   read      lwexpf
     c                   enddo
      * dann i forhold til tabell

      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utleding av datastruktur
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     utled_ds      begsr
     c                   eval      b_ok = *off
      *
     c     '"':' '       xlate     exclin        exclin
     c     'ñ':' '       xlate     exclin        exclin
      *
      * kol A - varenr
      *
     c                   eval      w_posi = %scan(';':exclin)
     c                   eval      d_vare = %trim(%subst(exclin:1:w_posi-1))
      *
      * kol B - ikke i bruk
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:1024-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
      *
      * kol C - ikke i bruk
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:1024-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
      *
      * kol d - ikke i bruk
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:1024-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
      *
      * kol E - enhet .
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:1024-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
6.32 c                   eval      d_enhe = %trim(%subst(exclin:1:w_posi-1))
6.32 c     lo:up         xlate     d_enhe        d_enhe
6.32  *
6.32  * kol F - leverandør
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:1024-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
     c                   eval      d_ldor = %trim(%subst(exclin:1:w_posi-1))
     c                   if        %check(digits:d_ldor) <> 0
     c                   leavesr
     c                   endif
     c                   eval      w_ldor = %dec(d_ldor:6:0)
      *
      * kol G - ikke i bruk
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:1024-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
      *
      * kol H - ikke i bruk
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:1024-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
      *
      * kol i - prisgr.
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:1024-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
6.32 c                   eval      d_prgr = %trim(%subst(exclin:1:w_posi-1))
6.32 c     lo:up         xlate     d_prgr        d_prgr
      *
      * kol j - ikke i bruk
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:1024-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
      *
      * kol k - ikke i bruk
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:1024-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
      *
      * kol l - ikke i bruk
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:1024-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
      *
6.32  *
6.32  * kol M - prisdato
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:1024-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
     c                   if        w_posi > 0
     c                   eval      d_pdat = %trim(%subst(exclin:1:w_posi-1))
     c                   else
     c                   eval      d_pdat = %trim(%subst(exclin:1:10))
     c                   endif
     c*     '/':'-'       xlate     exclin        exclin
     c                   move      d_pdat        w_pdat
     c*                   eval      w_pdat = %date(d_pdat:*ymd)
      *
     c                   eval      b_ok = *on
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     slett_pris    begsr
5.70 c                   eval      vvprlu_vare = d_vare
5.70 c                   eval      vvprlu_ldor = w_ldor
5.70 c                   eval      vvprlu_prgr = d_prgr
     c                   eval      vvprlu_enhe = d_enhe
     c                   eval      vvprlu_lety = 0
     c                   eval      vvprlu_pdat = w_pdat
     c     vvprlu_key    chain     vvprlu
     c                   if        %found(vvprlu)
     c                   delete    vvprlur
     c                   endif

      *
     c     end_slett     endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *
      * Key for oppdatering av vare-pris
      *
     c     vvprlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvprlu_vare
     c                   kfld                    vvprlu_ldor
     c                   kfld                    vvprlu_prgr
     c                   kfld                    vvprlu_enhe
     c                   kfld                    vvprlu_lety
     c                   kfld                    vvprlu_pdat
      *
     c                   eval      w_firm = l_firm
      *

      *
     c                   endsr
