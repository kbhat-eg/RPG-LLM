     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : JO611R
      * Beskrivelse . . : Matching, Utskrift ikke matchede varer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       210596 HKO  Nytt program
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjmatl4    if   e           k disk    rename(jmatpfr:jmatl4r)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
      *
     fjo611p    o    e             printer
      *
      * Definering av parametre
      *
     d p_parm          s             64
      *
      * Definering av Local data area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
      *
      * Datastruktur for parameterliste -inn
      *
     d                 ds
     d d_list                        64
     d  d_firm                        3  0 overlay(d_list)
     d  d_ldor                        6  0 overlay(d_list:4)
     d  d_ogrp                        2  0 overlay(d_list:10)
     d  d_hgrp                        2  0 overlay(d_list:12)
     d  d_ugrp                        3  0 overlay(d_list:14)
     d  d_va1f                       15    overlay(d_list:17)
     d  d_va1t                       15    overlay(d_list:32)
      *
      * Definering av variable i nøkler
      *
     d jmatl4_vkod     s                   like(jovkod)
     d jmatl4_ldor     s                   like(joldor)
     d jmatl4_ogrp     s                   like(joogrp)
     d jmatl4_hgrp     s                   like(johgrp)
     d jmatl4_ugrp     s                   like(jougrp)
     d rlevl1_levr     s                   like(rllevr)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
      *
      * Definering av variable
      *
     d b_frst          s              1    inz(*on)
     d utime           s              6  0
      *
     d w_firm          s                   like(jofirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Header
      *
     c                   exsr      hode
      *
      * Detalj
      *
     c                   exsr      detalj
      *
      * Avslutter program
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering og utskrift av hode-opplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode          begsr
      *
      * Legger inn arb.stasjon og bruker i faste felter
      *
     c                   eval      a1firm = d_firm
     c                   eval      a1user = l_user
     c                   eval      a1wsid = l_wsid
     c                   eval      a1date = udate
     c                   time                    utime
     c                   eval      a1time = utime
      *
      * Henter firmaopplysninger
      *
     c     afirl1_key    chain     afirl1                             90
     c                   eval      a1fnav = aafnav
      *
      * Henter parametre med beskrivelse
      *
     c                   eval      a2ldor = d_ldor
     c                   eval      a2ogrp = d_ogrp
     c                   eval      a2hgrp = d_hgrp
     c                   eval      a2ugrp = d_ugrp
     c                   eval      a2va1f = d_va1f
     c                   eval      a2va1t = d_va1t
      *
     c                   if        d_ldor <> 0
     c                   eval      rlevl1_levr = d_ldor
     c     rlevl1_key    chain     rlevl1                             90
     c                   if        *in90 = *off
     c                   eval      a2lnav = rlnavn
     c                   endif
     c                   endif
      *
     c                   if        d_ogrp <> 0
     c                   eval      vogrl1_oogr = d_ogrp
     c     vogrl1_key    chain     vogrl1                             90
     c                   if        *in90 = *off
     c                   eval      a2utxt = vgotxt
     c                   endif
     c                   endif
      *
     c                   if        d_hgrp <> 0
     c                   eval      vhgrl1_hogr = d_ogrp
     c                   eval      vhgrl1_hhgr = d_hgrp
     c     vhgrl1_key    chain     vhgrl1                             90
     c                   if        *in90 = *off
     c                   eval      a2utxt = vghtxt
     c                   endif
     c                   endif
      *
     c                   if        d_ugrp <> 0
     c                   eval      vugrl1_uogr = d_ogrp
     c                   eval      vugrl1_uhgr = d_hgrp
     c                   eval      vugrl1_uugr = d_ugrp
     c     vugrl1_key    chain     vugrl1                             90
     c                   if        *in90 = *off
     c                   eval      a2utxt = vgutxt
     c                   endif
     c                   endif
      *
      * Skriver heading
      *
     c                   write     a1hdr
     c                   write     a2hdr
     c                   write     a3hdr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utplukk til - og utkskrift av detaljlinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     detalj        begsr
      *
     c                   eval      jmatl4_vkod = 0
     c                   eval      jmatl4_ldor = d_ldor
     c                   eval      jmatl4_ogrp = d_ogrp
     c                   eval      jmatl4_hgrp = d_hgrp
     c                   eval      jmatl4_ugrp = d_ugrp
     c     jmatl4_key    setll     jmatl4
     c                   read      jmatl4                                 90
     c                   dow       *in90 = *off
      *
      * Test på vare-oppdateringskode
      *
     c                   if        jovkod <> 0
     c                   leave
     c                   endif
      *
      * Test på matche-kode
      *
     c                   if        jokode <> 0
     c                   goto      neste_vare
     c                   endif
      *
      * Test på leverandør
      *
     c                   if        d_ldor <> 0 and
     c                             joldor <> d_ldor
     c                   leave
     c                   endif
      *
      * Test på overgruppe
      *
     c                   if        d_ogrp <> 0 and
     c                             joogrp <> d_ogrp
     c                   goto      neste_vare
     c                   endif
      *
      * Test på hovedgruppe
      *
     c                   if        d_hgrp <> 0 and
     c                             johgrp <> d_hgrp
     c                   goto      neste_vare
     c                   endif
      *
      * Test på undergruppe
      *
     c                   if        d_ugrp <> 0 and
     c                             jougrp <> d_ugrp
     c                   goto      neste_vare
     c                   endif
      *
      * Test på varenummer
      *
     c                   if        d_va1f <> *blank and
     c                             jovar1 < d_va1f
     c                   goto      neste_vare
     c                   endif
      *
     c                   if        d_va1t <> *blank and
     c                             jovar1 > d_va1t
     c                   goto      neste_vare
     c                   endif
      *
      * Behandling ved brudd på leverandør
      *
     c                   if        d_ldor = 0 and
     c                             joldor <> a4ldor
     c                   eval      a4ldor = joldor
     c                   eval      rlevl1_levr = joldor
     c     rlevl1_key    chain     rlevl1                             91
     c                   if        *in91 = *off
     c                   eval      a4lnav = rlnavn
     c                   else
     c                   eval      a4lnav = *blank
     c                   endif
     c                   if        b_frst = *on
     c                   eval      b_frst = *off
     c                   else
     c                   write     a1hdr
     c                   write     a3hdr
     c                   endif
     c                   write     a4hdr
     c                   endif
      *
      * Flytter verdier til detaljlinje
      *
     c                   eval      d1ogrp = joogrp
     c                   eval      d1hgrp = johgrp
     c                   eval      d1ugrp = jougrp
     c                   eval      d1tek1 = jotek1
     c                   eval      d1tek2 = jotek2
     c                   eval      d1var1 = jovar1
     c                   eval      d1lva1 = jolvar
      *
      * Skriver detaljlinje
      *
     c                   exsr      skriv_dtl
      *
     c     neste_vare    tag
      *
     c                   read      jmatl4                                 90
      *
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift av detaljlinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_dtl     begsr
      *
      * Skriv detaljlinje
      *
     c                   write     d1dtl                                30
      *
     c                   exsr      overflow
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for overflow
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     overflow      begsr
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   write     a3hdr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_parm
      *
      * Key for les på matcheregister
      *
     c     jmatl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jmatl4_vkod
     c                   kfld                    jmatl4_ldor
     c                   kfld                    jmatl4_ogrp
     c                   kfld                    jmatl4_hgrp
     c                   kfld                    jmatl4_ugrp
      *
      * Key for oppslag på firma
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      *
      * Flytter parameter til datasturuktur
      *
     c                   eval      d_list = p_parm
     c                   eval      w_firm = d_firm
      *
     c                   eval      a4ldor = 999999
      *
     c                   endsr
