     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLOGI
      * Program . . . . : MM702R
      * Beskrivelse . . : Oppdaterer salg i 5 uker tilbake og tilbake 20 uke
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       251104 bø   Nytt program
      *       010307 bø   Justert reade
5.60  *       070806 bof  Muliggjøre å ta abc-kode fra lager
5.70  *       151208 bof  Utvidet med varetype på lager
      * 6.10  220609 BSA  Oppdatert med sporingsinformasjon
6.20  * 6.20  071110 bof  Utvidet med lager på sortiment -satt til 0
8.01  *PRG2   220622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner - COMP
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsstali    if   e           k disk    rename(sstapfr:sstalir)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvarlr    if   e           k disk    rename(vvarpfr:vvarlrr)
     fvvarl8    if   e           k disk    rename(vvarpfr:vvarl8r)
     fvsdtl6    if   e           k disk    rename(vsdtpfr:vsdtl6r)
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     fmmatlr    if   e           k disk    rename(mmatpfr:mmatlrr)
     fmprulu    uf a e           k disk    rename(mprupfr:mprulur)
      *
      *
     d mmatlr_vsor     s                   like(mmvsor)
     d mmatlr_ogrp     s                   like(mmogrp)
     d mmatlr_hgrp     s                   like(mmhgrp)
     d mmatlr_ugrp     s                   like(mmugrp)
     d mmatlr_ldor     s                   like(mmldor)
     d mmatlr_vare     s                   like(mmvare)
     d mprulu_aar      s                   like(mraar)
     d mprulu_uke      s                   like(mruke)
     d mprulu_lage     s                   like(mrlage)
     d mprulu_vare     s                   like(mrvare)
     d vlagl1_vare     s                   like(vlvare)
     d vvarl8_ogrp     s                   like(vvogrp)
     d vvarl8_hgrp     s                   like(vvhgrp)
     d vvarl8_ugrp     s                   like(vvugrp)
     d vvarlx_ogrp     s                   like(vvogrp)
     d vvarlx_hgrp     s                   like(vvhgrp)
     d vvarly_ogrp     s                   like(vvogrp)
     d vvarlr_vare     s                   like(vvvare)
     d vsdtl6_vsor     s                   like(vdvsor)
6.20 d vsdtl6_lage     s                   like(vdlage)
      *
      * Parameter-inn
     d p_dato          s               d   datfmt(*iso)
     d p_antu          s              3  0
      *
      * Local Data Area
      *
     d                uds
      *
6.10 d l_user                911    920
     d l_firm                944    946  0
      *
     d                 ds
     d d_list                       128
     d  d_vsor                       10    overlay(d_list)
     d  d_ogrp                        2  0 overlay(d_list:11)
     d  d_hgrp                        2  0 overlay(d_list:13)
     d  d_ugrp                        3  0 overlay(d_list:15)
     d  d_ldor                        6  0 overlay(d_list:18)
     d  d_vare                        8  0 overlay(d_list:24)
     d  d_crdo                        1  0 overlay(d_list:32)
     d  d_dato                        6  0 overlay(d_list:33)
     d  d_antu                        2  0 overlay(d_list:39)
      *
      * Definering av variable i nøkler
      *
     d sstali_puke     s                   like(sfpuke)
     d sstali_paar     s                   like(sfpaar)
     d sstali_vare     s                   like(sfvare)
     d sstali_lage     s                   like(sflage)
      *
      * Definering av variable
     d ar              s             11  3 DIM(100)
     d a_uke           s              2  0 DIM(100)
     d a_aar           s              4  0 DIM(100)
     d i               s              3  0
     d j               s              3  0
      *
     d w_dato          s               d   datfmt(*iso)
     d w_pdat          s               d   datfmt(*iso)
     d w_paar          s              4  0
     d w_dat60         s              6  0
     d w_puke          s              2  0
     d w_psts          s              1
     d w_firm          s                   like(sffirm)
     d w_salg          s                   like(sfanta)
     d w_uaar          s              4  0
     d w_uke           s              2  0
     d w_list          s            128
5.60 d w_kabc          s                   like(mrkabc)
      *
     d b_ok            s              1    inz(*off)
     d b_matr          s              1    inz(*off)
     d b_stat          s              1    inz(*off)
      *
     d w_aaruk         ds
     d sfpaar                         4  0
     d sfpuke                         2  0
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   exsr      ukeaar
      *
     c                   if        b_ok = *off
     c                   goto      avslutt
     c                   endif
      *
     c                   if        d_vsor <> *blank or
     c                             d_ogrp <> 0 or
     c                             d_hgrp <> 0 or
     c                             d_ugrp <> 0 or
     c                             d_vare <> 0
     c                   exsr      les_utvalg
     c                   else
      *
     c                   read      vvarl1
     c                   dow       not %eof
      *
     c                   exsr      beh_vare
      *
     c                   read      vvarl1
     c                   enddo
      *
     c                   endif
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
      *
     c                   return
      * --------------------------------------------------------------
      *  leser utvalg av varer
      * --------------------------------------------------------------
      *
     c     les_utvalg    begsr
      *
      *
     c                   if        d_vare <> 0
     c                   exsr      les_vare
     c                   goto      end_utvalg
     c                   endif
     c                   if        d_vsor <> *blank
     c                   exsr      les_vsor
     c                   goto      end_utvalg
     c                   endif
      *
     c                   if        d_ugrp <> 0
     c                   exsr      les_ugr
     c                   goto      end_utvalg
     c                   endif
      *
     c                   if        d_hgrp <> 0
     c                   exsr      les_hgr
     c                   goto      end_utvalg
     c                   endif
      *
     c                   if        d_ogrp <> 0
     c                   exsr      les_ogr
     c                   goto      end_utvalg
     c                   endif
      *
     c     end_utvalg    endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Leser tom leverandør på vareregister kaller oppdatering av lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_vare      begsr
      *
     c                   movel     d_vare        vvarlr_vare
     c     vvarlr_key    chain     vvarlr
     c                   if        %found(vvarlr)
     c                   exsr      beh_vare
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Leser varesortiment
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_vsor      begsr
      *
     c                   eval      vsdtl6_vsor = d_vsor
6.20 c                   eval      vsdtl6_lage = 0
     c     vsdtl6_key    setll     vsdtl6
     c     vsdtl6_key    reade     vsdtl6
     c                   dow       not %eof(vsdtl6)
     c                   eval      vvarlr_vare = vdvare
     c     vvarlr_key    chain     vvarlr
     c                   if        %found(vvarlr)
     c                   exsr      beh_vare
     c                   endif
     c     vsdtl6_key    reade     vsdtl6
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Leser tom ugr på vareregister kaller behandling av vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_ugr       begsr
      *
     c                   eval      vvarl8_ogrp = d_ogrp
     c                   eval      vvarl8_hgrp = d_hgrp
     c                   eval      vvarl8_ugrp = d_ugrp
     c     vvarl8_key    setll     vvarl8
     c     vvarl8_key    reade     vvarl8
     c                   dow       not %eof(vvarl8)
     c                   exsr      beh_vare
     c     vvarl8_key    reade     vvarl8
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Leser tom hgr på vareregister kaller behandling av vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_hgr       begsr
      *
     c                   eval      vvarlx_ogrp = d_ogrp
     c                   eval      vvarlx_hgrp = d_hgrp
     c     vvarlx_key    setll     vvarl8
     c     vvarlx_key    reade     vvarl8
     c                   dow       not %eof(vvarl8)
     c                   exsr      beh_vare
     c     vvarlx_key    reade     vvarl8
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Leser tom ogr på vareregister kaller oppdatering av lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_ogr       begsr
      *
     c                   eval      vvarly_ogrp = d_ogrp
     c     vvarly_key    setll     vvarl8
     c     vvarly_key    reade     vvarl8
     c                   dow       not %eof(vvarl8)
     c                   exsr      beh_vare
     c     vvarly_key    reade     vvarl8
     c                   enddo
      *
     c                   endsr
      *
      * --------------------------------------------------------------
      *  behandler lest vare
      * --------------------------------------------------------------
      *
     c     beh_vare      begsr
      *
     c                   exsr      rec_ktrl
     c                   if        b_ok   = *on
      *
      *
     c                   if        b_ok   = *on
      *
      * behandler for hvert lager
     c                   eval      vlagl1_vare = vvvare
readec     vlagl1_key    setll     vlagl1
readec     vlagl1_key    reade     vlagl1
readec                   dow       not %eof(vlagl1)
     c                   if        mmlage <> 0 and
     c                             mmlage <> vllage
     c                   goto      neste
     c                   endif
      *
      * sjekker varetype her
5.70  *
5.70 c                   if        vlvtyp <> 'L' and
5.70 c                             vlvtyp <> 'S'
5.70 c                   goto      neste
5.70 c                   endif
      *
5.60  * hvis lager har abc-kode brukes denne
5.60 c                   if        vlkabc <> *blank
5.60 c                   eval      w_kabc = vlkabc
5.60 c                   endif
      *
      * finner salg pr. uke
     c                   eval      j = 1
     c                   dow       j < 100
     c                   eval      w_uke  = a_uke(j)
     c                   eval      w_uaar = a_aar(j)
     c                   if        w_uke  <> 0 and
     c                             w_uaar <> 0
     c                   exsr      finn_salg
     c                   endif
     c                   eval      j = j + 1
     c                   enddo
      *
     c     neste         tag
      *
readec     vlagl1_key    reade     vlagl1
     c                   enddo
      *
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for kontroll mot parameter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     rec_ktrl      begsr
      *
     c                   eval      b_ok = *off
      *
     c                   if        vvfirm <> l_firm
     c                   leavesr
     c                   endif
      *
     c*                  if        vvvtyp <> 'L' and
     c*                            vvvtyp <> 'S'
     c*                  leavesr
     c*                  endif
      * varegruppe
     c                   if        d_ogrp <> 0 and
     c                             vvogrp <> d_ogrp
     c                   leavesr
     c                   endif
     c                   if        d_hgrp <> 0 and
     c                             vvhgrp <> d_hgrp
     c                   leavesr
     c                   endif
     c                   if        d_ugrp <> 0 and
     c                             vvugrp <> d_ugrp
     c                   leavesr
     c                   endif
      *
5.60 c                   eval      w_kabc = vvkabc
      *
      * Sett OK-indikator - record skal være med
      *
     c                   eval      b_ok = *on
      *
     c     end_ktrl      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for les av parameter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_parm      begsr
      *
     c                   eval      b_matr = *off
      *
     c                   eval      mmatlr_vsor  = *blank
     c                   eval      mmatlr_ogrp  = 0
     c                   eval      mmatlr_hgrp  = 0
     c                   eval      mmatlr_ugrp  = 0
     c                   eval      mmatlr_ugrp  = 0
     c                   eval      mmatlr_ldor  = 0
     c                   eval      mmatlr_vare  = vvvare
     c     mmatlr_key    chain     mmatlr
     c                   if        %found  and
     c                             mmakti  = 0
     c                   eval      b_matr  = *on
     c                   leavesr
     c                   endif
      *
     c                   eval      mmatlr_ogrp  = vvogrp
     c                   eval      mmatlr_hgrp  = vvhgrp
     c                   eval      mmatlr_ugrp  = vvugrp
     c                   eval      mmatlr_ldor  = vvldor
     c                   eval      mmatlr_vare  = vvvare
     c     mmatlr_key    chain     mmatlr
     c                   if        %found and
     c                             mmakti  = 0
     c                   eval      b_matr  = *on
     c                   leavesr
     c                   endif
      *
     c                   eval      mmatlr_vare  = *blank
     c     mmatlr_key    chain     mmatlr
     c                   if        %found and
     c                             mmakti  = 0
     c                   eval      b_matr  = *on
     c                   leavesr
     c                   endif
      *
     c                   eval      mmatlr_ldor  = 0
     c     mmatlr_key    chain     mmatlr
     c                   if        %found and
     c                             mmakti  = 0
     c                   eval      b_matr  = *on
     c                   leavesr
     c                   endif
      *
     c                   eval      mmatlr_ugrp  = 0
     c     mmatlr_key    chain     mmatlr
     c                   if        %found  and
     c                             mmakti  = 0
     c                   eval      b_matr  = *on
     c                   leavesr
     c                   endif
      *
     c                   eval      mmatlr_hgrp  = 0
     c     mmatlr_key    chain     mmatlr
     c                   if        %found  and
     c                             mmakti  = 0
     c                   eval      b_matr  = *on
     c                   leavesr
     c                   endif
      *
     c                   eval      mmatlr_ogrp  = 0
     c     mmatlr_key    chain     mmatlr
     c                   if        %found  and
     c                             mmakti  = 0
     c                   eval      b_matr  = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finne statistikkinformasjon salg
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_salg     begsr
     c                   eval      b_stat = *off
     c                   eval      i = 1
     c                   exsr      les_stat
      *
     c                   if        b_stat = *on
      *
      * oppdaterer ukeprognose
      *
     c                   clear                   mprulur
     c                   eval      mprulu_vare = vvvare
     c                   eval      mprulu_lage = vllage
     c                   eval      mprulu_aar  = w_uaar
     c                   eval      mprulu_uke  = w_uke
     c     mprulu_key    chain     mprulu
     c                   eval      mrants = w_salg
5.60 c                   eval      mrkabc = w_kabc
     c                   if        %found(mprulu)
6.10 c                   time                    mredat
6.10 c                   time                    mretim
6.10 c                   eval      mreusr = l_user
     c                   update    mprulur
     c                   else
     c                   eval      mrfirm = l_firm
     c                   eval      mrvare = mprulu_vare
     c                   eval      mrlage = mprulu_lage
     c                   eval      mraar  = mprulu_aar
     c                   eval      mruke  = mprulu_uke
6.10 c                   time                    mrodat
6.10 c                   time                    mrotim
6.10 c                   eval      mruser = l_user
6.10 c                   time                    mredat
6.10 c                   time                    mretim
6.10 c                   eval      mreusr = l_user
     c                   write     mprulur
     c                   endif
     c                   endif
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finne statistikkinformasjon salg
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_stat      begsr
      *
     c                   eval      w_salg = 0
      *
      * Les statistikkposter
      *
     c                   eval      sstali_vare = vvvare
     c                   eval      sstali_lage = vllage
     c                   eval      sstali_paar = w_uaar
     c                   eval      sstali_puke = w_uke
     c     sstali_key    setll     sstali
     c     sstali_key    reade     sstali
readec                   dow       not %eof(sstali)
      *
     c                   if        sflety <> mmlt01 and
     c                             sflety <> mmlt02 and
     c                             sflety <> mmlt03 and
     c                             sflety <> mmlt04 and
     c                             sflety <> mmlt05 and
     c                             sflety <> mmlt06 and
     c                             sflety <> mmlt07 and
     c                             sflety <> mmlt08 and
     c                             sflety <> mmlt09 and
     c                             sflety <> mmlt10
     c                   iter
     c                   endif
      *
     c                   if        sflage =  vllage
     c                   eval(h)   w_salg = w_salg + sfanta
     c                   eval      b_stat = *on
     c                   endif
      *
     c     sstali_key    reade     sstali
     c                   enddo
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finn hvilke uke man skal kjøre prognose for.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ukeaar        begsr
     c                   eval      b_ok = *off
     c     *dmy          move      p_dato        w_dat60
     c     *dmy          test (d)                w_dat60                33
     c                   if        *in33 = *on
     c                   goto      end_uke
     c                   endif
      *
     c                   eval      w_dato = p_dato
      *
     c                   eval      i = 1
     c                   dow       i <= p_antu
     c                   adddur    7:*d          w_dato
      *
     c                   eval      w_pdat = w_dato
     c                   call      'AX711R  '
     c                   parm                    w_pdat
     c                   parm                    w_paar
     c                   parm                    w_puke
     c                   parm                    w_psts
      * uke 53 tas ikke med i MPRUPF er uke 53 gjort om til uke 52
      * dette pga. sammenligningsgrunnlag fra år til år.
     c                   if        w_puke <> 53
     c                   if        w_psts = *blank
     c                   eval      a_aar(i) = w_paar
     c                   eval      a_uke(i) = w_puke
     c                   else
     c                   eval      a_aar(i) = 0
     c                   eval      a_uke(i) = 0
     c                   endif
      *
     c                   eval      i = i + 1
     c                   endif
     c                   enddo
     c                   eval      b_ok = *on
      *
     c     end_uke       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for les av salgsstatistikk
      *
     c     sstali_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sstali_vare
     c                   kfld                    sstali_lage
     c                   kfld                    sstali_paar
     c                   kfld                    sstali_puke
      *
      * Key for les av prognose matrise
      *
     c     mmatlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mmatlr_vsor
     c                   kfld                    mmatlr_ogrp
     c                   kfld                    mmatlr_hgrp
     c                   kfld                    mmatlr_ugrp
     c                   kfld                    mmatlr_ldor
     c                   kfld                    mmatlr_vare
      *
      * Key for les av lager
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl1_vare
      *
      * Key for å oppdatere ukesprognose
      *
     c     mprulu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mprulu_vare
     c                   kfld                    mprulu_lage
     c                   kfld                    mprulu_aar
     c                   kfld                    mprulu_uke
      *
      *  Key for les av varesortiment
      *
     c     vsdtl6_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vsdtl6_vsor
6.20 c                   kfld                    vsdtl6_lage
      *
      *  Key for les av vareregister
      *
     c     vvarlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlr_vare
      *
      *  Key for les av vareregister
      *
     c     vvarl8_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl8_ogrp
     c                   kfld                    vvarl8_hgrp
     c                   kfld                    vvarl8_ugrp
      *
      *  Key for les av vareregister
      *
     c     vvarlx_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlx_ogrp
     c                   kfld                    vvarlx_hgrp
      *
      *  Key for les av vareregister
      *
     c     vvarly_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarly_ogrp
      *
      *
     c     *entry        plist
     c                   parm                    w_list
     c                   eval      d_list = w_list
     c
     c                   eval      w_firm  = l_firm
     c*                  eval      w_dat60 = 151005
     c     *dmy          move      UDATE         p_dato
     c                   subdur    217:*d        p_dato
     c                   eval      p_antu = 26
      *
     c                   endsr
