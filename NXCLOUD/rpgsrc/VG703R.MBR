     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : VG703R
      * Beskrivelse . . : Henter ut kostprisfaktorer på angitt vare
      *                   Henter inntil 5 faktorer på varen
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       271118 ASK  Nytt program
 7.01 * 7.01  240220 ASK  Lagt inn mulighet for å bruke generell faktor uten leverandør
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *                                                                                 4
      *       LR = Avslutt program
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvgkfir    if   e           k disk    rename(vgkfst:vgkfirr)
     fvgkfi1    if   e           k disk    rename(vgkfst:vgkfi1r)
     fvgktir    if   e           k disk    rename(vgktst:vgktirr)
     fvvarl1    if   e           K disk    rename(vvarpfr:vvarl1r)
     fvgkti1    if   e           k disk    rename(vgktst:vgkti1r)
      *
      * Definering av parametre
      *
     d p_vare          s                   like(vgfvar)
     d p_lage          s                   like(vgflag)
     d p_ldor          s                   like(vgflev)
     d p_frec          s            300
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Datastruktur for kostprisfaktorer
      *
     d d_frec          ds
     d  d_fty1                       30
     d  d_fsi1                        1
     d  d_fak1                        8  6
     d  d_kto1                        6  0
     d  d_mot1                        6  0
     d  d_fty2                       30
     d  d_fsi2                        1
     d  d_fak2                        8  6
     d  d_kto2                        6  0
     d  d_mot2                        6  0
     d  d_fty3                       30
     d  d_fsi3                        1
     d  d_fak3                        8  6
     d  d_kto3                        6  0
     d  d_mot3                        6  0
     d  d_fty4                       30
     d  d_fsi4                        1
     d  d_fak4                        8  6
     d  d_kto4                        6  0
     d  d_mot4                        6  0
     d  d_fty5                       30
     d  d_fsi5                        1
     d  d_fak5                        8  6
     d  d_kto5                        6  0
     d  d_mot5                        6  0
      *
      * Definering av variabler i nøkler
      *
     d vgkfir_lage     s              2  0
     d vgkfir_ftyp     s                   like(vgftyp)
     d vgkfir_flev     s                   like(vgflev)
     d vgkfir_fogr     s                   like(vgfogr)
     d vgkfir_fhgr     s                   like(vgfhgr)
     d vgkfir_fugr     s                   like(vgfugr)
     d vgkfir_fvar     s                   like(vgfvar)
     d vgkfi1_lage     s              2  0
     d vgkfi1_flev     s                   like(vgflev)
     d vgktir_ftyp     s                   like(vtftyp)
     d vvarl1_vare     s                   like(vvvare)
     d vgkti1_ftyp     s                   like(vtftyp)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vgfirm)
     d w_fakt          s                   like(vgftyp)
     d w_tell          s              1  0
     d b_feil          s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedrutine - hent ut kostprisfaktorer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Sjekk om gyldig vare
      *
     c                   exsr      les_vare
     c                   if        b_feil = *on
     c                   goto      avslutt
     c                   endif
      *
      * Dersom ikke leverandør er sendt, bruk leverandør på vare
      *
     c                   if        p_ldor = 0
     c                   eval      p_ldor = vvldor
     c                   endif
      *
      * Leser i gjennom kostpris faktor register og finner evt. faktorer på varen
      *
     c                   eval      w_fakt = *blank
     c                   eval      w_tell = 0
     c                   eval      vgkfi1_flev = p_ldor
     c     vgkfi1_key    setll     vgkfi1
     c     vgkfi1_key    reade     vgkfi1r
     c                   dow       not %eof and vgfirm = w_firm
7.01  *
7.01  * Sjekk om gyldig faktor
7.01  *
     c                   if        vgftyp = w_fakt
     c                   goto      les_neste
     c                   else
     c                   exsr      les_faktor
     c                   if        b_feil = *on
     c                   goto      les_neste
     c                   endif
     c                   exsr      finn_fakt
     c                   eval      w_fakt = vgftyp
     c                   endif
    *
     c     les_neste     tag
     c     vgkfi1_key    reade     vgkfi1r
     c                   enddo
7.01  *
7.01  * Sjekker om det er lagt inn generell faktor på faktortyper som ennå ikke er funnet.
7.01  *
7.01 c                   eval      w_fakt = *blank
7.01 c                   eval      p_ldor = 0
7.01 c                   eval      vgkfi1_flev = p_ldor
7.01 c     vgkfi1_key    setll     vgkfi1
7.01 c     vgkfi1_key    reade     vgkfi1r
7.01 c                   dow       not %eof and vgfirm = w_firm
7.01 c                             and vgflev = 0
7.01  *
7.01 c                   if        vgftyp = w_fakt
7.01 c                   goto      les_neste1
7.01 c                   else
7.01 c                   exsr      les_faktor
7.01 c                   if        b_feil = *on
7.01 c                   goto      les_neste1
7.01 c                   endif
7.01 c                   endif
7.01  *
7.01  * Sjekk om faktor allerede er lagt inn eller finn faktor og legg i liste
7.01  *
7.01 c                   exsr      sjekk_fakt
7.01 c                   if        b_feil = *on
7.01 c                   goto      les_neste1
7.01 c                   endif
7.01 c                   exsr      finn_fakt
7.01 c                   eval      w_fakt = vgftyp
7.01*
7.01 c     les_neste1    tag
7.01 c     vgkfi1_key    reade     vgkfi1r
7.01 c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval       p_frec = d_frec
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppslag på vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_vare      begsr
      *
     c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1r
     c                   if        %found
     c                   eval      b_feil = *off
     c                   else
     c                   eval      b_feil = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppslag på faktortype
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_faktor    begsr
      *
     c                   eval      vgkti1_ftyp = vgftyp
     c     vgkti1_key    chain     vgkti1
     c                   if        %found
     c                   eval      b_feil = *off
     c                   else
     c                   eval      b_feil = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne aktuell faktor på faktortype
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_fakt     begsr
      *
     c                   eval      vgkfir_ftyp = vgftyp
     c                   eval      vgkfir_flev = p_ldor
      *
      * Sjekker varenummer
      *
     c                   eval      vgkfir_fogr = 0
     c                   eval      vgkfir_fhgr = 0
     c                   eval      vgkfir_fugr = 0
     c                   eval      vgkfir_fvar = p_vare
     c     vgkfir_key    chain     vgkfirr
     c                   if        %found(vgkfir)
     c                   goto      oppd_ds
     c                   endif
      *
      * Sjekker undergruppe
      *
     c                   eval      vgkfir_fogr = vvogrp
     c                   eval      vgkfir_fhgr = vvhgrp
     c                   eval      vgkfir_fugr = vvugrp
     c                   eval      vgkfir_fvar = *blank
     c     vgkfir_key    chain     vgkfirr
     c                   if        %found(vgkfir)
     c                   goto      oppd_ds
     c                   endif
      *
      * Sjekker hovedgruppe
      *
     c                   eval      vgkfir_fogr = vvogrp
     c                   eval      vgkfir_fhgr = vvhgrp
     c                   eval      vgkfir_fugr = 0
     c                   eval      vgkfir_fvar = *blank
     c     vgkfir_key    chain     vgkfirr
     c                   if        %found(vgkfir)
     c                   goto      oppd_ds
     c                   endif
      *
      * Sjekker overgruppe
      *
     c                   eval      vgkfir_fogr = vvogrp
     c                   eval      vgkfir_fhgr = 0
     c                   eval      vgkfir_fugr = 0
     c                   eval      vgkfir_fvar = *blank
     c     vgkfir_key    chain     vgkfirr
     c                   if        %found(vgkfir)
     c                   goto      oppd_ds
     c                   endif
      *
      * Sjekker leverandør
      *
     c                   eval      vgkfir_fogr = 0
     c                   eval      vgkfir_fhgr = 0
     c                   eval      vgkfir_fugr = 0
     c                   eval      vgkfir_fvar = *blank
     c     vgkfir_key    chain     vgkfirr
     c                   if        not %found(vgkfir)
     c                   goto      end_fakt
     c                   endif
      *
      * Oppdaterer datastruktur
      *
     c     oppd_ds       tag
     c                   eval      w_tell = w_tell + 1
      *
     c                   if        w_tell = 1
     c                   eval      d_fty1 = vgftyp
     c                   eval      d_fsi1 = vtkalk
     c                   eval      d_fak1 = vgffak
     c                   eval      d_kto1 = vthkto
     c                   eval      d_mot1 = vtmotk
     c                   endif
      *
     c                   if        w_tell = 2
     c                   eval      d_fty2 = vgftyp
     c                   eval      d_fsi2 = vtkalk
     c                   eval      d_fak2 = vgffak
     c                   eval      d_kto2 = vthkto
     c                   eval      d_mot2 = vtmotk
     c                   endif
      *
     c                   if        w_tell = 3
     c                   eval      d_fty3 = vgftyp
     c                   eval      d_fsi3 = vtkalk
     c                   eval      d_fak3 = vgffak
     c                   eval      d_kto3 = vthkto
     c                   eval      d_mot3 = vtmotk
     c                   endif
      *
     c                   if        w_tell = 4
     c                   eval      d_fty4 = vgftyp
     c                   eval      d_fsi4 = vtkalk
     c                   eval      d_fak4 = vgffak
     c                   eval      d_kto4 = vthkto
     c                   eval      d_mot4 = vtmotk
     c                   endif
      *
     c                   if        w_tell = 5
     c                   eval      d_fty5 = vgftyp
     c                   eval      d_fsi5 = vtkalk
     c                   eval      d_fak5 = vgffak
     c                   eval      d_kto5 = vthkto
     c                   eval      d_mot5 = vtmotk
     c                   endif
      *
     c     end_fakt      endsr
7.01  *
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  * Subrutine for å finne aktuell faktor på faktortype
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *
7.01 c     sjekk_fakt    begsr
7.01  *
7.01 c                   eval      b_feil = *off
7.01 c                   if        d_fty1 = vgftyp or
7.01 c                             d_fty2 = vgftyp or
7.01 c                             d_fty3 = vgftyp or
7.01 c                             d_fty4 = vgftyp or
7.01 c                             d_fty5 = vgftyp
7.01 c                   eval      b_feil = *on
7.01 c                   endif
7.01  *
7.01 c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_vare
     c                   parm                    p_lage
     c                   parm                    p_ldor
     c                   parm                    p_frec
      *
      * Key for posisjonering på faktortype
      *
     c     vgkfir_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vgkfir_lage
     c                   kfld                    vgkfir_flev
     c                   kfld                    vgkfir_ftyp
     c                   kfld                    vgkfir_fogr
     c                   kfld                    vgkfir_fhgr
     c                   kfld                    vgkfir_fugr
     c                   kfld                    vgkfir_fvar
      *
      * Key for posisjonering på lev/faktor
      *
     c     vgkfi1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vgkfi1_lage
     c                   kfld                    vgkfi1_flev
      *
      * Key for les av faktortype
      *
     c     vgktir_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vgktir_ftyp
      *
      *  Key for file les av vareregister
      *
     c     vvarl1_key    KLIST
     c                   KFLD                    w_firm
     c                   KFLD                    vvarl1_vare
      *
      * Key for les på faktortype
      *
     c     vgkti1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vgkti1_ftyp
      *
     c                   eval      w_firm = l_firm
     c                   eval      vgkfir_lage = p_lage
     c                   eval      vgkfi1_lage = p_lage
      *
     c                   endsr
