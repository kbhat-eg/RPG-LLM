     H decedit('0.') datedit(*dmy-) option(*nodebugio)
     h DftActgrp(*No) Actgrp(*NEW) BndDir('CGIDEV2/CGIDEV2')
      /copy hspecsbnd
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : AP050R
      * Beskrivelse . . : Oppslag mot tjeneste for å returnere Profile Manager
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       100221 bsa  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_figr                934    939
     d l_filg                937    939
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av parametere
      *
     d p_firm          s              3  0
     d p_memb          s             10
     d p_stat          s              1
      *
      * Definering av variable
      *
     d w_token         s           4000
     d w_tokeut        s           4014
     d w_indata        s           2000
     d w_url           s            200
     d w_url2          s            200
     d w_reqf          s            100
     d w_rspf          s            100
     d w_usrn          s             20
     d w_ident         s            221
     d*w_pass          s             50
     d w_reqt          s              6
     d w_enco          s             20
     d w_stat          s              1  0
     d w_kode          s              1
     d w_fell          s              6
     d w_type          s              2
     d w_fast          s             10
     d w_numm          s              8  0
     d w_size          s              8  0
     d w_start         s              4  0
     d w_slutt         s              4  0
     d w_lengd         s              4  0
     d w_kurl          s            200
     d w_kusr          s            200
     d w_pass          s            200
     d w_prox          s            200
     d IFS_Output      s            256a   Varying
     d IFS_Output1     s            256a   Varying
     d IFS_Output2     s            256a   Varying
     d IFS_Input       s            256a   Varying
     d API_Output2     s            256a   Varying
     d API_Input       s            256a   Varying
     d IFS_path        s            256a   Varying
     d Len             s             10I 0
     d API_status      s              1
      *
      /copy prototypeb
      /copy usec
      *--------------------------------------------------------------------
      * Change Directory
      *
      * int chdir(const char *path)
      *--------------------------------------------------------------------
     D chdir           PR            10I 0 ExtProc('chdir')
     D   path                          *   Value Options(*string)

     D fd              S             10I 0
     D RW              C                   6
     D R               C                   4
     D OWNER           C                   64
     D GROUP           C                   8
      *
      * Hovedprogram:
      *
      *  - legger ut request til Profil Manager API
      *    * request fil er tom
      *    * request header inneholder token
      *  - mottar responsfil fra API
      *  - henter ut data fra responsfil
      *  - rydder på IFS
      *
      * Bygg opp linker til requesten.
      *
     c                   eval      w_ident = '"' + %trim(w_usrn) + ':' +
     c                             %trim(w_pass) + '"'
      *
      * Oppretter request file (dummy)
      *
     c                   eval      IFS_path = '/home/' +
     c                                        l_figr +
     c                                        '/pmanager/'
      *
     c                   eval      IFS_Output1 = 'pm' +
     c                                           %trim(%char(w_numm)) +
     c                                           '.request'
      *
     c                   if        chdir(%Trim(IFS_path)) >= 0
     c                   eval      fd = open(IFS_Output1 :
     c                               O_CREAT+O_WRONLY+O_CODEPAGE :
     c                               RW*OWNER + RW*GROUP + R : 1252  )
     c                   if        fd < 0
     c                   goto      avslutt
     c                   endif
     c                   endif
     c                   callp     close(fd)
      *
      * Oppretter headers file
      *
     c                   eval      IFS_Output2 = 'pm' +
     c                                           %trim(%char(w_numm)) +
     c                                           '.request.headers'
      *
     c                   if        chdir(%Trim(IFS_path)) >= 0
     c                   eval      fd = open(IFS_Output2 :
     c                               O_CREAT+O_WRONLY+O_CODEPAGE :
     c                               RW*OWNER + RW*GROUP + R : 1252  )
     c                   if        fd < 0
     c                   goto      avslutt
     c                   endif
     c                   endif
     c                   callp     close(fd)
      *
      * Åpnes igjen for å skrive inn APIKEY
      *
     c                   eval      w_tokeut = %triml('eg-apps-token=' +
     c                                        %trim(w_token))
      *
     c                   eval      fd = open(IFS_Output2 :
     c                               O_WRONLY+O_TEXTDATA)
     c                   if        fd < 0
     c                   goto      avslutt
     c                   endif
     c                   eval      w_size = %size(%trim(w_tokeut))
     c                   callp     write(fd: %addr(w_tokeut): w_size)
     c                   callp     close(fd)
      *
      * Bygger navn på retur file
      *
     c                   eval      IFS_Input = 'pm' +
     c                                           %trim(%char(w_numm)) +
     c                                           '.response'
      *
      * Initierer og kaller webservice
      *
     c                   eval      w_url2 = %trim(w_url) + '/api/profiles'
     c                   eval      API_Output2 = %triml(IFS_path + IFS_Output1)
     c                   eval      API_Input = %triml(IFS_path + IFS_Input)
     c                   eval      w_reqt = 'GET'
     c**                 eval      w_usrn = '           '
     c**                 eval      w_pass = '            '
     c                   eval      w_reqf = API_Output2
     c                   eval      w_rspf = API_Input
     c                   eval      w_enco = 'ISO-8859-1'
     c*                  eval      w_enco = 'UTF-8'
     c                   eval      w_stat = 0
     c                   eval      w_usrn = *blanks
     c                   eval      w_pass = *blanks
      *
     c                   call      'AW702C'
     c                   parm                    w_url2
     c                   parm                    w_reqt
     c                   parm                    w_reqf
     c                   parm                    w_rspf
     c                   parm                    w_usrn
     c                   parm                    w_pass
     c                   parm                    w_enco
     c                   parm                    w_stat
      *
      * Behandler returfil
      *
     c                   eval      IFS_Input = 'pm' +
     c                                           %trim(%char(w_numm)) +
     c                                           '.response'
     c                   eval      API_Input = %triml(IFS_path + IFS_Input)
      *
      * Sjekk om fila er kommet i retur ?
      *
     c                   eval      fd = open(API_Input :
     c                               O_RDONLY+O_TEXTDATA)
     c                   if        fd < 0
     c                   eval      API_status = '9'
     c                   endif
      *
      * Henter ut data fra returfil
      *
     c                   if        API_status = '0'
      *
     c                   call      'AP051R'
     c                   parm                    p_memb
     c                   parm                    API_input
     c                   parm                    API_status
      *
     c                   endif
      *
      * Rydder opp i filer på IFS
      *
9.xx c**                 if        1 = 2
     c                   callp     unlink(IFS_Output1)
     c                   callp     unlink(IFS_Output2)
9.xx c                   if        1 = 2
     c                   callp     unlink(IFS_Input)
9.xx c                   endif
      *
     c                   eval      p_stat = API_status
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ERROR-rutine  - Brukes ved mislykket web-service kall
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *pssr         begsr
      *
     c                   eval      p_stat = '8'
     c                   goto      avslutt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Henter inn parameter
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_memb
     c                   parm                    p_stat
      *
     c                   eval      API_status = '0'
      *
      * Henter inn løpenummer teller
      *
     c                   eval      w_kode = '0'
     c                   eval      w_fell = 'NXTKLI'
     c                   eval      w_type = *blank
     c                   eval      w_fast = *blank
     c                   eval      w_numm = *zero
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    p_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
     c                   parm                    w_numm
      /Free
      *
       exec sql
        select afudss
        into   :w_url
        from   afpspf
        where  afufil = :l_filg and
               afuide = 'cldPMUrl';

      *
      * Henter inn appkey fra COMPANY_SETTINGS
      *
       exec sql
        select aposkval
        into   :w_token
        from   aposextnst
        where  aposfirm = :p_firm and
               apossyst = 'EGAPPS' and
               aposckey = 'CLOUD_API_KEY';

      /End-Free
      *
     c                   endsr
