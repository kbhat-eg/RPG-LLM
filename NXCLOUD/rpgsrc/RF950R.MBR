     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RH950R                                        *
      * Beskrivelse . . : Fjerning av poster/journal - parameter        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
T5.00 * R5.00     270103  Felt for IN03 gjort om til num.           HFL *
T5.40 * V5.40     131206  Lagt inn mulighet for regnskapsår > *year HFL   HFL *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KA = F1  = Valg av firma                                  *
      *       KC = F3  = Exit                                           *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     frf950d    cf   e             workstn
      *
     d                uds
     d rhpkda                300    305  0
     d rhprÅr                         4  0
     d rhpper                         2  0
     d rhppef                         2  0
     d rhppet                         2  0
     d rhpavf                         4  0
     d rhpavt                         4  0
     d rhpjof                         5  0
     d rhpjot                         5  0
     d rhpsrt                         1
     d rhpsml                         1
     d rhpavv                         1
     d rhpkgs                         1
     d rhpkks                         1
     d rhpsps                         1
     d rhpktu                         1
      *
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
E5.00d w_in03          s              1  0
T5.40d var             s             40    dim(2) ctdata perrcd(1)               Varsel m/svarkrav
T5.40d wsavli          s              2  0
T5.40d wsavpo          s              3  0
      *
     c     dummy         tag
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      *
      * A2BLD  - A2 Bilde for valg av parametre
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * A2BLD Behandle bilde for valg av utskriftskriteria              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     a2taga        tag
      *
      * Bare data
      *
     c     a2tagb        tag
      *
     c                   exfmt     a2bld
      *
      * Gå ut
      *
B001 c                   if        *inkc = *on                                  .....
E5.00c                   eval      w_in03 = 1                                       .
 001 c                   goto      xslutt                                           .
E001 c                   endif                                                  .....
      *
      *  Kontroll av regnskapsår
      *
S5.40c*                  if        a2prÅr = *zero or                                     .....
S5.40c*                            a2prÅr > *year                                        .....
E5.40c                   if        a2prÅr = *zero                                        .....
 001 c                   eval      *in34 = *on                                               .
 001 c                   goto      a2taga                                                    .
E001 c                   endif                                                           .....
      *
T5.40 *  Varsel ved regnskapsår større enn *year
      *
T5.40c                   if        a2prÅr > *year
T5.40c                   movel     var(1)        m1mld1
T5.40c                   movel     var(2)        m1mld2
T5.40c                   move      'N'           m1svar
T5.40c                   exsr      xm1win
T5.40c                   if        m1svar = 'N'
T5.40c                   eval      *in34 = *on                                               .
T5.40c                   goto      a2taga                                                    .
T5.30c                   endif
T5.40c                   endif                                                           .....
      *
      *  Kontroll av journalnr
      *
B001 c                   if        a2pjnr = *zero                                        .....
 001 c                   eval      *in35 = *on                                               .
 001 c                   goto      a2taga                                                    .
E001 c                   endif                                                           .....
      *
      * Skriv parameter
      *
     c                   move      a2prÅr        rhprÅr
     c                   move      a2pjnr        rhpjof
     c                   move      a2pjnr        rhpjot
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
     c                   return
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
T5.40 * M1WIN Behandle bilde for melding med svar                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
T5.40c     xm1win        begsr
      *
T5.40c                   eval      wsavli = 6
T5.40c                   eval      wsavpo = 22
T5.40c                   eval      w_arow = wsavli
T5.40c                   eval      w_acol = wsavpo
      *
T5.40c                   exfmt     m1win
      *
T5.40c                   if        *inkc = *on
T5.40c                   move      'N'           m1svar
T5.40c                   endif
      *
T5.40c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Beregner periode
      *
     c                   eval      a2prÅr = *year
E5.00c                   eval      w_in03 = *zero
      *
      * Sett kode for styring av overskrift på journal
      *
E5.00c                   eval      rhpsml = 'S'
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    w_in03
      *
     c                   endsr
**
Valgt år er neste år.
Bekreft dersom dette er riktig.
