     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : NO124R
      * Beskrivelse . . : Ekstern ordre-hode, overføring fra bestilling
      *                   til ordre
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       060204 AMD  Nytt program (Grunnlag kopiert fra NO120R)
5.32  * R5.32 080904 AMD  Lagt inn ordretype for nummerserie (se dok)
5.33  * R5.33 090904 AMD  Lagt inn oppdatering av krysskobling mellom
5.33  *                   Ordre/Bestilling (Ordrenr/suffix/linje)
5.53  * R5.53 210307 AMD  Lagt på mva på memosaldo for å få riktigere
      *                   beregning av kredittgrense
5.50  * R5.50 060807 KOB  Lagt inn kontroll mot beløpsgrense almenning
5.60  * R5.60 191107 AMD  Lagt inn løsning for kontroll av grense for
      *                   almenningsrabatter
      * 6.10  180609 BSA  Oppdatert med sporingsinformasjon
6.11  * 6.10  270710 BSA  Lagt inn sporingsreferanser mellom ordre/bestilling
6.11  * 6.10  200810 bof  Lagt på fakturatype fra kunden
6.20  * 6.20  231110 NHO  Proj er nå alfa
6.21  * V6.20 040511 AMD  Flere muligheter rundt kreditt-alternativer
6.22  * V6.20 140611 bsa  Henter inn memosaldo fra eget register
6.23  * V6.20 230112 bof  Hvis OI, så skal det ikke settes nye ref.til ordre
6.24  * V6.20 230112 bof  Setter også std. ordretype i bildet
6.25  * V6.20 230112 bof  Hvis innkjøpsenh. ikke gyldig salgs.enh omregn antall
6.26  * 6.20  280915 BOF  Lagt inn ryddeprogram ved oppr.av ordre
6.nu  * R6.30 030614 bsa  Programmet gjennomgått mtp nummerutvidelser.
6.30  * R6.30 210917 bsa  Legger inn verdi i FOUSER da det er et "krav"
6.30  *                   at feltet er utfylt for 2DP.
      *
7.01  * MGR   151216 sst  Ber flytende Memosaldo m/ordre pr ant. dager
7.01  *       170117 sst  Hente parameter meosaldo fra AFPSPF
7.02  * MGR   091117 sst  Tildele egn ordretype på internordre
7.03  * 7.00  250320 bsa  Innfører switchstyring av memosaldo
      *
8.01  * K000  231122 sst  Flyttet ant memo-dager og utv.kreditgrense til  UTVIDET_MEMOSALDO (nx)
8.02  *PRG2   220622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
8.03  * K000  130824 bsa  Henter nummerserie basert på feil ordretype.
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flodtl1    if   e           k disk    rename(lodtpfr:lodtl1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     ffusrl1    if   e           k disk    rename(fusrpfr:fusrl1r)
6.25 fvvenl2    if   e           k disk    rename(vvenpfr:vvenl2r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
6.22 frkmel1    if   e           k disk    rename(rkmepfr:rkmel1r)
     flohelu    uf   e           k disk    rename(lohepfr:lohelur)
     ffohelu    uf a e           k disk    rename(fohepfr:fohelur)
5.33 flodtlu    uf   e           k disk    rename(lodtpfr:lodtlur)
5.33 ffodtlu    uf   e           k disk    rename(fodtpfr:fodtlur)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
8.01 f*afpslr    if   e           k disk    rename(afpspfr:afpslrr)
7.02 flldil1    if   e           k disk    rename(lldipfr:lldil1r)
      *
     fno120d    cf   e             workstn
      *
      * Definering av parametre
      *
     d p_firm          s                   like(ldfirm)
6.nu d p_numm          s                   like(ldnumm)
     d p_suff          s                   like(ldsuff)
     d p_kund          s             20
     d p_avde          s                   like(ldavde)
     d p_lage          s                   like(ldlage)
      *
      * Definering av Local data area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
7.01 d l_filg                931    933
      *
      * Definering av variabler i nøkler
      *
     d lohel1_numm     s                   like(lonumm)
     d lohel1_suff     s                   like(losuff)
     d lodtl1_numm     s                   like(ldnumm)
     d lodtl1_suff     s                   like(ldsuff)
     d votyl1_otyp     s                   like(vaotyp)
     d fusrl1_user     s                   like(fbuser)
     d rkunl1_kund     s                   like(rkkund)
     d lohelu_numm     s                   like(lonumm)
     d lohelu_suff     s                   like(losuff)
     d fohelu_numm     s                   like(fonumm)
     d fohelu_suff     s                   like(fosuff)
     d fodtl1_numm     s                   like(fdnumm)
     d fodtl1_suff     s                   like(fdsuff)
     d fodtl1_line     s                   like(fdline)
5.33 d fodtlu_numm     s                   like(fdnumm)
5.33 d fodtlu_suff     s                   like(fdsuff)
5.33 d fodtlu_line     s                   like(fdline)
5.33 d lodtlu_numm     s                   like(ldnumm)
5.33 d lodtlu_suff     s                   like(ldsuff)
5.33 d lodtlu_line     s                   like(ldline)
6.25 d vvenl2_vare     s                   like(vevare)
6.25 d vvenl2_saen     s                   like(vesaen)
8.01 d*afpslr_ufil     s                   like(afufil)
8.01 d*afpslr_uide     s                   like(afuide)
7.02 d lldil1_firm     s                   like(lpfirm)
7.02 d lldil1_ldor     s                   like(lpldor)
7.02 d lldil1_lage     s                   like(lplage)
      *
      * Definering av variable
      *
3.11 d b_sper          s              1    inz(*off)
      *
     d w_kode          s              1
     d w_fast          s             10
     d w_sald          s             11  2
5.60 d w_sal2          s             11  2
     d w_tots          s             11  2
     d w_totr          s             11  2
     d w_totk          s             11  2
5.60 d w_tota          s             11  2
5.60 d w_totn          s             11  2
5.60 d w_tot1          s             11  2
5.60 d w_tot2          s             11  2
     d w_fell          s              6
3.11 d w_valg          s              2  0
3.11 d w_text          s             70
     d w_nref          s              6  0
     d w_kund          s              6  0
6.25 d w_anta          s                   like(ldanta)
6.25 d w_enh1          s                   like(ldenh1)
6.25 d w_saen          s                   like(ldenh1)
6.25 d w_antu          s                   like(ldanta)
      *
     d w_firm          s                   like(lofirm)
     d w_numm          s                   like(fonumm)
     d w_suff          s                   like(fosuff)
     d w_otyp          s                   like(footyp)
     d w_line          s                   like(fdline)
5.32 d w_onum          s                   like(vaonum)
5.32 d w_type          s                   like(vaonum)
5.50 d w_gral          s                   like(rkgral)
7.01  *w_utvgrns = utvidelse av memosaldo(Mestergruppen)
7.01 d w_utvgrns       s              2  0
7.01  *w_memodagr = antall dager ordre skal med i memosaldo
7.01 d w_memodagr      s              3  0
7.01 d w_3x            s              3
7.01 d p_fir           s              3
7.01 d p_kun           s              6
7.01 d w_kgrn          s             11  2
5.53  *
5.53  * Parametre til RS205R - hent mva %
5.53  *
5.53 d w_stat          s              1
5.53 d w_mvak          s              1
5.53 d w_mvtx          s             30
5.53 d w_mvas          s              6  2
5.53 d w_mvat          s              5  4
5.53 d w_mvaf          s             10  9
5.53 d w_bpro          s              5  2
5.53 d w_date          s               d   datfmt(*iso)
      *
7.03 d u_703           s              1    inz(*off)                            Kredit og memodager
      *
7.03   dcl-s co402_verdi1  char(1);
7.03   dcl-s co402_verdi2  char(2048);
7.03   DCL-PR CO402R EXTPGM('CO402R');
7.03     p_filgrp            char(3)     const;
7.03     p_firm              packed(3:0) const;
7.03     p_lager             packed(2:0) const;
7.03     p_nokkel            char(50)    const;
7.03     p_verdi1            char(1);
7.03     p_verdi2            char(2048);
7.03   END-PR;
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Avbryter dersom bestilling ikke finnes
      *
     c                   eval      lohel1_numm = p_numm
     c                   eval      lohel1_suff = p_suff
     c     lohel1_key    chain     lohel1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
      * Hent ordretype
      *
     c                   eval      w_otyp = faaot6
6.24 c                   eval      b1otyp = faaot6
7.02  *
7.02 c                   eval      lldil1_firm = lofirm
7.02 c                   eval      lldil1_ldor = loldor
7.02 c                   move      lolage        lldil1_lage
7.02 c     lldil1_key    chain     lldil1
7.02 c                   if        %found(lldil1)
7.02 c                             and   lpotyp = lootyp
7.02 c                   eval      votyl1_otyp = lootyp
7.02 c     votyl1_key    chain     votyl1
7.02 c                   if        %found(votyl1)
7.02 c                   eval      b1otyp = vaomot
7.02 c                   endif
7.02 c                   endif
7.02  *
     c                   eval      w_fast = l_wsid
      *
      * Legg inn kundenummer som skal til ordre
      *
     c                   movel     p_kund        w_kund
      *
      * Henter informasjon og kredittsperre og kredittgrense
      * Ved kredittsperre stoppes videre overføring
      *
     c                   eval      rkunl1_kund = w_kund
     c     rkunl1_key    chain     rkunl1
7.01  * Beregne memosaldo pr dato, Mestergruppen
7.03 c                   if        u_703 = *on
7.01 c                   if        w_memodagr > 0
7.01 c                   move      w_firm        p_fir
7.01 c                   move      rkunl1_kund   p_kun
7.01 c                   call      'FO763R'
7.01 c                   parm                    p_fir
7.01 c                   parm                    p_kun
7.01 c                   endif
7.03 c                   endif
6.22  * Henter inn memosaldo
6.22 c     rkunl1_key    chain     rkmel1
      *
6.21 c                   if        rkkres = 1
     c                   eval      votyl1_otyp = w_otyp
     c     votyl1_key    chain     votyl1
3.35 c                   if        vaoakk > 0
     c                   eval      b_sper = *on
     c                   eval      *in31 = *on
     c                   endif
     c                   endif
      *
     c                   if        rkgrns <> 0
5.53 c                   eval      w_sald = rksald + (rkmems * w_mvat)
7.01  *   Utvide kreditgrense   (Mestergruppen løsning 2017_01)  sst
7.03 c                   if        u_703 = *on
7.01 c                   if        w_sald < rkgrns * 1000
7.01 c                   eval      w_kgrn = rkgrns * 1000 *
7.01 c                                      (1+w_utvgrns/100)
7.01 c                   else
     c                   eval      w_kgrn = rkgrns * 1000
7.01 c                   endif
7.01 c**                 if        w_sald > rkgrns
7.01 c                   if        w_sald > w_kgrn
     c                   eval      *in42 = *on
     c                   endif
7.03 c                   else
7.03 c                   eval      w_sald = rksald + (rkmems * w_mvat)
7.03 c                   if        w_sald > rkgrns
7.03 c                   eval      *in42 = *on
7.03 c                   endif
7.03 c                   endif
     c                   endif
5.50  *
5.50  * Sjekk beløpsgrense almenning fra kunde dersom utfylt
5.50  *
5.50 c                   if        rkgral <> 0
5.50 c                   eval      w_gral = rkgral
5.50  *
5.50 c                   else
5.50  *
5.50  * Ellers hent beløpsgrense almenning
5.50  *
5.50 c                   call      'FO106R'
5.50 c                   parm                    w_kund
5.50 c                   parm                    w_gral
5.50  *
5.50 c                   endif
5.50  *
5.50 c                   if        w_gral <> 0
5.50 c                   eval      w_sald = rkkjal + rkmema
5.50 c                   if        w_sald > w_gral
5.50 c                   eval      *in43 = *on
5.50 c                   endif
5.50 c                   endif
      *
      * Behandler bildet
      *
     c     b1taga        tag
      *
     c                   exfmt     b1bld
      *
      * F3=Avslutt
      *
B001 c                   if        *inkc or *inkl
 001 c                   goto      avslutt
E001 c                   endif
      *
      * Avbryter ved kredittsperre
      *
B001 c                   if        b_sper = *on
 001 c                   goto      avslutt
E001 c                   endif
      *
8.03 c                   eval      w_otyp = b1otyp
      *
      * Oppretter ordre-hode
      *
     c                   exsr      ordre_hode
      *
      * Oppretter ordre-linje
      *
     c                   exsr      ordre_linje
      *
3.11  * Oppdater ordrehode og memosaldo med verdier fra ordrelinjer
      *
     c                   exsr      oppdat_ordre
      *
3.11  * Oppdater bestilling hode med ordrenummer
      *
     c                   eval      lohelu_numm = p_numm
     c                   eval      lohelu_suff = p_suff
     c     lohelu_key    chain     lohelur
6.11 c                   if        %found and
6.11 c                             loornr = 0
     c                   eval      loornr = fonumm
     c                   update    lohelur
     c                   endif
      *
      * Kaller program for ordrebehandling
      *
     c                   eval      w_valg = 2
      *
     c                   call      'FO100R'
     c                   parm                    w_valg
     c                   parm                    fonumm
     c                   parm                    fosuff
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppretter ordre-hode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ordre_hode    begsr
      *
      * Hent neste ledig nummer i.h.t. ordretype
      *
     c                   exsr      hent_nummer
      *
      * Initierer fil
      *
     c                   eval      fosuff = 00
     c                   eval      fomkod = 0
     c                   eval      fobind = 'N'
      *
     c                   eval      fofirm = lofirm
     c                   eval      fonumm = w_numm
7.02 c**                 eval      footyp = w_otyp
7.02 c                   eval      footyp = b1otyp
     c***                eval      folety = lolety
     c                   eval      fobdat = *loval
     c                   eval      foldat = loldat
     c                   eval      fobetb = 0
     c                   eval      foselg = 0
     c                   eval      folmat = 0
     c                   eval      fokund = w_kund
     c***                eval      fokpro = lokpro
     c                   eval      fonavn = lonavn
     c                   eval      foadr1 = loadr1
     c                   eval      foadr2 = loadr2
     c***                eval      fosted = %trim(loponr) + ' ' + losted
     c                   eval      fosted = losted
6.20 c**                 eval      foproj = 0
6.20 c                   eval      foproj = *blank
     c                   eval      foakti = *blank
     c                   eval      fovref = *blank
     c                   eval      fodref = lodref
6.11 c                   eval      fobenr = lonumm
6.11 c                   eval      fobsuf = losuff
6.11 c                   eval      forekv = %char(lonumm)
     c                   eval      fodate = *loval
     c                   eval      fotime = *loval
     c                   eval      forkat = 0
     c                   eval      folage = p_lage
     c                   eval      foavde = p_avde
3.38 c                   eval      fofsys = 'INT'
3.38 c                   movel     p_numm        fonref
6.30 c                   eval      fouser = l_user
      *
     c                   eval      w_suff = fosuff
      *
     c                   time                    fobdat
     c                   time                    fodate
     c                   time                    fotime
      *
      * Hent brukerinformasjon
      *
     c                   if        louser <> *blank
     c                   eval      fusrl1_user = louser
     c                   else
     c                   eval      fusrl1_user = l_user
     c                   endif
      *
     c     fusrl1_key    chain     fusrl1
     c                   if        %found
     c                   eval      fovref = fbvref
     c                   eval      foselg = fbselg
     c                   endif
      *
      * Henter kundeinformasjon
      *
     c                   eval      rkunl1_kund = w_kund
     c     rkunl1_key    chain     rkunl1
     c                   if        %found
      *
     c                   eval      fobetb = rkbetb
     c                   eval      fodist = rkdist
     c                   eval      folmat = rkform
     c                   eval      folbet = rklbet
     c                   eval      fovalk = rkvalk
     c                   eval      forkat = rkrabk
     c                   eval      fosped = rksped
     c                   eval      fokjor = rkkjØr
     c                   eval      fomkod = rkmkod
     c                   eval      fokobk = rkordb
     c                   eval      fokgfa = rkfagb
     c                   eval      fokgfr = rkfrgb
     c                   eval      fokgek = rkekgb
KOB  c                   eval      forabp = rkorab
3.38 c                   eval      foneto = rkneto
6.11 c                   eval      fofaty = rkfaty
      *
     c                   if        rkslgr <> 0
     c                   eval      foselg = rkslgr
     c                   endif
      *
     c                   if        fonavn = *blank and
     c                             foadr1 = *blank and
     c                             foadr2 = *blank
     c                   eval      fonavn = rknavn
     c                   eval      foadr1 = rkgate
     c                   eval      foadr2 = rkadr2
     c                   eval      fosted = rksted
     c                   endif
      *
     c                   endif
      *
      * Skriver til fil
      *
6.10 c                   time                    fodate
6.10 c                   time                    fotime
6.10 c                   eval      fooous = l_user
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = l_user
     c                   write     fohelur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppretter ordre-linje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ordre_linje   begsr
      *
      * Henter høyeste linjenummer fra ordre-linje
      *
     c                   exsr      hent_linjenr
      *
      * Leser alle bestillingslinjer, og legger disse ut som ordre-linjer
      *
     c                   eval      lodtl1_numm = p_numm
     c                   eval      lodtl1_suff = p_suff
     c     lodtl1_key    setll     lodtl1
     c     lodtl1_key    reade     lodtl1
     c                   dow       not %eof
      *
     c                   eval      w_kode = *blank
     c                   eval      w_line = w_line + 10
      *
      *
      *
     c                   if        ldvare <> *blank
6.25 c                   eval      w_anta = ldanta
6.25 c                   eval      w_enh1 = ldenh1
6.25  * sjekker enhet om gyldig salgsenhet
6.25 c                   exsr      sjekk_enhet
      *
     c                   call      'FD106R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    fonumm
     c                   parm                    fosuff
     c                   parm                    w_line
     c                   parm                    faalty
     c                   parm                    ldlety
     c                   parm                    ldvare
6.25 c                   parm                    w_anta
6.25 c                   parm                    w_enh1
     c                   parm                    ldldat
     c                   else
     c                   eval      w_text = ldtek1 + ldtek2
     c                   call      'FD111R'
     c                   parm                    w_firm
     c                   parm                    fonumm
     c                   parm                    fosuff
     c                   parm                    w_line
     c                   parm                    faatty
     c                   parm                    w_text
     c                   endif
5.33  *
5.33  * Oppdatering av krysskobling
5.33  * mellom ordre og bestilling
5.33  *
5.33  * Bestillingslinje
5.33 c                   eval      lodtlu_numm = ldnumm
5.33 c                   eval      lodtlu_suff = ldsuff
5.33 c                   eval      lodtlu_line = ldline
5.33 c     lodtlu_key    chain     lodtlu
5.33 c                   if        %found
6.23 c                   if        ldornu = 0 and
6.23 c                             ldorsu = 0 and
6.23 c                             ldorli = 0
5.33 c                   eval      ldornu = fonumm
5.33 c                   eval      ldorsu = fosuff
5.33 c                   eval      ldorli = w_line
6.23 c                   endif
6.10 c                   time                    ldedat
6.10 c                   time                    ldetim
6.10 c                   eval      ldeusr = l_user
5.33 c                   update    lodtlur
5.33 c                   endif
5.33  * Ordrelinje
5.33 c                   eval      fodtlu_numm = fonumm
5.33 c                   eval      fodtlu_suff = fosuff
5.33 c                   eval      fodtlu_line = w_line
5.33 c     fodtlu_key    chain     fodtlu
5.33 c                   if        %found
5.33 c                   eval      fdbenr = ldnumm
5.33 c                   eval      fdbsuf = ldsuff
5.33 c                   eval      fdblin = ldline
6.10 c                   time                    fdedat
6.10 c                   time                    fdetim
6.10 c                   eval      fdeusr = l_user
5.33 c                   update    fodtlur
5.33 c                   endif
      *
     c     lodtl1_key    reade     lodtl1
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter inn ordrenummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_nummer   begsr
      *
     c                   eval      w_kode = '0'
     c                   eval      w_fell = faaork
     c                   eval      w_fast = l_wsid
5.32 c                   eval      w_type = w_otyp
5.32  *
5.32  * Hente inn eventuell overstyrt
5.32  * ordretype for nummerserie
5.32  *
5.32 c                   call      'VA752R'
5.32 c                   parm                    w_firm
5.32 c                   parm                    w_otyp
5.32 c                   parm                    w_onum
5.32  *
5.32 c                   if        w_onum <> *blank
5.32 c                   eval      w_type = w_onum
5.32 c                   endif
      *
      * Kaller subprogram for henting av nummer fra nummer-register
      *
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
5.32 c                   parm                    w_type
     c                   parm                    w_fast
     c                   parm                    w_numm
6.26  *
6.26  * sjekker at ordrenr ikke har tidligere relasjoner liggende
6.26  *
6.26 c                   call      'FO415R '
6.26 c                   parm                    w_firm
6.26 c                   parm                    w_numm
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å hente høyeste linjenummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_linjenr  begsr
      *
     c                   eval      fodtl1_numm = fonumm
     c                   eval      fodtl1_suff = fosuff
     c                   eval      fodtl1_line = 9999
     c     fodtl1_key    setll     fodtl1
     c                   readp     fodtl1
     c                   if        not %eof and
     c                             fdfirm = w_firm and
     c                             fdnumm = fonumm and
     c                             fdsuff = fosuff
     c                   eval      w_line = fdline
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer ordre og memosaldo
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdat_ordre  begsr
      *
      * Henter ordretotal
      *
     c                   eval      w_tots = 0
     c                   eval      w_totr = 0
     c                   eval      w_totk = 0
5.60 c                   eval      w_tota = 0
5.60 c                   eval      w_totn = 0
5.60 c                   eval      w_tot1 = 0
5.60 c                   eval      w_tot2 = 0
      *
     c     fodtl1_key2   setll     fodtl1
     c     fodtl1_key2   reade     fodtl1
     c                   dow       not %eof
     c                   eval      w_tots = w_tots + fdsums
     c                   eval      w_totr = w_totr + fdsumr
     c                   eval      w_totk = w_totk + fdsumk
5.60  *
5.60 c                   eval      w_totn = fdsums
5.60 c                   eval      w_tot1 = 0
5.60 c                   eval      w_tot2 = 0
5.60  *
5.60 c                   if        fdrab1 <> 0
5.60 c                   eval      w_tot1 = w_totn * fdrab1 / 100
5.60 c                   eval      w_totn = w_totn - w_tot1
5.60 c                   endif
5.60  *
5.60 c                   if        fdrab2 <> 0
5.60 c                   eval      w_tot2 = w_totn * fdrab2 / 100
5.60 c                   eval      w_totn = w_totn - w_tot2
5.60 c                   endif
5.60  *
5.60  * NB!  w_tot1
5.60  * og   w_tot2 brukes om
5.60  * igjen og null-stilles
5.60  *
5.60 c                   eval      w_tot1 = 0
5.60 c                   eval      w_tot2 = 0
5.60  *
5.60 c                   if        fdbrr1 <> 0
5.60 c                   eval      w_tot1 = w_totn * fdbrr1 / 100
5.60 c                   eval      w_totn = w_totn - w_tot1
5.60 c                   endif
5.60  *
5.60 c                   if        fdbrr2 <> 0
5.60 c                   eval      w_tot2 = w_totn * fdbrr2 / 100
5.60 c                   eval      w_totn = w_totn - w_tot2
5.60 c                   endif
5.60  *
5.60 c                   eval      w_tota = w_tota + w_tot1
5.60 c                   eval      w_tota = w_tota + w_tot2
5.60  *
     c     fodtl1_key2   reade     fodtl1
     c                   enddo
      *
      * Beregner justeringsbeløp, og oppdaterer memosaldo
      *
     c                   eval      w_kode = *blank
     c                   eval      w_sald = w_tots - w_totr
5.60  *
5.60 c                   eval      w_sal2 = w_tota
      *
     c                   call      'FA922R'
     c                   parm                    w_firm
     c                   parm                    footyp
     c                   parm                    fokund
     c                   parm                    w_sald
5.60 c                   parm                    w_sal2
      *
      * Oppdaterer ordre-hode med ny saldo
      *
     c                   eval      fohelu_numm = fonumm
     c                   eval      fohelu_suff = fosuff
     c     fohelu_key    chain     fohelur
      *
     c                   eval      fotots = w_tots
     c                   eval      fototr = w_totr
     c                   eval      fototk = w_totk
5.60 c                   eval      fobrra = w_tota
      *
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = l_user
     c                   update    fohelur
      *
     c                   endsr
      *
6.25  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.25  * Subrutine for å sjekke enhet
6.25  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.25  *
6.25 c     sjekk_enhet   begsr
6.25  *
6.25 c                   eval      w_saen = *blank
6.25 c                   eval      vvenl2_vare = ldvare
6.25 c                   eval      vvenl2_saen = 1
6.25 c     vvenl2_key    setll     vvenl2
6.25 c     vvenl2_key2   reade     vvenl2
6.25 c                   dow       %eof = *off
6.25 c                   if        veenhe = ldenh1
6.25 c                   leavesr
6.25 c                   endif
6.25 c                   if        vesaen = 1
6.25 c                   eval      w_saen = veenhe
6.25 c                   endif
6.25 c     vvenl2_key2   reade     vvenl2
6.25 c                   enddo
6.25  *
6.25  * enhet er ikke lik - velg først salgsenhet og omberegn antall
6.25  * til denne enheten.
6.25 c                   call      'VA770R'
6.25 c                   parm                    w_firm
6.25 c                   parm                    ldvare
6.25 c                   parm                    ldenh1
6.25 c                   parm                    ldanta
6.25 c                   parm                    w_saen
6.25 c                   parm                    w_antu
6.25 c                   eval      w_enh1 = w_saen
6.25 c                   eval      w_anta = w_antu
6.25  *
6.25 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
6.nu c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_kund
     c                   parm                    p_avde
     c                   parm                    p_lage
      *
      * Key for oppslag på bestillings-hode
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
     c                   kfld                    lohel1_suff
      *
      * Key for les på bestillings-linje
      *
     c     lodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtl1_numm
     c                   kfld                    lodtl1_suff
5.33  *
5.33  * Key for update av bestillings-linje
5.33  *
5.33 c     lodtlu_key    klist
5.33 c                   kfld                    w_firm
5.33 c                   kfld                    lodtlu_numm
5.33 c                   kfld                    lodtlu_suff
5.33 c                   kfld                    lodtlu_line
      *
      * Key for oppslag på ordre-type
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for oppslag på ordre-status
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på ordre-bruker
      *
     c     fusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fusrl1_user
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppdatering av ekstern ordre-hode
      *
     c     lohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelu_numm
     c                   kfld                    lohelu_suff
      *
      * Key for oppdatering av ordre-hode
      *
     c     fohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelu_numm
     c                   kfld                    fohelu_suff
      *
      * Key for oppslag på ordre-linje
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
     c                   kfld                    fodtl1_line
5.33  *
5.33  * Key for update av ordre-linje
5.33  *
5.33 c     fodtlu_key    klist
5.33 c                   kfld                    w_firm
5.33 c                   kfld                    fodtlu_numm
5.33 c                   kfld                    fodtlu_suff
5.33 c                   kfld                    fodtlu_line
      *
     c     fodtl1_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
6.25  *
6.25  * Key for les av vare-enhet
6.25  *
6.25 c     vvenl2_key    klist
6.25 c                   kfld                    w_firm
6.25 c                   kfld                    vvenl2_vare
6.25 c                   kfld                    vvenl2_saen
6.25  *
6.25 c     vvenl2_key2   klist
6.25 c                   kfld                    w_firm
6.25 c                   kfld                    vvenl2_vare
7.01  *
7.01  * Key for propertiesfil
7.01  *
8.01 c*    afpslr_key    klist
8.01 c*                  kfld                    afpslr_ufil
8.01 c*                  kfld                    afpslr_uide
7.02  *
7.02  * Key for propertiesfil
7.02  *
7.02 c     lldil1_key    klist
7.02 c                   kfld                    lldil1_firm
7.02 c                   kfld                    lldil1_ldor
7.02 c                   kfld                    lldil1_lage
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
      * Henter informasjon fra ordre-status
      *
     c     fstsl1_key    chain     fstsl1
5.53  *
5.53  * Hent mva % for beregning av memosaldo inkl. mva
5.53  *
5.53 c                   eval      w_stat = *blank
5.53 c                   eval      w_mvak = '3'
5.53 c                   time                    w_date
5.53  *
5.53 c                   call      'RS205R'
5.53 c                   parm                    w_stat
5.53 c                   parm                    w_mvak
5.53 c                   parm                    w_mvtx
5.53 c                   parm                    w_date
5.53 c                   parm                    w_mvas
5.53 c                   parm                    w_mvat
5.53 c                   parm                    w_mvaf
5.53 c                   parm                    w_bpro
      *
7.01  *
7.01  *   Hent inn % utvidelse av kreditgrense
8.01 c*                  eval      afpslr_ufil = l_filg
8.01 c*                  eval      afpslr_uide = 'KRGRNSUTV'
8.01 c*    afpslr_key    chain     afpslrr
8.01 c*                  if        %found(afpslr)
8.01 c*                  movel     afudss        w_utvgrns
8.01 c*                  endif
7.01  *
7.01  *   Hent inn ant dager fram ordre skal med i memosaldo
8.01 c*                  eval      afpslr_uide = 'MEMODAGER'
8.01 c*    afpslr_key    chain     afpslrr
8.01 c*                  if        %found(afpslr)
8.01 c*                  movel     afudss        w_3x
8.01 c*                  move      w_3x          w_memodagr
8.01 c*                  endif
7.03  *
7.03  * Utvidet memosaldo
8.01  *   Hent inn % utvidelse av kreditgrense
8.01  *   Hent inn ant dager fram ordre skal med i memosaldo
7.13  *
8.04 c                   eval      w_memodagr = 999
8.04 c                   eval      w_utvgrns  = 0
7.03  *
7.03   CallP CO402R(l_filg:w_firm:0:'UTVIDET_MEMOSALDO':
7.03                    co402_verdi1:co402_verdi2);
7.03   if co402_verdi1 = '1';
7.03     u_703 = *on;
8.01     if  %subst(co402_verdi2:1:3) <> *blank;
8.01       w_memodagr = %dec(%subst(co402_verdi2:1:3):3:0);
8.01     endif;
8.01     if  %subst(co402_verdi2:133:2) <> *blank;
8.01       w_utvgrns = %dec(%subst(co402_verdi2:133:2):2:0);
8.01     endif;
7.03   endif;
     c                   endsr
