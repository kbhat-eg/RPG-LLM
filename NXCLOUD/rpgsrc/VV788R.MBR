     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VV788R
      * Beskrivelse . . : Vare, sletting i annen filgruppe
      *
      *                   Program for fjerning av alle slettede varer i
      *                   filgruppen/firmaet det kopieres til.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       191199 HKO  Nytt program
      *       100402 HKO  Lagt inn behandling av nye parametre
      *                   Sjekker om vare finnes før slette-program kalles
      *       181207 bof  Lagt inn standard testye parametre
8.01  *PRG2   220622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner - COMP
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Kopiers fra...
      *
     fvvasl1    if   e           k disk    rename(vvaspfr:vvasl1r)
      *
      * Kopiers til... (slettes fra)
      *
     fvvarlu    if   e           k disk    rename(vvarpfr:vvarlur)
     fsstal2    if   e           k disk    rename(sstapfr:sstal2r)
      *
      * Datastruktur for parameterliste -inn
      *
     d                 ds
     d d_list                       260
     d  d_fgrp                        3    overlay(d_list:1)
     d  d_firm                        3  0 overlay(d_list:4)
      *
     d                 ds
     d d_list2                        8
     d  d_fgrt                        3    overlay(d_list2:1)
     d  d_firt                        3  0 overlay(d_list2:4)
      *
      * Definering av parametre
      *
     d p_list          s            260
     d p_list2         s              8
      *
      * Definering av variable i nøkler
      *
     d vvarlu_vare     s                   like(vvvare)
     d sstal2_vare     s                   like(sfvare)
     d sstal2_paar     s                   like(sfpaar)
     d sstal2_bida     s                   like(sfbida)
      *
      * Definering av variable
      *
     d b_inlr          s              1    inz(*off)
     d b_ordr          s              1    inz(*off)
     d b_lagr          s              1    inz(*off)
     d b_slett         s              1    inz(*off)
      *
     d w_firm          s                   like(vvfirm)
     d w_firt          s                   like(vvfirm)
      *
     d w_dato          s               d   datfmt(*iso)
     d w_dato_8        s              8
     d w_paar          s              4  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Finner dato og årstall ett år tilbake i tid
      *
     c                   time                    w_dato
     c                   adddur    -1:*y         w_dato
      *
     c     *iso0         move      w_dato        w_dato_8
     c                   movel     w_dato_8      w_paar
      *
      *
      * Leser alle slettede varer i filgruppe/firmaet det kopieres fra
      *
     c     vvasl1_key    setll     vvasl1
     c     vvasl1_key    reade     vvasl1                                 90
     c                   dow       *in90 = *off
     c                   exsr      behandling
     c     vvasl1_key    reade     vvasl1                                 90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sletting av vare fra vareregister, og oppretting av
      * vare i register over slettede varer.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
      * Dersom vare finnes, kalles program for sletting av vare
      *
     c                   eval      vvarlu_vare = vvvare
     c     vvarlu_key    chain     vvarlu
     c                   if        %found
      * test om vare kan slettes
     c                   eval      b_slett = *on
     c                   exsr      ktrl_vare
      *
     c                   if        b_slett = *on
     c                   eval      b_inlr = *off
     c                   call      'VV400R'
     c                   parm                    w_firt
     c                   parm                    vvvare
     c                   parm                    b_inlr
     c                   endif
      *
     c                   endif
      *
     c     end_behandl   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kontrollere at varen kan slettes
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ktrl_vare     begsr
      *
      * Kaller program for å sjekke om en vare finnes i ordre eller har
      * beholdningstall i noen lagre
      *
     c                   eval      b_inlr = *off
      *
     c                   call      'VV490R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   parm                    b_inlr
     c                   parm                    b_ordr
     c                   parm                    b_lagr
      *
      * Vare som finnes i ordre kan ikke slettes
      *
     c                   if        b_ordr = *on  or
     c                             b_lagr = *on
     c                   eval      b_slett = *off
     c                   goto      end_ktrl
     c                   endif
      *
      * Test på salg siste år
      *
     c                   eval      sstal2_vare = vvvare
     c                   eval      sstal2_paar = w_paar
     c                   eval      sstal2_bida = w_dato
     c     sstal2_key    setll     sstal2
     c                   read      sstal2
     c                   if        not %eof and
     c                             sffirm = vvfirm and
     c                             sfvare = vvvare
     c                   eval      b_slett = *off
     c                   goto      end_ktrl
     c                   endif
      *
     c     end_ktrl      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_list
     c                   parm                    p_list2
      *
      * Kopierer fra .....
      *
      * Key for les på register over slettede varer
      *
     c     vvasl1_key    klist
     c                   kfld                    w_firm
      *
      * Kopierer til .....
      *
      * Key for oppslag på vare
      *
     c     vvarlu_key    klist
     c                   kfld                    w_firt
     c                   kfld                    vvarlu_vare
      *
      * Key for les på salgsstatistikk
      *
     c     sstal2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sstal2_vare
     c                   kfld                    sstal2_paar
     c                   kfld                    sstal2_bida
      *
      *
      * Kopiering av parameterliste til datastruktur og enkelt-parametre
      *
     c                   eval      d_list = p_list
     c                   eval      d_list2 = p_list2
      *
     c                   eval      w_firm = d_firm
     c                   eval      w_firt = d_firt
      *
     c                   endsr
