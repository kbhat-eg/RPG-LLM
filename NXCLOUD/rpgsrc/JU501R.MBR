     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JU501R
      * Beskrivelse . . : Modul, vise
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       120905 HKO  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjmodl1    if   e           k disk    rename(jmodpfr:jmodl1r)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fjprol1    if   e           k disk    rename(jpropfr:jprol1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
      *
     fju501d    cf   e             workstn
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_modn          s                   like(jumodn)
      *
      * Definering av Local data area
      *
     d                uds
     d l_fnav                951    980
      *
      * Definering av variable i nøkler
      *
     d jmodl1_vare     s                   like(jumodn)
     d jlevl1_ldor     s                   like(jlldor)
     d jprol1_prod     s                   like(jqprod)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      jmodl1_vare = p_modn
     c     jmodl1_key    chain     jmodl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      c2modn = p_modn
     c                   eval      c2mte1 = jumte1
     c                   eval      c2mte2 = jumte2
     c                   eval      c2merk = jumerk
     c                   eval      c2ldor = juldor
     c                   eval      c2prod = juprod
     c                   eval      c2ogrp = juogrp
     c                   eval      c2hgrp = juhgrp
     c                   eval      c2ugrp = juugrp
     c                   eval      c2mint = jumint
     c                   eval      c2eusr = jueusr
     c     *dmy          move      juodat        c2odat
     c     *dmy          move      juedat        c2edat
      *
     c                   eval      c2lnav = *blank
     c                   eval      c2pnav = *blank
     c                   eval      c2utxt = *blank
      *
     c                   if        c2ldor <> 0
     c                   eval      jlevl1_ldor = c2ldor
     c     jlevl1_key    chain     jlevl1
     c                   if        %found
     c                   eval      c2lnav = jlnavn
     c                   endif
     c                   endif
      *
     c                   if        c2prod <> 0
     c                   eval      jprol1_prod = c2prod
     c     jprol1_key    chain     jprol1
     c                   if        %found
     c                   eval      c2pnav = jqnavn
     c                   endif
     c                   endif
      *
     c                   if        c2ugrp <> 0
     c                   eval      vugrl1_uogr = c2ogrp
     c                   eval      vugrl1_uhgr = c2hgrp
     c                   eval      vugrl1_uugr = c2ugrp
     c     vugrl1_key    chain     vugrl1
     c                   if        %found
     c                   eval      c2utxt = vgutxt
     c                   endif
     c                   endif
      *
     c                   exfmt     c2bld
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_modn
      *
      * Key for oppslag på modul
      *
     c     jmodl1_key    klist
     c                   kfld                    jmodl1_vare
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for oppslag på produsent
      *
     c     jprol1_key    klist
     c                   kfld                    jprol1_prod
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
