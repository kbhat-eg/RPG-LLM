     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LD110R
      * Beskrivelse . . : Bestilling-linje, vedlikehold av tekstlinje
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       191102 HKO  Nytt program
      * 6.10  170609 BSA  Oppdatert med sporingsinformasjon
6.nu  * 6.30  100614 bof  Gjennomgått mp. nummerutvidelse
7.00  * 7.00  141119 bkv  132 bilde kompatibelt
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
     fvltyl1    if   e           k disk    rename(vltypfr:vltyl1r)
     flodtl1    if   e           k disk    rename(lodtpfr:lodtl1r)
     flodtlu    uf a e           k disk    rename(lodtpfr:lodtlur)
      *
     fld110d    cf   e             workstn
      *
      * Definering av parametre
      *
     d p_kode          s              1
     d p_firm          s                   like(lofirm)
     d p_numm          s                   like(lonumm)
     d p_suff          s                   like(losuff)
     d p_line          s                   like(ldline)
     d p_ltyp          s                   like(ldltyp)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
      *
      * Definering av variable i nøkler
      *
     d vltyl1_ltyp     s                   like(valtyp)
      *
      * Definering av variable
      *
     d w_firm          s                   like(ldfirm)
     d w_numm          s                   like(ldnumm)
     d w_suff          s                   like(ldsuff)
     d w_line          s                   like(ldline)
      *
     d w_ldat          s                   like(ldldat)
      *
      * Definering av konstanter
      *
     d lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer skjermbildet
      *
     c     lodtl1_key    chain     lodtl1
     c                   if        %found
     c                   eval      c1text = ldtek1 + ldtek2
     c                   eval      c1ltyp = ldltyp
     c                   eval      c1ldat = 0
     c                   if        ldldat <> *loval
     c     *dmy          move      ldldat        c1ldat
     c                   endif
     c                   else
     c                   eval      c1text = *blank
     c                   eval      c1ltyp = p_ltyp
     c                   eval      c1ldat = 0
     c                   if        c1ltyp = *blank
     c                   eval      c1ltyp = laatty
     c                   endif
     c                   endif
      *
      * Viser registreringbildet
      *
     c     c1tag         tag
      *
     c                   exfmt     c1win
      *
     c                   if        *inkc or *inkl
     c                   goto      avslutt
     c                   endif
      *
      * Validering
      *
      * Linjetype
      *
     c                   if        c1ltyp = *blank
     c                   eval      c1ltyp = laatty
     c                   endif
      *
     c                   eval      vltyl1_ltyp = c1ltyp
     c     vltyl1_key    chain     vltyl1
     c                   if        not %found
     c                   eval      *in31 = *on
     c                   goto      c1tag
     c                   endif
      *
     c                   if        valktx = 0
     c                   eval      *in32 = *on
     c                   goto      c1tag
     c                   endif
      *
      * Leveringsdato
      *
     c                   if        c1ldat <> 0
     c     *dmy          test(d)                 c1ldat                 33
     c                   if        *in31 = *on
     c                   goto      c1tag
     c                   endif
     c                   endif
      *
      * Leveringsdato kan ikke være eldre enn bestillingsdato
      *
     c                   if        c1ldat <> 0
     c     *dmy          move      c1ldat        w_ldat
     c                   if        w_ldat < lobdat
     c                   eval      *in34 = *on
     c                   goto      c1tag
     c                   endif
     c                   endif
      *
      * Oppdaterer bestilling-linje
      *
     c                   clear                   lodtlur
      *
     c     lodtlu_key    chain     lodtlur
      *
     c                   eval      ldotyp = lootyp
     c                   eval      ldlage = lolage
      *
     c                   eval      ldldor = loldor
      *
     c                   eval      ldtek1 = %subst(c1text:1:35)
     c                   eval      ldtek2 = %subst(c1text:36:35)
      *
     c                   if        laahoy = 1
     c     lo:up         xlate     ldtek1        ldtek1
     c     lo:up         xlate     ldtek2        ldtek2
     c                   endif
      *
     c                   eval      ldltyp = c1ltyp
      *
     c                   eval      ldldat = *loval
     c                   if        c1ldat <> 0
     c     *dmy          move      c1ldat        ldldat
     c                   endif
      *
     c                   if        not %found(lodtlu)
     c                   eval      ldfirm = w_firm
     c                   eval      ldnumm = w_numm
     c                   eval      ldsuff = w_suff
     c                   eval      ldline = w_line
6.10 c                   time                    ldodat
6.10 c                   time                    ldotim
6.10 c                   eval      ldousr = l_user
6.10 c                   time                    ldedat
6.10 c                   time                    ldetim
6.10 c                   eval      ldeusr = l_user
     c                   write     lodtlur
     c                   else
6.10 c                   time                    ldedat
6.10 c                   time                    ldetim
6.10 c                   eval      ldeusr = l_user
     c                   update    lodtlur
     c                   endif
      *
      * Legger inn kode som sier at minst en linje er registrert
      *
     c                   eval      p_kode = *on
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_kode
     c                   parm                    p_firm
     c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_line
     c                   parm                    p_ltyp
      *
      * Key for oppslag på bestilling-hode
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
      *
      * Key for oppslag på kode-register
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på linjetype
      *
     c     vltyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltyl1_ltyp
      *
      * Key for oppslag på bestilling-linje
      *
     c     lodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
     c                   kfld                    w_line
      *
      * Key for oppdatering av bestilling-linje
      *
     c     lodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
     c                   kfld                    w_line
      *
      * Henter parametre
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_numm = p_numm
     c                   eval      w_suff = p_suff
     c                   eval      w_line = p_line
      *
      * Hent opplysninger fra bestilling-hode
      *
     c     lohel1_key    chain     lohel1r                            90
      *
      * Hent opplysninger fra kode-register
      *
     c     lstsl1_key    chain     lstsl1r                            90
      *
      *
     c                   endsr
