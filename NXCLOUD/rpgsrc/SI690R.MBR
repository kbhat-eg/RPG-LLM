      /title  ASSTAT Bytting av leverandørnummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASSTAT
      * Program . . . . : SI690R
      * Beskrivelse . . : Bytting av leverandørnummer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       080500 HKO  Nytt program
      *       290500 HKO  Opprettet og byttet til indeks for direkte les
      *                   på salgsstatistikk
8.01  *PRG2   220622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner - COMP
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsstalg    uf   e           k disk    rename(sstapfr:sstalgr)
     fsistl3    if   e           k disk    rename(sistpfr:sistl3r)
     fsistl1    uf   e           k disk    rename(sistpfr:sistl1r)
      *
      * Definering av parametere
      *
     d p_firm          s                   like(sffirm)
     d p_gldo          s                   like(sfldor)
     d p_ldor          s                   like(sfldor)
      *
      * Definering av variable i nøkler
      *
     d sstalg_ldor     s                   like(sfldor)
     d sistl1_paar     s                   like(sipaar)
     d sistl1_binr     s                   like(sibinr)
     d sistl1_line     s                   like(siline)
     d sistl1_bida     s                   like(sibida)
      *
      * Definering av variable
      *
     d w_firm          s                   like(sffirm)
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Salgsstatistikk
      *
     c                   eval      sstalg_ldor = p_gldo
     c     sstalg_key    setll     sstalg
     c     sstalg_key    reade     sstalgr                                90
     c                   dow       *in90 = *off
     c                   eval      sfldor = p_ldor
     c                   update    sstalgr
     c     sstalg_key    reade     sstalgr                                90
     c                   enddo
      *
      * Innkjøpsstatistikk
      *
     c     sistl3_key    setll     sistl3
     c     sistl3_key    reade     sistl3                                 90
     c                   dow       *in90 = *off
     c                   if        sildor = p_gldo or
     c                             sivaln = p_gldo or
     c                             sifaln = p_gldo
     c                   eval      sistl1_paar = sipaar
     c                   eval      sistl1_binr = sibinr
     c                   eval      sistl1_line = siline
     c                   eval      sistl1_bida = sibida
     c     sistl1_key    setll     sistl1
     c     sistl1_key    reade     sistl1r                                91
     c                   dow       *in91 = *off
     c                   if        sildor = p_gldo
     c                   eval      sildor = p_ldor
     c                   endif
     c                   if        sivaln = p_gldo
     c                   eval      sivaln = p_ldor
     c                   endif
     c                   if        sifaln = p_gldo
     c                   eval      sifaln = p_ldor
     c                   endif
     c                   update    sistl1r
     c     sistl1_key    reade     sistl1r                                91
     c                   enddo
     c                   endif
     c     sistl3_key    reade     sistl3                                 90
     c                   enddo
      *
      * Avslutt program
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine sfr initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_gldo
     c                   parm                    p_ldor
      *
      * Key for oppdatering av salgsstatistikk
      *
     c     sstalg_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sstalg_ldor
      *
      * Key for les på innkjøpsstatistikk
      *
     c     sistl3_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppdatering av innkjøpsstatistikk
      *
     c     sistl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sistl1_paar
     c                   kfld                    sistl1_binr
     c                   kfld                    sistl1_line
     c                   kfld                    sistl1_bida
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
