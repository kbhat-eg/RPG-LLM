     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : LV100R
      * Beskrivelse . . : Registrering/Vedlikehold av tellebunter
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *                   Nytt program
      *       161203 HKO  Endret parameter ved kall til VP900R
      *       120804 HKO  Endret parameter ved kall til VP900R
5.70  * PRGR  021208 bof  Utvidet med prisgruppe
6.10  * 6.10  170609 BSA  Oppdatert med sporingsinformasjon
6.11  * 6.10  140709 bof  Utvidet eannr til 14 og brukt std. oppslag
6.12  * 6.10  181109 bsa  Tar ikke hensyn til en miks av 13 og 14 siffer på ean koden
6.13  * 6.10  181109 bsa  Hvis diversevarer skal det ikke sjekkes mot enhet.
6.21  *       211010 bof  lagt verdi i time-stamp
6.22  * 6.20  250311 BSA  Timestamp må få sluttidspunkt for batch som
6.22  *                   verdi ved opprettelse. Dette reverserer 6.21.
6.23  * 6.20 190511  bof  Utvidet vl710r med div. info
6.24  * 6.20  290212 bof  Justert hente pris ihht. lev.nr på lager
6.nu  * 6.30 160914  nho  Endring vedr. nummerutvidelse
6.31  * 6.30 190515  bof  Skal ikke kunne telle varetype = T
6.32  * 6.30 030117  bof  Endret edtwrd i skjermbildet
6.33  * 6.30  170918 bkv  Utvidet med trelast sortiment
7.xx  * 7.00 190516  bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
8.04  *PRG2   160622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = avslutt
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flbdtlr    if   e           k disk    rename(lbdtpfr:lbdtlrr)
     flbdtl1    if   e           k disk    rename(lbdtpfr:lbdtl1r)
     flbdtl2    uf   e           k disk    rename(lbdtpfr:lbdtl2r)
     flbdtlu    uf a e           k disk    rename(lbdtpfr:lbdtlur)
     flbhelr    if   e           k disk    rename(lbhepfr:lbhelrr)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvarl3    if   e           k disk    rename(vvarpfr:vvarl3r)
     fvvarl4    if   e           k disk    rename(vvarpfr:vvarl4r)
     fvvenlr    if   e           k disk    rename(vvenpfr:vvenlrr)
     fvvenl2    if   e           k disk    rename(vvenpfr:vvenl2r)
6.22 flbchl1    if   e           k disk    rename(lbchpfr:lbchl1r)
6.33 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
     flv100d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Datastruktur for parameter
      *
     d                 ds
     d d_prec                        30
     d d_retu                         1    overlay(d_prec)
     d d_firm                         3  0 overlay(d_prec:2)
     d d_batc                        10    overlay(d_prec:5)
     d d_numm                         6  0 overlay(d_prec:15)
     d d_type                        10    overlay(d_prec:21)
      *
      *  Overføring av priser
      *
     d                 ds
     d hirec                        120
     d  hifirm                        3  0 overlay(hirec)
     d  hirkat                        3  0 overlay(hirec:4)
     d  hikund                        6  0 overlay(hirec:7)
     d  hikpro                        6  0 overlay(hirec:13)
     d  hivare                       15    overlay(hirec:19)
     d  hilety                        1  0 overlay(hirec:34)
     d  hienhe                        3    overlay(hirec:35)
     d  hidato                         d   overlay(hirec:38) datfmt(*iso)
     d  hitime                         t   overlay(hirec:48) timfmt(*iso)
6.nu d  hinumm                        8  0 overlay(hirec:56)
6.nu d  hikode                        1    overlay(hirec:64)
6.nu d  hidekn                        5  2 overlay(hirec:65)
6.nu d  hiavgf                        1  0 overlay(hirec:70)
6.nu d  hipriv                        1  0 overlay(hirec:71)
6.nu d  hitilb                        1    overlay(hirec:72)
6.nu d  hiskaf                        1    overlay(hirec:73)
6.nu d  hildor                        6  0 overlay(hirec:74)
6.nu d  hisapr                        9  2 overlay(hirec:80)
6.nu d  hikopr                        9  2 overlay(hirec:89)
8.04 d  hiprgr                        2    overlay(hirec:98)
     d  hiinlr                        1    overlay(hirec:120)
      *
      *                                                     Record fra
     d                 ds
     d horec                         80
     d  holety                        1  0 overlay(horec)
     d  hokpri                        1    overlay(horec:2)
     d  hooppr                        9  2 overlay(horec:3)
     d  hosapr                        9  2 overlay(horec:12)
     d  hokopr                        9  2 overlay(horec:21)
     d  horab1                        5  2 overlay(horec:30)
     d  horab2                        5  2 overlay(horec:35)
     d  hodekn                        5  2 overlay(horec:40)
     d  hopros                        5  2 overlay(horec:45)
     d  hokamp                        5    overlay(horec:50)
     d  hoalme                        4  0 overlay(horec:55)
     d  hobrty                        1  0 overlay(horec:59)
     d  hobrr1                        5  2 overlay(horec:60)
     d  hobrr2                        5  2 overlay(horec:65)
     d  hoomva                        1    overlay(horec:70)
8.04 d  hoprgr                        2    overlay(horec:71)
      *
6.22 d                 ds
6.22 d d_stmp                        26
6.22 d  d_dato                       10d   overlay(d_stmp)
6.22 d  d_ski1                        1    overlay(d_stmp:11) inz('-')
6.22 d  d_time                        8t   overlay(d_stmp:12)
6.22 d  d_ski2                        1    overlay(d_stmp:20) inz('.')
6.22 d  d_zero                        6    overlay(d_stmp:21) inz('000000')
      *
      * Local Data Area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variable i nøkler
      *
     d lbdtlr_batc     s                   like(lvbatc)
     d lbdtlr_numm     s                   like(lvnumm)
     d lbdtlr_line     s                   like(lvline)
     d lbdtl1_batc     s                   like(lvbatc)
     d lbdtl1_numm     s                   like(lvnumm)
     d lbdtl1_line     s                   like(lvline)
     d lbdtl2_batc     s                   like(lvbatc)
     d lbdtl2_numm     s                   like(lvnumm)
     d lbdtl2_vare     s                   like(lvvare)
     d lbdtlu_batc     s                   like(lvbatc)
     d lbdtlu_numm     s                   like(lvnumm)
     d lbdtlu_line     s                   like(lvline)
     d lbhelr_batc     s                   like(lubatc)
     d lbhelr_numm     s                   like(lunumm)
     d vvarl1_vare     s                   like(vvvare)
     d vvarl3_nobb     s                   like(vvnobb)
     d vvarl4_lvar     s                   like(vvlvar)
     d vvenlr_vare     s                   like(vevare)
     d vvenlr_enhe     s                   like(veenhe)
     d vvenl2_vare     s                   like(vevare)
     d vvenl2_saen     s                   like(vesaen)
6.22 d lbchl1_batc     s                   like(lubatc)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
      *
     d w_firm          s                   like(lvfirm)
     d w_batc          s                   like(lvbatc)
     d w_line          s                   like(lvline)
     d w_linb          s                   like(lvline)
     d w_vare          s                   like(vvvare)
     d w_tek1          s                   like(vvtek1)
     d w_prec          s             30
     d w_mvaf          s             10  9
     d w_mvap          s              6  2
     d w_mvat          s              5  4
     d w_date          s               d   datfmt(*iso)
     d w_anta          s                   like(lvantt)
     d w_enhe          s                   like(vvenhl)
     d w_enh1          s                   like(vvenhl)
     d w_enh2          s                   like(vvenhl)
     d w_enh3          s                   like(vvenhl)
8.04 d w_prgr          s              2
5.70 d w_avde          s              4  0
5.70 d w_vtyp          s                   like(vvvtyp)
6.11 d w_eann          s             14  0
6.11 d w_eava          s                   like(vvvare)
6.11 d w_eaen          s                   like(vvenhe)
6.11 d w_east          s              1
6.23 d w_utda          s                   like(vvudat)
6.23 d w_ldor          s                   like(vvldor)
6.23 d b_inlr          s              1    inz(*off)
6.33 d w_lv_nobb       s                   like(vvlnob)
6.33 d w_lv_modn       s                   like(vvlmod)
6.33 d w_lv_lldo       s                   like(vvlnle)
6.33 d w_lv_llva       s                   like(vvllva)
6.33 d w_lv_vare       s                   like(vvvare)
6.33 d w_nobb          s                   like(vvnobb)
     d x               s              1  0
      *
     d b_forn          s              1    inz(*off)
     d b_endr          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_frst          s              1    inz(*off)
     d b_blab          s              1    inz(*off)
     d b_beof          s              1    inz(*off)
     d b_antk          s              1    inz(*off)
      *
      * Definering av konstanter
      *
7.xx d c_sfil          s              2  0 inz(13)
      *
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * w_curn - Current recordnummer på siden
      * w_virn - Vis den side som inneholder denne record.
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C2WIN  - C2 Vindu for endring av linje
      * C3WIN  - C3 Vindu for registrering av diversevarer
      * C4WIN  - C4 Vindu for registrering av manuell telling
      * C5WIN  - C5 Vindu for registrering via scanning
      * B4WIN  - B4 Vindu for slette
      * B5WIN  - B5 Vindu antallskontroll
      *
     C/Exec SQL
     C+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR
     C/End-Exec
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      dsp_subfile
      *
      * Gå rett i registreringsvindu første gang
      *
     c                   if        b_frst = *on
     c                   exsr      xc6win
     c                   eval      b_frst = *off
     c                   endif
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_pgm
     c                   when      *inkd
     c                   exsr      sp_input
     c                   eval      b2vare = w_vare
     c                   goto      b2tagb
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc6win
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkg
     c                   exsr      snu_bla
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2line <> 0 or
     c                             b2vare <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å snu blaretning
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     snu_bla       begsr
      *
     c                   if        b_blab = *off
     c                   eval      b_blab = *on
     c                   eval      b1line = 9999
     c                   else
     c                   eval      b_blab = *off
     c                   eval      b1line = 0
     c                   endif
      *
     c                   if        w_seqe = ' '
     c                   eval      lbdtl1_line = b1line
     c     lbdtl1_key    setll     lbdtl1
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
     c                   endif
      *
     c     end_snu       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   else
     c                   eval      b1line = w_linb
     c                   endif
     c                   if        b_blab = *on
     c                   eval      b1line = b1line + 1
     c                   endif
      *
     c                   select
     c                   when      w_seqe = ' '
     c                   eval      lbdtl1_line = b1line
     c     lbdtl1_key    setll     lbdtl1
     c                   other
     c                   eval      lbdtl2_vare = b1vare
     c     lbdtl2_key    setll     lbdtl2
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på linjenummer
      *
     c                   if        b2line <> 0
     c                   eval      w_seqe = *blank
     c                   eval      lbdtl1_line = b2line
     c     lbdtl1_key    setll     lbdtl1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på varenummer
      *
     c                   if        b2vare <> *blank
     c                   eval      w_seqe = 'V'
     c                   eval      lbdtl2_vare = b2vare
     c     lbdtl2_key    setll     lbdtl2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2line = 0
     c                   eval      b2vare = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre post
      *
     c                   if        b1valg = 2
     c                   exsr      xc2win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slette post
      *
     c                   if        b1valg = 4
     c                   exsr      xb4win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Fortløpende oppdatering
      *
     c                   if        b1valg = 7
     c                   exsr      xc7win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av endringsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2win        begsr
      *
     c                   eval      lbdtlr_line = b1line
     c     lbdtlr_key    chain     lbdtlr
     c                   if        not %found
     c                   goto      end_c2win
     c                   endif
      *
     c                   if        lutype = 'DIVERSE   '
     c                   eval      b_endr = *on
     c                   exsr      reg_diverse
     c                   eval      b_endr = *off
     c                   eval      b_forn = *on
     c                   goto      end_c2win
     c                   endif
      *
     c                   movel     lvvare        c2vare
     c                   eval      c2tek1 = lvtek1
     c                   eval      c2antt = lvantt
     c                   eval      c2enh1 = lvenh1
      *
     c     xc2_ut        tag
     c                   exfmt     c2win
      *
     c                   if        *inkc = *on or
     c                             *inkl = *on
     c                   goto      end_c2win
     c                   endif
      *
      *  Tester gyldige verdier
      *
     c                   eval      w_enh1 = c2enh1
     c                   eval      w_vare = c2vare
     c                   exsr      sjekk_enh
     c                   if        b_feil = *on
     c                   eval      *in31 = *on
     c                   goto      xc2_ut
     c                   endif
      *
     c                   eval      w_anta = c2antt
     c                   eval      w_enhe = c2enh1
     c                   eval      b_antk = *off
     c                   exsr      sjekk_antall
     c                   if        b_antk = *on
     c                   eval      c2antt = 0
     c                   goto      xc2_ut
     c                   endif
      *
      *  Endrer valgt linje i telleliste
      *
     c                   eval      lbdtlu_line = b1line
     c     lbdtlu_key    chain     lbdtlur
     c                   if        %found
     c                   eval      lvantt = c2antt
     c                   eval      lvenh1 = c2enh1
6.10 c                   time                    lvedat
6.10 c                   time                    lvetim
6.10 c                   eval      lveusr = l_user
6.21 c**6.22             time                    lvtims
     c                   update    lbdtlur
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb4win        begsr
      *
     c                   eval      b4line = b1line
     c                   eval      b4vare = b1vare
     c                   eval      b4tek1 = b1tek1
     c                   eval      b4antt = b1anta
     c                   eval      b4enh1 = b1enhe
      *
     c                   exfmt     b4win
      *
     c                   if        *inkc = *on or
     c                             *inkl = *on
     c                   goto      end_b4win
     c                   endif
      *
      *  Sletter valgt linje i telleliste
      *
     c                   eval      lbdtlu_line = b1line
     c     lbdtlu_key    chain     lbdtlur
     c                   if        %found
     c                   delete    lbdtlur
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_b4win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av antalls kontroll
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_antall  begsr
      *
     c                   if        w_anta < 1000
     c                   goto      end_antall
     c                   endif
      *
     c                   eval      b5antt = w_anta
     c                   eval      b5enh1 = w_enhe
      *
     c                   exfmt     b5win
      *
     c                   if        *inkh = *off
     c                   eval      b_antk = *on
     c                   endif
      *
     c     end_antall    endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling nyregistrering avhengig av bunttype
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc6win        begsr
      *
     c                   select
     c                   when      lutype = 'DIVERSE   '
     c                   exsr      reg_diverse
     c                   when      lutype = 'SCANNE    '
     c                   exsr      reg_scanne
     c                   other
     c                   exsr      reg_manuell
     c                   endsl
      *
     c     end_c6win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sekvensiell oppdatering etter linjenummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc7win        begsr
      *
     c                   eval      lbdtlr_line = b1line
     c     lbdtlr_key    setll     lbdtlr
     c                   read      lbdtlr
      *
     c                   dow       not %eof and
     c                             lvbatc = d_batc and
     c                             lvnumm = lunumm
      *
     c                   eval      c2tek1 = lvtek1
     c                   eval      c2antt = lvantt
     c                   eval      c2enh1 = lvenh1
      *
     c     xc7_ut        tag
     c                   exfmt     c2win
      *
     c                   if        *inkc = *on or
     c                             *inkl = *on
     c                   goto      end_c7win
     c                   endif
      *
      *  Tester gyldige verdier
      *
     c                   eval      w_anta = c2antt
     c                   eval      w_enhe = c2enh1
     c                   eval      b_antk = *off
     c                   exsr      sjekk_antall
     c                   if        b_antk = *on
     c                   eval      c2antt = 0
     c                   goto      xc7_ut
     c                   endif
      *
     c                   eval      w_vare = lvvare
     c                   eval      w_enh1 = c2enh1
     c                   exsr      sjekk_enh
     c                   if        b_feil = *on
     c                   eval      *in31 = *on
     c                   goto      xc7_ut
     c                   endif
      *
      *  Endrer valgt linje i telleliste
      *
     c                   eval      lbdtlu_line = lvline
     c     lbdtlu_key    chain     lbdtlur
     c                   if        %found
     c                   eval      lvantt = c2antt
     c                   eval      lvenh1 = c2enh1
6.10 c                   time                    lvedat
6.10 c                   time                    lvetim
6.10 c                   eval      lveusr = l_user
6.21 c**6.22             time                    lvtims
     c                   update    lbdtlur
     c                   eval      b_forn = *on
     c                   endif
      *
     c                   read      lbdtlr
     c                   enddo
      *
     c     end_c7win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for registrering av diversevarer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     reg_diverse   begsr
      *
     c     c3tag0        tag
      *
     c                   if        b_endr = *on
     c                   eval      lbdtlr_line = b1line
     c     lbdtlr_key    chain     lbdtlr
     c                   if        not %found
     c                   goto      end_diverse
     c                   endif
      *
     c                   movel     lvvare        c3vare
     c                   eval      c3tek1 = lvtek1
     c                   eval      c3antt = lvantt
     c                   eval      c3enh1 = lvenh1
     c                   eval      c3spny = lvspny
     c                   eval      c3kpny = lvkpny
     c                   else
     c                   eval      c3vare = *blank
     c                   eval      c3tek1 = *blank
     c                   eval      c3antt = 0
     c                   eval      c3enh1 = *blank
     c                   eval      c3spny = 0
     c                   eval      c3kpny = 0
     c                   eval      c3spmv = 0
     c                   end
      *
     c     c3tag1        tag
     c                   exfmt     c3win
      *
      * Varesøk?
      *
     c                   if        *inkd = *on
     c                   exsr      sp_input
     c                   movel     w_vare        c3vare
     c                   goto      c3tag1
     c                   endif
      *
      * Avslutte registrering?
      *
     c                   if        *inkc = *on or
     c                             *inkl = *on or
     c                             c3vare = *blank
     c                   goto      end_diverse
     c                   endif
      *
      * Sjekk antall, varenummer og type
      *
     c                   eval      w_vare = c3vare
     c                   exsr      les_vare
      *
     c                   if        b_feil = *on or
5.70 c                             w_vtyp <> 'D'
     c                   eval      *in31 = *on
     c                   goto      c3tag1
     c                   endif
      *
      * Kontroller input
      *
     c                   eval      w_anta = c3antt
     c                   eval      w_enhe = c3enh1
     c                   eval      b_antk = *off
     c                   exsr      sjekk_antall
     c                   if        b_antk = *on
     c                   eval      c3antt = 0
     c                   goto      c3tag1
     c                   endif
      *
     c                   if        c3tek1 = *blank
     c                   eval      *in32 = *on
     c                   goto      c3tag1
     c                   endif
      *
     c                   if        c3antt = 0
     c                   eval      *in33 = *on
     c                   goto      c3tag1
     c                   endif
      *
6.13 c                   if        w_vtyp <> 'D'
     c                   if        c3enh1 = *blank
     c                   eval      *in34 = *on
     c                   goto      c3tag1
     c                   endif
6.13 c                   endif
      *
     c                   if        c3spny = 0 or
     c                             c3kpny = 0
     c                   exsr      beregn_pris
     c                   eval      c3spmv = 0
     c                   endif
      *
     c                   if        c3spny = 0
     c                   eval      *in35 = *on
     c                   goto      c3tag1
     c                   endif
      *
     c                   if        c3kpny = 0
     c                   eval      *in36 = *on
     c                   goto      c3tag1
     c                   endif
      *
      * Registrer ny tellelinje
      *
     c                   if        b_endr = *off
     c                   eval      w_line = w_line + 1
      *
     c                   eval      lvfirm = w_firm
     c                   eval      lvbatc = lubatc
     c                   eval      lvnumm = lunumm
     c                   eval      lvline = w_line
     c                   eval      lvtlin = 0
     c                   eval      lvdlop = 0
     c                   eval      lvogrp = vvogrp
     c                   eval      lvhgrp = vvhgrp
     c                   eval      lvugrp = vvugrp
     c                   eval      lvvare = c3vare
     c                   eval      lvlage = lulage
     c                   eval      lvplas = *blank
     c                   eval      lvtek1 = c3tek1
     c                   eval      lvenh1 = c3enh1
     c                   eval      lvspny = c3spny
     c                   eval      lvkpny = c3kpny
     c                   eval      lvantt = c3antt
6.10 c                   time                    lvodat
6.10 c                   time                    lvotim
6.10 c                   eval      lvousr = l_user
6.10 c                   time                    lvedat
6.10 c                   time                    lvetim
6.10 c                   eval      lveusr = l_user
6.21 c**6.22             time                    lvtims
6.22  * Vi må legge ut sluttidspunkt for batchen, hvis ikke blir timestamp
6.22  * helt vilkårlig.
6.22 c                   eval      lbchl1_batc = lubatc
6.22 c     lbchl1_key    chain     lbchl1
6.22 c                   if        %found
6.22 c                   eval      d_dato = lcedat
6.22 c                   eval      d_time = lcetim
6.22 c                   eval      lvtims = %timestamp(d_stmp)
6.22 c                   else
6.22 c                   time                    lvtims
6.22 c                   endif
6.22  *
     c                   write     lbdtlur
     c                   endif
      *
      * Endre eksisterende tellelinje
      *
     c                   if        b_endr = *on
     c                   eval      lbdtlu_line = b1line
     c     lbdtlu_key    chain     lbdtlu
     c                   if        %found
     c                   eval      lvvare = c3vare
     c                   eval      lvtek1 = c3tek1
     c                   eval      lvenh1 = c3enh1
     c                   eval      lvspny = c3spny
     c                   eval      lvkpny = c3kpny
     c                   eval      lvantt = c3antt
6.10 c                   time                    lvedat
6.10 c                   time                    lvetim
6.10 c                   eval      lveusr = l_user
6.21 c                   time                    lvtims
     c                   update    lbdtlur
     c                   endif
     c                   endif
      *
      * Flere linjer?
      *
     c                   if        b_endr = *off
      *
     c                   eval      w_fcrn = 0
     c                   eval      w_linb = w_line
      *
     c                   exsr      forny
     c                   eval      b_frst = *on
     c                   exsr      dsp_subfile
     c                   eval      b_frst = *off
      *
     c                   goto      c3tag0
     c                   endif
      *
     c     end_diverse   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for manuell registrering
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     reg_manuell   begsr
      *
     c     c4tag0        tag
     c                   eval      c4vare = *blank
     c                   eval      c4tek1 = *blank
     c                   eval      c4antt = 0
     c                   eval      c4enh1 = *blank
     c                   eval      c4enh2 = *blank
     c                   eval      c4enh3 = *blank
     c                   eval      *in39 = *off
     c                   eval      *in80 = *off
     c                   eval      *in81 = *off
     c                   eval      *in82 = *off
      *
     c     c4tag1        tag
     c                   exfmt     c4win
      *
      * Varesøk?
      *
     c                   if        *inkd = *on
     c                   exsr      sp_input
     c                   movel     w_vare        c4vare
     c                   goto      c4tag1
     c                   endif
      *
      * Avslutte registrering?
      *
     c                   if        *inkc = *on or
     c                             *inkl = *on or
     c                             c4vare = *blank
     c                   goto      end_manuell
     c                   endif
      *
      * F1=Bytting av enhet
      *
     c                   if        *inka
     c                   select
     c                   when      *in80 = *on and
     c                             c4enh2 <> *blank
     c                   eval      *in80 = *off
     c                   eval      *in81 = *on
     c                   when      *in81 = *on
     c                   eval      *in81 = *off
     c                   if        c4enh3 <> *blank
     c                   eval      *in82 = *on
     c                   else
     c                   eval      *in80 = *on
     c                   endif
     c                   when      *in82 = *on
     c                   eval      *in82 = *off
     c                   eval      *in80 = *on
     c                   endsl
     c                   goto      c4tag1
     c                   endif
      *
      * Identifiser vare
      *
     c                   if        *in39 = *off
     c                   eval      w_vare = c4vare
     c                   exsr      les_vare
      *
     c                   if        b_feil = *on
     c                   eval      *in31 = *on
     c                   goto      c4tag1
     c                   endif
      *
5.70 c                   if        w_vtyp = 'D' or
6.31 c                             w_vtyp = 'T'
     c                   eval      *in31 = *on
     c                   goto      c4tag1
     c                   endif
      *
     c                   eval      c4vare = vvvare
     c                   eval      c4tek1 = vvtek1
     c                   eval      c4enh1 = w_enh1
     c                   eval      c4enh2 = w_enh2
     c                   eval      c4enh3 = w_enh3
      *
     c                   if        c4antt = 0
     c                   eval      *in39 = *on
     c                   goto      c4tag1
     c                   endif
     c                   endif
      *
      * Kontroller input
      *
     c                   eval      w_anta = c4antt
     c                   eval      w_enhe = c4enh1
     c                   eval      b_antk = *off
     c                   exsr      sjekk_antall
     c                   if        b_antk = *on
     c                   eval      c4antt = 0
     c                   goto      c4tag1
     c                   endif
      *
     c                   eval      w_vare = c4vare
     c                   select
     c                   when      *in80 = *on
     c                   eval      w_enh1 = c4enh1
     c                   when      *in81 = *on
     c                   eval      w_enh1 = c4enh2
     c                   when      *in82 = *on
     c                   eval      w_enh1 = c4enh3
     c                   endsl
      *
     c                   exsr      sjekk_enh
     c                   if        b_feil = *on
     c                   eval      *in33 = *on
     c                   goto      c4tag1
     c                   endif
      *
      * Registrer tellelinje
      *
     c                   eval      w_line = w_line + 1
      *
     c                   eval      lvfirm = w_firm
     c                   eval      lvbatc = lubatc
     c                   eval      lvnumm = lunumm
     c                   eval      lvline = w_line
     c                   eval      lvtlin = 0
     c                   eval      lvdlop = 0
     c                   eval      lvogrp = vvogrp
     c                   eval      lvhgrp = vvhgrp
     c                   eval      lvugrp = vvugrp
     c                   eval      lvvare = c4vare
     c                   eval      lvlage = lulage
     c                   eval      lvplas = *blank
     c                   eval      lvtek1 = vvtek1
     c                   eval      lvenh1 = w_enh1
     c                   eval      lvspny = 0
     c                   eval      lvkpny = 0
     c                   eval      lvantt = c4antt
6.10 c                   time                    lvodat
6.10 c                   time                    lvotim
6.10 c                   eval      lvousr = l_user
6.10 c                   time                    lvedat
6.10 c                   time                    lvetim
6.10 c                   eval      lveusr = l_user
6.21 c**6.22             time                    lvtims
6.22  * Vi må legge ut sluttidspunkt for batchen, hvis ikke blir timestamp
6.22  * helt vilkårlig.
6.22 c                   eval      lbchl1_batc = lubatc
6.22 c     lbchl1_key    chain     lbchl1
6.22 c                   if        %found
6.22 c                   eval      d_dato = lcedat
6.22 c                   eval      d_time = lcetim
6.22 c                   eval      lvtims = %timestamp(d_stmp)
6.22 c                   else
6.22 c                   time                    lvtims
6.22 c                   endif
6.22  *
     c                   write     lbdtlur
      *
     c                   eval      w_fcrn = 0
     c                   eval      w_linb = w_line
      *
     c                   exsr      forny
     c                   eval      b_frst = *on
     c                   exsr      dsp_subfile
     c                   eval      b_frst = *off
      *
     c                   goto      c4tag0
      *
     c     end_manuell   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for registrering via scanning
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     reg_scanne    begsr
      *
     c     c5tag0        tag
     c                   eval      c5vare = *blank
     c                   eval      c5tek1 = *blank
     c                   eval      c5antt = 0
     c                   eval      c5enh1 = *blank
     c                   eval      c5enh2 = *blank
     c                   eval      c5enh3 = *blank
     c                   eval      *in39 = *off
     c                   eval      *in80 = *off
     c                   eval      *in81 = *off
     c                   eval      *in82 = *off
      *
     c     c5tag1        tag
     c                   exfmt     c5win
      *
      * Varesøk?
      *
     c                   if        *inkd = *on
     c                   exsr      sp_input
     c                   movel     w_vare        c5vare
     c                   goto      c5tag1
     c                   endif
      *
      * Avslutte registrering?
      *
     c                   if        *inkc = *on or
     c                             *inkl = *on or
     c                             c5vare = *blank
     c                   goto      end_scanne
     c                   endif
      *
      * F1=Bytting av enhet
      *
     c                   if        *inka
     c                   select
     c                   when      *in80 = *on and
     c                             c5enh2 <> *blank
     c                   eval      *in80 = *off
     c                   eval      *in81 = *on
     c                   when      *in81 = *on
     c                   eval      *in81 = *off
     c                   if        c5enh3 <> *blank
     c                   eval      *in82 = *on
     c                   else
     c                   eval      *in80 = *on
     c                   endif
     c                   when      *in82 = *on
     c                   eval      *in82 = *off
     c                   eval      *in80 = *on
     c                   endsl
     c                   goto      c5tag1
     c                   endif
      *
      * Identifiser scannet vare
      *
     c                   if        *in39 = *off
     c                   exsr      finn_vare
     c                   exsr      les_vare
      *
     c                   if        b_feil = *on
     c                   eval      *in31 = *on
     c                   goto      c5tag1
     c                   endif
      *
5.70 c                   if        w_vtyp = 'D' or
6.31 c                             w_vtyp = 'T'
     c                   eval      *in31 = *on
     c                   goto      c5tag1
     c                   endif
      *
     c                   eval      *in39 = *on
     c                   eval      c5vare = vvvare
     c                   eval      c5tek1 = vvtek1
     c                   eval      c5antt = 0
     c                   eval      c5enh1 = w_enh1
     c                   eval      c5enh2 = w_enh2
     c                   eval      c5enh3 = w_enh3
     c                   goto      c5tag1
     c                   endif
      *
      * Kontroller input
      *
     c                   eval      w_anta = c5antt
     c                   eval      w_enhe = c5enh1
     c                   eval      b_antk = *off
     c                   exsr      sjekk_antall
     c                   if        b_antk = *on
     c                   eval      c5antt = 0
     c                   goto      c5tag1
     c                   endif
      *
     c                   if        c5antt = 0
     c                   eval      *in32 = *on
     c                   goto      c5tag1
     c                   endif
      *
     c                   eval      w_vare = c5vare
     c                   select
     c                   when      *in80 = *on
     c                   eval      w_enh1 = c5enh1
     c                   when      *in81 = *on
     c                   eval      w_enh1 = c5enh2
     c                   when      *in82 = *on
     c                   eval      w_enh1 = c5enh3
     c                   endsl
     c                   exsr      sjekk_enh
     c                   if        b_feil = *on
     c                   eval      *in33 = *on
     c                   goto      c5tag1
     c                   endif
      *
      * Registrer tellelinje
      *
     c                   eval      w_line = w_line + 1
      *
     c                   eval      lvfirm = w_firm
     c                   eval      lvbatc = lubatc
     c                   eval      lvnumm = lunumm
     c                   eval      lvline = w_line
     c                   eval      lvtlin = 0
     c                   eval      lvdlop = 0
     c                   eval      lvogrp = vvogrp
     c                   eval      lvhgrp = vvhgrp
     c                   eval      lvugrp = vvugrp
     c                   eval      lvvare = c5vare
     c                   eval      lvlage = lulage
     c                   eval      lvplas = *blank
     c                   eval      lvtek1 = vvtek1
     c                   eval      lvenh1 = w_enh1
     c                   eval      lvspny = 0
     c                   eval      lvkpny = 0
     c                   eval      lvantt = c5antt
6.10 c                   time                    lvodat
6.10 c                   time                    lvotim
6.10 c                   eval      lvousr = l_user
6.10 c                   time                    lvedat
6.10 c                   time                    lvetim
6.10 c                   eval      lveusr = l_user
6.21 c**6.22             time                    lvtims
6.22  * Vi må legge ut sluttidspunkt for batchen, hvis ikke blir timestamp
6.22  * helt vilkårlig.
6.22 c                   eval      lbchl1_batc = lubatc
6.22 c     lbchl1_key    chain     lbchl1
6.22 c                   if        %found
6.22 c                   eval      d_dato = lcedat
6.22 c                   eval      d_time = lcetim
6.22 c                   eval      lvtims = %timestamp(d_stmp)
6.22 c                   else
6.22 c                   time                    lvtims
6.22 c                   endif
6.22  *
     c                   write     lbdtlur
      *
     c                   eval      w_fcrn = 0
     c                   eval      w_linb = w_line
      *
     c                   exsr      forny
     c                   eval      b_frst = *on
     c                   exsr      dsp_subfile
     c                   eval      b_frst = *off
      *
     c                   goto      c5tag0
      *
     c     end_scanne    endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = ' '
     c                   if        b_blab = *on
     c                   readp     lbdtl1
     c                   else
     c                   read      lbdtl1
     c                   endif
     c                   other
     c                   read      lbdtl2
     c                   endsl
      *
     c                   if        %eof or
     c                             lvfirm <> w_firm or
     c                             lvbatc <> d_batc or
     c                             lvnumm <> lunumm
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      lbdtl1_line = lvline
     c                   other
     c                   eval      lbdtl2_vare = lvvare
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1line = lvline
     c                   movel     lvvare        b1vare
     c                   eval      b1tek1 = lvtek1
     c                   eval(h)   b1anta = lvantt
     c                   eval      b1enhe = lvenh1
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = ' '
     c     lbdtl1_key    setgt     lbdtl1
     c                   readp     lbdtl1
     c                   other
     c     lbdtl2_key    setgt     lbdtl2
     c                   readp     lbdtl2
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = ' '
     c                   readp     lbdtl1
     c                   other
     c                   readp     lbdtl2
     c                   endsl
      *
     c                   if        %eof or
     c                             lvfirm <> w_firm or
     c                             lvbatc <> d_batc or
     c                             lvnumm <> lunumm
     c                   eval      b_beof = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = ' '
     c                   eval      lbdtl1_line = lvline
     c                   other
     c                   eval      lbdtl2_vare = lvvare
     c                   endsl
      *
     c                   enddo
      *
     c                   if        b_beof = *on
     c                   select
     c                   when      w_seqe = ' '
     c                   if        b_blab = *off
     c                   eval      lbdtl1_line = 0
     c                   else
     c                   eval      lbdtl1_line = 999999
     c                   endif
     c     lbdtl1_key    setll     lbdtl1
     c                   other
     c     lbdtl2_key    setll     lbdtl2
     c                   endsl
     c                   eval      b_beof = *off
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
      *
     c                   if        b_frst = *on
     c                   write     b2ctl
     c                   else
     c                   exfmt     b2ctl
     c                   endif
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for spørring på input
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sp_input      begsr
      *
      *  Spørring på varenummer
      *
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_tek1
      *
      *
     c     sp_end        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for henting av vareinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_vare      begsr
      *
     c                   eval      b_feil = *off
     c                   eval      vvarl1_vare = w_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   eval      b_feil = *on
     c                   goto      vare_end
     c                   endif
      *
      * henter varetype
      *
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    vvvare
5.70 c                   parm                    lulage
5.70 c                   parm                    w_vtyp
6.23 c                   parm                    w_utda
6.23 c                   parm                    w_ldor
6.23 c                   parm                    b_inlr
6.33 c                   parm                    w_lv_nobb
6.33 c                   parm                    w_lv_modn
6.33 c                   parm                    w_lv_lldo
6.33 c                   parm                    w_lv_llva
      *
      * Henter tre salgsenheter.
      *
     c                   eval      *in80 = *on
     c                   eval      *in81 = *off
     c                   eval      *in82 = *off
      *
     c                   eval      x = 0
     c                   eval      w_enh1 = *blank
     c                   eval      w_enh2 = *blank
     c                   eval      w_enh3 = *blank
      *
     c                   eval      vvenl2_vare = vvvare
     c                   eval      vvenl2_saen = 1
     c     vvenl2_key    setll     vvenl2
     c     vvenl2_key2   reade     vvenl2                                 90
     c                   dow       *in90 = *off and
     c                             x < 3
      *
     c                   eval      x = x + 1
      *
     c                   select
     c                   when      x = 1
     c                   eval      w_enh1 = veenhe
     c                   when      x = 2
     c                   eval      w_enh2 = veenhe
     c                   when      x = 3
     c                   eval      w_enh3 = veenhe
     c                   endsl
      *
     c     vvenl2_key2   reade     vvenl2                                 90
     c                   enddo
      *
     c     vare_end      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne vare på EAN,NOBB,LEV.VNR
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_vare     begsr
      *
      * Sjekk, ean-nummer
      *
6.12 c                   move      c5vare        w_eann
6.11 c                   call      'VE710R  '
6.11 c                   parm                    w_firm
6.11 c                   parm                    w_eann
6.11 c                   parm                    w_eava
6.11 c                   parm                    w_eaen
6.11 c                   parm                    w_east
6.11 c                   if        w_east = '1'
6.12 c                   eval      w_vare = w_eava
     c                   goto      end_finn_vare
     c                   endif
      *
      * Sjekk, nobb-nummer
      *
6.33  * sjekker om logistikk-vare
6.33 c                   eval      w_lv_vare = *blank
6.33 c                   move      b1vare        w_nobb
6.33 c/exec sql
6.33 c+ select jvqvnr
6.33 c+ into   :w_lv_vare
6.33 c+ from   jvklpf
6.33 c+ where  jvquno = :w_nobb and jvqfir = :w_firm
6.33 c+ fetch first 1 rows only
6.33 c/end-exec
6.33 c                   if        w_lv_vare <> *blank
6.33 c                   eval      w_vare = w_lv_vare
6.33 c                   goto      end_finn_vare
6.33 c                   endif
6.33  *
     c                   movel     c5vare        vvarl3_nobb
     c     vvarl3_key    chain     vvarl3r
     c                   if        %found
     c                   eval      w_vare = vvvare
     c                   goto      end_finn_vare
     c                   endif
      *
      * Sjekk, leverandørens varenummer
      *
6.33  * sjekker om logistikk-vare
6.33 c                   eval      w_lv_vare = *blank
6.33 c/exec sql
6.33 c+ select vvlvnr
6.33 c+ into   :w_lv_vare
6.33 c+ from   vvlepf
6.33 c+ where  vvllva = :c5vare
6.33 c+ fetch first 1 rows only
6.33 c/end-exec
6.33 c                   if        w_lv_vare <> *blank
6.33 c                   eval      w_vare = w_lv_vare
6.33 c                   goto      end_finn_vare
6.33 c                   endif
     c                   movel     c5vare        vvarl4_lvar
     c     vvarl4_key    chain     vvarl4r
     c                   if        %found
     c                   eval      w_vare = vvvare
     c                   goto      end_finn_vare
     c                   endif
      *
      * Ingen tilslag - prøver eget varenummer
      *
     c                   eval      w_vare = c5vare
      *
     c     end_finn_vare endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for bergning av pris ut i fra pris inkl. mva
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     beregn_pris   begsr
      *
      *  Finner hvilken mva-sats som skal benyttes
      *
     c                   time                    w_date
      *
     c                   call      'FÅ705R'
     c                   parm                    w_firm
     c                   parm                    w_date
     c                   parm                    w_mvap
     c                   parm                    w_mvat
     c                   parm                    w_mvaf
      *
     c                   eval      hifirm = w_firm
     c                   eval      hirkat = 0
     c                   eval      hikund = 0
     c                   eval      hikpro = 0
     c                   eval      hivare = c3vare
     c                   eval      hilety = 0
     c                   eval      hienhe = c3enh1
     c                   eval      hidato = w_date
     c                   eval      hitime = *loval
     c                   eval      hinumm = 0
     c                   eval      hikode = *blank
     c                   eval      hidekn = 0
     c                   eval      hiavgf = 0
     c                   eval      hipriv = 0
     c                   eval      hiskaf = *off
6.24 c                   eval      hildor = w_ldor
     c                   eval      hisapr = 0
     c                   eval      hikopr = 0
5.70 c                   eval      hiprgr = w_prgr
      *
     c                   call      'VP900R'
     c                   parm                    hirec
     c                   parm                    horec
      *
     c                   if        c3spny = 0 and
     c                             c3spmv <> 0
     c                   eval(h)   c3spny = c3spmv * w_mvaf
     c                   eval      c3spmv = 0
     c                   endif
      *
     c                   if        c3kpny = 0 and
     c                             hopros <> 0
     c                   eval      c3kpny = c3spny * hopros
     c                   endif
      *
     c                   if        c3spny = 0 and
     c                             hopros <> 0
     c                   eval(h)   c3spny = c3kpny / hopros
     c                   endif
      *
     c     end_beregn    endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for kontroll av gyldig enhet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_enh     begsr
      *
     c                   eval      b_feil = *off
     c                   movel     w_vare        vvenlr_vare
     c                   eval      vvenlr_enhe = w_enh1
     c     vvenlr_key    chain     vvenlr
     c                   if        not %found
     c                   eval      b_feil = *on
     c                   endif
      *
     c     sjekk_end     endsr
5.70  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5 70  * Subrutine for å hente prisgruppe
5.70  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.70  *
5.70 c     hent_prisgr   begsr
5.70  *
5.70 c                   eval      w_avde = 0
5.70 c                   call      'VL712R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    lulage
5.70 c                   parm                    w_avde
5.70 c                   parm                    w_prgr
5.70  *
5.70 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    w_prec
      *
      *  Key for les av TELLEBUNT
      *
     c     lbdtlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbdtlr_batc
     c                   kfld                    lbdtlr_numm
     c                   kfld                    lbdtlr_line
      *
      *  Key for posisjonering av TELLEBUNT pr. linje
      *
     c     lbdtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbdtl1_batc
     c                   kfld                    lbdtl1_numm
     c                   kfld                    lbdtl1_line
      *
      *  Key for posisjonering av TELLEBUNT pr. vare
      *
     c     lbdtl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbdtl2_batc
     c                   kfld                    lbdtl2_numm
     c                   kfld                    lbdtl2_vare
      *
      *  Key for oppdatering av TELLEBUNT
      *
     c     lbdtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbdtlu_batc
     c                   kfld                    lbdtlu_numm
     c                   kfld                    lbdtlu_line
      *
      *  Key for les av LAGERTELLINGSBUNT-HODE
      *
     c     lbhelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbhelr_batc
     c                   kfld                    lbhelr_numm
      *
      *  Key for les av VARE på eget varenummer
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på nobb-nummer
      *
     c     vvarl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl3_nobb
      *
      *  Key for oppslag på leverandørens varenr
      *
     c     vvarl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl4_lvar
      *
      *  Key for les av VARE/ENHET
      *
     c     vvenlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenlr_vare
     c                   kfld                    vvenlr_enhe
      *
      * Key for les på vare-enhet
      *
     c     vvenl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
     c                   kfld                    vvenl2_saen
      *
     c     vvenl2_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
6.22  *
6.22  *  Key for oppslag på batchregister
6.22  *
6.22 c     lbchl1_key    klist
6.22 c                   kfld                    w_firm
6.22 c                   kfld                    lbchl1_batc
      *
      *  Legger inn verdier i nøklene
      *
     c                   eval      d_prec = w_prec
     c                   eval      w_firm = d_firm
     c                   eval      lbdtlr_batc = d_batc
     c                   eval      lbdtl1_batc = d_batc
     c                   eval      lbdtl2_batc = d_batc
     c                   eval      lbdtlu_batc = d_batc
     c                   eval      lbhelr_batc = d_batc
     c                   eval      lbdtlr_numm = d_numm
     c                   eval      lbdtl1_numm = d_numm
     c                   eval      lbdtl2_numm = d_numm
     c                   eval      lbdtlu_numm = d_numm
     c                   eval      lbhelr_numm = d_numm
      *
      *  Les tellebunt hode
      *
     c     lbhelr_key    chain     lbhelr
      *
     c                   if        %found
     c                   eval      b2numm = lunumm
     c                   eval      b2text = lutext
     c                   eval      b2type = lutype
     c                   endif
      *
     c                   exsr      hent_prisgr
      *
      * Finn sist brukte linje på bunt
      *
     c                   eval      lbdtl1_line = 999999
     c     lbdtl1_key    setll     lbdtl1
     c                   readp     lbdtl1
     c                   if        %eof or
     c                             lvbatc <> lubatc or
     c                             lvnumm <> lunumm
     c                   eval      w_line = 0
     c                   else
     c                   eval      w_line = lvline
     c                   endif
      *
      * Sjekker om nyregistrering og direkte utlegg av registreringsvindu
      *
     c                   if        d_retu = 'N'
     c                   eval      b_frst = *on
     c                   endif
      *
      * Posisjoner på start bunt og fyll subfile
      *
     c                   eval      lbdtl1_line = 0
     c                   eval      w_seqe = ' '
     c     lbdtl1_key    setll     lbdtl1
     c                   exsr      crt_subfile
      *
     c                   eval      *in22 = *on
      *
     c                   endsr
      *
