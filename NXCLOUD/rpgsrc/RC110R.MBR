     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RC110R
      * Beskrivelse . . : Vedlikehold saksregister
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       221200 KOB  Nytt program
s.ss  *       130919 sst  Fjenet hardkodet testsak for oppslag mot test
7.xx  *       150120 bkv  Cloud tilpasninger
7.01  *       070920 sst  Verdier i felter vedr check-boks i grafisk
7.02  *       091220 sst  Justert størrelse på subfile
7.03  *       091220 sst  Bare vise saker på det inkssobyrå man er inne på
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frcnfl1    if   e           k disk    rename(rcnfpfr:rcnfl1r)
     frcnfl2    if   e           k disk    rename(rcnfpfr:rcnfl2r)
     frcnflr    uf   e           k disk    rename(rcnfpfr:rcnflrr)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
7.03 frca0l1    if   e           k disk    rename(rca0pfr:rca0l1r)
      *
     frc110d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av array
      *
     d a_meld          s             11    dim(3) ctdata perrcd(1)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_file                931    933
     d l_firm                944    946  0
      *
      * Definering av parametre
      *
      *
      * Definering av variabler i nøkler
      *
     d rcnfl1_nask     s                   like(rcnask)
     d rcnfl2_nsak     s                   like(rcnsak)
     d rcnflr_nask     s                   like(rcnask)
     d rcnflr_nasb     s                   like(rcnasb)
     d rkunl1_kund     s                   like(rkkund)
      *
      * Definering av variable
      *
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d                 ds
     d w_nref                        20
     d  w_file                        3    overlay(w_nref:5)
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(rcnfir)
     d w_nask_gml      s                   like(rcnask)
     d w_nasb_gml      s                   like(rcnasb)
     d w_vise          s              1    inz('A')
     d w_kun           s              6
     d w_ref           s              9
     d i               s              3  0
     d w_lindorf       s            256
      *
      * Definering av parameter mot Lindorf
      * Søke på Kundenr
     d*p_lindorf1      s            256    inz('https://access.lindorf+
     d*                                        f.com/Assignment/+
     d*                                              Details/xxxxxxxx')
     d p_lindorf1      s            256    inz('https://collection.lindorf+
     d                                         f.no/Assignment/Details/xxx+
     d                                         xxxxx')
      * Søke på Fødselsdato 25.11.2019 ?? 84
     d p_lindorf2      s            256    inz('https://collection.lindorf+
     d                                          f.no/Search?submitted=+
     d                                          true&isCsvRequest=false&+
     d                                          SearchWord=XXXXXXXXXXX+
     d                                          &searchField=SSN')
      * Søke på Organisasjonsnr            84
     d p_lindorf3      s            256    inz('https://collection.lindorf+
     d                                          f.no/Search?submitted=+
     d                                          true&isCsvRequest=false&+
     d                                          SearchWord=XXXXXXXXX+
     d                                          &searchField=CompanyID')
      * Søke på Kreditors referansenr      84
     d p_lindorf4      s            256    inz('https://collection.lindorf+
     d                                          f.no/Search?submitted=+
     d                                          true&isCsvRequest=false&+
     d                                          SearchWord=XXXXXXXXX+
     d                                          &searchField=Creditors+
     d                                          Reference')
      *
7.xx d c_sfil          s              2  0 inz(15)
      *
      /eject
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_srrn - Relativt recordnummer i subfile
      * w_spge - Sidestørrelse i subfile (page)
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * w_virn - Vis den side som inneholder denne record.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * S1WIN  - S1 Standardvindu som inneholder definisjon av
      *             størrelse og utseende.
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
      *
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
      *
     c                   when      *inkg
     c                   if        w_vise = 'A'
     c                   eval      w_vise = ' '
     c                   else
     c                   eval      w_vise = 'A'
     c                   endif
     c                   exsr      forny
     c                   goto      b2tagb
      *
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
      *
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
      *
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2valg = 0
     c                   if        b2nask <> 0 or
     c                             b2nsak <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
     c                   endif
      *
      * Behandling av subfile control
      *
     c                   if        b2valg <> 0
     c                   exsr      control
     c                   if        b_feil = *on
     c                   goto      b2tagb
     c                   else
     c                   goto      b2taga
     c                   endif
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   select
     c                   when      w_seqe = 'B'
     c     rcnfl2_key    setll     rcnfl2
     c                   other
     c     rcnfl1_key    setll     rcnfl1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på bilag
      *
     c                   if        b2nask <> 0
     c                   eval      w_seqe = ' '
     c                   eval      rcnfl1_nask = b2nask
     c     rcnfl1_key    setll     rcnfl1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på saksnummer
      *
     c                   if        b2nsak <> *blank
     c                   eval      w_seqe = 'B'
     c                   eval      rcnfl2_nsak = b2nsak
     c     rcnfl2_key    setll     rcnfl2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2nask = 0
     c                   eval      b2nsak = *blank
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behanding av subfile-control
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     control       begsr
      *
     c                   eval      b_forn = *off
     c                   eval      b_feil = *off
      *
      * Validering av nøkkel
      *
     c                   eval      rcnflr_nask = b2nask
     c     rcnflr_key    chain     rcnflr                             90
     c                   if        *in90
     c                   eval      b_feil = *on
     c                   eval      *in32  = *on
     c                   goto      end_ctl
     c                   endif
      *
      * Endre
      *
     c                   if        b2valg = 2
     c                   eval      c2nask = b2nask
     c                   exsr      xc2bld
     c                   goto      end_ctl
     c                   endif
      *
      * Vise
      *
     c                   if        b2valg = 5
     c                   eval      c2nask = b2nask
     c                   exsr      xc2bld
     c                   goto      end_ctl
     c                   endif
      *
     c     end_ctl       tag
      *
     c                   eval      b2valg = 0
     c                   eval      b2nask = 0
     c                   eval      b2nsak = *blank
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
      * Beholder eksisterende posisjonering i subfilen
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      w_virn = w_srrn
      *
      * Endre post
      *
     c                   if        b1valg = 2
     c                   eval      c2nask = b1nask
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vise post
      *
     c                   if        b1valg = 5
     c                   eval      c2nask = b1nask
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slå opp i Lindorf
      *
     c                   if        b1valg = 7
     c                   eval      w_lindorf = p_lindorf1
     c                   eval      i = %scan('xxxxxxxx':w_lindorf)
     c                   eval      %subst(w_lindorf:i:8) = b1nsak
s.ss c**                 eval      %subst(w_lindorf:i:8) = '29709869'
     c                   call      'AP628C'
     c                   parm                    w_lindorf
     c*                  move      '000000000'   w_ref
     c*                  move      b1nasb        w_ref
     c*                  eval      %subst(p_lindorf2:84:9) = w_ref
     c*                  call      'AP628C'
     c*                  parm                    p_lindorf1
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      rcnflr_nask = b1nask
     c                   eval      rcnflr_nasb = b1nasb
     c     rcnflr_key    chain     rcnflr                             90
      *
     c                   if        *in90 = *off
     c                   eval      c2nyen = a_meld(2)
     c                   eval      c2nsak = rcnsak
     c                   eval      c2nasb = rcnasb
     c                   eval      c2nsta = rcnsta
     c     *dmy          move      rcbdat        c2bdat
     c                   eval      c2ncnk = rcncnk
     c                   eval      c2nref = rcnref
     c                   eval      c2nsto = rcnsto
     c                   eval      c2nomk = rcnomk
     c                   eval      c2nrnt = rcnrnt
     c                   eval      c2nprv = rcnprv
     c                   eval      c2ndat = rcndat
     c                   eval      c2navr = rcnavr
     c                   eval      c2novÅ = rcnovÅ
     c                   eval      c2nled = rcnled
     c                   eval      c2ninb = rcninb
     c                   eval      c2ninr = rcninr
      *
     c                   eval      c2navn =  *blank
     c                   eval      rkunl1_kund = rcnask
     c     rkunl1_key    chain     rkunl1                             91
     c                   if        *in91 = *off
     c                   movel     rknavn        c2navn
     c                   endif
      *
     c                   else
     c                   eval      c2nyen = a_meld(1)
     c                   eval      c2nsak = *blank
     c                   endif
      *
     c                   if        b1valg = 5 or b2valg = 5
     c                   eval      *in30 = *on
     c                   eval      c2nyen = a_meld(3)
     c                   endif
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Oppdatering av bilag
      *
     c                   clear                   rcnflrr
      *
     c                   eval      rcnflr_nask = b1nask
     c                   eval      rcnflr_nasb = b1nasb
     c     rcnflr_key    chain     rcnflrr                            90
      *
     c                   eval      rcnsta = c2nsta
     c                   eval      rcnsak = c2nsak
     c                   eval      rcninb = c2ninb
     c                   eval      rcndat = c2ndat
7.01 c                   eval      rcnovÅ = c2novÅ
7.01 c                   eval      rcnavr = c2navr
      *
     c                   if        *in90 = *off
     c                   update    rcnflrr
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1nsta = rcnsta
     c                   eval      b1nsak = rcnsak
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      rcnfl2                                 15
     c                   other
     c                   read      rcnfl1                                 15
     c                   endsl
      *
     c                   if        *in15
     c                   goto      end_crt
     c                   endif
      *
     c                   if        rcnfir <> w_firm
     c                   eval      *in15 = *on
     c                   goto      end_crt
     c                   endif
      *
      * Vis bare trans fra riktig filgruppe
      *
     c                   eval      w_nref = rcnref
     c                   if        w_file <> l_file
     c                   goto      neste
     c                   endif
7.03  *
7.03  * Vis bare trans på aktivt  inkasssofirma
7.03  *
7.03 c                   if        rcncnk <> rc0cnk
7.03 c                   goto      neste
7.03 c                   endif
      *
      * Åpne saker/Alle saker
      *
     c                   if        w_vise = ' ' and
     c                             rcnsta = 'A'
     c                   goto      neste
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1nask = rcnask
     c                   eval      b1nsta = rcnsta
     c                   eval      b1nsak = rcnsak
     c                   eval      b1nasb = rcnasb
     c     *dmy          move      rcbdat        b1bdat
     c                   eval      b1nsto = rcnsto
     c                   eval      b1navn =  *blank
     c                   eval      rkunl1_kund = rcnask
     c     rkunl1_key    chain     rkunl1                             91
     c                   if        *in91 = *off
     c                   movel     rknavn        b1navn
     c                   endif
      *
     c                   write     b1sfl
      *
     c     neste         tag
      *
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
     c                   eval      *in22 = *off
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på kunde
      *
     c     rcnfl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rcnfl1_nask
      *
      * Key for posisjonering på saksnummere
      *
     c     rcnfl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rcnfl2_nsak
      *
      * Key for oppdatering av bilag
      *
     c     rcnflr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rcnflr_nask
     c                   kfld                    rcnflr_nasb
      *
      * Key for les av kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
7.03  *
7.03 c     rca0l1_key    klist
7.03 c                   kfld                    w_firm
      *
      * Henter firmanummer fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
7.03  *
7.03  *  Finner faste opplysninger
7.03  *
7.03 c     rca0l1_key    chain     rca0l1
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      rcnfl1_nask = 0
     c     rcnfl1_key    setll     rcnfl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
**
  Ny post                  001
  Endring                  002
  Vise                     003
