     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LL700R
      *  Beskrivelse . . : Innkjøpsplanlegging
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign
      * --------- ------  --------------------------------------------------
5.50  *           061206  Hvis stat.og lag.forskjellige - omberegn BØ
      *                   til lagerenhet.
      *  BØ       150307  Tar aldri best.pkt,min,mm. fra varereg. kun lager
5.50  *  BØ       290507  Korrigert utregning ved omregning lager/stat.enh.
5.70  * PRGR  021208 bof  Utvidet med prisgruppe
6.11  * 6.10  130709 bof  Utivdet eannr til 14 og brukt std. oppslag
6.12  * 6.10  261010 bof  Når lager er med i parameter, skal dette brukes
6.20  * 6.20  120112 bsa  Utvidet VP750R med kampanje
7.01  * 7.00  190220 bkv  Legger inn (o) i DDS. Tag til newlook om output
7.01  *                   (o) er filter i SID fil i Newlook
7.02  * K000  240118 bsa  Legger ut informasjon om erstatningsvare.
7.03  * K000  301018 bsa  Endret ledeteksten på Bestillingspunkt og mengde.
8.01  *PRG2   150622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     fsstalb    if   e           k disk    rename(sstapfr:sstalbr)
     fsistlb    if   e           k disk    rename(sistpfr:sistlbr)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
7.02 fvverl4    if   e           k disk    rename(vverpfr:vverl4r)
7.02 fvve2l1    if   e           k disk    rename(vve2pfr:vve2l1r)
      *
     fll700d    cf   e             workstn
      *
      * Definering av arrays
      *
     d a_mtxt          s              4    dim(12) ctdata perrcd(1)
7.03 d a_txt           s             18    dim(4) ctdata perrcd(1)
     d a_htxt          s              8    dim(13)
     d a_psal          s              8  1 dim(13)
     d a_hsal          s              8  1 dim(13)
     d a_pkjo          s              8  1 dim(13)
     d a_hkjo          s              8  1 dim(13)
      *
      *  Local Data Area
      *
     d                uds
     d l_firm                944    946  0
     d l_wsid                901    910
     d l_user                        10
7.03 d l_filg                931    933
     d l_fnav                951    980
     d l_lage               1001   1002  0
      *
      * Definering av variable i parametre
      *
     d p_vare          s                   like(vvvare)
     d p_lage          s                   like(vllage)
      *
      * Definering av variable i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vlagl1_lage     s                   like(vllage)
     d vlagl1_vare     s                   like(vlvare)
     d sstalb_paar     s                   like(sfpaar)
     d sstalb_vare     s                   like(sfvare)
     d sistlb_paar     s                   like(sipaar)
     d sistlb_vare     s                   like(sivare)
     d vvenl1_vare     s                   like(vevare)
7.02 d vverl4_envr     s                   like(vvenvr)
7.02 d vve2l1_envr     s                   like(v2envr)
7.02 d vve2l1_egvr     s                   like(v2egvr)
      *
      *   Definering av øvrige variable
      *
     d w_firm          s                   like(vvfirm)
     d w_adrs          s             30
     d w_ponr          s              4  0
     d w_pste          s             30
     d w_avde          s              4  0
     d w_lety          s              1  0
     d w_kpny          s              9  2
     d w_ipny          s              9  2
     d w_date          s               d   datfmt(*iso)
     d w_dmnd          s              2  0
     d w_smnd          s              2  0
     d w_aaar          s              4
     d w_daar          s              4  0
     d w_faar          s              4  0
     d w_mtel          s              2  0
     d w_maks          s             11  3
     d w_mini          s             11  3
     d w_sikl          s             11  3
     d w_bpun          s             11  3
     d w_bmen          s             11  3
     d w_ltid          s              3  0
     d w_omrl          s                   like(veomre)
     d w_omrs          s                   like(veomre)
8.01 d w_prgr          s              2
6.11 d w_eann          s             14  0
6.11 d w_eava          s                   like(vvvare)
6.11 d w_eaen          s                   like(vvenhe)
6.11 d w_east          s              1
     d w_leng          s              2  0
6.11 d w_eanx          s             14
6.20 d w_kamp          s              5
      *
     d b_feil          s              1    inz(*off)
     d b_lage          s              1    inz(*off)
     d b_scan          s              1    inz(*off)
      *
7.03   dcl-s co402_verdi1  char(1);
7.03   dcl-s co402_verdi2  char(2048);
7.03   DCL-PR CO402R EXTPGM('CO402R');
7.03     p_filgrp            char(3)     const;
7.03     p_firm              packed(3:0) const;
7.03     p_lager             packed(2:0) const;
7.03     p_nokkel            char(50)    const;
7.03     p_verdi1            char(1);
7.03     p_verdi2            char(2048);
7.03   END-PR;
7.03 d u_bpkt          s              1    inz(*off)
      *
     d c_null          c                   '0000000000000'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekk om parametre inn, eller legg ut bilde for inntasting
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   exsr      lag_hdng
     c                   if        p_lage <> 0
     c                   eval      a1lage = p_lage
     c                   else
     c                   eval      a1lage = l_lage
     c                   endif
     c                   if        p_vare <> *blank
     c                   movel     p_vare        a1vare
     c                   goto      les_a1bld
     c                   endif
      *
     c                   eval      a1vare = *blank
      *
     c     skr_a1bld     tag
     c                   exfmt     a1bld
      *
     c     les_a1bld     tag
      *
      * F3/F12=Avslutt
      *
     c                   if        *inkc = *on or
     c                             *inkl = *on
     c                   goto      end_pgm
     c                   endif
      *
      * F4=Spørring
      *
     c                   if        *inkd = *on and
     c                             w_arcd = 'A1BLD'
     c                   exsr      blank_skjerm
     c                   exsr      sp_input
     c                   if        b_feil = *on
     c                   goto      skr_a1bld
     c                   endif
     c                   endif
7.02  *
7.02  * F6=Innkjøpsplanlegging gammel vare
7.02  *
7.02 c                   if        *inkf = *on
7.02 c                   exsr      innkj_erst
7.02 c                   goto      skr_a1bld
7.02 c                   endif
      *
      * F13=Scanning
      *
     c                   if        *inkm = *on or
     c                             b_scan = *on
     c                   eval      b_scan = *on
     c                   exsr      scanning
     c                   if        b_scan = *off
     c                   goto      skr_a1bld
     c                   endif
     c                   endif
      *
     c                   exsr      blank_skjerm
     c                   exsr      finn_vare
     c                   exsr      finn_lager
     c                   exsr      finn_salg
     c                   exsr      finn_kjop
7.02  *
7.02  * Sjekk om denne erstatter annen vare
7.02  *
7.02 c                   eval      vverl4_envr = vvarl1_vare
7.02 c     vverl4_key    chain     vverl4
7.02 c                   if        %found
7.02  * Er det erstatningsvare for dette lageret ?
7.02 c                   eval      vve2l1_envr = vvenvr
7.02 c                   eval      vve2l1_egvr = vvegvr
7.02 c     vve2l1_key    chain     vve2l1
7.02 c                   if        %found
7.02 c                   if        v2elag = 0 or
7.02 c                             v2elag = p_lage
7.02 c                   exsr      erst_vare
7.02 c                   exsr      erst_lager
7.02 c                   endif
7.02 c                   else
7.02 c                   exsr      erst_vare
7.02 c                   exsr      erst_lager
7.02 c                   endif
7.02 c                   endif
      *
     c                   exfmt     a1bld
      *
     c                   goto      les_a1bld
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     end_pgm       tag
     c                   eval      *inlr = *on
     c                   return
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finne vareinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_vare     begsr
      *
      * Legger data ut i skjermbilde
      *
     c                   movel     a1vare        vvarl1_vare
     c     vvarl1_key    chain     vvarl1r
     c                   if        not %found
     c                   eval      *in31 = *on
     c                   goto      vare_end
     c                   endif
      *
     c                   eval      a1tek1 = vvtek1
     c                   eval      a1enhl = vvenhl
     c                   if        vvenhl <> vvenhs
     c                   exsr      hent_omr
     c                   endif
     c                   exsr      hent_inpri
      *
     c     vare_end      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finne lagerinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_lager    begsr
      *
      * Sjekk om gyldig lager
      *
     c                   exsr      sjekk_lager
     c                   if        b_feil = *on
     c                   eval      *in32 = *on
     c                   goto      lager_end
     c                   endif
      *
      * Henter verdier fra lager
      *
     c                   eval      vlagl1_vare = vvarl1_vare
     c                   eval      vlagl1_lage = a1lage
     c     vlagl1_key    chain     vlagl1r
     c                   if        not %found
     c                   goto      lager_end
     c                   endif
      *
      * Verdier fra lager eller vare?
      * fom v. 550 tas verdier kun fra lager
      *
     c                   eval      w_mini = vlmini
      *
     c                   eval      w_maks = vlmaks
      *
     c                   eval      w_bpun = vlbpun
      *
     c                   eval      w_bmen = vlbmen
      *
     c                   eval      w_sikl = vlsikl
      *
     c                   eval      w_ltid = vlltid
      *
     c                   eval      a1behl = vlbehl
     c                   eval      a1mini = w_mini
     c                   eval      a1maks = w_maks
     c                   eval      a1pakk = vlpakk
     c                   eval      a1sikl = w_sikl
     c                   eval      a1oord = vloord
     c                   eval      a1bbes = vlbbes
     c                   eval      a1bpun = w_bpun
     c                   eval      a1bmen = w_bmen
     c                   eval      a1ltid = w_ltid
     c                   eval      a1disp = vlbehl + vlbbes -
     c                                      vlpakk - vloord
7.03  *
7.03 c                   if        u_bpkt = *off
7.03 c                   eval      a1bptx = a_txt(1)
7.03 c                   eval      a1bmtx = a_txt(2)
7.03 c                   else
7.03 c                   eval      a1bptx = a_txt(3)
7.03 c                   eval      a1bmtx = a_txt(4)
7.03 c                   endif
      *
      * Sjekk om negativ beholdning/disp.beholdning
      *
     c                   if        a1behl < 0
     c                   eval      *in41 = *on
     c                   endif
     c                   if        a1disp < 0
     c                   eval      *in42 = *on
     c                   endif
      *
     c     lager_end     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finne statistikkinformasjon salg
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_salg     begsr
      *
     c                   eval      a_psal = 0
     c                   extrct    w_date:*m     w_dmnd
     c                   eval      sstalb_vare = vvarl1_vare
      *
      * Les statistikkposter fra forrige år og akkumuler i tabell
      *
     c                   eval      sstalb_paar = w_faar
     c     sstalb_key    setll     sstalb
     c     sstalb_key    reade     sstalb
      *
     c                   dow       not %eof
      *
      * Gjør om til lagerenhet
      *
     c                   if        vvenhs <> vvenhl
     c                   if        w_omrl <> 0
     c                   eval      sfanta = sfanta * w_omrs / w_omrl
     c                   endif
     c                   endif
     c                   if        sflage = a1lage and
     c                             sfpmnd >= w_dmnd
     c                   eval(h)   a_psal(sfpmnd) = a_psal(sfpmnd) + sfanta
     c                   endif
      *
     c     sstalb_key    reade     sstalb
     c                   enddo
      *
      * Les statistikkposter fra dette år og akkumuler i tabell
      *
     c                   eval      sstalb_paar = w_daar
     c     sstalb_key    setll     sstalb
     c     sstalb_key    reade     sstalb
      *
     c                   dow       not %eof and
     c                             sfpmnd <= w_dmnd
      *
      * Gjør om til lagerenhet
      *
     c                   if        vvenhs <> vvenhl
     c                   if        w_omrl <> 0
     c                   eval      sfanta = sfanta * w_omrs / w_omrl
     c                   endif
     c                   endif
      *
     c                   if        sflage = a1lage
     c                   if        sfpmnd = w_dmnd
     c                   eval(h)   a_psal(13) = a_psal(13) + sfanta
     c                   else
     c                   eval(h)   a_psal(sfpmnd) = a_psal(sfpmnd) + sfanta
     c                   endif
     c                   endif
      *
     c     sstalb_key    reade     sstalb
     c                   enddo
      *
      * Legger salgstall ut i riktig skjermfelt
      *
     c                   eval      w_mtel = 1
      *
     c                   dow       w_mtel < 13
     c                   if        w_dmnd = 13
     c                   eval      w_dmnd = 1
     c                   endif
     c                   eval      a_hsal(w_mtel) = a_psal(w_dmnd)
     c                   eval      w_mtel = w_mtel + 1
     c                   eval      w_dmnd = w_dmnd + 1
     c                   enddo
     c                   eval      a_hsal(13) = a_psal(13)
      *
     c                   eval      a1sa01 = a_hsal(01)
     c                   eval      a1sa02 = a_hsal(02)
     c                   eval      a1sa03 = a_hsal(03)
     c                   eval      a1sa04 = a_hsal(04)
     c                   eval      a1sa05 = a_hsal(05)
     c                   eval      a1sa06 = a_hsal(06)
     c                   eval      a1sa07 = a_hsal(07)
     c                   eval      a1sa08 = a_hsal(08)
     c                   eval      a1sa09 = a_hsal(09)
     c                   eval      a1sa10 = a_hsal(10)
     c                   eval      a1sa11 = a_hsal(11)
     c                   eval      a1sa12 = a_hsal(12)
     c                   eval      a1sa13 = a_hsal(13)
      *
     c     salg_end      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finne statistikkinformasjon kjøp
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_kjop     begsr
      *
     c                   eval      a_pkjo = 0
     c                   extrct    w_date:*m     w_dmnd
     c                   eval      sistlb_vare = vvarl1_vare
      *
      * Les statistikkposter fra forrige år og akkumuler i tabell
      *
     c                   eval      sistlb_paar = w_faar
     c     sistlb_key    setll     sistlb
     c     sistlb_key    reade     sistlb
      *
     c                   dow       not %eof
      *
     c                   if        silage = a1lage and
     c                             sipmnd >= w_dmnd
     c                   eval(h)   a_pkjo(sipmnd) = a_pkjo(sipmnd) + sianta
     c                   endif
      *
     c     sistlb_key    reade     sistlb
     c                   enddo
      *
      * Les statistikkposter fra dette år og akkumuler i tabell
      *
     c                   eval      sistlb_paar = w_daar
     c     sistlb_key    setll     sistlb
     c     sistlb_key    reade     sistlb
      *
     c                   dow       not %eof and
     c                             sipmnd <= w_dmnd
      *
     c                   if        silage = a1lage
     c                   if        sipmnd = w_dmnd
     c                   eval(h)   a_pkjo(13) = a_pkjo(13) + sianta
     c                   else
     c                   eval(h)   a_pkjo(sipmnd) = a_pkjo(sipmnd) + sianta
     c                   endif
     c                   endif
      *
     c     sistlb_key    reade     sistlb
     c                   enddo
      *
      * Legger kjøpstall ut i riktig skjermfelt
      *
     c                   eval      w_mtel = 1
      *
     c                   dow       w_mtel < 13
     c                   if        w_dmnd = 13
     c                   eval      w_dmnd = 1
     c                   endif
     c                   eval      a_hkjo(w_mtel) = a_pkjo(w_dmnd)
     c                   eval      w_mtel = w_mtel + 1
     c                   eval      w_dmnd = w_dmnd + 1
     c                   enddo
     c                   eval      a_hkjo(13) = a_pkjo(13)
      *
     c                   eval      a1kj01 = a_hkjo(01)
     c                   eval      a1kj02 = a_hkjo(02)
     c                   eval      a1kj03 = a_hkjo(03)
     c                   eval      a1kj04 = a_hkjo(04)
     c                   eval      a1kj05 = a_hkjo(05)
     c                   eval      a1kj06 = a_hkjo(06)
     c                   eval      a1kj07 = a_hkjo(07)
     c                   eval      a1kj08 = a_hkjo(08)
     c                   eval      a1kj09 = a_hkjo(09)
     c                   eval      a1kj10 = a_hkjo(10)
     c                   eval      a1kj11 = a_hkjo(11)
     c                   eval      a1kj12 = a_hkjo(12)
     c                   eval      a1kj13 = a_hkjo(13)
      *
     c     kjop_end      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å blanke ut skjermbilde
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     blank_skjerm  begsr
      *
      * Blanker felter i skjermbilde
      *
     c                   eval      *in41 = *off
     c                   eval      *in42 = *off
     c                   eval      a1tek1 = *blank
     c                   eval      a1enhl = *blank
     c                   eval      a1enhi = *blank
     c                   eval      a1behl = 0
     c                   eval      a1mini = 0
     c                   eval      a1maks = 0
     c                   eval      a1pakk = 0
     c                   eval      a1sikl = 0
     c                   eval      a1oord = 0
     c                   eval      a1bbes = 0
     c                   eval      a1bpun = 0
     c                   eval      a1bmen = 0
     c                   eval      a1disp = 0
     c                   eval      a1ltid = 0
     c                   eval      a1sa01 = 0
     c                   eval      a1sa02 = 0
     c                   eval      a1sa03 = 0
     c                   eval      a1sa04 = 0
     c                   eval      a1sa05 = 0
     c                   eval      a1sa06 = 0
     c                   eval      a1sa07 = 0
     c                   eval      a1sa08 = 0
     c                   eval      a1sa09 = 0
     c                   eval      a1sa10 = 0
     c                   eval      a1sa11 = 0
     c                   eval      a1sa12 = 0
7.02  * Felter fra erstatningsvare
7.02 c                   eval      a1evar = *blanks
7.02 c                   eval      a1etek = *blanks
7.02 c                   eval      a1ebeh = 0
7.02 c                   eval      a1epak = 0
7.02 c                   eval      a1eord = 0
7.02 c                   eval      a1ebes = 0
7.02 c                   eval      a1edis = 0
      *
     c                   endsr
      *
7.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.02  *  Subrutine for å finne erstatnings vareinformasjon
7.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.02  *
7.02 c     erst_vare     begsr
7.02  *
7.02  * Legger data ut i skjermbilde
7.02  *
7.02 c                   eval      vvarl1_vare = vvegvr
7.02 c     vvarl1_key    chain     vvarl1r
7.02 c                   if        not %found
7.02 c                   goto      erst_end
7.02 c                   endif
7.02  *
7.02 c                   eval      a1evar = vvarl1_vare
7.02 c                   eval      a1etek = vvtek1
7.02  *
7.02 c     erst_end      endsr
7.02  *
7.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.02  *  Subrutine for å finne lagerinformasjon for erstatningsvare
7.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.02  *
7.02 c     erst_lager    begsr
7.02  *
7.02  * Henter verdier fra lager
7.02  *
7.02 c                   eval      vlagl1_vare = vvarl1_vare
7.02 c                   eval      vlagl1_lage = a1lage
7.02 c     vlagl1_key    chain     vlagl1r
7.02 c                   if        not %found
7.02 c                   goto      erst_lgr_end
7.02 c                   endif
7.02  *
7.02  * Verdier fra lager
7.02  *
7.02 c                   eval      a1ebeh = vlbehl
7.02 c                   eval      a1epak = vlpakk
7.02 c                   eval      a1eord = vloord
7.02 c                   eval      a1ebes = vlbbes
7.02 c                   eval      a1edis = vlbehl + vlbbes -
7.02 c                                      vlpakk - vloord
7.02  *
7.02  * Sjekk om negativ beholdning/disp.beholdning
7.02  *
7.02 c*                  if        a1ebeh < 0
7.02 c*                  eval      *in45 = *on
7.02 c*                  endif
7.02 c*                  if        a1edis < 0
7.02 c*                  eval      *in46 = *on
7.02 c*                  endif
7.02  *
7.02 c     erst_lgr_end  endsr
7.02  *
7.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.02  *  Subrutine for overgang til innkjøpsplanlegging gammel vare
7.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.02  *
7.02 c     innkj_erst    begsr
7.02  *
7.02  * Overgnag til innkjøpsplanlegging gammel vare
7.02  *
7.02 c                   call      'LL702R'
7.02 c                   parm                    a1evar
7.02 c                   parm                    a1lage
7.02  *
7.02 c                   endsr
7.02  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Henter innkjøpsenhet og priser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_inpri    begsr
      *
     c                   eval      w_lety = 0
6.20 c                   eval      w_kamp = *blank
      *
     c                   call      'VP750R'
     c                   parm                    w_firm
     c                   parm                    vvarl1_vare
5.70 c                   parm                    w_prgr
     c                   parm                    w_lety
     c                   parm                    w_date
     c                   parm                    a1enhi
     c                   parm                    w_kpny
     c                   parm                    w_ipny
6.20 c                   parm                    w_kamp
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å sjekke gyldig lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_lager   begsr
      *
     c                   call      'FÅ720R'
     c                   parm                    w_firm
     c                   parm                    a1lage
     c                   parm                    a1ltek
     c                   parm                    w_adrs
     c                   parm                    w_ponr
     c                   parm                    w_pste
     c                   parm                    w_avde
     c                   parm                    b_feil
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av scanning
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     scanning      begsr
      *
     c                   eval      e1eann = *blank
      *
     c     e1taga        tag
      *
     c                   exfmt     e1win
      *
     c                   if        *inkc or *inkl
     c                   eval      b_scan = *off
     c                   goto      end_e1win
     c                   endif
      *
      * EAN-nummer må oppgis
      *
     c                   if        e1eann = *blank
     c                   eval      *in31 = *on
     c                   goto      e1taga
     c                   endif
      *
      * Sjekk om vare finnes for EAN-nummer
      *
     c                   eval      w_leng = %len(%trim(e1eann))
6.11 c                   eval      w_eanx = %subst(c_null:1:14-w_leng) +
     c                                      e1eann
      *
6.11 c                   move      w_eanx        w_eann
6.11 c                   call      'VE710R  '
6.11 c                   parm                    w_firm
6.11 c                   parm                    w_eann
6.11 c                   parm                    w_eava
6.11 c                   parm                    w_eaen
6.11 c                   parm                    w_east
6.11 c                   if        w_east = *blank
     c                   eval      *in32 = *on
     c                   goto      e1taga
     c                   endif
      *
     c                   movel     w_eava        a1vare
      *
     c                   eval      e1eann = *blank
      *
     c     end_e1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for spørring på input A1BLD
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sp_input      begsr
      *
      *  Spørring på varenummer
      *
     c                   if        w_afld = 'A1VARE'
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    a1vare
     c                   parm                    a1tek1
      *
     c                   exsr      sjekk_lager
     c                   goto      sp_end
     c                   endif
      *
      *  Spørring lager
      *
     c                   if        w_afld = 'A1LAGE'
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    a1lage
     c                   parm                    a1ltek
     c                   goto      sp_end
     c                   endif
      *
     c     sp_end        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finne redigere månedsheading
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lag_hdng      begsr
      *
      * Finn start måned og år
      *
     c                   extrct    w_date:*m     w_dmnd
     c                   extrct    w_date:*y     w_daar
     c                   eval      w_faar = w_daar - 1
     c                   move      w_faar        w_aaar
      *
      * Legger ut riktig måned i tabell
      *
     c                   eval      w_mtel = 1
     c                   dow       w_mtel < 14
     c                   if        w_dmnd >12
     c                   eval      w_dmnd = 1
     c                   move      w_daar        w_aaar
     c                   endif
     c                   eval      a_htxt(w_mtel) = %trimr(a_mtxt(w_dmnd) +
     c                                                     w_aaar)
     c                   eval      w_dmnd = w_dmnd + 1
     c                   eval      w_mtel = w_mtel + 1
     c                   enddo
      *
      * Flytter overskrift ut i skjermfelter
      *
     c                   eval      a1he01 = a_htxt(1)
     c                   eval      a1he02 = a_htxt(2)
     c                   eval      a1he03 = a_htxt(3)
     c                   eval      a1he04 = a_htxt(4)
     c                   eval      a1he05 = a_htxt(5)
     c                   eval      a1he06 = a_htxt(6)
     c                   eval      a1he07 = a_htxt(7)
     c                   eval      a1he08 = a_htxt(8)
     c                   eval      a1he09 = a_htxt(9)
     c                   eval      a1he10 = a_htxt(10)
     c                   eval      a1he11 = a_htxt(11)
     c                   eval      a1he12 = a_htxt(12)
     c                   eval      a1he13 = a_htxt(13)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finne omregningsfaktor
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_omr      begsr
      *
     c                   eval      vvenl1_vare = vvvare
     c     vvenl1_key    setll     vvenl1
     c     vvenl1_key    reade     vvenl1                                 91
     c                   dow       *in91 = *off
     c                   if        vvenhl = veenhe
     c                   eval      w_omrl = veomre
     c                   endif
     c                   if        vvenhs = veenhe
     c                   eval      w_omrs = veomre
     c                   endif
     c     vvenl1_key    reade     vvenl1                                 91
     c                   enddo
      *
     c                   if        w_omrs = 0
     c                   eval      w_omrs = 1
     c                   endif
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for les av VARE
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *  Key for les av LAGER
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl1_vare
     c                   kfld                    vlagl1_lage
      *
      * Key for les av salgsstatistikk
      *
     c     sstalb_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sstalb_paar
     c                   kfld                    sstalb_vare
      *
      * Key for les av innkjøpsstatistikk
      *
     c     sistlb_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sistlb_paar
     c                   kfld                    sistlb_vare
      *
      * Key for les av vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
7.02  *
7.02  * Key for les av erstaningsvarer
7.02  *
7.02 c     vverl4_key    klist
7.02 c                   kfld                    w_firm
7.02 c                   kfld                    vverl4_envr
7.02  *
7.02  * Key for les av erstaningsvarer
7.02  *
7.02 c     vve2l1_key    klist
7.02 c                   kfld                    w_firm
7.02 c                   kfld                    vve2l1_envr
7.02 c                   kfld                    vve2l1_egvr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    p_vare
     c                   parm                    p_lage
      *
      *  Legge inn firmanummer i nøklene
      *
     c                   eval      w_firm = l_firm
     c                   time                    w_date
5.70  *
5.70  * Hent prisgruppe
5.70  *
5.70 c                   eval      w_avde = 0
6.11 c                   if        p_lage = 0
6.11 c                   eval      p_lage = l_lage
6.11 c                   endif
5.70 c                   call      'VL712R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    p_lage
5.70 c                   parm                    w_avde
5.70 c                   parm                    w_prgr
7.03  *
7.03  * Endring 7.01 Styre ledetekster til bestillingspkt og mengde
7.03  *
7.03   CallP CO402R(l_filg:w_firm:0:'LEDETEKSTER_BESTILLINGSPKT':
7.03                    co402_verdi1:co402_verdi2);
7.03   if co402_verdi1 = '1';
7.03     u_bpkt = *on;
7.03   endif;
      *
     c                   endsr
**
Jan-
Feb-
Mar-
Apr-
Mai-
Jun-
Jul-
Aug-
Sep-
Okt-
Nov-
Des-
** Ledetekster
Bestillingspunkt :
Bestillingsmengde:
Hyllemin         :
Hyllemax         :
