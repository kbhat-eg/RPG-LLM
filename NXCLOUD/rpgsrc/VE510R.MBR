     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VE510R
      * Beskrivelse . . : Vare-enhet, vise for vare
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       111199 HKO  Nytt program
      *       020805 HKO  Fjernet linjenr og erstattet dette med
      *                   individuell rekkefølge for salgsenhet,
      *                   innkjøpsenhet og prisbokenhet
      *       050805 HKO  Viser lengde, bredde, høyde, volum og vekt
6.10  *       191009 bof  Lagt på valg for å vise enhet pr. prisgiver
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     fvvenlr    if   e           k disk    rename(vvenpfr:vvenlrr)
      *
     fve510d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vvfirm)
     d p_vare          s                   like(vvvare)
      *
      * Definering av Local data area
      *
     d                uds
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variable i nøkler
      *
     d vvenlr_enhe     s                   like(veenhe)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_firm          s                   like(vvfirm)
     d w_vare          s                   like(vvvare)
      *
      * Konstanter
      *
     d c_sfil          s              2  0 inz(10)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierering av skjermbildet
      *
     c     vvarl1_key    chain     vvarl1                             90
     c                   if        *in90 = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      b2vare = vvvare
     c                   eval      b2tek1 = vvtek1
     c                   eval      b2enhe = vvenhe
      *
      * Henter vare-enhet informasjon
      *
     c     vvenl1_key    setll     vvenl1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Behandler valg i subfilen
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av valg i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
6.10  * Vis pr. prisgiver
6.10  *
6.10 c                   if        b1valg = 7
6.10 c                   call      'VE515R  '
6.10 c                   parm                    w_firm
6.10 c                   parm                    w_vare
6.10 c                   eval      b1valg = 0
6.10 c                   update    b1sfl
6.10 c                   iter
6.10 c                   endif
      *
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å vise detaljer om enheten
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      c2enhe = b1enhe
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      vvenlr_enhe = c2enhe
     c     vvenlr_key    chain     vvenlr
     c                   if        not %found
     c                   leavesr
     c                   endif
      *
     c                   eval      c2omre = veomre
     c                   eval      c2leng = veleng
     c                   eval      c2bred = vebred
     c                   eval      c2hoyd = vehoyd
     c                   eval      c2volu = vevolu
     c                   eval      c2vekt = vevekt
     c                   eval      c2saen = vesaen
     c                   eval      c2inen = veinen
     c                   eval      c2pben = vepben
     c                   eval      c2eusr = veeusr
     c     *dmy          move      veodat        c2odat
     c     *dmy          move      veedat        c2edat
      *
     c                   exfmt     c2bld
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile med enhet-informasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
      * Fyller subfile med enheter
      *
     c                   dou       w_srrn = w_spge
      *
     c     vvenl1_key    reade     vvenl1
      *
     c                   if        %eof
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1enhe = veenhe
     c                   eval      b1omre = veomre
     c                   eval      b1saen = vesaen
     c                   eval      b1inen = veinen
     c                   eval      b1pben = vepben
     c                   eval      b1volu = vevolu
     c                   eval      b1vekt = vevekt
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
      *
      * Key for les på vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
      *
      * Key for oppslag på vare-enhet
      *
     c     vvenlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
     c                   kfld                    vvenlr_enhe
      *
      *
      * Henter firma og vare fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_vare = p_vare
      *
     c                   endsr
