     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLOGI
      * Program . . . . : MM501R
      * Beskrivelse . . : prognose - lister varer med ukeprognose
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       020904 bø   Nytt program
      *       310507 bø   Utvidet b.punkt, m.meng,mini til 3 desimaler
5.60  *       080808 bø   Benytte ABC-kode på lager også
6.3q  *       260515 bof  Datoformat sql
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = prutect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fmprul1    if   e           k disk    rename(mprupfr:mprul1r)
     fmprulu    uf   e           k disk    rename(mprupfr:mprulur)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
5.60 fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
      *
     fmm501d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Informution Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
     d h1kabc          ds
     d  d_kabc                        1
     d  d_kxyz                        1
      *
      * Definering av variabler i nøkler
      *
     d mprul1_vare     s                   like(mrvare)
     d mprulu_vare     s                   like(mrvare)
     d mprulu_lage     s                   like(mrlage)
     d vvarl1_vare     s                   like(vvvare)
5.60 d vlagl1_vare     s                   like(vlvare)
5.60 d vlagl1_lage     s                   like(vllage)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
     d w_lety          s              1  0
     d w_kopr          s              9  2
     d w_inpr          s              9  2
     d w_lver          s             11  2
     d w_dato          s               d   datfmt(*iso)
     d s_vare          s                   like(mrvare)
     d s_lage          s                   like(mrlage)
     d s_kabc          s                   like(mrkabc)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_open_sok      s              1    inz(*off)
     d b_abck          s              1    inz(*off)
     d b_blaf          s              1    inz(*off)
      *
     d w_firm          s                   like(mrfirm)
     d w_vare          s                   like(mrvare)
     d w_tek1          s                   like(vvtek1)
     d w_navn          s             30
     d w_dag           s              2  0
     d w_mnd           s              2  0
     d w_aar           s              4  0
     d w_uke           s              2  0
     d w_stat          s              1
     d w_jkod          s              2  0
     d w_jtxt          s             30
     d w_pdat          s               d   datfmt(*iso)
     d w_paar          s              4  0
     d w_puke          s              2  0
     d w_psts          s              1
     d w_abc           s              1
     d w_xyz           s              1
5.60 d w_kabc          s                   like(vvkabc)
5.60 d w_labc          s                   like(vvlabc)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.3q C/Exec SQL
6.3q C+  Set Option DatFmt=*ISO
6.3q C/End-Exec
      *
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inka
     c                   exsr      xh1win
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2tagb
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkh
     c                   call      'MM100R  '
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Søk - ved sql
      *
     c                   if        b2vare <> *blank or
     c                             b2lage <> 0      or
     c                             b2kabc <> *blank
     c                   exsr      sØk
     c                   goto      b2tagb
     c                   endif
      *
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      mprul1_vare = b1vare
     c     mprul1_key    setll     mprul1
     c                   eval      b_open_sok = *on
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for søking i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sØk           begsr
      *
     c                   eval      w_seqe = ' '
     c                   eval      b_open_sok = *on
      *
     c                   movel     b2vare        s_vare
     c                   eval      s_lage  = b2lage
     c                   eval      s_kabc  = b2kabc
      *
     c                   if        b2vare <> *blank
     c                   eval      w_seqe  = 'O'
     c                   goto      end_sok
     c                   endif
      *
     c                   if        b2lage <> 0
     c                   eval      w_seqe  = 'L'
     c                   goto      end_sok
     c                   endif
      *
     c                   if        b2kabc <> *blank
     c                   eval      w_seqe  = 'A'
     c                   goto      end_sok
     c                   endif
      *
     c     end_sok       tag
      *
      * Blanker subfile, og fyller opp i.h.t. søk
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker søkefelt
      *
     c                   eval      b2vare = *blank
     c                   eval      b2lage = 0
     c                   eval      b2kabc = *blank
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c*    posisjoner    begsr
      *
      * Vare
      *
     c*                  if        b2vare <> *blank
     c*                  eval      w_seqe = ' '
     c*                  eval      mprul1_vare = b1vare
     c*    mprul1_key    setll     mprul1
     c*                  endif
      *
     c*    end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c*                  eval      *in22 = *off
      *
     c*                  exsr      clr_subfile
     c*                  exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c*                  eval      b2vare = *blank
      *
     c*                  endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      *
      * Sletter prognose for dette varenummeret
      *
     c                   if        b1valg = 4
     c                   movel     b1vare        d1vare
     c                   eval      d1lage  =  b1lage
     c                   eval      d1tek1  =  b1text
     c                   exsr      xd1win
     c*                  exsr      forny
     c                   eval      b1valg = 0
     c                   iter
     c                   endif
      *
      *
      * Vise prognose pr. uke
      *
     c                   if        b1valg = 5
     c     *dmy          move      UDATE         w_dato
      *
     c                   eval      w_pdat = w_dato
     c                   call      'AX711R  '
     c                   parm                    w_pdat
     c                   parm                    w_paar
     c                   parm                    w_puke
     c                   parm                    w_psts
     c                   eval      w_uke = w_puke
     c                   eval      w_aar = w_paar
     c                   movel     b1vare        w_vare
     c                   call      'MM502R'
     c                   parm                    w_vare
     c                   parm                    b1lage
     c                   parm                    w_aar
     c                   parm                    w_uke
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   movel     d1vare        mprulu_vare
     c                   eval      mprulu_lage = d1lage
readec     mprulu_key    setll     mprulu
readec     mprulu_key    reade     mprulu
readec                   dow       not %eof
     c                   delete    mprulur
readec     mprulu_key    reade     mprulu
     c                   enddo
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * xh1win  Bilde for behandling av bla-alternativer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xh1win        begsr
      *
     c     h1taga        tag
      *
     c                   write     h1win
     c                   exfmt     h1win
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc
     c                   goto      end_h1win
     c                   endsl
      *
     c                   if        h1kabc <> *blank
     c                   eval      b_blaf = *on
     c                   else
     c                   eval      b_blaf = *off
     c                   endif
      *
     c     end_h1win     endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
      * Initierer SQL for søk
      *
      *
     c                   if        b_open_sok = *on
      *
     c                   select
     c                   when      w_seqe = 'O'
      * pr varenr.
      *
     c/exec sql
     c+                  declare sok_o cursor for
     c+                  select distinct
     c+                         mrfirm,
     c+                         mrvare,
     c+                         mrlage,
     c+                         mrkabc
     c+                  from   mprupf
     c+                  where  mrfirm = :w_firm and
     c+                         mrvare >= :s_vare
     c+                  order by mrfirm, mrvare
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_o
     c/end-exec
     c/exec sql
     c+                  open sok_o
     c/end-exec
     c                   when      w_seqe = 'L'
      *
      * pr lagernr.
      *
     c/exec sql
     c+                  declare sok_l cursor for
     c+                  select distinct
     c+                         mrfirm,
     c+                         mrvare,
     c+                         mrlage,
     c+                         mrkabc
     c+                  from   mprupf
     c+                  where  mrfirm = :w_firm and
     c+                         mrlage >= :s_lage
     c+                  order by mrfirm, mrlage
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_l
     c/end-exec
     c/exec sql
     c+                  open sok_l
     c/end-exec
      *
     c                   when      w_seqe = 'A'
      *
      * pr abckode
      *
     c/exec sql
     c+                  declare sok_a cursor for
     c+                  select distinct
     c+                         mrfirm,
     c+                         mrvare,
     c+                         mrlage,
     c+                         mrkabc
     c+                  from   mprupf
     c+                  where  mrfirm = :w_firm and
     c+                         mrkabc >= :s_kabc
     c+                  order by mrfirm, mrkabc
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_a
     c/end-exec
     c/exec sql
     c+                  open sok_a
     c/end-exec
      *
     c                   endsl
      *
     c                   endif
      *
      * Fyller opp subfile
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'O'
      *
     c/exec sql
     c+                  fetch next
     c+                  from  sok_o
     c+                  into  :mrfirm,
     c+                        :mrvare,
     c+                        :mrlage,
     c+                        :mrkabc
     c/end-exec
      *
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'L'
      *
     c/exec sql
     c+                  fetch next
     c+                  from  sok_l
     c+                  into  :mrfirm,
     c+                        :mrvare,
     c+                        :mrlage,
     c+                        :mrkabc
     c/end-exec
      *
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'A'
      *
     c/exec sql
     c+                  fetch next
     c+                  from  sok_a
     c+                  into  :mrfirm,
     c+                        :mrvare,
     c+                        :mrlage,
     c+                        :mrkabc
     c/end-exec
      *
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   endsl
      *
     c                   if        *in15 = *on
     c                   leave
     c                   endif
      *
     c                   if        mrfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
      * Uaktuell abc-kode ?
      *
     c                   move      mrkabc        w_xyz
     c                   movel     mrkabc        w_abc
     c                   if        h1kabc <> *blank
     c                   eval      b_abck = *off
      * bare xyz
     c                   if        d_kabc = *blank
     c                   if        d_kxyz <> w_xyz
     c                   iter
     c                   else
     c                   eval      b_abck = *on
     c                   endif
     c                   endif
      * bare abc
     c                   if        b_abck = *off
     c                   if        d_kxyz = *blank
     c                   if        d_kabc <> w_abc
     c                   iter
     c                   else
     c                   eval      b_abck = *on
     c                   endif
     c                   endif
     c                   endif
      * både og
     c                   if        b_abck = *off
     c                   if        h1kabc <> mrkabc
     c                   iter
     c                   else
     c                   eval      b_abck = *on
     c                   endif
     c                   endif
     c                   endif
5.60  *
5.60 c                   movel     mrvare        vlagl1_vare
5.60 c                   eval      vlagl1_lage = mrlage
5.60 c     vlagl1_key    chain     vlagl1
      *
     c                   movel     mrvare        vvarl1_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found(vvarl1)
     c                   movel     vvtek1        b1text
     c                   else
     c                   eval      b1text = 'Ukjent vare'
     c                   endif
      *
      * Bare låste ?
      *
     c                   if        h1barl <> 0 and
     c                             vvlabc <> 1
     c                   iter
     c                   endif
      *
      * Bare låste med denne abckode ?
      *
5.60 c                   if        vlkabc <> *blank
5.60 c                   eval      w_kabc = vlkabc
5.60 c                   eval      w_labc = vllabc
5.60 c                   else
5.60 c                   eval      w_kabc = vvkabc
5.60 c                   eval      w_labc = vvlabc
5.60 c                   endif
     c
     c                   if        h1labc <> *blank
5.60 c                   if        w_labc = 0
     c                   iter
     c                   endif
5.60 c                   if        w_kabc <> h1labc
     c                   iter
     c                   endif
     c                   endif
      *
      * Skriver til subfile
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1vare = mrvare
     c*                  movel     mrvare        vvarl1_vare
     c*    vvarl1_key    chain     vvarl1
     c*                  if        %found(vvarl1)
     c*                  movel     vvtek1        b1text
     c*                  else
     c*                  eval      b1text = 'Ukjent vare'
     c*                  endif
     c                   eval      b1lage = mrlage
     c                   eval      b1kabc = mrkabc
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   eval      b_open_sok = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   exsr      forny
     c                   goto      end_bck
      *
     c*                  if        *in15
     c*    mprul1_key    setgt     mprul1
     c*                  readp     mprul1
     c*                  endif
      *
      * Leser tilbake en side
      *
     c*                  eval      w_stel = 0
      *
     c*                  dou       w_stel = w_ssrn + c_sfil
      *
     c*                  readp     mprul1
      *
     c*                  if        %eof or
     c*                            mrfirm <> w_firm
     c*                  leave
     c*                  endif
      *
     c*                  eval      w_stel = w_stel + 1
      *
     c*                  eval      mprul1_vare = mrvare
      *
     c*                  enddo
      *
     c*                  if        %eof
     c*    mprul1_key    setll     mprul1
     c*                  endif
      *
      * Blanker og fyller opp subfile
      *
     c*                  exsr      clr_subfile
     c*                  exsr      crt_subfile
      *
     c     end_bck       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
     c                   if        w_afld = 'B2VARE'
     c                   movel     b2vare        w_vare
     c                   call      'VV500R  '
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_tek1
     c                   movel     w_vare        b2vare
     c                   goto      end_spØrr
     c                   endif
      * Lager i key
     c                   if        w_afld = 'B2LAGE'
     c                   call      'RA510R  '
     c                   parm                    w_firm
     c                   parm                    w_jkod
     c                   parm                    w_jtxt
     c                   eval      b2lage = w_jkod
     c                   goto      end_spØrr
     c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på prognose
      *
     c     mprul1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mprul1_vare
      *
      * Key for å slette alle uker på et varenr.
      *
     c     mprulu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mprulu_vare
     c                   kfld                    mprulu_lage
      *
      * Key for les av vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
5.60  *
5.60  * Key for les av lager
5.60  *
5.60 c     vlagl1_key    klist
5.60 c                   kfld                    w_firm
5.60 c                   kfld                    vlagl1_vare
5.60 c                   kfld                    vlagl1_lage
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
     c                   eval      s_vare = *blank
     c                   eval      s_lage = 0
     c                   eval      s_kabc = *blank
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c     mprul1_key    setll     mprul1
      *
     c                   eval      b_open_sok = *on
     c                   eval      w_seqe     = 'O'
     c                   exsr      crt_subfile
      *
     c                   endsr
