     H decedit('0.') datedit(*dmy-) option(*nodebugio)
     h DftActgrp(*No) Actgrp(*NEW)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : NM725R
      * Beskrivelse . . : Eksport av historisk salg til Blueridge datahub
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       161221 bsa  Nytt program
      *
8.01  * K000  130122 bsa  Feil håndtering av member.
8.02  * K000  280122 bsa  Kalkulerer mandag i uka
8.03  * K000  010222 bsa  Lokal leverandør på lager, skal overstyre
8.03  * K000              evnt hovedleverandør fra vare.
8.04  * K000  010222 bsa  Rettet omregningsfaktor.
8.05  * K000  020222 bsa  Bruke pakkseddeldato ved utreging av salgsperiode
8.06  * K000  070222 bsa  Feil håndtering av leverandør/prisgiver.
8.07  * K000  080222 bsa  Ikke alle prisgivere har reskontronummer. Endrer logikk.
8.08  * K000  210222 bsa  Internsalget skal ikke med i totalsalg.
8.09  * K000  210222 bsa  Endring av logikk rundt det å finne leverandør.
8.10  * K000  230222 bsa  Fortsetter med logikken, da det er endel "hull" i
8.10  * K000              grunndata rundt leverandører.
8.11  * K000  040322 bsa  Manglet varetekst
      *
      *
9.xx  * 9.xx  161221 bsa  Div test
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsstal1    if   e           k disk    rename(sstapfr:sstal1r)
     fbrshiu    uf a e           k disk    rename(brshst:brshiur)
     fvvenlr    if   e           k disk    rename(vvenpfr:vvenlrr)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
8.03 fjlevl3    if   e           k disk    rename(jlevpfr:jlevl3r)
8.06 frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     fra10l1    if   e           k disk    rename(ra10pfr:ra10l1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
8.09 fjvkhl1    if   e           k disk    rename(jvkhpfr:jvkhl1r)
     fafpspf    if   e           k disk    rename(afpspfr:afpspfr)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_figr                934    939
     d l_filg                937    939
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d brshiu_hmem     s                   like(brhmem)
     d brshiu_hvar     s                   like(brhvar)
     d brshiu_hlag     s                   like(brhlag)
     d brshiu_hper     s                   like(brhper)
     d vvenlr_vare     s                   like(vevare)
     d vvenlr_enhe     s                   like(veenhe)
     d votyl1_otyp     s                   like(vaotyp)
     d jlevl1_ldor     s                   like(jlldor)
8.03 d jlevl3_enum     s                   like(jlenum)
8.06 d rlevl1_levr     s                   like(rllevr)
     d vvarl1_vare     s                   like(vvvare)
     d ra10l1_jkod     s                   like(rajkod)
     d vlagl1_lage     s                   like(vllage)
     d vlagl1_vare     s                   like(vlvare)
8.09 d jvkhl1_kvnr     s                   like(jvkvnr)
      *
      * Definering av datastrukturer
      *
      *
      * Definering av parametere
      *
     d p_memb          s             10
      *
      * Definering av variable
      *
     d w_kode          s              1
     d w_fell          s              6
     d w_type          s              2
     d w_fast          s             10
     d w_numm          s              8  0
     d w_size          s              8  0
     d w_mal           s            200
     d w_path          s            200
     d w_firm          s                   like(sffirm)
     d w_inlr          s              1
     d w_memb          s             10
     d w_omrl          s                   like(veomre)
     d w_omrs          s                   like(veomre)
     d w_senh          s                   like(veenhe)
     d w_anta          s             11  3
     d w_lety          s              1  0
     d w_otyp          s              2
     d w_lage          s              2  0
     d w_lagid         s              5
     d w_vare          s             15
     d w_peri          s             10
8.02 d w_dato          s               d   datfmt(*iso)
8.02 d w_pfda          s               d   datfmt(*iso)
8.02 d w_ptda          s               d   datfmt(*iso)
8.02 d w_saar          s              4  0
8.02 d w_suke          s              2  0
8.02 d w_stat          s              1
8.09 d w_valg          s              1  0
8.09 d w_jdor          s              6  0
8.09 d w_ldor          s              6  0
8.09 d w_ltxt          s             30
      *
8.03 d s_ldor          s              6  0
      *
8.02 d p_inlr          s              1
      *
      * Felter som legges ut i json rapporten
      *
     d w_date          s               d   datfmt(*iso)
      *
     d b_ordr          s              1    inz(*off)
     d b_same          s              1    inz(*off)
      *
     c/Exec SQL
     c+  Set Option DatFmt=*ISO
     c/End-Exec
      *
      *-----------------------------------------------------------------*
      * Hovedprogram:                                                   *
      *   Plukker salg fra siste 3 år. Skal ikke ta med kreditordre.    *
      *   Internordre, definert som ordretype "FI", håndteres på        *
      *   egen måte.                                                    *
      *   1 - Hent alle ordre fra salgsstatistikk basert på fakturadato *
      *       med unntak av ordretype "FI".                             *
      *   2 - Lese alle internordre for samme periode, og oppdaterer    *
      *       det som allerede er funnet.                               *
      *   3 - Skal ikke ta med PK1, dvs omsetning på lager 99.          *
      *                                                                 *
      *                                                                 *
      *-----------------------------------------------------------------*
      *
      * Henter inn løpenummer og lag id hvis ikke
      *
     c                   if        p_memb = *blanks
     c                   eval      w_kode = '0'
     c                   eval      w_fell = 'NXTKLI'
     c                   eval      w_type = *blank
     c                   eval      w_fast = *blank
     c                   eval      w_numm = *zero
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
     c                   parm                    w_numm
      *
     c                   eval      w_memb = 'AA' + %char(w_numm)
     c                   eval      brshiu_hmem = w_memb
8.01 c                   eval      p_memb = w_memb
     c                   else
     c                   eval      brshiu_hmem = p_memb
     c                   endif
      *
      * Hent historisk salg
      *
     c                   exsr      salg_ordre
      *
      * Avslutt program
      *
     c     avslutt       tag
8.02  *
8.02  * Må avslutte AS710, slik at den lukker underliggende programmer
8.02  *
8.02 c                   eval      w_pfda  = *loval
8.02 c                   eval      w_ptda  = *loval
8.02 c                   eval      w_stat = *blanks
8.02 c                   eval      p_inlr  = *on
8.02  *
8.02 c                   call      'AS710R'
8.02 c                   parm                    w_saar
8.02 c                   parm                    w_suke
8.02 c                   parm                    w_pfda
8.02 c                   parm                    w_ptda
8.02 c                   parm                    w_stat
8.02 c                   parm                    p_inlr
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Finner salg fra 3 år tilbake i tid.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     salg_ordre    begsr
      *
      * Nullstiller teller for antall varer funnet
      *
     c                   eval      w_size = 0
     c                   eval      w_lety = 0
     c                   eval      w_lage = 99
     c                   eval      w_vare = *blanks
      *
      * Så finner vi alle salg 3 år tilbake i tid
      *
     c                   eval      *in15 = *off
      *
     c/exec sql
     c+                  declare sok cursor for
     c+                  select sffirm,
     c+                         sfpaar,
     c+                         sfpmnd,
     c+                         sfpuke,
     c+                         sfbida,
8.05 c+                         sfpada,
     c+                         sflage,
     c+                         sfvare,
8.11 c+                         sftek1,
8.11 c+                         sftek2,
     c+                         sfanta,
     c+                         sfenhe,
     c+                         sfvtyp,
     c+                         sfbinr,
     c+                         sfotyp
     c+                  from   sstapf
     c+                  where  sffirm = :w_firm and
     c+                         sfbida >= :w_date and
     c+                         sflage <> :w_lage and
     c+                         sfvare <> :w_vare and
     c+                         sflety = :w_lety
     c+                  order by sffirm, sfpaar, sfpmnd, sfbinr
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok
     c/end-exec
     c/exec sql
     c+                  open sok
     c/end-exec
      *
     c                   dow       *in15 = *off
      *
     c/exec sql
     c+                  fetch next
     c+                  from  sok
     c+                  into  :sffirm,
     c+                        :sfpaar,
     c+                        :sfpmnd,
     c+                        :sfpuke,
     c+                        :sfbida,
8.05 c+                        :sfpada,
     c+                        :sflage,
     c+                        :sfvare,
8.11 c+                        :sftek1,
8.11 c+                        :sftek2,
     c+                        :sfanta,
     c+                        :sfenhe,
     c+                        :sfvtyp,
     c+                        :sfbinr,
     c+                        :sfotyp
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   if        *in15
     c                   leave
     c                   endif
      *
      * Ikke kreditordre, ref Gisle Åsberg
      *
     c                   eval      b_ordr = *off
     c                   eval      votyl1_otyp = sfotyp
     c     votyl1_key    chain     votyl1
     c                   if        %found
     c                   if        vaokre <> '-'
     c                   eval      b_ordr = *on
     c                   endif
     c                   endif
      *
     c                   if        b_ordr = *on
      *
      * Ikke hvis antall er 0 eller varetyper <> L/S
      *
     c                   if        sfanta <> 0 and
     c                             sfvtyp = 'S' or
     c                             sfanta <> 0  and
     c                             sfvtyp = 'L'
      *
      * Sjekk om det er EVR vare
      *
     c                   eval      vvarl1_vare = sfvare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   goto      ny_slinje
     c                   endif
      *
      * Sjekk om det er lagervare
      *
     c                   eval      vlagl1_lage = sflage
     c                   eval      vlagl1_vare = sfvare
     c     vlagl1_key    chain     vlagl1
     c                   if        not %found or
     c                             vlvtyp <> 'L'
     c                   goto      ny_slinje
     c                   endif
      *
     c                   if        sflage < 10
     c                   eval      w_lagid = l_filg + '0' + %char(sflage)
     c                   else
     c                   eval      w_lagid = l_filg + %char(sflage)
     c                   endif
      *
      * Finner salgsenhet 1, og sjekker om samme. Hvis ikke må det regnes om.
      *
     c                   eval      w_senh = *blanks
     c                   eval      w_omrs = 0
     c                   eval      w_omrl = 0
     c                   eval      w_anta = 0
     c                   eval      vvenlr_vare = sfvare
     c                   eval      vvenlr_enhe = *blanks
     c     vvenlr_key    setll     vvenlr
     c     vvenlr_key2   reade     vvenlr
     c                   dow       not %eof
     c                   if        vesaen = 1
     c                   eval      w_omrs = veomre
     c                   eval      w_senh = veenhe
     c                   endif
     c                   if        sfenhe = veenhe
     c                   eval      w_omrl = veomre
     c                   endif
     c     vvenlr_key2   reade     vvenlr
     c                   enddo
      *
     c                   if        w_omrs = 0
     c                   eval      w_omrs = 1
     c                   endif
     c                   if        w_omrl = 0
     c                   eval      w_omrl = 1
     c                   endif
      *
     c                   eval      w_anta = sfanta
     c                   if        sfenhe <> w_senh
8.04 c*                  eval      w_anta = w_anta * w_omrs / w_omrl
8.04 c                   eval      w_anta = w_anta * w_omrl / w_omrs
     c                   endif
      *
      * Så legger vi ut salgsinfo, som brukes i neste program (NM726R)
      *
     c                   eval      brshiu_hlag = sflage
     c                   eval      brshiu_hvar = sfvare
8.02  *
8.02  * Ny hådntering av dato. I FO616 beregnes år etter bilagsperioden, mens ukenummer
8.02  * hentes ut ifra leveringsdato. Vi må reberegne ukenummer ut ifra bilagsdato, og
8.02  * deretter hente ut mandagen for uke.
8.05  * Bruk pakkseddeldato hvis finnes, deretter bilagsdato
8.02  *
8.05 c                   if        sfpada <> *loval
8.05 c                   eval      w_dato = sfpada
8.05 c                   else
8.02 c                   eval      w_dato = sfbida
8.05 c                   endif
8.02 c                   eval      w_saar = 0
8.02 c                   eval      w_suke = 0
8.02 c                   eval      w_stat = *blanks
8.02  *
8.02 c                   call      'AX711R'
8.02 c                   parm                    w_dato
8.02 c                   parm                    w_saar
8.02 c                   parm                    w_suke
8.02 c                   parm                    w_stat
8.02  *
8.02 c                   if        w_suke > 53
8.02 c                   eval      w_suke = 53
8.02 c                   endif
8.02  * Så finner vi dato for mandag og søndag denne uka
8.02 c                   eval      w_pfda  = *loval
8.02 c                   eval      w_ptda  = *loval
8.02 c                   eval      p_inlr  = *off
8.02 c                   eval      w_stat = *blanks
8.02  *
8.02 c                   call      'AS710R'
8.02 c                   parm                    w_saar
8.02 c                   parm                    w_suke
8.02 c                   parm                    w_pfda
8.02 c                   parm                    w_ptda
8.02 c                   parm                    w_stat
8.02 c                   parm                    p_inlr
8.02  *
8.02 c                   eval      brshiu_hper = %trim(%char(w_pfda))
8.02  *
8.02 c**                 if        sfpuke < 10
8.02 c**                 eval      brshiu_hper = %trim(%char(sfpaar) + '0' +
8.02 c**                                         %char(sfpuke))
8.02 c**                 else
8.02 c**                 eval      brshiu_hper = %trim(%char(sfpaar) +
8.02 c**                                         %char(sfpuke))
8.02 c**                 endif
8.02  *
     c     brshiu_key    chain     brshiu
     c                   if        not %found
     c                   clear                   brshiur
     c                   eval      brhmem = brshiu_hmem
     c                   eval      brhlag = brshiu_hlag
     c                   eval      brhvar = brshiu_hvar
     c                   eval      brhper = brshiu_hper
     c                   time                    brhoda
     c                   time                    brhoti
     c                   eval      brhous = l_user
      *
     c                   eval      w_size = w_size + 1
     c                   eval      brhlin = w_size
     c                   eval      brhlid = w_lagid
      *
      * Hent info fra lager
      *
     c                   eval      ra10l1_jkod = sflage
     c     ra10l1_key    chain     ra10l1
     c                   if        %found
     c                   eval      brhltx = rajtxt
     c                   endif
8.09  *
8.09  * 1. Hvis VVNLEV er 0, bruk VVLDOR. Vi forusetter at dette ikke gjelder
8.09  *    logistikkvarer.
8.09  * 2. Hvis logistikkvare, i JVKHPF, hent JVKLDO og slå opp i JLEVPF. Hvis
8.09  *    reskontronummer er 0, eller ikke treff i RLEV, bruk VVLDOR.
8.09  * 3. Hent vareeier fra JVARPF, hent reskontronummer fra JLEVPF. Hvis
8.09  *    reskontronummer er 0 eller ikke treff i RLEV, bruk VVLDOR.
8.09  * Ref dialog med Jørn
8.09  *
8.09 c                   eval      w_valg = 0
8.09  * Alt 1
8.09 c                   if        vvnlev =  0 and
8.09 c                             vvldor <> 0
8.09 c                   eval      w_valg = 1
8.09 c                   endif
8.09  * Alt 2
8.10 c                   if        w_valg = 0
8.09 c                   eval      jvkhl1_kvnr = vvvare
8.09 c     jvkhl1_key    chain     jvkhl1
8.09 c                   if        %found
8.09 c                   eval      w_valg = 2
8.09 c                   endif
8.10 c                   endif
8.09  * Alt 3
8.10 c                   if        w_valg = 0
8.09 c                   if        vvnlev <> 0
8.10 c                   eval      jlevl1_ldor = vvnlev
8.10 c     jlevl1_key    chain     jlevl1
8.10 c                   if        %found and
8.10 c                             jlenum <> 0
8.09 c                   eval      w_valg = 3
8.10 c                   else
8.10 c                   eval      w_valg = 1
8.10 c                   endif
8.09 c                   endif
8.10 c                   endif
8.09  *
8.09 c                   if        w_valg = 1
8.09 c                   eval      w_ldor = vvldor
8.09 c                   exsr      hent_rlev
8.09 c                   eval      s_ldor = vvldor
8.09 c                   elseif    w_valg = 2
8.09 c                   eval      w_jdor = jvkldo
8.09 c                   exsr      hent_jlev
8.09 c                   elseif    w_valg = 3
8.09 c                   eval      w_jdor = vvnlev
8.09 c                   exsr      hent_jlev
8.09 c                   else
8.09 c                   endif
8.09  *
8.09  * Overstyrt på lager ?
8.09 c                   if        vlbldo <> 0 and
8.09 c                             s_ldor <> vvldor
8.09 c                   eval      w_ldor = vlbldo
8.09 c                   exsr      hent_rlev
8.09 c                   endif
8.09  *
8.06 c                   eval      brhldo = w_ldor
8.06 c                   eval      brhldt = w_ltxt
      *
     c                   eval      brhvtx = %trim(sftek1 + sftek2)
     c                   eval      brhvar = sfvare
      *
     c                   endif
      *
     c                   if        sfotyp = 'FI'
     c                   eval      brhiqt = brhiqt + w_anta
8.08 c                   else
8.08 c                   eval      brhsqt = brhsqt + w_anta
     c                   endif
      *
     c                   if        not %found(brshiu)
     c                   write     brshiur
     c                   else
     c                   update    brshiur
     c                   endif
      *
     c                   endif
      *
     c                   endif
      *
     c     ny_slinje     tag
      *
     c                   enddo
      *
     c     salg_slutt    endsr
      *
8.09  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.09  * Henter leverandørinformasjon
8.09  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.09  *
8.09 c     hent_rlev     begsr
8.09  *
8.09  * Slå opp mot leveradør for å hente inn land/valutakode
8.09  *
8.09 c                   eval      rlevl1_levr = w_ldor
8.09 c     rlevl1_key    chain     rlevl1
8.09 c                   if        %found
8.09 c                   eval      w_ltxt = rlnavn
8.09 c                   endif
8.09  *
8.09 c                   endsr
8.09  *
8.09  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.09  * Henter leverandørinformasjon via JLEVPF - NOBB vareeier
8.09  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.09  *
8.09 c     hent_jlev     begsr
8.09  *
8.09 c                   eval      jlevl1_ldor = w_jdor
8.09 c     jlevl1_key    chain     jlevl1
8.09 c                   if        %found
8.09 c                   eval      w_ldor = jlenum
8.09 c                   eval      s_ldor = jlenum
8.09 c                   eval      w_ltxt = jlnavn
8.09 c                   endif
8.09  *
8.09 c                   endsr
8.09  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ERROR-rutine  - Brukes ved mislykket web-service kall
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *pssr         begsr
      *
     c**                 eval      p_link = 'Blueridge eksport feilet'
     c                   goto      avslutt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Henter inn parameter
      *
     c     *entry        plist
     c                   parm                    p_memb
      *
      * Key for info fra enhet
      *
     c     vvenlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenlr_vare
     c                   kfld                    vvenlr_enhe
      *
      * Key for info fra enhet
      *
     c     vvenlr_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvenlr_vare
      *
      * Key for les på ordretype
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for les på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for les på lager
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl1_vare
     c                   kfld                    vlagl1_lage
      *
      * Key for info fra blueridge export
      *
     c     brshiu_key    klist
     c                   kfld                    brshiu_hmem
     c                   kfld                    brshiu_hvar
     c                   kfld                    brshiu_hlag
     c                   kfld                    brshiu_hper
      *
      * Key for les på lager
      *
     c     ra10l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra10l1_jkod
      *
      * Key for info fra leverandørregister (prisgriver)
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
8.03  *
8.03  * Key for info fra leverandørregister (prisgriver)
8.03  *
8.03 c     jlevl3_key    klist
8.03 c                   kfld                    jlevl3_enum
8.06  *
8.06  * Key for les på leverandør
8.06  *
8.06 c     rlevl1_key    klist
8.06 c                   kfld                    w_firm
8.06 c                   kfld                    rlevl1_levr
8.09  *
8.09  * Key for info fra logistikkvarenummer
8.09  *
8.09 c     jvkhl1_key    klist
8.09 c                   kfld                    w_firm
8.09 c                   kfld                    jvkhl1_kvnr
      *
     c                   eval      w_firm = l_firm
      *
      * Beregn datoen vi skal salget
      *
     c                   time                    w_date
     c                   subdur    3:*y          w_date
      *
     c                   endsr
      *
