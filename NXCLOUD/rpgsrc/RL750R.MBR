      /title  ASOKON Leverandør, oppdaterer søketekst
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON
      * Program . . . . : RL750R
      * Beskrivelse . . : Leverandør, oppdaterer søketekst
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       250101 KOB  Nytt program
      * R5.10 100603 KOB  Lagt inn alfafelt først i søketekst
      * R5.20 101103 KOB  Lagt inn foretaksnummer i søketekst
      * R5.60 120107 KOB  Lagt inn bankkontonummer i søketekst
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     frlsolu    uf a e           k disk    rename(rlsopfr:rlsolur)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(rlfirm)
     d p_levr          s                   like(rllevr)
      *
      * Definering av variable i nøkler
      *
     d rlevl1_levr     s                   like(rllevr)
     d rlsolu_slev     s                   like(rlslev)
      *
      * Definering av variable
      *
     d w_l             s              2  0
     d w_leng          s              2  0
     d w_levr_alf      s              6
     d w_ftnr_alf      s              9
     d w_bgir_alf      s             11
      *
     d w_firm          s                   like(rlfirm)
     d w_stek          s                   like(rlstek)
     d w_tlfn          s                   like(rltlfn)
     d w_mobn          s                   like(rlmobn)
      *
      * Definering av konstanter
      *
     d Lo              c                   'abcdefghijklmnopqrstuvwxyzæøå'
     d Up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅ'
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      rlevl1_levr = p_levr
     c     rlevl1_key    chain     rlevl1                             90
     c                   if        *in90 = *off
     c                   exsr      oppdater
     c                   endif
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer søketekst
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdater      begsr
      *
      * Alfafelt
      *
     c                   eval      w_stek = rlalfa
      *
      * Navn
      *
     c     Lo:Up         xlate     rlnavn        rlnavn
     c                   eval      w_stek = %trimr(w_stek) + ' ' +
     c                                      %trim(rlnavn)
      *
      * Adresse-1
      *
     c                   if        rlgate <> *blank
     c     Lo:Up         xlate     rlgate        rlgate
     c                   eval      w_stek = %trimr(w_stek) + ' ' +
     c                                      %trim(rlgate)
     c                   endif
      *
      * Adresse-2
      *
     c                   if        rladr2 <> *blank
     c     Lo:Up         xlate     rladr2        rladr2
     c                   eval      w_stek = %trimr(w_stek) + ' ' +
     c                                      %trim(rladr2)
     c                   endif
      *
      * Poststed
      *
     c                   if        rlsted <> *blank
     c     Lo:Up         xlate     rlsted        rlsted
     c                   eval      w_stek = %trimr(w_stek) + ' ' +
     c                                      %trim(rlsted)
     c                   endif
      *
      * Telefonnummer
      *
     c                   if        rltlfn <> *blank
      *
     c                   eval      w_tlfn = %trim(rltlfn)
     c                   eval      w_leng = %len(%trim(rltlfn))
      *
     c     ' '           scan      w_tlfn        w_l
     c                   dow       w_l <> 0 and
     c                             w_l <  w_leng
     c                   eval      w_tlfn = %subst(w_tlfn:1:w_l-1) +
     c                                      %subst(w_tlfn:w_l+1:11-w_l)
     c                   eval      w_tlfn = %trim(w_tlfn)
     c                   eval      w_leng = %len(%trim(w_tlfn))
     c     ' '           scan      w_tlfn        w_l
     c                   enddo
      *
     c                   eval      w_stek = %trimr(w_stek) + ' ' +
     c                                      %trim(w_tlfn)
      *
     c                   endif
      *
      * Mobiltelefon-nummer
      *
     c                   if        rlmobn <> *blank
      *
     c                   eval      w_mobn = %trim(rlmobn)
     c                   eval      w_leng = %len(%trim(rlmobn))
      *
     c     ' '           scan      w_mobn        w_l
     c                   dow       w_l <> 0 and
     c                             w_l <  w_leng
     c                   eval      w_mobn = %subst(w_mobn:1:w_l-1) +
     c                                      %subst(w_mobn:w_l+1:11-w_l)
     c                   eval      w_mobn = %trim(w_mobn)
     c                   eval      w_leng = %len(%trim(w_mobn))
     c     ' '           scan      w_mobn        w_l
     c                   enddo
      *
     c                   eval      w_stek = %trimr(w_stek) + ' ' +
     c                                      %trim(w_mobn)
      *
     c                   endif
      *
      * Leverandørnummer
      *
     c                   move      rllevr        w_levr_alf
      *
     c                   eval      w_stek = %trimr(w_stek) + ' ' +
     c                                      w_levr_alf
      *
      * Foretaksnummer
      *
     c                   if        rlftnr <> 0
     c                   move      rlftnr        w_ftnr_alf
     c                   eval      w_stek = %trimr(w_stek) + ' ' +
     c                                      w_ftnr_alf
     c                   endif
      *
      * Bankgironummer
      *
     c                   if        rlbgir <> 0
     c                   move      rlbgir        w_bgir_alf
     c                   eval      w_stek = %trimr(w_stek) + ' ' +
     c                                      w_bgir_alf
     c                   endif
      *
      * Oppdaterer/oppretter søketekst
      *
     c                   eval      rlsolu_slev = rllevr
     c     rlsolu_key    chain     rlsolur                            91
      *
     c                   eval      rlstek = w_stek
      *
     c                   if        *in91 = *off
     c                   update    rlsolur
     c                   else
     c                   eval      rlsfir = w_firm
     c                   eval      rlslev = rllevr
     c                   write     rlsolur
     c                   endif
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_levr
      *
      * Key for oppslag på leverandørregister
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for oppdatering av søketekst-register
      *
     c     rlsolu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlsolu_slev
      *
      *
      * Henter firma fra paramter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
