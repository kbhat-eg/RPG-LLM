     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : LO802R
      * Beskrivelse . . : Varemottak, hovedrutine
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       240402 ASK  Nytt program
      * V5.50 030407 KOB  Henter etikettstyring fra fastregister
6.10  * R6.10 250809 BSA  Utvidet ref. felter fra 20 til 30 posisjoner
6.20  * R6.20 300511 BSA  Lagt inn F tast for å vise logg.
6.21  * R6.20 300511 BSA  Legger ut leverandørens ordrenummer
6.22  * R6.20 300511 BSA  Legger ut siste loggkode
6.nu  * R6.30 030614 bof  Utvidelse vedr. utvidelse nummerserie
6.31  * 6.30  171014 bof  sjekk om best. har sperre kode før man får begynne
7.xx  * 7.00  090316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flohelr    if   e           k disk    rename(lohepfr:lohelrr)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fvfaspf    if a e           k disk
      *
     flo801d    cf   e             workstn
      *
      * Parameter-ut
      *
     d                 ds
6.nu d d_prec                        35
     d  d_firm                        3  0 overlay(d_prec)
6.nu d  d_numm                        8  0 overlay(d_prec:4)
6.nu d  d_suff                        2  0 overlay(d_prec:12)
6.nu d  d_ldat                         d   overlay(d_prec:14)
6.nu d  d_lage                        2  0 overlay(d_prec:24)
6.nu d  d_innk                        4  0 overlay(d_prec:26)
6.nu d  d_rest                        1  0 overlay(d_prec:30)
6.nu d  d_etik                        1  0 overlay(d_prec:31)
6.nu d  d_leti                        1  0 overlay(d_prec:32)
6.nu d  d_seti                        1  0 overlay(d_prec:33)
6.nu d  d_motk                        1  0 overlay(d_prec:34)
6.nu d  d_dumm                        1    overlay(d_prec:35)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(lofirm)
     d p_numm          s                   like(lonumm)
     d p_suff          s                   like(losuff)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d lohelr_numm     s                   like(lonumm)
     d lohelr_suff     s                   like(losuff)
     d rlevl1_levr     s                   like(rllevr)
      *
      * Definering av variable
      *
     d w_firm          s                   like(lofirm)
     d w_lage          s                   like(lolage)
     d w_innk          s                   like(loinnk)
     d w_dato          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_latx          s             20
     d w_intx          s             20
     d w_ettx          s             20
     d w_stat          s              1
6.nu d w_prec          s             35
6.20 d w_numm          s                   like(lonumm)
6.20 d w_suff          s                   like(losuff)
6.31 d w_mld1          s             50
6.22 d p_logk          s              1
6.22 d p_lbes          s             20
      *
     d b_feil          s              1    inz(*off)
6.31 d b_sper          s              1    inz(*off)
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * A1BLD  - A1 Skjermbilde for bestillingshode informasjon
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * A1BLD Behandle bestillingshodebilde med valg
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser valgt bestilling og leverandør
      *
     c     lohelr_key    chain     lohelr
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      rlevl1_levr = loldor
     c     rlevl1_key    chain     rlevl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
6.31  *
6.31  * sjekk om bestilling er sperre
6.31 c                   exsr      sjekk_sperr
6.31 c                   if        b_sper = *on
6.31 c                   goto      avslutt
6.31 c                   endif
      *
     c                   eval      a1otyp = lootyp
     c     *dmy          move      lobdat        a1bdat
     c                   eval      a1numm = lonumm
     c                   eval      a1suff = losuff
     c                   eval      a1ornr = loornr
6.21 c                   eval      a1sorn = losorn
     c                   eval      a1orsu = loorsu
     c                   eval      a1ldor = loldor
     c                   eval      a1lena = lolena
     c                   eval      a1navn = lonavn
     c                   eval      a1adr1 = loadr1
     c                   eval      a1adr2 = loadr2
     c                   eval      a1sted = losted
     c                   eval      a1vref = lovref
     c                   eval      a1dref = lodref
     c                   eval      a1lage = lolage
     c                   eval      a1innk = loinnk
     c                   eval      a1rest = rlrest
     c                   eval      d_leti = rlhyle
     c                   eval      a1etik = d_motk
      *
     c                   eval      w_lage = lolage
     c                   exsr      sjekk_lager
     c                   eval      a1lage = w_lage
     c                   eval      a1latx = w_latx
      *
     c                   eval      w_innk = loinnk
     c                   exsr      sjekk_selger
     c                   eval      a1innk = w_innk
     c                   eval      a1intx = w_intx
     c     *dmy          move      w_dato        a1ldat
6.22  * Finn siste loggkode for bestillingen
6.22 c                   exsr      finn_lkode
6.22  *
     c     a1taga        tag
     c                   exfmt     a1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      sp_input
     c                   goto      a1taga
6.20 c                   when      *inkj
6.20 c                   exsr      vis_logg
6.20 c                   goto      a1taga
     c                   endsl
      *
      * Sjekk evt.overstyringer
      *
     c                   eval      w_lage = a1lage
     c                   exsr      sjekk_lager
     c                   if        b_feil = *on
     c                   eval      *in31 = *on
     c                   goto      a1taga
     c                   endif
     c                   eval      a1latx = w_latx
      *
     c                   eval      w_innk = a1innk
     c                   exsr      sjekk_selger
     c                   if        b_feil = *on
     c                   eval      *in32 = *on
     c                   goto      a1taga
     c                   endif
     c                   eval      a1intx = w_intx
      *
     c     *dmy          test(d)                 a1ldat                 33
     c                   if        *in33 = *on
     c                   goto      a1taga
     c                   endif
      *
     c                   if        a1rest <> 0 and
     c                             a1rest <> 1
     c                   eval      *in34 = *on
     c                   goto      a1taga
     c                   endif
      *
5.50 c                   if        a1etik > 1
     c                   eval      *in35 = *on
     c                   goto      a1taga
     c                   endif
      *
HOFF c                   if        a1epak > 1
HOFF c                   eval      *in36 = *on
HOFF c                   goto      a1taga
HOFF c                   endif
HOFF  *
      * Kaller opp program for linjebehandling
      *
     c                   eval      d_firm = w_firm
     c                   eval      d_numm = a1numm
     c                   eval      d_suff = a1suff
     c     *ymd          move      a1ldat        d_ldat
     c                   eval      d_lage = a1lage
     c                   eval      d_innk = a1innk
     c                   eval      d_rest = a1rest
5.50 c                   eval      d_etik = a1etik
     c                   eval      d_dumm = '*'
      *
     c                   eval      w_prec = d_prec
HOFF  *
HOFF c                   if        a1epak = 1
HOFF c                   call      'LO960C'
HOFF c                   parm                    w_prec
HOFF c                   endif
HOFF  *
     c                   call      'LO802R'
     c                   parm                    w_prec
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
6.22  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.22  * Subrutine for henting av siste loggkode
6.22  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.22  *
6.22 c     finn_lkode    begsr
6.22  *
6.22 c                   eval      p_logk = *blank
6.22 c                   eval      p_lbes = *blank
6.22 c                   call      'LM501R'
6.22 c                   parm                    w_firm
6.22 c                   parm                    a1numm
6.22 c                   parm                    a1suff
6.22 c                   parm                    p_logk
6.22 c                   parm                    p_lbes
6.22  *
6.22 c                   if        p_logk <> *blank
6.22 c                   eval      a1logk = p_logk
6.22 c                   eval      a1lbes = p_lbes
6.22 c                   endif
6.22  *
6.22 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kontroll av lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_lager   begsr
      *
     c                   eval      b_feil = *off
     c                   call      'RS710R'
     c                   parm                    w_stat
     c                   parm                    w_lage
     c                   parm                    w_latx
      *
     c                   if        w_stat <> *blank
     c                   eval      b_feil = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kontroll av innkjøper (selger)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_selger  begsr
      *
     c                   eval      b_feil = *off
     c                   call      'RS709R'
     c                   parm                    w_stat
     c                   parm                    w_innk
     c                   parm                    w_intx
      *
     c                   if        w_stat <> *blank
     c                   eval      b_feil = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for spørring på input
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sp_input      begsr
      *
      *  Spørring på lager
      *
     c                   if        w_afld = 'A1LAGE'
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    w_lage
     c                   parm                    w_latx
     c                   eval      a1lage = w_lage
     c                   exsr      sjekk_lager
     c                   eval      a1latx = w_latx
     c                   goto      end_sp
     c                   endif
      *
      *  Spørring på innkjøper
      *
     c                   if        w_afld = 'A1INNK'
     c                   call      'RA509R'
     c                   parm                    w_firm
     c                   parm                    w_innk
     c                   parm                    w_intx
     c                   eval      a1innk = w_innk
     c                   eval      a1intx = w_intx
     c                   goto      end_sp
     c                   endif
      *
     c     end_sp        endsr
      *
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  * Subrutine for spørring på input
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  *
6.20 c     vis_logg      begsr
6.20  *
6.20  *  Call program for loggkoder
6.20  *
6.20 c                   eval      w_numm = p_numm
6.20 c                   eval      w_suff = p_suff
6.20  *
6.20 c                   call      'LM500R'
6.20 c                   parm                    w_firm
6.20 c                   parm                    w_numm
6.20 c                   parm                    w_suff
6.20  *
6.20 c                   endsr
6.20  *
6.31  *
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  * Subrutine for å sjekke om bestilling er sperret
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  *
6.31 c     sjekk_sperr   begsr
6.31  *
6.31 c                   eval      b_sper = *off
6.31  *
6.31 c                   if        lokode <> 0
6.31 c                   eval      b_sper = *on
6.31 c                   eval      w_mld1 = 'Bestilling er i bruk av annen -
6.31 c                             bruker'
6.31 c                   call      'AA005R'
6.31 c                   parm                    w_mld1
6.31 c                   endif
6.31  *
6.31 c                   endsr
6.31  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_numm
     c                   parm                    p_suff
      *
      *  Key for les av BESTILLING
      *
     c     lohelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelr_numm
     c                   kfld                    lohelr_suff
      *
      *  Key for les av LEVERANDØR
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for faste data etiketter
      *
     C     vfaspf_key    klist
     C                   kfld                    w_firm
      *
      * Henter verdier fra parametre og LDA
      *
     c                   eval      lohelr_numm = p_numm
     c                   eval      lohelr_suff = p_suff
      *
     c                   eval      w_firm = l_firm
     c                   time                    w_dato
     c                   time                    w_time
      *
      * Henter std. etikettype
      *
     c     vfaspf_key    chain     vfaspf
     c                   if        %found
     c                   eval      d_seti = vnetyp
     c                   eval      d_motk = vnmotk
     c                   else
     c                   eval      d_seti = 0
     c                   eval      d_motk = 0
     c                   endif
      *
     c                   endsr
