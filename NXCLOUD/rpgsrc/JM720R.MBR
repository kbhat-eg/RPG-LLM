     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JM720R
      * Beskrivelse . . : Kampanje-vare, oppdater fra NIB
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       100701 HKO  Nytt program
      *       060105 HKO  Henter varenr på bakgrunn av Nobbnr
      *                   Sletter eventuell eksisterende kampanje
8.01  *PRG2   220622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fpckampf   if   e           k disk
      *
     fjvarl3    if   e           k disk    rename(jvarpfr:jvarl3r)
     fjkahlu    uf a e           k disk    rename(jkahpfr:jkahlur)
     fjkavlu    uf a e           k disk    rename(jkavpfr:jkavlur)
      *
      * Definering av Local Data Area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Datastrukturer
      *
      * Definering av Norgros kampanje-recordformat
      *
     d                 ds
     d d_hrec                       256
     d  d_h_htyp                      3    overlay(d_hrec:1)   inz('1,"')
     d  d_h_kamp                      5    overlay(d_hrec:4)
     d  d_h_fil1                      3    overlay(d_hrec:9)   inz('","')
     d  d_h_besk                     35    overlay(d_hrec:12)
     d  d_h_fil2                      2    overlay(d_hrec:47)  inz('",')
     d  d_h_fdat                      8    overlay(d_hrec:49)
     d  d_h_fil3                      1    overlay(d_hrec:57)  inz(',')
     d  d_h_tdat                      8    overlay(d_hrec:58)
     d  d_h_fil4                      2    overlay(d_hrec:66)  inz(',"')
     d  d_h_ansv                     10    overlay(d_hrec:68)
     d  d_h_fil5                      1    overlay(d_hrec:78)  inz('"')
      *
     d                 ds
     d d_vrec                       256
     d  d_v_vtyp                      2    overlay(d_vrec:1)   inz('2,')
     d  d_v_vnob                      8  0 overlay(d_vrec:3)
     d  d_v_fil1                      2    overlay(d_vrec:11)  inz(',"')
     d  d_v_vvar                     15    overlay(d_vrec:13)
     d  d_v_fil2                      2    overlay(d_vrec:28)  inz('",')
     d  d_v_vldo                      6  0 overlay(d_vrec:30)
     d  d_v_fil3                      2    overlay(d_vrec:36)  inz(',"')
     d  d_v_vbet                     45    overlay(d_vrec:38)
     d  d_v_fil4                      3    overlay(d_vrec:83)  inz('","')
     d  d_v_vanm                     45    overlay(d_vrec:86)
     d  d_v_fil5                      2    overlay(d_vrec:131) inz('",')
     d  d_v_vkse                      1  0 overlay(d_vrec:133)
     d  d_v_fil6                      1    overlay(d_vrec:134) inz(',')
     d  d_v_vkai                      9  2 overlay(d_vrec:135)
     d  d_v_fil7                      1    overlay(d_vrec:144) inz(',')
     d  d_v_vkaa                      9  2 overlay(d_vrec:145)
     d  d_v_fil8                      1    overlay(d_vrec:154) inz(',')
     d  d_v_vkau                      9  2 overlay(d_vrec:155)
      *
      * Definering av variable i nøkler
      *
     d jkahlu_kamp     s                   like(jmkamp)
     d jkavlu_vkam     s                   like(jmvkam)
     d jvarl3_nobb     s                   like(jvnobb)
      *
      * Definering av variable
      *
     d w_rect          s              1
     d w_dato_6        s              6  0
      *
     d w_firm          s                   like(jmfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser kampanjefil
      *
     c                   read      pckampf
     c                   dow       not %eof
      *
     c                   eval      w_rect = %subst(pckamp:1:1)
      *
     c                   select
     c                   when      w_rect = '1'
     c                   exsr      hode
     c                   when      w_rect = '2'
     c                   exsr      varelinje
     c                   endsl
      *
     c                   read      pckampf
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av hoderecord
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode          begsr
      *
     c                   eval      d_hrec = pckamp
      *
      * Sletter eventuell eksisterende kampanje
      *
     c                   eval      jkahlu_kamp = d_h_kamp
     c     jkahlu_key    chain     jkahlur
     c                   if        %found
     c                   delete    jkahlur
     c                   eval      jkavlu_vkam = d_h_kamp
     c     jkavlu_key    setll     jkavlu
     c     jkavlu_key    reade     jkavlur
     c                   dow       not %eof
     c                   delete    jkavlur
     c     jkavlu_key    reade     jkavlur
     c                   enddo
     c                   endif
      *
      * Skriver til kampanje-hode
      *
     c                   clear                   jkahlur
      *
     c                   eval      jmfirm = w_firm
     c                   eval      jmkamp = d_h_kamp
     c                   eval      jmprgr = *blank
     c                   eval      jmbesk = d_h_besk
     c                   eval      jmansv = d_h_ansv
     c                   eval      jmodat = *loval
     c                   eval      jmeusr = l_user
      *
     c                   move      d_h_fdat      w_dato_6
     c     *ymd          move      w_dato_6      jmfdat
      *
     c                   move      d_h_tdat      w_dato_6
     c     *ymd          move      w_dato_6      jmtdat
      *
     c                   time                    jmedat
     c                   time                    jmetim
      *
     c                   write     jkahlur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av varelinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     varelinje     begsr
      *
     c                   eval      d_vrec = pckamp
      *
      * Finner varenr på bakgrunn av nobbnr
      *
     c                   eval      jvarl3_nobb = d_v_vnob
     c     jvarl3_key    chain     jvarl3
     c                   if        not %found
     c                   movel     d_v_vnob      jvvare
     c                   endif
      *
      * Skriver til kampanje-vare
      *
     c                   clear                   jkavlur
      *
     c                   eval      jmvfir = w_firm
     c                   eval      jmvkam = d_h_kamp
     c                   eval      jmvvar = jvvare
     c                   eval      jmvldo = d_v_vldo
     c                   eval      jmvkai = d_v_vkai
     c                   eval      jmvkpr = 0
     c                   eval      jmvkaa = d_v_vkaa
     c                   eval      jmvkau = d_v_vkau
     c                   eval      jmvans = *blank
     c                   eval      jmveus = l_user
      *
     c                   time                    jmveda
     c                   time                    jmveti
      *
     c                   write     jkavlur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på vare
      *
     c     jvarl3_key    klist
     c                   kfld                    jvarl3_nobb
      *
      * Key for oppdatering av kampanje-hode
      *
     c     jkahlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jkahlu_kamp
      *
      * Key for oppdatering av kampanje-vare
      *
     c     jkavlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jkavlu_vkam
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
