     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RK681R                                              *
      * Beskrivelse . . : Factoring - utplukk                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      * R5.40     120405  Nytt program                                   KOB  *
      * R5.40     120406  Lagt inn utskrift i bilagsnummerrekkefølge     KOB  *
      * R5.60     171008  Lagt inn egen timestamp for arbeidsfil         KOB  *
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *       KA = F1  = Valg av firma                                  *
      *       KC = F3  = Exit                                           *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     frktrl1    if   e           k disk    rename(rktrpfr:rktrl1r)
     fra01l1    if   e           k disk    rename(ra01pfr:ra01l1r)
     frkwbpf    o    e           k disk    rename(rkwbpfr:rkwbpfr)
      *
     d                uds
     d  pfirm                300    302  0
     d  pnavn                303    332
     d  puser                333    342
     d  pkdat                343    348  0
     d  palfa                475    475
     d  pbilk                476    477  0
     d  pparam               300    490
      *
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d w_fdat          s               d   datfmt(*iso)
     d w_kdat          s               d   datfmt(*iso)
     d w_forf          s               d   datfmt(*iso)
     d w_rest          s                   like(rnrest)
     d w_firm          s                   like(rkfirm)
      *
     d                 ds
     d w_dato                         6  0                                      Hele utvekslingsfelt
     d  w_dag                         2  0 overlay(w_dato)                      Status
     d  w_mnd                         2  0 overlay(w_dato:3)                    Firma
     d  w_aar                         2  0 overlay(w_dato:5)                    Firma
      *
     d w_udat          s              6  0
      *
      * Definering av variable
      *
     d w_tmst          s               z
     d w_tmst_20       s             20
     d w_tell          s              6  0
     d w_tell_alf      s              6
      *
     d n               s              2  0
     d wkun            s              7  0
     d rktrl1_kund     s                   like(rkkund)
     d rkunl1_kund     s                   like(rkkund)
     d ra01l1_bilk     s                   like(rnbilk)
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Utplukk av kunder til factoring                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c                   eval      w_firm = l_firm
      *
     c     rkunl1_key    setll     rkunl1                                 61
     c     rkunl1_key    reade     rkunl1                                 61
      *
     c                   dow       *in61 = *off
      *
      * Kunden skal ikke behandles
      *
     c                   if        rkfack = 1
     c                   goto      nykund
     c                   endif
      *
     c                   exsr      beh_trans
      *
     c     nykund        tag
      *
     c     rkunl1_key    reade     rkunl1                                 61
      *
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Utplukk av kundeposter til purring                              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     beh_trans     begsr
      *
     c                   eval      rktrl1_kund = rkkund
      *
     c     rktrl1_key    setll     rktrl1                                 62
     c     rktrl1_key    reade     rktrl1                                 62
      *
     c                   dow       *in62 = *off
      *
      * Posten er gjort opp
      *
     c                   if        rnrest = 0
     c                   goto      neste
     c                   endif
      *
      * Posten er merket at den ikke skal til factoring
      *
     c                   if        rnfack = 1
     c                   goto      neste
     c                   endif
      *
      * Sendt til factoring tidligere
      *
     c                   if        rnfada <> *loval
     c                   goto      neste
     c                   endif
      *
      * Reklamasjon
      *
     c                   if        rnbilk = 89  or
     c                             rnrekl = 'J'
     c                   goto      neste
     c                   endif
      *
      * Bilagskode gyldig
      *
     c                   eval      ra01l1_bilk = rnbilk
     c     ra01l1_key    chain     ra01l1
     c                   if        raafac = ' '
     c                   goto      neste
     c                   endif
      *
      * Bilagskode er oppgitt
      * og kode matcher ikke
      *
     c                   if        pbilk <> 0 and
     c                             pbilk <> rnbilk
     c                   goto      neste
     c                   endif
      *
     c                   select
     c                   when      palfa = 'A'
     c                   eval      rwkalf = rkalfa
     c                   when      palfa = 'K'
     c                   movel     rnkund        rwkalf
     c                   other
     c                   movel     rnbiln        rwkalf
     c                   endsl
      *
      * Teller/Timestamp
      *
     c                   eval      w_tell = w_tell + 1
     c                   move      w_tell        w_tell_alf
     c                   time                    rwtist
     c     *iso0         move      rwtist        w_tmst_20
     c                   move      w_tell_alf    w_tmst_20
     c     *iso0         move      w_tmst_20     rwtist
      *
     c                   write     rkwbpfr
      *
     c     neste         tag
      *
     c     rktrl1_key    reade     rktrl1                                 62
     c                   enddo
      *
     c                   endsr
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c     *dmy          move      pkdat         w_kdat
      *
      * Key for lesing av kunderegister
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for lesing av kundeposteringer
      *
     c     rktrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rktrl1_kund
      *
      * Key for lesing av bilagskoder
      *
     c     ra01l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra01l1_bilk
      *
     c                   endsr
