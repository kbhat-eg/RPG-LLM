     H/TITLE  ASSHOP: Sjekker om arb.stasjon og bruker er gyldige
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASSHOP
      * Program . . . . : BOSJKR
      * Beskrivelse . . : Sjekker om arb.stasjon og bruker er gyldige
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
7.xx  * 7.00  151116 bsa  Endringer reltart til ny GUI. Endre slik at sjekk skjer
7.xx  *                   skjer mot bruker og ikke terminalid.
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     FBOSJKD    CF   E             WORKSTN
      *
     FBSTSL1    IF   E           K DISK
     F                                     RENAME(BSTSPFR:BSTSL1R)
     FBWSDL1    IF   E           K DISK
     F                                     RENAME(BWSDPFR:BWSDL1R)
      *
     D MLD             S             50    DIM(5) CTDATA PERRCD(1)               MELDINGER
      *
      *
     D                UDS
     D  DSWSID               901    910
     D  DSUSER               911    920
     D  DSSFIR               944    946  0
     D  DSFNAV               951    980
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  C1BLD Behandle åpningsbildet                                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     C1TAGA        TAG
      *
     C                   MOVE      *OFF          *IN50
      *
      *  Sjekk,  Finnes status-koder lagt opp i STATUS-REGISTER
      *
     C     STSKEY        CHAIN     BSTSL1                             90
     C     *IN90         IFEQ      *ON
     C                   MOVE      *ON           *IN50
     C                   MOVE      MLD(01)       C1MLD1
     C                   END
      *
      *  Sjekk,  Finnes arbeid-stasjon opplagt i SKJERM/KASSE-REGISTER
7.xx  *  Sjekk mot bruker hvis ikke skjermid er lagt opp i registeret.
7.xx  *
     C                   MOVE      DSWSID        WSDNUM
     C     WSDKEY        CHAIN     BWSDL1                             90
     C     *IN90         IFEQ      *ON
7.xx C                   MOVE      DSUSER        WSDNUM
7.xx C     WSDKEY        CHAIN     BWSDL1                             90
7.xx C     *IN90         IFEQ      *ON
     C                   MOVE      *ON           *IN50
     C                   MOVE      MLD(02)       C1MLD2
7.xx C                   END
     C                   END
      *
      *  Sjekk   Har Arbeidstasjon lov til og utføre denne oppgaven
      *
     C     BWKTYP        IFLT      WPINTK
     C     WPINTK        IFEQ      1
     C                   MOVE      *ON           *IN50
     C                   MOVE      MLD(03)       C1MLD3
     C                   END
     C     WPINTK        IFEQ      2
     C                   MOVE      *ON           *IN50
     C                   MOVE      MLD(04)       C1MLD3
     C                   END
     C                   END
      *
      *  Dersom feil er oppdaget sendes skjermbilde ut
      *
     C     *IN50         IFEQ      *ON
     C                   EXFMT     C1BLD
      *
      *  Flytter inn kode for feil, neste program skal ikke startes
      *
     C                   MOVE      '9'           WPKODE
      *
     C     *INKC         CABEQ     *ON           XSLUTT
      *
     C                   END
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     XSLUTT        TAG
      *
     C                   SETON                                        LR
     C                   RETURN
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     *INZSR        BEGSR
      *
      *  Redefinering av nøkler
      *
     C     *LIKE         DEFINE    BAAFIR        STSFIR
      *
     C     *LIKE         DEFINE    BWFIRM        WSDFIR
     C     *LIKE         DEFINE    BWWSID        WSDNUM
      *
      *  Key for file : BUTIKK-KODE-REGISTER
      *
     C     STSKEY        KLIST
     C                   KFLD                    STSFIR
      *
      *  Key for file : SKJERM/KASSE-REGISTER
      *
     C     WSDKEY        KLIST
     C                   KFLD                    WSDFIR
     C                   KFLD                    WSDNUM
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     *ENTRY        PLIST
     C                   PARM                    WPKODE            1
      *
     C                   MOVE      WPKODE        WPINTK            1 0
      *
      *  Legger inn firmanummer i nøkklene
      *
     C                   MOVE      DSSFIR        STSFIR
     C                   MOVE      DSSFIR        WSDFIR
      *
      *  Legge inn firma-
      *  nummer / navn i vise-felter
      *
     C                   MOVE      DSFNAV        C1FNAV
     C                   MOVE      DSSFIR        C1AFIR
      *
     C                   ENDSR
      *
**     M E L D I N G E R                         !
   STATUS-KODER finnes ikke lagt opp
   ARBEIDSTASJON finnes ikke lagt opp
   ARBEIDSTASJON kan ikke starte dagsoppgjør
   ARBEIDSTASJON kan ikke starte journal-liste
   BRUKER har ikke lov og kjøre butikk-system
