     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : LM900R
      * Beskrivelse . . : Oppdatering av loggfil for ordreforløp
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       090307 ASK  Nytt program
6.nu  * 6.30  280514 bof  Endring vedr. nummerutvidelse
6.30  * 6.30  101218 bsa  Gjør oppslag før skriving
      *
      *
      *
      *
      *
      *
      *
      *
5.01  *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
3.32 flloglu    uf a e           k disk    rename(llogpfr:lloglur)
      *
      * Definering av parametre
      *
     d p_urec          s             80
     d p_stat          s              1
      *
      * Definering av Local data area (LDA)
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Datastruktur
      *
     d                 ds
     d d_urec                        80
     d  d_firm                        3  0 overlay(d_urec)
6.nu d  d_numm                        8  0 overlay(d_urec:4)
6.nu d  d_suff                        2  0 overlay(d_urec:12)
6.nu d  d_kode                        1    overlay(d_urec:14)
6.nu d  d_date                         d   overlay(d_urec:15) datfmt(*iso)
6.nu d  d_time                         t   overlay(d_urec:25) timfmt(*iso)
6.nu d  d_user                       10    overlay(d_urec:33)
6.nu d  d_wsid                       10    overlay(d_urec:43)
      *
      * Definering av variable i nøkler
      *
6.30 d lloglu_numm     s                   like(lmnumm)
6.30 d lloglu_suff     s                   like(lmsuff)
6.30 d lloglu_kode     s                   like(lmkode)
6.30 d lloglu_date     s                   like(lmdate)
6.30 d lloglu_time     s                   like(lmtime)
      *
      * Definering av variable
      *
     d w_firm          s                   like(lmfirm)
      *
      * --------------------------------------------------------------------
      * Henter og initierer parametre
      * --------------------------------------------------------------------
      *
     c                   eval      d_urec = p_urec
     c                   eval      p_stat = *on
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
      * Sjekker om transdato og klokkeslett følger transaksjonen
      * - legger inn systemdato og klokke hvis ikke utfylt.
      *
     c                   if        d_date = *loval
     c                   time                    d_date
     c                   endif
      *
     c                   if        d_time = *loval
     c                   time                    d_time
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdatering av logg - fil
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   exsr      oppr_logg
     c                   goto      avslutt
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting av transakajon i logg - file
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppr_logg     begsr
      *
      * Redigerer data og skriver transaksjon til logg - filen
      *
6.30 c                   eval      lloglu_numm = d_numm
6.30 c                   eval      lloglu_suff = d_suff
6.30 c                   eval      lloglu_kode = d_kode
6.30 c                   eval      lloglu_date = d_date
6.30 c                   eval      lloglu_time = d_time
6.30 c     lloglu_key    chain     lloglu
6.30 c                   if        not %found
      *
     c                   eval      lmfirm = w_firm
     c                   eval      lmnumm = d_numm
     c                   eval      lmsuff = d_suff
     c                   eval      lmkode = d_kode
     c                   eval      lmdate = d_date
     c                   eval      lmtime = d_time
     c                   eval      lmuser = l_user
     c                   eval      lmwsid = l_wsid
      *
     c                   write     lloglur
      *
6.30 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_stat
     c                   parm                    p_urec
6.30  *
6.30  *  Key for les av LOGG-HODE
6.30  *
6.30 c     lloglu_key    klist
6.30 c                   kfld                    w_firm
6.30 c                   kfld                    lloglu_numm
6.30 c                   kfld                    lloglu_suff
6.30 c                   kfld                    lloglu_kode
6.30 c                   kfld                    lloglu_date
6.30 c                   kfld                    lloglu_time
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
