     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RL401R                                        *
      * Beskrivelse . . : Sletting av leverandører                      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
T5.00 * V3.40     230403  Lev. lagt inn etter xx.xx.xx blir ikke slettet HFL  *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KA = F1  = Valg av firma                                  *
      *       KC = F3  = Exit                                           *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     frlevlu    uf   e           k disk    rename(rlevpfr:rlevlur)
     frlsolu    uf   e           k disk    rename(rlsopfr:rlsolur)
     frltrl1    if   e           k disk    rename(rltrpfr:rltrl1r)
     frl401p    o    e             printer oflind(*in30)
      *
      * Definering av variable i nøkler
      *
     d rlevlu_levr     s                   like(rllevr)
     d rlsolu_slev     s                   like(rlslev)
     d rltrl1_levr     s                   like(rllevr)
      *
      * Definering av Local data area
      *
     d                uds
      *
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d                 ds
     d w_dato                         6  0
     d  w_dag                         2  0 overlay(w_dato)
     d  w_mnd                         2  0 overlay(w_dato:3)
     d  w_aar                         2  0 overlay(w_dato:5)
      *
     d w_type          s              1
     d w_edat          s               d   datfmt(*iso)
T5.00d w_ndat          s               d   datfmt(*iso)
     d w_sdat          s               d   datfmt(*iso)
      *
     d w_fast          s              6  0
      *
     d w_tota          s              7  0
     d w_anta          s              5  0
     d w_firm          s                   like(l_firm)
     d w_levr          s                   like(rllevr)
     d w_slett         s              1
      *
      * Les kunderegister
      *
     c     rlevl1_key    setll     rlevl1                                                    .
     c     rlevl1_key    reade     rlevl1                                 90
      *
     c                   dow       *in90 = *off
      *
     c                   exsr      kontroll
      *
     c                   if        w_slett = ' '
      *
     c                   exsr      utskrift
      *
     c                   if        w_type  = 'N'
      *
     c                   eval      rlevlu_levr = rllevr
     c     rlevlu_key    chain     rlevlu                             90
     c                   if        *in90 = *off
     c                   delete    rlevlur
     c                   endif
      *
     c                   eval      rlsolu_slev = rllevr
     c     rlsolu_key    chain     rlsolu                             90
     c                   if        *in90 = *off
     c                   delete    rlsolur
     c                   endif
      *
     c                   endif
     c                   endif
      *
     c     rlevl1_key    reade     rlevl1                                 90
     c                   enddo
      *
     c     slutt         tag
      *
     c                   eval      *inlr = *on
     c                   eval      t1tota = w_tota
     c                   eval      t1anta = w_anta
      *
     c                   write     t1tot
      *
      * Call til subrutine for close av filer
      *
     c                   eval      w_slett = 'S'
     c                   eval      w_levr  = *zeros
     c                   call      'RL792C'
     c                   parm                    w_firm
     c                   parm                    rllevr
     c                   parm                    w_slett
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kontroll
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kontroll      begsr
      *
      * Tell opp antall kunder
      *
     c                   eval      w_tota = w_tota + 1
     c                   eval      w_slett = ' '
      *
      * Sjekk siste transaksjonsdato
      *
     c                   if        rlndat  > w_edat
     c                   eval      w_slett = 'X'
     c                   endif
      *
T5.00 * Sjekk Leverandør lagt inn dato
      *
T5.00c                   eval      w_sdat  = rlndat
      *
T5.00c                   if        w_sdat  > w_ndat
T5.00c                   eval      w_slett = 'X'
T5.00c                   endif
      *
      * Sjekk om saldo = 0
      *
     c                   if        rlsald <> 0
     c                   eval      w_slett = 'X'
     c                   endif
      *
      * Sjekk om det finnes posteringer på leverandør
      *
     c                   if        w_slett = *blank
     c                   eval      rltrl1_levr = rllevr
     c     rltrl1_key    chain     rltrl1                             91
     c                   if        *in91 = *off
     c                   eval      w_slett = 'X'
     c                   endif
     c                   endif
      *
      * Sjekk om det finnes bestilling på leverandør
      *
     c                   if        w_slett = *blank
     c                   eval      w_levr = rllevr
     c                   call      'RL792C'
     c                   parm                    w_firm
     c                   parm                    w_levr
     c                   parm                    w_slett
     c                   endif
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     utskrift      begsr
      *
      * Tell antall leverandører som kan/blir slettet
      *
     c                   eval      w_anta = w_anta + 1
      *
      * Sjekker om overflow
      *
     c                   if        *in30 = *on                                               .
     c                   write     a1hdr                                                     .
     c                   eval      *in30 = *off                                              .
     c                   endif                                                           .....
      *
     c                   write     d1det
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    w_type
     c                   parm                    w_edat
T5.00c                   parm                    w_ndat
      *
      * Key for leverandørregister
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppdatering av leverandør
      *
     c     rlevlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevlu_levr
      *
      * Key for oppdatering av leverandør-søketekst
      *
     c     rlsolu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlsolu_slev
      *
      * Key for oppslag på leverandørposter
      *
     c     rltrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rltrl1_levr
      *
     c                   eval      w_firm = l_firm
      *
     c                   if        w_type = 'J'
     c                   eval      *in11 = *on
     c                   else
     c                   eval      *in11 = *off
     c                   endif
      *
      * Skriv heading på første side
      *
     c                   write     a1hdr
      *
     c                   endsr
      *
