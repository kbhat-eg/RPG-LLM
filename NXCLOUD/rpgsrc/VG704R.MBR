     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : VG704R
      * Beskrivelse . . : Setter startverdi for glidende gj.sn. kostpris på alle lagervarer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       240119 ASK  Nytt program
      *
8.01  *PRG2   150622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     fvgkpiu    uf a e           k disk    rename(vgkpst:vgkpiur)
     fvgkpi1    if a e           k disk    rename(vgkpst:vgkpi1r)
     fvgkkir    if   e           k disk    rename(vgkkst:vgkkirr)
     fvvarl1    if a e           k disk    rename(vvarpfr:vvarl1r)
     fvg704d    cf   e             workstn
      *
      * Definering av parametre
      *
     d p_frec          s            300
     d p_firm          s                   like(vvfirm)
     d p_vare          s                   like(vvvare)
8.01 d p_prgr          s              2
     d p_lety          s              1  0
     d p_dato          s               d   datfmt(*iso)
     d p_enhe          s                   like(vvenhl)
     d p_sapr          s              9  2
     d p_gmpr          s              9  2
     d p_kopr          s              9  2
     d p_inpr          s              9  2
     d p_lage          s              2  0
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Datastruktur for kostprisfaktorer
      *
     d d_frec          ds
     d  d_fty1                       30
     d  d_fsi1                        1
     d  d_fak1                        8  6
     d  d_kto1                        6  0
     d  d_mot1                        6  0
     d  d_fty2                       30
     d  d_fsi2                        1
     d  d_fak2                        8  6
     d  d_kto2                        6  0
     d  d_mot2                        6  0
     d  d_fty3                       30
     d  d_fsi3                        1
     d  d_fak3                        8  6
     d  d_kto3                        6  0
     d  d_mot3                        6  0
     d  d_fty4                       30
     d  d_fsi4                        1
     d  d_fak4                        8  6
     d  d_kto4                        6  0
     d  d_mot4                        6  0
     d  d_fty5                       30
     d  d_fsi5                        1
     d  d_fak5                        8  6
     d  d_kto5                        6  0
     d  d_mot5                        6  0
      *
      * Definering av variable i nøkler
      *
     d vgkpiu_lage     s              2  0
     d vgkpiu_hkod     s                   like(vghkod)
     d vgkpiu_vare     s                   like(vgvare)
     d vgkpi1_lage     s              2  0
     d vgkpi1_hkod     s                   like(vghkod)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vvfirm)
     d w_inkp          s              9  2
     d w_nykp          s              9  2
     d w_sign          s              1  0
     d w_til1          s              9  2
     d w_til2          s              9  2
     d w_til3          s              9  2
     d w_til4          s              9  2
     d w_til5          s              9  2
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram - Sette startverdi kostpriser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Behandling av parameterbilde
      *
     c     c2taga        tag
      *
     c                   if        vgprlg = 'N'
     c                   eval      *in40 = *on
     c                   endif
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl
     c                   goto      avslutt
     c                   endif
      *
     c                   if        *inkd
     c                   exsr      sok_lager
     c                   goto      c2taga
     c                   endif
      *
     c                   eval      vgkpiu_lage = c2lage
     c                   eval      vgkpi1_lage = c2lage
      *
      * Validering av input
      *
     c                   if        c2slta <> 'J'
     c                   eval      *in41 = *off
     c                   eval      *in42 = *off
      *
      * Sjekk startdato
      *
     c                   if        c2sdat = 0
     c                   eval      *in41 = *on
     c                   exsr      xc1msg
     c                   goto      c2taga
     c                   endif
      *
      * Sjekk priskode
      *
     c                   if        c2kprk = *blank
     c                   eval      *in42 = *on
     c                   exsr      xc1msg
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
      * Oppdatering
      *
     c                   if        c2slta <> 'J'
     c                   exsr      oppd_gkpris
     c                   else
     c                   exsr      slett_alle
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for feilmeldinger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine innlegging av startverd
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_gkpris   begsr
      *
     c     vvarl1_key    setll     vvarl1
     c     vvarl1_key    reade     vvarl1
     c                   dow       not %eof
      *
      * Sjekk om lagervare
      *
     c                   if        vvvtyp <> 'L'
     c                   goto      les_neste
     c                   endif
      *
      * Finn aktuell pris i prisregister
      *
     c                   eval      p_firm = w_firm
     c                   eval      p_vare = vvvare
     c                   eval      p_enhe = vvenhl
     c                   eval      p_prgr = *blank
     c                   eval      p_lety = 0
     c                   eval      p_lage = c2lage
     c                   eval      w_inkp = 0
      *
     c                   call      'VP730R'
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_prgr
     c                   parm                    p_lety
     c                   parm                    p_dato
     c                   parm                    p_enhe
     c                   parm                    p_sapr
     c                   parm                    p_gmpr
     c                   parm                    p_kopr
     c                   parm                    p_inpr
     c                   parm                    p_lage
      *
     c                   if        c2kprk = 'I'
     c                   eval      w_inkp = p_inpr
     c                   else
     c                   eval      w_inkp = p_kopr
     c                   endif
      *
      * Hent kostprisfaktorer
      *
     c                   eval      p_frec = *blank
      *
     c                   call      'VG703R'
     c                   parm                    vvvare
     c                   parm                    c2lage
     c                   parm                    vvldor
     c                   parm                    p_frec
     c                   eval       d_frec = p_frec
      *
     c                   exsr      finn_kostt
     c                   eval      w_nykp = w_inkp + w_til1 + w_til2 + w_til3
     c                                             + w_til4 + w_til5
      *
      * Oppdater kostprisregister
      *
     c                   eval      vgkpiu_hkod = *blank
     c                   movel     vvvare        vgkpiu_vare
     c     vgkpiu_key    chain     vgkpiu
      *
     c                   if        %found
     c                   delete    vgkpiur
     c                   end
      *
     c                   clear                   vgkpiur
      *
      * Sjekker om det skal legges kostpris pr lager
      *
     c                   if        w_nykp = 0
     c                   goto      les_neste
     c                   endif
      *
     c                   if        vgprlg = 'N'
     c                   eval      vglage = 0
     c                   else
     c                   eval      vglage = c2lage
     c                   endif
      *
     c                   eval      vgfirm = w_firm
     c                   eval      vghkod = ' '
     c                   movel     vvvare        vgvare
     c     *dmy          move      c2sdat        vggdat
     c                   time                    vggtim
     c                   eval      vgenhe = vvenhl
     c                   eval      vgtype = 'S'
     c                   eval      vgcost = w_nykp
      *
     c                   time                    vgttim
     c                   time                    vgbdat
     c                   eval      vgbusr = l_user
      *
     c                   write     vgkpiur
      *
     c     les_neste     tag
     c     vvarl1_key    reade     vvarl1
     c                   enddo
      *
     c     end_gkpris    endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine sletting av alle startverdier
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     slett_alle    begsr
      *
     c                   eval      vgkpi1_hkod = ' '
     c     vgkpi1_key    setll     vgkpi1
     c     vgkpi1_key    reade     vgkpi1
     c                   dow       not %eof
      *
     c                   eval      vgkpiu_hkod = vghkod
     c                   eval      vgkpiu_vare = vgvare
     c     vgkpiu_key    chain     vgkpiu
      *
     c                   if        %found and
     c                             vgtype = 'S'
     c                   delete    vgkpiur
     c                   end
      *
     c     vgkpi1_key    reade     vgkpi1
     c                   enddo
      *
     c     end_alle      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å beregne kostpris tillegg og fradrag
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_kostt    begsr
      *
     c                   eval      w_til1 = 0
     c                   eval      w_til2 = 0
     c                   eval      w_til3 = 0
     c                   eval      w_til4 = 0
     c                   eval      w_til5 = 0
      *
     c                   if        d_fty1 <> *blank
     c                   if        d_fsi1 = '-'
     c                   eval      w_sign = -1
     c                   else
     c                   eval      w_sign = 1
     c                   endif
     c                   eval(h)   w_til1  = ((w_inkp * d_fak1)/100) * w_sign
     c                   endif
      *
     c                   if        d_fty2 <> *blank
     c                   if        d_fsi2 = '-'
     c                   eval      w_sign = -1
     c                   else
     c                   eval      w_sign = 1
     c                   endif
     c                   eval(h)   w_til2  = ((w_inkp * d_fak2)/100) * w_sign
     c                   endif
      *
     c                   if        d_fty3 <> *blank
     c                   if        d_fsi3 = '-'
     c                   eval      w_sign = -1
     c                   else
     c                   eval      w_sign = 1
     c                   endif
     c                   eval(h)   w_til3  = ((w_inkp * d_fak3)/100) * w_sign
     c                   endif
      *
     c                   if        d_fty4 <> *blank
     c                   if        d_fsi4 = '-'
     c                   eval      w_sign = -1
     c                   else
     c                   eval      w_sign = 1
     c                   endif
     c                   eval(h)   w_til4  = ((w_inkp * d_fak4)/100) * w_sign
     c                   endif
      *
     c                   if        d_fty5 <> *blank
     c                   if        d_fsi5 = '-'
     c                   eval      w_sign = -1
     c                   else
     c                   eval      w_sign = 1
     c                   endif
     c                   eval(h)   w_til5  = ((w_inkp * d_fak5)/100) * w_sign
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å søke på lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sok_lager     begsr
      *
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    c2lage
     c                   parm                    c2lana
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for les av vareregister
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppdatering av kostpris
      *
     c     vgkpiu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vgkpiu_lage
     c                   kfld                    vgkpiu_hkod
     c                   kfld                    vgkpiu_vare
      *
      * Key for les av kostpris
      *
     c     vgkpi1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vgkpi1_lage
     c                   kfld                    vgkpi1_hkod
      *
      * Key for kostpris parameter
      *
     c     vgkkir_key    klist
     c                   kfld                    w_firm
      *
     c                   eval      w_firm = l_firm
      *
      * Slår opp i koderegister og henter standardverdier
      *
     c     vgkkir_key    chain     vgkkir
      *
     c                   endsr
