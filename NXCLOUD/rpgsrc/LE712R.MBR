      /TITLE  ASLAGR: Lagertelling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LE712R
      *  Beskrivelse . . : Oppdatering av lager-register utfra lagerbok
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      *  --------- ------  -------------------------------------------------
      * 6.10  260609 BSA  Oppdatert med sporingsinformasjon
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvhislu    uf   e           k disk    rename(vhispfr:vhislur)
     fvlaglu    uf   e           k disk    rename(vlagpfr:vlaglur)
      *
      * Local Data Area
      *
     d                uds
6.10 d l_user                911    920
     d l_firm                944    946  0
      *
      *   Definering av variable i nøkler
      *
     d vhislu_dato     s                   like(vhdato)
     d vhislu_lage     s                   like(vhlage)
     d vhislu_time     s                   like(vhtime)
     d vhislu_vare     s                   like(vhvare)
     d vlaglu_lage     s                   like(vllage)
     d vlaglu_vare     s                   like(vlvare)
      *
      *   Definering av variable
      *
     d p_dato          s                   like(vhdato)
     d p_time          s                   like(vhtime)
     d p_vare          s                   like(vlvare)
     d p_lage          s                   like(vllage)
     d w_firm          s                   like(vlfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Behandle utplukk, Setter inn riktige parameterverdier
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     vhislu_key    setll     vhislur
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Leser poster fra register for utplukks-test
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     start         tag
      *
     c                   eval      *in90 = *off
     c                   read      vhislur                                90
      *
     c                   if        *in90 = *on
     c                   goto      end_pgm
     c                   endif
      *
      *  Sjekk   Er firmanummer innenfor grensene, Ellers hopp til Slutt
      *
     c                   if        vhfirm <> w_firm
     c                   goto      end_pgm
     c                   endif
      *
      *  Sjekk   Er varenummer innenfor grensene, Ellers hopp til slutt
      *
     c                   if        vhvare <> p_vare
     c                   goto      end_pgm
     c                   endif
      *
      *  Sjekk   Er lager riktig                , Ellers hopp til Start
      *
     c                   if        vhlage <> p_lage
     c                   goto      end_pgm
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Oppdaterer  LAGER-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      vlaglu_vare = vhvare
     c                   eval      vlaglu_lage = vhlage
     c     vlaglu_key    chain     vlaglur                            92
      *
      *  Alle transaksjoner fra historisk lagerbok, untatt tellinger,
      *  ETTER sluttidspunkt for telling blir tatt med.
      *
     c                   if        vhkode <> 'T'
     c                   if        vhtype = 'U'
     c                   eval      vlbehl = vlbehl - vhanta
     c                   eval      vltell = vltell - vhanta
     c                   else
     c                   eval      vlbehl = vlbehl + vhanta
     c                   eval      vltell = vltell + vhanta
     c                   endif
     c                   endif
      *
     c                   if        *in92 = *off
6.10 c                   time                    vledat
6.10 c                   time                    vletim
6.10 c                   eval      vleusr = l_user
     c                   update    vlaglur
     c                   endif
      *
     c                   eval      vhbehl = vlbehl
6.10 c                   time                    vhedat
6.10 c                   time                    vhetim
6.10 c                   eval      vheusr = l_user
     c                   update    vhislur
      *
      *  Velg neste record fra HISTORISK LAGERBOK
      *
     c                   goto      start
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for file : HISTORISK-LAGERBOK-REGISTER
      *
     c     vhislu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhislu_lage
     c                   kfld                    vhislu_vare
     c                   kfld                    vhislu_dato
     c                   kfld                    vhislu_time
      *
      *  Key for file : LAGER-REGISTER
      *
     c     vlaglu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlaglu_vare
     c                   kfld                    vlaglu_lage
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    p_dato
     c                   parm                    p_time
     c                   parm                    p_vare
     c                   parm                    p_lage
      *
      *  Legge inn startverdier i nøklene
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      vhislu_lage = p_lage
     c                   eval      vhislu_vare = p_vare
     c                   eval      vhislu_dato = p_dato
     c                   eval      vhislu_time = p_time
      *
     c                   endsr
