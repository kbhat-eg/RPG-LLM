     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JW510R
      * Beskrivelse . . : Vare-pakning, vise
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       180899 HKO  Nytt program
      *       020605 HKO  Lagt inn Bestillbar og PSE
      *       130905 HKO  Lagt inn DUNN-nr, sammensatt av enhet, antall i
      *                   pakning og opprettet dato
      *       290208 bof  Utvidet med modulnr. på forpakning
6.10  *       260809 bof  Endringer vedr. GTIN og fjerning av DUNN
6.11  *       100909 bof  Utvidet med pakningsklasse
6.12  *       290909 bof  Fjernet felter JVPKPF og lagt på JVPPPF
6.30  *       050314 bof  Utvidet nøkkel i jvpkpf og fjernet jvpppf
6.30  *                   + gtin og bestillbar
6.31  *       010414 bof  Utvidet nøkkel med prisgiver i bildet
6.31  *                   + fjernet valg 7
6.32  *       221014 bsa  Jsutert i skjermbildet pga jacada
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjvpklr    if   e           k disk    rename(jvpkpfr:jvpklrr)
     fjvpkl1    if   e           k disk    rename(jvpkpfr:jvpkl1r)
     fvenhl1    if   e           k disk    rename(venhpfr:venhl1r)
     fjforl5    if   e           k disk    rename(jforpfr:jforl5r)
     fjforl1    if   e           k disk    rename(jforpfr:jforl1r)
6.30 fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
      *
     fjw510d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(jzfirm)
     d p_vare          s                   like(jwvare)
6.30 d p_ldor          s                   like(jwldor)
      *
      * Definering av Local data area
      *
     d                uds
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variable i nøkler
      *
     d jvpklr_pakn     s                   like(jwpakn)
     d jvpkl1_pakn     s                   like(jwpakn)
     d jforl5_vare     s                   like(jzvare)
     d jforl1_ogrp     s                   like(jzogrp)
     d jforl1_hgrp     s                   like(jzhgrp)
     d jforl1_ugrp     s                   like(jzugrp)
     d jforl1_ldor     s                   like(jzldor)
     d jforl1_modn     s                   like(jzmodn)
     d jforl1_vare     s                   like(jzvare)
     d venhl1_eenh     s                   like(vaeenh)
6.30 d jlevl1_ldor     s                   like(jlldor)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
      *
     d w_firm          s                   like(jzfirm)
     d w_vare          s                   like(jwvare)
6.30 d w_ldor          s                   like(jwldor)
      *
     d s_enh1          s                   like(jzenh1)
     d s_enh2          s                   like(jzenh2)
     d s_enh3          s                   like(jzenh3)
     d s_eni1          s                   like(jzeni1)
     d s_eni2          s                   like(jzeni2)
     d s_eni3          s                   like(jzeni3)
     d s_enp1          s                   like(jzenp1)
     d s_enp2          s                   like(jzenp2)
     d s_enp3          s                   like(jzenp3)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(12)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c     jvpkl1_key    setll     jvpkl1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1vare = b2vare
     c                   eval      c1pakn = b1pakn
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
6.12  *
6.12 c*                  if        b1valg = 7
6.12 c*                  eval      c1pakn = b1pakn
6.12 c*                  call      'JW511R  '
6.12 c*                  parm                    w_firm
6.12 c*                  parm                    w_vare
6.12 c*                  parm                    c1pakn
6.12 c*                  eval      b1valg = 0
6.12 c*                  update    b1sfl
6.12 c*                  iter
6.12 c*                  endif
      *
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * C2BLD Behandle bilde for ny - endring - vise
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      jvpklr_pakn = b1pakn
     c     jvpklr_key    chain     jvpklr
     c                   if        not %found
     c                   leavesr
     c                   endif
      *
     c                   eval      c2enhe = jwenhe
     c                   eval      c2ofak = jwofak
     c                   eval      c2ofap = jwofap
     c                   eval      c2bred = jwbred
     c                   eval      c2leng = jwleng
     c                   eval      c2hoyd = jwhoyd
     c                   eval      c2vekt = jwvekt
     c                   eval      c2volu = jwvolu
     c                   eval      c2paks = jwpaks
     c                   eval      c2antp = jwantp
6.30 c                   eval      c2gtin = jwgtin
6.30 c                   eval      c2best = jwbest
     c                   eval      c2psen = jwpsen
     c                   eval      c2eusr = jweusr
      *
     c     *dmy          move      jwodat        c2odat
     c     *dmy          move      jwedat        c2edat
      *
      * Henter beskrivelse
      *
     c                   eval      c2etxt = *blank
      *
     c                   eval      venhl1_eenh = c2enhe
     c     venhl1_key    chain     venhl1
     c                   if        %found
     c                   eval      c2etxt = vaetxt
     c                   endif
      *
     c                   exfmt     c2bld
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Fyll opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      jvpkl1                                 15
      *
     c                   if        *in15
     c                   leave
     c                   endif
      *
     c                   if        jwvare <> w_vare
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1pakn = jwpakn
     c                   eval      b1enhe = jwenhe
     c                   eval      b1ofak = jwofak
     c                   eval      b1ofap = jwofap
     c                   eval      b1saen = 0
     c                   eval      b1inen = 0
     c                   eval      b1pben = 0
     c                   eval      b1psen = jwpsen
6.30 c                   eval      b1gtin = jwgtin
     c                   eval      b1rsts = *blank
      *
     c                   select
     c                   when      jwenhe = s_enh1
     c                   eval      b1saen = 1
     c                   when      jwenhe = s_enh2
     c                   eval      b1saen = 2
     c                   when      jwenhe = s_enh3
     c                   eval      b1saen = 3
     c                   endsl
      *
     c                   select
     c                   when      jwenhe = s_eni1
     c                   eval      b1inen = 1
     c                   when      jwenhe = s_eni2
     c                   eval      b1inen = 2
     c                   when      jwenhe = s_eni3
     c                   eval      b1inen = 3
     c                   endsl
      *
     c                   select
     c                   when      jwenhe = s_enp1
     c                   eval      b1pben = 1
     c                   when      jwenhe = s_enp2
     c                   eval      b1pben = 2
     c                   when      jwenhe = s_enp3
     c                   eval      b1pben = 3
     c                   endsl
      *
     c                   if        jwrsts = 1
     c                   eval      b1rsts = 'L'
     c                   endif
      *
     c                   write     b1sfl
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
6.30 c                   parm                    p_ldor
      *
      * Key for oppslag på vare
      *
     c     jvarl1_key    klist
     c                   kfld                    w_vare
      *
      * Key for oppslag på vare-pakning
      *
     c     jvpklr_key    klist
     c                   kfld                    w_vare
6.30 c                   kfld                    w_ldor
     c                   kfld                    jvpklr_pakn
      *
      * Key for posisjonering på vare-pakning
      *
     c     jvpkl1_key    klist
     c                   kfld                    w_vare
6.30 c                   kfld                    w_ldor
     c                   kfld                    jvpkl1_pakn
      *
      * Key for oppslag på forpakning m/varenummer
      *
     c     jforl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jforl5_vare
      *
      * Key for oppslag på forpakning
      *
     c     jforl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jforl1_ogrp
     c                   kfld                    jforl1_hgrp
     c                   kfld                    jforl1_ugrp
     c                   kfld                    jforl1_ldor
     c                   kfld                    jforl1_modn
     c                   kfld                    jforl1_vare
      *
      * Key for oppslag på enhet
      *
     c     venhl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    venhl1_eenh
6.30  *
6.30  * Key for oppslag på leverandør
6.30  *
6.30 c     jlevl1_key    klist
6.30 c                   kfld                    jlevl1_ldor
      *
      *
      * Henter firma og vare fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_vare = p_vare
6.30 c                   eval      w_ldor = p_ldor
      *
      * Henter vareinformasjon
      *
     c     jvarl1_key    chain     jvarl1
      *
      * Henter enheter fra forpakning
      *
     c                   eval      jforl5_vare = jvvare
     c     jforl5_key    chain     jforl5                             90
     c                   if        *in90 = *on
     c                   eval      jforl1_ogrp = jvogrp
     c                   eval      jforl1_hgrp = jvhgrp
     c                   eval      jforl1_ugrp = jvugrp
     c                   eval      jforl1_ldor = jvldor
     c                   eval      jforl1_modn = jvmodn
     c                   eval      jforl1_vare = *blank
     c     jforl1_key    chain     jforl1                             90
     c                   if        *in90 = *on
     c                   eval      jforl1_modn = 0
     c     jforl1_key    chain     jforl1                             90
     c                   if        *in90 = *on
     c                   eval      jforl1_ldor = 0
     c     jforl1_key    chain     jforl1                             90
     c                   if        *in90 = *on
     c                   eval      jforl1_ugrp = 0
     c     jforl1_key    chain     jforl1                             90
     c                   if        *in90 = *on
     c                   eval      jforl1_hgrp = 0
     c     jforl1_key    chain     jforl1                             90
     c                   endif
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
     c                   if        *in90 = *off
     c                   eval      s_enh1 = jzenh1
     c                   eval      s_enh2 = jzenh2
     c                   eval      s_enh3 = jzenh3
     c                   eval      s_eni1 = jzeni1
     c                   eval      s_eni2 = jzeni2
     c                   eval      s_eni3 = jzeni3
     c                   eval      s_enp1 = jzenp1
     c                   eval      s_enp2 = jzenp2
     c                   eval      s_enp3 = jzenp3
     c                   endif
      *
      * Initierer skjermbilde
      *
     c                   eval      b2vare = w_vare
     c                   eval      b2tek1 = jvtek1
6.30  *
6.30 c                   eval      jlevl1_ldor = p_ldor
6.30 c     jlevl1_key    chain     jlevl1                             90
6.30 c                   if        *in90 = *off
6.30 c                   eval      b2ldor = jlldor
6.30 c                   eval      b2lnav = jlnavn
6.30 c                   endif
      *
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og viser bilde
      *
     c     jvpkl1_key    setll     jvpkl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
