     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FR410R
      * Beskrivelse . . : Rabattmatrise, sletter rabattavtale for
      *                   rabattkategori/kunde
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       031202 HKO  Nytt program
      *       020703 HKO  Lagt inn rabattkategori
5.70  *       281008 bof  Utvidet med prisgruppe
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
5.70 ffrabld    uf   e           k disk    rename(frabpfr:frabldr)
     ffrabl3    uf   e           k disk    rename(frabpfr:frabl3r)
      *
      * Definering parametre
      *
     d p_firm          s                   like(frfirm)
     d p_rkat          s                   like(frrkat)
     d p_kund          s                   like(frkund)
      *
      * Definering av variabler i nøkler
      *
5.70 d frabld_rkat     s                   like(frrkat)
     d frabl3_kund     s                   like(frkund)
      *
      * Definering av variable
      *
     d w_firm          s                   like(frfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Sletter rabatter på rabattkategori
      *
     c                   if        p_rkat <> 0
5.70 c                   eval      frabld_rkat = p_rkat
5.70 c     frabld_key    setll     frabld
5.70 c     frabld_key    reade     frabldr
     c                   dow       not %eof
      *
5.70 c                   delete    frabldr
      *
5.70 c     frabld_key    reade     frabldr
     c                   enddo
     c                   endif
      *
      * Sletter rabatter på kunde
      *
     c                   if        p_kund <> 0
     c                   eval      frabl3_kund = p_kund
     c     frabl3_key    setll     frabl3
     c     frabl3_key    reade     frabl3r
     c                   dow       not %eof
      *
     c                   if        frkpro = 0
     c                   delete    frabl3r
     c                   endif
      *
     c     frabl3_key    reade     frabl3r
     c                   enddo
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametere
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_rkat
     c                   parm                    p_kund
      *
      * Key for oppdatering av rabattmatrise
      *
5.70 c     frabld_key    klist
     c                   kfld                    w_firm
5.70 c                   kfld                    frabld_rkat
      *
     c     frabl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    frabl3_kund
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
