     H decedit('0.') datedit(*dmy-) option(*nodebugio)
     h DftActgrp(*No) Actgrp(*NEW)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : NM726R
      * Beskrivelse . . : Eksport av åpne ordre med kilde fra Blueridge
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       060422 bsa  Nytt program
8.01  *       200522 bsa  Justeringer etter test.
8.02  *       200522 bsa  Endret logikken på nivå vi legger ut poster
8.03  *       050922 bsa  Regner om til salgsenhet.
      *
      *
9.xx  * 9.xx  280222 bsa  Div test
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flotii2    uf   e           k disk    rename(lotist:lotii2r)
     flotii3    uf   e           k disk    rename(lotist:lotii3r)
8.02 flotiir    uf   e           k disk    rename(lotist:lotiirr)
     flfhelr    if   e           k disk    rename(lfhepfr:lfhelrr)
     flfdtlr    if   e           k disk    rename(lfdtpfr:lfdtlrr)
     flohelr    if   e           k disk    rename(lohepfr:lohelrr)
     flodtlr    if   e           k disk    rename(lodtpfr:lodtlrr)
     fra10l1    if   e           k disk    rename(ra10pfr:ra10l1r)
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     fbrooiu    uf a e           k disk    rename(broost:brooiur)
8.03 fvvenlr    if   e           k disk    rename(vvenpfr:vvenlrr)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_filg                937    939
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d brooiu_omem     s                   like(boomem)
     d brooiu_oref     s                   like(booref)
8.02 d brooiu_ooid     s                   like(boooid)
     d brooiu_ovar     s                   like(boovar)
     d brooiu_olag     s                   like(boolag)
     d lohelr_numm     s                   like(lonumm)
     d lohelr_suff     s                   like(losuff)
     d lodtlr_numm     s                   like(ldnumm)
     d lodtlr_suff     s                   like(ldsuff)
     d lodtlr_line     s                   like(ldline)
     d ra10l1_jkod     s                   like(rajkod)
     d lfdtlr_numm     s                   like(lfnumm)
     d lfdtlr_line     s                   like(lfline)
     d vvarl1_vare     s                   like(vvvare)
     d vlagl1_lage     s                   like(vllage)
     d vlagl1_vare     s                   like(vlvare)
     d rlevl1_levr     s                   like(rllevr)
     d lotii2_tnum     s                   like(lotnum)
     d lotii2_tsuf     s                   like(lotsuf)
     d lotii3_tfor     s                   like(lotfor)
8.02 d lotiir_tref     s                   like(lotref)
     d votyl1_otyp     s                   like(vaotyp)
8.02 d lfhelr_numm     s                   like(lhnumm)
8.03 d vvenlr_vare     s                   like(vevare)
8.03 d vvenlr_enhe     s                   like(veenhe)
      *
      * Definering av datastrukturer
      *
      *
      * Definering av parametere
      *
     d p_memb          s             10
      *
      * Definering av variable
      *
     d w_kode          s              1
     d w_fell          s              6
     d w_type          s              2
     d w_fast          s             10
     d w_numm          s              8  0
     d w_size          s              8  0
     d w_firm          s                   like(lffirm)
     d w_inlr          s              1
     d w_memb          s             10
8.03 d w_omrl          s                   like(veomre)
8.03 d w_omrs          s                   like(veomre)
8.03 d w_senh          s                   like(veenhe)
     d w_anta          s             11  3
     d w_ldor          s              6  0
     d w_ltxt          s             30
8.01 d w_lagt          s             30
8.01 d w_laid          s              5
     d w_code          s              1
     d w_ovrf          s              1
     d w_date          s               d   datfmt(*iso)
      *
     d b_blue          s              1    inz(*off)
     d b_ordr          s              1    inz(*off)
     d b_best          s              1    inz(*off)
     d b_pakk          s              1    inz(*off)
     d b_fakt          s              1    inz(*off)
      *
     c/Exec SQL
     c+  Set Option DatFmt=*ISO , commit=*none ,
     c+  DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR
     c/End-Exec
      *
      *-----------------------------------------------------------------*
      * Hovedprogram:                                                   *
      *   Henter inn forventet bestillingsinformasjon. Det gjør at vi   *
      *   må sjekke flere tabeller for å finne resultatet. NB Det er    *
      *   samme ID som må følge Blueridge ID fra NX. Det betyr at selv  *
      *   om bestillingsforslaget har blitt bestilling, sendes ID som   *
      *   bestillingsforslag.                                           *
      *   1 - Hent alle bestillingsforslag med Blueridge opphav         *
      *   2 - Hent alle bestillinger med Blueridge opphav               *
      *                                                                 *
      *-----------------------------------------------------------------*
      *
      * Henter inn løpenummer teller
      *
     c                   if        p_memb = *blanks
     c                   eval      w_kode = '0'
     c                   eval      w_fell = 'NXTKLI'
     c                   eval      w_type = *blank
     c                   eval      w_fast = *blank
     c                   eval      w_numm = *zero
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
     c                   parm                    w_numm
      *
     c                   eval      w_memb = 'AA' + %char(w_numm)
     c                   eval      brooiu_omem = w_memb
     c                   eval      p_memb = w_memb
     c                   else
     c                   eval      brooiu_omem = p_memb
     c                   endif
      *
      * Hent info fra bestillingsforslag
      *
     c                   exsr      hent_bfors
      *
      * Hent gårsdagens bonger basert på gårsdagens bonger.
      *
     c                   exsr      hent_best
      *
      * Tar en runde for å sjekke om enkelte bestillinger eller bestillingsforslag
      * ikke er behandlet. Disse må merkes for oppdatert.
      *
     c                   exsr      mark_loti
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hent bestillinger med Bluebridge opphav
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_bfors    begsr
      *
      * Nullstiller teller for antall varer funnet
      *
     c                   eval      w_size = 0
     c                   eval      w_ovrf = *blanks
      *
      * Så finner vi alle bestillingsforslag fra Blueridge.
      *
     c                   eval      *in15 = *off
      *
     c/exec sql
     c+                  declare sok cursor for
     c+                  select lhfirm,
     c+                         lhnumm,
     c+                         lhotyp,
     c+                         lhlage,
     c+                         lhovrf,
     c+                         lhbdat,
     c+                         lhldat,
     c+                         lhldor,
     c+                         lhoppr
     c+                  from   lfhepf
     c+                  where  lhfirm = :w_firm and
     c+                         lhovrf = :w_ovrf and
     c+                         lhoppr = :w_code
     c+                  order by lhfirm, lhnumm
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok
     c/end-exec
     c/exec sql
     c+                  open sok
     c/end-exec
      *
     c                   dow       *in15 = *off
      *
     c/exec sql
     c+                  fetch next
     c+                  from  sok
     c+                  into  :lhfirm,
     c+                        :lhnumm,
     c+                        :lhotyp,
     c+                        :lhlage,
     c+                        :lhovrf,
     c+                        :lhbdat,
     c+                        :lhldat,
     c+                        :lhldor,
     c+                        :lhoppr
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   if        *in15
     c                   leave
     c                   endif
      *
      * Finnes bestillingsforslag med opphav i Blueridge ?
      *
     c                   eval      lotii3_tfor = lhnumm
     c     lotii3_key    chain     lotii3
     c                   if        %found and
     c                             lotnum = 0 and
     c                             lotko1 = 'B'
     c                   eval      b_ordr = *on
     c                   time                    loteda
     c                   time                    loteti
     c                   eval      loteus = l_user
     c                   update    lotii3r
     c                   else
     c                   unlock    lotii3
     c                   endif
      *
     c                   if        b_ordr = *on
      *
      * Hent leverandørinfo
      *
     c                   eval      w_ldor = lhldor
     c                   exsr      hent_rlev
      *
      * Les linjer fra bestillingsforslag
      *
     c                   eval      lfdtlr_numm = lhnumm
     c                   eval      lfdtlr_line = 0
     c     lfdtlr_key    setll     lfdtlr
     c     lfdtlr_key2   reade     lfdtlr
     c                   dow       not %eof
      * Ikke tekstlinjer
     c                   if        %trim(lfvare) <> *blanks and
     c                             lfanta <> 0
      *
      * Sjekk om det er EVR vare
      *
     c                   eval      vvarl1_vare = lfvare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   goto      ny_blinje
     c                   endif
9.xx  *
9.xx c**                 if        lflage = 15 and
9.xx c**                           %trim(lfvare) = '26925164'
9.xx c**                 goto      nest_xx
9.xx c**                 endif
9.xx c**   nest_xx       tag
      *
      * Sjekk om det er lagervare
      *
     c                   eval      vlagl1_lage = lflage
     c                   eval      vlagl1_vare = lfvare
     c     vlagl1_key    chain     vlagl1
     c                   if        not %found or
     c                             vlvtyp <> 'L'
     c                   goto      ny_blinje
     c                   endif
      *
      * Finner salgsenhet 1, og sjekker om samme. Hvis ikke må det regnes om.
      * Bruker antall og lager fra bestillingsforslaget
      *
     c                   eval      w_anta = 0
8.03 c                   eval      w_senh = *blanks
8.03 c                   eval      w_omrs = 0
8.03 c                   eval      w_omrl = 0
8.03 c                   eval      vvenlr_vare = lfvare
8.03 c                   eval      vvenlr_enhe = *blanks
8.03 c     vvenlr_key    setll     vvenlr
8.03 c     vvenlr_key2   reade     vvenlr
8.03 c                   dow       not %eof
8.03 c                   if        vesaen = 1
8.03 c                   eval      w_omrs = veomre
8.03 c                   eval      w_senh = veenhe
8.03 c                   endif
8.03 c                   if        lfenh1 = veenhe
8.03 c                   eval      w_omrl = veomre
8.03 c                   endif
8.03 c     vvenlr_key2   reade     vvenlr
8.03 c                   enddo
      *
8.03 c                   if        w_omrs = 0
8.03 c                   eval      w_omrs = 1
8.03 c                   endif
8.03 c                   if        w_omrl = 0
8.03 c                   eval      w_omrl = 1
8.03 c                   endif
      *
8.03 c                   eval      w_anta = lfanta
8.03 c                   if        lfenh1 <> w_senh
8.03 c                   eval      w_anta = w_anta * w_omrl / w_omrs
8.03 c                   endif
      *
      * Hent info fra lager
      *
8.01 c                   eval      w_lagt = *blanks
8.01 c                   eval      w_laid = *blanks
     c                   eval      ra10l1_jkod = lflage
     c     ra10l1_key    chain     ra10l1
     c                   if        %found
8.01 c                   eval      w_lagt = rajtxt
     c                   endif
8.01 c                   eval      w_laid = l_filg+ %char(lflage)
      *
      * Kan være samme vare er plukket med to ulike prefiks. Denne akkumuleres
      * inn på samme transaksjon.
      *
     c                   eval      brooiu_olag = lflage
     c                   eval      brooiu_ovar = lfvare
     c                   eval      brooiu_oref = lotref
8.02 c                   eval      brooiu_ooid = %char(lfnumm)
     c     brooiu_key    chain     brooiu
     c                   if        not %found
      *
     c                   clear                   brooiur
     c                   eval      boomem = brooiu_omem
     c                   eval      boovar = brooiu_ovar
     c                   eval      boolag = brooiu_olag
     c                   eval      booref = brooiu_oref
8.02 c                   eval      boooid = brooiu_ooid
8.02 c                   eval      w_size = w_size + 1
8.02 c                   eval      boolin = w_size
     c                   eval      booldo = w_ldor
     c                   eval      booldt = w_ltxt
8.01 c                   eval      booltx = w_lagt
8.01 c                   eval      boolid = w_laid
     c                   eval      booosu = 0
      *
     c                   eval      boovtx = %trim(lftek1 + lftek2)
     c                   eval      boovar = lfvare
     c                   eval      boobqt = boobqt + w_anta
     c**                 eval      boolqt = boolqt + w_anta
8.03 c**                 eval      booenh = lfenh1
8.03 c                   eval      booenh = w_senh
     c                   eval      booord = lhbdat
     c                   eval      boolda = lhldat
     c                   if        lfldat <> *loval
     c                   eval      boolda = lfldat
     c                   endif
      *
     c                   endif
      *
     c                   time                    boooda
     c                   time                    boooti
     c                   eval      booous = l_user
      *
     c                   if        not %found(brooiu)
     c                   write     brooiur
     c                   else
     c                   update    brooiur
     c                   endif
      *
     c                   endif
      *
     c     ny_blinje     tag
      *
     c     lfdtlr_key2   reade     lfdtlr
     c                   enddo
      *
     c                   endif
      *
     c                   enddo
      *
     c     besf_slutt    tag
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Finner bestillinger med Bluebridge opphav
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_best     begsr
      *
      * Les linjer fra tilleggstabell
      *
     c                   eval      lotii2_tnum = 1
     c                   eval      lotii2_tsuf = 0
     c     lotii2_key    setll     lotii2
     c                   read      lotii2
     c                   dow       not %eof
      *
      * Finnes denne i bestillingsregistret ?
      *
     c                   eval      b_blue = *off
     c                   eval      b_ordr = *off
     c                   eval      b_best = *off
     c                   eval      b_pakk = *off
     c                   eval      b_fakt = *off
      *
9.xx c**                 if        lotref = '587506'
      *
      * Hvis oppføringen av en eller annen grunn er merket ferdig
      *
     c                   if        lotsta = 0 and
     c                             lotko1 = 'B'
      *
     c                   eval      lohelr_numm = lotnum
     c                   eval      lohelr_suff = 0
     c     lohelr_key    setll     lohelr
     c     lohelr_key2   reade     lohelr
     c                   dow       not %eof
      *
     c                   eval      b_ordr = *on
      *
      * Riktig nivå på ordretype ??
      *
     c                   eval      votyl1_otyp = lootyp
     c     votyl1_key    chain     votyl1
     c                   if        %found
     c                   eval      b_ordr = *on
      * Hvis kommet til faktura, skal den merkes som det, og behandles spes.
     c                   if        vaoakk > 2
     c                   eval      b_ordr = *off
     c                   eval      b_fakt = *on
     c                   endif
      * Hvis negativ ordretype, skal den ikke behandles (vareretur)
     c                   if        vaokre = '-'
     c                   eval      b_ordr = *off
     c                   eval      b_fakt = *off
     c                   endif
      *
     c                   endif
      *
     c                   if        b_ordr = *on
      *
      * Skille mellom bestilinger som er varemottatt og andre.
      *
     c                   if        vaoakk = 1
     c                   eval      b_best = *on
     c                   endif
     c                   if        vaoakk = 2
     c                   eval      b_pakk = *on
     c                   endif
      *
      * Hent info fra lager
      *
8.01 c                   eval      w_lagt = *blanks
8.01 c                   eval      w_laid = *blanks
     c                   eval      ra10l1_jkod = lolage
     c     ra10l1_key    chain     ra10l1
     c                   if        %found
8.01 c                   eval      w_lagt = rajtxt
     c                   endif
8.01 c                   eval      w_laid = l_filg+ %char(lolage)
      *
      * Hent leverandørinfo
      *
     c                   eval      w_ldor = loldor
     c                   exsr      hent_rlev
      *
      * Les linjer fra bestillingslinjer
      *
     c                   eval      lodtlr_numm = lonumm
     c                   eval      lodtlr_suff = losuff
     c                   eval      lodtlr_line = 0
     c     lodtlr_key    setll     lodtlr
     c     lodtlr_key2   reade     lodtlr
     c                   dow       not %eof
      * Ikke tekstlinjer
     c                   if        %trim(ldvare) <> *blanks and
     c                             ldanta <> 0
      *
      * Sjekk om det er EVR vare
      *
     c                   eval      vvarl1_vare = ldvare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   goto      ny_bestl
     c                   endif
9.xx  *
9.xx c**                 if        lflage = 15 and
9.xx c**                           %trim(ldvare) = '26925164'
9.xx c**                 goto      nest_xx
9.xx c**                 endif
9.xx c**   nest_xx       tag
      *
      * Sjekk om det er lagervare
      *
     c                   eval      vlagl1_lage = lolage
     c                   eval      vlagl1_vare = ldvare
     c     vlagl1_key    chain     vlagl1
     c                   if        not %found or
     c                             vlvtyp <> 'L'
     c                   goto      ny_bestl
     c                   endif
      *
      * Finner salgsenhet 1, og sjekker om samme. Hvis ikke må det regnes om.
      * Bruker antall og lager fra bestilling.
      *
     c                   eval      w_anta = 0
8.03 c                   eval      w_senh = *blanks
8.03 c                   eval      w_omrs = 0
8.03 c                   eval      w_omrl = 0
8.03 c                   eval      vvenlr_vare = ldvare
8.03 c                   eval      vvenlr_enhe = *blanks
8.03 c     vvenlr_key    setll     vvenlr
8.03 c     vvenlr_key2   reade     vvenlr
8.03 c                   dow       not %eof
8.03 c                   if        vesaen = 1
8.03 c                   eval      w_omrs = veomre
8.03 c                   eval      w_senh = veenhe
8.03 c                   endif
8.03 c                   if        ldenh1 = veenhe
8.03 c                   eval      w_omrl = veomre
8.03 c                   endif
8.03 c     vvenlr_key2   reade     vvenlr
8.03 c                   enddo
      *
8.03 c                   if        w_omrs = 0
8.03 c                   eval      w_omrs = 1
8.03 c                   endif
8.03 c                   if        w_omrl = 0
8.03 c                   eval      w_omrl = 1
8.03 c                   endif
      *
     c                   eval      w_anta = ldanta
8.03 c                   if        ldenh1 <> w_senh
8.03 c                   eval      w_anta = w_anta * w_omrl / w_omrs
8.03 c                   endif
      *
      * Kan være samme vare er plukket med to ulike prefiks. Denne akkumuleres
      * inn på samme transaksjon.
      *
     c                   eval      brooiu_olag = lolage
     c                   eval      brooiu_ovar = ldvare
     c                   eval      brooiu_oref = lotref
8.02 c                   if        lotfor = 0
8.02 c                   eval      brooiu_ooid = %char(lonumm)+'-'+%char(ldline)
8.02 c                   else
8.02 c                   eval      brooiu_ooid = %char(lotfor)
8.02 c                   endif
     c     brooiu_key    chain     brooiu
     c                   if        not %found
      *
     c                   clear                   brooiur
     c                   eval      boomem = brooiu_omem
     c                   eval      boovar = brooiu_ovar
     c                   eval      boolag = brooiu_olag
     c                   eval      booref = brooiu_oref
8.02 c                   eval      boooid = brooiu_ooid
     c                   eval      booldo = w_ldor
     c                   eval      booldt = w_ltxt
     c                   eval      booosu = losuff
     c                   eval      boovar = ldvare
8.03 c**                 eval      booenh = ldenh1
8.03 c                   eval      booenh = w_senh
     c                   eval      boovtx = %trim(ldtek1 + ldtek2)
8.01 c                   eval      booltx = w_lagt
8.01 c                   eval      boolid = w_laid
8.02 c                   eval      w_size = w_size + 1
8.02 c                   eval      boolin = w_size
8.02 c                   eval      b_blue = *on
     c                   endif
      *
8.02 c**                 eval      boobqt = boobqt + w_anta
8.02 c                   eval      boobqt = w_anta
     c                   if        b_pakk = *on
     c                   eval      boolqt = boolqt + w_anta
     c                   endif
     c                   eval      booord = lobdat
     c                   eval      boolda = loldat
     c                   if        ldldat <> *loval
     c                   eval      boolda = ldldat
     c                   endif
      *
     c                   time                    boooda
     c                   time                    boooti
     c                   eval      booous = l_user
      *
     c                   if        not %found(brooiu)
     c                   write     brooiur
     c                   else
     c                   update    brooiur
     c                   endif
      *
     c                   endif
      *
     c     ny_bestl      tag
      *
     c     lodtlr_key2   reade     lodtlr
     c                   enddo
      *
      *  Ordretype ikke gyldig
      *
     c                   endif
      *
      * Les neste bestillingshode
      *
     c     lohelr_key2   reade     lohelr
     c                   enddo
      *
      * Les neste tilleggsregister oppføring
      *
     c                   endif
      *
      * Tar en runde for å sjekke om bestillingen er fakturert. Da må den
      * merkes spesielt. Hvordan ??
      * Avventer funksjonalitet.
      *
     c                   if        b_blue = *off
     c*                  exsr      mark_bestf
     c                   endif
      *
      * Oppdater Blueridge overføring slik at vi kan finne
      *
     c                   if        b_blue = *on
     c                   time                    loteda
     c                   time                    loteti
     c                   eval      loteus = l_user
     c                   update    lotii2r
     c                   else
     c                   unlock    lotii2
     c                   endif
      *
9.xx c**                 endif
      *
     c                   read      lotii2
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter leverandørinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_rlev     begsr
      *
      * Slå opp mot leveradør for å hente inn land/valutakode
      *
     c                   eval      rlevl1_levr = w_ldor
     c     rlevl1_key    chain     rlevl1
     c                   if        %found
     c                   eval      w_ltxt = rlnavn
     c                   endif
      *
     c                   endsr
      *
8.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.02  * Merker bestillinger i LOTIST med ferdig, hvis bestilling er
8.02  * plukket til fakturering eller slettet.
8.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.02  *
8.02 c     mark_loti     begsr
8.02  *
8.02  * Les hele LOTIST tabellen, og merk de vi ikke finner som avsluttet
8.02  *
8.02 c                   eval      lotiir_tref = *blanks
8.02 c     lotiir_key    setll     lotiir
8.02 c                   read      lotiir
8.02 c                   dow       not %eof
8.02 c                   if        lotsta = 0
8.02  * Er det kun bestillingsforslag ? (Er det slettet ?)
8.02 c                   if        lotfor <> 0 and
8.02 c                             lotnum = 0
8.02 c                   eval      lfhelr_numm = lotfor
8.02 c     lfhelr_key    chain     lfhelr
8.02 c                   if        not %found
8.02 c                   eval      lotsta = 1
8.02 c                   update    lotiirr
8.02 c                   endif
8.02  * Er det kun bestilling. (Er det slettet - eller ordretypen feil ?
8.02 c                   elseif    lotfor = 0 and
8.02 c                             lotnum <> 0
8.02 c                   eval      lohelr_numm = lotnum
8.02 c     lohelr_key    chain     lohelr
8.02 c                   if        not %found
8.02 c                   eval      lotsta = 1
8.02 c                   update    lotiirr
8.02 c                   else
8.02 c                   eval      votyl1_otyp = lootyp
8.02 c     votyl1_key    chain     votyl1
8.02 c                   if        %found and
8.02 c                             vaoakk  > 2
8.02 c                   eval      lotsta = 1
8.02 c                   update    lotiirr
8.02 c                   endif
8.02 c                   endif
8.02 c                   endif
8.02 c                   else
8.02 c                   unlock    lotiir
8.02 c                   endif
8.02 c                   read      lotiir
8.02 c                   enddo
8.02  *
8.02 c                   endsr
8.02  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ERROR-rutine  - Brukes ved mislykket web-service kall
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *pssr         begsr
      *
     c**                 eval      p_link = 'Blueridge eksport feilet'
     c                   goto      avslutt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Henter inn parameter
      *
     c     *entry        plist
     c                   parm                    p_memb
      *
      * Key for info fra blueridge export
      *
     c     brooiu_key    klist
     c                   kfld                    brooiu_omem
     c                   kfld                    brooiu_oref
8.02 c                   kfld                    brooiu_ooid
     c                   kfld                    brooiu_olag
     c                   kfld                    brooiu_ovar
      *
      * Key for info fra bestilling tilleggsregister
      *
     c     lotii2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lotii2_tnum
     c                   kfld                    lotii2_tsuf
      *
      * Key for info fra bestilling tilleggsregister
      *
     c     lotii3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lotii3_tfor
8.02  *
8.02  * Key for info fra bestilling tilleggsregister
8.02  *
8.02 c     lotiir_key    klist
8.02 c                   kfld                    w_firm
8.02 c                   kfld                    lotiir_tref
      *
      * Key for les på bestillingslinje
      *
     c     lodtlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlr_numm
     c                   kfld                    lodtlr_suff
     c                   kfld                    lodtlr_line
      *
      * Key for les på bestillingslinje
      *
     c     lodtlr_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlr_numm
     c                   kfld                    lodtlr_suff
      *
      * Key for les på bestillingshode
      *
     c     lohelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelr_numm
     c                   kfld                    lohelr_suff
      *
      * Key for les på bestillingshode
      *
     c     lohelr_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    lohelr_numm
8.02  *
8.02  * Key for les på bestillingsforslag linje
8.02  *
8.02 c     lfhelr_key    klist
8.02 c                   kfld                    w_firm
8.02 c                   kfld                    lfhelr_numm
      *
      * Key for les på bestillingsforslag linje
      *
     c     lfdtlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lfdtlr_numm
     c                   kfld                    lfdtlr_line
      *
      * Key for les på bestillingsforslag linje
      *
     c     lfdtlr_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    lfdtlr_numm
      *
      * Key for les på lager
      *
     c     ra10l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra10l1_jkod
      *
      * Key for info fra ordretype
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for info fra leverandørregister (prisgriver)
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for les på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for les på lager
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl1_vare
     c                   kfld                    vlagl1_lage
8.03  *
8.03  * Key for les på enheter
8.03  *
8.03 c     vvenlr_key    klist
8.03 c                   kfld                    w_firm
8.03 c                   kfld                    vvenlr_vare
8.03 c                   kfld                    vvenlr_enhe
8.03  *
8.03  * Key for les på enheter
8.03  *
8.03 c     vvenlr_key2   klist
8.03 c                   kfld                    w_firm
8.03 c                   kfld                    vvenlr_vare
      *
     c                   eval      w_firm = l_firm
      *
      * Henter inn appkey fra COMPANY_SETTINGS
      *
       exec sql
        select aposkval
        into   :w_code
        from   aposextnst
        where  aposfirm = :w_firm and
               apossyst = 'NGN-PURCHASE-MODULE' and
               aposckey = 'purchase.externalsystem.blueridge';

      *
      * Beregn datoen
      *
     c                   time                    w_date
9.xx c**   w_date        subdur    1:*d          w_date
      *
     c                   endsr
      *
