     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LH714R
      * Beskrivelse . . : Leser detaljene og finner status og sum til head.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       110604 bø   Nytt program
      *       010904 bø   Justert status mht. vekt/sum grenser
      *       150305 bø   Justert status mht. leverandør på varen
6.10  * R6.10 040909 bof  Utvidet for status hvis man kjører kjedesortiment
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flfhel1    if   e           k disk    rename(lfhepfr:lfhel1r)
     flfdtl1    if   e           k disk    rename(lfdtpfr:lfdtl1r)
     fmppalr    if   e           k disk    rename(mppapfr:mppalrr)
      *
      * Parameter-inn
     d p_numm          s                   like(lhnumm)
     d p_stat          s                   like(lhstat)
     d p_tbel          s                   like(lhtbel)
     d p_tvkt          s             11  3
      *
      * Local Data Area
      *
     d                uds
      *
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d mppalr_ldor     s                   like(mpldor)
     d mppalr_lnum     s                   like(mplnum)
      *
      * Definering av variable
      *
     d w_sum           s             11  2
     d w_vkt           s             11  3
     d w_tbel          s             11  2
     d w_112           s             11  2
     d w_stat          s                   like(lfstat)
      *
     d b_feil          s              1    inz(*off)
     d b_sts0          s              1    inz(*off)
     d b_sts1          s              1    inz(*off)
     d b_sts2          s              1    inz(*off)
     d b_sts3          s              1    inz(*off)
     d b_sts4          s              1    inz(*off)
     d b_sts5          s              1    inz(*off)
     d b_sts6          s              1    inz(*off)
     d b_sts7          s              1    inz(*off)
     d b_sts8          s              1    inz(*off)
6.11 d b_sts9          s              1    inz(*off)
      *
     d w_firm          s                   like(lffirm)
     d w_numm          s                   like(lfnumm)
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram - leser detaljene - finner sum og status
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      w_sum = 0
     c                   eval      w_vkt = 0
     c                   eval      w_stat = '0'
     c                   eval      b_sts0  = *off
     c                   eval      b_sts1  = *off
     c                   eval      b_sts2  = *off
     c                   eval      b_sts3  = *off
     c                   eval      b_sts4  = *off
     c                   eval      b_sts5  = *off
     c                   eval      b_sts6  = *off
     c                   eval      b_sts7  = *off
     c                   eval      b_sts8  = *off
6.10 c                   eval      b_sts9  = *off
readec     lfdtl1_key    setll     lfdtl1
readec                   if        %found
readec                   dou       %eof
readec     lfdtl1_key    reade     lfdtl1
readec                   if        not %eof
     c                   eval      w_sum = w_sum + (lfanta * lflbel)
     c                   eval      w_vkt = w_vkt + (lfanta * lfvekt)
     c                   select
     c                   when      lfstat = '0'
     c                   eval      b_sts0 = *on
     c                   when      lfstat = '1'
     c                   eval      b_sts1 = *on
     c                   when      lfstat = '2'
     c                   eval      b_sts2 = *on
     c                   when      lfstat = '3'
     c                   eval      b_sts3 = *on
     c                   when      lfstat = '4'
     c                   eval      b_sts4 = *on
     c                   when      lfstat = '5'
     c                   eval      b_sts5 = *on
     c                   when      lfstat = '6'
     c                   eval      b_sts6 = *on
     c                   when      lfstat = '7'
     c                   eval      b_sts7 = *on
     c                   when      lfstat = '8'
     c                   eval      b_sts8 = *on
6.10 c                   when      lfstat = '9'
6.10 c                   eval      b_sts9 = *on
     c                   endsl
      *
readec                   endif
     c                   enddo
readec                   endif
      *
     c                   select
      * varestatus 8
     c                   when      b_sts8 = *on
     c                   eval      w_stat = '6'
      * varestatus 7
     c                   when      b_sts7 = *on
     c                   eval      w_stat = '5'
      * varestatus 4 - 6
     c                   when      b_sts6 = *on  or
     c                             b_sts5 = *on  or
     c                             b_sts4 = *on
     c                   eval      w_stat = '4'
6.10  * varestatus 9
6.10 c                   when      b_sts9 = *on
6.10 c                   eval      w_stat = '7'
      * varestatus 3
     c                   when      b_sts3 = *on
     c                   eval      w_stat = '3'
      * varestatus 2
     c                   when      b_sts2 = *on
     c                   eval      w_stat = '1'
      * varestatus 0 eller 1
     c                   when      b_sts0 = *on or
     c                             b_sts1 = *on
     c                   eval      w_stat = '0'
     c                   endsl
      *
      * hvis status 0 så må status for vekt / sum sjekkes
     c                   if        w_stat = '0'
     c                   eval      mppalr_ldor = lhldor
     c                   eval      mppalr_lnum = lhlnum
     c     mppalr_key    chain     mppalr
     c                   if        %found(mppalr)
     c                   if        mpgvek <> 0 and
     c                             w_vkt  <  mpgvek
     c                   eval      w_stat = '2'
     c                   endif
     c                   if        mpgbel <> 0 and
     c                             w_sum  <  mpgbel
     c                   eval      w_stat = '2'
     c                   endif
     c                   endif
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Slutt- oppgaver
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      p_tbel = w_sum
     c                   eval      p_tvkt = w_vkt
     c                   eval      p_stat = w_stat
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *
      *  Key for les av heading
      *
     c     lfhel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
      *
      *  Key for les av FORSLAGS-LINJE
      *
     c     lfdtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
      *
      *  Key for les av bestillingsparameter
      *
     c     mppalr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mppalr_ldor
     c                   kfld                    mppalr_lnum
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    p_numm
     c                   parm                    p_stat
     c                   parm                    p_tbel
     c                   parm                    p_tvkt
      *
      *  Setter startverdier
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_numm = p_numm
      *
     c     lfhel1_key    chain     lfhel1
      *
     c                   endsr
