     H decedit('0.') datedit(*dmy-) option(*nodebugio)
     h DftActgrp(*No) BndDir('CGIDEV2/CGIDEV2')
      /copy hspecsbnd
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : Fo793R
      * Beskrivelse . . : Lager xml til Cobuilder (nytt format)
      *                   basert på Neb
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       161213 bof  Nytt program
6.20  *       060214 bof  Korrigeringer ihht. Cobuilder
6.21  *       130314 bof  Tatt unna UTF-konv. ++
6.23  *       060214 bof  Utvidet med gtin-nr
6.24  *       090915 bof  lagt foretaksnr i delivery-neb
6.30  * 6.30  280115 bsa  Skriver til datakø for å trigge java sniffer
6.31a * 6.30  050615 bsa  Nullstill minne for bruk av XML
6.31  *       290416 bof  Ikke krav på rolle på kontaktpersoner.
6.32  *       050618 bof  Satt verdi på supplierdep
7.01  *       240219 bof  Satt på SupplierID
7.02  *       250219 bof  Utvidet med oppslag på resk.nr hvis ikke org.nr på va-lev
7.03  *       250219 bof  Skal ikke kalle datakø
7.04  *       020320 bof  Endret id fra 4 til 5 pos.
7.05  *       180920 bof  sikre at den er på riktig bilag hele tiden
7.05  *       191120 bof  VA varer må også sjekkes. GTIN kan gi lev.nr
7.05  *       190321 bof  Må også sjekke VA mht. gtin + feilrett.vedr switch
9.01  *       210823 mst  Utvider teller
9.02  *       210823 mst  Utvider teller    11(0)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffbdol1    uf   e           k disk    rename(fbdopfr:fbdol1r)
     fraa8l1    if   e           k disk    rename(raa8pfr:raa8l1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
6.31 f*ukpl1    if   e           k disk    rename(rukppfr:rukpl1r)
     fafpslr    if   e           k disk    rename(afpspfr:afpslrr)
6.21 ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
6.21 fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
7.05 fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
6.21 fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
7.02 frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
6.23 fvvenl2    if   e           k disk    rename(vvenpfr:vvenl2r)
6.23 fvvarl3    if   e           k disk    rename(vvarpfr:vvarl3r)
6.23 fjvpkl3    if   e           k disk    rename(jvpkpfr:jvpkl3r)
7.05 fjvpkl1    if   e           k disk    rename(jvpkpfr:jvpkl1r)
      *
     d p_bnum          s             15
      *
6.10 d p_str_in        s            512
6.10 d p_str_out       s            512
6.10 d p_lr            s              1
6.30 d p_filg          s             10
6.30 d p_xmlf          s            256
      *
      * Definering av variabler i nøkler
      *
     d rkunl1_kund     s                   like(rkkund)
6.31 d*rukpl1_kund     s                   like(rkkund)
     d fbdol1_bnum     s                   like(fwbnum)
     d afpslr_ufil     s                   like(l_filg)
     d afpslr_uide     s                   like(afuide)
6.21 d fkprl1_kund     s                   like(flkund)
6.21 d fkprl1_kpro     s                   like(flkpro)
6.21 d vvarl1_vare     s                   like(vvvare)
7.05 d jvarl1_vare     s                   like(jvvare)
6.21 d jlevl1_ldor     s                   like(jlldor)
7.02 d rlevl1_levr     s                   like(rllevr)
6.23 d vvenl2_vare     s                   like(vevare)
6.23 d vvenl2_saen     s                   like(vesaen)
6.23 d vvarl3_nobb     s                   like(vvnobb)
6.23 d jvpkl3_gtin     s                   like(jwgtin)
7.05 d jvpkl1_vare     s                   like(jwvare)
      *
9.01 d*w_tell          s              4  0
9.02 d w_tell          s             11  0
     d w_firm          s                   like(rkfirm)
7.05 d w_ldor          s                   like(vvldor)
     d w_path          s             50
     d w_xmlp          s             50
     d w_dato          s              8
     d w_ftex          s             14
     d w_ftnr          s              9
     d w_tlfn          s             15
     d w_proj          s             50
     d w_pnav          s             50
     d w_padr          s             50
     d w_kprs          s             50
     d w_knav          s             50
     d w_kgat          s             50
     d w_kste          s             50
     d w_bper          s             50
     d w_fnav          s             50
     d w_land          s              2
     d w_flan          s              2
     d w_tlfp          s             15
6.20 d w_gtin          s             14  0
6.20 d w_stat          s              1
7.04 d w_id            s              5
     d w_tek1          s                   like(vvtek1)
7.01 d w_vare          s                   like(vvvare)
7.02 d w_orgn          s                   like(jlorgn)
6.30 d w_dtaq          s              1
      *
      *  UDS Local Data Area
      *
     d                uds
     d l_user                911    920
     d l_filg                931    933
     d l_firm                944    946  0
      *
     d b_first         s              1    inz(*off)
     d b_ok            s              1    inz(*off)
6.24 d b_cobu          s              1    inz(*off)
      *
     d XML_Output      s            256a   Varying
     d XML_path        s            256a
     d Out_path        s            256a
      *
     d tmpdate         s               d
      *
     d d_dato          ds            10
     d  d_aar                         4    overlay(d_dato:1)
     d  d_mnd                         2    overlay(d_dato:6)
     d  d_dag                         2    overlay(d_dato:9)
      *
      /copy prototypeb
      /copy usec
      *
     d lo              c                   'abcdefghijklmnopqrstuvwxyzæøåÆØÅ'
     d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÆØÅ'
7.01  *
7.01 d u_s701          s              1    inz(*off)                            Klient
7.01  *
7.01  * Definering av eksterne prg
7.01  *
7.01   dcl-s co402_verdi1  char(1);
7.01   dcl-s co402_verdi2  char(2048);
7.01   DCL-PR CO402R EXTPGM('CO402R');
7.01     p_filgrp            char(3)     const;
7.01     p_firm              packed(3:0) const;
7.01     p_lager             packed(2:0) const;
7.01     p_nokkel            char(50)    const;
7.01     p_verdi1            char(1);
7.01     p_verdi2            char(2048);
7.02   END-PR;
7.01  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
7.05 c                   eval      b_ok = *off
7.05 c                   eval      b_first = *on
      *
      * Leser firmadata ifm avsenderinformasjon
      *
     c     afir_key      chain     afirl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      afpslr_ufil = l_filg
     c                   eval      afpslr_uide = 'COBUMALNY'
     c     afpslr_key    chain     afpslrr
     c                   if        not %found(afpslr)
     c                   goto      avslutt
     c                   endif
     c                   eval      XML_path=afudss
      *
     c                   eval      afpslr_ufil = l_filg
     c                   eval      afpslr_uide = 'COBUOUTNY '
     c     afpslr_key    chain     afpslrr
     c                   if        not %found(afpslr)
     c                   goto      avslutt
     c                   endif
     c                   eval      OUT_path=afudss
6.30  *
6.30  * Finn ut om vi skal skrive til datakø
6.30  *
6.30 c                   eval      w_dtaq = '0'
6.30 c                   eval      afpslr_uide = 'DATAQ     '
6.30 c     afpslr_key    chain     afpslrr
6.30 c                   if        %found
6.30 c                   movel     afudss        w_dtaq
6.30 c                   endif
      *
      *
      * cobuilder varer
     c                   eval      fbdol1_bnum = p_bnum
      *
     c     fbdol1_key    setll     fbdol1r
     c     fbdol1_key    reade     fbdol1r
     c                   dow       not %eof
      *
7.05 c                   if        fwcovf = 0 and
7.05 c                             fwbnum = p_bnum
     c
      *
     c                   if        b_first = *on
     c                   exsr      xml_env
     c                   exsr      xml_header
     c                   endif
      *
     c                   if        b_ok = *on
      *
6.20 c                   exsr      hent_gtin
     c                   exsr      xml_line
      *
     c                   eval      fwcovf = 1
     c                   update    fbdol1r
     c                   else
      * hvis ikke ok, så skal de ikke til cobuilder, settes til 9
     c                   eval      fwcovf = 9
     c                   update    fbdol1r
      *
     c                   endif
      *
     c                   endif
      *
     c     fbdol1_key    reade     fbdol1r
     c                   enddo
      *
      * Skriv xml til IFS (NB Kun hvis det er funnet noe å skrive)
      *
     c                   if        w_tell > 0
     c                   exsr      xml_end
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av mal
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xml_env       begsr
      *
      * Hent inn xml templaten
      *
     c                   eval      w_xmlp = %trim(XML_path)
      *
     c                   callp     GetHTMLIFS(w_xmlp :'<AS400>')
6.31ac                   callp     ClrHtmlBuffer
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for håndtering av mal og hode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xml_header    begsr
      *
      * Hent inn xml templaten
      *
     c                   eval      b_ok = *off
      *
     c                   eval      w_xmlp = %trim(XML_path)
      *
     c                   callp     GetHTMLIFS(w_xmlp :'<AS400>')
      *
      * Hent inn info fra e-handels register
      *
     c     raa8l1_key    chain     raa8l1
     c                   if        not %found
     c                   leavesr
     c                   endif
      *
      * Hent inn info fra kunderegisteret
      *
     c                   eval      rkunl1_kund = fwbkun
     c     rkunl1_key    chain     rkunl1
     c                   if        not %found
     c                   leavesr
     c                   endif
      * SJEKK om en av kontaktpersonene har rolle COBUILD
6.31 c*                  eval      b_cobu = *off
6.31 c*                  eval      rukpl1_kund = rkkund
6.31 c*    rukpl1_key    setll     rukpl1
6.31 c*    rukpl1_key    reade     rukpl1
6.31 c*                  dow       not %eof(rukpl1)
6.31 c*    lo:up         xlate     rukrol        rukrol
6.31 c*                  if        rukrol = 'COBUILDER'
6.31 c*                  eval      b_cobu = *on
6.31 c*                  endif
6.31 c*    rukpl1_key    reade     rukpl1
6.31 c*                  enddo
6.31 c*                  if        b_cobu = *off
6.31 c*                  leavesr
6.31 c*                  endif
      *
      * sjekker om prosjektet er krysset av med Cobuilder
      *
     c                   eval      fkprl1_kund = fwbkun
     c                   eval      fkprl1_kpro = fwbkpr
     c     fkprl1_key    chain     fkprl1
     c                   if        not %found
     c                   leavesr
     c                   else
     c                   if        flcobu = 0
     c                   leavesr
     c                   endif
     c                   endif
      * hvis foretaksnr = 0, så sendes den ikke til cobuilder
     c                   if        rkftnr = 0
     c                   leavesr
     c                   endif
      *
     c                   move      rkftnr        w_ftnr
     c                   eval      w_ftex = 'NO' + w_ftnr + 'MVA'
      *
     c                   movel     fwbtms        d_dato
     c                   eval      w_dato = d_aar + d_mnd + d_dag
      *
      * kundeprosjekt
      *
     c                   eval      fkprl1_kund = fwbkun
     c                   eval      fkprl1_kpro = fwbkpr
     c     fkprl1_key    chain     fkprl1
     c                   if        not %found
     c                   leavesr
     c                   endif
     c                   if        fltlfa <> *blank
     c                   movel     fltlfa        w_tlfp
     c                   else
     c                   movel     fltlfm        w_tlfp
     c                   endif
     c                   eval      w_proj  = *blank
     c                   if        flepro <> *blank
     c                   movel     flepro        w_proj
     c                   else
     c                   movel     flnavn        w_proj
     c                   endif
6.10  *
6.10  * Scann av strenger - prosjektnavn
6.10  *
6.21 c                   eval      p_str_in = *blank
6.21 c                   eval      p_lr = '0'
6.21 c                   movel     w_proj        p_str_in
6.21 c                   call      'AP613R'
6.21 c                   parm                    p_str_in
6.21 c                   parm                    p_str_out
6.21 c                   parm                    p_lr
6.21 c                   movel     p_str_out     w_proj
6.10  *
6.10  * Scann av strenger - Kontaktperson
6.10  *
6.21 c                   eval      p_str_in = *blank
6.21 c                   eval      p_lr = '0'
6.21 c                   movel     fwbper        p_str_in
6.21 c                   call      'AP613R'
6.21 c                   parm                    p_str_in
6.21 c                   parm                    p_str_out
6.21 c                   parm                    p_lr
6.21 c                   movel     p_str_out     w_bper
6.10  *
6.10  * Scann av strenger - prosjektnavn  customerref
6.10  *
6.21 c                   eval      p_str_in = *blank
6.21 c                   eval      p_lr = '0'
6.21 c                   movel     flnavn        p_str_in
6.21 c                   call      'AP613R'
6.21 c                   parm                    p_str_in
6.21 c                   parm                    p_str_out
6.21 c                   parm                    p_lr
6.21 c                   movel     p_str_out     w_pnav
6.21 c*                  eval      w_pnav = *blank
6.21 c*                  movel     flnavn        w_pnav
6.10  *
6.10  * Scann av strenger - prosjektadresse
6.10  *
6.21 c                   eval      w_padr = *blank
6.21 c                   if        fladr1 <> *blank
6.21 c                   movel     fladr1        w_padr
6.21 c                   else
6.21 c                   movel     flnavn        w_padr
6.21 c                   endif
6.21 c                   eval      p_str_in = *blank
6.21 c                   eval      p_lr = '0'
6.21 c                   movel     w_padr        p_str_in
6.21 c                   call      'AP613R'
6.21 c                   parm                    p_str_in
6.21 c                   parm                    p_str_out
6.21 c                   parm                    p_lr
6.21 c                   movel     p_str_out     w_padr
6.10  *
6.10  * Scann av strenger - kontaktperson
6.10  *
6.21 c                   eval      p_str_in = *blank
6.21 c                   eval      p_lr = '0'
6.21 c                   movel     flkprs        p_str_in
6.21 c                   call      'AP613R'
6.21 c                   parm                    p_str_in
6.21 c                   parm                    p_str_out
6.21 c                   parm                    p_lr
6.21 c                   movel     p_str_out     w_kprs
6.21 c*                  eval      w_kprs = *blank
6.21 c*                  movel     flkprs        w_kprs
6.10  *
6.10  * Scann av strenger - kundenavn
6.10  *
6.21 c                   eval      p_str_in = *blank
6.21 c                   eval      p_lr = '0'
6.21 c                   movel     rknavn        p_str_in
6.21 c                   call      'AP613R'
6.21 c                   parm                    p_str_in
6.21 c                   parm                    p_str_out
6.21 c                   parm                    p_lr
6.21 c                   movel     p_str_out     w_knav
6.21 c*                  eval      w_knav = *blank
6.21 c*                  movel     rknavn        w_knav
6.10  *
6.10  * Scann av strenger - kundegate
6.10  *
6.21 c                   eval      p_str_in = *blank
6.21 c                   eval      p_lr = '0'
6.21 c                   movel     rkgate        p_str_in
6.21 c                   call      'AP613R'
6.21 c                   parm                    p_str_in
6.21 c                   parm                    p_str_out
6.21 c                   parm                    p_lr
6.21 c                   movel     p_str_out     w_kgat
6.21 c*                  eval      w_kgat = *blank
6.21 c*                  movel     rkgate        w_kgat
6.10  *
6.10  * Scann av strenger - kundested
6.10  *
6.21 c                   eval      p_str_in = *blank
6.21 c                   eval      p_lr = '0'
6.21 c                   movel     rksted        p_str_in
6.21 c                   call      'AP613R'
6.21 c                   parm                    p_str_in
6.21 c                   parm                    p_str_out
6.21 c                   parm                    p_lr
6.21 c                   movel     p_str_out     w_kste
6.21 c*                  eval      w_kste = *blank
6.21 c*                  movel     rksted        w_kste
6.10  *
6.10  * Scann av strenger - firmanavn
6.10  *
6.21 c                   eval      p_str_in = *blank
6.21 c                   eval      p_lr = '0'
6.21 c                   movel     aafnav        p_str_in
6.21 c                   call      'AP613R'
6.21 c                   parm                    p_str_in
6.21 c                   parm                    p_str_out
6.21 c                   parm                    p_lr
6.21 c                   movel     p_str_out     w_fnav
6.21 c*                  eval      w_fnav = *blank
6.21 c*                  movel     aafnav        w_fnav
6.10  *
      *landkode
     c                   eval      w_flan  = 'NO'
     c                   eval      w_land  = 'NO'
     c                   if        rkland <> *blank
     c                   movel     rkland        w_land
     c                   endif
      *telefon
     c                   eval      w_tlfn  = rktlfn
     c                   if        w_tlfn  = *blank
     c                   movel     rkmobn        w_tlfn
     c                   endif
7.01  *supplier ID - får verdi i init rutina
7.01 c                   if        u_s701 = *off
7.01 c                   eval      w_id = 'xxxx'
7.01 c                   endif
      *
      /Free
           // Skriv standard topp
               WrtSection('Topp');

           // Skriv ordrehode informasjon
               UpdHTMLVar('Ordernumber':             fwbnum);
               UpdHTMLVar('Orderdate':               w_dato);
               UpdHTMLVar('Projectnumber':    %trim(w_proj));
               UpdHTMLVar('Customerref':      %trim(w_pnav));
               UpdHTMLVar('Buyerid':          %char(fwbftn));
               UpdHTMLVar('Buyername':        %trim(w_knav));
               UpdHTMLVar('Buyerstreet':      %trim(w_kgat));
               UpdHTMLVar('Buyeradress':      %char(rkponr));
               UpdHTMLVar('Buyercity':        %trim(w_kste));
               UpdHTMLVar('Buyerpostal':      %char(rkponr));
               UpdHTMLVar('Buyercountry':     %trim(w_land));
               UpdHTMLVar('BuyerCname':       %trim(w_bper));
               UpdHTMLVar('BuyerCphone':      %trim(w_tlfn));
               UpdHTMLVar('BuyerCmail':       %trim(fwbmai));
               UpdHTMLVar('BuyerTaxVAT':      %trim(w_ftex));
               WrtSection('Orderheader');

               UpdHTMLVar('supplierGLN':      %char(ra8ean));
               UpdHTMLVar('SupplierID':       %trim(w_id));
               UpdHTMLVar('Suppliername':     %trim(w_fnav));
               UpdHTMLVar('Supplierdep':      %char(ra8ean));
               UpdHTMLVar('Suppliercoutry':   %trim(w_flan));
               UpdHTMLVar('Suppcontname':     '  '         );
               UpdHTMLVar('Suppcontphone':    %trim(aaftlf));
               UpdHTMLVar('Suppcontmail':     %trim(aamail));
               WrtSection('Supplierstart');

               UpdHTMLVar('Deliveryplace':    %trim(w_pnav));
               UpdHTMLVar('Deliveryident':    %trim(w_ftnr));
               UpdHTMLVar('Deliveryname':     %trim(w_knav));
               UpdHTMLVar('Deliverystreet':   %trim(w_padr));
               UpdHTMLVar('Delicontname':     %trim(w_kprs));
               UpdHTMLVar('Delicontphone':    %trim(w_tlfp));
               UpdHTMLVar('Delicontmail':     %trim(flmail));
               WrtSection('Deliverystart');

      /End-free
      *
     c                   eval      b_first = *off
     c                   eval      b_ok    = *on
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for håndtering av linjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xml_line      begsr
      *
      * finner orgnr. på lev.av vare
      *
7.02 c                   eval      w_orgn = 0
     c                   movel     fwbnob        vvarl1_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found(vvarl1)
7.05 c                   if        vvvtyp <> 'L' and
7.05 c                             vvvtyp <> 'S'
7.05 c                   leavesr
7.05 c                   endif
7.05 c                   eval      w_tek1 = vvtek1
     c                   eval      jlevl1_ldor = vvnlev
     c     jlevl1_key    chain     jlevl1
     c                   if        %found(jlevl1) and
     c                             jlorgn <> 0
7.02 c                   eval      w_orgn = jlorgn
7.02 c                   else
7.02 c                   eval      rlevl1_levr = vvldor
7.02 c     rlevl1_key    chain     rlevl1
7.02 c                   if        %found(rlevl1) and
7.02 c                             rlftnr <> 0
7.02 c                   eval      w_orgn = rlftnr
7.02 c                   endif
7.02 c                   endif
7.05 c                   else
7.05  * prøver med VA
7.05 c                   movel     fwbnob        jvarl1_vare
7.05 c     jvarl1_key    chain     jvarl1
7.05 c                   if        %found(jvarl1)
7.05 c                   eval      w_tek1 = jvtek1
7.05 c                   eval      jlevl1_ldor = jvldor
7.05 c     jlevl1_key    chain     jlevl1
7.05 c                   if        %found(jlevl1) and
7.05 c                             jlorgn <> 0
7.05 c                   eval      w_orgn = jlorgn
7.05 c                   else
7.05 c                   eval      rlevl1_levr = jlenum
7.05 c     rlevl1_key    chain     rlevl1
7.05 c                   if        %found(rlevl1) and
7.05 c                             rlftnr <> 0
7.05 c                   eval      w_orgn = rlftnr
7.05 c                   endif
7.05 c                   endif

7.05 c                   endif
     c                   endif
     c
7.02 c                   if        w_orgn > 0
7.05 c*                  eval      w_tek1 = vvtek1
      *
     c                   eval      w_tell = w_tell + 1
      *
      /Free
           // Skriv ordrehode informasjon
               UpdHTMLVar('Ordreline':      %char(w_tell));
6.20           WrtSection('OrderLineStart');
6.20  /End-free
6.20  *
6.20 c                   if        w_gtin <> 0
6.20  * hvis gtin er utfylt brukes dette utlegget
6.20  *
6.20  /Free
6.20           UpdHTMLVar('Articlegtin':    %char(w_gtin));
6.20           WrtSection('OrderLineGTIN');
6.20
6.20  /End-free
6.20  *
6.20 c                   endif
6.20  /Free
6.20       // Skriv ordrehode informasjon resten
               UpdHTMLVar('Articleno':      %char(fwbnob));
               UpdHTMLVar('ArticleDescription': %trim(w_tek1));
7.02           UpdHTMLVar('Articlesuppid':  %char(w_orgn));
               UpdHTMLVar('Quantity':            '1');

6.20           WrtSection('OrderLineRest');

      /End-free
      *
     c                   endif
     c*                  endif
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for håndtering av linjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xml_end       begsr
      *
      /Free
           // Skriv siste tagg
               UpdHTMLVar('Controllineno':  %char(w_tell));
               WrtSection('Footer');

      /End-free
      *
      * Lag navn til xml fila
      *
     c                   eval      w_path = %trim(Out_path)
     c                   eval      XML_Output = %trim(w_path) + 'Cobuild_'+
     c                             %char(ra8ean) + '_' + %trim(fwbnum) +
     c                             '.xml'
      *
     c                   callp     WrtHtmlToStmf(XML_Output: 819)
6.30  * Skriver til datakø for å aktivere sniffer, hvis dataq er aktiv
7.03 c*                   if        w_dtaq = '1'
7.03 c*                   eval      p_xmlf = xml_output
7.03 c*                   eval      p_filg = 'ADB'+l_filg
7.03 c*                   call      'AP629C'
7.03 c*                   parm                    p_filg
7.03 c*                   parm                    p_xmlf
7.03 c*                   endif
      *
     c                   endsr
6.20  *
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  * Subrutine for å hente gtin-nr
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  *
6.20 c     hent_gtin     begsr
6.20  *
6.20 c                   eval      w_gtin = 0
7.05 c                   eval      w_ldor = 0
7.01 c                   eval      w_vare = *blank
7.05 c*                   if        u_s701 = *on
7.01 c                   movel     fwbnob        vvarl1_vare
7.01 c     vvarl1_key    chain     vvarl1
7.01 c                   if        %found(vvarl1)
7.01 c                   eval      w_vare = vvvare
7.05 c                   else
7.05 c*                   else
6.20 c                   eval      vvarl3_nobb = fwbnob
6.20 c     vvarl3_key    chain     vvarl3
7.01 c                   if        %found(vvarl3)
7.01 c                   eval      w_vare = vvvare
7.05 c                   else
7.05 c                   exsr      va_sjekk
7.05 c                   leavesr
7.01 c                   endif
7.05 c                   endif
7.05 c*                   endif
7.01 c                   if        w_vare <> *Blank
7.01 c                   eval      vvenl2_vare = w_vare
6.20 c                   eval      vvenl2_saen = 1
6.20 c     vvenl2_key    chain     vvenl2
6.20 c                   if        %found(vvenl2)
6.20 c                   call      'VE714R '
6.20 c                   parm                    w_firm
7.01 c                   parm                    w_vare
6.20 c                   parm                    veenhe
6.20 c                   parm                    w_gtin
6.20 c                   parm                    w_stat
6.20 c                   endif
6.20 c                   endif
6.20  *
6.20  * sjekker om gtin-nr er Trelastindustriens gtin-nr -
6.20  * da skal det ikke legges fordi disse ikke har dok.
6.20  *
6.20 c                   if        w_gtin <> 0
6.20 c                   eval      jvpkl3_gtin = w_gtin
6.20 c     jvpkl3_key    setll     jvpkl3
6.20 c     jvpkl3_key    reade     jvpkl3
6.20 c                   dow       not %eof(jvpkl3)
7.05 c                   eval      w_ldor = jwldor
6.20 c                   if        jwldor = 60764
6.20 c                   eval      w_gtin = 0
7.05 c                   eval      w_ldor = 0
6.20 c                   leavesr
6.20 c                   endif
6.20 c     jvpkl3_key    reade     jvpkl3
6.20 c                   enddo
6.20 c                   endif
6.20  *
6.20 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.05  * Subrutine for initiering av program
7.05  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.05  *
7.05 c     va_sjekk      begsr
7.05  * prøver med VA
7.05 c                   movel     fwbnob        jvarl1_vare
7.05 c     jvarl1_key    chain     jvarl1
7.05 c                   if        %found(jvarl1)
7.05  * finner gtin på F-pak
7.05 c                   eval      jvpkl1_vare = jvvare
7.05 c     jvpkl1_key    setll     jvpkl1
7.05 c     jvpkl1_key    reade     jvpkl1
7.05 c                   dow       not %eof(jvpkl1)
7.05 c                   if        jwldor <> 60764
7.05 c                   if        jwpkla = 'F'
7.05 c                   eval      w_gtin = jwgtin
7.05 c                   eval      w_ldor = jwldor
7.05 c                   leavesr
7.05 c                   endif
7.05 c                   endif
7.05 c     jvpkl1_key    reade     jvpkl1
7.05 c                   enddo
7.05 c                   endif
7.05 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_bnum
      *
      * Key for oppslag på FBDO
      *
     c     fbdol1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fbdol1_bnum
      *
      * Key for oppslag på RKUN
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
6.24  *
6.24  * Key for oppslag på RKUN rolle
6.31  *
6.31 c*    rukpl1_key    klist
6.31 c*                  kfld                    w_firm
6.31 c*                  kfld                    rukpl1_kund
      *
      * Key for oppslag på RAA8
      *
     c     raa8l1_key    klist
     c                   kfld                    w_firm
      *
      * Key for propertiesfil
      *
     c     afpslr_key    klist
     c                   kfld                    afpslr_ufil
     c                   kfld                    afpslr_uide
6.21  *
6.21  * Key for oppslag på kundeprosjekt
6.21  *
6.21 c     fkprl1_key    klist
6.21 c                   kfld                    w_firm
6.21 c                   kfld                    fkprl1_kund
6.21 c                   kfld                    fkprl1_kpro
6.21  *
6.21  * Key for oppslag på vare
6.21  *
6.21 c     vvarl1_key    klist
6.21 c                   kfld                    w_firm
6.21 c                   kfld                    vvarl1_vare
6.21  *
6.21  * Key for oppslag på leverandør
6.21  *
6.21 c     jlevl1_key    klist
6.21 c                   kfld                    jlevl1_ldor
7.05  *
7.05  * Key for oppslag på vare
7.05  *
7.05 c     jvarl1_key    klist
7.05 c                   kfld                    jvarl1_vare
7.05  *
7.02  * Key for oppslag på leverandør
7.02  *
7.02 c     rlevl1_key    klist
7.02 c                   kfld                    w_firm
7.02 c                   kfld                    rlevl1_levr
      *
      * Key for firma
      *
     c     afir_key      klist
     c                   kfld                    w_firm
6.23  *
6.23  * Key for les av vare enhet
6.23  *
6.23 c     vvenl2_key    klist
6.23 c                   kfld                    w_firm
6.23 c                   kfld                    vvenl2_vare
6.23 c                   kfld                    vvenl2_saen
6.23  *
6.23  * Key for les av vare
6.23  *
6.23 c     vvarl3_key    klist
6.23 c                   kfld                    w_firm
6.23 c                   kfld                    vvarl3_nobb
6.23  *
6.23  * Key for les av vare
6.23  *
6.23 c     jvpkl3_key    klist
6.23 c                   kfld                    jvpkl3_gtin
7.05  *
7.05  * Key for les av vare
7.05  *
7.05 c     jvpkl1_key    klist
7.05 c                   kfld                    jvpkl1_vare
      *
     c                   eval      w_firm = l_firm
     c                   eval      fbdol1_bnum = p_bnum
     c                   eval      b_first = *on
     c                   eval      w_tell = 0
7.01  *
7.01  * Hvis faktura/kreditnota og direkte skal sette fakturatype = 0
7.01  *
7.01   CallP CO402R(l_filg:w_firm:0:'FO793_701':
7.01   co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01     u_s701 = *on;
7.01   endif;
7.01   w_id = %trim(co402_verdi2);
7.02  *
      *
     c                   endsr
