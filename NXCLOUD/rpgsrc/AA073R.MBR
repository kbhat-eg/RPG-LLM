      /title  ASPGPL Vedlikehold av filkopiering-linje-register
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASPGPL
      * Program . . . . : AA073R
      * Beskrivelse . . : Vedlikehold av subsystemer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       040613 sst  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fasbslr    if   e           k disk    rename(asbspfr:asbslrr)
     fasbslu    uf a e           k disk    rename(asbspfr:asbslur)
     fasbsl1    if   e           k disk    rename(asbspfr:asbslr1)
     fasbsl2    if   e           k disk    rename(asbspfr:asbsl2r)
     fasbsl3    if   e           k disk    rename(asbspfr:asbsl3r)
      *
     faa073d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av array
      *
     d a_meld          s             11    dim(3) ctdata perrcd(1)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
      *
      * Definering av parametre
      *
     d p_linj          s                   like(aylinj)
      *
      * Definering av variabler i nøkler
      *
     d asbslu_linj     s                   like(aylinj)
     d asbslr_linj     s                   like(aylinj)
     d asbsl1_linj     s                   like(aylinj)
     d asbsl2_subs     s                   like(aysubs)
     d asbsl2_linj     s                   like(aylinj)
     d asbsl3_bibl     s                   like(aybibl)
     d asbsl3_linj     s                   like(aylinj)
      *
      * Definering av variable
      *
     d w_spge          s              3  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d w_feil          s              1    inz(*off)
     d w_forn          s              1    inz(*off)
     d w_anul          s              1    inz(*off)
      *
     d w_besk          s             35
      *
     d w_linj          s                   like(aylinj)
     d w_linj_gml      s                   like(aylinj)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(11)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_srrn - Relativt recordnummer i subfile
      * w_spge - Sidestørrelse i subfile (page)
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * wvisrn - Vis den side som inneholder denne record.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meleingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/ endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      * S1WIN  - S1 Standardvindu som inneholder definisjon av
      *             størrelse og utseende.  Bildene D1WIN og K1WIN
      *             er referert til dette.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      dsp_subfile
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2valg = *blank
     c                   if        b2linj <> 0      or
     c                             b2subs <> *blank or
     c                             b2bibl <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
     c                   endif
      *
      * Behandling av subfile control
      *
     c                   if        b2valg <> *blank
     c                   exsr      control
     c                   if        w_feil = *on
     c                   goto      b2tagb
     c                   else
     c                   goto      b2taga
     c                   endif
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   select
     c                   when      w_seqe = 'B'
     c     asbsl3_key    setll     asbsl3
     c                   when      w_seqe = 'A'
     c     asbsl2_key    setll     asbsl2
     c                   other
     c     asbsl1_key    setll     asbsl1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjonerer på filgruppe
      *
     c                   if        b2linj <> 0
     c                   eval      w_seqe      = *blank
     c                   eval      asbsl1_linj = b2linj
     c     asbsl1_key    setll     asbsl1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjonerer på Subsystem
      *
     c                   if        b2subs <> *blank
     c                   eval      w_seqe      = 'A'
     c                   eval      asbsl2_subs = b2subs
     c                   eval      asbsl2_linj = *zero
     c     asbsl2_key    setll     asbsl2
     c                   goto      end_pos
     c                   endif
      *
      * Posisjonerer på bibliotek
      *
     c                   if        b2bibl <> *blank
     c                   eval      w_seqe      = 'B'
     c                   eval      asbsl3_bibl = b2bibl
     c                   eval      asbsl3_linj = *zero
     c     asbsl3_key    setll     asbsl3
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2linj = *zero
     c                   eval      b2subs = *blank
     c                   eval      b2bibl = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behanding av subfile-control
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     control       begsr
      *
     c                   eval      w_forn  = *off
     c                   eval      w_feil  = *off
      *
      * Opprette
      *
     c                   if        b2valg = '1'
     c                   eval      c1linj = b2linj
     c                   exsr      xc1bld
     c                   goto      end_ctl
     c                   endif
      *
      * Validering av nøkkel
      *
     c                   if        b2linj = *zero
     c                   eval      w_feil = *on
     c                   eval      *in31  = *on
     c                   goto      end_ctl
     c                   endif
      *
     c                   eval      asbslr_linj = b2linj
     c     asbslr_key    chain     asbslr                             90
     c                   if        *in90
     c                   eval      w_feil = *on
     c                   eval      *in32  = *on
     c                   goto      end_ctl
     c                   endif
      *
      * Endre
      *
     c                   if        b2valg = '2'
     c                   eval      c1linj = b2linj
     c                   exsr      xc2bld
     c                   goto      end_ctl
     c                   endif
      *
      * Kopiere
      *
     c                   if        b2valg = '3'
     c                   eval      k1linj = b2linj
     c                   exsr      xk1win
     c                   goto      end_ctl
     c                   endif
      *
      * Slette
      *
     c                   if        b2valg = '4'
     c                   eval      d1linj = b2linj
     c                   exsr      xd1win
     c                   goto      end_ctl
     c                   endif
      *
      * Vise
      *
     c                   if        b2valg = '5'
     c                   eval      c1linj = b2linj
     c                   exsr      xc2bld
     c                   goto      end_ctl
     c                   endif
      *
     c     end_ctl       tag
      *
     c                   eval      b2valg = *blank
     c                   eval      b2linj = *zero
     c                   eval      b2subs = *blank
     c                   eval      b2bibl = *blank
      *
     c                   if        w_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
      * Beholder eksisterende posisjonering i subfilen
      *
     c                   if        wcurrn > 0
     c                   eval      wvisrn = wcurrn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   eval      w_forn  = *off
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
      * Endre post
      *
     c                   if        b1valg = '2'
     c                   eval      c1linj = b1linj
     c                   exsr      xc2bld
     c                   eval      b1valg = *blank
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopiere post
      *
     c                   if        b1valg = '3'
     c                   eval      k1linj = b1linj
     c                   exsr      xk1win
     c                   eval      b1valg = *blank
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slette post
      *
     c                   if        b1valg = '4'
     c                   eval      d1linj = b1linj
     c                   exsr      xd1win
     c                   eval      b1valg = *blank
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vise post
      *
     c                   if        b1valg = '5'
     c                   eval      c1linj = b1linj
     c                   exsr      xc2bld
     c                   eval      b1valg = *blank
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        w_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   goto      end_c1bld
     c                   endif
      *
      * Validering
      *
     c                   eval      w_linj = c1linj
      *
     c                   exsr      sjekk_key
      *
     c                   eval      c1linj = w_linj
      *
     c                   if        w_feil = *on
     c                   goto      c1taga
     c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      asbslr_linj = c1linj
     c     asbslr_key    chain     asbslr                             90
     c                   if        *in90 = *off
     c                   exsr      xc1msg
     c                   if        w_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   goto      c1taga
      *
     c     end_c1bld     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbilde legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      w_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      w_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle skjermbilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      asbslr_linj = c1linj
     c     asbslr_key    chain     asbslr                             90
      *
     c                   if        *in90 = *off
     c                   eval      c2nyen = a_meld(2)
     c                   eval      c1linj = aylinj
     c                   eval      c2subs = aysubs
     c                   eval      c2bibl = aybibl
     c                   eval      c2text = aytext
     c                   eval      c2eusr = ayeusr
     c                   eval      c2edat = 0
     c                   if        ayedat <> *loval
     c     *dmy          move      ayedat        c2edat
     c                   endif
     c                   else
     c                   eval      c2nyen = a_meld(1)
     c**                 eval      c1linj = *zero
     c                   eval      c2subs = *blank
     c                   eval      c2bibl = *blank
     c                   eval      c2text = *blank
     c                   eval      c2eusr = *blank
     c                   eval      c2edat = 0
     c                   endif
      *
     c                   if        b1valg = '3' or b2valg = '3'
     c                   eval      c2nyen = a_meld(1)
     c                   eval      c1linj = k1linj
     c                   eval      asbslr_linj = k1linj
     c                   endif
      *
      * Dersom 'vise' settes protect-indikatoer
      *
     c                   if        b1valg = '5' or b2valg = '5'
     c                   eval      *in30 = *on
     c                   eval      c2nyen = a_meld(3)
     c                   endif
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd = *on
      *
     c*                  if        wactfe = 'C2LVSO'
     c*                  call      'VC500R'
     c*                  parm                    w_lfir
     c*                  parm                    c2lvso
     c*                  parm                    w_besk
     c*                  goto      c2tagb
     c*                  endif
      *
     c                   endif
      *
      * Validering
      *
      *    - Subsystem
     c                   if        c2subs = *blank
     c                   eval      *in41 = *on
     c                   goto      c2tagb
     c                   endif
      *    - bibliotek
     c                   if        c2bibl = *blank
     c                   eval      *in42 = *on
     c                   goto      c2tagb
     c                   endif
      *    - beskrivelse
     c                   if        c2text = *blank
     c                   eval      *in43 = *on
     c                   goto      c2tagb
     c                   endif
      *
      * Oppdaterer tabell
      *
     c                   clear                   asbslur
      *
     c                   eval      asbslu_linj = c1linj
     c     asbslu_key    chain     asbslur                            90
      *
     c                   eval      aysubs = c2subs
     c                   eval      aybibl = c2bibl
     c                   eval      aytext = c2text
     c                   eval      ayeusr = l_user
     c                   time                    ayedat
     c                   time                    ayetim
      *
     c                   if        *in90 = *off
     c                   update    asbslur
     c                   else
     c                   eval      aylinj = c1linj
     c                   write     asbslur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = '2'
     c                   eval      b1subs = aysubs
     c                   eval      b1bibl = aybibl
     c                   eval      b1text = aytext
     c                   endif
      *
     c                   if        b1valg = '1' or b2valg = '1' or
     c                             b1valg = '3' or b2valg = '3'
     c                   eval      w_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      asbslr_linj = d1linj
     c     asbslr_key    chain     asbslr                             90
     c                   eval      d1subs = aysubs
      *
     c                   write     s1win
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      w_forn  = *on
      *
     c                   eval      asbslu_linj = d1linj
     c     asbslu_key    chain     asbslur                            90
     c                   delete    asbslur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      w_linj_gml = k1linj
      *
      * Fyller kopierings-bildet
      *
     c                   eval      asbslr_linj = k1linj
     c     asbslr_key    chain     asbslr                             90
      *
     c                   write     s1win
      *
     c     k1tagb        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Validering
      *
     c                   eval      w_linj = k1linj
      *
     c                   exsr      sjekk_key
      *
     c                   eval      k1linj = w_linj
      *
     c                   if        w_feil = *on
     c                   goto      k1tagb
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      asbslr_linj = k1linj
     c     asbslr_key    chain     asbslr                             90
     c                   if        *in90 = *off
     c                   eval      *in33 = *on
     c                   goto      k1tagb
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1linj = w_linj_gml
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      asbsl3                                 15
     c                   when      w_seqe = 'A'
     c                   read      asbsl2                                 15
     c                   other
     c                   read      asbsl1                                 15
     c                   endsl
      *
     c                   if        *in15
     c                   goto      end_crt
     c                   endif
      *
     c*                  if        aylinj <> p_linj
     c*                  goto      end_crt
     c*                  endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = *blank
     c                   eval      b1linj = aylinj
     c                   eval      b1subs = aysubs
     c                   eval      b1bibl = aybibl
     c                   eval      b1text = aytext
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      wvisrn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
     c                   eval      *in22 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for validering av nøkkel
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_key     begsr
      *
     c                   eval      w_feil  = *off
      *
      * Sjekker om korrekt kombinasjon av nøkkel-felt
      *
     c                   if        w_linj = *zero
     c                   eval      w_feil = *on
     c                   eval      *in31  = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c                   if        w_linj = p_linj
     c                   eval      w_feil = *on
     c                   eval      *in35  = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *
      * Key for les på subsystem-register
      *
     c     asbslr_key    klist
     c                   kfld                    asbslr_linj
      *
      * Key for oppdatering av filkopiering-linje-register
      *
     c     asbslu_key    klist
     c                   kfld                    asbslu_linj
      *
      * Key for posisjonering på linje
      *
     c     asbsl1_key    klist
     c                   kfld                    asbsl1_linj
      *
      * Key for posisjonering på subsystem
      *
     c     asbsl2_key    klist
     c                   kfld                    asbsl2_subs
     c                   kfld                    asbsl2_linj
      *
      * Key for posisjonering på bibliotek
      *
     c     asbsl3_key    klist
     c                   kfld                    asbsl3_bibl
     c                   kfld                    asbsl3_linj
      *
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      *in22 = *on
      *
     c     asbsl1_key    setll     asbsl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
**
  Ny post                  001
  Endring                  002
  Vise                     003
