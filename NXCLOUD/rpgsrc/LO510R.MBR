      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  System. . . . . : ASLAGR                                       *
      *  Program . . . . : LO510R                                       *
      *  Beskrivelse . . : Spørring mot bestillinger                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  Spesialversjon. :                                              *
      *  Tilpasset for . :                                              *
      *                                                                 *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.*
      *  --------- ------  ---------------------------------------------*
6.10  *  V6.10 AMD 050511  Antall ble 10 x for stort pga. bruk av MOVE  *
6.nu  *  v6.30 bof 300514  gjennomgått mtp. nummerutvidelser            *
      *                                                                 *
      *                                                                 *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     FLOHEL1    IF   E           K DISK    RENAME(LOHEPFR:LOHEL1R)
     FLODTLL    IF   E           K DISK    RENAME(LODTPFR:LODTLLR)
     FLODTLK    IF   E           K DISK    RENAME(LODTPFR:LODTLKR)
      *
     FLO510D    CF   E             WORKSTN SFILE(D1SFL:SRRN02)
      *
     D                UDS
     D  l_firm               944    946  0
     D  l_fnav               951    980
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Kaller opp subrutine for å vise bestillings-linjer             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C                   MOVE      WPLDOR        D2LDOR
     C                   MOVE      WPNAVN        D2LENA
      *
     C                   EXSR      XD1BLD
      *
     C                   GOTO      XSLUTT
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     XSLUTT        TAG
      *
     C                   SETON                                        LR
     C                   RETURN
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  D1 Behandle bilde for VISE VARELINJER                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     CSR   XD1BLD        BEGSR
      *
      *  Hent opplysninger fra BESTILLING-LINJE-REGISTER
      *
     C                   MOVE      *ZERO         KLLLIN
     C     KLLKEY        SETLL     LODTLL
     C     KLKKEY        SETLL     LODTLK
     C                   EXSR      XCLR02
     C                   EXSR      XCRT02
      *
      *  Send Alt
      *
     C     D2TAGA        TAG
     C                   WRITE     D2CMD
      *
      *  Bare data
      *
     C     D2TAGB        TAG
     C                   EXSR      XDSP02
      *
      *  F1=Vise bla-alternativer
      *
     C     *INKA         IFEQ      *ON
     C                   MOVE      '1'           WRETUR
     C                   CALL      'VA556R'
     C                   PARM                    WRETUR
     C                   PARM                    HEDFIR
     C                   PARM                    WPOTYP
     C                   ENDIF
      *
      *  F3=Avslutt
      *
     C     *INKC         CABEQ     *ON           XD1SLT
      *
      *  F5=Forny
      *
     C     *INKE         IFEQ      *ON
     C                   EXSR      XLSP02
     C                   GOTO      D2TAGB
     C                   ENDIF
      *
      *  Roll
      *
     C     *IN10         IFEQ      *ON
     C                   EXSR      XCRT02
     C                   GOTO      D2TAGB
     C                   ENDIF
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandling av subfile, Foretar valgene                         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *  Sjekk om valg
      *  er foretatt
      *
     C                   DO        SSRN02
     C                   READC     D1SFL                                  16
     C     *IN16         IFEQ      *ON
     C                   LEAVE
     C                   ENDIF
      *
      *  Sjekk alle linjer
      *
     C****>                MOVE D1LINE    KLLLIN
     C****>                EXSR XD7WIN
      *
     C                   ENDDO
      *
     C                   EXSR      XLSP02
     C                   GOTO      D2TAGA
      *
      *  Avslutter behandlingen av ordrelinjer
      *
     CSR   XD1SLT        ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Fornyer subfilen (dvs. loader subfilen pånytt)                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XLSP02        BEGSR
      *
     C     SFRN02        IFNE      *ZERO
     C     SFRN02        CHAIN     D1SFL                              60
     C                   END
      *
     C                   MOVE      D1LINE        KLLLIN
      *
     C     KLLKEY        SETLL     LODTLL
     C     KLKKEY        SETLL     LODTLK
      *
     C                   EXSR      XCLR02
     C                   EXSR      XCRT02
      *
     C                   ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Display subfile                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XDSP02        BEGSR
      *
     C     SRRN02        IFNE      0
     C                   MOVE      *ON           *IN12
     C                   ENDIF
      *
     C                   MOVE      *ON           *IN13
     C                   EXFMT     D2CTL
     C                   MOVE      *OFF          *IN12
     C                   MOVE      *OFF          *IN13
      *
     C                   ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tøm subfile                                                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XCLR02        BEGSR
     C                   MOVE      *ON           *IN14
     C                   WRITE     D2CTL
     C                   MOVE      *OFF          *IN14
      *
     C                   MOVE      *OFF          *IN15
     C                   Z-ADD     0             SRRN02
     C                   Z-ADD     0             SFRN02
     C                   Z-ADD     0             SSRN02
     C                   Z-ADD     0             SPGE02
      *
     C                   ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Fyll opp subfile                                               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XCRT02        BEGSR
     C                   ADD       14            SPGE02
     C                   Z-ADD     SSRN02        SRRN02
      *
     C     SRRN02        DOUEQ     SPGE02
      *
     C     WPVALG        IFEQ      1
     C                   READ      LODTLL                                 15
     C                   ELSE
     C                   READ      LODTLK                                 15
     C                   ENDIF
      *
     C     *IN15         IFEQ      *OFF
     C     LDLDOR        IFNE      KLLDOR
     C                   MOVE      *ON           *IN15
     C                   END
     C                   END
      *
     C     *IN15         IFEQ      *OFF
     C     WPVALG        IFEQ      2
     C     LDVARE        IFEQ      *BLANK
     C                   ITER
     C                   END
     C                   END
     C                   END
      *
     C     *IN15         CABEQ     '1'           XEND02
      *
     C     WPOTYP        IFNE      *BLANK
     C     LDOTYP        IFNE      WPOTYP
     C                   ITER
     C                   END
     C                   END
      *
     C                   ADD       1             SRRN02
     C                   MOVE      LDNUMM        D1NUMM
     C                   MOVE      LDSUFF        D1SUFF
     C                   MOVE      LDLINE        D1LINE
     C                   MOVEL     LDVARE        D1VARE
     C                   MOVEL     LDTEK1        D1TEK1
     C                   MOVE      LDENH1        D1ENH1
6.10 c***>               MOVE      LDANTA        D1ANTA
6.10 c                   z-add     ldanta        d1anta
     c                   eval      d1dato = 0
      *
     C                   MOVE      LDNUMM        HEDNUM
     C                   MOVE      LDSUFF        HEDSUF
     C     HEDKEY        CHAIN     LOHEL1R                            90
     c                   if        ldldat <> *loval
     c     *dmy          move      ldldat        d1dato
     c                   else
     c                   if        loldat <> *loval
     c     *dmy          move      loldat        d1dato
     c                   endif
     c                   endif
      *
     C                   WRITE     D1SFL
     C                   ENDDO
      *
     C     XEND02        TAG
      *
     C     SRRN02        IFGT      SSRN02
     C     SSRN02        ADD       1             SFRN02
     C                   ENDIF
      *
     C                   Z-ADD     SRRN02        SSRN02
     C                   Z-ADD     SFRN02        SVRN02
      *
     C                   ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     *INZSR        BEGSR
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     C                   Z-ADD     0             SPGE01            3 0
     C                   Z-ADD     0             SRRN01            4 0
     C                   MOVE      *BLANK        WSEQEN            1
     C                   MOVE      *BLANK        WRETUR            1
     C                   MOVE      *BLANK        WPENOR            1
     C                   MOVE      *BLANK        WPIPOR            1
     C                   MOVE      *BLANK        WPPLOR            1
      *
     C                   Z-ADD     0             SPGE02            3 0
     C                   Z-ADD     0             SRRN02            4 0
      *
      *  Redefinering av felter
      *
     C     *LIKE         DEFINE    SRRN01        SFRN01
     C     *LIKE         DEFINE    SRRN01        SSRN01
      *
     C     *LIKE         DEFINE    SRRN02        SFRN02
     C     *LIKE         DEFINE    SRRN02        SSRN02
      *
     C     *LIKE         DEFINE    LDLINE        WWLINE
      *
      *  Redefinering av parameterfeltene
      *
     C     *LIKE         DEFINE    LDOTYP        WPOTYP
     C     *LIKE         DEFINE    LDFIRM        WPFIRM
     C     *LIKE         DEFINE    LDLDOR        WPLDOR
      *
      *  Redefinering av nøkler
      *
     C     *LIKE         DEFINE    LOFIRM        HEDFIR
     C     *LIKE         DEFINE    LONUMM        HEDNUM
     C     *LIKE         DEFINE    LOSUFF        HEDSUF
      *
     C     *LIKE         DEFINE    LDFIRM        KLLFIR
     C     *LIKE         DEFINE    LDLDOR        KLLDOR
     C     *LIKE         DEFINE    LDNUMM        KLLNUM
     C     *LIKE         DEFINE    LDSUFF        KLLSUF
     C     *LIKE         DEFINE    LDLINE        KLLLIN
     C     *LIKE         DEFINE    LDVARE        KLLVAR
      *
      *  Key for file : BESTILLING-HODE-REGISTER
      *
     C     HEDKEY        KLIST
     C                   KFLD                    HEDFIR
     C                   KFLD                    HEDNUM
     C                   KFLD                    HEDSUF
      *
      *  Key for file : LEVERANDØR-BESTILLING-LINJE-REGISTER
      *
     C     KLLKEY        KLIST
     C                   KFLD                    KLLFIR
     C                   KFLD                    KLLDOR
     C                   KFLD                    KLLNUM
     C                   KFLD                    KLLSUF
     C                   KFLD                    KLLLIN
      *
      *  Key for file : LEVERANDØR-VARE-BESTILLING-LINJE-REGISTER
      *
     C     KLKKEY        KLIST
     C                   KFLD                    KLLFIR
     C                   KFLD                    KLLDOR
     C                   KFLD                    KLLVAR
     C                   KFLD                    KLLNUM
     C                   KFLD                    KLLSUF
     C                   KFLD                    KLLLIN
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     *ENTRY        PLIST
     C                   PARM                    WPPLOR
     C                   PARM                    WPOTYP
     C                   PARM                    WPVALG            1 0
     C                   PARM                    WPFIRM
     C                   PARM                    WPLDOR
     C                   PARM                    WPNAVN           30
      *
      *  Legger inn firma i nøkklene
      *
     C                   MOVE      WPFIRM        HEDFIR
     C                   MOVE      WPFIRM        KLLFIR
      *
      *  Legger inn leverandør i nøkklene
      *
     C                   MOVE      WPLDOR        KLLDOR
      *
      *  Nullstiller felter
      *
     C                   MOVE      *ZERO         KLLNUM
     C                   MOVE      *ZERO         KLLSUF
     C                   MOVE      *ZERO         KLLLIN
      *
     C                   ENDSR
