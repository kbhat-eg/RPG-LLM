     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JM551R
      * Beskrivelse . . : Spørring/utplukk kampanjevarer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       130398 HKO  Nytt program
7.xx  * 7.00  171116 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  *       120516 bof  utvidet med prisgruppe på va kampanje
8.01  *PRG2   130622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjkavlr    if   e           k disk    rename(jkavpfr:jkavlrr)
     fjkavl1    if   e           k disk    rename(jkavpfr:jkavl1r)
     fjkavl2    if   e           k disk    rename(jkavpfr:jkavl2r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
      *
     fjkavlu    uf a e           k disk    rename(jkavpfr:jkavlur)
      *
     fjm551d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(jmvfir)
     d p_kamp          s                   like(jmvkam)
7.01 d p_prgr          s                   like(jmvprg)
     d p_tkam          s                   like(jmvkam)
     d p_user          s             10
      *
      * Definering av Local data area
      *
     d                uds
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d jkavlr_vvar     s                   like(jmvvar)
     d jkavl1_vvar     s                   like(jmvvar)
     d jkavl2_vldo     s                   like(jmvldo)
     d jkavl2_vvar     s                   like(jmvvar)
     d jvarl1_vare     s                   like(jvvare)
     d jlevl1_ldor     s                   like(jlldor)
     d jkavlu_vkam     s                   like(jmvkam)
7.01 d jkavlu_vprg     s                   like(jmvprg)
     d jkavlu_vvar     s                   like(jmvvar)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
      *
     d w_navn          s                   like(jlnavn)
     d w_tek1          s                   like(jvtek1)
     d w_vare          s                   like(jvvare)
     d w_firm          s                   like(jmvfir)
     d w_kamp          s                   like(jmvkam)
7.01 d w_prgr          s                   like(jmvprg)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile                                  DSP SFL
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2taga
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile                                  CRT SFL
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2vldo <> 0 or
     c                             b2vvar <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'V'
     c                   eval      jkavl1_vvar = b1vvar
     c     jkavl1_key    setll     jkavl1
     c                   other
     c                   eval      jkavl2_vldo = b1vldo
     c                   eval      jkavl2_vvar = b1vvar
     c     jkavl2_key    setll     jkavl2
     c                   endsl
      *
     c                   exsr      clr_subfile                                  TØM SFL
     c                   exsr      crt_subfile                                  CRT SFL
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Leverandørnavn
      *
     c                   if        b2vldo <> 0
     c                   eval      w_seqe = *blank
     c                   eval      jkavl2_vldo = b2vldo
     c                   eval      jkavl2_vvar = b2vvar
     c     jkavl2_key    setll     jkavl2
     c                   goto      end_pos
     c                   endif
      *
      * Varenummer
      *
     c                   if        b2vvar <> *blank
     c                   eval      w_seqe = 'V'
     c                   eval      jkavl1_vvar = b2vvar
     c     jkavl1_key    setll     jkavl1
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile                                  TØM SFL
     c                   exsr      crt_subfile                                  CRT SFL
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2vldo = 0
     c                   eval      b2vvar = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Velg
      *
     c                   if        b1valg = 1
      *
     c                   eval      jkavlu_vkam = p_tkam
7.01 c                   eval      jkavlu_vprg = p_prgr
     c                   eval      jkavlu_vvar = b1vvar
     c     jkavlu_key    chain     jkavlur                            91
     c                   eval      jkavlr_vvar = b1vvar
     c     jkavlr_key    chain     jkavlr                             92
      *
     c                   eval      jmvkam = p_tkam
7.01 c                   eval      jmvprg = p_prgr
     c                   eval      jmveus = p_user
      *
     c                   time                    jmveti
     c                   time                    jmveda
      *
     c                   if        *in91 = *on
     c                   write     jkavlur
     c                   else
     c                   update    jkavlur
     c                   endif
      *
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
      *
     c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil                     SFL END
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   if        w_seqe = 'V'
     c                   read      jkavl1                                 15
     c                   else
     c                   read      jkavl2                                 15
     c                   endif
      *
     c                   if        *in15                                        EOF
     c                   leave
     c                   endif
      *
     c                   if        jmvfir <> w_firm or
     c                             jmvkam <> w_kamp
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'V'
     c                   eval      jkavl1_vvar = jmvvar
     c                   other
     c                   eval      jkavl2_vldo = jmvldo
     c                   eval      jkavl2_vvar = jmvvar
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1vvar = jmvvar
     c                   eval      b1tek1 = *blank
     c                   eval      b1vldo = jmvldo
     c                   eval      b1lnav = *blank
     c                   eval      b1vkai = jmvkai
     c                   eval      b1vkau = jmvkau
     c                   eval      b1vkaa = jmvkaa
      *
      * Henter varetekst-1 og leverandørnavn
      *
     c                   eval      jvarl1_vare = b1vvar
     c     jvarl1_key    chain     jvarl1                             90
     c                   if        *in90 = *off
     c                   eval      b1tek1 = jvtek1
     c                   endif
      *
     c                   if        b1vldo <> 0
     c                   eval      jlevl1_ldor = b1vldo
     c     jlevl1_key    chain     jlevl1                             90
     c                   if        *in90 = *off
     c                   eval      b1lnav = jlnavn
     c                   endif
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'V'
     c     jkavl1_key    setgt     jkavl1
     c                   readp     jkavl1
     c                   other
     c     jkavl2_key    setgt     jkavl2
     c                   readp     jkavl2
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'V'
     c                   readp     jkavl1
     c                   other
     c                   readp     jkavl2
     c                   endsl
      *
     c                   if        %eof or
     c                             jmvfir <> w_firm or
     c                             jmvkam <> w_kamp
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'V'
     c                   eval      jkavl1_vvar = jmvvar
     c                   other
     c                   eval      jkavl2_vldo = jmvldo
     c                   eval      jkavl2_vvar = jmvvar
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'V'
     c     jkavl1_key    setll     jkavl1
     c                   other
     c     jkavl2_key    setll     jkavl2
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Vare
      *
     c                   if        w_afld = 'B2VVAR'
     c                   eval      w_vare = *blank
     c                   eval      w_tek1 = *blank
     c                   call      'JV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_tek1
     c                   if        w_vare <> *blank
     c                   eval      b2vvar = w_vare
     c                   endif
     c                   goto      end_spØrr
     c                   endif
      *
      * Leverandør
      *
     c                   if        w_afld = 'B2VLDO'
     c                   call      'JL500R'
     c                   parm                    b2vldo
     c                   parm                    w_navn
     c                   goto      end_spØrr
     c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_kamp
7.01 c                   parm                    p_prgr
     c                   parm                    p_tkam
     c                   parm                    p_user
      *
      * Key for oppslag på varenummer i kampanje
      *
     c     jkavlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_kamp
7.01 c                   kfld                    w_prgr
     c                   kfld                    jkavlr_vvar
      *
      * Key for posisjonering på leverandør
      *
     c     jkavl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_kamp
     c                   kfld                    jkavl2_vldo
7.01 c                   kfld                    w_prgr
     c                   kfld                    jkavl2_vvar
      *
      * Key for posisjonering på varenummer
      *
     c     jkavl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_kamp
7.01 c                   kfld                    w_prgr
     c                   kfld                    jkavl1_vvar
      *
      * Key for oppslag på vare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for oppdatering av kampanje-vare
      *
     c     jkavlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jkavlu_vkam
7.01 c                   kfld                    jkavlu_vprg
     c                   kfld                    jkavlu_vvar
      *
      *
      * Henter firma og kampanje fra parameter, og fyller opp subfile
      *
     c                   eval      w_firm = p_firm
7.01 c                   eval      w_prgr = p_prgr
     c                   eval      w_kamp = p_kamp
     c                   eval      b2kamp = p_kamp
7.01 c                   eval      b2prgr = p_prgr
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      jkavl2_vldo = 0
     c                   eval      jkavl2_vvar = *blank
     c     jkavl2_key    setll     jkavl2
      *
     c                   eval      *in22 = *on
     c                   exsr      crt_subfile
      *
     c                   endsr
