      /TITLE  ASOFAK Lev.vare, oppdaterer varer/priser - gruppebehandl.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLVAR
      * Program . . . . : FW127R
      * Beskrivelse . . : Oppdatering av varer/priser, gruppebehandling
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       060699 HKO  Nytt program
      * 6.10  230609 BSA  Oppdatert med sporingsinformasjon
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fflval2    if   e           k disk    rename(flvapfr:flval2r)
     fvvarl5    if   e           k disk    rename(vvarpfr:vvarl5r)
     fflvalu    uf   e           k disk    rename(flvapfr:flvalur)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
      *
     ffw127d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
6.10 d l_user                911    920
      *
      * Definering av parametre
      *
     d p_firm          s                   like(fwfirm)
     d p_ldor          s                   like(fwldor)
     d p_ogrp          s                   like(vvogrp)
     d p_hgrp          s                   like(vvhgrp)
     d p_ugrp          s                   like(vvugrp)
     d p_lvaf          s                   like(fwlvar)
     d p_lvat          s                   like(fwlvar)
      *
      * Definering av variable i nøkler
      *
     d flval2_lvar     s                   like(fwlvar)
     d vvarl5_lvar     s                   like(vvlvar)
     d flvalu_line     s                   like(fwline)
     d rlevl1_levr     s                   like(rllevr)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(fwfirm)
     d w_ldor          s                   like(fwldor)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer skjermbildet
      *
     c                   eval      b1ldor = p_ldor
     c                   eval      b1ogrp = p_ogrp
     c                   eval      b1hgrp = p_hgrp
     c                   eval      b1ugrp = p_ugrp
     c                   eval      b1lvaf = p_lvaf
     c                   eval      b1lvat = p_lvat
     c                   eval      b1valh = 0
     c                   eval      b1valf = 0
      *
      * Henter leverandørnavn
      *
     c                   if        p_ldor <> 0
     c                   eval      rlevl1_levr = p_ldor
     c     rlevl1_key    chain     rlevl1                             90
     c                   eval      b1navn = rlnavn
     c                   endif
      *
      * Henter beskrivelse av varegruppe
      *
     c                   if        p_ogrp <> 0
     c                   eval      vogrl1_oogr = p_ogrp
     c     vogrl1_key    chain     vogrl1                             90
     c                   if        *in90 = *off
     c                   eval      b1utxt = vgotxt
     c                   endif
     c                   endif
      *
     c                   if        p_hgrp <> 0
     c                   eval      vhgrl1_hogr = p_ogrp
     c                   eval      vhgrl1_hhgr = p_hgrp
     c     vhgrl1_key    chain     vhgrl1                             90
     c                   if        *in90 = *off
     c                   eval      b1utxt = vghtxt
     c                   endif
     c                   endif
      *
     c                   if        p_ugrp <> 0
     c                   eval      vugrl1_uogr = p_ogrp
     c                   eval      vugrl1_uhgr = p_hgrp
     c                   eval      vugrl1_uugr = p_ugrp
     c     vugrl1_key    chain     vugrl1                             90
     c                   if        *in90 = *off
     c                   eval      b1utxt = vgutxt
     c                   endif
     c                   endif
      *
      * Behandler bildet
      *
     c     b1tag         tag
      *
     c                   exfmt     b1bld
      *
      * Funksjonstaster
      *
     c                   if        *inkc = *on or *inkl = *on
     c                   goto      avslutt
     c                   endif
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1tag
     c                   endif
      *
      * Oppdaterer vare i.h.t. valg
      *
     c                   exsr      oppdater
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
     c                   if        b1valh = 1 and
     c                             b1valf = 1
     c                   eval      b_feil = *on
     c                   eval      *in31 = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av holde-kode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdater      begsr
      *
     c                   if        b1valh = 1 or
     c                             b1valf = 1
      *
     c                   eval      flval2_lvar = p_lvaf
     c     flval2_key    setll     flval2
     c                   read      flval2                                 90
     c                   dow       *in90 = *off
      *
     c                   if        fwfirm <> w_firm or
     c                             fwldor <> w_ldor
     c                   leave
     c                   endif
      *
     c                   if        p_lvat <> *blank and
     c                             fwlvar >  p_lvat
     c                   leave
     c                   endif
      *
     c                   eval      vvarl5_lvar = fwlvar
     c     vvarl5_key    chain     vvarl5                             91
     c                   if        *in91 = *on
     c                   goto      les_neste
     c                   endif
      *
     c                   if        p_ogrp <> 0 and
     c                             p_ogrp <> vvogrp
     c                   goto      les_neste
     c                   endif
      *
     c                   if        p_hgrp <> 0 and
     c                             p_hgrp <> vvhgrp
     c                   goto      les_neste
     c                   endif
      *
     c                   if        p_ugrp <> 0 and
     c                             p_ugrp <> vvugrp
     c                   goto      les_neste
     c                   endif
      *
     c                   eval      flvalu_line = fwline
     c     flvalu_key    chain     flvalur                            91
      *
     c                   if        b1valh = 1
     c                   eval      fwhkod = 1
     c                   else
     c                   eval      fwhkod = 0
     c                   endif
      *
     c                   if        *in91 = *off
6.10 c                   time                    fwedat
6.10 c                   time                    fwetim
6.10 c                   eval      fweusr = l_user
     c                   update    flvalur
     c                   endif
      *
     c     les_neste     tag
      *
     c                   read      flval2                                 90
     c                   enddo
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ldor
     c                   parm                    p_ogrp
     c                   parm                    p_hgrp
     c                   parm                    p_ugrp
     c                   parm                    p_lvaf
     c                   parm                    p_lvat
      *
      * Key for les på leverandør vareregister
      *
     c     flval2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    flval2_lvar
      *
      * Key for oppslag på vareregister
      *
     c     vvarl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    vvarl5_lvar
      *
      * Key for oppdatering av leverandør vareregister
      *
     c     flvalu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    flvalu_line
      *
      * Key for oppslag på leverandør-register
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      *
      * Henter firmanummer og leverandør fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_ldor = p_ldor
      *
     c                   endsr
