     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RP602R
      * Beskrivelse . . : Arb.ordre,spørring
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
6.20  *       181210 NHO  Nytt program
7.01  * K00   060520 bsa  Takler brede skjermer.
7.xx  * K00   080520 bsa  Skrevet om til cloud.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frp4ol1    if   e           k disk    rename(rp4opfr:rp4ol1r)
     frp4ol2    if   e           k disk    rename(rp4opfr:rp4ol2r)
     frp4ol3    if   e           k disk    rename(rp4opfr:rp4ol3r)
     frp4ol4    if   e           k disk    rename(rp4opfr:rp4ol4r)
     frp4ol5    if   e           k disk    rename(rp4opfr:rp4ol5r)
     frp1pl1    if   e           k disk    rename(rp1ppfr:rp1pl1r)
     frp2ul1    if   e           k disk    rename(rp2upfr:rp2ul1r)
     frp3al1    if   e           k disk    rename(rp3apfr:rp3al1r)
     frp602d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av parametre
      *
T5.00d p_firm          s                   like(rp4frm)
     d p_4ord          s                   like(rp4ord)
     d p_proj          s                   like(rp4pro)
     d p_upro          s                   like(rp4upr)
     d p_akti          s                   like(rp4akt)
     d p_txt1          s                   like(rp4txt)
     d p_prt1          s                   like(rp4txt)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
T5.00d rp4ol1_firm     s                   like(rp4frm)
     d rp4ol1_4ord     s                   like(rp4ord)
     d rp4ol2_proj     s                   like(rp4pro)
     d rp4ol2_upro     s                   like(rp4upr)
     d rp4ol2_akti     s                   like(rp4akt)
     d rp4ol2_arbo     s                   like(rp4ord)
     d rp4ol3_alfa     s                   like(rp4alf)
     d rp4ol3_proj     s                   like(rp4pro)
     d rp4ol3_upro     s                   like(rp4upr)
     d rp4ol3_akti     s                   like(rp4akt)
     d rp4ol3_arbo     s                   like(rp4ord)
     d rp4ol4_upro     s                   like(rp4upr)
     d rp4ol4_proj     s                   like(rp4pro)
     d rp4ol4_akti     s                   like(rp4akt)
     d rp4ol4_arbo     s                   like(rp4ord)
     d rp4ol5_akti     s                   like(rp4akt)
     d rp4ol5_proj     s                   like(rp4pro)
     d rp4ol5_upro     s                   like(rp4upr)
     d rp4ol5_arbo     s                   like(rp4ord)
     d rp1pl1_pro      s                   like(rp1pro)
     d rp2ul1_pro      s                   like(rp2pro)
     d rp2ul1_upr      s                   like(rp2upr)
     d rp3al1_pro      s                   like(rp3pro)
     d rp3al1_upr      s                   like(rp3upr)
     d rp3al1_akt      s                   like(rp3akt)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_eof           s              1    inz(*off)
     d b_valg_ok       s              1    inz(*off)
      *
      *
     d uh              c                   'abcdefghijklmnopqrstuvwxyzæøåÆØÅ'
     d lo              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZ²ÔÖ²ÔÖ'
      *
     d w_firm          s                   like(rp4frm)
     d w_idag          s                   like(rp4fed)
     d w_tekstv        s              1    inz('O')
      *
      * Definering av konstanter
      *
E5.50d c_sfil          s              2  0 inz(8)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * w_curn - Current recordnummer på siden
      * w_virn - Vis den side som inneholder denne record.
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        %subst(b2alfa:1:1) = '*'
     c                   select
     c                   when      %subst(b2alfa:1:2) = '*P'
     c                   eval      w_tekstv = 'P'
     c                   when      %subst(b2alfa:1:2) = '*U'
     c                   eval      w_tekstv = 'U'
     c                   when      %subst(b2alfa:1:2) = '*A'
     c                   eval      w_tekstv = 'A'
     c                   other
     c                   eval      w_tekstv = 'O'
     c                   endsl
     c                   eval      b2alfa = *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
     c                   if        b24ord <> *blanks or
     c                             b24pro <> *blank  or
     c                             b24upr <> *blank  or
     c                             b24akt <> *blank  or
     c                             b2alfa <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
     c                   if        b_valg_ok = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_sfrn <> 0
     c     w_sfrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'A'
     c     rp4ol2_key    setll     rp4ol2
     c                   when      w_seqe = 'B'
     c     rp4ol3_key    setll     rp4ol3
     c                   when      w_seqe = 'C'
     c     rp4ol4_key    setll     rp4ol4
     c                   when      w_seqe = 'D'
     c     rp4ol5_key    setll     rp4ol5
     c                   other
     c     rp4ol1_key    setll     rp4ol1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på Arb.ordre
      *
     c                   if        b24ord <> *blank
     c                   eval      w_seqe = ' '
     c                   eval      rp4ol1_4ord = b24ord
     c     rp4ol1_key    setll     rp4ol1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på Prosjekt
      *
     c                   if        b24pro <> *blank
     c                   eval      w_seqe = 'A'
     c                   eval      rp4ol2_proj = b24pro
     c     rp4ol2_key    setll     rp4ol2
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på beskrivelse
      *
     c                   if        b2alfa <> *blank
     c                   eval      w_seqe = 'B'
     c                   eval      rp4ol3_alfa = b2alfa
     c     rp4ol3_key    setll     rp4ol3
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på Underprosjekt
      *
     c                   if        b24upr <> *blank
     c                   eval      w_seqe = 'C'
     c                   eval      rp4ol4_upro = b24upr
     c     rp4ol4_key    setll     rp4ol4
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på Aktivitet
      *
     c                   if        b24akt <> *blank
     c                   eval      w_seqe = 'D'
     c                   eval      rp4ol5_akti = b24akt
     c     rp4ol5_key    setll     rp4ol5
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b24ord = *blanks
     c                   eval      b2alfa = *blank
     c                   eval      b24pro = *blank
     c                   eval      b24upr = *blank
     c                   eval      b24akt = *blank
     c                   eval      b24sts = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Velg
      *
     c                   if        b1valg = 1
      * Hent tekst for prosjekt       ?????????????????????????????????sst
     c                   eval      rp1pl1_pro = b14pro
     c     rp1pl1_key    chain     rp1pl1r                            60
     c                   if        *in60 = *off
     c                   move      rp1txt        p_prt1
     c                   endif
      *
     c                   eval      rp2ul1_pro = rp4pro
     c                   eval      rp2ul1_upr = rp4upr
     c     rp2ul1_key    chain     rp2ul1
     c                   if        %found(rp2ul1) and
     c                             rp2sts = 'A'
     c                   eval      *in43 = *on
     c                   endif
      *
     c                   eval      rp3al1_pro = rp4pro
     c                   eval      rp3al1_upr = rp4upr
     c                   eval      rp3al1_akt = rp4akt
     c     rp3al1_key    chain     rp3al1
     c                   if        %found(rp3al1) and
     c                             rp3sts = 'A'
     c                   eval      *in44 = *on
     c                   endif
      *
     c                   eval      p_4ord = b14ord
     c                   eval      p_txt1 = b14txt
     c                   eval      p_proj = b14pro
     c                   eval      p_upro = b14upr
     c                   eval      p_akti = b14akt
     c                   eval      b_valg_ok = *on
     c                   leave
     c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'A'
     c                   read      rp4ol2                                 15
     c                   when      w_seqe = 'B'
     c                   read      rp4ol3                                 15
     c                   when      w_seqe = 'C'
     c                   read      rp4ol4                                 15
     c                   when      w_seqe = 'D'
     c                   read      rp4ol5                                 15
     c                   other
     c                   read      rp4ol1                                 15
     c                   endsl
      *
     c                   if        %eof or
     c                             rp4frm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
      * Skal slettede poster taes med?
     c                   if        b24sts = 'N' and
     c                             rp4frm = w_firm and
     c                             rp4sts = 'A'
     c                   goto      nyles
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *NINA
     c                   select
     c                   when      w_seqe = 'A'
     c                   eval      rp4ol2_proj = rp4pro
     c                   eval      rp4ol2_upro = rp4upr
     c                   eval      rp4ol2_akti = rp4akt
     c                   eval      rp4ol2_arbo = rp4ord
     c                   when      w_seqe = 'B'
     c                   eval      rp4ol3_alfa = rp4alf
     c                   eval      rp4ol3_proj = rp4pro
     c                   eval      rp4ol3_upro = rp4upr
     c                   eval      rp4ol3_akti = rp4akt
     c                   eval      rp4ol3_arbo = rp4ord
     c                   when      w_seqe = 'C'
     c                   eval      rp4ol4_proj = rp4pro
     c                   eval      rp4ol4_upro = rp4upr
     c                   eval      rp4ol4_akti = rp4akt
     c                   eval      rp4ol4_arbo = rp4ord
     c                   when      w_seqe = 'D'
     c                   eval      rp4ol5_proj = rp4pro
     c                   eval      rp4ol5_upro = rp4upr
     c                   eval      rp4ol5_akti = rp4akt
     c                   eval      rp4ol5_arbo = rp4ord
     c                   other
     c                   eval      rp4ol1_4ord = rp4ord
     c                   endsl
      *Nina slutt
     c                   eval      *in41 = *off
     c                   eval      *in42 = *off
     c                   eval      *in43 = *off
     c                   eval      *in44 = *off
     c                   eval      b1valg = 0
     c                   eval      b14ord = rp4ord
     c                   eval      b14txt = rp4txt
     c                   eval      b14pro = rp4pro
     c                   eval      b14upr = rp4upr
     c                   eval      b14akt = rp4akt
     c                   eval      b14sts = rp4sts
     c                   if        rp4sts ='A'
     c                   eval      *in41 = *on
     c                   endif
     c                   eval      rp1pl1_pro = rp4pro
     c     rp1pl1_key    chain     rp1pl1
     c                   if        %found(rp1pl1) and
     c                             rp1sts = 'A'
     c                   eval      *in42 = *on
     c                   endif
      *
     c                   if        w_tekstv = 'P'
     c                   eval      b14txt = rp1txt
     c                   endif
      *
     c                   if        w_tekstv = 'U'
     c                   eval      rp2ul1_pro = rp4pro
     c                   eval      rp2ul1_upr = rp4upr
     c     rp2ul1_key    chain     rp2ul1
     c                   if        %found(rp2ul1)
     c                   eval      b14txt = rp2txt
     c                   endif
     c                   endif
      *
     c                   if        w_tekstv = 'A'
     c                   eval      rp3al1_pro = rp4pro
     c                   eval      rp3al1_upr = rp4upr
     c                   eval      rp3al1_akt = rp4akt
     c     rp3al1_key    chain     rp3al1
     c                   if        %found(rp3al1)
     c                   eval      b14txt = rp3txt
     c                   endif
     c                   endif
      *
     c                   write     b1sfl
      *
     c     nyles         tag
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   select
     c                   when      w_seqe = 'A'
     c     rp4ol2_key    setll     rp4ol2
     c                   when      w_seqe = 'B'
     c     rp4ol3_key    setll     rp4ol3
     c                   when      w_seqe = 'C'
     c     rp4ol4_key    setll     rp4ol4
     c                   when      w_seqe = 'D'
     c     rp4ol5_key    setll     rp4ol5
     c                   other
     c     rp4ol1_key    setll     rp4ol1
     c                   endsl
      *
      * Leser tilbake en side + posisjoneringsrad
      *
     c                   eval      b_eof  = *off
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = c_sfil + 1
      *
     c                   select
     c                   when      w_seqe = 'A'
     c                   readp     rp4ol2
     c                   when      w_seqe = 'B'
     c                   readp     rp4ol3
     c                   when      w_seqe = 'C'
     c                   readp     rp4ol4
     c                   when      w_seqe = 'D'
     c                   readp     rp4ol5
     c                   other
     c                   readp     rp4ol1
     c                   endsl
      *
     c                   if        %eof or
     c                             rp4frm <> w_firm
     c                   eval      b_eof = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   if        w_stel <= c_sfil
     c                   select
     c                   when      w_seqe = 'A'
     c                   eval      rp4ol2_proj = rp4pro
     c                   eval      rp4ol2_upro = rp4upr
     c                   eval      rp4ol2_akti = rp4akt
     c                   eval      rp4ol2_arbo = rp4ord
     c                   when      w_seqe = 'B'
     c                   eval      rp4ol3_alfa = rp4alf
     c                   eval      rp4ol3_proj = rp4pro
     c                   eval      rp4ol3_upro = rp4upr
     c                   eval      rp4ol3_akti = rp4akt
     c                   eval      rp4ol3_arbo = rp4ord
     c                   when      w_seqe = 'C'
     c                   eval      rp4ol4_proj = rp4pro
     c                   eval      rp4ol4_upro = rp4upr
     c                   eval      rp4ol4_akti = rp4akt
     c                   eval      rp4ol4_arbo = rp4ord
     c                   when      w_seqe = 'D'
     c                   eval      rp4ol5_proj = rp4pro
     c                   eval      rp4ol5_upro = rp4upr
     c                   eval      rp4ol5_akti = rp4akt
     c                   eval      rp4ol5_arbo = rp4ord
     c                   other
     c                   eval      rp4ol1_4ord = rp4ord
     c                   endsl
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_eof = *on
     c                   select
     c                   when      w_seqe = 'A'
     c     rp4ol2_key    setll     rp4ol2
     c                   when      w_seqe = 'B'
     c     rp4ol3_key    setll     rp4ol3
     c                   when      w_seqe = 'C'
     c     rp4ol4_key    setll     rp4ol4
     c                   when      w_seqe = 'D'
     c     rp4ol5_key    setll     rp4ol5
     c                   other
     c     rp4ol1_key    setll     rp4ol1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
T5.00c                   parm                    p_firm
     c                   parm                    p_4ord
     c                   parm                    p_txt1
     c                   parm                    p_proj
     c                   parm                    p_upro
     c                   parm                    p_akti
     c                   parm                    p_prt1
      *
      * Key for kontroll
      *
      *
     c     rp1pl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rp1pl1_pro
      *
     c     rp2ul1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rp2ul1_pro
     c                   kfld                    rp2ul1_upr
      *
     c     rp3al1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rp3al1_pro
     c                   kfld                    rp3al1_upr
     c                   kfld                    rp3al1_akt
      *
     c     rp4ol1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rp4ol1_4ord
      *
      * Key for posisjonering på Prosjekt
      *
     c     rp4ol2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rp4ol2_proj
     c                   kfld                    rp4ol2_upro
     c                   kfld                    rp4ol2_akti
     c                   kfld                    rp4ol2_arbo
      *
      * Key for posisjonering på beskrivelse
      *
     c     rp4ol3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rp4ol3_alfa
     c                   kfld                    rp4ol3_proj
     c                   kfld                    rp4ol3_upro
     c                   kfld                    rp4ol3_akti
     c                   kfld                    rp4ol3_arbo
      *
      * Key for posisjonering på underordre
      *
     c     rp4ol4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rp4ol4_upro
     c                   kfld                    rp4ol4_proj
     c                   kfld                    rp4ol4_akti
     c                   kfld                    rp4ol4_arbo
      *
      * Key for posisjonering på aktivitet
      *
     c     rp4ol5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rp4ol5_akti
     c                   kfld                    rp4ol5_proj
     c                   kfld                    rp4ol5_upro
     c                   kfld                    rp4ol5_arbo
      *
      *
      * Henter parametre, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
     c     *dmy          move      udate         w_idag
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      rp4ol1_4ord = *blanks
     c     rp4ol1_key    setll     rp4ol1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
