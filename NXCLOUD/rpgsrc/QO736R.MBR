     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : QO736R
      * Beskrivelse . . : Plukke ordrenummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *
Q.01  * 7.00  140219 bsa  Quick wins - skrevet om til å bruke 27x132 størrelse
7.01  * 7.00  030619 bsa  Tillater plukking for kontantkunder.
7.02  * 7.00  020221 bsa  Legger opp til utskrift av plukkliste.
7.02  * 7.00              Styres av switch.
      *
8.01  *K0000  081222 bsa  Tar hensyn til om ordren skal splittes
8.02  *K0000  081222 bsa  Stiller tilbake opptatt status før splitting
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     fqo736d    cf   e             workstn
      *
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffohelU    Uf   e           k disk    rename(fohepfr:fohelUr)
     fvinfl1    if   e           k disk    rename(vinfpfr:vinfl1r)
     ffusrl1    if   e           k disk    rename(fusrpfr:fusrl1r)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffloglr    if   e           k disk    rename(flogpfr:floglrr)
      *
      *
      * Definering av variable i nøkler
      *
     d vinfl1_ityp     s                   like(vaityp)
     d fusrl1_user     s                   like(fbuser)
     d rkunl1_kund     s                   like(rkkund)
     d floglr_aarr     s                   like(fuaarr)
     d floglr_numm     s                   like(funumm)
     d floglr_suff     s                   like(fusuff)
      *
      * Definering av variable
      *
     d w_alf1          s              1
     d w_firm          s                   like(fofirm)
     d w_aarr          s              4  0
     d w_date          s               d   datfmt(*iso)
     d p_kode          s              1
     d w_ldat          s                   like(foldat)
7.02 d w_retu          s              1
     d p_urec          s             80
     d p_stat          s              1
     d p_numm          s                   like(fonumm)
     d p_suff          s                   like(fosuff)
     d p_pluk          s              1
      *
     d                 ds
     d d_urec                        80
     d  d_ufir                        3  0 overlay(d_urec)
     d  d_aarr                        4  0 overlay(d_urec:4)
     d  d_unum                        8  0 overlay(d_urec:8)
     d  d_usuf                        2  0 overlay(d_urec:16)
     d  d_ukod                        1    overlay(d_urec:18)
     d  d_date                         d   overlay(d_urec:19) datfmt(*iso)
     d  d_time                         t   overlay(d_urec:29) timfmt(*iso)
     d  d_user                       10    overlay(d_urec:37)
     d  d_wsid                       10    overlay(d_urec:47)
      *
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
7.02 d l_filg                931    933
     d l_firm                944    946  0
      *---------------------------------------------------------------------
      * Stand Alone Fields - TOP
      *---------------------------------------------------------------------
     d hednum          s                   like(fonumm)
     d hedsuf          s                   like(fosuff)
     d otytyp          s                   like(vaotyp)
      *
7.02 d u_702           s              1    inz(*off)                            Endring 7.02
      *
7.02   dcl-s co402_verdi1  char(1);
7.02   dcl-s co402_verdi2  char(2048);
7.02   DCL-PR CO402R EXTPGM('CO402R');
7.02     p_filgrp            char(3)     const;
7.02     p_firm              packed(3:0) const;
7.02     p_lager             packed(2:0) const;
7.02     p_nokkel            char(50)    const;
7.02     p_verdi1            char(1);
7.02     p_verdi2            char(2048);
7.02   END-PR;
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Hent opplysninger fra ORDRE-HODE-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c                   eval      hednum = p_numm
     c                   eval      hedsuf = p_suff
     c     hedkey        chain     fohel1r                            90
      *
      *  Hent opplysninger fra ORDREYTYPE-REGISTER
      *
     c                   move      footyp        otytyp
     c     otykey        chain     votyl1r                            90
      *
      *  Sjekk,  Ordretyper <> akkumulatorkode 1 kan ikke plukkes
      *
     c                   if        vaoakk <> 1
     c                   exfmt     a1msg3
     c                   eval      p_pluk = *off
     c                   goto      avslutt
     c                   endif
      *
      *  Hent opplysninger fra Kunderegister
      *
     c                   eval      rkunl1_kund = fokund
     c     rkunl1_key    chain     rkunl1r
      *
      *
      * Sjekk om det er foretatt plukking på Radioterminal
      *
     c                   exsr      sjk_rt
      *
      *  Sjekk,  Kontantkunde kan ikke plukkes til pakkseddel
      *
7.01 c                   if        1 = 2
     c                   if        rkkres =  2
     c                   exfmt     a1msg2
     c                   eval      p_pluk = *off
     c                   goto      avslutt
     c                   endif
7.01 c                   endif
      *
      *  Sjekk,  Kunde med kredittsprerre kan ikke plukkes til pakkseddel
      *
     c                   if        rkkres =  1
     c                   exfmt     a1msg1
     c                   eval      p_pluk = *off
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      floglr_aarr = %subdt(w_date:*years)
     c                   eval      floglr_numm = fonumm
     c                   eval      floglr_suff = fosuff
     c     floglr_key    setll     floglr
     c     floglr_key    reade     floglr
     c                   dow       not %eof
     c                   if        fukode = '2'
     c                   exfmt     a1msg4
     c                   eval      p_pluk = *off
     c                   goto      avslutt
     c                   endif
     c     floglr_key    reade     floglr
     c                   enddo
      *
      * Vis bide for plukking
      *
     c                   exsr      o1subr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdatering av tilleggsinformasjon ved kjørekontorløsning
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     o1subr        begsr
      *
      * Initierer skjermbilde
      *
7.02 c                   eval      o1pluk = 0
7.02 c                   if        u_702 = *on
7.02 c                   eval      o1pluk = 1
7.02 c                   endif
     c                   eval      o1ldat = 0
     c                   eval      o1inf1 = foinf1
     c                   eval      o1inf2 = foinf2
      *
     c                   if        foldat <> *loval
     c     *dmy          move      foldat        o1ldat
     c                   endif
      *
      * Send bilde o1win
      *
     c     o1tagb        tag
      *
     c                   exfmt     o1win
      *
      * F3=Avslutt
      *
     c                   if        *inkc or *inkl
     c                   eval      *inkc = *on
     c                   leavesr
     c                   endif
      *
      * F4=Spørring
      *
      *
      * Validering
      *
      *
      * Leveringsdato må oppgis
      *
     c                   if        o1ldat = 0
     c                   eval      *in33 = *on
     c                   goto      o1tagb
     c                   endif
      *
      * Leveringsdato må være en gyldig dato
      *
     c     *dmy          test(d)                 o1ldat                 34
     c                   if        *in34 = *on
     c                   goto      o1tagb
     c                   endif
      *
      * Leveringsdato må være større enn/lik ordre-dato
      *
     c     *dmy          move      o1ldat        w_ldat
     c                   if        w_ldat < fobdat
     c                   eval      *in35 = *on
     c                   goto      o1tagb
     c                   endif
      *
      * Oppdaterer ordre
      *
     c     hedkey        chain     fohelur
     c                   if        %found(fohelu)
     c                   eval      foldat = %date(o1ldat:*dmy)
     c                   eval      foinf1 = o1inf1
     c                   eval      foinf2 = o1inf2
8.02  * Tilbakestill kode som sier at ordre er opptatt
8.02 c                   if        fokode = 1
8.02 c                   eval      fokode = 0
8.02 c                   move      *loval        fokoda
8.02 c                   move      *loval        fokoti
8.02 c                   eval      fokous = *blank
8.02 c                   eval      fokows = *blank
8.02 c                   endif
      *
     c                   time                    foedat
     c                   time                    foetim
     c                   eval      foeusr = l_user
     c                   update    fohelur
     c                   endif
      *
      * oppdat. status
      *
     c                   eval      d_ufir = fofirm
     c                   eval      d_unum = fonumm
     c                   eval      d_usuf = fosuff
     c                   eval      d_ukod = '2'
     c                   exsr      upd_logg
      *
8.01  * sjekker om ordre må splittes i flere suffix
8.01  *
8.01 c                   if        faak02 = 1
8.01 c                   call      'FO775R '
8.01 c                   parm                    fofirm
8.01 c                   parm                    fonumm
8.01 c                   parm                    fosuff
8.01 c                   endif
7.02  *
7.02  * Skal plukkliste skrives
7.02  *
7.02 c                   if        u_702 = *on
7.02 c                   if        o1pluk = 1
7.02 c                   eval      w_retu = *blank
7.02 c                   call      'FO600R'
7.02 c                   parm                    w_retu
7.02 c                   parm                    fofirm
7.02 c                   parm                    fonumm
7.02 c                   parm                    fosuff
7.02 c                   endif
7.02  *
7.02  * Sett også automatisk kode 4 for plukking. Koden settes uavhengig av
7.02  * valg om utskrift.
7.02  *
7.02 c                   eval      d_ufir = l_firm
7.02 c                   eval      d_unum = fonumm
7.02 c                   eval      d_usuf = fosuff
7.02 c                   eval      d_ukod = '4'
7.02 c                   exsr      upd_logg
7.02 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å sjekke om det er plukket noe på radioterminal
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_rt        begsr
      *
      *  Sjekker siste status - bare status 7 skal behandles
      *
     c                   eval      p_kode = *blank
     c                   eval      w_aarr = %subdt(fobdat:*years)
     c                   call      'FU706R '
     c                   parm                    w_aarr
     c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_kode
      *
     c                   if        p_kode <> '7'
     c                   goto      end_rt
     c                   endif
      *
     c     end_rt        endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for slette plukk-reg. på RadioTerm.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     upd_logg      begsr
      *
      *
     c                   time                    d_date
     c                   time                    d_time
     c                   eval      d_user = l_user
     c                   eval      d_wsid = l_wsid
     c                   eval      d_aarr = %subdt(fobdat:*years)
     c                   eval      p_urec = d_urec
     c                   call      'FU900R  '
     c                   parm                    p_stat
     c                   parm                    p_urec
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      *  Key for file : ORDRETYPE-REGISTER
      *
     c     otykey        klist
     c                   kfld                    w_firm
     c                   kfld                    otytyp
      *
      *  Key for file : ORDRE-HODE-REGISTER
      *
     c     hedkey        klist
     c                   kfld                    w_firm
     c                   kfld                    hednum
     c                   kfld                    hedsuf
      *
      * Key for file : KUNDE-REGISTER
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på ordre-info-type
      *
     c     vinfl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vinfl1_ityp
      *
      * Key for brukerregister
      *
     c     fusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fusrl1_user
      *
      * Key for ofak kode-register
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på loggfil
      *
     c     floglr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    floglr_aarr
     c                   kfld                    floglr_numm
     c                   kfld                    floglr_suff
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *entry        plist
     c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_pluk
      *
      *  Legger inn firma i nøkklene
      *
     c                   eval      w_firm = l_firm
      *
      * Hente inn brukerregister
      *
     c                   eval      fusrl1_user = l_user
     c     fusrl1_key    chain     fusrl1r                            90
      *
      * Hente inn ofak kode-register
      *
     c     fstsl1_key    chain     fstsl1r                            90
      *
     c                   time                    w_date
7.02  *
7.02  * Endring 7.02 - kjøre forenklet plukkeprosess
7.02  *
7.02   *in57 = *off;
7.02   CallP CO402R(l_filg:w_firm:0:'FORENKLET_PLUKKING':
7.02                    co402_verdi1:co402_verdi2);
7.02   if co402_verdi1 = '1';
7.02     *in57 = *on;
7.02     u_702 = *on;
7.02   endif;
      *
     c                   endsr
