     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
     fra05l1    if   e           k disk    rename(ra05pfr:ra05l1r)
     f                                     prefix(F:1)
     fra05pf    if   e           k disk    rename(ra05pfr:ra05pfr)
     f                                     prefix(S:1)
     fra05lu    uf a e           k disk    rename(ra05pfr:ra05lur)
      *
      *  Dette programmet henter data fra ADBxxx/ra05PF m/raefir = Firma
      *  NB!!!  RA05pf skal overstyres til RA05PS (Ny standard)
      *
     d                uds
     d l_firm                944    946  0
     d l_user                911    920
      *
     d ra05l1_fir      s                       like(raefir)
     d ra05l1_kod      s                       like(raekod)
     d ra05pf_fir      s                       like(raefir)
     d ra05pf_kod      s                       like(raekod)
     d ra05lu_fir      s                       like(raefir)
     d ra05lu_kod      s                       like(raekod)
      *
     d w_iso_dat       s               d   datfmt(*iso)
     d w_firm          s              3S 0
     d w_fx            s              3
     d w_mx            s              4
     d w_bx            s              2
      *
     d w_m1            s             50    inz('Momskode X finnes før på +
     d                                     firma = fff, mva% nn,nn,')
     d w_m2            s             50    inz('og beh.kode nn. Vil du +
     d                                     kjøre videre og oppdatere?')
     d w_sv            s              1
     d w_in12          s              1  0
      *
     c     start         tag
     c                   eval      ra05l1_fir = *zero
     c                   eval      ra05l1_kod = *blank
     c     ra05l1_key    setll     ra05l1
     c                   read      ra05l1
      *      Behandle alle firmaer i til gruppe
     c                   dow       not %eof(ra05l1)
     c                   exsr      behfirm
      *      Les forbi alle andre koder på dette firma
     c                   eval      w_firm = faefir
     c                   dow       not %eof(ra05l1) and
     c                             faefir = w_firm
     c                   read      ra05l1
     c                   enddo
      *
     c                   enddo
      *
     c                   eval      *inlr = *on
     c                   return
      *
     c     BehFirm       begsr
      *
     c                   eval      ra05pf_fir = *zero
     c                   eval      ra05pf_kod = *blank
     c     ra05pf_key    setll     ra05pf
     c                   read      ra05pf
      *
     c                   dow       not %eof(ra05pf)
     c                   eval      ra05lu_fir = faefir
     c                   eval      ra05lu_kod = saekod
     c     ra05lu_key    chain     ra05lu
     c                   if        %found(ra05lu)
     c                   if        raepro <> saepro or
     c                             raemda <> saemda or
     c                             raegpr <> saegpr or
     c                             raebmk <> saebmk or
     c                             raebpr <> saebpr or
     c                             raepda <> saepda or
     c                             raebgp <> saebgp
      *    Gi feilmelding om at kode finnes fra før med andre opplysninger
     c                   exsr      feilmsg
     c                   if        w_in12 = 1
     c                   eval      *inlr = *on
     c                   return
     c                   endif
     c                   if        w_sv = 'J'
     c                   eval      raetxt = saetxt
     c                   eval      raepro = saepro
     c                   eval      raemda = saemda
     c                   eval      raegpr = saegpr
     c                   eval      raebmk = saebmk
     c                   eval      raebpr = saebpr
     c                   eval      raepda = saepda
     c                   eval      raebgp = saebgp
     c                   eval      raedat = saedat
     c                   eval      raetim = saetim
     c                   eval      raeusr = saeusr
     c                   update    ra05lur
     c                   endif
     c                   goto      nextlu
     c                   endif
     c                   else
     c                   eval      raefir = faefir
     c                   eval      raekod = saekod
     c                   eval      raetxt = saetxt
     c                   eval      raepro = saepro
     c                   eval      raemda = saemda
     c                   eval      raegpr = saegpr
     c                   eval      raebmk = saebmk
     c                   eval      raebpr = saebpr
     c                   eval      raepda = saepda
     c                   eval      raebgp = saebgp
     c                   eval      raedat = saedat
     c                   eval      raetim = saetim
     c                   eval      raeusr = saeusr
     c                   write     ra05lur
     c                   endif
     c     nextlu        tag
     c                   read      ra05pf
     c                   enddo
      *
     c                   endsr
      *
      *
      *
     c     feilmsg       begsr
      *
     c                   eval      %subst(w_m1:10:1) = raekod
     c                   move      raefir        w_fx
     c                   eval      %subst(w_m1:34:3) = w_fx
     c                   move      raepro        w_mx
     c                   eval      %subst(w_m1:44:2) = %subst(w_mx:1:2)
     c                   eval      %subst(w_m1:47:2) = %subst(w_mx:3:2)
     c                   move      raebmk        w_bx
     c                   eval      %subst(w_m2:13:2) = w_bx
      *
     c                   call      'AX018R'
     c                   parm                    w_m1
     c                   parm                    w_m2
     c                   parm                    w_sv
     c                   parm                    w_in12
      *
     c                   endsr
      *
      * ----------------------------------------------------------------
      *
     c     *inzsr        begsr
      *
     c     ra05l1_key    klist
     c                   kfld                    ra05l1_fir
     c                   kfld                    ra05l1_kod
      *
     c     ra05pf_key    klist
     c                   kfld                    ra05pf_fir
     c                   kfld                    ra05pf_kod
      *
     c     ra05lu_key    klist
     c                   kfld                    ra05lu_fir
     c                   kfld                    ra05lu_kod
      *
     c                   endsr
      *
