     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JR754R
      * Beskrivelse . . : Leser fil på recordformat JERNIA (Jernia),
      *                   og oppretter/oppdaterer priser på eksisterende
      *                   varer i LVR.
      *
      *                   Dersom en ny vare matcher mot Nobbnr eller
      *                   EAN-nummer i LVR opprettes prisen på prisgivende
      *                   leverandør på varen.
      *
      *                   Prisoppdatering skjer på bakgrunn av match på
      *                   leverandørs varenummer.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       111099 HKO  Nytt program
      *       301101 HKO  Hopper over rader uten endringskode
      *                   Unngår forsendelsesrad fra Jernia
      *                   Tilpasset nytt AS2-format
      *       270803 HKO  Lagt inn test på Nobbnummer ved oppretting av pris
      *                   samt fjernet test på mottaksfilter
      *       201003 HKO  Tilpasset nytt Jernia-format
      *       020605 HKO  Legger ut EAN-nummer på vare-pris
      *       140905 HKO  Kaller program for oppdatering av søketekst
6.11  *       201009 bof  Oppdater vare-pakning-prisgiver med ean-nr
6.12  *       120510 bof  Lagt på eann på lev.pris inntil videre-skal utgå
6.20  *       091210 bof  Søker gtin på vare-pakning-prisgiver  (samme som 6.13)
6.21  *       280314 bof  Takle blank i gtin                           som 6.13)
6.30  * 6.30  260113 bof  Omskriving i forb. endring pakning VA
6.31  * 6.30  110714 bof  endret nøkkel i varepris - dato + lvar til jvlepf
6.32  * 6.30  270315 bof  Hvis pakn.mangler for prisg.,dann ut fra vareeier(6.31 i BMP)
6.33  * 6.30  151216 bof  Endrer slik at man rydder pris,før ny
7.01  * 7.00  251119 bsa  Legger ut et regnark med priser som ikke blir funnet
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjrvapf    if   e           k disk
      *
     fjrapl1    if   e           k disk    rename(jrappfr:jrapl1r)
6.31 fjvlel2    if   e           k disk    rename(jvlepfr:jvlel2r)
     fjvarl3    if   e           k disk    rename(jvarpfr:jvarl3r)
6.20 fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjvarld    if   e           k disk    rename(jvarpfr:jvarldr)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fjvprlu    uf a e           k disk    rename(jvprpfr:jvprlur)
6.31 fjvlelu    uf a e           k disk    rename(jvlepfr:jvlelur)
6.30 fjvpkl2    uf   e           k disk    rename(jvpkpfr:jvpkl2r)
6.32 fjvpklu    uf a e           k disk    rename(jvpkpfr:jvpklur)
6.30 fjvpkl3    if   e           k disk    rename(jvpkpfr:jvpkl3r)
7.01 flwexpf    uf a e           k disk    rename(lexcpfr:lwexpfr)
      *
      * Definering av parametre
      *
     d p_list          s             32
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
      *
      * Definering av parameterliste -inn
      *
     d                 ds
     d d_list                        32
     d  d_firm                        3  0 overlay(d_list)
     d  d_rapp                        2  0 overlay(d_list:4)
     d  d_dato                         d   overlay(d_list:6) datfmt(*iso)
      *
      * Definering av recordbeskrivelse - Jernia
      *
     d                 ds
     d d_inpu                       259
     d  d_ekod                        1    overlay(d_inpu)
     d  d_vare                       15    overlay(d_inpu:2)
     d  d_tek1                       30    overlay(d_inpu:17)
     d  d_enhe                        3    overlay(d_inpu:47)
     d  d_lvar                       20    overlay(d_inpu:50)
     d  d_ldor                        8    overlay(d_inpu:70)
     d  d_vgr1                        2    overlay(d_inpu:78)
     d  d_vgr2                        4    overlay(d_inpu:80)
     d  d_vgr3                        7    overlay(d_inpu:84)
     d**d_eann                       15  0 overlay(d_inpu:91)
6.20 d  d_eann                       14  0 overlay(d_inpu:92)
6.21 d  d_eannx                      14    overlay(d_inpu:92)
     d  d_ean2                       15  0 overlay(d_inpu:106)
     d  d_ean3                       15  0 overlay(d_inpu:121)
     d  d_ean4                       15  0 overlay(d_inpu:136)
     d  d_nobb                        8    overlay(d_inpu:151)
     d  d_pend                        1    overlay(d_inpu:159)
     d  d_inpr                       10  2 overlay(d_inpu:160)
     d  d_saim                       10  2 overlay(d_inpu:170)
     d  d_antp                       10  0 overlay(d_inpu:180)
     d  d_akod                        1    overlay(d_inpu:190)
     d  d_sort                        4    overlay(d_inpu:191)
     d  d_evar                       15    overlay(d_inpu:195)
     d  d_eean                       15  0 overlay(d_inpu:210)
     d  d_enob                        8    overlay(d_inpu:225)
     d  d_raba                        5  2 overlay(d_inpu:233)
     d  d_fdat                        6  0 overlay(d_inpu:238)
     d  d_tdat                        6  0 overlay(d_inpu:244)
     d  d_crdo                       10    overlay(d_inpu:250)
      *
      * Definering av variable i nøkkler
      *
     d jrapl1_rapp     s                   like(jrrapp)
6.31 d jvlel2_lldo     s                   like(jvlldo)
6.31 d jvlel2_llva     s                   like(jvllva)
     d jvarl3_nobb     s                   like(jvnobb)
6.20 d jvarl1_vare     s                   like(jvvare)
     d jvarld_ean1     s                   like(jvean1)
     d jlevl1_ldor     s                   like(jlldor)
     d jvprlu_vare     s                   like(jxvare)
     d jvprlu_ldor     s                   like(jxldor)
6.31 d jvlelu_lvnr     s                   like(jvlvnr)
6.31 d jvlelu_lldo     s                   like(jvlldo)
6.30 d jvpkl3_gtin     s                   like(jwgtin)
6.11 d jvpkl2_vare     s                   like(jwvare)
6.30 d jvpkl2_ldor     s                   like(jwldor)
6.11 d jvpkl2_enhe     s                   like(jwenhe)
      *
      * Definering av variable
      *
     d b_inlr          s              3    inz(*off)
      *
     d w_firm          s              3  0
      *
     d w_pris          s                   like(jxpris)
7.01 d w_ex            s            512
      *
      * Definering av konstanter
      *
     d Lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d Up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter rapportør-informasjon
      *
     c                   eval      jrapl1_rapp = d_rapp
     c     jrapl1_key    chain     jrapl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
      * Henter leverandørinformasjon
      *
     c                   eval      jlevl1_ldor = jrldor
     c     jlevl1_key    chain     jlevl1
      *
     c                   eval      jlnavn = %trim(jlnavn)
     c     Lo:Up         xlate     jlnavn        jlnavn
      *
      * Behandling
      *
     c                   exsr      behandling
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av varer/priser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
     c                   read      jrvapf
     c                   dow       not %eof(jrvapf)
      *
     c                   eval      d_inpu = jrvrad
      *
     c                   if        d_ekod = *blank or
     c                             d_vare = '00000000'
     c                   goto      les_neste
     c                   endif
      *
6.21  *
6.21  * for sikkerhets skyld,settes blanke i eann til 0
6.21  *
6.21 c     ' ':'0'       xlate     d_eannx       d_eannx
      *
     c                   eval      jxvare = *blank
      *
6.33 c*                  eval      jvlel2_lldo = jrldor
6.33 c*                  eval      jvlel2_llva = d_vare
6.33 c*    jvlel2_key    chain     jvlel2
6.33 c*                  if        %found
6.33 c*                  exsr      endr_pris
6.33 c*                  else
     c                   exsr      oppr_pris
6.33 c*                  endif
6.32  *
6.32  * sjekker om pakning finnes, hvis ikke dann
6.32  *
6.32 c                   exsr      sjekk_pakn
      *
6.11  * danner gtin på vare-pakning-prisgiver
6.11  *
6.11 c                   if        d_eann <> 0 and
6.11 c                             d_enhe <> *blank
6.11 c                   exsr      dann_gtin
6.11 c                   endif
      *
      * Oppdaterer søketekst
      *
     c                   if        jxvare <> *blank
     c                   eval      b_inlr = *on
     c                   call      'JV790R'
     c                   parm                    w_firm
     c                   parm                    jxvare
     c                   parm                    b_inlr
     c                   endif
      *
     c     les_neste     tag
      *
     c                   read      jrvapf
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting av pris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppr_pris     begsr
      *
     c                   if        d_nobb = *blank and
     c                             d_eann = 0
     c                   leavesr
     c                   endif
      *
     c                   if        d_nobb <> *blank
     c                   move      d_nobb        jvarl3_nobb
     c     jvarl3_key    chain     jvarl3
     c                   if        not %found and
     c                             d_eann = 0
7.01 c                   exsr      regneark
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        d_eann <> 0
     c                   if        d_nobb = *blank or
     c                             not %found(jvarl3)
     c                   eval      jvarld_ean1 = d_eann
     c     jvarld_key    chain     jvarld
     c                   if        not %found
6.20  * prøver gtin på paknig
6.30 c                   eval      jvpkl3_gtin = d_eann
6.30 c     jvpkl3_key    chain     jvpkl3
6.20 c                   if        not %found
7.01 c                   exsr      regneark
6.20 c                   leavesr
6.20 c                   endif
6.30 c                   eval      jvarl1_vare = jwvare
6.20 c     jvarl1_key    chain     jvarl1
6.20 c                   if        not %found
6.20 c                   leavesr
6.20 c                   endif
6.20 c                   endif
      *
     c                   endif
     c                   endif
      *
      * Sletter eventuelt eksisterende priser for leverandør på varen
      *
     c                   eval      jvprlu_vare = jvvare
     c                   eval      jvprlu_ldor = jrldor
     c     jvprlu_key    setll     jvprlu
     c     jvprlu_key    reade     jvprlur
     c                   dow       not %eof
     c                   delete    jvprlur
     c     jvprlu_key    reade     jvprlur
     c                   enddo
      *
      * Oppretter pris på vare for leverandør
      *
     c                   clear                   jvprlur
      *
     c                   eval      jxvare = jvvare
     c                   eval      jxldor = jrldor
6.31 c*                  eval      jxlvar = d_vare
6.31 c                   movel     d_vare        jxlvar
     c                   eval      jxpris = d_inpr
     c                   eval      jxgdat = d_dato
     c                   eval      jxtdat = *loval
     c                   eval      jxvalu = *blank
6.12 c                   eval      jxeann = d_eann
     c                   eval      jxcrdo = 0
     c                   eval      jxrsts = 1
     c                   eval      jxeusr = l_user
      *
     c                   time                    jxodat
     c                   time                    jxedat
     c                   time                    jxetim
      *
     c                   if        d_crdo = 'CD'
     c                   eval      jxcrdo = 1
     c                   endif
      *
     c                   write     jvprlur
6.31  *
6.31  * Sletter eventuelt vare-leverandør
6.31  *
6.31 c                   eval      jvlelu_lvnr = jvvare
6.31 c                   eval      jvlelu_lldo = jrldor
6.31 c     jvlelu_key    setll     jvlelu
6.31 c     jvlelu_key    reade     jvlelur
6.31 c                   dow       not %eof
6.31 c                   delete    jvlelur
6.31 c     jvlelu_key    reade     jvlelur
6.31 c                   enddo
6.31  *
6.31  * Oppretter vare leverandør
6.31  *
6.31 c                   clear                   jvlelur
6.31  *
6.33 c                   eval      jvlvnr = jvvare
6.31 c                   eval      jvlldo = jrldor
6.33 c                   movel     d_vare        jvllva
6.31 c                   eval      jvleus = l_user
6.31 c                   eval      jvlous = l_user
6.31  *
6.31 c                   time                    jvloda
6.31 c                   time                    jvleda
6.31 c                   time                    jvloti
6.31 c                   time                    jvleti
6.31 c                   write     jvlelur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for endring av pris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.33  * I 6.30 oppdateres ikke prisen, den slettes først og så
6.33  * dannes ny. Ettersom man ikke får en god styring på til.dato
6.33  * så vil det bli liggende 1 gyldig pris
6.33  *
6.33 c*    endr_pris     begsr
6.33  *
6.33 c*                  eval      w_pris = d_inpr
6.33  *
6.33 c*                  if        w_pris = jxpris
6.33 c*                  leavesr
6.33 c*                  endif
6.33  *
6.33 c*                  eval      jvprlu_vare = jxvare
6.33 c*                  eval      jvprlu_ldor = jrldor
6.33 c*    jvprlu_key    chain     jvprlur
6.33  *
6.33 c*                  eval      jxlvar = d_vare
6.33 c*                  movel     d_vare        jxlvar
6.33 c*                  eval      jxpris = w_pris
6.33 c*                  eval      jxgdat = d_dato
6.33 c*                  eval      jxtdat = *loval
6.33 c*                  eval      jxeann = d_eann
6.33 c*                  eval      jxcrdo = 0
6.33 c*                  eval      jxeusr = l_user
6.33  *
6.33 c*                  time                    jxedat
6.33 c*                  time                    jxetim
6.33  *
6.33 c*                  if        d_crdo = 'CD'
6.33 c*                  eval      jxcrdo = 1
6.33 c*                  endif
6.33  *
6.33 c*                  update    jvprlur
6.33  *
6.33 c*                  endsr
6.32  *
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  * Subrutine for å evt. å danne pakning på prisgiver.
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  *
6.32 c     sjekk_pakn    begsr
6.32 c                   eval      jvarl1_vare = jxvare
6.22 c     jvarl1_key    chain     jvarl1
6.22 c                   if        not %found
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32  * leser vare-pakning for å se at den finnes
6.32  *
6.32 c                   eval      jvpkl2_vare = jvvare
6.32 c                   eval      jvpkl2_ldor = jrldor
6.32 c     jvpkl2_key    chain     jvpkl2
6.32 c                   if        %found
6.32 c                   leavesr
6.32 c                   endif
6.32  * leser vareeiers pakning
6.32 c                   eval      jvpkl2_vare = jvvare
6.32 c                   eval      jvpkl2_ldor = jvldor
6.32 c     jvpkl2_key2   setll     jvpkl2
6.32 c     jvpkl2_key2   reade     jvpkl2
6.32 c                   dow       not %eof(jvpkl2)
6.32 c                   eval      jwldor = jrldor
6.32 c                   eval      jwfdat = *loval
6.32 c                   eval      jwtdat = *loval
6.32 c                   time                    jwodat
6.32 c                   eval      jweusr  = l_user
6.32 c                   time                    jwedat
6.32 c                   time                    jwetim
6.32 c                   write     jvpklur
6.32  *
6.32 c     jvpkl2_key2   reade     jvpkl2
6.32 c                   enddo
6.32  *
6.32 C                   endsr
6.11  *
6.11  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.11  * Subrutine for å danne gtin på vare-pakning-prisgiver
6.11  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.11  *
6.11 c     dann_gtin     begsr
6.11  *
6.11  * leser vare-pakning for å se at den finnes
6.11  *
6.11 c                   eval      jvpkl2_vare = jvvare
6.30 c                   eval      jvpkl2_ldor = jrldor
6.11 c                   eval      jvpkl2_enhe = d_enhe
6.11 c     jvpkl2_key    chain     jvpkl2
6.30 c                   eval      jwgtin = d_eann
6.30 c                   eval      jwrsts = 1
6.30 c                   time                    jwedat
6.30 c                   time                    jwetim
6.30 c                   eval      jweusr = l_user
6.30 c                   if        %found(jvpkl2)
6.30 c                   update    jvpkl2r
6.30 c                   endif
6.11  *
6.11 c                   endsr
      *
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  * Subrutine for å skrive ut regneark på priser som ikke blir oppdatert
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *
7.01 c     regneark      begsr
7.01  *
7.01 c                   eval      w_Ex = *blank
7.01 c                   eval      w_ex = %trim(d_nobb) + X'05' +
7.01 c                                    %trim(d_lvar) + X'05' +
7.01 c                                    %trim(d_tek1) + X'05' +
7.01 c                                    %char(d_eann) + X'05'
7.01 c                   eval      exclin   = w_ex
7.01 c                   write     lwexpfr
7.01  *
7.01 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_list
      *
      * Key for oppslag på rapportør
      *
     c     jrapl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jrapl1_rapp
      *
      * Key for oppslag på vare-pris
      *
6.31 c     jvlel2_key    klist
6.31 c                   kfld                    jvlel2_lldo
6.31 c                   kfld                    jvlel2_llva
6.31  *
6.31  * Key for oppdatering av vare-pris
6.31  *
6.31 c     jvlelu_key    klist
6.31 c                   kfld                    jvlelu_lvnr
6.31 c                   kfld                    jvlelu_lldo
      *
      * Key for oppdatering av vare-pris
      *
     c     jvprlu_key    klist
     c                   kfld                    jvprlu_vare
     c                   kfld                    jvprlu_ldor
      *
      * Key for oppslag på vare
      *
     c     jvarl3_key    klist
     c                   kfld                    jvarl3_nobb
6.20  *
6.20 c     jvarl1_key    klist
6.20 c                   kfld                    jvarl1_vare
      *
     c     jvarld_key    klist
     c                   kfld                    jvarld_ean1
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
6.20  *
6.20  * Key for les gtin på  vare-pakning-prisgiver
6.20  *
6.30 c     jvpkl3_key    klist
6.30 c                   kfld                    jvpkl3_gtin
6.11  *
6.11  * Key for les av vare-pakning
6.11  *
6.11 c     jvpkl2_key    klist
6.11 c                   kfld                    jvpkl2_vare
6.30 c                   kfld                    jvpkl2_ldor
6.11 c                   kfld                    jvpkl2_enhe
6.32  *
6.32  * Key for les av vare-pakning
6.32  *
6.32 c     jvpkl2_key2   klist
6.32 c                   kfld                    jvpkl2_vare
6.32 c                   kfld                    jvpkl2_ldor
      *
      *
      * Fyller datastruktur fra parameter, og henter firma
      *
     c                   eval      d_list = p_list
     c                   eval      w_firm = d_firm
      *
     c                   endsr
