      /TITLE  ASLAGR: Lagertelling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LE602R
      *  Beskrivelse . . : Utskrift av telle-liste, plukker varenummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign
      * --------- ------  --------------------------------------------------
      *           070900  Programmet er helt omskrevet ang.        ask
      *                   logikk for utplukk.
5.70  * PRGR  231008 sst  Prisgruppe
6.10  *       281211 bof  Rettet feil vedr. antlager på skaffevare
6.20  * 6.20      060511  Lagt inn nye listevalg i telleliste      ask
6.21  * 6.20  190511 bof  Henter utg. fra lager - utvidet paramtre
6.22  * 6.20  050413 ask  Manglet uvidelse av parm i et kall til VL710R
6.30  * 6.30  170918 bkv  Utvidet med trelast sortiment
8.01  *PRG2   150622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fra10l1    if   e           k disk    rename(ra10pfr:ra10l1r)
     flpstlu    uf a e           k disk    rename(lpstpfr:lpstlur)
6.30 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
      * Definering av array
      *
     d a_lage          s              2    dim(100)
      *
      * Datastruktur for parameterliste
      *
     d                 ds
     d d_prec                       128
     d  d_batc                       10    overlay(d_prec)
     d  d_fvar                       15    overlay(d_prec:11)
     d  d_tvar                       15    overlay(d_prec:26)
     d  d_fogr                        2  0 overlay(d_prec:41)
     d  d_togr                        2  0 overlay(d_prec:43)
     d  d_fhgr                        2  0 overlay(d_prec:45)
     d  d_thgr                        2  0 overlay(d_prec:47)
     d  d_fugr                        3  0 overlay(d_prec:49)
     d  d_tugr                        3  0 overlay(d_prec:52)
     d  d_fpla                        8    overlay(d_prec:55)
     d  d_tpla                        8    overlay(d_prec:63)
     d  d_flag                        2  0 overlay(d_prec:71)
     d  d_tlag                        2  0 overlay(d_prec:73)
     d  d_list                        1  0 overlay(d_prec:75)
     d  d_hovd                        1  0 overlay(d_prec:76)
     d  d_val1                        1  0 overlay(d_prec:77)
     d  d_val2                        1  0 overlay(d_prec:78)
     d  d_val3                        1  0 overlay(d_prec:79)
     d  d_opda                        6  0 overlay(d_prec:80)
     d  d_vtyp                        1    overlay(d_prec:86)
     d  d_pans                        4    overlay(d_prec:87)
     d  d_levr                        6  0 overlay(d_prec:92)
      *
      *  Local Data Area
      *
     d                uds
     d l_wsid                901    910
     d l_user                        10
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d lpstlu_lage     s                   like(lzlage)
     d lpstlu_vare     s                   like(lzvare)
     d vlagl1_lage     s                   like(vllage)
     d vlagl1_vare     s                   like(vlvare)
     d vvarl1_vare     s                   like(vvvare)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d ra10l1_kode     s                   like(rajkod)
      *
      * Definering av variable
      *
     d w_opda          s               d   datfmt(*iso)
     d w_firm          s                   like(lzfirm)
     d w_lage          s                   like(vllage)
     d w_plas          s                   like(vlplas)
     d w_vare          s                   like(vvvare)
     d w_parm          s            128
     d w_anta          s                   like(lzantl)
     d w_ltel          s              3  0
     d w_sort          s             60
     d w_ogr_alf       s              2
     d w_hgr_alf       s              2
     d w_ugr_alf       s              3
     d w_lag_alf       s              2
     d w_var_alf       s             15
6.20 d w_lvr_alf       s              6
5.70 d w_vtyp          s                   like(vvvtyp)
6.21 d w_utda          s               d   datfmt(*iso)
6.21 d w_ldor          s                   like(vvldor)
6.30 d w_lv_nobb       s                   like(vvlnob)
6.30 d w_lv_modn       s                   like(vvlmod)
6.30 d w_lv_lldo       s                   like(vvlnle)
6.30 d w_lv_llva       s                   like(vvllva)
      *
6.21 d b_inlr          s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Behandle utplukk, Setter inn riktige parameterverdier
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      d_prec = w_parm
      *
     c                   if        d_opda <> 0
     c     *dmy          move      d_opda        w_opda
     c                   endif
      *
      *  Sjekk mot eksisterende poster i lager?
      *
     c                   if        d_list = 0
     c                   exsr      lag_tab
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Leser fra vare-register
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      vvarl1_vare = d_fvar
     c                   eval      w_vare = d_tvar
     c     vvarl1_key    setll     vvarl1r
      *
     c                   read      vvarl1r                                90
      *
     c                   dow       *in90 = *off
      *
      *  Er firmanummer innenfor grensene, Ellers hopp til Slutt
      *
     c                   if        vvfirm <> w_firm
     c                   leave
     c                   endif
      *
      *  Sjekk   Er varenummer innenfor grensene, Ellers hopp til Slutt
      *
     c                   if        vvvare > w_vare
     c                   leave
     c                   endif
      *
      *  Sjekk   Er overgruppe innenfor grensene  Hvis nei les neste post
      *
     c                   if        vvogrp < d_fogr or
     c                             vvogrp > d_togr
     c                   goto      neste_vare
     c                   endif
      *
      *  Sjekk   Er hovedgruppe innenfor grensene  Hvis nei les neste post
      *
     c                   if        vvhgrp < d_fhgr or
     c                             vvhgrp > d_thgr
     c                   goto      neste_vare
     c                   endif
      *
      *  Sjekk   Er Undergruppe innenfor grensene  Hvis nei les neste post
      *
     c                   if        vvugrp < d_fugr or
     c                             vvugrp > d_tugr
     c                   goto      neste_vare
     c                   endif
      *
      * Seleksjon på produktansvarlig?
      *
     c                   if        d_pans <> *blank
      *
     c                   eval      vugrl1_uogr = vvogrp
     c                   eval      vugrl1_uhgr = vvhgrp
     c                   eval      vugrl1_uugr = vvugrp
     c     vugrl1_key    chain     vugrl1                             95
      *
     c                   if        *in95 = *off and
     c                             vgupra = d_pans
     c                   goto      pans_ok
     c                   endif
      *
     c                   eval      vhgrl1_hogr = vvogrp
     c                   eval      vhgrl1_hhgr = vvhgrp
     c     vhgrl1_key    chain     vhgrl1                             95
      *
     c                   if        *in95 = *off and
     c                             vghpra = d_pans
     c                   goto      pans_ok
     c                   endif
      *
     c                   eval      vogrl1_oogr = vvogrp
     c     vogrl1_key    chain     vogrl1                             95
      *
     c                   if        *in95 = *off and
     c                             vgopra = d_pans
     c                   goto      pans_ok
     c                   endif
      *
     c                   goto      neste_vare
      *
     c                   endif
      *
     c     pans_ok       tag
      *
      *  Sjekk   Er Varetype valgt og er den evt gyldig. "T" utelukkes.
      *
5.70  *  Flyttet til etter les av lager for og finne denne pr lager
5.70 c*                  if        vvvtyp = 'T'
5.70 c*                  goto      neste_vare
5.70 c*                  endif
5.70  *  Flyttet til etter les av lager for og finne denne pr lager
5.70 c*                  if        d_vtyp <> *blank and
5.70 c*                            vvvtyp <> d_vtyp
5.70 c*                  goto      neste_vare
5.70 c*                  endif
      *
      *  Sjekk   Er Leverandør valgt og er det den riktige?
      *
     c                   if        d_levr <> 0 and
     c                             vvldor <> d_levr
     c                   goto      neste_vare
     c                   endif
      *
      *  Legg ut rader for alle lagre eller bare de som finnes
      *
     c                   if        d_list = 0
     c                   exsr      lag_alle
     c                   else
     c                   exsr      lag_finnes
     c                   endif
     c                   exsr      sorsub
      *
      *  Velg neste Vare-register post
      *
     c     neste_vare    tag
     c                   read      vvarl1r                                90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine som legger ut rader for alle aktuelle lagre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lag_alle      begsr
      *
     c                   eval      vlagl1_vare = vvvare
     c                   eval      w_ltel = 1
     c                   dow       a_lage(w_ltel) <> *blank
     c                   move      a_lage(w_ltel)w_lage
     c                   eval      vlagl1_lage = w_lage
      *
     c     vlagl1_key    chain     vlagl1                             93
     c                   if        *in93 = *off
     c                   eval      w_anta = vlbehl
     c                   else
     c                   eval      w_anta = 0
     c                   endif
5.70  *
5.70  * Kaller program for henting av varetype
5.70  *
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    vvvare
5.70 c                   parm                    vllage
5.70 c                   parm                    w_vtyp
6.21 c                   parm                    w_utda
6.21 c                   parm                    w_ldor
6.21 c                   parm                    b_inlr
6.30 c                   parm                    w_lv_nobb
6.30 c                   parm                    w_lv_modn
6.30 c                   parm                    w_lv_lldo
6.30 c                   parm                    w_lv_llva
5.70 c                   if        w_vtyp = 'T'
5.70 c                   goto      end_alle
5.70 c                   endif
5.70  *  Flyttet hit for å hente type fra dette lager og vare
5.70 c                   if        d_vtyp <> *blank and
5.70 c                             w_vtyp <> d_vtyp
5.70 c                   goto      end_alle
5.70 c                   endif
      *
      *  Er Lagerplass innenfor grensene?
      *
     c                   if        d_fpla <> *blank and
     c                             d_tpla <> *blank
     c                   if        vlplas < d_fpla or
     c                             vlplas > d_tpla
     c                   goto      end_alle
     c                   endif
     c                   endif
      *
      * Er bare null eller negativt valgt?
      *
     c                   if        d_val2 = 1 and
     c                             w_anta > -0.001
     c                   goto      end_alle
     c                   endif
      *
      * Finn sorteringsbegrep og skriv rad
      *
     c                   exsr      sorsub
     c                   exsr      skriv_rad
     c                   eval      w_anta = 0
     c                   eval      w_ltel = w_ltel + 1
     c                   enddo
      *
     c     end_alle      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine som legger ut rader for lagre som finnes
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lag_finnes    begsr
      *
     c                   eval      vlagl1_vare = vvvare
     c                   eval      vlagl1_lage = d_flag
     c     vlagl1_key    setll     vlagl1
      *
     c                   read      vlagl1                                 94
      *
     c                   dow       *in94 = *off and
     c                             vlfirm = w_firm and
     c                             vlvare = vvvare and
     c                             vllage <= d_tlag
      *
      *  Er Lagerplass innenfor grensene?
      *
     c                   if        d_fpla <> *blank and
     c                             d_tpla <> *blank
     c                   if        vlplas < d_fpla or
     c                             vlplas > d_tpla
     c                   goto      end_finnes
     c                   endif
     c                   endif
      *
      * Er bare null eller negativt valgt?
      *
     c                   if        d_val2 = 1 and
     c                             w_anta > -0.001
     c                   goto      end_finnes
     c                   endif
5.70  *
5.70  * Kaller program for henting av varetype
5.70  *
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    vvvare
5.70 c                   parm                    vllage
5.70 c                   parm                    w_vtyp
6.22 c                   parm                    w_utda
6.22 c                   parm                    w_ldor
6.22 c                   parm                    b_inlr
6.30 c                   parm                    w_lv_nobb
6.30 c                   parm                    w_lv_modn
6.30 c                   parm                    w_lv_lldo
6.30 c                   parm                    w_lv_llva
5.70 c                   if        w_vtyp = 'T'
5.70 c                   goto      end_finnes
5.70 c                   endif
5.70  *  Flyttet hit for å hente type fra dette lager og vare
5.70 c                   if        d_vtyp <> *blank and
5.70 c                             w_vtyp <> d_vtyp
5.70 c                   goto      end_finnes
5.70 c                   endif
      *
      * Finn sorteringsbegrep og skriv rad
      *
     c                   eval      w_anta = vlbehl
     c                   eval      w_lage = vllage
      *
     c                   exsr      sorsub
     c                   exsr      skriv_rad
     c                   eval      w_anta = 0
      *
     c                   read      vlagl1                                 94
     c                   enddo
      *
     c     end_finnes    endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine som oppdaterer uplukkspost
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_rad     begsr
      *
6.10 c                   clear                   lpstlur
     c                   eval      lpstlu_vare = vvvare
     c                   eval      lpstlu_lage = w_lage
     c     lpstlu_key    chain     lpstlur                            92
      *
     c                   eval      lzvare = vvvare
     c                   eval      lztek1 = vvtek1
     c                   eval      lzenh1 = vvenhl
     c                   eval      lzlage = w_lage
     c                   eval      lzogrp = vvogrp
     c                   eval      lzhgrp = vvhgrp
     c                   eval      lzugrp = vvugrp
     c                   eval      lzplas = vlplas
5.70 c                   if        w_vtyp = 'L'
     c                   eval      lzantl = w_anta
6.10 c                   else
6.10 c                   eval      lzantl = 0
     c                   endif
     c                   eval      lzsort = w_sort
      *
     c                   if        *in92 = *off
     c                   update    lpstlur
     c                   else
     c                   eval      lzfirm = w_firm
     c                   eval      lzvare = lpstlu_vare
     c                   eval      lzlage = lpstlu_lage
     c                   write     lpstlur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine som legger inn sorteringsbegrep
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * - - - - - - - - - - - - - - - - - - - - - - - *
      *  Hva betyr de enkelte valg                    *
      *                                               *
      *  Valg1   Lager/vgr/varenummer                 *
6.20  *  Valg2   Lager/vgr/varetekst                  *
6.20  *  Valg3   Lager/varetekst                      *
6.20  *  Valg4   Lagerplass/varetekst                 *
6.20  *  Valg5   Lager/vgr/varetekst/lagerplass       *
6.20  *  Valg6   Lager/lev/vgr/varetekst/lagerplass   *
      * - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     sorsub        begsr
      *
      *  Flytter sorteringsbegreper til alfa-felter
      *
     c                   move      vvogrp        w_ogr_alf
     c                   move      vvhgrp        w_hgr_alf
     c                   move      vvugrp        w_ugr_alf
     c                   move      w_lage        w_lag_alf
     c                   move      vvvare        w_var_alf
6.20 c                   move      vvldor        w_lvr_alf
      *
      *  Sorteringsrekkefølge
      *
     c                   if        d_val3 = 1
     c                   eval      w_sort = w_lag_alf + w_ogr_alf +
     c                                      w_hgr_alf + w_ugr_alf +
     c                                      w_var_alf
     c                   endif
      *
6.20 c                   if        d_val3 = 2
6.20 c                   eval      w_sort = w_lag_alf + w_ogr_alf +
6.20 c                                      w_hgr_alf + w_ugr_alf +
6.20 c                                      vvtek1
     c                   endif
      *
6.20 c                   if        d_val3 = 3
6.20 c                   eval      w_sort = w_lag_alf + vvtek1
6.20 c                   endif
      *
6.20 c                   if        d_val3 = 4
6.20 c                   eval      w_sort = vlplas + vvtek1
6.20 c                   endif
      *
     c                   if        d_val3 = 5
6.20 c                   eval      w_sort = w_lag_alf + w_ogr_alf +
6.20 c                                      w_hgr_alf + w_ugr_alf +
6.20 c                                      vvtek1 + vlplas
     c                   endif
      *
     c                   if        d_val3 = 6
6.20 c                   eval      w_sort = w_lag_alf +
6.20 c                                      w_lvr_alf + w_ogr_alf +
6.20 c                                      w_hgr_alf + w_ugr_alf +
6.20 c                                      vvtek1 + vlplas
     c                   endif
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine som finner aktuelle lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lag_tab       begsr
      *
     c                   eval      w_ltel = 1
     c                   eval      a_lage(*) = *blank
     c                   eval      ra10l1_kode = d_flag
     c     ra10l1_key    setll     ra10l1
      *
     c                   read      ra10l1                                 91
     c                   dow       *in91 = *off and
     c                             rajfir = w_firm and
     c                             rajkod <= d_tlag
     c                   move      rajkod        a_lage(w_ltel)
     c                   eval      w_ltel = w_ltel + 1
     c                   read      ra10l1                                 91
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for les av VARE
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *  Key for les av LAGER
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl1_vare
     c                   kfld                    vlagl1_lage
      *
      *  Key for les av overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      *  Key for les av hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      *  Key for les av undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for lager-kode
      *
     c     ra10l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra10l1_kode
      *
      *  Key for oppdatering av LAGERUTSKRIFT-HJELPE-REGISTER
      *
     c     lpstlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lpstlu_vare
     c                   kfld                    lpstlu_lage
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     *entry        plist
     c                   parm                    w_parm
      *
      *  Legge inn firmanummer i nøklene
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
