     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FR601R
      * Beskrivelse . . : Rabattmatrise, utskrift av matrise - parameter
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       070599 HKO  Nytt program
      *       200300 HKO  Lagt inn leverandør som nivå i rabattmatrisen
      *                   Endret testing av input
      *       170204 HKO  Erstattet rabattgruppe med varegruppe og modul
5.70  * PRGR  211008 bof  Utvidet med prisgruppe                       l
7.xx  * 7.00  010416 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
8.01  *PRG2   100622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffr601d    cf   e             workstn
      *
      * Definering av LDA
      *
     d                uds
     d l_firm                944    946  0
      *
      * Datastruktur for parameterliste -ut
      *
     d                 ds
     d d_list                       120
     d  d_firm                        3  0 overlay(d_list:  1)
     d  d_rkaf                        3  0 overlay(d_list:  4)
     d  d_rkat                        3  0 overlay(d_list:  7)
     d  d_kkaf                        4  0 overlay(d_list: 10)
     d  d_kkat                        4  0 overlay(d_list: 14)
     d  d_kunf                        6  0 overlay(d_list: 18)
     d  d_kunt                        6  0 overlay(d_list: 24)
     d  d_kprf                        6  0 overlay(d_list: 30)
     d  d_kprt                        6  0 overlay(d_list: 36)
     d  d_ogrf                        2  0 overlay(d_list: 42)
     d  d_hgrf                        2  0 overlay(d_list: 44)
     d  d_ugrf                        3  0 overlay(d_list: 46)
     d  d_ogrt                        2  0 overlay(d_list: 49)
     d  d_hgrt                        2  0 overlay(d_list: 51)
     d  d_ugrt                        3  0 overlay(d_list: 53)
     d  d_ldof                        6  0 overlay(d_list: 56)
     d  d_ldot                        6  0 overlay(d_list: 62)
     d  d_modf                        8  0 overlay(d_list: 68)
     d  d_modt                        8  0 overlay(d_list: 76)
     d  d_varf                       15    overlay(d_list: 84)
     d  d_vart                       15    overlay(d_list: 99)
8.01 d  d_prgr                        2    overlay(d_list:114)
     d  d_dumm                        1    overlay(d_list:120) inz('*')
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s              3  0
     d w_rtxt          s             30
     d w_ktxt          s             30
     d w_knav          s             30
     d w_pnav          s             30
     d w_gtxt          s             35
     d w_lnav          s             30
     d w_mtxt          s             35
     d w_vare          s             15
     d w_vtxt          s             35
     d w_vgrf          s              7  0
     d w_vgrt          s              7  0
      *
     d w_stmp          s               z
     d w_stmp_alf      s             26
     d w_memb          s             10
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Behandling av skjermbilde
      *
     c     b1taga        tag
      *
     c                   exfmt     b1bld
      *
      * F3=Avslutt
      *
     c                   if        *inkc or *inkl
     c                   goto      avslutt
     c                   endif
      *
      * F4=Spørring
      *
     c                   if        *inkd = *on
     c                   exsr      spØrring
     c                   goto      b1taga
     c                   endif
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1taga
     c                   endif
      *
     c                   exsr      flytt_par
      *
      * Kaller utskriftsprogram
      *
     c                   call      'FR601C'
     c                   parm                    d_list
     c                   parm                    w_memb
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørringer (FD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Rabattkategori
      *
     c                   if        w_afld = 'B1RKAF'
     c                   call      'RA506R'
     c                   parm                    w_firm
     c                   parm                    b1rkaf
     c                   parm                    w_rtxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'B1RKAT'
     c                   call      'RA506R'
     c                   parm                    w_firm
     c                   parm                    b1rkat
     c                   parm                    w_rtxt
     c                   leavesr
     c                   endif
      *
      * Kundekategori
      *
     c                   if        w_afld = 'B1KKAF'
     c                   call      'RA511R'
     c                   parm                    w_firm
     c                   parm                    b1kkaf
     c                   parm                    w_ktxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'B1KKAT'
     c                   call      'RA511R'
     c                   parm                    w_firm
     c                   parm                    b1kkat
     c                   parm                    w_ktxt
     c                   leavesr
     c                   endif
      *
      * Kunde
      *
     c                   if        w_afld = 'B1KUNF'
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    b1kunf
     c                   parm                    w_knav
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'B1KUNT'
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    b1kunt
     c                   parm                    w_knav
     c                   leavesr
     c                   endif
      *
      * Kundeprosjekt
      *
     c                   if        w_afld = 'B1KPRF'
     c                   call      'FL500R'
     c                   parm                    w_firm
     c                   parm                    b1kunf
     c                   parm                    b1kprf
     c                   parm                    w_knav
     c                   parm                    w_pnav
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'B1KPRT'
     c                   call      'FL500R'
     c                   parm                    w_firm
     c                   parm                    b1kunt
     c                   parm                    b1kprt
     c                   parm                    w_knav
     c                   parm                    w_pnav
     c                   leavesr
     c                   endif
      *
      * Overgruppe
      *
     c                   if        w_afld = 'B1OGRF'
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b1ogrf
     c                   parm                    w_gtxt
     c                   leavesr
     c                   endif                                                           .....
      *
     c                   if        w_afld = 'B1OGRT'
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b1ogrt
     c                   parm                    w_gtxt
     c                   leavesr
     c                   endif                                                           .....
      *
      * Hovedgruppe
      *
     c                   if        w_afld = 'B1HGRF'
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    b1ogrf
     c                   parm                    b1hgrf
     c                   parm                    w_gtxt
     c                   leavesr
     c                   endif                                                           .....
      *
     c                   if        w_afld = 'B1HGRT'
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    b1ogrt
     c                   parm                    b1hgrt
     c                   parm                    w_gtxt
     c                   leavesr
     c                   endif                                                           .....
      *
      * Undergruppe
      *
     c                   if        w_afld = 'B1UGRF'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    b1ogrf
     c                   parm                    b1hgrf
     c                   parm                    b1ugrf
     c                   parm                    w_gtxt
     c                   leavesr
     c                   endif                                                           .....
      *
     c                   if        w_afld = 'B1UGRT'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    b1ogrt
     c                   parm                    b1hgrt
     c                   parm                    b1ugrt
     c                   parm                    w_gtxt
     c                   leavesr
     c                   endif                                                           .....
      *
      * Leverandør
      *
     c                   if        w_afld = 'B1LDOF'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    b1ldof
     c                   parm                    w_lnav
     c                   leavesr
     c                   endif                                                           .....
      *
     c                   if        w_afld = 'B1LDOT'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    b1ldot
     c                   parm                    w_lnav
     c                   leavesr
     c                   endif                                                           .....
      *
      * Modul
      *
     c                   if        w_afld = 'B1MODF'
     c                   call      'FO707C'
     c                   parm                    w_firm
     c                   parm                    b1modf
     c                   parm                    w_mtxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'B1MODT'
     c                   call      'FO707C'
     c                   parm                    w_firm
     c                   parm                    b1modt
     c                   parm                    w_mtxt
     c                   leavesr
     c                   endif
      *
      * Vare
      *
     c                   if        w_afld = 'B1VARF'
     c                   movel     b1varf        w_vare
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_vtxt
     c                   movel     w_vare        b1varf
     c                   leavesr
     c                   endif                                                           .....
      *
     c                   if        w_afld = 'B1VART'
     c                   movel     b1vart        w_vare
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_vtxt
     c                   movel     w_vare        b1vart
     c                   leavesr
     c                   endif                                                           .....
5.70  *
5.70  * Prisgruppe
5.70  *
5.70 c                   if        w_afld = 'B1PRGR'
5.70 c                   call      'RA530R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    b1prgr
5.70 c                   parm                    w_rtxt
5.70 c                   leavesr
5.70 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
      * Rabattkategori
      *
     c                   if        b1rkaf <> 0 and
     c                             b1rkat <> 0 and
     c                             b1rkaf > b1rkat
     c                   eval      *in31 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      * Kundekategori
      *
     c                   if        b1kkaf <> 0 and
     c                             b1kkat <> 0 and
     c                             b1kkaf > b1kkat
     c                   eval      *in32 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      * Kunde
      *
     c                   if        b1kunf <> 0 and
     c                             b1kunt <> 0 and
     c                             b1kunf > b1kunt
     c                   eval      *in33 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      * Kundeprosjekt
      *
     c                   if        b1kprf <> 0 and
     c                             b1kprt <> 0 and
     c                             b1kprf > b1kprt
     c                   eval      *in34 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      * Varegruppe
      *
     c                   eval      w_vgrf = b1ogrf * 100000 +
     c                                      b1hgrf * 1000 + b1ugrf
     c                   eval      w_vgrt = b1ogrt * 100000 +
     c                                      b1hgrt * 1000 + b1ugrt
      *
     c                   if        w_vgrf <> 0 and
     c                             w_vgrt <> 0 and
     c                             w_vgrf > w_vgrt
     c                   eval      *in35 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      * Leverandør
      *
     c                   if        b1ldof <> 0 and
     c                             b1ldot <> 0 and
     c                             b1ldof > b1ldot
     c                   eval      *in36 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      * Modul
      *
     c                   if        b1modf <> 0 and
     c                             b1modt <> 0 and
     c                             b1modf > b1modt
     c                   eval      *in37 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      * Vare
      *
     c                   if        b1varf <> 0 and
     c                             b1vart <> 0 and
     c                             b1varf > b1vart
     c                   eval      *in38 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for flytting av felter til DS-parametre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     flytt_par     begsr
      *
     c                   eval      d_firm = w_firm
      *
5.70 c                   eval      d_prgr = b1prgr
     c                   eval      d_rkaf = b1rkaf
     c                   eval      d_kkaf = b1kkaf
     c                   eval      d_kunf = b1kunf
     c                   eval      d_kprf = b1kprf
     c                   eval      d_ogrf = b1ogrf
     c                   eval      d_hgrf = b1hgrf
     c                   eval      d_ugrf = b1ugrf
     c                   eval      d_ldof = b1ldof
     c                   eval      d_modf = b1modf
      *
     c                   if        b1rkat = 0
     c                   eval      d_rkat = b1rkaf
     c                   else
     c                   eval      d_rkat = b1rkat
     c                   endif
      *
     c                   if        b1kkat = 0
     c                   eval      d_kkat = b1kkaf
     c                   else
     c                   eval      d_kkat = b1kkat
     c                   endif
      *
     c                   if        b1kunt = 0
     c                   eval      d_kunt = b1kunf
     c                   else
     c                   eval      d_kunt = b1kunt
     c                   endif
      *
     c                   if        b1kprt = 0
     c                   eval      d_kprt = b1kprf
     c                   else
     c                   eval      d_kprt = b1kprt
     c                   endif
      *
     c                   if        b1ogrt = 0
     c                   eval      d_ogrt = b1ogrf
     c                   else
     c                   eval      d_ogrt = b1ogrt
     c                   endif
      *
     c                   if        b1hgrt = 0
     c                   eval      d_hgrt = b1hgrf
     c                   else
     c                   eval      d_hgrt = b1hgrt
     c                   endif
      *
     c                   if        b1ugrt = 0
     c                   eval      d_ugrt = b1ugrf
     c                   else
     c                   eval      d_ugrt = b1ugrt
     c                   endif
      *
     c                   if        b1ldot = 0
     c                   eval      d_ldot = b1ldof
     c                   else
     c                   eval      d_ldot = b1ldot
     c                   endif
      *
     c                   if        b1modt = 0
     c                   eval      d_modt = b1modf
     c                   else
     c                   eval      d_modt = b1modt
     c                   endif
      *
     c                   if        b1varf = 0
     c                   eval      d_varf = *blank
     c                   else
     c                   movel     b1varf        d_varf
     c                   endif
      *
     c                   if        b1vart = 0
     c                   eval      d_vart = d_varf
     c                   else
     c                   movel     b1vart        d_vart
     c                   endif
      *
      * Lager membernavn sammensatt av 'W' + timestamp
      *
     c                   time                    w_stmp
      *
     c                   move      w_stmp        w_stmp_alf
     c                   eval      w_memb = 'W' +
     c                                      %subst(w_stmp_alf:12:2) +
     c                                      %subst(w_stmp_alf:15:2) +
     c                                      %subst(w_stmp_alf:18:2) +
     c                                      %subst(w_stmp_alf:21:3)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
