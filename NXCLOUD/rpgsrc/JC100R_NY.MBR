     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JC100R
      * Beskrivelse . . : Påslag, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       190899 HKO  Nytt program
      *       181003 HKO  Lagt inn modul som nivå, og tillatt leverandør
      *                   uten varegruppe som et nivå
      *       100605 HKO  Lagt inn varetype som nivå, og tillater utvidede
      *                   kombinasjoner
      *       030105 bof  test på datofelter
5.70  * PRGR  161108 bof  Utvidet med prisgruppe
6.11  *       050811 bof  Feilretting vedr. spørring på varegrupper.
6.20  *       050511 bof  utvidet med - bruk hovedlev. salgspris
6.21  *       250511 bsa  Feil felttype
6.30  *       030715 bof  Feilretting vedr. søk på prisgruppe/overgruppe
6.31  *       130416 nho  Feilretting vedr. søk på prisgruppe/overgruppe
6.31  *                   MÅ også ta setll med de andre verdiene dersom
6.31  *                   de er utfyllt
6.32  * 6.30  011118 bof  Utvidet nytt pkt 8, vise varer under modul
6.33  * 6.30  011118 bof  Utvidet med filter
6.34  * 6.30  131218 bof  Kun ekte nobbnr, ikke logistikk-varer
6.35  * 6.30  171218 bof  Hvis filter, så sett på farge
7.xx  * 7.00  290716 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  *       190421 bof  NXS8236 utvidet verdi på jchlsp 0,1 og 2.         *
8.01  *       211221 bof  NXS9551 utvidet med nobbpris fra i nøkkel + felter
8.02  *       280422 bof  Bare påslag fra nobb skal synes (nobbpris ikke impl)
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       21 = Home
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjpfalr    if   e           k disk    rename(jpfapfr:jpfalrr)
     fjpfalu    uf a e           k disk    rename(jpfapfr:jpfalur)
     fjpfal1    if   e           k disk    rename(jpfapfr:jpfal1r)
     fjpfal2    if   e           k disk    rename(jpfapfr:jpfal2r)
     fjpfal3    if   e           k disk    rename(jpfapfr:jpfal3r)
     fjpfal4    if   e           k disk    rename(jpfapfr:jpfal4r)
     fjpfal5    if   e           k disk    rename(jpfapfr:jpfal5r)
     fjpfal6    if   e           k disk    rename(jpfapfr:jpfal6r)
     fjpfal7    if   e           k disk    rename(jpfapfr:jpfal7r)
     fjpfal8    if   e           k disk    rename(jpfapfr:jpfal8r)
     fjpfal9    if   e           k disk    rename(jpfapfr:jpfal9r)
5.70 fjpfalc    if   e           k disk    rename(jpfapfr:jpfalcr)
     fjpfala    if   e           k disk    rename(jpfapfr:jpfalar)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fjmodl1    if   e           k disk    rename(jmodpfr:jmodl1r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fvpkol1    if   e           k disk    rename(vpkopfr:vpkol1r)
     fvvtyl1    if   e           k disk    rename(vvtypfr:vvtyl1r)
     fvltpl1    if   e           k disk    rename(vltppfr:vltpl1r)
5.70 fra30l1    if   e           k disk    rename(ra30pfr:ra30l1r)
      *
     fjc100d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variable i nøkler
      *
5.70 d jpfalr_prgr     s                   like(jcprgr)
     d jpfalr_ogrp     s                   like(jcogrp)
     d jpfalr_hgrp     s                   like(jchgrp)
     d jpfalr_ugrp     s                   like(jcugrp)
     d jpfalr_ldor     s                   like(jcldor)
     d jpfalr_modn     s                   like(jcmodn)
     d jpfalr_vare     s                   like(jcvare)
     d jpfalr_kpri     s                   like(jckpri)
     d jpfalr_vtyp     s                   like(jcvtyp)
     d jpfalr_lety     s                   like(jclety)
8.01 d jpfalr_nofr     s                   like(jcnofr)
5.70 d jpfalu_prgr     s                   like(jcprgr)
     d jpfalu_ogrp     s                   like(jcogrp)
     d jpfalu_hgrp     s                   like(jchgrp)
     d jpfalu_ugrp     s                   like(jcugrp)
     d jpfalu_ldor     s                   like(jcldor)
     d jpfalu_modn     s                   like(jcmodn)
     d jpfalu_vare     s                   like(jcvare)
     d jpfalu_kpri     s                   like(jckpri)
     d jpfalu_vtyp     s                   like(jcvtyp)
     d jpfalu_lety     s                   like(jclety)
8.01 d jpfalu_nofr     s                   like(jcnofr)
5.70 d jpfal1_prgr     s                   like(jcprgr)
     d jpfal1_ogrp     s                   like(jcogrp)
     d jpfal1_hgrp     s                   like(jchgrp)
     d jpfal1_ugrp     s                   like(jcugrp)
     d jpfal1_ldor     s                   like(jcldor)
     d jpfal1_modn     s                   like(jcmodn)
     d jpfal1_vare     s                   like(jcvare)
     d jpfal1_kpri     s                   like(jckpri)
     d jpfal1_vtyp     s                   like(jcvtyp)
     d jpfal1_lety     s                   like(jclety)
8.01 d jpfal1_nofr     s                   like(jcnofr)
     d jpfal2_hgrp     s                   like(jchgrp)
     d jpfal3_ugrp     s                   like(jcugrp)
     d jpfal4_ldor     s                   like(jcldor)
     d jpfal5_modn     s                   like(jcmodn)
     d jpfal6_vare     s                   like(jcvare)
     d jpfal7_kpri     s                   like(jckpri)
     d jpfal8_vtyp     s                   like(jcvtyp)
     d jpfal9_lety     s                   like(jclety)
5.70 d jpfalc_ogrp     s                   like(jcogrp)
6.31 d jpfalc_hgrp     s                   like(jchgrp)
6.31 d jpfalc_ugrp     s                   like(jcugrp)
5.70 d jpfala_prgr     s                   like(jcprgr)
     d jpfala_vare     s                   like(jcvare)
     d jpfala_ldor     s                   like(jcldor)
     d jpfala_kpri     s                   like(jckpri)
     d jpfala_vtyp     s                   like(jcvtyp)
     d jpfala_lety     s                   like(jclety)
8.01 d jpfala_nofr     s                   like(jcnofr)
     d jlevl1_ldor     s                   like(jlldor)
     d jmodl1_modn     s                   like(jumodn)
     d jvarl1_vare     s                   like(jvvare)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d vpkol1_pkod     s                   like(vapkod)
     d vvtyl1_vtyp     s                   like(vavtyp)
     d vltpl1_lety     s                   like(vylety)
     d ra30l1_dkod     s                   like(redkod)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(jcfirm)
5.70 d w_prgr          s                   like(jcprgr)
     d w_ogrp          s                   like(jcogrp)
     d w_hgrp          s                   like(jchgrp)
     d w_ugrp          s                   like(jcugrp)
     d w_ldor          s                   like(jcldor)
     d w_modn          s                   like(jcmodn)
     d w_vare          s                   like(jcvare)
     d w_kpri          s                   like(jckpri)
     d w_vtyp          s                   like(jcvtyp)
     d w_lety          s                   like(jclety)
      *
5.70 d w_gtxt          s                   like(redtxt)
     d w_otxt          s                   like(vgotxt)
     d w_htxt          s                   like(vghtxt)
     d w_utxt          s                   like(vgutxt)
     d w_lnav          s                   like(jlnavn)
     d w_mte1          s                   like(jumte1)
     d w_tek1          s                   like(jvtek1)
     d w_ptxt          s                   like(vaptxt)
     d w_ttxt          s                   like(vavtxt)
     d w_lbes          s                   like(vybesk)
6.33 d w_teller        s              8  0
6.33 d w_lv_nobb       s                   like(jvnobb)
6.34 d w_lv_vare       s                   like(jvvare)
6.3d d w_undv          s              8  0
6.3d d w_logl          s             15
6.3d d w_jvqvnr        s                   like(jcvare)
6.33 d w_jvquno        s              8  0
      *
5.70 d s_prgr          s                   like(jcprgr)
     d s_ogrp          s                   like(jcogrp)
     d s_hgrp          s                   like(jchgrp)
     d s_ugrp          s                   like(jcugrp)
     d s_ldor          s                   like(jcldor)
     d s_modn          s                   like(jcmodn)
     d s_vare          s                   like(jcvare)
     d s_kpri          s                   like(jckpri)
     d s_vtyp          s                   like(jcvtyp)
     d s_lety          s                   like(jclety)
8.01 d s_nofr          s                   like(jcnofr)
      *
      * Definering av konstanter
      *
8.01 d c_sfil          s              2  0 inz(15)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for å opprette ny rad
      * C1MSG  - C1 Meleingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.33 C/Exec SQL
6.33 C+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR
6.33 C/End-Exec
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
6.33 c                   when      *inka
6.33 c                   exsr      xf1win
6.33 c                   exsr      forny
6.33 c                   goto      b2taga
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2tagb
     c                   when      *inke
     c                   exsr      forny
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
5.70 c                   if        b2prgr <> *blank or
5.70 c                             b2ogrp <> 0 or
     c                             b2hgrp <> 0 or
     c                             b2ugrp <> 0 or
     c                             b2ldor <> 0 or
     c                             b2modn <> 0 or
     c                             b2vare <> *blank or
     c                             b2kpri <> *blank or
     c                             b2vtyp <> *blank or
     c                             b2lety <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'H'
     c                   eval      jpfal2_hgrp = b1hgrp
     c     jpfal2_key    setll     jpfal2
     c                   when      w_seqe = 'U'
     c                   eval      jpfal3_ugrp = b1ugrp
     c     jpfal3_key    setll     jpfal3
     c                   when      w_seqe = 'L'
     c                   eval      jpfal4_ldor = b1ldor
     c     jpfal4_key    setll     jpfal4
     c                   when      w_seqe = 'M'
     c                   eval      jpfal5_modn = b1modn
     c     jpfal5_key    setll     jpfal5
     c                   when      w_seqe = 'V'
     c                   eval      jpfal6_vare = b1vare
     c     jpfal6_key    setll     jpfal6
     c                   when      w_seqe = 'P'
     c                   eval      jpfal7_kpri = b1kpri
     c     jpfal7_key    setll     jpfal7
     c                   when      w_seqe = 'T'
     c                   eval      jpfal8_vtyp = b1vtyp
     c     jpfal8_key    setll     jpfal8
     c                   when      w_seqe = 'Y'
     c                   eval      jpfal9_lety = b1lety
     c     jpfal9_key    setll     jpfal9
5.70 c                   when      w_seqe = 'O'
5.70 c                   eval      jpfalc_ogrp = b1ogrp
5.70 c     jpfalc_key    setll     jpfalc
     c                   other
5.70 c                   eval      jpfal1_prgr = b1prgr
     c                   eval      jpfal1_ogrp = b1ogrp
     c                   eval      jpfal1_hgrp = b1hgrp
     c                   eval      jpfal1_ugrp = b1ugrp
     c                   eval      jpfal1_ldor = b1ldor
     c                   eval      jpfal1_modn = b1modn
     c                   eval      jpfal1_vare = b1vare
     c                   eval      jpfal1_kpri = b1kpri
     c                   eval      jpfal1_vtyp = b1vtyp
     c                   eval      jpfal1_lety = b1lety
     c     jpfal1_key    setll     jpfal1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
5.70  * Prisgruppe
      *
     c                   if        b2prgr <> *blank
     c                   eval      w_seqe = *blank
5.70 c                   eval      jpfal1_prgr = b2prgr
     c                   eval      jpfal1_ogrp = b2ogrp
     c                   eval      jpfal1_hgrp = b2hgrp
     c                   eval      jpfal1_ugrp = b2ugrp
     c                   eval      jpfal1_ldor = b2ldor
     c                   eval      jpfal1_modn = b2modn
     c                   eval      jpfal1_vare = b2vare
     c                   eval      jpfal1_kpri = b2kpri
     c                   eval      jpfal1_vtyp = b2vtyp
     c                   eval      jpfal1_lety = b2lety
     c     jpfal1_key    setll     jpfal1
     c                   goto      end_pos
     c                   endif
6.11 c                   if        b2ogrp <> 0
6.30 c                   eval      w_seqe = 'O'
6.30 c                   eval      jpfalc_ogrp = b2ogrp
6.31 c                   eval      jpfalc_hgrp = b2hgrp
6.31 c                   eval      jpfalc_ugrp = b2ugrp
6.30 c     jpfalc_key    setll     jpfalc
6.11 c                   goto      end_pos
6.11 c                   endif
      *
      * Hovedgruppe
      *
     c                   if        b2hgrp <> 0
     c                   eval      w_seqe = 'H'
     c                   eval      jpfal2_hgrp = b2hgrp
     c     jpfal2_key    setll     jpfal2
     c                   goto      end_pos
     c                   endif
      *
      * Undergruppe
      *
     c                   if        b2ugrp <> 0
     c                   eval      w_seqe = 'U'
     c                   eval      jpfal3_ugrp = b2ugrp
     c     jpfal3_key    setll     jpfal3
     c                   goto      end_pos
     c                   endif
      *
      * Leverandør
      *
     c                   if        b2ldor <> 0
     c                   eval      w_seqe = 'L'
     c                   eval      jpfal4_ldor = b2ldor
     c     jpfal4_key    setll     jpfal4
     c                   goto      end_pos
     c                   endif
      *
      * Modul
      *
     c                   if        b2modn <> 0
     c                   eval      w_seqe = 'M'
     c                   eval      jpfal5_modn = b2modn
     c     jpfal5_key    setll     jpfal5
     c                   goto      end_pos
     c                   endif
      *
      * Vare
      *
     c                   if        b2vare <> *blank
     c                   eval      w_seqe = 'V'
     c                   eval      jpfal6_vare = b2vare
     c     jpfal6_key    setll     jpfal6
     c                   goto      end_pos
     c                   endif
      *
      * Priskode
      *
     c                   if        b2kpri <> *blank
     c                   eval      w_seqe = 'P'
     c                   eval      jpfal7_kpri = b2kpri
     c     jpfal7_key    setll     jpfal7
     c                   goto      end_pos
     c                   endif
      *
      * Varetype
      *
     c                   if        b2vtyp <> *blank
     c                   eval      w_seqe = 'T'
     c                   eval      jpfal8_vtyp = b2vtyp
     c     jpfal8_key    setll     jpfal8
     c                   goto      end_pos
     c                   endif
      *
      * Leveringstype
      *
     c                   if        b2lety <> 0
     c                   eval      w_seqe = 'Y'
     c                   eval      jpfal9_lety = b2lety
     c     jpfal9_key    setll     jpfal9
     c                   goto      end_pos
     c                   endif
5.70  *
5.70  * Overgruppe
5.70  *
5.70 c                   if        b2ogrp <> 0
5.70 c                   eval      w_seqe = 'O'
5.70 c                   eval      jpfalc_ogrp = b2ogrp
5.70 c     jpfalc_key    setll     jpfalc
5.70 c                   goto      end_pos
5.70 c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
5.70 c                   eval      b2prgr = *blank
     c                   eval      b2ogrp = 0
     c                   eval      b2hgrp = 0
     c                   eval      b2ugrp = 0
     c                   eval      b2ldor = 0
     c                   eval      b2modn = 0
     c                   eval      b2vare = *blank
     c                   eval      b2kpri = *blank
     c                   eval      b2vtyp = *blank
     c                   eval      b2lety = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
5.70 c                   eval      c1prgr = b1prgr
     c                   eval      c1ogrp = b1ogrp
     c                   eval      c1hgrp = b1hgrp
     c                   eval      c1ugrp = b1ugrp
     c                   eval      c1ldor = b1ldor
     c                   eval      c1modn = b1modn
     c                   eval      c1vare = b1vare
     c                   eval      c1kpri = b1kpri
     c                   eval      c1vtyp = b1vtyp
     c                   eval      c1lety = b1lety
8.01 c                   eval      c1nofr = b1nofr
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
6.35 c                   eval      w_jvqvnr = b1vare
6.35 c                   eval      w_ldor   = b1ldor
6.35 c                   exsr      finn_logivar
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
5.70 c                   eval      k1prgr = b1prgr
     c                   eval      k1ogrp = b1ogrp
     c                   eval      k1hgrp = b1hgrp
     c                   eval      k1ugrp = b1ugrp
     c                   eval      k1ldor = b1ldor
     c                   eval      k1modn = b1modn
     c                   eval      k1vare = b1vare
     c                   eval      k1kpri = b1kpri
     c                   eval      k1vtyp = b1vtyp
     c                   eval      k1lety = b1lety
8.01 c                   eval      k1nofr = b1nofr
     c                   exsr      xk1win
     c                   eval      b1valg = 0
6.35 c                   eval      w_jvqvnr = b1vare
6.35 c                   eval      w_ldor   = b1ldor
6.35 c                   exsr      finn_logivar
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
5.70 c                   eval      d1prgr = b1prgr
     c                   eval      d1ogrp = b1ogrp
     c                   eval      d1hgrp = b1hgrp
     c                   eval      d1ugrp = b1ugrp
     c                   eval      d1ldor = b1ldor
     c                   eval      d1modn = b1modn
     c                   eval      d1vare = b1vare
     c                   eval      d1kpri = b1kpri
     c                   eval      d1vtyp = b1vtyp
     c                   eval      d1lety = b1lety
8.01 c                   eval      d1nofr = b1nofr
     c                   exsr      xd1win
     c                   eval      b1valg = 0
6.35 c                   eval      w_jvqvnr = b1vare
6.35 c                   eval      w_ldor   = b1ldor
6.35 c                   exsr      finn_logivar
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
5.70 c                   eval      c1prgr = b1prgr
     c                   eval      c1ogrp = b1ogrp
     c                   eval      c1hgrp = b1hgrp
     c                   eval      c1ugrp = b1ugrp
     c                   eval      c1ldor = b1ldor
     c                   eval      c1modn = b1modn
     c                   eval      c1vare = b1vare
     c                   eval      c1kpri = b1kpri
     c                   eval      c1vtyp = b1vtyp
     c                   eval      c1lety = b1lety
8.01 c                   eval      c1nofr = b1nofr
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
6.35 c                   eval      w_jvqvnr = b1vare
6.35 c                   eval      w_ldor   = b1ldor
6.35 c                   exsr      finn_logivar
     c                   update    b1sfl
     c                   iter
     c                   endif
6.32  *
6.32  * Vise varer under modul
6.32  *
6.32 c                   if        b1valg = 8
6.32 c                   call      'JV548R'
6.32 c                   parm                    w_firm
6.32 c                   parm                    b1modn
6.32 c                   eval      b1valg = 0
6.35 c                   eval      w_jvqvnr = b1vare
6.35 c                   eval      w_ldor   = b1ldor
6.35 c                   exsr      finn_logivar
6.32 c                   update    b1sfl
6.32 c                   iter
6.32 c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
5.70 c                   eval      c1prgr = b2prgr
     c                   eval      c1ogrp = b2ogrp
     c                   eval      c1hgrp = b2hgrp
     c                   eval      c1ugrp = b2ugrp
     c                   eval      c1ldor = b2ldor
     c                   eval      c1modn = b2modn
     c                   eval      c1vare = b2vare
     c                   eval      c1kpri = b2kpri
     c                   eval      c1vtyp = b2vtyp
     c                   eval      c1lety = b2lety
      *
     c                   exsr      hent_info
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      c1taga
     c                   endsl
      *
      * Sjekker input
      *
5.70 c                   eval      w_prgr = c1prgr
     c                   eval      w_ogrp = c1ogrp
     c                   eval      w_hgrp = c1hgrp
     c                   eval      w_ugrp = c1ugrp
     c                   eval      w_ldor = c1ldor
     c                   eval      w_modn = c1modn
     c                   eval      w_vare = c1vare
     c                   eval      w_kpri = c1kpri
     c                   eval      w_vtyp = c1vtyp
     c                   eval      w_lety = c1lety
     c                   exsr      sjekk_input
5.70 c                   eval      c1prgr = w_prgr
     c                   eval      c1ogrp = w_ogrp
     c                   eval      c1hgrp = w_hgrp
     c                   eval      c1ugrp = w_ugrp
     c                   eval      c1modn = w_modn
     c                   if        b_feil = *on
     c                   goto      c1taga
     c                   endif
      *
      * Sender opp informasjonsbildet dersom rad finnes fra før
      *
     c                   if        c1vare = *blank
5.70 c                   eval      jpfalr_prgr = c1prgr
     c                   eval      jpfalr_ogrp = c1ogrp
     c                   eval      jpfalr_hgrp = c1hgrp
     c                   eval      jpfalr_ugrp = c1ugrp
     c                   eval      jpfalr_ldor = c1ldor
     c                   eval      jpfalr_modn = c1modn
     c                   eval      jpfalr_vare = c1vare
     c                   eval      jpfalr_kpri = c1kpri
     c                   eval      jpfalr_vtyp = c1vtyp
     c                   eval      jpfalr_lety = c1lety
8.01 c                   eval      jpfalr_nofr = c1nofr
     c     jpfalr_key    chain     jpfalr                             90
     c                   else
5.70 c                   eval      jpfala_prgr = c1prgr
     c                   eval      jpfala_vare = c1vare
     c                   eval      jpfala_ldor = c1ldor
     c                   eval      jpfala_kpri = c1kpri
     c                   eval      jpfala_vtyp = c1vtyp
     c                   eval      jpfala_lety = c1lety
8.01 c                   eval      jpfala_nofr = c1nofr
     c     jpfala_key    chain     jpfala                             90
     c                   if        *in90 = *off
5.70 c                   eval      c1prgr = jcprgr
     c                   eval      c1ogrp = jcogrp
     c                   eval      c1hgrp = jchgrp
     c                   eval      c1ugrp = jcugrp
     c                   eval      c1modn = jcmodn
     c                   endif
     c                   endif
      *
     c                   if        *in90 = *off
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
5.70 c                   eval      c1prgr = *blank
     c                   eval      c1gtxt = *blank
     c                   eval      c1ogrp = 0
     c                   eval      c1otxt = *blank
     c                   eval      c1hgrp = 0
     c                   eval      c1htxt = *blank
     c                   eval      c1ugrp = 0
     c                   eval      c1utxt = *blank
     c                   eval      c1ldor = 0
     c                   eval      c1lnav = *blank
     c                   eval      c1modn = 0
     c                   eval      c1mte1 = *blank
     c                   eval      c1vare = *blank
     c                   eval      c1tek1 = *blank
     c                   eval      c1kpri = *blank
     c                   eval      c1ptxt = *blank
     c                   eval      c1vtyp = *blank
     c                   eval      c1ttxt = *blank
     c                   eval      c1lety = 0
8.01 c                   eval      c1nofr = 0
     c                   eval      c1lbes = *blank
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
5.70 c                   eval      b2prgr = *blank
     c                   eval      b2ogrp = 0
     c                   eval      b2hgrp = 0
     c                   eval      b2ugrp = 0
     c                   eval      b2ldor = 0
     c                   eval      b2modn = 0
     c                   eval      b2vare = *blank
     c                   eval      b2kpri = *blank
     c                   eval      b2vtyp = *blank
     c                   eval      b2lety = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * C2BLD Behandle bilde for ny - endring - vise
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
5.70 c                   eval      jpfalr_prgr = c1prgr
     c                   eval      jpfalr_ogrp = c1ogrp
     c                   eval      jpfalr_hgrp = c1hgrp
     c                   eval      jpfalr_ugrp = c1ugrp
     c                   eval      jpfalr_ldor = c1ldor
     c                   eval      jpfalr_modn = c1modn
     c                   eval      jpfalr_vare = c1vare
     c                   eval      jpfalr_kpri = c1kpri
     c                   eval      jpfalr_vtyp = c1vtyp
     c                   eval      jpfalr_lety = c1lety
8.01 c                   eval      jpfalr_nofr = c1nofr
     c     jpfalr_key    chain     jpfalr                             90
      *
     c                   if        *in90 = *off
     c                   eval      c2kofa = jckofa
     c                   eval      c2kobe = jckobe
     c                   eval      c2safa = jcsafa
6.20 c                   eval      c2sphl = jcsphl
8.01 c                   eval      c2sano = jcsano
8.02 c*                   eval      c2nobb = jcnobb
8.01 c                   eval      c2kof2 = jckof2
     c                   eval      c2eusr = jceusr
     c                   if        jcodat <> *loval
     c     *dmy          move      jcodat        c2odat
     c                   else
     c                   eval      c2odat = 0
     c                   endif
     c                   if        jcedat <> *loval
     c     *dmy          move      jcedat        c2edat
     c                   else
     c                   eval      c2edat = 0
     c                   endif
     c                   else
     c                   eval      c2kofa = 1
     c                   eval      c2kobe = 0
     c                   eval      c2safa = 1
6.21 c                   eval      c2sphl = 0
8.01 c                   eval      c2sano = 0
8.02 c*                   eval      c2nobb = 0
8.01 c                   eval      c2kof2 = 0
     c                   eval      c2odat = 0
     c                   eval      c2edat = 0
     c                   eval      c2eusr = *blank
     c                   endif
      *
     c                   if        b1valg = 3
5.70 c                   eval      c1prgr = k1prgr
     c                   eval      c1ogrp = k1ogrp
     c                   eval      c1hgrp = k1hgrp
     c                   eval      c1ugrp = k1ugrp
     c                   eval      c1ldor = k1ldor
     c                   eval      c1modn = k1modn
     c                   eval      c1vare = k1vare
     c                   eval      c1kpri = k1kpri
     c                   eval      c1vtyp = k1vtyp
     c                   eval      c1lety = k1lety
8.01 c                   eval      c1nofr = k1nofr
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
      * Henter beskrivelse av nivå
      *
     c                   exsr      hent_info
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2kofa <= 0
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
6.20 c                   if        c2safa <= 0   and
6.20 c                             c2sphl = 0
     c                   eval      *in32 = *on
     c                   goto      c2taga
     c                   endif
      *
      *
      * Oppdatering
      *
     c                   clear                   jpfalur
      *
5.70 c                   eval      jpfalu_prgr = c1prgr
     c                   eval      jpfalu_ogrp = c1ogrp
     c                   eval      jpfalu_hgrp = c1hgrp
     c                   eval      jpfalu_ugrp = c1ugrp
     c                   eval      jpfalu_ldor = c1ldor
     c                   eval      jpfalu_modn = c1modn
     c                   eval      jpfalu_vare = c1vare
     c                   eval      jpfalu_kpri = c1kpri
     c                   eval      jpfalu_vtyp = c1vtyp
     c                   eval      jpfalu_lety = c1lety
8.01 c                   eval      jpfalu_nofr = c1nofr
     c     jpfalu_key    chain     jpfalur                            90
      *
     c                   if        c1vare <> *blank
     c                   eval      jvarl1_vare = c1vare
     c     jvarl1_key    chain     jvarl1                             91
     c                   if        *in91 = *off
     c                   eval      c1ogrp = jvogrp
     c                   eval      c1hgrp = jvhgrp
     c                   eval      c1ugrp = jvugrp
     c                   endif
     c                   endif
      *
5.70 c                   eval      jcprgr = c1prgr
     c                   eval      jcogrp = c1ogrp
     c                   eval      jchgrp = c1hgrp
     c                   eval      jcugrp = c1ugrp
     c                   eval      jckofa = c2kofa
8.01 c                   eval      jckof2 = c2kof2
     c                   eval      jckobe = c2kobe
     c                   eval      jcsafa = c2safa
6.20 c                   eval      jcsphl = c2sphl
8.01 c                   eval      jcsano = c2sano
8.02 c*                   eval      jcnobb = c2nobb
     c                   eval      jceusr = l_user
      *
     c                   time                    jcedat
     c                   time                    jcetim
      *
     c                   if        *in90 = *off
     c                   update    jpfalur
     c                   else
     c                   eval      jcfirm = w_firm
     c                   eval      jcldor = c1ldor
     c                   eval      jcmodn = c1modn
     c                   eval      jcvare = c1vare
     c                   eval      jckpri = c1kpri
     c                   eval      jcvtyp = c1vtyp
     c                   eval      jclety = c1lety
8.01 c                   eval      jcnofr = c1nofr
     c                   time                    jcodat
     c                   write     jpfalur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1kofa = c2kofa
     c                   eval      b1kobe = c2kobe
     c                   eval      b1safa = c2safa
     c                   eval      b1lnav = c1lnav
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
5.70 c                   eval      jpfalr_prgr = d1prgr
     c                   eval      jpfalr_ogrp = d1ogrp
     c                   eval      jpfalr_hgrp = d1hgrp
     c                   eval      jpfalr_ugrp = d1ugrp
     c                   eval      jpfalr_ldor = d1ldor
     c                   eval      jpfalr_modn = d1modn
     c                   eval      jpfalr_vare = d1vare
     c                   eval      jpfalr_kpri = d1kpri
     c                   eval      jpfalr_vtyp = d1vtyp
     c                   eval      jpfalr_lety = d1lety
8.01 c                   eval      jpfalr_nofr = d1nofr
     c     jpfalr_key    chain     jpfalr                             90
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
5.70 c                   eval      jpfalu_prgr = d1prgr
     c                   eval      jpfalu_ogrp = d1ogrp
     c                   eval      jpfalu_hgrp = d1hgrp
     c                   eval      jpfalu_ugrp = d1ugrp
     c                   eval      jpfalu_ldor = d1ldor
     c                   eval      jpfalu_modn = d1modn
     c                   eval      jpfalu_vare = d1vare
     c                   eval      jpfalu_kpri = d1kpri
     c                   eval      jpfalu_vtyp = d1vtyp
     c                   eval      jpfalu_lety = d1lety
8.01 c                   eval      jpfalu_nofr = d1nofr
     c     jpfalu_key    chain     jpfalur                            90
      *
     c                   delete    jpfalur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
5.70 c                   eval      s_prgr = k1prgr
     c                   eval      s_ogrp = k1ogrp
     c                   eval      s_hgrp = k1hgrp
     c                   eval      s_ugrp = k1ugrp
     c                   eval      s_ldor = k1ldor
     c                   eval      s_modn = k1modn
     c                   eval      s_vare = k1vare
     c                   eval      s_kpri = k1kpri
     c                   eval      s_vtyp = k1vtyp
     c                   eval      s_lety = k1lety
8.01 c                   eval      s_nofr = k1nofr
      *
      * Fyller kopierings-bildet
      *
5.70 c                   eval      jpfalr_prgr = k1prgr
     c                   eval      jpfalr_ogrp = k1ogrp
     c                   eval      jpfalr_hgrp = k1hgrp
     c                   eval      jpfalr_ugrp = k1ugrp
     c                   eval      jpfalr_ldor = k1ldor
     c                   eval      jpfalr_modn = k1modn
     c                   eval      jpfalr_vare = k1vare
     c                   eval      jpfalr_kpri = k1kpri
     c                   eval      jpfalr_vtyp = k1vtyp
     c                   eval      jpfalr_lety = k1lety
8.01 c                   eval      jpfalr_nofr = k1nofr
     c     jpfalr_key    chain     jpfalr                             90
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd = *on
     c                   exsr      spØrring
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker input
      *
5.70 c                   eval      w_prgr = k1prgr
     c                   eval      w_ogrp = k1ogrp
     c                   eval      w_hgrp = k1hgrp
     c                   eval      w_ugrp = k1ugrp
     c                   eval      w_ldor = k1ldor
     c                   eval      w_modn = k1modn
     c                   eval      w_vare = k1vare
     c                   eval      w_kpri = k1kpri
     c                   eval      w_vtyp = k1vtyp
     c                   eval      w_lety = k1lety
8.01 c                   exsr      sjekk_input
     c                   eval      k1ogrp = w_ogrp
     c                   eval      k1hgrp = w_hgrp
     c                   eval      k1ugrp = w_ugrp
     c                   eval      k1modn = w_modn
     c                   if        b_feil = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   if        k1vare = *blank
5.70 c                   eval      jpfalr_prgr = k1prgr
     c                   eval      jpfalr_ogrp = k1ogrp
     c                   eval      jpfalr_hgrp = k1hgrp
     c                   eval      jpfalr_ugrp = k1ugrp
     c                   eval      jpfalr_ldor = k1ldor
     c                   eval      jpfalr_modn = k1modn
     c                   eval      jpfalr_vare = k1vare
     c                   eval      jpfalr_kpri = k1kpri
     c                   eval      jpfalr_vtyp = k1vtyp
     c                   eval      jpfalr_lety = k1lety
8.01 c                   eval      jpfalr_nofr = k1nofr
     c     jpfalr_key    chain     jpfalr                             90
     c                   else
5.70 c                   eval      jpfala_prgr = k1prgr
5.70 c                   eval      jpfala_vare = k1vare
     c                   eval      jpfala_ldor = k1ldor
     c                   eval      jpfala_kpri = k1kpri
     c                   eval      jpfala_vtyp = k1vtyp
     c                   eval      jpfala_lety = k1lety
8.01 c                   eval      jpfala_nofr = k1nofr
     c     jpfala_key    chain     jpfala                             90
     c                   endif
      *
     c                   if        *in90 = *off
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
5.70 c                   eval      c1prgr = s_prgr
     c                   eval      c1ogrp = s_ogrp
     c                   eval      c1hgrp = s_hgrp
     c                   eval      c1ugrp = s_ugrp
     c                   eval      c1ldor = s_ldor
     c                   eval      c1modn = s_modn
     c                   eval      c1vare = s_vare
     c                   eval      c1kpri = s_kpri
     c                   eval      c1vtyp = s_vtyp
     c                   eval      c1lety = s_lety
8.01 c                   eval      c1nofr = s_nofr
      *
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
6.33  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.33  * F1 Behandle bilde for filter
6.33  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.33  *
6.33 c     xf1win        begsr
6.33  *
6.33  *  Send Alt
6.33  *
6.33 c     xf1tag        tag
6.33  *
6.33 c     f1taga        tag
6.33 c                   exfmt     f1win
6.33  *
6.33  * Behandling av funksjonstaster
6.33  *
6.33 c                   select
6.33 c                   when      *inkc or *inkl
6.33 c                   goto      end_f1win
6.33 c                   when      *inkd
6.33 c                   exsr      spØrring
6.33 c                   goto      f1taga
6.33 c                   endsl
6.33  *
6.33  * Validering
6.33  *
6.33 c                   if        f1ogrp = 0 and
6.33 c                             (f1hgrp <> 0 or
6.33 c                              f1ugrp <> 0)
6.33 c                   eval      *in33 = *on
6.33 c                   goto      f1taga
6.33 c                   endif
6.33  *
6.33 c                   if        f1ugrp <> 0 and
6.33 c                             (f1ogrp = 0 or
6.33 c                              f1hgrp = 0)
6.33 c                   eval      *in35 = *on
6.33 c                   goto      f1taga
6.33 c                   endif
6.33  *
6.33 c*                   if        f1modn <> 0 and
6.33 c*                             (f1ogrp = 0 or
6.33 c*                              f1hgrp = 0 or
6.33 c*                              f1ugrp = 0)
6.33 c*                   eval      *in36 = *on
6.33 c*                   goto      f1taga
6.33 c*                   endif
6.33  *
6.33 c     end_f1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Fyll opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'H'
     c                   read      jpfal2
     c                   when      w_seqe = 'U'
     c                   read      jpfal3
     c                   when      w_seqe = 'L'
     c                   read      jpfal4
     c                   when      w_seqe = 'M'
     c                   read      jpfal5
     c                   when      w_seqe = 'V'
     c                   read      jpfal6
     c                   when      w_seqe = 'P'
     c                   read      jpfal7
     c                   when      w_seqe = 'T'
     c                   read      jpfal8
     c                   when      w_seqe = 'Y'
     c                   read      jpfal9
5.70 c                   when      w_seqe = 'O'
5.70 c                   read      jpfalc
     c                   other
     c                   read      jpfal1
     c                   endsl
      *
     c                   if        %eof or
     c                             jcfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
6.33  *
6.33  * Filter
6.33  *
6.33 c                   if        f1prgr <> *blank and
6.33 c                             f1prgr <> jcprgr
6.33 c                   iter
6.33 c                   endif
      *
6.33 c                   if        f1ogrp <> 0
6.33  *
6.33 c                   if        f1ogrp <> jcogrp
6.33 c                   iter
6.33 c                   endif
6.33  *
6.33 c                   if        f1hgrp <> 0
6.33  *
6.33 c                   if        f1hgrp <> jchgrp
6.33 c                   iter
6.33 c                   endif
6.33  *
6.33 c                   if        f1ugrp <> 0 and
6.33 c                             f1ugrp <> jcugrp
6.33 c                   iter
6.33 c                   endif
6.33  *
6.33 c                   endif
6.33  *
6.33 c                   endif
6.33  *
6.33 c                   if        f1modn <> 0 and
6.33 c                             f1modn <> jcmodn
6.33 c                   iter
6.33 c                   endif
      *
6.33 c                   if        f1lvar <> *blank
6.33 c                   movel     jcvare        w_lv_nobb
6.33 c/exec sql
6.33 c+ select count(*)
6.33 c+ into   :w_teller
6.33 c+ from   jvklpf
6.33 c+ where  jvqvnr = :f1lvar and
6.33 c+        jvquno = :w_lv_nobb
6.33 c/end-exec
6.33 c                   if        w_teller = 0
6.33 c                   iter
6.33 c                   endif
6.33 c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'H'
     c                   eval      jpfal2_hgrp = jchgrp
     c                   when      w_seqe = 'U'
     c                   eval      jpfal3_ugrp = jcugrp
     c                   when      w_seqe = 'L'
     c                   eval      jpfal4_ldor = jcldor
     c                   when      w_seqe = 'M'
     c                   eval      jpfal5_modn = jcmodn
     c                   when      w_seqe = 'V'
     c                   eval      jpfal6_vare = jcvare
     c                   when      w_seqe = 'P'
     c                   eval      jpfal7_kpri = jckpri
     c                   when      w_seqe = 'T'
     c                   eval      jpfal8_vtyp = jcvtyp
     c                   when      w_seqe = 'Y'
     c                   eval      jpfal9_lety = jclety
5.70 c                   when      w_seqe = 'O'
5.70 c                   eval      jpfalc_ogrp = jcogrp
     c                   other
5.70 c                   eval      jpfal1_prgr = jcprgr
     c                   eval      jpfal1_ogrp = jcogrp
     c                   eval      jpfal1_hgrp = jchgrp
     c                   eval      jpfal1_ugrp = jcugrp
     c                   eval      jpfal1_ldor = jcldor
     c                   eval      jpfal1_modn = jcmodn
     c                   eval      jpfal1_vare = jcvare
     c                   eval      jpfal1_kpri = jckpri
     c                   eval      jpfal1_vtyp = jcvtyp
     c                   eval      jpfal1_lety = jclety
8.01 c                   eval      jpfal1_nofr = jcnofr
     c                   endsl
      *
     c                   eval      b1valg = 0
5.70 c                   eval      b1prgr = jcprgr
     c                   eval      b1ogrp = jcogrp
     c                   eval      b1hgrp = jchgrp
     c                   eval      b1ugrp = jcugrp
     c                   eval      b1ldor = jcldor
     c                   eval      b1lnav = *blank
     c                   eval      b1modn = jcmodn
     c                   eval      b1vare = jcvare
     c                   eval      b1kpri = jckpri
     c                   eval      b1vtyp = jcvtyp
     c                   eval      b1lety = jclety
8.01 c                   eval      b1nofr = jcnofr
     c                   eval      b1kofa = jckofa
     c                   eval      b1kobe = jckobe
     c                   eval      b1safa = jcsafa
      *
      * Henter leverandørnavn
      *
     c                   if        b1ldor <> 0
     c                   eval      jlevl1_ldor = b1ldor
     c     jlevl1_key    chain     jlevl1
     c                   if        %found
     c                   eval      b1lnav = jlnavn
     c                   endif
     c                   endif
      *
6.35  * sjekk om logistikk-vare
6.35 c                   eval      w_jvqvnr = jcvare
6.35 c                   eval      w_ldor   = jcldor
6.35 c                   exsr      finn_logivar
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'H'
     c     jpfal2_key    setgt     jpfal2
     c                   readp     jpfal2
     c                   when      w_seqe = 'U'
     c     jpfal3_key    setgt     jpfal3
     c                   readp     jpfal3
     c                   when      w_seqe = 'L'
     c     jpfal4_key    setgt     jpfal4
     c                   readp     jpfal4
     c                   when      w_seqe = 'M'
     c     jpfal5_key    setgt     jpfal5
     c                   readp     jpfal5
     c                   when      w_seqe = 'V'
     c     jpfal6_key    setgt     jpfal6
     c                   readp     jpfal6
     c                   when      w_seqe = 'P'
     c     jpfal7_key    setgt     jpfal7
     c                   readp     jpfal7
     c                   when      w_seqe = 'T'
     c     jpfal8_key    setgt     jpfal8
     c                   readp     jpfal8
     c                   when      w_seqe = 'Y'
     c     jpfal9_key    setgt     jpfal9
     c                   readp     jpfal9
5.70 c                   when      w_seqe = 'O'
5.70 c     jpfalc_key    setgt     jpfalc
5.70 c                   readp     jpfalc
     c                   other
     c     jpfal1_key    setgt     jpfal1
     c                   readp     jpfal1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'H'
     c                   readp     jpfal2
     c                   when      w_seqe = 'U'
     c                   readp     jpfal3
     c                   when      w_seqe = 'L'
     c                   readp     jpfal4
     c                   when      w_seqe = 'M'
     c                   readp     jpfal5
     c                   when      w_seqe = 'V'
     c                   readp     jpfal6
     c                   when      w_seqe = 'P'
     c                   readp     jpfal7
     c                   when      w_seqe = 'T'
     c                   readp     jpfal8
     c                   when      w_seqe = 'Y'
     c                   readp     jpfal9
5.70 c                   when      w_seqe = 'O'
5.70 c                   readp     jpfalc
     c                   other
     c                   readp     jpfal1
     c                   endsl
      *
     c                   if        %eof or
     c                             jcfirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'H'
     c                   eval      jpfal2_hgrp = jchgrp
     c                   when      w_seqe = 'U'
     c                   eval      jpfal3_ugrp = jcugrp
     c                   when      w_seqe = 'L'
     c                   eval      jpfal4_ldor = jcldor
     c                   when      w_seqe = 'M'
     c                   eval      jpfal5_modn = jcmodn
     c                   when      w_seqe = 'V'
     c                   eval      jpfal6_vare = jcvare
     c                   when      w_seqe = 'P'
     c                   eval      jpfal7_kpri = jckpri
     c                   when      w_seqe = 'T'
     c                   eval      jpfal8_vtyp = jcvtyp
     c                   when      w_seqe = 'Y'
     c                   eval      jpfal9_lety = jclety
5.70 c                   when      w_seqe = 'O'
5.70 c                   eval      jpfalc_ogrp = jcogrp
     c                   other
5.70 c                   eval      jpfal1_prgr = jcprgr
     c                   eval      jpfal1_ogrp = jcogrp
     c                   eval      jpfal1_hgrp = jchgrp
     c                   eval      jpfal1_ugrp = jcugrp
     c                   eval      jpfal1_ldor = jcldor
     c                   eval      jpfal1_modn = jcmodn
     c                   eval      jpfal1_vare = jcvare
     c                   eval      jpfal1_kpri = jckpri
     c                   eval      jpfal1_vtyp = jcvtyp
     c                   eval      jpfal1_lety = jclety
8.01 c                   eval      jpfal1_nofr = jcnofr
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'H'
     c     jpfal2_key    setll     jpfal2
     c                   when      w_seqe = 'U'
     c     jpfal3_key    setll     jpfal3
     c                   when      w_seqe = 'L'
     c     jpfal4_key    setll     jpfal4
     c                   when      w_seqe = 'M'
     c     jpfal5_key    setll     jpfal5
     c                   when      w_seqe = 'V'
     c     jpfal6_key    setll     jpfal6
     c                   when      w_seqe = 'P'
     c     jpfal7_key    setll     jpfal7
     c                   when      w_seqe = 'T'
     c     jpfal8_key    setll     jpfal8
     c                   when      w_seqe = 'Y'
     c     jpfal9_key    setll     jpfal9
5.70 c                   when      w_seqe = 'O'
5.70 c     jpfalc_key    setll     jpfalc
     c                   other
     c     jpfal1_key    setll     jpfal1
     c                   endsl
      *
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Overgruppe
      *
     c                   if        w_afld = 'B2OGRP'
     c                   eval      b2hgrp = 0
     c                   eval      b2ugrp = 0
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    w_otxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1OGRP'
     c                   eval      c1hgrp = 0
     c                   eval      c1ugrp = 0
     c                   eval      c1otxt = *blank
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    c1ogrp
     c                   parm                    c1otxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1OGRP'
     c                   eval      k1hgrp = 0
     c                   eval      k1ugrp = 0
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    k1ogrp
     c                   parm                    w_otxt
     c                   leavesr
     c                   endif
      *
6.33 c                   if        w_afld = 'F1OGRP'
6.33 c                   eval      f1hgrp = 0
6.33 c                   eval      f1ugrp = 0
6.33 c                   call      'VG510R'
6.33 c                   parm                    w_firm
6.33 c                   parm                    f1ogrp
6.33 c                   parm                    w_otxt
6.33 c                   leavesr
6.33 c                   endif
6.33  *
      * Hovedgruppe
      *
     c                   if        w_afld = 'B2HGRP'
     c                   eval      b2ugrp = 0
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    b2hgrp
     c                   parm                    w_htxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1HGRP'
     c                   eval      c1ugrp = 0
     c                   eval      c1htxt = *blank
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    c1ogrp
     c                   parm                    c1hgrp
     c                   parm                    c1htxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1HGRP'
     c                   eval      k1ugrp = 0
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    k1ogrp
     c                   parm                    k1hgrp
     c                   parm                    w_htxt
     c                   leavesr
     c                   endif
6.33  *
6.33 c                   if        w_afld = 'F1HGRP'
6.33 c                   eval      f1ugrp = 0
6.33 c                   call      'VG511R'
6.33 c                   parm                    w_firm
6.33 c                   parm                    f1ogrp
6.33 c                   parm                    f1hgrp
6.33 c                   parm                    w_htxt
6.33 c                   leavesr
6.33 c                   endif
6.33  *
      *
      * Undergruppe
      *
     c                   if        w_afld = 'B2UGRP'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    b2hgrp
     c                   parm                    b2ugrp
     c                   parm                    w_utxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1UGRP'
     c                   eval      c1utxt = *blank
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    c1ogrp
     c                   parm                    c1hgrp
     c                   parm                    c1ugrp
     c                   parm                    c1utxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1UGRP'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    k1ogrp
     c                   parm                    k1hgrp
     c                   parm                    k1ugrp
     c                   parm                    w_utxt
     c                   leavesr
     c                   endif
6.33  *
6.33 c                   if        w_afld = 'F1UGRP'
6.33 c                   call      'VG512R'
6.33 c                   parm                    w_firm
6.33 c                   parm                    f1ogrp
6.33 c                   parm                    f1hgrp
6.33 c                   parm                    f1ugrp
6.33 c                   parm                    w_utxt
6.33 c                   leavesr
6.33 c                   endif
6.33  *
      *
      * Leverandør
      *
     c                   if        w_afld = 'B2LDOR'
     c                   call      'JL500R'
     c                   parm                    b2ldor
     c                   parm                    w_lnav
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1LDOR'
     c                   call      'JL500R'
     c                   parm                    c1ldor
     c                   parm                    c1lnav
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1LDOR'
     c                   call      'JL500R'
     c                   parm                    k1ldor
     c                   parm                    w_lnav
     c                   leavesr
     c                   endif
      *
      * Modul
      *
     c                   if        w_afld = 'B2MODN'
     c                   call      'JU500R'
     c                   parm                    w_firm
     c                   parm                    b2modn
     c                   parm                    w_mte1
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1MODN'
     c                   call      'JU500R'
     c                   parm                    w_firm
     c                   parm                    c1modn
     c                   parm                    c1mte1
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1MODN'
     c                   call      'JU500R'
     c                   parm                    w_firm
     c                   parm                    k1modn
     c                   parm                    w_mte1
     c                   leavesr
     c                   endif
6.33  *
6.33 c                   if        w_afld = 'F1MODN'
6.33 c                   call      'JU500R'
6.33 c                   parm                    w_firm
6.33 c                   parm                    f1modn
6.33 c                   parm                    w_mte1
6.33 c                   leavesr
6.33 c                   endif
      *
      * Varenummer
      *
     c                   if        w_afld = 'B2VARE'
     c                   eval      w_vare = b2vare
     c                   call      'JV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_tek1
     c                   eval      b2vare = w_vare
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1VARE'
     c                   eval      w_vare = c1vare
     c                   call      'JV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    c1tek1
     c                   eval      c1vare = w_vare
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1VARE'
     c                   eval      w_vare = k1vare
     c                   call      'JV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_tek1
     c                   eval      k1vare = w_vare
     c                   leavesr
     c                   endif
      *
      * Priskode
      *
     c                   if        w_afld = 'B2KPRI'
     c                   call      'VA560R'
     c                   parm                    w_firm
     c                   parm                    b2kpri
     c                   parm                    w_ptxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1KPRI'
     c                   call      'VA560R'
     c                   parm                    w_firm
     c                   parm                    c1kpri
     c                   parm                    c1ptxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1KPRI'
     c                   call      'VA560R'
     c                   parm                    w_firm
     c                   parm                    k1kpri
     c                   parm                    w_ptxt
     c                   leavesr
     c                   endif
      *
      * Varetype
      *
     c                   if        w_afld = 'B2VTYP'
     c                   call      'VA570R'
     c                   parm                    w_firm
     c                   parm                    b2vtyp
     c                   parm                    w_ttxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1VTYP'
     c                   call      'VA570R'
     c                   parm                    w_firm
     c                   parm                    c1vtyp
     c                   parm                    c1ttxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1VTYP'
     c                   call      'VA570R'
     c                   parm                    w_firm
     c                   parm                    k1vtyp
     c                   parm                    w_ttxt
     c                   leavesr
     c                   endif
      *
      * Leveringstype
      *
     c                   if        w_afld = 'B2LETY'
     c                   call      'VY500R'
     c                   parm                    w_firm
     c                   parm                    b2lety
     c                   parm                    w_lbes
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1LETY'
     c                   call      'VY500R'
     c                   parm                    w_firm
     c                   parm                    c1lety
     c                   parm                    c1lbes
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1LETY'
     c                   call      'VY500R'
     c                   parm                    w_firm
     c                   parm                    k1lety
     c                   parm                    w_lbes
     c                   leavesr
     c                   endif
     c                   if        w_afld = 'K1LETY'
     c                   call      'VY500R'
     c                   parm                    w_firm
     c                   parm                    k1lety
     c                   parm                    w_lbes
     c                   leavesr
     c                   endif
5.70  *
5.70  * Prisgruppe
5.70  *
5.70 c                   if        w_afld = 'B2PRGR'
5.70 c                   call      'RA530R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    b2prgr
5.70 c                   parm                    w_gtxt
5.70 c                   leavesr
5.70 c                   endif
5.70  *
5.70 c                   if        w_afld = 'C1PRGR'
5.70 c                   call      'RA530R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    c1prgr
5.70 c                   parm                    c1gtxt
5.70 c                   leavesr
5.70 c                   endif
7.xx c                   if        w_afld = 'K1PRGR'
7.xx c                   call      'RA530R'
7.xx c                   parm                    w_firm
7.xx c                   parm                    k1prgr
7.xx c                   parm                    k1gtxt
7.xx c                   leavesr
7.xx c                   endif
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker inntastede verdier, og henter beskrivelser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
5.70 c                   eval      w_gtxt = *blank
     c                   eval      w_otxt = *blank
     c                   eval      w_htxt = *blank
     c                   eval      w_utxt = *blank
     c                   eval      w_lnav = *blank
     c                   eval      w_mte1 = *blank
     c                   eval      w_tek1 = *blank
     c                   eval      w_ptxt = *blank
     c                   eval      w_ttxt = *blank
     c                   eval      w_lbes = *blank
      *
      * Modul
      *
     c                   if        w_modn <> 0
     c                   eval      jmodl1_modn = w_modn
     c     jmodl1_key    chain     jmodl1                             39
     c                   if        *in39 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      w_ogrp = juogrp
     c                   eval      w_hgrp = juhgrp
     c                   eval      w_ugrp = juugrp
     c                   eval      w_mte1 = jumte1
     c                   endif
      *
      * Vare
      *
     c                   if        w_vare <> *blank
     c                   eval      jvarl1_vare = w_vare
     c     jvarl1_key    chain     jvarl1                             40
     c                   if        *in40 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
6.34  * sjekker om logistikkvare, ikke lov, skal bruke ekte nobbnr
6.34 c                   eval      w_lv_vare = *blank
6.34 c/exec sql
6.34 c+ select jvkvnr
6.34 c+ into   :w_lv_vare
6.34 c+ from   jvkhpf
6.34 c+ where  jvkvnr = :w_vare
6.34 c/end-exec
6.34 c                   if        w_lv_vare <> *blank
6.34 c                   eval      *in47 = *on
6.34 c                   eval      b_feil = *on
6.34 c                   leavesr
6.34 c                   endif
     c                   eval      w_ogrp = jvogrp
     c                   eval      w_hgrp = jvhgrp
     c                   eval      w_ugrp = jvugrp
     c                   eval      w_modn = jvmodn
     c                   eval      w_tek1 = jvtek1
     c                   endif
      *
      * Overgruppe
      *
     c                   if        w_ogrp <> 0 and
     c                             w_hgrp = 0 and
     c                             w_ugrp = 0
     c                   eval      vogrl1_oogr = w_ogrp
     c     vogrl1_key    chain     vogrl1                             35
     c                   if        *in35 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      w_otxt = vgotxt
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        w_hgrp <> 0 and
     c                             w_ugrp = 0
     c                   eval      vhgrl1_hogr = w_ogrp
     c                   eval      vhgrl1_hhgr = w_hgrp
     c     vhgrl1_key    chain     vhgrl1                             36
     c                   if        *in36 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      w_htxt = vghtxt
     c                   endif
      *
      * Undergruppe
      *
     c                   if        w_ugrp <> 0
     c                   eval      vugrl1_uogr = w_ogrp
     c                   eval      vugrl1_uhgr = w_hgrp
     c                   eval      vugrl1_uugr = w_ugrp
     c     vugrl1_key    chain     vugrl1                             37
     c                   if        *in37 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      w_utxt = vgutxt
     c                   endif
      *
      * Leverandør
      *
     c                   if        w_ldor <> 0
     c                   eval      jlevl1_ldor = w_ldor
     c     jlevl1_key    chain     jlevl1                             38
     c                   if        *in38 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      w_lnav = jlnavn
     c                   endif
      *
      * Priskode
      *
     c                   if        w_kpri <> *blank
     c                   eval      vpkol1_pkod = w_kpri
     c     vpkol1_key    chain     vpkol1
     c                   if        not %found
     c                   eval      *in41 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      w_ptxt = vaptxt
     c                   endif
      *
     c                   if        w_kpri <> *blank and
     c                             w_kpri <> 'B' and
     c                             w_kpri <> 'N'
     c                   eval      *in42 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      * Varetype
      *
     c                   if        w_vtyp <> *blank
     c                   eval      vvtyl1_vtyp = w_vtyp
     c     vvtyl1_key    chain     vvtyl1                             43
     c                   if        *in43 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      w_ttxt = vavtxt
     c                   endif
      *
     c                   if        w_vtyp <> *blank and
     c                             w_vtyp <> 'S'
     c                   eval      *in44 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      * Leveringstype
      *
     c                   if        w_lety <> 0
     c                   eval      vltpl1_lety = w_lety
     c     vltpl1_key    chain     vltpl1                             45
     c                   if        *in45 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      w_lbes = vybesk
     c                   endif
      *
5.70  * Prisgruppe
5.70  *
5.70 c                   if        w_prgr <> *blank
5.70 c                   eval      ra30l1_dkod = w_prgr
5.70 c     ra30l1_key    chain     ra30l1                             46
5.70 c                   if        *in46 = *on
5.70 c                   eval      b_feil = *on
5.70 c                   leavesr
5.70 c                   endif
5.70 c                   eval      w_gtxt = redtxt
5.70 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter beskrivelse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_info     begsr
      *
5.70 c                   eval      c1gtxt = *blank
     c                   eval      c1otxt = *blank
     c                   eval      c1htxt = *blank
     c                   eval      c1utxt = *blank
     c                   eval      c1lnav = *blank
     c                   eval      c1mte1 = *blank
     c                   eval      c1tek1 = *blank
     c                   eval      c1ptxt = *blank
     c                   eval      c1ttxt = *blank
     c                   eval      c1lbes = *blank
      *
     c                   eval      c1nobb = 0
      *
5.70  * Prisgruppe
5.70  *
5.70 c                   if        c1prgr <> *blank
5.70 c                   eval      ra30l1_dkod = c1prgr
5.70 c     ra30l1_key    chain     ra30l1                             91
5.70 c                   if        *in91 = *off
5.70 c                   eval      c1gtxt = redtxt
5.70 c                   endif
5.70 c                   endif
      *
      * Overgruppe
      *
     c                   if        c1ogrp <> 0
     c                   eval      vogrl1_oogr = c1ogrp
     c     vogrl1_key    chain     vogrl1                             91
     c                   if        *in91 = *off
     c                   eval      c1otxt = vgotxt
     c                   endif
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        c1hgrp <> 0
     c                   eval      vhgrl1_hogr = c1ogrp
     c                   eval      vhgrl1_hhgr = c1hgrp
     c     vhgrl1_key    chain     vhgrl1                             91
     c                   if        *in91 = *off
     c                   eval      c1htxt = vghtxt
     c                   endif
     c                   endif
      *
      * Undergruppe
      *
     c                   if        c1ugrp <> 0
     c                   eval      vugrl1_uogr = c1ogrp
     c                   eval      vugrl1_uhgr = c1hgrp
     c                   eval      vugrl1_uugr = c1ugrp
     c     vugrl1_key    chain     vugrl1                             91
     c                   if        *in91 = *off
     c                   eval      c1utxt = vgutxt
     c                   endif
     c                   endif
      *
      * Leverandør
      *
     c                   if        c1ldor <> 0
     c                   eval      jlevl1_ldor = c1ldor
     c     jlevl1_key    chain     jlevl1                             91
     c                   if        *in91 = *off
     c                   eval      c1lnav = jlnavn
     c                   endif
     c                   endif
      *
      * Modulnr
      *
     c                   if        c1modn <> 0
     c                   eval      jmodl1_modn = c1modn
     c     jmodl1_key    chain     jmodl1
     c                   if        %found
     c                   eval      c1mte1 = jumte1
     c                   endif
     c                   endif
      *
      * Varenummer
      *
     c                   if        c1vare <> *blank
     c                   eval      jvarl1_vare = c1vare
     c     jvarl1_key    chain     jvarl1                             91
     c                   if        *in91 = *off
     c                   eval      c1tek1 = jvtek1
     c                   eval      c1nobb = jvnobb
     c                   endif
     c                   endif
      *
      * Priskode
      *
     c                   if        c1kpri <> *blank
     c                   eval      vpkol1_pkod = c1kpri
     c     vpkol1_key    chain     vpkol1
     c                   if        %found
     c                   eval      c1ptxt = vaptxt
     c                   endif
     c                   endif
      *
      * Varetype
      *
     c                   if        c1vtyp <> *blank
     c                   eval      vvtyl1_vtyp = c1vtyp
     c     vvtyl1_key    chain     vvtyl1
     c                   if        %found
     c                   eval      c1ttxt = vavtxt
     c                   endif
     c                   endif
      *
      * Leveringstype
      *
     c                   if        c1lety <> 0
     c                   eval      vltpl1_lety = c1lety
     c     vltpl1_key    chain     vltpl1
     c                   if        %found
     c                   eval      c1lbes = vybesk
     c                   endif
     c                   endif
      *
     c                   endsr
6.35  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.35  * Finn info om logistikkvare for riktig prisgiver
6.35  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.35  *
6.35 c     finn_logivar  begsr
6.35  *
6.35 c                   eval      *in50 = *off
6.35 c                   eval      *in51 = *off
6.35 c
6.35 c                   if        %trim(w_jvqvnr) = ''
6.35 c                   leavesr
6.35 c                   endif
6.35 c
6.35 c                   eval      w_logl = ''
6.35 c                   eval      w_undv = 0
6.35 c                   movel     w_jvqvnr      w_jvquno
6.35 c/exec sql
6.35 c+                  select jvquno, jvqvnr
6.35 c+                  into :w_undv, :w_logl
6.35 c+                  from jvklpf
6.35 c+                  where (jvquno = :w_jvquno or
6.35 c+                  jvqvnr = :w_jvqvnr)
6.35 c+                  and jvqfir = :w_firm and
6.35 c+                  jvqldo = :w_ldor
6.35 c+                  fetch first 1 rows only
6.35 c/end-exec
6.35 c                   if        w_logl = w_jvqvnr
6.35 c                   eval      *in51 = *on
6.35 c                   elseif    w_undv > 0
6.35 c                   eval      *in50 = *on
6.35 c                   endif
6.35 c
6.35 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på påslag
      *
     c     jpfalr_key    klist
     c                   kfld                    w_firm
5.70 c                   kfld                    jpfalr_prgr
     c                   kfld                    jpfalr_ogrp
     c                   kfld                    jpfalr_hgrp
     c                   kfld                    jpfalr_ugrp
     c                   kfld                    jpfalr_ldor
     c                   kfld                    jpfalr_modn
     c                   kfld                    jpfalr_vare
     c                   kfld                    jpfalr_kpri
     c                   kfld                    jpfalr_vtyp
     c                   kfld                    jpfalr_lety
8.01 c                   kfld                    jpfalr_nofr
      *
      * Key for oppdatering av påslag
      *
     c     jpfalu_key    klist
     c                   kfld                    w_firm
5.70 c                   kfld                    jpfalu_prgr
     c                   kfld                    jpfalu_ogrp
     c                   kfld                    jpfalu_hgrp
     c                   kfld                    jpfalu_ugrp
     c                   kfld                    jpfalu_ldor
     c                   kfld                    jpfalu_modn
     c                   kfld                    jpfalu_vare
     c                   kfld                    jpfalu_kpri
     c                   kfld                    jpfalu_vtyp
     c                   kfld                    jpfalu_lety
8.01 c                   kfld                    jpfalu_nofr
      *
      * Key for posisjonering på overgruppe
      *
     c     jpfal1_key    klist
     c                   kfld                    w_firm
5.70 c                   kfld                    jpfal1_prgr
     c                   kfld                    jpfal1_ogrp
     c                   kfld                    jpfal1_hgrp
     c                   kfld                    jpfal1_ugrp
     c                   kfld                    jpfal1_ldor
     c                   kfld                    jpfal1_modn
     c                   kfld                    jpfal1_vare
     c                   kfld                    jpfal1_kpri
     c                   kfld                    jpfal1_vtyp
     c                   kfld                    jpfal1_lety
8.01 c                   kfld                    jpfal1_nofr
      *
      * Key for posisjonering på hovedgruppe
      *
     c     jpfal2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jpfal2_hgrp
      *
      * Key for posisjonering på undergruppe
      *
     c     jpfal3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jpfal3_ugrp
      *
      * Key for posisjonering på leverandør
      *
     c     jpfal4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jpfal4_ldor
      *
      * Key for posisjonering på modul
      *
     c     jpfal5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jpfal5_modn
      *
      * Key for posisjonering på varenummer
      *
     c     jpfal6_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jpfal6_vare
      *
      * Key for posisjonering på priskode
      *
     c     jpfal7_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jpfal7_kpri
      *
      * Key for posisjonering på varetype
      *
     c     jpfal8_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jpfal8_vtyp
      *
      * Key for posisjonering på leveringstype
      *
     c     jpfal9_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jpfal9_lety
5.70  *
5.70  * Key for posisjonering på overgruppe
5.70  *
5.70 c     jpfalc_key    klist
5.70 c                   kfld                    w_firm
5.70 c                   kfld                    jpfalc_ogrp
6.31 c                   kfld                    jpfalc_hgrp
6.31 c                   kfld                    jpfalc_ugrp
      *
      * Key for oppslag på påslag m/varenummer
      *
     c     jpfala_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jpfala_vare
5.70 c                   kfld                    jpfala_prgr
     c                   kfld                    jpfala_ldor
     c                   kfld                    jpfala_kpri
     c                   kfld                    jpfala_vtyp
     c                   kfld                    jpfala_lety
8.01 c                   kfld                    jpfala_nofr
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for oppslag på modul
      *
     c     jmodl1_key    klist
     c                   kfld                    jmodl1_modn
      *
      * Key for oppslag på vare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag på priskode
      *
     c     vpkol1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vpkol1_pkod
      *
      * Key for oppslag på varetype
      *
     c     vvtyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvtyl1_vtyp
      *
      * Key for oppslag på leveringstype
      *
     c     vltpl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltpl1_lety
5.70  *
5.70  * Key for oppslag på prisgruppe
5.70  *
5.70 c     ra30l1_key    klist
5.70 c                   kfld                    w_firm
5.70 c                   kfld                    ra30l1_dkod
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c     jpfal1_key    setll     jpfal1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
