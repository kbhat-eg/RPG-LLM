     h option(*nodebugio : *srcstmt) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : LI800R
      * Beskrivelse . . : Hent godkjente pakksedler
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       021019 bsa  Nytt program
7.01  *       170220 bsa  Må lukke "cursor" for å få den åpnet med riktige
7.01  *                   verdier igjen.
7.02  *       120520 bsa  Innfører låsekoder på linjenivå.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *    30-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fxphost    uf   e           k disk    rename(xphost:xphoiur)
     fxpkost    if   e           k disk    rename(xpkost:xpkoiur)
     flphoiu    uf a e           k disk    rename(lphost:lphoiur)
     flpkoi1    if   e           k disk    rename(lpkost:lpkoi1r)
     flpkli1    if   e           k disk    rename(lpklst:lpkli1r)
     flppai1    if   e           k disk    rename(lppast:lppai1r)
     f
      *
      * Definering av parametre
      *
     d p_dflldor       s              6  0
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_filg                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
      * Definering av variable
      *
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_akti          s              1    inz(*off)
     d b_read          s              1    inz(*off)
      *
     d w_firm          s                   like(xhfirm)
     d w_fsni          s                   like(xofsni)
     d w_fniv          s                   like(xofniv)
     d w_fspa          s                   like(xofspa)
     d w_sscc          s                   like(xosscc)
     d w_fast          s             10
     d w_fell          s              6
     d w_type          s              2
     d w_kode          s              1
     d w_mnum          s              8  0
     d w_dtyp          s              4
     d w_aarr          s              4  0
     d w_stat          s              2
     d w_numm          s              8  0
     d w_suff          s              2  0
     d w_venh          s              3
     d w_vant          s             15  4
7.02 d w_lock          s              1  0
      *
     d sqlquery        s           4096
     d w_antvar        s              3  0
     d w_teller        s              3  0
     d w_teller2       s              3  0
     d b_vgrpsjekk     s              3  0
     d b_inlr          s              3    inz(*off)
      *
      *  Tabell DS
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     C/Exec SQL
     C+  Set Option DatFmt=*ISO , commit=*none
     C/End-Exec
      *
      * Hovedprogram
      *
     c                   read      xphoiur
     c                   dow       not %eof
      *
      * Hent ut nytt meldingsnummer fra teller
      *
     c                   if        xhstat = 1
     c                   exsr      hnt_nummer
      *
     c                   clear                   lphoiur
     c                   eval      phfirm = xhfirm
     c                   eval      phtype = *blanks
     c                   eval      phmnum = w_mnum
     c                   eval      phaarr = w_aarr
     c                   eval      phpaid = xhpaid
     c                   eval      phpver = xhpver
     c                   eval      phpadt = xhpadt
     c                   eval      phpsdt = xhpsdt
     c                   eval      phpedt = xhpedt
     c                   eval      phbnum = xhbnum
     c                   eval      phlonr = xhlonr
     c                   eval      phprnr = xhprnr
     c                   eval      phtrst = xhtrst
     c                   eval      phtmat = xhtmat
     c                   eval      phtinf = xhtinf
     c                   eval      phtvol = xhtvol
     c                   eval      phtvek = xhtvek
     c                   eval      phthoy = xhthoy
     c                   eval      phtlen = xhtlen
     c                   eval      phtbre = xhtbre
     c                   eval      phlmat = xhlmat
     c                   eval      phtinf = xhtinf
     c                   eval      phlbet = xhlbet
     c                   eval      phmvar = xhmvar
7.02 c                   eval      phlock = w_lock
      *
     c                   time                    phodat
     c                   time                    photim
     c                   eval      phousr = l_user
     c                   time                    phedat
     c                   time                    phetim
     c                   eval      pheusr = l_user
      *
     c                   write     lphoiur
      *
      * Hent alle fra XPPA til LPPA med samme GUID
      *
      /FREE

       exec sql
         INSERT INTO LPPAST
         (PPFIRM,PPTYPE,PPMNUM,PPAARR,PPPAID,PPPVER
         ,PPPATY,PPSTAT,PPPART,PPNAVN,PPADR1,PPADR2
         ,PPPONR,PPSTED,PPLAND,PPKONT,PPTLNR,PPEMAL,PPODAT
         ,PPOTIM,PPOUSR,PPEDAT,PPETIM,PPEUSR)
         SELECT
          XPFIRM,:w_dtyp,:w_mnum,:w_aarr,:xhpaid,:xhpver
         ,XPPATY,:w_stat,XPPART,XPNAVN,XPADR1,XPADR2
         ,XPPONR,XPSTED,XPLAND,XPKONT,XPTLNR,XPEMAL,CURRENT_DATE
         ,CURRENT_TIME,:l_user,CURRENT_DATE,CURRENT_TIME
         ,:l_user
         FROM XPPAST WHERE
            ( XPGUID = :xhguid AND XPFIRM = :xhfirm );

      /END-FREE
      *
      * Hent alle fra XPKO til LPKO
      *
      /FREE

       exec sql
         INSERT INTO LPKOST
         (POFIRM,POTYPE,POMNUM,POAARR,POPAID,POPVER
         ,POFSNI,POFNIV,POFSPA,POKOTY,POPANT,POKSNR
         ,POSSCC,POHOYD,POHENH,POLENG,POLENH,POBRED
         ,POBENH,POVOLU,POVENH,POBVKT,PONVKT,POVEEN
7.02     ,POSTAT,POLOCK,POODAT,POOTIM,POOUSR,POEDAT,POETIM
         ,POEUSR)
         SELECT
          XOFIRM,:w_dtyp,:w_mnum,:w_aarr,:xhpaid,:xhpver
         ,XOFSNI,XOFNIV,XOFSPA,XOKOTY,XOPANT,XOKSNR
         ,XOSSCC,XOHOYD,XOHENH,XOLENG,XOLENH,XOBRED
         ,XOBENH,XOVOLU,XOVENH,XOBVKT,XONVKT,XOVEEN
7.02     ,:w_stat,:w_lock,CURRENT_DATE,CURRENT_TIME,:l_user
         ,CURRENT_DATE,CURRENT_TIME,:l_user
         FROM XPKOST WHERE
            ( XOGUID = :xhguid AND XOFIRM = :xhfirm );

      /END-FREE
      *
     c/exec sql
     c+        declare sok_l cursor for
     c+        select xofsni,
     c+               xofniv,
     c+               xofspa,
     c+               xosscc
     c+        from xpkost
     c+        where  xoguid = :xhguid and
     c+               xofirm = :xhfirm
     c/end-exec
     c/exec sql
     c+        open sok_l
     c/end-exec
      *
      * Hent alle fra XPKL til LPKL
      *
     c                   eval      b_read = *on
     c                   dow       b_read = *on
      *
     c/exec sql
     c+        fetch next
     c+        from sok_l
     c+        into   :w_fsni,
     c+               :w_fniv,
     c+               :w_fspa,
     c+               :xosscc
     c/end-exec
      *
     c                   if        sqlcod = 100 or sqlcod < 0
     c                   eval      b_read = *off
     c                   endif
      *
     c                   if        b_read = *on
      *
      /FREE

       exec sql
         INSERT INTO LPKLST
         (PLFIRM,PLTYPE,PLMNUM,PLAARR,PLPAID,PLPVER
         ,PLFSNI,PLFNIV,PLLINR,PLFSPA,PLSSCC,PLGTIN
         ,PLLVAR,PLNOBB,PLTUNN,PLFINF,PLNAVN,PLBANT
         ,PLBENH,PLPANT,PLPENH,PLRANT,PLRENH,PLRKOD
         ,PLRBES,PLRDAT,PLVANT,PLVENH,PLBNUM,PLNUMM
7.02     ,PLSUFF,PLBLIN,PLSTAT,PLLOCK,PLODAT,PLOTIM,PLOUSR
         ,PLEDAT,PLETIM,PLEUSR)
         SELECT
          XLFIRM,:w_dtyp,:w_mnum,:w_aarr,:xhpaid,:xhpver
         ,:w_fsni,:w_fniv,XLLINR,:w_fspa,XLSSCC,XLGTIN
         ,XLLVAR,XLNOBB,XLTUNN,XLFINF,XLNAVN,XLBANT
         ,XLBENH,XLPANT,XLPENH,XLRANT,XLRENH,XLRKOD
         ,XLRBES,XLRDAT,:w_vant,:w_venh,XLBNUM,:w_numm
7.02     ,:w_suff,XLBLIN,:w_stat,:w_lock,CURRENT_DATE,CURRENT_TIME,:l_user
         ,CURRENT_DATE,CURRENT_TIME,:l_user
         FROM XPKLST WHERE
            ( XLGUID = :xhguid AND XLFIRM = :xhfirm AND XLSSCC = :xosscc );

      /END-FREE
      *
     c                   endif
      *
     c                   enddo
      *
7.01 c/exec sql
7.01 c+        close sok_l
7.01 c/end-exec
      *
9.xx c**                 if        1 = 2
      *
      /FREE

       // Slette parter fra integrasjonstabell
       exec sql
         DELETE FROM XPPAST
           WHERE XPGUID = :xhguid AND XPFIRM = :xhfirm;

       // Slette kolli fra integrasjonstabell
       exec sql
         DELETE FROM XPKOST
           WHERE XOGUID = :xhguid AND XOFIRM = :xhfirm;

       // Slette kollilinjer fra integrasjonstabell
       exec sql
         DELETE FROM XPKLST
           WHERE XLGUID = :xhguid AND XLFIRM = :xhfirm;

      /END-FREE
      *
     c                   delete    xphost
      *
9.xx c**                 endif
      *
     c                   endif
      *
     c                   read      xphoiur
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å hente/oppdatere EDI-meldingsnummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hnt_nummer    begsr
      *
     c                   eval      w_fell = 'LEDIML'
     c                   eval      w_type = *blank
     c                   eval      w_fast = *blank
     c                   eval      w_mnum = 0
     c                   eval      w_kode = '0'
      *
      *  Kaller opp subprogram for henting av nummer fra NUMMER-REGISTER
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
     c                   parm                    w_mnum
      *
     c     end_mnum      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c*     *entry        plist
     c*                   parm                    p_dflldor
      *
      * Henter firma og referanse fra parameter
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_dtyp = *blanks
     c                   eval      w_aarr = *year
     c                   eval      w_stat = *blanks
7.02 c                   eval      w_lock = 0
      *
     c                   endsr
