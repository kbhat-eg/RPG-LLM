     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JR839R
      * Beskrivelse . . : Leser std. csv format - nobb priser
      *                   Denne oppdaterer nettopris på betingelse.
      *
      *                   Prisoppdatering skjer på bakgrunn av match på
      *                   leverandørens varenummer,nobbnr  eller ean-nr.
      *                   Oppdaterer betingelse
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       231115 bof  Nytt program
6.31  *       160916 bof  Feilretting vedr. påslag, fjerner dette.
8.01  *PRG2   140622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjrvapf    if   e           k disk
      *
     fjrapl1    if   e           k disk    rename(jrappfr:jrapl1r)
     fjvarl8    if   e           k disk    rename(jvarpfr:jvarl8r)
     fjbetl5    uf   e           k disk    rename(jbetpfr:jbetl5r)
     fjbetlu    uf a e           k disk    rename(jbetpfr:jbetlur)
     fjraelu    uf a e           k disk    rename(jraepfr:jraelur)
     fjwrkpf    uf a e           k disk    rename(jwrkpfr:jwrkpfr)
     fjpfal6    if   e           k disk    rename(jpfapfr:jpfal6r)
     fjpfalu    uf a e           k disk    rename(jpfapfr:jpfalur)
      *
      * Definering av parametre
      *
     d p_list          s             32
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
      *
      * Definering av parameterliste -inn
      *
     d                 ds
     d d_list                        32
     d  d_firm                        3  0 overlay(d_list)
     d  d_rapp                        2  0 overlay(d_list:4)
     d  d_dato                         d   overlay(d_list:6) datfmt(*iso)
8.01 d  d_prgr                        2    overlay(d_list:30)
      *
      * Definering av recordbeskrivelse - Ahlsell
      *
     d                 ds
     d d_inpu                       140
     d  d_lvnr                       15    overlay(d_inpu:1)
     d  d_netp                       10    overlay(d_inpu:93)
     d   d_netp_kr                    7  0 overlay(d_netp:1)
     d   d_netp_Øre                   2  2 overlay(d_netp:9)
     d  d_eann                       13    overlay(d_inpu:117)
     d  d_eannn                      13  0 overlay(d_eann:1)
     d  d_nobb                        8    overlay(d_inpu:130)
     d  d_nobbn                       8  0 overlay(d_nobb:1)
      *
     d  d_ex           s            512
     d ew92            c                   '       ,  -'
      *
      * Definering av variable i nøkler
      *
     d jrapl1_rapp     s                   like(jrrapp)
     d jvarl8_ldor     s                   like(jvldor)
     d jvarl8_lvar     s                   like(jvlvar)
     d jbetl5_ldor     s                   like(jpldor)
     d jbetl5_vare     s                   like(jpvare)
     d jraelu_eldo     s                   like(jpeldo)
     d jraelu_eprg     s                   like(jpeprg)
     d jraelu_eogr     s                   like(jpeogr)
     d jraelu_ehgr     s                   like(jpehgr)
     d jraelu_eugr     s                   like(jpeugr)
     d jraelu_emod     s                   like(jpemod)
     d jraelu_evar     s                   like(jpevar)
     d jraelu_evty     s                   like(jpevty)
     d jraelu_elet     s                   like(jpelet)
     d jraelu_ebrs     s                   like(jpebrs)
     d jraelu_ebli     s                   like(jpebli)
     d jpfal6_vare     s                   like(jcvare)
     d jpfal6_prgr     s                   like(jcprgr)
      *
      * Definering av variable
      *
     d b_inlr          s              1
     d b_ok            s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s              3  0
     d w_netp          s              9  2
     d w_leng          s              2  0
     d w_posi          s              2  0
     d w_navn          s             12
      *
      *
      * Definering av konstanter
      *
     d c_digi          s             15    inz('0123456789, ')
     d c_null          s              7    inz('0000000')
      *
     d Lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d Up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter rapportør-informasjon
      *
     c                   eval      jrapl1_rapp = d_rapp
     c     jrapl1_key    chain     jrapl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
     c*                  exsr      feil_init
      *
      * Leser fil.
      *
     c                   read      jrvapf
     c                   dow       not %eof
     c                   exsr      behandling
     c                   read      jrvapf
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
     c*                  if        b_feil = *on
     c*                  eval      w_navn = 'fl' +
     c*                                     %char(jrldor) +
     c*                                     '.xls'
     c*                  call      'JR809C '
     c*                  parm                    w_navn
     c*                  parm                    jrfldr
     c*                  endif
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av varer/priser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
     c                   eval      d_inpu = *blank
     c                   exsr      utled_ds
      *
     c                   if        d_lvnr = *blank or b_ok = *off
     c                   goto      end_oppdat
     c                   endif
      *
      * Starter oppdatering
      *
      * finner vare ut fra lev varenr.
      *
     c                   eval      jvarl8_ldor = jrldor
     c                   eval      jvarl8_lvar = d_lvnr
     c     jvarl8_key    chain     jvarl8
     c                   if        %found
     c                   exsr      oppd_beti
     c                   endif
      *
     c     end_oppdat    endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting av betingelse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_beti     begsr
      *
      * Sletter betinglese på vare-leveradnør
      *
     c                   eval      jbetl5_ldor = jrldor
     c                   eval      jbetl5_vare = jvvare
     c     jbetl5_key    setll     jbetl5r
     c     jbetl5_key    reade     jbetl5r
     c                   dow       not %eof
     c                   if        jprsts = 0   and
     c                             jpprgr = d_prgr
     c                   delete    jbetl5r
      *
     c                   eval      jraelu_eldo = jpldor
     c                   eval      jraelu_eprg = jpprgr
     c                   eval      jraelu_eogr = jpogrp
     c                   eval      jraelu_ehgr = jphgrp
     c                   eval      jraelu_eugr = jpugrp
     c                   eval      jraelu_emod = jpmodn
     c                   eval      jraelu_evar = jpvare
     c                   eval      jraelu_evty = jpvtyp
     c                   eval      jraelu_elet = jplety
     c                   eval      jraelu_ebrs = jprsts
     c                   eval      jraelu_ebli = jpline
     c     jraelu_key    setll     jraelur
     c     jraelu_key    reade     jraelur
     c                   dow       not %eof
     c                   delete    jraelur
     c     jraelu_key    reade     jraelur
     c                   enddo
     c                   endif
     c     jbetl5_key    reade     jbetl5r
     c                   enddo
      *
      * Oppretter betingelse
      *
     c                   clear                   jbetlur
      *
     c                   eval      jpfirm = w_firm
     c                   eval      jpldor = jrldor
     c                   eval      jpprgr = d_prgr
     c                   eval      jpogrp = jvogrp
     c                   eval      jphgrp = jvhgrp
     c                   eval      jpugrp = jvugrp
     c                   eval      jpmodn = jvmodn
     c                   eval      jpvare = jvvare
     c                   eval      jpvtyp = *blank
     c                   eval      jplety = 0
     c                   eval      jprsts = 0
     c                   eval      jpline = 1
     c                   eval      jpbeti = 'Nettopris'
     c                   eval      jpfdat = *loval
     c                   eval      jptdat = *loval
     c                   eval      jpdato = *loval
     c                   eval      jpakti = 1
     c                   eval      jpeusr = l_user
      *
     c                   time                    jpodat
     c                   time                    jpedat
     c                   time                    jpetim
     c
     c                   write     jbetlur
      *
      * Oppretter rabattelement
      *
     c                   clear                   jraelur
      *
     c                   eval      jpefir = w_firm
     c                   eval      jpeldo = jpldor
     c                   eval      jpeprg = jpprgr
     c                   eval      jpeogr = jpogrp
     c                   eval      jpehgr = jphgrp
     c                   eval      jpeugr = jpugrp
     c                   eval      jpemod = jpmodn
     c                   eval      jpevar = jpvare
     c                   eval      jpevty = jpvtyp
     c                   eval      jpelet = jplety
     c                   eval      jpebrs = jprsts
     c                   eval      jpebli = jpline
     c                   eval      jperst = 0
     c                   eval      jpelin = 1
     c                   eval      jperae = 'Nettopris'
     c                   eval      jpereg = 'N'
     c                   eval      jpever = d_netp_kr + d_netp_Øre
     c                   eval      jpefda = *loval
     c                   eval      jpetda = *loval
     c                   eval      jpeeus = l_user
      *
     c                   time                    jpeoda
     c                   time                    jpeeda
     c                   time                    jpeeti
     c
     c                   write     jraelur
      *
      *
      * Oppretter/oppdaterer søketekster
      *
     c                   eval      b_inlr = *on
      *
     c                   call      'JV790R'
     c                   parm                    w_firm
     c                   parm                    jvvare
     c                   parm                    b_inlr
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting av påslag
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.31 c*    oppd_pasl     begsr
6.31  *
6.31  * hvis det finnes skal det ikke dannes
6.31  *
6.31 c*                  eval      jpfal6_vare = jvvare
6.31 c*                  eval      jpfal6_prgr = d_prgr
6.31 c*    jpfal6_key    chain     jpfal6r
6.31 c*                  if        %found
6.31 c*                  leavesr
6.31 c*                  endif
6.31  *
6.31 c*                  clear                   jpfalur
6.31  *
6.31 c*                  eval      jcfirm = w_firm
6.31 c*                  eval      jcprgr = d_prgr
6.31 c*                  eval      jcogrp = jvogrp
6.31 c*                  eval      jchgrp = jvhgrp
6.31 c*                  eval      jcugrp = jvugrp
6.31 c*                  eval      jcldor = jrldor
6.31 c*                  eval      jcmodn = jvmodn
6.31 c*                  eval      jcvare = jvvare
6.31 c*                  eval      jckofa = 1
6.31 c*                  eval      jckobe = 0
6.31  *
6.31 c*                  if        d_prgr = 'L'
6.31 c*                  eval      jcsafa = 3
6.31 c*                  else
6.31 c*                  eval      jcsafa = 2
6.31 c*                  endif
6.31  *
6.31 c*                  eval      jceusr = l_user
6.31  *
6.31 c*                  time                    jcodat
6.31 c*                  time                    jcedat
6.31 c*                  time                    jcetim
6.31  *
6.31 c*                  write     jpfalur
6.31  *
6.31 c*                  endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utleding av datastruktur
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     utled_ds      begsr
      *
     c     '"':' '       xlate     jrvrad        jrvrad
     c                   eval      b_ok = *off
      *
      * Lev. varenr
      *
     c                   eval      w_posi = %scan(';':jrvrad)
     c                   eval      d_lvnr = %trim(%subst(jrvrad:1:w_posi-1))
      * kun blanke hoppes over
     c                   if        d_lvnr = *blank
     c                   leavesr
     c                   endif
      *
      * EAN
      *
     c                   eval      jrvrad = %subst(jrvrad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':jrvrad)
     c                   eval      d_eann = %trim(%subst(jrvrad:1:w_posi-1))
     c     ' ':'0'       xlate     d_eann        d_eann
      *
      *  nettopris
      *
     c                   eval      jrvrad = %subst(jrvrad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':jrvrad)
     c                   eval      d_netp = %trim(%subst(jrvrad:1:w_posi-1))
     c                   if        %check(c_digi : d_netp) > *zero
     c                   leavesr
     c                   endif
      *
      * nobb.nr
      *
     c                   eval      jrvrad = %subst(jrvrad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':jrvrad)
     c                   if        w_posi = 0
     c                   eval      d_nobb = %trim(%subst(jrvrad:1:8))
     c                   else
     c                   eval      d_nobb = %trim(%subst(jrvrad:1:w_posi-1))
     c                   endif
     c     ' ':'0'       xlate     d_nobb        d_nobb
      *
      * Utleder  nettopris
      *
     c                   if        d_netp <> *blank
      *
     c                   eval      d_netp = %trim(d_netp)
     c                   eval      w_posi = %scan(',':d_netp)
     c                   eval      w_leng = %len(%trim(d_netp))
      *
     c                   if        w_posi = 0
     c                   eval      w_posi = w_leng + 1
     c                   eval      d_netp = %trim(d_netp) + '.00'
     c                   endif
      *
     c     ' ':'0'       xlate     d_netp        d_netp
      *
     c                   if        (w_posi-1) < 7
     c                   eval      d_netp = %subst(c_null:1:7-w_posi+1) +
     c                                      %subst(d_netp:1:w_posi-1) +
     c                                      '.' +
     c                                      %subst(d_netp:w_posi+1:2)
     c                   endif
      *
     c                   else
     c                   eval      d_netp = '0000000.00'
     c                   endif
      *
     c                   eval      b_ok = *on
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å skrive ut varer som ikke ble lest inn
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c*    feilliste     begsr
      *
     c*                  eval      b_feil = *on
     c*                  eval      w_netp = d_netp_kr + d_netp_Øre
      *
     c*                  eval      d_ex = *blank
     c*                  eval      d_ex = %trim(d_lvnr) + X'05' +
     c*                                   %char(d_Eannn) + X'05' +
     c*                                   %char(d_nobbn) + X'05' +
     c*                                   %editw(w_netp : ew92) + X'05'
     c*                  eval      jwdata   = d_ex
     c*                  write     jwrkpfr
     c*                  endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å nullstiller JWRKPF og skrive heading
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c*    feil_init     begsr
      *
     c*                  read      jwrkpf
     c*                  dow       not %eof(jwrkpf)
     c*                  delete    jwrkpfr
     c*                  read      jwrkpf
     c*                  enddo
      *
     c*                  eval      d_ex = *blank
     c*                  eval      d_ex = 'Lev.varenr.'  + X'05' +
     c*                                   'GTIN-nr'      + X'05' +
     c*                                   'Nobb.nr '     + X'05' +
     c*                                   'Nettopris'    + X'05'
     c*                  eval      jwdata   = d_ex
     c*                  write     jwrkpfr
     c*                  endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_list
      *
      * Key for oppslag på rapportør
      *
     c     jrapl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jrapl1_rapp
      *
      * Key for oppslag på vare m/lev.varenummer
      *
     c     jvarl8_key    klist
     c                   kfld                    jvarl8_ldor
     c                   kfld                    jvarl8_lvar
      *
      * Key for oppdatering av betingelse
      *
     c     jbetl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jbetl5_ldor
     c                   kfld                    jbetl5_vare
      *
      * Key for oppdatering av rabattelement
      *
     c     jraelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jraelu_eldo
     c                   kfld                    jraelu_eprg
     c                   kfld                    jraelu_eogr
     c                   kfld                    jraelu_ehgr
     c                   kfld                    jraelu_eugr
     c                   kfld                    jraelu_emod
     c                   kfld                    jraelu_evar
     c                   kfld                    jraelu_evty
     c                   kfld                    jraelu_elet
     c                   kfld                    jraelu_ebrs
     c                   kfld                    jraelu_ebli
      *
      * Key for oppdatering av kode/status
      *
     c     jstslu_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppdatering av påslag
      *
     c     jpfal6_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jpfal6_vare
     c                   kfld                    jpfal6_prgr
      *
      *
      * Fyller datastruktur fra parameter, og henter firma
      *
     c                   eval      d_list = p_list
     c                   eval      w_firm = d_firm
      *
     c                   endsr
