     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLOGI
      * Program . . . . : MM512R
      * Beskrivelse . . : Innkjøpsparameter Arbeidsregister (simulert)
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       190405 bø   Nytt program
      *       310507 bø   Utvidet b.punkt, m.meng,mini til 3 desimaler
5.60  *       050408 bø   Lagerenhet fra vare-register
5.70  * PRGR  021208 bof  Utvidet med prisgruppe
6.20  * 6.20  120112 bsa  Utvidet VP750R med kampanje
7.xx  * 7.00  210316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS          *
      *
8.01  *PRG2   160622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
pref fmprolr    if   e           k disk    rename(mpropfr:mprolrr)
pref f                                     prefix(mx:2)
     fmprwl3    if   e           k disk    rename(mpropfr:mprol3r)
     fmprwl4    if   e           k disk    rename(mpropfr:mprol4r)
     fmprwl5    if   e           k disk    rename(mpropfr:mprol5r)
     fmprwl6    if   e           k disk    rename(mpropfr:mprol6r)
     fmprwl7    if   e           k disk    rename(mpropfr:mprol7r)
     fmprwl8    if   e           k disk    rename(mpropfr:mprol8r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
      *
     fmm512d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Informotion Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
     d h1kabc          ds
     d  d_kabc                        1
     d  d_kxyz                        1
      *
      * Definering av variabler i nøkler
      *
     d mprolr_vare     s                   like(movare)
     d mprwl3_vare     s                   like(movare)
     d mprwl4_lage     s                   like(molage)
     d mprwl5_kabc     s                   like(mokabc)
     d mprwl6_bpun     s                   like(mobpun)
     d mprwl7_bmen     s                   like(mobmen)
     d mprwl8_mini     s                   like(momini)
     d vvarl1_vare     s                   like(vvvare)
     d vlagl1_vare     s                   like(vlvare)
     d vlagl1_lage     s                   like(vllage)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
     d w_lety          s              1  0
     d w_kopr          s              9  2
     d w_inpr          s              9  2
     d w_lver          s             11  2
     d w_enhe          s                   like(vlenhl)
     d w_dato          s               d   datfmt(*iso)
     d w_abc           s              1
     d w_xyz           s              1
8.01 d w_prgr          s              2
5.70 d w_avde          s              4  0
6.20 d w_kamp          s              5
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_vise          s              1    inz(*off)
     d b_blaf          s              1    inz(*off)
     d b_abck          s              1    inz(*off)
      *
     d w_firm          s                   like(mofirm)
     d w_vsor          s                   like(movsor)
     d w_ogrp          s                   like(moogrp)
     d w_hgrp          s                   like(mohgrp)
     d w_ugrp          s                   like(mougrp)
     d w_ldor          s                   like(moldor)
     d w_vare          s                   like(movare)
     d w_vark          s                   like(movare)
     d w_vares         s                   like(movare)
     d w_tek1          s                   like(vvtek1)
     d w_navn          s             30
     d w_dag           s              2  0
     d w_mnd           s              2  0
     d w_aar           s              4  0
     d w_uke           s              2  0
     d w_stat          s              1
     d w_jkod          s              2  0
     d w_jtxt          s             30
     d p_vsor          s                   like(movsor)
     d p_ogrp          s                   like(moogrp)
     d p_hgrp          s                   like(mohgrp)
     d p_ugrp          s                   like(mougrp)
     d p_ldor          s                   like(moldor)
     d p_vark          s                   like(movare)
     d w_pdat          s               d   datfmt(*iso)
     d w_paar          s              4  0
     d w_puke          s              2  0
     d w_psts          s              1
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnumoer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnumoer i subfile
      * w_sfrn - Første recordnumoer på siste side
      * w_ssrn - Siste recordnumoer på siste side
      * w_curn - Current recordnumoer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inka
     c                   exsr      xh1win
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2taga
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkk
     c                   if        b_vise = *off
     c                   eval      b_vise = *on
     c                   else
     c                   eval      b_vise = *off
     c                   endif
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2vare <> *blank or
     c                             b2lage <> 0      or
     c                             b2kabc <> *blank or
     c                             b2bpun <> 0      or
     c                             b2bmen <> 0      or
     c                             b2mini <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   if        b_blaf = *off
     c                   select
     c                   when      w_seqe = 'C'
     c                   eval      mprwl4_lage = b1lage
     c     mprwl4_key    setll     mprwl4
     c                   when      w_seqe = 'D'
     c                   eval      mprwl5_kabc = b1kabc
     c     mprwl5_key    setll     mprwl5
     c                   when      w_seqe = 'E'
     c                   eval      mprwl6_bpun = b1bpun
     c     mprwl6_key    setll     mprwl6
     c                   when      w_seqe = 'F'
     c                   eval      mprwl7_bmen = b1bmen
     c     mprwl7_key    setll     mprwl7
     c                   when      w_seqe = 'G'
     c                   eval      mprwl8_mini = b1mini
     c     mprwl8_key    setll     mprwl8
     c                   other
     c                   eval      mprwl3_vare = b1vare
     c     mprwl3_key    setll     mprwl3
     c                   endsl
     c                   endif
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Vare
      *
     c                   if        b2vare <> *blank
     c                   eval      w_seqe = ' '
     c                   movel     b2vare        mprwl3_vare
     c     mprwl3_key    setll     mprwl3
     c                   goto      end_pos
     c                   endif
      *
      * Lager
      *
     c                   if        b2lage <> 0
     c                   eval      w_seqe = 'C'
     c                   eval      mprwl4_lage = b2lage
     c     mprwl4_key    setll     mprwl4
     c                   goto      end_pos
     c                   endif
      *
      * abckode
      *
     c                   if        b2kabc <> *blank
     c                   eval      w_seqe = 'D'
     c                   eval      mprwl5_kabc = b2kabc
     c     mprwl5_key    setll     mprwl5
     c                   goto      end_pos
     c                   endif
      *
      * best.punkt
      *
     c                   if        b2bpun <> 0
     c                   eval      w_seqe = 'E'
     c                   eval      mprwl6_bpun = b2bpun
     c     mprwl6_key    setll     mprwl6
     c                   goto      end_pos
     c                   endif
      *
      * best.mengde
      *
     c                   if        b2bmen <> 0
     c                   eval      w_seqe = 'F'
     c                   eval      mprwl7_bmen = b2bmen
     c     mprwl7_key    setll     mprwl7
     c                   goto      end_pos
     c                   endif
      *
      * mini.lager
      *
     c                   if        b2mini <> 0
     c                   eval      w_seqe = 'G'
     c                   eval      mprwl8_mini = b2mini
     c     mprwl8_key    setll     mprwl8
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2vare = *blank
     c                   eval      b2kabc = *blank
     c                   eval      b2lage = 0
     c                   eval      b2bpun = 0
     c                   eval      b2bmen = 0
     c                   eval      b2mini = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Vis
      *
     c*                  if        b1valg = 5
     c*                  eval      c1type = b1type
     c*                  eval      c1ogrp = b1ogrp
     c*                  eval      c1hgrp = b1hgrp
     c*                  eval      c1ugrp = b1ugrp
     c*                  exsr      xc2bld
     c*                  eval      b1valg = 0
     c*                  update    b1sfl
     c*                  iter
     c*                  endif
      *
      * Vise prognose pr. uke
      *
     c                   if        b1valg = 7
     c     *dmy          move      UDATE         w_dato
      *
     c                   eval      w_pdat = w_dato
     c                   call      'AX711R  '
     c                   parm                    w_pdat
     c                   parm                    w_paar
     c                   parm                    w_puke
     c                   parm                    w_psts
     c                   eval      w_uke = w_puke
     c                   eval      w_aar = w_paar
     c                   movel     b1vare        w_vare
     c                   call      'MM502R'
     c                   parm                    w_vare
     c                   parm                    b1lage
     c                   parm                    w_aar
     c                   parm                    w_uke
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vise lagervedlikehold
      *
     c                   if        b1valg = 8
     c                   movel     b1vare        w_vare
     c                   call      'LL101R'
     c                   parm                    w_vare
     c                   parm                    b1lage
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vise lagerspørring
      *
     c                   if        b1valg = 9
     c                   movel     b1vare        w_vare
     c                   call      'LL502R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * xh1win  Bilde for behandling av bla-alternativer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xh1win        begsr
      *
     c     h1taga        tag
      *
     c                   write     h1win
     c                   exfmt     h1win
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc
     c                   goto      end_h1win
     c                   endsl
      *
     c                   if        h1kabc <> *blank
     c                   eval      b_blaf = *on
     c                   else
     c                   eval      b_blaf = *off
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'G'
     c                   eval      mprwl8_mini = 0
     c     mprwl8_key    setll     mprwl8
     c                   when      w_seqe = 'F'
     c                   eval      mprwl7_bmen = 0
     c     mprwl7_key    setll     mprwl7
     c                   when      w_seqe = 'E'
     c                   eval      mprwl6_bpun = 0
     c     mprwl6_key    setll     mprwl6
     c                   when      w_seqe = 'D'
     c                   eval      mprwl5_kabc = *blank
     c     mprwl5_key    setll     mprwl5
     c                   when      w_seqe = 'C'
     c                   eval      mprwl4_lage = 0
     c     mprwl4_key    setll     mprwl4
     c                   other
     c                   eval      mprwl3_vare = *blank
     c     mprwl3_key    setll     mprwl3
     c                   endsl
      *
     c     end_h1win     endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømoing av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'C'
     c                   read      mprwl4
     c                   when      w_seqe = 'D'
     c                   read      mprwl5
     c                   when      w_seqe = 'E'
     c                   read      mprwl6
     c                   when      w_seqe = 'F'
     c                   read      mprwl7
     c                   when      w_seqe = 'G'
     c                   read      mprwl8
     c                   other
     c                   read      mprwl3
     c                   endsl
      *
     c                   if        %eof or
     c                             mofirm <> w_firm or
     c                             moogrp <> w_ogrp or
     c                             mohgrp <> w_hgrp or
     c                             mougrp <> w_ugrp or
     c                             moldor <> w_ldor or
     c                             movark <> w_vark
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
      * Uaktuell varesortiment
      *
     c                   if        w_vsor <> *blank and
     c                             movsor <> w_vsor
     c                   iter
     c                   endif
      *
      * Uaktuell overgruppe
      *
     c                   if        w_ogrp <> 0  and
     c                             moogrp <> w_ogrp
     c                   iter
     c                   endif
      *
      * Uaktuell hovedgruppe
      *
     c                   if        w_hgrp <> 0  and
     c                             w_hgrp <> mohgrp
     c                   iter
     c                   endif
      *
      * Uaktuell undergruppe
      *
     c                   if        w_ugrp <> 0 and
     c                             w_ugrp <> mougrp
     c                   iter
     c                   endif
      *
      * Uaktuell vare
      *
     c                   if        w_vark <> *blank and
     c                             movark <> w_vark
     c                   iter
     c                   endif
      *
      * Uaktuell lager
      *
     c                   if        h1lage <> 0 and
     c                             molage <> h1lage
     c                   iter
     c                   endif
      *
      * Uaktuell abckode
      *
     c                   move      mokabc        w_xyz
     c                   movel     mokabc        w_abc
     c                   if        h1kabc <> *blank
     c                   eval      b_abck = *off
      * bare xyz
     c                   if        d_kabc = *blank
     c                   if        d_kxyz <> w_xyz
     c                   iter
     c                   else
     c                   eval      b_abck = *on
     c                   endif
     c                   endif
      * bare abc
     c                   if        b_abck = *off
     c                   if        d_kxyz = *blank
     c                   if        d_kabc <> w_abc
     c                   iter
     c                   else
     c                   eval      b_abck = *on
     c                   endif
     c                   endif
     c                   endif
      * både og
     c                   if        b_abck = *off
     c                   if        h1kabc <> mokabc
     c                   iter
     c                   else
     c                   eval      b_abck = *on
     c                   endif
     c                   endif
     c                   endif
      *
      * Uaktuell best.punkt
      *
     c                   if        h1bpun <> 0
     c                   if        h1bpov = 0 and
     c                             mobpun > h1bpun
     c                   iter
     c                   endif
     c                   if        h1bpov = 1 and
     c                             mobpun < h1bpun
     c                   iter
     c                   endif
     c                   endif
      *
      * Uaktuell best.mengde
      *
     c                   if        h1bmen <> 0
     c                   if        h1bmov = 0 and
     c                             mobmen > h1bmen
     c                   iter
     c                   endif
     c                   if        h1bmov = 1 and
     c                             mobmen < h1bmen
     c                   iter
     c                   endif
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'C'
     c                   eval      mprwl4_lage = molage
     c                   when      w_seqe = 'D'
     c                   eval      mprwl5_kabc = mokabc
     c                   when      w_seqe = 'E'
     c                   eval      mprwl6_bpun = mobpun
     c                   when      w_seqe = 'F'
     c                   eval      mprwl7_bmen = mobmen
     c                   when      w_seqe = 'G'
     c                   eval      mprwl8_mini = momini
     c                   other
     c                   eval      mprwl3_vare = movare
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1vare = movare
     c                   movel     movare        vvarl1_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found(vvarl1)
     c                   movel     vvtek1        b1tek1
     c                   else
     c                   eval      b1tek1 = 'Ukjent vare'
     c                   endif
     c                   eval      b1enhl = vvenhl
      *
     c                   eval      b1lage = molage
     c                   movel     movare        vlagl1_vare
     c                   eval      b1kabc = mokabc
     c                   eval      vlagl1_lage = molage
     c     vlagl1_key    chain     vlagl1
5.60 c                   eval      w_enhe = vvenhl
     c                   eval      b1text = *blank
     c                   if        b_vise = *off
     c                   eval      b1text = %subst(b1vare:1:8) + ' ' +
     c                                      %editw(b1lage:'  ') + ' ' +
     c                                      %subst(b1kabc:1:2) + ' ' +
     c                                      %subst(b1tek1:1:17) + ' ' +
     c                                      %subst(b1enhl:1:3)
     c                   else
     c                   movel     b1tek1        b1text
     c                   endif
     c                   exsr      finn_pris
     c                   eval(h)   b1mini = momini
     c                   eval(h)   b1bmen = mobmen
     c                   eval(h)   b1bpun = mobpun
     c                   eval      b1vsor = movsor
     c                   eval      b1ogrp = moogrp
     c                   eval      b1hgrp = mohgrp
     c                   eval      b1ugrp = mougrp
     c*                  eval      b1bmenh = 0
     c*                  eval      b1bpunh = 0
     c*                  eval      b1minih = 0
     c                   if        b_vise = *on
     c                   eval      mprolr_vare = movare
     c     mprolr_key    chain     mprolr
     c                   if        %found(mprolr)
     c*                  eval(h)   b1bmenh = mxbmen
     c*                  eval(h)   b1bpunh = mxbpun
     c*                  eval(h)   b1minih = mxmini
     c                   else
     c*                  eval      b1bmenh = 0
     c*                  eval      b1bpunh = 0
     c*                  eval      b1minih = 0
     c                   endif
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'C'
     c     mprwl4_key    setgt     mprwl4
     c                   readp     mprwl4
     c                   when      w_seqe = 'D'
     c     mprwl5_key    setgt     mprwl5
     c                   readp     mprwl5
     c                   when      w_seqe = 'E'
     c     mprwl6_key    setgt     mprwl6
     c                   readp     mprwl6
     c                   when      w_seqe = 'F'
     c     mprwl7_key    setgt     mprwl7
     c                   readp     mprwl7
     c                   when      w_seqe = 'G'
     c     mprwl8_key    setgt     mprwl8
     c                   readp     mprwl8
     c                   other
     c     mprwl3_key    setgt     mprwl3
     c                   readp     mprwl3
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'C'
     c                   readp     mprwl4
     c                   when      w_seqe = 'D'
     c                   readp     mprwl5
     c                   when      w_seqe = 'E'
     c                   readp     mprwl6
     c                   when      w_seqe = 'F'
     c                   readp     mprwl7
     c                   when      w_seqe = 'G'
     c                   readp     mprwl8
     c                   other
     c                   readp     mprwl3
     c                   endsl
      *
     c                   if        %eof or
     c                             mofirm <> w_firm or
     c                             moogrp <> w_ogrp
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'C'
     c                   eval      mprwl4_lage = molage
     c                   when      w_seqe = 'D'
     c                   eval      mprwl5_kabc = mokabc
     c                   when      w_seqe = 'E'
     c                   eval      mprwl6_bpun = mobpun
     c                   when      w_seqe = 'F'
     c                   eval      mprwl7_bmen = mobmen
     c                   when      w_seqe = 'G'
     c                   eval      mprwl8_mini = momini
     c                   other
     c                   eval      mprwl3_vare = movare
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'C'
     c     mprwl4_key    setll     mprwl4
     c                   when      w_seqe = 'D'
     c     mprwl5_key    setll     mprwl5
     c                   when      w_seqe = 'E'
     c     mprwl6_key    setll     mprwl6
     c                   when      w_seqe = 'F'
     c     mprwl7_key    setll     mprwl7
     c                   when      w_seqe = 'G'
     c     mprwl8_key    setll     mprwl8
     c                   other
     c     mprwl3_key    setll     mprwl3
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
     c                   movel     b2vare        w_vares
     c                   call      'VV500R  '
     c                   parm                    w_firm
     c                   parm                    w_vares
     c                   parm                    w_tek1
     c                   movel     w_vares       b2vare
      *
     c     end_spØrr     endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Finner kostpris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_pris     begsr
      *
5.70 c                   exsr      hent_prisgr
     c                   eval      w_lety = 0
6.20 c                   eval      w_kamp = *blanks
     c                   if        mokdat <> *loval
     c                   eval      w_dato  = mokdat
     c                   else
     c                   time                    w_dato
     c                   endif
      *
     c                   call      'VP750R'
     c                   parm                    w_firm
     c                   parm                    vvvare
5.70 c                   parm                    w_prgr
     c                   parm                    w_lety
     c                   parm                    w_dato
     c                   parm                    w_enhe
     c                   parm                    w_kopr
     c                   parm                    w_inpr
6.20 c                   parm                    w_kamp
      *
     c                   endsr
      *
5 70  *
5.70  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5 70  * Subrutine for å hente prisgruppe
5.70  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.70  *
5.70 c     hent_prisgr   begsr
5.70  *
5.70 c                   eval      w_avde = 0
5.70 c                   call      'VL712R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    molage
5.70 c                   parm                    w_avde
5.70 c                   parm                    w_prgr
5.70  *
5.70 c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på vare
      *
     c     mprwl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vsor
     c                   kfld                    w_ogrp
     c                   kfld                    w_hgrp
     c                   kfld                    w_ugrp
     c                   kfld                    w_ldor
     c                   kfld                    w_vark
     c                   kfld                    mprwl3_vare
      *
      * Key for posisjonering på lager
      *
     c     mprwl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vsor
     c                   kfld                    w_ogrp
     c                   kfld                    w_hgrp
     c                   kfld                    w_ugrp
     c                   kfld                    w_ldor
     c                   kfld                    w_vark
     c                   kfld                    mprwl4_lage
      *
      * Key for posisjonering på kabc
      *
     c     mprwl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vsor
     c                   kfld                    w_ogrp
     c                   kfld                    w_hgrp
     c                   kfld                    w_ugrp
     c                   kfld                    w_ldor
     c                   kfld                    w_vark
     c                   kfld                    mprwl5_kabc
      *
      * Key for posisjonering på best.punkt
      *
     c     mprwl6_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vsor
     c                   kfld                    w_ogrp
     c                   kfld                    w_hgrp
     c                   kfld                    w_ugrp
     c                   kfld                    w_ldor
     c                   kfld                    w_vark
     c                   kfld                    mprwl6_bpun
      *
      * Key for posisjonering på best.mengde
      *
     c     mprwl7_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vsor
     c                   kfld                    w_ogrp
     c                   kfld                    w_hgrp
     c                   kfld                    w_ugrp
     c                   kfld                    w_ldor
     c                   kfld                    w_vark
     c                   kfld                    mprwl7_bmen
      *
      * Key for posisjonering på best.mengde
      *
     c     mprwl8_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vsor
     c                   kfld                    w_ogrp
     c                   kfld                    w_hgrp
     c                   kfld                    w_ugrp
     c                   kfld                    w_ldor
     c                   kfld                    w_vark
     c                   kfld                    mprwl8_mini
      *
      * Key for les av prognose
      *
     c     mprolr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mprolr_vare
      *
      * Key for les av vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for les av lager
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl1_vare
     c                   kfld                    vlagl1_lage
      *
     C     *ENTRY        PLIST
     C                   PARM                    p_vsor
     C                   PARM                    p_ogrp
     C                   PARM                    p_hgrp
     C                   PARM                    p_ugrp
     C                   PARM                    p_ldor
     C                   PARM                    p_vark
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_ogrp = p_ogrp
     c                   eval      w_vsor = p_vsor
     c                   eval      w_hgrp = p_hgrp
     c                   eval      w_ugrp = p_ugrp
     c                   eval      w_ldor = p_ldor
     c                   eval      w_vark = p_vark
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c     mprwl3_key    setll     mprwl3
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
