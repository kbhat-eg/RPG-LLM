     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : VG601R
      * Beskrivelse . . : Varegruppe, utskrift - parameter
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       051099 HKO  Nytt program
3.11  * 3.11  020100 AMD  Lagt inn utskrift av produktansv. på alle nivå
      *       161103 HKO  Fjernet behandling av rabattgruppe
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
      *
     fvg601p    o    e             printer
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av variabler i nøkler
      *
     d vhgrl1_hogr     s                   like(vghogr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vgufir)
     d w_timestamp     s             12  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initiering og utskrift av hode
      *
     c                   exsr      hode
      *
      * Behandler utskift
      *
     c                   exsr      behandling
      *
      * Avslutt
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av hode-opplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode          begsr
      *
      * Henter firmaopplysninger
      *
     c     afirl1_key    chain     afirl1r                            90
     c                   eval      a1fna1 = aafnav
     c                   eval      a1fna2 = aafnav
      *
      * Legger inn buker og dato i faste felter
      *
     c                   eval      a1user = l_user
      *
     c                   time                    w_timestamp
     c                   move      w_timestamp   a1date
      *
      * Skriver header
      *
     c                   write     a1hdr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av utskrift
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
     c     vogrl1_key    setll     vogrl1
     c     vogrl1_key    reade     vogrl1                                 90
     c                   dow       *in90 = *off
      *
      * Skriver overgruppe-informasjon
      *
     c                   eval      d1oogr = vgoogr
     c                   eval      d1otxt = vgotxt
     c                   eval      d1okhv = vgokhv
3.11 c                   eval      d1opra = vgopra
      *
     c                   write     d1dtl                                30
     c                   exsr      overflow
      *
      * Leser alle tilhørende hovedgrupper, og skriver detaljlinje for disse
      *
     c                   eval      vhgrl1_hogr = vgoogr
     c     vhgrl1_key    setll     vhgrl1
     c     vhgrl1_key    reade     vhgrl1                                 91
     c                   dow       *in91 = *off
      *
     c                   eval      d2hogr = vghogr
     c                   eval      d2hhgr = vghhgr
     c                   eval      d2htxt = vghtxt
     c                   eval      d2hkhv = vghkhv
     c                   eval      d2hens = vghens
     c                   eval      d2henl = vghenl
     c                   eval      d2henp = vghenp
3.11 c                   eval      d2hpra = vghpra
      *
     c                   write     d2dtl                                30
     c                   exsr      overflow
      *
      * Leser alle tilhørende undergrupper, og skriver detaljlinje for disse
      *
     c                   eval      vugrl1_uogr = vghogr
     c                   eval      vugrl1_uhgr = vghhgr
     c     vugrl1_key    setll     vugrl1
     c     vugrl1_key    reade     vugrl1                                 92
      *
     c                   dow       *in92 = *off
      *
     c                   eval      d3uogr = vguogr
     c                   eval      d3uhgr = vguhgr
     c                   eval      d3uugr = vguugr
     c                   eval      d3utxt = vgutxt
     c                   eval      d3ukhv = vgukhv
     c                   eval      d3uens = vguens
     c                   eval      d3uenl = vguenl
     c                   eval      d3uenp = vguenp
     c                   eval      d3udek = vgudek
     c                   eval      d3upra = vgupra
     c                   eval      d3uavr = vguavr
     c                   eval      d3ulag = vgulag
      *
     c                   write     d3dtl                                30
     c                   exsr      overflow
      *
     c     vugrl1_key    reade     vugrl1                                 92
     c                   enddo
      *
     c     vhgrl1_key    reade     vhgrl1                                 91
     c                   enddo
      *
     c     vogrl1_key    reade     vogrl1                                 90
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for håndtering av overflow
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     overflow      begsr
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for les på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for les på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
      *
      * Key for les på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
      *
      * Key for oppslag på firmaregister
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
