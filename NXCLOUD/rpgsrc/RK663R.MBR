     H/TITLE    Trekker sammen poster på generalnota til en post
     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON
      * Program . . . . : RK663R
      * Beskrivelse . . : Trekker sammen poster på generalnota til en post
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Spesialversjon. :
      * Tilpasset for . : Mestergruppen
      *
      * Referanse  Dato   Endringsbeskrivelse                            Sig
      * --------- ------  ---------------------------------------------- ---
      * V5.40     010208  Nytt program                                   HFL
      * V5.60     090309  Lagt inn brudd på kundenummer                  KOB
      * V5.60     100309  Endret logikk vedr brudd generalnota/faktura   KOB
      * V5.60     110309  Lagt inn kontroll mot kode for sammentrekk     KOB
      *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frkwbl3    if   e           k disk    rename(rkwbpfr:rkwbl3r)
     f                                     prefix('Z')
     frkwbpf    uf a e           k disk
      *
     d s_kund          s                   like(rnkund)
     d s_bdat          s                   like(rnbdat)
     d s_raar          s                   like(rnraar)
     d s_samf          s                   like(rnsamf)
     d s_rfda          s                   like(rwrfda)
     d s_rtda          s                   like(rwrtda)
      *
     d w_firm          s                   like(rnfirm)
     d w_belØ          s                   like(rnbelØ)
     d w_rest          s                   like(rnrest)
     d w_rent          s                   like(rwrent)
     d w_rgrl          s                   like(rwrgrl)
      *
      * Definering av variable for timestamp
      *
     d w_tmst          s               z
     d w_tmst_20       s             20
     d w_tell          s              6  0
     d w_tell_alf      s              6
      *
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      * local data
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     d                uds
     d  pfirm                300    302  0
     d  pnavn                303    332
     d  puser                333    342
     d  pkdat                343    348  0
     d  praar                349    352  0
     d  prper                353    354  0
     d  pavdf                355    358  0
     d  pavdt                359    362  0
     d  pkatf                363    366  0
     d  pkatt                367    370  0
     d  pself                371    374  0
     d  pselt                375    378  0
     d  pknrf                379    384  0
     d  pknrt                385    390  0
     d  kun                  391    450  0 dim(10)
     d  pknr1                391    396  0
     d  pknr2                397    402  0
     d  pknr3                403    408  0
     d  pknr4                409    414  0
     d  pknr5                415    420  0
     d  pknr6                421    426  0
     d  pknr7                427    432  0
     d  pknr8                433    438  0
     d  pknr9                439    444  0
     d  pknr0                445    450  0
     d  pfdat                451    456  0
     d  plope                457    459  0
     d  ppkod                460    460
     d  pgrns                464    465  0
     d  pgbyr                466    466
     d  pmini                467    468  0
     d  pbskr                469    469
     d  pegrn                470    470
     d  pajor                471    471
     d  ptext                472    472  0
     d  ppunr                473    473  0
     d  ppbeh                474    474
     d  palfa                475    475
     d  pdfgg                476    481  0
     d  pgeby                482    484  0
     d  ppros                485    488  2
     d  psgen                489    489
     d  pparam               300    490
      *
     d  l_user               911    920
     d  l_firm               944    946  0
     d  l_fnav               951    980
      *
     c     rkwbl3_key    setll     rkwbl3
     c     rkwbl3_key    reade     rkwbl3                                 60
      *
     c     *in60         doweq     *off
      *
      * Poster skal ikke trekkes sammen
      *
     c                   if        psgen = ' '
     c                   exsr      felt
     c                   write     rkwbpfr
     c                   goto      neste
     c                   endif
      *
      * Poster skal trekkes sammen
      *
     c                   if        psgen = 'J'
      *
      * Poster som ikke er på generalnota tas med som de er
      *
     c                   if        zrnsamf = *zero
      *
     c                   if        s_samf  <> *zero
     c                   exsr      trekk
     c                   write     rkwbpfr
     c                   endif
      *
     c                   exsr      felt
     c                   write     rkwbpfr
     c                   eval      s_samf = 0
     c                   goto      neste
      *
     c                   else
      *
      * Poster som er på generalnota trekkes sammen til 1 post pr år/samf
      *
     c                   if        s_samf  <> *zero
     c                   if        zrnraar <> s_raar or
     c                             zrnkund <> s_kund or
     c                             zrnsamf <> s_samf
     c                   exsr      trekk
     c                   write     rkwbpfr
     c                   endif
     c                   endif
      *
     c                   exsr      felt
     c                   eval      w_belØ = w_belØ + rnbelØ
     c                   eval      w_rest = w_rest + rnrest
     c                   eval      w_rent = w_rent + rwrent
     c                   eval      w_rgrl = w_rgrl + rwrgrl
     c                   eval      s_kund = rnkund
     c                   eval      s_bdat = rnbdat
     c                   eval      s_raar = rnraar
     c                   eval      s_samf = rnsamf
     c                   eval      s_rfda = rwrfda
     c                   eval      s_rtda = rwrtda
     c                   endif
      *
     c                   endif
      *
     c     neste         tag
     c     rkwbl3_key    reade     rkwbl3                                 60
     c                   enddo
      *
     c     slutt         tag
      *
     c                   if        w_belØ  <> *zero
     c                   exsr      trekk
     c                   write     rkwbpfr
     c                   endif
      *
     c                   seton                                        lr
      *
     c/eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sammentrekking til 1 post (generalnota)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     trekk         begsr
      *
     c                   eval      rnbelØ = w_belØ
     c                   eval      rnrest = w_rest
     c                   eval      rwrent = w_rent
     c                   eval      rwrgrl = w_rgrl
     c                   eval      rnkund = s_kund
     c                   eval      rnbdat = s_bdat
     c                   eval      rnbiln = s_samf
     c                   eval      rwrfda = s_rfda
     c                   eval      rwrtda = s_rtda
     c                   eval      rntext = 'GENERALNOTA'
      *
      * Teller/Timestamp
      *
     c                   eval      w_tell = w_tell + 1
     c                   move      w_tell        w_tell_alf
     c                   time                    rntist
     c     *iso0         move      rntist        w_tmst_20
     c                   move      w_tell_alf    w_tmst_20
     c     *iso0         move      w_tmst_20     rntist
     c                   eval      w_belØ = *zero
     c                   eval      w_rest = *zero
     c                   eval      w_rent = *zero
     c                   eval      w_rgrl = *zero
     c                   endsr
      *
     c/eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for flytting av felt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     c     felt          begsr
      *
     c                   eval      rnfirm = zrnfirm
     c                   eval      rnkund = zrnkund
     c                   eval      rnbdat = zrnbdat
     c                   eval      rnbiln = zrnbiln
     c                   eval      rnfdat = zrnfdat
     c                   eval      rnbelØ = zrnbelØ
     c                   eval      rnrest = zrnrest
     c                   eval      rntext = zrntext
     c                   eval      rnraar = zrnraar
     c                   eval      rnrper = zrnrper
     c                   eval      rnbilk = zrnbilk
     c                   eval      rnvalk = zrnvalk
     c                   eval      rnvalb = zrnvalb
     c                   eval      rnvalr = zrnvalr
     c                   eval      rnjour = zrnjour
     c                   eval      rnsamf = zrnsamf
     c                   eval      rnstat = zrnstat
     c                   eval      rnrefn = zrnrefn
     c                   eval      rnvirk = zrnvirk
     c                   eval      rnavde = zrnavde
     c                   eval      rnkont = zrnkont
     c                   eval      rnspes = zrnspes
     c                   eval      rnslgr = zrnslgr
     c                   eval      rnrekl = zrnrekl
     c                   eval      rnrenk = zrnrenk
     c                   eval      rnrdat = zrnrdat
     c                   eval      rnantp = zrnantp
     c                   eval      rnpdat = zrnpdat
     c                   eval      rnauto = zrnauto
     c                   eval      rnadat = zrnadat
     c                   eval      rnfack = zrnfack
     c                   eval      rnfada = zrnfada
     c                   eval      rntist = zrntist
     c                   eval      rwkalf = zrwkalf
     c                   eval      rwrent = zrwrent
     c                   eval      rwrgrl = zrwrgrl
     c                   eval      rwdagr = zrwdagr
     c                   eval      rwrfda = zrwrfda
     c                   eval      rwrtda = zrwrtda
     c                   endsr
      *
     c/eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c     rkwbl3_key    klist
     c                   kfld                    w_firm
      *
     c                   move      l_firm        w_firm
      *
     c                   endsr
