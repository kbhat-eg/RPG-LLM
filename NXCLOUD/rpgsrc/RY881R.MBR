     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : RY881R
      * Beskrivelse . . : Påslag, oppdaterer leverandør på vare-påslag
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       150806 HKO  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjpfalu    uf   e           k disk    rename(jpfapfr:jpfalur)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fjvprl1    if   e           k disk    rename(jvprpfr:jvprl1r)
      *
      * Definering av LDA
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d jvprl1_vare     s                   like(jxvare)
     d jvprl1_ldor     s                   like(jxldor)
      *
      * Definering av variable
      *
     d w_firm          s                   like(jcfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser og oppdaterer påslagstabellen
      *
     c     jpfalu_key    setll     jpfalu
     c     jpfalu_key    reade     jpfalur
     c                   dow       not %eof
      *
     c                   if        jcvare <> *blank and
     c                             jcldor = 0
      *
     c                   eval      vvarl1_vare = jcvare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found and
     c                             vvnlev <> 0
     c                   eval      jvprl1_vare = vvvare
     c                   eval      jvprl1_ldor = vvnlev
     c     jvprl1_key    chain     jvprl1
     c                   if        %found
     c                   eval      jcldor = vvnlev
     c                   update    jpfalur
     c                   endif
     c                   endif
      *
     c                   endif
      *
     c     jpfalu_key    reade     jpfalur
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for les/oppdatering av rabattmatrise
      *
     c     jpfalu_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på vare-pris
      *
     c     jvprl1_key    klist
     c                   kfld                    jvprl1_vare
     c                   kfld                    jvprl1_ldor
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
