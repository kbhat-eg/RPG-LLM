     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKUN
      * Program . . . . : RF820R
      * Beskrivelse . . : Velger ut kort som skal benyttes for gen. av
      *                   grunnlag.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       080910 bsa  Nytt program
6.10  *       100311 bsa  ID må ligge ved for filnavn
6.11  *       090812 bsa  Legger inn parametre for styring av kort og lager
6.20  *       290914 bsa  Sjekker om overføring allerede pågår.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frukslr    if   e           k disk    rename(rukspfr:rukslrr)
     frukslu    uf   e           k disk    rename(rukspfr:rukslur)
     fxmlgrlpf  o    e             disk
      *
      *
      * Definering av parametre
      *
     d p_dato          s               d   datfmt(*iso)
     d p_dgnr          s              1  0
6.11 d p_kort          s              1
6.11 d p_lagr          s              2
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      *
     d rukslr_skty     s                   like(ruskty)
     d rukslr_slag     s                   like(ruslag)
     d rukslu_skty     s                   like(ruskty)
     d rukslu_slag     s                   like(ruslag)
      *
      * Definering av variable
      *
     d w_firm          s                   like(rusfir)
     d w_30            s              3  0
     d w_fdat          s               d   datfmt(*iso)
     d w_tdat          s               d   datfmt(*iso)
     d w_time          s               t
     d w_xrec          s            400
6.11 d w_kort          s              1  0
6.11 d w_lagr          s              2  0
6.20 d w_stat          s              1
6.20 d w_navn          s             10
      *
     d s_skty          s                   like(ruskty)
     d s_slag          s                   like(ruslag)
      *
     d b_dann          s              1    inz(*off)
     d d_bmid          s             15
      *
     d                 ds
     d d_list                       128
     d  d_ktyp                        2  0 overlay(d_list:1)
     d  d_fdat                         d   overlay(d_list:3)  datfmt(*iso)
     d  d_tdat                         d   overlay(d_list:13) datfmt(*iso)
     d  d_tidl                        1  0 overlay(d_list:23)
     d  d_lagr                        2  0 overlay(d_list:24)
     d  d_ftxt                       30    overlay(d_list:26)
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      *
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Finne dag
      *
     c                   time                    p_dato
     c                   eval      p_dgnr = 0
     c                   call      'MP701R  '
     c                   parm                    p_dato
     c                   parm                    p_dgnr
      *
      *
     c     rukslr_key    setll     rukslr
      *
     c                   read      rukslr
      *
     c                   dow       not %eof(rukslr)
     c                   exsr      behandling
     c                   read      rukslr
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Tester om leverandør /parameter skal danne bestillingsforlag
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
     c                   eval      b_dann = *off
      *
     c                   if        rushld = 'A'
      *
     c                   select
      *
     c                   when      rusfrk = 'MD'
     c     p_dato        subdur    rusdat        w_30:*M
     c                   if        w_30 >= 1
     c                   eval      b_dann = *on
     c                   endif
      *
     c                   when      rusfrk = 'DG'
     c     p_dato        subdur    rusdat        w_30:*D
     c                   if        w_30 >= 1
     c                   eval      b_dann = *on
     c                   endif
      *
     c                   when      rusfrk = '14'
     c                   exsr      beh_14d
      *
     c                   when      rusfrk = 'UK'
     c                   exsr      beh_uke
     c                   endsl
      *
     c                   if        b_dann = *on
6.11  * Sjekk mot valgt korttype (hvis korttype er angitt)
6.11 c                   if        w_kort <> 0 and
6.11 c                             w_kort <> ruskty
6.11 c                   goto      end_kort
6.11 c                   endif
6.11  * Sjekk mot valgt lager (hvis lager er angitt)
6.11 c                   if        w_lagr <> 0 and
6.11 c                             w_lagr <> ruslag
6.11 c                   goto      end_kort
6.11 c                   endif
6.11 c
     c***                eval      d_fdat = w_fdat
     c                   eval      d_fdat = rusdat
     c                   eval      d_tdat = w_tdat
     c                   eval      d_tidl = 0
     c                   eval      d_lagr = ruslag
     c                   eval      d_ftxt = rusbes
      * Finn ut hvilket program som skal kjøres
     c                   if        ruskty = 3
6.20  * Sjekk om overføring allerede skjer, eller at forrige avsluttet i error
6.20 c                   eval      w_stat = *blanks
6.20 c                   eval      w_navn = 'LOGDNBKORT'
6.20 c                   call      'AF712R'
6.20 c                   parm                    w_navn
6.20 c                   parm                    w_stat
6.20  *
6.20 c                   if        w_stat = *blanks
      * 3=Maxbo kort  (DNBNOR)
     c                   eval      d_ktyp = 3
6.10 c                   eval      d_bmid = ruskid
     c                   call      'RF811C_SBM'
     c                   parm                    d_list
6.10 c                   parm                    d_bmid
6.20 c                   endif
      * 4=GE Moneybank (XL kjeden)
     c                   elseif    ruskty = 4
6.20  * Sjekk om overføring allerede skjer, eller at forrige avsluttet i error
6.20 c                   eval      w_stat = *blanks
6.20 c                   eval      w_navn = 'LOGGECKORT'
6.20 c                   call      'AF712R'
6.20 c                   parm                    w_navn
6.20 c                   parm                    w_stat
6.20  *
6.20 c                   if        w_stat = *blanks
6.10 c                   eval      d_bmid = ruskid
     c                   eval      d_ktyp = 4
     c                   call      'RF812C_SBM'
     c                   parm                    d_list
6.10 c                   parm                    d_bmid
6.20 c                   endif
      * 5=BM Kort (DNBNOR)
     c                   elseif    ruskty = 5
6.20  * Sjekk om overføring allerede skjer, eller at forrige avsluttet i error
6.20 c                   eval      w_stat = *blanks
6.20 c                   eval      w_navn = 'LOGDNBKORT'
6.20 c                   call      'AF712R'
6.20 c                   parm                    w_navn
6.20 c                   parm                    w_stat
6.20  *
6.20 c                   if        w_stat = *blanks
      * Logg hendelse
     c                   eval      w_xrec = *blanks
     c                   eval      w_xrec = 'Startet opp prog ' +
     c                                      'for statistik RF814'
     c                   eval      xmlgrl = w_xrec
     c                   write     xmlgrlpfr
      *
     c                   eval      d_ktyp = 5
6.10 c                   eval      d_bmid = ruskid
     c                   call      'RF814C_SBM'
     c                   parm                    d_list
     c                   parm                    d_bmid
6.20 c                   endif
      * 6=NBBL Fordelskort
     c                   elseif    ruskty = 6
6.20  * Sjekk om overføring allerede skjer, eller at forrige avsluttet i error
6.20 c                   eval      w_stat = *blanks
6.20 c                   eval      w_navn = 'KORTITUM'
6.20 c                   call      'AF712R'
6.20 c                   parm                    w_navn
6.20 c                   parm                    w_stat
6.20  *
6.20 c                   if        w_stat = *blanks
     c                   eval      d_ktyp = 6
6.10 c                   eval      d_bmid = ruskid
     c                   eval      w_xrec = 'Startet opp prog ' +
     c                                      'for statistik RF813'
     c                   eval      xmlgrl = w_xrec
     c                   write     xmlgrlpfr
     c                   eval      w_xrec = 'Param: ' + d_list
     c                   eval      xmlgrl = w_xrec
     c                   write     xmlgrlpfr
     c                   call      'RF813C_SBM'
     c                   parm                    d_list
6.10 c                   parm                    d_bmid
6.20 c                   endif
     c                   endif
      *
      * Oppdater med at utlegg av transaksjoner er kjørt
      *
     c                   eval      rukslu_skty = ruskty
     c                   eval      rukslu_slag = ruslag
     c     rukslu_key    chain     rukslu
     c                   if        %found
     c                   eval      rusdat = w_tdat
     c                   eval      rustim = w_time
     c                   eval      rususr = l_user
     c                   update    rukslur
     c                   endif
      *
     c                   endif
      *
     c                   endif
      *
6.11 c     end_kort      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker de 14-daglige
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     beh_14d       begsr
      *
     c     p_dato        subdur    rusdat        w_30:*D
     c                   if        w_30 >  7
      * søndag
     c                   if        p_dgnr = 1  and
     c                             rusdg7 = '1'
     c                   eval      b_dann = *on
     c                   goto      end_14d
     c                   endif
      * mandag
     c                   if        p_dgnr = 2  and
     c                             rusdg1 = '1'
     c                   eval      b_dann = *on
     c                   goto      end_14d
     c                   endif
      * tirsdag
     c                   if        p_dgnr = 3  and
     c                             rusdg2 = '1'
     c                   eval      b_dann = *on
     c                   goto      end_14d
     c                   endif
      * onsdag
     c                   if        p_dgnr = 4  and
     c                             rusdg3 = '1'
     c                   eval      b_dann = *on
     c                   goto      end_14d
     c                   endif
      * torsdag
     c                   if        p_dgnr = 5  and
     c                             rusdg4 ='1'
     c                   eval      b_dann = *on
     c                   goto      end_14d
     c                   endif
      * fredag
     c                   if        p_dgnr = 6  and
     c                             rusdg5 = '1'
     c                   eval      b_dann = *on
     c                   goto      end_14d
     c                   endif
      * lørdag
     c                   if        p_dgnr = 7  and
     c                             rusdg6 = '1'
     c                   eval      b_dann = *on
     c                   goto      end_14d
     c                   endif
      *
     c                   endif
      *
     c     end_14d       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker de ukentlige
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     beh_uke       begsr
      *
     c     p_dato        subdur    rusdat        w_30:*D
     c                   if        w_30 >= 1
      * søndag
     c                   if        p_dgnr = 1  and
     c                             rusdg7 = '1'
     c                   eval      b_dann = *on
     c                   goto      end_uke
     c                   endif
      * mandag
     c                   if        p_dgnr = 2  and
     c                             rusdg1 = '1'
     c                   eval      b_dann = *on
     c                   goto      end_uke
     c                   endif
      * tirsdag
     c                   if        p_dgnr = 3  and
     c                             rusdg2 = '1'
     c                   eval      b_dann = *on
     c                   goto      end_uke
     c                   endif
      * onsdag
     c                   if        p_dgnr = 4  and
     c                             rusdg3 = '1'
     c                   eval      b_dann = *on
     c                   goto      end_uke
     c                   endif
      * torsdag
     c                   if        p_dgnr = 5  and
     c                             rusdg4 = '1'
     c                   eval      b_dann = *on
     c                   goto      end_uke
     c                   endif
      * fredag
     c                   if        p_dgnr = 6  and
     c                             rusdg5 = '1'
     c                   eval      b_dann = *on
     c                   goto      end_uke
     c                   endif
      * lørdag
     c                   if        p_dgnr = 7  and
     c                             rusdg6 = '1'
     c                   eval      b_dann = *on
     c                   goto      end_uke
     c                   endif
      *
     c                   endif
      *
     c     end_uke       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
6.11  *
6.11  * Parametre
6.11  *
6.11 c     *entry        plist
6.11 c                   parm                    p_kort
6.11 c                   parm                    p_lagr
      *
      * Key for les på parameter pr. lev.
      *
     c     rukslr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rukslr_skty
     c                   kfld                    rukslr_slag
      *
      * Key for oppdatering av parameter pr. lev.
      *
     c     rukslu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rukslu_skty
     c                   kfld                    rukslu_slag
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
     c                   time                    w_tdat
     c                   time                    w_time
      * Logg hendelse
     c                   eval      w_xrec = *blanks
     c                   eval      w_xrec = 'Startet opp prog ' +
     c                                      'for RF820R-SBM'
     c                   eval      xmlgrl = w_xrec
     c                   write     xmlgrlpfr
      *
6.11 c                   eval      w_kort = %int(p_kort)
6.11 c                   eval      w_lagr = %int(p_lagr)
6.11  * Hvis korttype er 0, må også lager settes til 0
6.11 c                   if        w_kort = 0
6.11 c                   eval      w_lagr = 0
6.11 c                   endif
      *
     c                   endsr
