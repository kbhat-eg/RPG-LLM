     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FD506R
      * Beskrivelse . . : Ordre-linje slettet, vis detaljer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       010508 KOB  Nytt program
5.70  * PRGR  251108 BOF  Utvidet med prisgruppe
6.11  * 6.10  290609 bof  Utivdet eannr til 14 og brukt std. oppslag
6.nu  * R6.30 270514 bsa  Programmet gjennomgått mtp nummerutvidelser.
8.01  *PRG2   090622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     fsohel1    if   e           k disk    rename(sohepfr:sohel1r)
     ffdesl1    if   e           k disk    rename(fdespfr:fdesl1r)
     ffusrl1    if   e           k disk    rename(fusrpfr:fusrl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
      *
     ffd506d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_fnav                951    980
      *
      * Definering av parametre
      *
     d p_firm          s                   like(fsfirm)
     d p_aarr          s                   like(soaarr)
     d p_kund          s                   like(fskund)
6.nu d p_numm          s                   like(fsnumm)
     d p_suff          s                   like(fssuff)
     d p_line          s                   like(fsline)
      *
      * Definering av variabler i nøkler
      *
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d sohel1_aarr     s                   like(soaarr)
     d sohel1_binr     s                   like(sobinr)
     d sohel1_numm     s                   like(sonumm)
     d sohel1_suff     s                   like(sosuff)
     d fdesl1_numm     s                   like(fsnumm)
     d fdesl1_suff     s                   like(fssuff)
     d fdesl1_line     s                   like(fsline)
     d fusrl1_user     s                   like(fbuser)
     d rlevl1_levr     s                   like(rllevr)
     d vvarl1_vare     s                   like(vvvare)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
      *
      * Definering av variable
      *
     d w_firm          s                   like(fsfirm)
     d w_raba          s                   like(fsrab1)
     d w_nett          s                   like(fssums)
6.11 d w_east          s              1
6.11 d w_eann          s                   like(fseann)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle bildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer skjermbildet
      *
     c                   eval      fdesl1_numm = p_numm
     c                   eval      fdesl1_suff = p_suff
     c                   eval      fdesl1_line = p_line
     c     fdesl1_key    chain     fdesl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      vvarl1_vare = fsvare
     c     vvarl1_key    chain     vvarl1
6.11  *
6.11  * Kaller program for henting av ean-nr
6.11  *
6.11 c                   call      'VE711R'
6.11 c                   parm                    w_firm
6.11 c                   parm                    fsvare
6.11 c                   parm                    fsenh1
6.11 c                   parm                    w_eann
6.11 c                   parm                    w_east
      *
     c                   eval      b1numm = fsnumm
     c                   eval      b1otyp = fsotyp
     c                   eval      b1vare = fsvare
     c                   eval      b1tek1 = fstek1
     c                   eval      b1tek2 = fstek2
     c                   eval      b1ogrp = fsogrp
     c                   eval      b1hgrp = fshgrp
     c                   eval      b1ugrp = fsugrp
     c                   eval      b1utxt = *blank
     c                   eval      b1ldor = fsldor
     c                   eval      b1lnav = *blank
     c                   eval      b1lvar = fslvar
     c                   eval      b1nobb = fsnobb
     c                   eval      b1nmod = vvnmod
6.11 c                   eval      b1eann = w_eann
5.70 c                   eval      b1prgr = fsprgr
     c                   eval      b1anta = fsanta
     c                   eval      b1enh1 = fsenh1
     c                   eval      b1kpri = fskpri
     c                   eval      b1lety = fslety
     c                   eval      b1kopr = fskopr
     c                   eval      b1sapr = fssapr
     c                   eval      b1rab1 = fsrab1
     c                   eval      b1rab2 = fsrab2
     c                   eval      b1brr1 = fsbrr1
     c                   eval      b1brr2 = fsbrr2
     c                   eval      b1dekn = fspasl
     c                   eval      b1rkr1 = 0
     c                   eval      b1rkr2 = 0
     c                   eval      b1bkr1 = 0
     c                   eval      b1bkr2 = 0
      *
      * Linjerabatt - enhet
      *
     c                   eval      w_nett = fssapr
      *
     c                   eval(h)   b1rkr1 = fssapr * fsrab1 / 100
     c                   eval(h)   w_nett = w_nett - b1rkr1
     c                   eval(h)   b1rkr2 = w_nett * fsrab2 / 100
     c                   eval(h)   w_nett = w_nett - b1rkr2
     c                   eval(h)   b1bkr1 = w_nett * fsbrr1 / 100
     c                   eval(h)   w_nett = w_nett - b1bkr1
     c                   eval(h)   b1bkr2 = w_nett * fsbrr2 / 100
      *
     c                   eval      b1nett = fssapr - b1rkr1 - b1rkr2 -
     c                                      b1bkr1 - b1bkr2
      *
      * Legger inn priser totalt
      *
     c                   eval      b1sumk = fssumk
     c                   eval      b1sums = fssums
     c                   eval      b1srk1 = 0
     c                   eval      b1srk2 = 0
     c                   eval      b1sbk1 = 0
     c                   eval      b1sbk2 = 0
      *
      * Linjerabatt - total
      *
     c                   eval      w_nett = fssums
      *
     c                   eval(h)   b1srk1 = fssums * fsrab1 / 100
     c                   eval(h)   w_nett = w_nett - b1srk1
     c                   eval(h)   b1srk2 = w_nett * fsrab2 / 100
     c                   eval(h)   w_nett = w_nett - b1srk2
     c                   eval(h)   b1sbk1 = w_nett * fsbrr1 / 100
     c                   eval(h)   w_nett = w_nett - b1sbk1
     c                   eval(h)   b1sbk2 = w_nett * fsbrr2 / 100
      *
     c                   eval      b1sumn = fssums - b1srk1 - b1srk2 -
     c                                      b1sbk1 - b1sbk2
      *
      * Kostpriser og DG skal kun vises dersom dette er tillatt for bruker
      *
     c                   if        fbko01 = 0
     c                   eval      b1dekn = 0
     c                   eval      b1kopr = 0
     c                   eval      b1sumk = 0
     c                   endif
      *
      * Hent navn på leverandør
      *
     c                   eval      rlevl1_levr = fsldor
     c     rlevl1_key    chain     rlevl1
     c                   if        %found
     c                   eval      b1lnav = rlnavn
     c                   endif
      *
      * Hent beskrivelse av varegruppe
      *
     c                   eval      vugrl1_uogr = fsogrp
     c                   eval      vugrl1_uhgr = fshgrp
     c                   eval      vugrl1_uugr = fsugrp
     c     vugrl1_key    chain     vugrl1
     c                   if        %found
     c                   eval      b1utxt = vgutxt
     c                   endif
      *
      * Slettet av data
      *
     c                   eval      b1slus = fsslus
     c                   eval      b1spgm = fsspgm
     c                   eval      b1slws = fsslws
      *
     c                   if        fsslda <> *loval
     c     *dmy          move      fsslda        b1slda
     c                   endif
      *
      * Behandler bildet
      *
     c     b1taga        tag
     c                   exfmt     b1bld
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   endsl
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
6.nu c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_line
      *
      * Key for oppslag på ordre-hode
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      * Key for oppslag på fakturanummer
      *
     c     sohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sohel1_aarr
     c                   kfld                    sohel1_binr
     c                   kfld                    sohel1_numm
      *
      * Key for oppslag på ordre-linje
      *
     c     fdesl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fdesl1_numm
     c                   kfld                    fdesl1_suff
     c                   kfld                    fdesl1_line
      *
      * Key for oppslag på bruker
      *
     c     fusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fusrl1_user
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
      * Hente inn brukerregister
      *
     c                   eval      fusrl1_user = l_user
     c     fusrl1_key    chain     fusrl1r
      *
      *
     c                   endsr
