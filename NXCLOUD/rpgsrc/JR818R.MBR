     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JR754R
      * Beskrivelse . . : Leser fil på recordformat VISMA CS-IMPORT
      *                   og oppretter/oppdaterer priser på eksisterende
      *                   varer i LVR.
      *
      *                   Prisoppdatering skjer på bakgrunn av match på
      *                   GTIN-nr
      *
      *                   Hvis varen ikke finnes, så opprettes den som
      *                   9000-vare.
      *
      *
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       160412 bof  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjrvapf    if   e           k disk
      *
     fjrapl1    if   e           k disk    rename(jrappfr:jrapl1r)
     fjvprl2    if   e           k disk    rename(jvprpfr:jvprl2r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fjvarlu    uf a e           k disk    rename(jvarpfr:jvarlur)
     fjvprlu    uf a e           k disk    rename(jvprpfr:jvprlur)
     fjstslu    uf   e           k disk    rename(jstspfr:jstslur)
     fjvpklu    uf a e           k disk    rename(jvpkpfr:jvpklur)
6.30 fjvpkl3    if   e           k disk    rename(jvpkpfr:jvpkl3r)
     fjpfal6    uf a e           k disk    rename(jpfapfr:jpfal6r)
     fjpfalu    uf a e           k disk    rename(jpfapfr:jpfalur)
      *
      * Definering av parametre
      *
     d p_list          s             32
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
      *
      * Definering av parameterliste -inn
      *
     d                 ds
     d d_list                        32
     d  d_firm                        3  0 overlay(d_list)
     d  d_rapp                        2  0 overlay(d_list:4)
     d  d_dato                         d   overlay(d_list:6) datfmt(*iso)
      *
      * Definering av recordbeskrivelse - Jernia
      *
     d                 ds
     d d_inpu                       280
     d  d_ekod                        1    overlay(d_inpu)
     d  d_vare                       15    overlay(d_inpu:2)
     d  d_tek1                       30    overlay(d_inpu:17)
     d  d_enhe                        3    overlay(d_inpu:47)
     d  d_lvar                       20    overlay(d_inpu:50)
     d  d_ldor                        8    overlay(d_inpu:70)
     d  d_vgrp                        7    overlay(d_inpu:78)
     d  d_vgr1                        2  0 overlay(d_vgrp:1)
     d  d_vgr2                        2  0 overlay(d_vgrp:3)
     d  d_vgr3                        3  0 overlay(d_vgrp:5)
     d  d_gtin                       14    overlay(d_inpu:91)
     d  d_gtinn                      14  0 overlay(d_gtin:1)
     d  d_tek2                       30    overlay(d_inpu:105)
     d  d_rcid                        4    overlay(d_inpu:135)
     d  d_utgk                        1    overlay(d_inpu:139)
     d  d_inpr                       11    overlay(d_inpu:160)
     d  d_inpr_kr                     8  0 overlay(d_inpr:1)
     d  d_inpr_ore                    2  2 overlay(d_inpr:10)
     d* d_antp                       10  0 overlay(d_inpu:180)
     d* d_akod                        1    overlay(d_inpu:190)
     d  d_sort                        8    overlay(d_inpu:191)
     d  d_evar                       15    overlay(d_inpu:195)
     d  d_raba                        5  2 overlay(d_inpu:233)
     d  d_fdat                        6  0 overlay(d_inpu:238)
     d  d_tdat                        6  0 overlay(d_inpu:244)
     d  d_crdo                       10    overlay(d_inpu:250)
     d  d_sapr                       12    overlay(d_inpu:260)
     d  d_sapr_kr                     9  0 overlay(d_sapr:1)
     d  d_sapr_ore                    2  2 overlay(d_sapr:11)
      *
      * Definering av variable i nøkkler
      *
     d jrapl1_rapp     s                   like(jrrapp)
     d jvprl2_ldor     s                   like(jxldor)
     d jvprl2_lvar     s                   like(jxlvar)
     d jvarl1_vare     s                   like(jvvare)
     d jlevl1_ldor     s                   like(jlldor)
     d jvprlu_vare     s                   like(jxvare)
     d jvprlu_ldor     s                   like(jxldor)
     d jvarlu_vare     s                   like(jvvare)
     d jvpklu_vare     s                   like(jwvare)
6.30 d jvpkl3_gtin     s                   like(jwgtin)
     d jpfal6_vare     s                   like(jcvare)
      *
      * Definering av variable
      *
     d b_inlr          s              3    inz(*off)
     d b_4100          s              3    inz(*off)
     d b_4300          s              3    inz(*off)
      *
     d w_firm          s              3  0
      *
     d w_pris          s                   like(jxpris)
     d w_sapr          s                   like(jxpris)
     d w_inpr          s                   like(jxpris)
     d len             s              3  0
     d w_leng          s              3  0
     d w_posi          s              3  0
     d st              s              3  0
     d w_14x           s             14
     d w_7x            s              7
     d w_udat          s               d   datfmt(*iso)
     d w_mvap          s              6  2
     d w_mvat          s              5  4
     d w_mvaf          s             10  9
      *
      * Definering av konstanter
      *
     d Lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d Up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
      *
     d c_null          s             15    inz('000000000000000')
     d digits          c                   ' 0123456789'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter rapportør-informasjon
      *
     c                   eval      jrapl1_rapp = d_rapp
     c     jrapl1_key    chain     jrapl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
      * Henter leverandørinformasjon
      *
     c                   eval      jlevl1_ldor = jrldor
     c     jlevl1_key    chain     jlevl1
      *
     c                   eval      jlnavn = %trim(jlnavn)
     c     Lo:Up         xlate     jlnavn        jlnavn
      *
      * Behandling
      *
     c                   exsr      behandling
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av varer/priser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
     c                   read      jrvapf
     c                   dow       not %eof(jrvapf)
      *
     c                   eval      b_4100 = *off
     c                   eval      b_4300 = *off
      *
     c                   eval      d_inpu = *blank
     c                   select
     c                   when      %subst(jrvrad:1:4) = '4100'
     c                   exsr      utled_4100
     c                   when      %subst(jrvrad:1:4) = '4300'
     c                   exsr      utled_4300
     c                   endsl
      *
     c                   if        d_rcid <> '4100' and
     c                             d_rcid <> '4300'
     c                   goto      les_neste
     c                   endif
      *
     c                   if        B_4100 = *on
      * finner vare ut fra gtin-nr
6.30 c                   eval      jvpkl3_gtin = d_gtinn
6.30 c     jvpkl3_key    chain     jvpkl3
6.30 c                   if        not %found(jvpkl3)
     c                   exsr      dann_vare
     c                   else
6.30 c                   eval      jvarl1_vare = jwvare
     c     jvarl1_key    chain     jvarl1
     c                   if        %found(jvarl1) and
     c                             jvldor = jrldor and
     c                             jvvare >= '90000000'
      * kun endre hvis leverandør er vareier og 9000-serie
     c                   exsr      endr_vare
     c                   endif
     c                   endif
     c                   endif
      *
     c                   if        B_4300 = *on
      * finner vare ut fra gtin-nr
6.30 c                   eval      jvpkl3_gtin = d_gtinn
6.30 c     jvpkl3_key    chain     jvpkl3
6.30 c                   if        %found(jvpkl3)
      *
      * leser vare
6.30 c                   eval      jvarl1_vare = jwvare
     c     jvarl1_key    chain     jvarl1
     c                   if        %found(jvarl1)
      * sjekker om det ligger pris
     c                   eval      jvprl2_ldor = jrldor
6.30 c                   eval      jvprl2_lvar = jwvare
     c     jvprl2_key    chain     jvprl2
     c                   if        %found
     c                   exsr      endr_pris
     c                   else
     c                   exsr      oppr_pris
     c                   endif
      *
     c                   endif
      *
     c                   endif
      *
     c                   endif
      *
      * Oppdaterer søketekst
      *
     c                   if        jvvare <> *blank
     c                   eval      b_inlr = *on
     c                   call      'JV790R'
     c                   parm                    w_firm
     c                   parm                    jxvare
     c                   parm                    b_inlr
     c                   endif
      *
     c     les_neste     tag
      *
     c                   read      jrvapf
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting av pris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppr_pris     begsr
      *
      * Sletter eventuelt eksisterende priser for leverandør på varen
      *
     c                   eval      jvprlu_vare = jvvare
     c                   eval      jvprlu_ldor = jrldor
     c     jvprlu_key    setll     jvprlu
     c     jvprlu_key    reade     jvprlur
     c                   dow       not %eof
     c                   delete    jvprlur
     c     jvprlu_key    reade     jvprlur
     c                   enddo
      *
      * Oppretter pris på vare for leverandør
      *
     c                   clear                   jvprlur
      *
     c                   eval      jxvare = jvvare
     c                   eval      jxldor = jrldor
     c                   eval      jxlvar = d_vare
     c                   eval      jxpris = d_inpr_kr + d_inpr_ore
     c                   eval      w_inpr = jxpris
     c                   eval      w_sapr = (d_sapr_kr + d_sapr_ore) * w_mvaf
     c                   eval      jxgdat = d_dato
     c                   eval      jxtdat = *loval
     c                   eval      jxvalu = *blank
     c                   eval      jxeann = d_gtinn
     c                   eval      jxcrdo = 0
     c                   eval      jxrsts = 1
     c                   eval      jxeusr = l_user
      *
     c                   time                    jxodat
     c                   time                    jxedat
     c                   time                    jxetim
      *
     c                   if        d_crdo = 'CD'
     c                   eval      jxcrdo = 1
     c                   endif
      *
     c                   write     jvprlur
      *
     c                   exsr      dann_pasl
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for endring av pris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     endr_pris     begsr
      *
     c                   eval      w_pris = d_inpr_kr + d_inpr_ore
      *
     c                   if        w_pris = jxpris
     c                   leavesr
     c                   endif
      *
     c                   eval      jvprlu_vare = jxvare
     c                   eval      jvprlu_ldor = jrldor
     c     jvprlu_key    chain     jvprlur
      *
     c                   eval      jxlvar = d_vare
     c                   eval      jxpris = w_pris
     c                   eval      w_inpr = jxpris
     c                   eval      w_sapr = (d_sapr_kr + d_sapr_ore) * w_mvaf
     c                   eval      jxgdat = d_dato
     c                   eval      jxtdat = *loval
     c                   eval      jxeann = d_gtinn
     c                   eval      jxcrdo = 0
     c                   eval      jxeusr = l_user
      *
     c                   time                    jxedat
     c                   time                    jxetim
      *
     c                   update    jvprlur
      *
     c                   exsr      dann_pasl
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å danne vare (9000-serien)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dann_vare     begsr
      *
     c                   clear                   jvarlur
     c                   exsr      ny_vare
      *
     c                   eval      jvogrp = d_vgr1
     c                   eval      jvhgrp = d_vgr2
     c                   eval      jvugrp = d_vgr3
      *
     c                   eval      jvtek1 = d_tek1
     c                   eval      jvtek2 = d_tek2
     c                   eval      jvldor = jrldor
     c                   eval      jvlvar = d_lvar
     c                   eval      jvpvar = *blank
     c                   eval      jvpenh = d_enhe
     c                   eval      jvsort = d_sort
     c                   eval      jveusr = l_user
     c                   if        d_utgk = '2'
     c                   time                    jvudat
     c                   endif
     c                   time                    jvetim
     c                   time                    jvedat
      *
     c                   write     jvarlur
      *
     c                   exsr      dann_pakning
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å endre vare (9000-serien)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     endr_vare     begsr
      *
6.30 c                   eval      jvarlu_vare = jwvare
     c     jvarlu_key    chain     jvarlu
     c                   if        %found(jvarlu)
     c                   eval      jvogrp = d_vgr1
     c                   eval      jvhgrp = d_vgr2
     c                   eval      jvugrp = d_vgr3
      *
     c                   eval      jvtek1 = d_tek1
     c                   eval      jvtek2 = d_tek2
     c                   eval      jvldor = jrldor
     c                   eval      jvlvar = d_lvar
     c                   eval      jvpvar = *blank
     c                   eval      jvpenh = d_enhe
     c                   eval      jveusr = l_user
     c                   if        d_utgk = '2'
     c                   time                    jvudat
     c                   endif
     c                   time                    jvetim
     c                   time                    jvedat
      *
     c                   update    jvarlur
      *
     c                   exsr      dann_pakning
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av ny vare - henter 9000-nr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ny_vare       begsr
      *
     c                   eval      jvstat = 2
      *
      * Henter neste ledige varenummer, og oppdaterer statusregisteret
      *
     c     jstslu_key    chain     jstslu
     c                   eval      jaavsi = jaavsi + 1
     c                   update    jstslur
      *
     c                   movel     jaavsi        jvvare
      *
      * Opprettelsesdato
      *
     c                   time                    jvodat
     c                   time                    jvadat
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting av vare-pakning
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dann_pakning  begsr
      *
     c                   exsr      rydd_pakning
      *
     c                   clear                   jvpklur
      *
     c                   eval      jwvare = jvvare
6.30 c                   eval      jwldor = jrldor
     c                   eval      jwpakn = '000'
     c                   eval      jwenhe = d_enhe
     c                   eval      jwofak = 1
     c                   eval      jwrsts = 1
     c                   eval      jweusr = l_user
      *
     c                   time                    jwodat
     c                   time                    jwedat
     c                   time                    jwetim
      *
     c                   write     jvpklur
      *
     c                   clear                   jvpklur
      *
     c                   eval      jwvare = jvvare
6.30 c                   eval      jwldor = jrldor
     c                   eval      jwpakn = '001'
     c                   eval      jwenhe = d_enhe
     c                   eval      jwofak = 1
6.30 c                   eval      jwgtin = d_gtinn
6.30 c                   eval      jwbest = 1
     c                   eval      jwrsts = 1
     c                   eval      jweusr = l_user
      *
     c                   time                    jwodat
     c                   time                    jwedat
     c                   time                    jwetim
      *
     c                   write     jvpklur
      *
      * vare-pakning-prisgiver
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å rydde evt. pakninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     rydd_pakning  begsr
      *
      * Sletter forpakninger som ikke er registert manuelt
      *
     c                   eval      jvpklu_vare = jvvare
     c     jvpklu_key    setll     jvpklu
     c     jvpklu_key    reade     jvpklur
     c                   dow       not %eof
     c                   if        jwrsts = 1
     c                   delete    jvpklur
     c                   else
     c                   unlock    jvpklu
     c                   endif
     c     jvpklu_key    reade     jvpklur                                91
     c                   enddo
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å danne påslag
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dann_pasl     begsr
      *
      * Sletter påslag
      *
     c                   eval      jpfal6_vare = jvvare
     c     jpfal6_key    chain     jpfal6r
     c                   if        %found
     c                   delete    jpfal6r
     c                   endif
      *
     c                   clear                   jpfalur
      *
     c                   eval      jcfirm = w_firm
5.70 c                   eval      jcprgr = *blank
     c                   eval      jcogrp = jvogrp
     c                   eval      jchgrp = jvhgrp
     c                   eval      jcugrp = jvugrp
     c                   eval      jcldor = jrldor
     c                   eval      jcmodn = jvmodn
     c                   eval      jcvare = jvvare
     c                   eval      jckofa = 1
     c                   eval      jckobe = 0
      *
     c                   if        w_sapr <> 0 and
     c                             w_inpr <> 0
      *
     c                   if        (w_Sapr / w_inpr) < 100
     c                   eval(h)   jcsafa = w_sapr / w_inpr
     c                   eval      jceusr = l_user
      *
     c                   time                    jcodat
     c                   time                    jcedat
     c                   time                    jcetim
      *
     c                   write     jpfalur
      *
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utleding av datastruktur for 4100 record
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     utled_4100    begsr
      *
     c     '"':' '       xlate     jrvrad        jrvrad
     c                   eval      b_4100 =*off
      *
      * recid.
      *
     c                   eval      w_posi = %scan(';':jrvrad)
     c                   eval      d_rcid = %trim(%subst(jrvrad:1:w_posi-1))
      *
     c                   if        d_rcid <> '4100'
     c                   leavesr
     c                   endif
      *
      * GTIN-nr
      *
     c                   eval      jrvrad = %subst(jrvrad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':jrvrad)
     c                   eval      d_gtin = %trim(%subst(jrvrad:1:w_posi-1))
     c                   eval      len     = %len(%trim(d_gtin))
     c                   if        len     > 0
     c                   eval      st = 15 - len
     c                   eval      w_14x  = *blank
     c                   eval      %subst(w_14x:st:len) = %subst(d_gtin:1:len)
     c                   eval      d_gtin = w_14x
     c                   endif
     c     ' ':'0'       xlate     d_gtin        d_gtin
      *
      *  varetekst 1
      *
     c                   eval      jrvrad = %subst(jrvrad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':jrvrad)
     c                   eval      d_tek1 = %trim(%subst(jrvrad:1:w_posi-1))
      *
     c     X'46':'Æ'     xlate     d_tek1        d_tek1
     c     X'A0':'æ'     xlate     d_tek1        d_tek1
     c     X'77':'Ø'     xlate     d_tek1        d_tek1
     c     X'90':'ø'     xlate     d_tek1        d_tek1
     c     X'2A':'Å'     xlate     d_tek1        d_tek1
     c     X'EF':'å'     xlate     d_tek1        d_tek1
      *
      *
      *  varetekst 2
      *
     c                   eval      jrvrad = %subst(jrvrad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':jrvrad)
     c                   eval      d_tek2 = %trim(%subst(jrvrad:1:w_posi-1))
      *
     c     X'46':'Æ'     xlate     d_tek2        d_tek2
     c     X'A0':'æ'     xlate     d_tek2        d_tek2
     c     X'77':'Ø'     xlate     d_tek2        d_tek2
     c     X'90':'ø'     xlate     d_tek2        d_tek2
     c     X'2A':'Å'     xlate     d_tek2        d_tek2
     c     X'EF':'å'     xlate     d_tek2        d_tek2
      *
      * ubrukt felt 5
      *
     c                   eval      jrvrad = %subst(jrvrad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':jrvrad)
      *
      * ubrukt felt 6
      *
     c                   eval      jrvrad = %subst(jrvrad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':jrvrad)
      *
      * Lev.varenr
      *
     c                   eval      jrvrad = %subst(jrvrad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':jrvrad)
     c                   eval      d_lvar = %trim(%subst(jrvrad:1:w_posi-1))
      *
      * Varegruppe
      *
     c                   eval      jrvrad = %subst(jrvrad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':jrvrad)
     c                   eval      d_vgrp = %trim(%subst(jrvrad:1:w_posi-1))
     c                   eval      len     = %len(%trim(d_vgrp))
     c                   if        len     > 0   and
     c                             len     < 8
     c                   eval      st = 8 - len
     c                   eval      w_7x  = *blank
     c                   eval      %subst(w_7x:st:len) = %subst(d_vgrp:1:len)
     c                   eval      d_vgrp = w_7x
     c                   endif
     c     ' ':'0'       xlate     d_vgrp        d_vgrp
      *
      * Enhet
      *
     c                   eval      jrvrad = %subst(jrvrad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':jrvrad)
     c                   eval      d_enhe = %trim(%subst(jrvrad:1:w_posi-1))
      *
      * sortiment
      *
     c                   eval      jrvrad = %subst(jrvrad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':jrvrad)
     c                   eval      d_sort = %trim(%subst(jrvrad:1:w_posi-1))
      *
      * ubrukt felt 11 - 18
      *
     c                   eval      jrvrad = %subst(jrvrad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':jrvrad)
      *
     c                   eval      jrvrad = %subst(jrvrad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':jrvrad)
      *
     c                   eval      jrvrad = %subst(jrvrad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':jrvrad)
      *
     c                   eval      jrvrad = %subst(jrvrad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':jrvrad)
      *
     c                   eval      jrvrad = %subst(jrvrad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':jrvrad)
      *
     c                   eval      jrvrad = %subst(jrvrad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':jrvrad)
      *
     c                   eval      jrvrad = %subst(jrvrad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':jrvrad)
      *
     c                   eval      jrvrad = %subst(jrvrad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':jrvrad)
      *
      * sortiment
      *
     c                   eval      jrvrad = %subst(jrvrad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':jrvrad)
     c                   eval      d_utgk = %trim(%subst(jrvrad:1:w_posi-1))
      *
     c                   eval      b_4100 =*on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utleding av datastruktur for 4300 record
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     utled_4300    begsr
      *
     c     '"':' '       xlate     jrvrad        jrvrad
     c                   eval      b_4300 =*off
      *
      * recid.
      *
     c                   eval      w_posi = %scan(';':jrvrad)
     c                   eval      d_rcid = %trim(%subst(jrvrad:1:w_posi-1))
      *
     c                   if        d_rcid <> '4300'
     c                   leavesr
     c                   endif
      *
      * GTIN-nr
      *
     c                   eval      jrvrad = %subst(jrvrad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':jrvrad)
     c                   eval      d_gtin = %trim(%subst(jrvrad:1:w_posi-1))
     c                   eval      len     = %len(%trim(d_gtin))
     c                   if        len     > 0
     c                   eval      st = 15 - len
     c                   eval      w_14x  = *blank
     c                   eval      %subst(w_14x:st:len) = %subst(d_gtin:1:len)
     c                   eval      d_gtin = w_14x
     c                   endif
     c     ' ':'0'       xlate     d_gtin        d_gtin
      *
      *  grunnpris /lev.rab 1 lev.rab 2
      *
     c                   eval      jrvrad = %subst(jrvrad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':jrvrad)
      *
     c                   eval      jrvrad = %subst(jrvrad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':jrvrad)
      *
     c                   eval      jrvrad = %subst(jrvrad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':jrvrad)
      *
      * Innkjøpspris
      *
     c                   eval      jrvrad = %subst(jrvrad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':jrvrad)
     c                   eval      d_inpr = %trim(%subst(jrvrad:1:w_posi-1))
      *
      * Salgspris
      *
     c                   eval      jrvrad = %subst(jrvrad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':jrvrad)
     c                   eval      d_sapr = %trim(%subst(jrvrad:1:w_posi-1))
      *
      * Utleder  innkjøpspris
      *
     c                   if        d_inpr <> *blank
      *
     c                   eval      d_inpr = %trim(d_inpr)
     c                   eval      w_posi = %scan(',':d_inpr)
     c                   eval      w_leng = %len(%trim(d_inpr))
      *
     c                   if        w_posi = 0
     c                   eval      w_posi = w_leng + 1
     c                   eval      d_inpr = %trim(d_inpr) + '.00'
     c                   endif
      *
     c     ' ':'0'       xlate     d_inpr        d_inpr
      *
     c                   if        (w_posi-1) < 8
     c                   eval      d_inpr = %subst(c_null:1:8-w_posi+1) +
     c                                      %subst(d_inpr:1:w_posi-1) +
     c                                      '.' +
     c                                      %subst(d_inpr:w_posi+1:2)
     c                   endif
      *
     c                   else
     c                   eval      d_inpr = '00000000.00'
     c                   endif
      *
      * Utleder  salgspris
      *
     c                   if        d_sapr <> *blank
      *
     c                   eval      d_sapr = %trim(d_sapr)
     c                   eval      w_posi = %scan(',':d_sapr)
     c                   eval      w_leng = %len(%trim(d_sapr))
      *
     c                   if        w_posi = 0
     c                   eval      w_posi = w_leng + 1
     c                   eval      d_sapr = %trim(d_sapr) + '.00'
     c                   endif
      *
     c     ' ':'0'       xlate     d_sapr        d_sapr
      *
     c                   if        (w_posi-1) < 9
     c                   eval      d_sapr = %subst(c_null:1:8-w_posi+1) +
     c                                      %subst(d_sapr:1:w_posi-1) +
     c                                      '.' +
     c                                      %subst(d_sapr:w_posi+1:2)
     c                   endif
      *
     c                   else
     c                   eval      d_sapr = '000000000.00'
     c                   endif
      *
     c                   eval      b_4300 =*on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_list
      *
      * Key for oppslag på rapportør
      *
     c     jrapl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jrapl1_rapp
      *
      * Key for oppslag på vare-pris
      *
     c     jvprl2_key    klist
     c                   kfld                    jvprl2_ldor
     c                   kfld                    jvprl2_lvar
      *
      * Key for oppdatering av vare-pris
      *
     c     jvprlu_key    klist
     c                   kfld                    jvprlu_vare
     c                   kfld                    jvprlu_ldor
      *
      * Key for oppslag på leverandør
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for oppdatering av vare
      *
     c     jvarlu_key    klist
     c                   kfld                    jvarlu_vare
      *
      * Key for oppdatering av vare-pakning
      *
     c     jvpklu_key    klist
     c                   kfld                    jvpklu_vare
      *
      * Key for oppdatering av kode/status
      *
     c     jstslu_key    klist
     c                   kfld                    w_firm
      *
      * Key for les av vare-pakning-prisgiver pr. gtinnr
      *
6.30 c     jvpkl3_key    klist
6.30 c                   kfld                    jvpkl3_gtin
      *
      * Key for oppdatering av påslag
      *
     c     jpfal6_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jpfal6_vare
      *
      *
      * Fyller datastruktur fra parameter, og henter firma
      *
     c                   eval      d_list = p_list
     c                   eval      w_firm = d_firm
      *
      * Henter mva-faktor
      *
     c                   time                    w_udat
      *
     c                   call      'FÅ705R'
     c                   parm                    w_firm
     c                   parm                    w_udat
     c                   parm                    w_mvap
     c                   parm                    w_mvat
     c                   parm                    w_mvaf
      *
      *
     c                   endsr
