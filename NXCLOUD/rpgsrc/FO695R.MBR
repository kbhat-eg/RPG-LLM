     h datedit(*dmy.) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO695R
      * Beskrivelse . . : Utskrift av leveringsplanleggings-liste
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
3.11  * 3.11  130900 AMD  Oppdatert til ny standard
3.11  * 3.11  130900 AMD  Lagt på valg av leveringsmåte
3.11  *                   Lagt inn F4-søk på ordretype og leveringsmåte
      * R5.21 200803 AMD  Lagt om til AP600R i stedet for AP100R
6.nu  * R6.30 280514 bsa  Programmet gjennomgått mtp nummerutvidelser.
7.xx  * 7.00  290316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
      *
     ffo695d    cf   e             workstn
      *
      *
      * Datastruktur for File-member
      *
     d                 ds
     d d_ambr                        10
     d  d_abok                        1    overlay(d_ambr)
     d  d_afir                        3  0 overlay(d_ambr:2)
     d  d_alst                        6  0 overlay(d_ambr:5)
      *
      * Datastruktur for Parameter-liste ut
      *
     d                 ds
     d d_prec                        64
     d  d_otyp                        2    overlay(d_prec)
6.nu d  d_fonr                        8  0 overlay(d_prec:5)
6.nu d  d_tonr                        8  0 overlay(d_prec:13)
6.nu d  d_fkun                        6  0 overlay(d_prec:21)
6.nu d  d_tkun                        6  0 overlay(d_prec:27)
6.nu d  d_fdat                       10d   datfmt(*iso)
6.nu d                                     overlay(d_prec:33)
6.nu d  d_tdat                       10d   datfmt(*iso)
6.nu d                                     overlay(d_prec:43)
6.nu d  d_val1                        1  0 overlay(d_prec:53)
6.nu d  d_val2                        1  0 overlay(d_prec:54)
6.nu d  d_lmat                        2  0 overlay(d_prec:55)
      *
      * Definering av Local data area                                   *
      *
     d lda            uds
     d l_ruti                800    809
     d l_chgv                844    844
     d l_sfir                944    946  0
     d l_fnav                951    980
      *
      * Definering av variable i nøkler
      *                                                                -----
     d w_firm          s                   like(vaofir)
     d votyl1_otyp     s                   like(vaotyp)
      *
      * Definering av variable
      *                                                                -----
     d w_numm          s              6  0
     d w_qnnn          s              1
5.21 d w_in03          s              1
     d w_fdat          s                   like(d_fdat)
     d w_tdat          s                   like(d_tdat)
     d w_valg          s              1
3.11 d w_qkod          s              2  0
3.11 d w_qtxt          s             20
5.21 d w_tvbt          s              1
5.21 d w_btch          s              1
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandling av valg fra/til                                     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     b1tag         tag
      *
      *  Nullstiller variable som skal vises i skjermbildet
      *
     c                   eval      b1otyp = *blank
     c                   eval      b1fonr = *zero
     c                   eval      b1tonr = *zero
     c                   eval      b1fkun = *zero
     c                   eval      b1tkun = *zero
     c                   eval      b1fdat = 0
     c                   eval      b1tdat = 0
3.11 c                   eval      b1lmat = 0
      *
     c     b1taga        tag
      *
     c     b1tagb        tag
     c                   exfmt     b1bld
      *
      *  F3=Gå tilbake  (Avslutt)
      *
     c     *inkc         cabeq     *on           xslutt
3.11  *
3.11  * Spørring (F4)
3.11  *
3.11 c                   if        *inkd  = *on
3.11 c                   if        w_arcd = 'B1BLD'
3.11  *
3.11  *  Spørring på ordretyper
3.11  *
3.11 c                   if        w_afld = 'B1OTYP'
3.11 c                   call      'VA550R'
3.11 c                   parm                    w_firm
3.11 c                   parm                    b1otyp
3.11 c                   eval      votyl1_otyp = b1otyp
3.11 c     votyl1_key    chain     votyl1r                            90
3.11 c                   if        *in90  = *off
3.11 c                   eval      b1otxt = vaotxt
3.11 c                   endif
3.11 c                   goto      b1tagb
3.11 c                   endif
3.11  *
3.11  * Spørring på leveringsmåte
3.11  *
3.11 c                   if        w_afld = 'B1LMAT'
3.11 c                   call      'RA517R'
3.11 c                   parm                    w_firm
3.11 c                   parm                    w_qkod
3.11 c                   parm                    w_qtxt
3.11 c                   if        w_qkod <> 0
3.11 c                   eval      b1lmat = w_qkod
3.11 c                   eval      b1lmtx = w_qtxt
3.11 c                   endif
3.11 c                   goto      b1tagb
3.11 c                   endif
3.11  *
3.11 c                   endif
3.11 c                   endif
3.11  *
      *
      *  Sjekk   Finnes ordretype i ORDRE-TYPE-REGISTER
      *
     c                   eval      votyl1_otyp = b1otyp
     c     votyl1_key    chain     votyl1r                            90
     c                   if        *in90 = *on
     c                   eval      *in31 = *on
     c                   goto      b1tagb
     c                   endif
      *
      *  Sjekk   Er dato fra større enn dato til
      *
     c                   eval      w_fdat = *loval
     c                   eval      w_tdat = *loval
      *
     c                   if        b1fdat <> 0
     c     *dmy          move      b1fdat        w_fdat
     c                   endif
      *
     c                   if        b1tdat <> 0
     c     *dmy          move      b1tdat        w_tdat
     c                   endif
      *
     c                   if        w_fdat > w_tdat
     c                   eval      *in35  = *on
     c                   goto      b1tagb
     c                   endif
      *
      *  Legger inntastede opplysninger i Hjelpe-felter for Parameter
      *
     c                   eval      d_otyp = b1otyp
     c                   eval      d_fonr = b1fonr
     c                   eval      d_tonr = b1tonr
     c                   eval      d_fkun = b1fkun
     c                   eval      d_tkun = b1tkun
     c                   eval      d_fdat = w_fdat
     c                   eval      d_tdat = w_tdat
3.11 c                   eval      d_lmat = b1lmat
      *
      *  Dersom kundenummer til=0 skal t.o.m. alle kunder velges
      *
     c                   if        d_tkun = *zero
     c                   eval      d_tkun = 999999
     c                   endif
      *
      *  Dersom dato til=0 skal t.o.m. alle datoer velges
      *
     c                   if        d_tdat = *loval
     c                   move      *hival        d_tdat
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandle bilde for valg av sortering/uttrekk osv.              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     c1tag         tag
      *
      *  Nullstiller variable som skal vises i skjermbildet
      *
     c                   eval      c1val1 = 1
     c                   eval      c1val2 = *zero
      *
     c     c1taga        tag
      *
     c     c1tagb        tag
     c                   exfmt     c1bld
      *
      *  F3=Gå tilbake  (Avslutt)
      *
     c     *inkc         cabeq     *on           b1tag
      *
      *  Legger inntastede opplysninger i Hjelpe-felter for Parameter
      *
     c                   eval      d_val1 = c1val1
     c                   eval      d_val2 = c1val2
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Inntasting er ferdig !  Oppstart av utvalg/utskriftsprogrammer *
      *  Kaller opp neste 'CL'-program                                  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      *  Legg inn verdi for bare henting av opplysninger
      *
     c                   eval      l_chgv = *blank                               KODE VISE BILDE
     c                   eval      l_ruti = 'FO698'                              RUTINE
     c                   out       *dtaara
      *
      *  Kaller opp program før utskrift
      *
5.21 c                   call      'AP600R'
5.21 c                   parm                    l_ruti
5.21 c                   parm                    l_chgv
5.21 c                   parm                    w_tvbt
5.21 c                   parm                    w_in03
5.21 c                   parm                    w_btch
      *
      *  F4=Legge jobb på jobbkø
      *
     c                   if        *inkd = *on
     c                   move      '1'           w_valg
     c                   endif
      *
      *  Henter siste nummer for File-member
      *
     c                   call      'FA920R'
     c                   parm                    w_qnnn
     c                   parm                    w_numm
      *
     c                   move      'K'           d_abok
     c                   move      w_numm        d_alst
      *
      *  Kaller opp subprogram
      *
     c                   call      'FO696C'
     c                   parm                    d_prec
     c                   parm                    d_ambr
     c                   parm                    w_valg
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     *inzsr        begsr
      *
      *  Definisjon av local data area for output
      *
     c     *dtaara       define    *lda          lda
      *
5.21 c                   eval      w_in03 = *blank
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     c                   eval      w_valg = *zero
     c                   eval      w_qnnn = *zero
     c                   eval      w_numm = *zero
      *
      *  Key for file : ORDRE-TYPE-REGISTER
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      *  Legge inn firmanummer i nøkklene
      *
     c                   eval      w_firm = l_sfir
     c                   eval      d_afir = l_sfir
      *
     c                   endsr
