     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JP702R
      * Beskrivelse . . : Betingelse, innlesing på flere filgrupper
      *                   styreprogram
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       300608 bof  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     facpll1    if   e           k disk    rename(acplpfr:acpll1r)
      *
      * Definering av parametre
      *
     d p_fgrf          s                   like(aclfgr)
     d p_firf          s                   like(aclfir)
     d p_fldr          s             20
      *
      * Definering av variable i nøkler
      *
     d acpll1_lfgr     s                   like(aclfgr)
     d acpll1_lfir     s                   like(aclfir)
     d acpll1_lfgt     s                   like(aclfgt)
     d acpll1_lfit     s                   like(aclfit)
      *
      * Definering av variable
      *
     d w_fgrf          s                   like(aclfgr)
     d w_firf          s                   like(aclfir)
     d w_fgrt          s                   like(aclfgr)
     d w_firt          s                   like(aclfir)
     d w_tmst          s               z
     d w_tmst_20       s             20
     d w_memb          s             10
     d w_fldr          s             20
     d w_feil          s              1
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *Behandler øverste nivå (MAS)
     c                   eval      w_fgrt = p_fgrf
     c                   eval      w_firt = p_firf
      *
     c                   call      'JP701C'
     c                   parm                    w_fgrt
     c                   parm                    w_firt
      *
     c                   call      'JP702C'
     c                   parm                    w_fldr
     c                   parm                    w_fgrt
     c                   parm                    w_feil
      *
      * behandler undernivå
      *
     c                   eval      acpll1_lfgr = w_fgrf
     c                   eval      acpll1_lfir = w_firf
     c     acpll1_key    setll     acpll1
     c     acpll1_key    reade     acpll1
     c                   dow       not %eof
     c                   exsr      behandling
     c     acpll1_key    reade     acpll1
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   move      *on           *inlr
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine som kaller program for kopiering av filgruppe/firma
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
     c                   eval      w_fgrt = aclfgt
     c                   eval      w_firt = aclfit
      *
     c                   call      'JP701C'
     c                   parm                    w_fgrt
     c                   parm                    w_firt
      *
     c                   call      'JP702C'
     c                   parm                    w_fldr
     c                   parm                    w_fgrt
     c                   parm                    w_feil
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firf
     c                   parm                    p_fgrf
     c                   parm                    p_fldr
      *
      * Key for les på filkopierings-linje-register
      *
     c     acpll1_key    klist
     c                   kfld                    acpll1_lfgr
     c                   kfld                    acpll1_lfir
      *
      *
      * Kopiering av parameterliste til datastruktur
      *
     c                   eval      w_fgrf = p_fgrf
     c                   eval      w_firf = p_firf
     c                   eval      w_fldr = p_fldr
      *
     c                   endsr
