     h option(*nodebugio)
      /title  ASOFAK Henter ny priser  fra regneark
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FW725R
      * Beskrivelse . . : Danning av leverandør vareregister, T.M. Bergersen
      *
      * Spesialversjon. : Tilpasset innlesing internt for T.M Bergersen
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       080408 bof  Nytt program
5.70  * prgr  160609 bof  Utvidet med prisgruppe
      * 6.10  290609 BSA  Oppdatert med sporingsinformasjon
6.20  * 6.20  030411 bof  Utvidet vvprpf med prisgiver
8.01  *PRG2   100622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fflvwpf    if   e           k disk
     ffenkl1    if   e           k disk    rename(fenkpfr:fenkl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvprl1    if   e           k disk    rename(vvprpfr:vvprl1r)
     fvvenl2    if   e           k disk    rename(vvenpfr:vvenl2r)
     fvvprlu    uf a e           k disk    rename(vvprpfr:vvprlur)
      *
      * Definering av datastruktur
      *
      *  Recordbeskrivelse - Emco
      *
     d                 ds
     d d_inpu                       256
     d  d_evar                       15    overlay(d_inpu:1)
     d  d_vare                       15    overlay(d_inpu:16)
     d  d_tek1                       35    overlay(d_inpu:31)
     d  d_tek2                       35    overlay(d_inpu:66)
     d  d_nobb                        8    overlay(d_inpu:101)
     d  d_eann                       13    overlay(d_inpu:109)
     d  d_enhe                        3    overlay(d_inpu:122)
     d  d_inpr                       10    overlay(d_inpu:125)
     d   d_inpr_kr                    7  0 overlay(d_inpr:1)
     d   d_inpr_Øre                   2  2 overlay(d_inpr:9)
     d  d_sapr                       10    overlay(d_inpu:135)
     d   d_sapr_kr                    7  0 overlay(d_sapr:1)
     d   d_sapr_Øre                   2  2 overlay(d_sapr:9)
     d  d_anta                       12    overlay(d_inpu:145)
     d   d_anta_hel                   8  0 overlay(d_anta:1)
     d   d_anta_des                   3  3 overlay(d_anta:10)
     d  d_vekt                       10    overlay(d_inpu:157)
     d   d_vekt_hel                   6  0 overlay(d_vekt:1)
     d   d_vekt_des                   3  3 overlay(d_vekt:8)
     d  d_lety                        2    overlay(d_inpu:167)
     d  d_kopr                       10    overlay(d_inpu:169)
     d   d_kopr_kr                    7  0 overlay(d_kopr:1)
     d   d_kopr_Øre                   2  2 overlay(d_kopr:9)
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_ldor          s              6  0
      *
      * Definering av variable i nøkler
      *
     d fenkl1_enhf     s                   like(feenhf)
     d vvarl1_vare     s                   like(vvvare)
     d vvenl2_vare     s                   like(vevare)
     d vvenl2_saen     s                   like(vesaen)
     d vvprlu_vare     s                   like(vpvare)
6.20 d vvprlu_ldor     s                   like(vpldor)
5.70 d vvprlu_prgr     s                   like(vpprgr)
     d vvprlu_enhe     s                   like(vpenhe)
     d vvprlu_lety     s                   like(vplety)
     d vvprlu_pdat     s                   like(vppdat)
      *
      * Definering av variable
      *
     d b_ok            s              1    inz(*off)
     d w_posi          s              4  0
     d w_leng          s              4  0
     d w_firm          s                   like(vpfirm)
     d w_dato          s              6  0
     d w_pdat          s                   like(vppdat)
     d w_lety          s              1
     d w_enh1          s                   like(veenhe)
     d w_enh2          s                   like(veenhe)
     d w_enh3          s                   like(veenhe)
     d w_omr1          s                   like(veomre)
     d w_omr2          s                   like(veomre)
     d w_omr3          s                   like(veomre)
     d w_sap1          s                   like(vpsapr)
     d w_sap2          s                   like(vpsapr)
     d w_sap3          s                   like(vpsapr)
     d w_kop1          s                   like(vpkopr)
     d w_kop2          s                   like(vpkopr)
     d w_kop3          s                   like(vpkopr)
     d w_inp1          s                   like(vpkopr)
     d w_inp2          s                   like(vpkopr)
     d w_inp3          s                   like(vpkopr)
      *
      * Definering av konstanter
      *
     d Lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d Up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
     d digits          c                   ' 0123456789'
     d c_null          s             15    inz('000000000000000')
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser input-fil, og legger data ut på leverandør vareregister
      *
     c                   read      flvwpf
     c                   dow       not %eof
      *
     c                   eval      b_ok = *off
     c                   eval      d_inpu = *blank
     c                   exsr      utled_ds
      *
     c                   if        b_ok = *on
     c                   if        %check(digits : d_vare) = 0
     c                   eval      vvarl1_vare = d_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found(vvarl1)
     c                   exsr      oppd_pris
     c                   endif
     c                   endif
     c                   endif
      *
     c                   read      flvwpf
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Finner oppdaterer priser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_pris     begsr
      *
     c                   exsr      finn_enhe
      *
      * Oppretter vare-pris
      *
      *
      * Enhet-1
     c                   if        w_enh1 <> *blank and
     c                             w_sap1 <> 0 and
     c                             w_inp1 <> 0 and
     c                             w_kop1 <> 0
     c                   eval      vvprlu_vare  = d_vare
6.20 c                   eval      vvprlu_ldor  = vvldor
5.70 c                   eval      vvprlu_prgr  = *blank
     c                   eval      vvprlu_enhe  = w_enh1
     c                   eval      vvprlu_lety  = 0
     c                   eval      vvprlu_pdat  = w_pdat
     c     vvprlu_key    chain     vvprlu
     c                   eval(h)   vpsapr = w_sap1
     c                   eval(h)   vpinpr = w_inp1
     c                   eval(h)   vpkopr = w_kop1
     c                   eval      vpbupr = 0
     c                   eval      vpeusr = l_user
     c                   time                    vpedat
     c                   time                    vpetim
      *
     c                   if        not %found(vvprlu)
     c                   eval      vpfirm = w_firm
5.70 c                   eval      vpprgr = *blank
     c                   eval      vpvare = vvprlu_vare
6.20 c                   eval      vpldor = vvprlu_ldor
     c                   eval      vplety = vvprlu_lety
     c                   eval      vpenhe = vvprlu_enhe
     c                   eval      vppdat = vvprlu_pdat
6.10 c                   time                    vpodat
6.10 c                   time                    vpotim
6.10 c                   eval      vpousr = l_user
     c                   write     vvprlur
     c                   else
     c                   update    vvprlur
     c                   endif
      *
     c                   endif
      *
      * Enhet-2
      *
     c                   if        w_enh2 <> *blank and
     c                             w_sap2 <> 0 and
     c                             w_inp2 <> 0 and
     c                             w_kop2 <> 0
     c                   eval      vvprlu_vare  = d_vare
6.20 c                   eval      vvprlu_ldor  = vvldor
5.70 c                   eval      vvprlu_prgr  = *blank
     c                   eval      vvprlu_enhe  = w_enh2
     c                   eval      vvprlu_lety  = 0
     c                   eval      vvprlu_pdat  = w_pdat
     c     vvprlu_key    chain     vvprlu
     c                   eval(h)   vpsapr = w_sap2
     c                   eval(h)   vpinpr = w_inp2
     c                   eval(h)   vpkopr = w_kop2
     c                   eval      vpbupr = 0
     c                   eval      vpeusr = l_user
     c                   time                    vpedat
     c                   time                    vpetim
      *
     c                   if        not %found(vvprlu)
     c                   eval      vpfirm = w_firm
5.70 c                   eval      vpprgr = *blank
     c                   eval      vpvare = vvprlu_vare
6.20 c                   eval      vpldor = vvprlu_ldor
     c                   eval      vplety = vvprlu_lety
     c                   eval      vpenhe = vvprlu_enhe
     c                   eval      vppdat = vvprlu_pdat
6.10 c                   time                    vpodat
6.10 c                   time                    vpotim
6.10 c                   eval      vpousr = l_user
     c                   write     vvprlur
     c                   else
     c                   update    vvprlur
     c                   endif
      *
     c                   endif
      *
      * Enhet-3
      *
     c                   if        w_enh3 <> *blank and
     c                             w_sap3 <> 0 and
     c                             w_inp3 <> 0 and
     c                             w_kop3 <> 0
     c                   eval      vvprlu_vare  = d_vare
6.20 c                   eval      vvprlu_ldor  = vvldor
5.70 c                   eval      vvprlu_prgr  = *blank
     c                   eval      vvprlu_enhe  = w_enh3
     c                   eval      vvprlu_lety  = 0
     c                   eval      vvprlu_pdat  = w_pdat
     c     vvprlu_key    chain     vvprlu
     c                   eval(h)   vpsapr = w_sap3
     c                   eval(h)   vpinpr = w_inp3
     c                   eval(h)   vpkopr = w_kop3
     c                   eval      vpbupr = 0
     c                   eval      vpeusr = l_user
     c                   time                    vpedat
     c                   time                    vpetim
      *
     c                   if        not %found(vvprlu)
     c                   eval      vpfirm = w_firm
5.70 c                   eval      vpprgr = *blank
     c                   eval      vpvare = vvprlu_vare
6.20 c                   eval      vpldor = vvprlu_ldor
     c                   eval      vplety = vvprlu_lety
     c                   eval      vpenhe = vvprlu_enhe
     c                   eval      vppdat = vvprlu_pdat
6.10 c                   time                    vpodat
6.10 c                   time                    vpotim
6.10 c                   eval      vpousr = l_user
     c                   write     vvprlur
     c                   else
     c                   update    vvprlur
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Finner 3 salgsenhet for varen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_enhe     begsr
      *
      *
      * Finner tre salgsenheter på varen
      *
     c                   eval      w_enh1 = *blank
     c                   eval      w_enh2 = *blank
     c                   eval      w_enh3 = *blank
     c                   eval      w_omr1 = 0
     c                   eval      w_omr2 = 0
     c                   eval      w_omr3 = 0
      *
     c                   eval      vvenl2_vare = d_vare
     c                   eval      vvenl2_saen = 1
     c     vvenl2_key    setll     vvenl2
5.41 c     vvenl2_key2   reade     vvenl2
5.41 c                   dow       not %eof and
5.41 c                             vesaen > 0
      *
     c                   select
     c                   when      w_enh1 = *blank
     c                   eval      w_enh1 = veenhe
     c                   eval      w_omr1 = veomre
     c                   when      w_enh1 <> *blank and
     c                             w_enh2 = *blank
     c                   eval      w_enh2 = veenhe
     c                   eval      w_omr2 = veomre
     c                   when      w_enh1 <> *blank and
     c                             w_enh2 <> *blank and
     c                             w_enh3 = *blank
     c                   eval      w_enh3 = veenhe
     c                   eval      w_omr3 = veomre
     c                   leave
     c                   endsl
      *
5.41 c     vvenl2_key2   reade     vvenl2
     c                   enddo
      *
      * Finner salgspris for alle salgsenheter
      *
     c                   eval      w_sap1 = 0
     c                   eval      w_sap2 = 0
     c                   eval      w_sap3 = 0
     c                   eval      w_kop1 = 0
     c                   eval      w_kop2 = 0
     c                   eval      w_kop3 = 0
     c                   eval      w_inp1 = 0
     c                   eval      w_inp2 = 0
     c                   eval      w_inp3 = 0
      *
     c                   select
     c                   when      d_enhe = w_enh1
     c                   eval      w_sap1 = d_sapr_kr + d_sapr_Øre
     c                   eval      w_sap2 = w_sap1 * w_omr2 / w_omr1
     c                   eval      w_sap3 = w_sap1 * w_omr3 / w_omr1
     c                   eval      w_kop1 = d_kopr_kr + d_kopr_Øre
     c                   eval      w_kop2 = w_kop1 * w_omr2 / w_omr1
     c                   eval      w_kop3 = w_kop1 * w_omr3 / w_omr1
     c                   eval      w_inp1 = d_inpr_kr + d_kopr_Øre
     c                   eval      w_inp2 = w_inp1 * w_omr2 / w_omr1
     c                   eval      w_inp3 = w_inp1 * w_omr3 / w_omr1
     c                   when      d_enhe = w_enh2
     c                   eval      w_sap2 = d_sapr_kr + d_sapr_Øre
     c                   eval      w_sap1 = w_sap2 * w_omr1 / w_omr2
     c                   eval      w_sap3 = w_sap2 * w_omr3 / w_omr2
     c                   eval      w_kop2 = d_kopr_kr + d_kopr_Øre
     c                   eval      w_kop1 = w_kop2 * w_omr1 / w_omr2
     c                   eval      w_kop3 = w_kop2 * w_omr3 / w_omr2
     c                   eval      w_inp2 = d_inpr_kr + d_inpr_Øre
     c                   eval      w_inp1 = w_inp2 * w_omr1 / w_omr2
     c                   eval      w_inp3 = w_inp2 * w_omr3 / w_omr2
     c                   when      d_enhe = w_enh3
     c                   eval      w_sap3 = d_sapr_kr + d_sapr_Øre
     c                   eval      w_sap1 = w_sap3 * w_omr1 / w_omr3
     c                   eval      w_sap2 = w_sap3 * w_omr2 / w_omr3
     c                   eval      w_kop3 = d_kopr_kr + d_kopr_Øre
     c                   eval      w_kop1 = w_kop3 * w_omr1 / w_omr3
     c                   eval      w_kop2 = w_kop3 * w_omr2 / w_omr3
     c                   eval      w_inp3 = d_inpr_kr + d_inpr_Øre
     c                   eval      w_inp1 = w_inp3 * w_omr1 / w_omr3
     c                   eval      w_inp2 = w_inp3 * w_omr2 / w_omr3
     c                   endsl
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utleding av datastruktur
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     utled_ds      begsr
      *
     c     '"':' '       xlate     fwirad        fwirad
      *
      * Varenummer
      *
     c                   eval      w_posi = %scan(';':fwirad)
     c                   eval      d_vare = %trim(%subst(fwirad:1:w_posi-1))
     c                   if        %check(digits : d_vare) <>0
     c                   leavesr
     c                   endif
      *
      * Varetype
      *
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
      *
      * Enhet
      *
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
     c                   eval      d_enhe = %trim(%subst(fwirad:1:w_posi-1))
     c                   if        d_enhe = *blank
     c                   leavesr
     c                   endif
      *
      * Varetekst-1
      *
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
     c                   eval      d_tek1 = %trim(%subst(fwirad:1:w_posi-1))
      *
      * Varetekst-2
      *
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
     c                   eval      d_tek2 = %trim(%subst(fwirad:1:w_posi-1))
      * lev.dør /lnav /lvar
      *
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
      *
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
      *
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
      * nobb
      *
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
      * sist oppdatert
      *
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
      *
      * Ekstirende priser og data
      *
      *vpsapr
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
      * m mva
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
      * kostpris
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
      * innkj. pris
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
      * butikk pris
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
      * dekning
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
      * edat
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
      * eusr
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
      *
      * Kostpris
      *
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
     c                   eval      d_kopr = %trim(%subst(fwirad:1:w_posi-1))
     c                   if        d_kopr = '-'
     c                   eval      d_kopr = *blank
     c                   endif
     c                   if        d_kopr = *blank
     c                   leavesr
     c                   endif
      * Innkj.pris
      *
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
     c                   eval      d_inpr = %trim(%subst(fwirad:1:w_posi-1))
     c                   if        d_inpr = '-'
     c                   eval      d_inpr = *blank
     c                   endif
      *
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
      * Salgspris
      *
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
     c                   eval      d_sapr = %trim(%subst(fwirad:1:w_posi-1))
     c                   if        d_sapr = '-'
     c                   eval      d_sapr = *blank
     c                   endif
     c                   if        d_sapr = *blank
     c                   leavesr
     c                   endif
      *
      *
      * salgspris m mva
      *
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
      *
      * Utleder salgspris
      *
     c                   if        d_sapr <> *blank
      *
     c                   eval      d_sapr = %trim(d_sapr)
     c                   eval      w_posi = %scan(',':d_sapr)
     c                   eval      w_leng = %len(%trim(d_sapr))
      *
     c                   if        w_posi = 0
     c                   eval      w_posi = w_leng + 1
     c                   eval      d_sapr = %trim(d_sapr) + '.00'
     c                   endif
      *
     c     ' ':'0'       xlate     d_sapr        d_sapr
      *
     c                   if        (w_posi-1) < 7
     c                   eval      d_sapr = %subst(c_null:1:7-w_posi+1) +
     c                                      %subst(d_sapr:1:w_posi-1) +
     c                                      '.' +
     c                                      %subst(d_sapr:w_posi+1:2)
     c                   endif
      *
     c                   else
     c                   eval      d_sapr = '0000000.00'
     c                   endif
      *
      * Utleder innkjøpspris
      *
     c                   if        d_inpr <> *blank
      *
     c                   eval      d_inpr = %trim(d_inpr)
     c                   eval      w_posi = %scan(',':d_inpr)
     c                   eval      w_leng = %len(%trim(d_inpr))
      *
     c                   if        w_posi = 0
     c                   eval      w_posi = w_leng + 1
     c                   eval      d_inpr = %trim(d_inpr) + '.00'
     c                   endif
      *
     c     ' ':'0'       xlate     d_inpr        d_inpr
      *
     c                   if        (w_posi-1) < 7
     c                   eval      d_inpr = %subst(c_null:1:7-w_posi+1) +
     c                                      %subst(d_inpr:1:w_posi-1) +
     c                                      '.' +
     c                                      %subst(d_inpr:w_posi+1:2)
     c                   endif
      *
     c                   else
     c                   eval      d_inpr = '0000000.00'
     c                   endif
      *
      * Utleder kostpris
      *
     c                   if        d_kopr <> *blank
      *
     c                   eval      d_kopr = %trim(d_kopr)
     c                   eval      w_posi = %scan(',':d_kopr)
     c                   eval      w_leng = %len(%trim(d_kopr))
      *
     c                   if        w_posi = 0
     c                   eval      w_posi = w_leng + 1
     c                   eval      d_kopr = %trim(d_kopr) + '.00'
     c                   endif
      *
     c     ' ':'0'       xlate     d_kopr        d_kopr
      *
     c                   if        (w_posi-1) < 7
     c                   eval      d_kopr = %subst(c_null:1:7-w_posi+1) +
     c                                      %subst(d_kopr:1:w_posi-1) +
     c                                      '.' +
     c                                      %subst(d_kopr:w_posi+1:2)
     c                   endif
      *
     c                   else
     c                   eval      d_kopr = '0000000.00'
     c                   endif
      *
     c                   eval      b_ok = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på enhet-konvertering
      *
     c     fenkl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fenkl1_enhf
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for les på vare-enhet
      *
     c     vvenl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
     c                   kfld                    vvenl2_saen
5.41  *
5.41  * Key2 for les på vare-enhet
5.41  *
5.41 c     vvenl2_key2   klist
5.41 c                   kfld                    w_firm
5.41 c                   kfld                    vvenl2_vare
      *
      * Key for oppdater vare-pris
      *
     c     vvprlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvprlu_vare
6.20 c                   kfld                    vvprlu_ldor
5.70 c                   kfld                    vvprlu_prgr
     c                   kfld                    vvprlu_enhe
     c                   kfld                    vvprlu_lety
     c                   kfld                    vvprlu_pdat
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   eval      w_dato = 250308
     c     *dmy          move      w_dato        w_pdat
      *
     c                   endsr
