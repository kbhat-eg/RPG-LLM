     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VV109R
      * Beskrivelse . . : Vare, vedlikehold - bytt enhet
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       150903 HKO  Nytt program
      *       300304 HKO  Lagt inn bytting av navn på enhet i filgrupper
      *                   hvor master-vareregister kopieres til
6.30  *       270116 bof  Sjekk om vare ligger på LBDTPF/LTELPF på aktiv batch
6.31  *       210317 bof  Vise hvilke tellinger som er aktive.
7.xx  * 7.00  030216 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     fvenhl1    if   e           k disk    rename(venhpfr:venhl1r)
6.30 flbdtl3    if   e           k disk    rename(lbdtpfr:lbdtl3r)
6.30 fltell3    if   e           k disk    rename(ltelpfr:ltell3r)
6.30 flbchl1    if   e           k disk    rename(lbchpfr:lbchl1r)
      *
     fvv109d    cf   e             workstn
      *
6.31  * Array
6.31 d a_txt           s             40    dim(15)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vvfirm)
     d p_vare          s                   like(vvvare)
      *
      * Definering av variabler i nøkler
      *
     d vvenl1_enhe     s                   like(veenhe)
     d venhl1_eenh     s                   like(vaeenh)
6.30 d lbdtl3_vare     s                   like(lvvare)
6.30 d ltell3_vare     s                   like(levare)
6.30 d lbchl1_batc     s                   like(lcbatc)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
     d w_etxt          s             35
6.31 d i               s              3  0
      *
     d w_firm          s                   like(vvfirm)
     d w_vare          s                   like(vvvare)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Avbryter dersom vare ikke finnes
      *
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
      * Behandler bildet
      *
     c     b1taga        tag
      *
     c                   exfmt     b1bld
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b1taga
     c                   endsl
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1taga
     c                   endif
      *
      * Kaller program for bytting av enhet gjennom hele systemet
      *
     c                   call      'VV755R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    b1enhg
     c                   parm                    b1enhn
      *
      * Dersom filkopiering og enheten byttes i master-register byttes
      * også navn på enhet i alle filgrupper det kopieres til
      *
     c                   call      'VV790R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    b1enhg
     c                   parm                    b1enhn
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørringer (F4)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
     c                   if        w_afld = 'B1ENHG'
     c                   call      'VE550R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    b1enhg
     c                   parm                    w_etxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'B1ENHN'
     c                   call      'VA510R'
     c                   parm                    w_firm
     c                   parm                    b1enhn
     c                   parm                    w_etxt
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
6.31 c                   eval      i = 0
6.31 c                   eval      a_txt(*) = *blank
6.30  *
6.30  * sjekker om vare ligger på en telling (LBDTPF/LTELPF) på aktiv batch
6.30  *
6.30 c                   eval      ltell3_vare = p_vare
6.31 c     ltell3_key    setll     ltell3
6.31 c     ltell3_key    reade     ltell3
6.31 c                   dow       not %eof(ltell3)  and
6.31 c                             i < 15
6.31 c                   eval      lbchl1_batc = lebatc
6.31 c     lbchl1_key    chain     lbchl1
6.31 c                   if        %found(lbchl1) and
6.31 c                             lcsper = 0
6.31 c                   eval      i = i + 1
6.31 c                   eval      a_txt(i)  = 'Batch : ' +
6.31 c                                      %trim(lebatc)
6.31 c                   endif
6.31 c     ltell3_key    reade     ltell3
6.31 c                   enddo
6.31  *flytter fra matrise til bilde
6.31 c                   if        i > 0
6.31 c                   eval      n1tx01 = a_txt(1)
6.31 c                   eval      n1tx02 = a_txt(2)
6.31 c                   eval      n1tx03 = a_txt(3)
6.31 c                   eval      n1tx04 = a_txt(4)
6.31 c                   eval      n1tx05 = a_txt(5)
6.31 c                   eval      n1tx06 = a_txt(6)
6.31 c                   eval      n1tx07 = a_txt(7)
6.31 c                   eval      n1tx08 = a_txt(8)
6.31 c                   eval      n1tx09 = a_txt(9)
6.31 c                   eval      n1tx10 = a_txt(10)
6.31 c                   eval      n1tx11 = a_txt(11)
6.31 c                   eval      n1tx12 = a_txt(12)
6.31 c                   eval      n1tx13 = a_txt(13)
6.31 c                   exfmt     n1win
6.31 c                   eval      *in37  = *on
6.31 c                   eval      b_feil = *on
6.31 c                   leavesr
6.31 c                   endif
6.31  *
6.30 c                   eval      lbdtl3_vare = p_vare
6.30 c     lbdtl3_key    setll     lbdtl3
6.30 c     lbdtl3_key    reade     lbdtl3
6.30 c                   dow       not %eof(lbdtl3)
6.30 c                   eval      lbchl1_batc = lvbatc
6.30 c     lbchl1_key    chain     lbchl1
6.30 c                   if        %found(lbchl1) and
6.30 c                             lcsper = 0
6.30 c                   eval      *in37  = *on
6.30 c                   eval      b_feil = *on
6.30 c                   goto      end_sjekk
6.30 c                   endif
6.30 c     lbdtl3_key    reade     lbdtl3
6.30 c                   enddo
      *
      *
      * Gammel enhet må oppgis
      *
     c                   if        b1enhg = *blank
     c                   eval      *in31  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * Gammel enhet må finnes på varen
      *
     c                   eval      vvenl1_enhe = b1enhg
     c     vvenl1_key    chain     vvenl1
     c                   if        not %found
     c                   eval      *in32  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * Ny enhet må oppgis
      *
     c                   if        b1enhn = *blank
     c                   eval      *in33  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * Ny enhet må ikke finnes på varen fra før
      *
     c                   eval      vvenl1_enhe = b1enhn
     c     vvenl1_key    chain     vvenl1
     c                   if        %found
     c                   eval      *in34  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * Ny enhet må være en gyldig enhet
      *
     c                   eval      venhl1_eenh = b1enhn
     c     venhl1_key    chain     venhl1
     c                   if        not %found
     c                   eval      *in35  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * Gammel og ny enhet skal ikke være like
      *
     c                   if        b1enhn = b1enhg
     c                   eval      *in36  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
      *
      * Key for oppslag på vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
     c                   kfld                    vvenl1_enhe
      *
      * Key for oppslag på enhet
      *
     c     venhl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    venhl1_eenh
6.30  *
6.30  * Key for les av telling
6.30  *
6.30 c     ltell3_key    klist
6.30 c                   kfld                    w_firm
6.30 c                   kfld                    ltell3_vare
6.30  *
6.30  * Key for les av telling
6.30  *
6.30 c     lbdtl3_key    klist
6.30 c                   kfld                    w_firm
6.30 c                   kfld                    lbdtl3_vare
6.30  *
6.30  * Key for les av batch
6.30  *
6.30 c     lbchl1_key    klist
6.30 c                   kfld                    w_firm
6.30 c                   kfld                    lbchl1_batc
      *
      *
      * Henter firma og vare fra parmeter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_vare = p_vare
      *
     c                   endsr
