     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VV620R
      * Beskrivelse . . : Vare, utskrift for scanning - parameter
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       260898 HKO  Nytt program
      *       230101 HKO  Lagt inn membernavn ved kall til utskriftsprogram
6.20  *       121110 BOF  Utvidet med lager på sortiment
6.21  *       081214 NHO  Utvidet med lager ved call til VC500R
7.xx  * 7.00  010416 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvshel1    if   e           k disk    rename(vshepfr:vshel1r)
      *
     fvv620d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Datastruktur for parameterliste -ut
      *
     d                 ds
     d d_list                        32
     d  d_firm                        3  0 overlay(d_list)
     d  d_vsor                       10    overlay(d_list:4)
6.20 d  d_lage                        2  0 overlay(d_list:14)
      *
      * Definering av variabler i nøkler
      *
     d vshel1_vsor     s                   like(vcvsor)
6.20 d vshel1_lage     s                   like(vclage)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
     d w_tmst          s               z
     d w_tmst_20       s             20
     d w_memb          s             10
6.20 d w_stat          s              1
      *
     d w_firm          s                   like(vcfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     b1taga        tag
     c                   exfmt     b1bld
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b1taga
     c                   endsl
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1taga
     c                   endif
      *
      * Kaller utskriftsprogram
      *
     c                   eval      d_firm = w_firm
     c                   eval      d_vsor = b1vsor
6.20 c                   eval      d_lage = b1lage
      *
     c                   time                    w_tmst
     c     *iso0         move      w_tmst        w_tmst_20
      *
     c                   eval      w_memb = 'V' + %subst(w_tmst_20:9:9)
      *
     c                   call      'VV620C'
     c                   parm                    d_list
     c                   parm                    w_memb
      *
     c                   goto      b1taga
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av spørringer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Varesortiment
      *
     c                   if        w_afld = 'B1VSOR'
     c                   call      'VC500R'
     c                   parm                    w_firm
     c                   parm                    b1vsor
6.21 c                   parm                    b1lage
     c                   parm                    b1besk
     c                   goto      end_spØrr
     c                   endif
6.10  *
6.10  * Varesortiment
6.10  *
6.10 c                   if        w_afld = 'B1LAGE'
6.10 c                   call      'RA510R'
6.10 c                   parm                    w_firm
6.10 c                   parm                    b1lage
6.10 c                   parm                    b1ltxt
6.10 c                   goto      end_spØrr
6.10 c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
     c                   eval      b1besk = *blank
      *
      * Varesortiment
      *
     c                   if        b1vsor = *blank
     c                   eval      *in31  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c                   eval      vshel1_vsor = b1vsor
6.20 c                   eval      vshel1_lage = b1lage
     c     vshel1_key    chain     vshel1                             90
     c                   if        *in90
     c                   eval      *in32  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      b1besk = vcbesk
6.10  *
6.10  * Varesortiment
6.10  *
6.10 c                   if        b1lage <>0
6.10 c                   call      'RS710R '
6.10 c                   parm                    w_stat
6.10 c                   parm                    b1lage
6.10 c                   parm                    b1ltxt
6.10 c                   if        w_stat <> *blank
6.10 c                   eval      *in33  = *on
6.10 c                   eval      b_feil = *on
6.10 c                   goto      end_sjekk
6.10 c                   endif
6.10 c                   endif
      *
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på varesortiment-hode
      *
     c     vshel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vshel1_vsor
6.20 c                   kfld                    vshel1_lage
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
