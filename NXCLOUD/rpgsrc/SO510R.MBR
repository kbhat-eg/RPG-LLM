      /TITLE  ASOFAK Faktura, vise faktura
     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : SO510R
      * Beskrivelse . . : Faktura, vise faktura
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       220699 HKO  Nytt program
3.31  * 3.31  291100 AMD  Lagt inn kontroll på *loval før flytting
3.32  * 3.32  200601 AMD  Lagt inn valg for generering av ordre fra
3.32  *                   historisk faktura-register.
5.20  * 5.20  311003 AMD  Lagt inn selger og nettbutikk-referanse
5.30  * 5.30  131104 KOB  Lagt inn kontroll mot kundenr.
5.31  * 5.31  100205 GRO  Lagt inn fakturatype
5.51  * 5.51  040107 AMD  Lagt inn visning av leveringstype
5.52  * 5.52  140307 KOB  Lagt inn visning av kid/kid referanse
5.60  * R5.60 250408 KOB  Lagt inn ny funksjon for å vise arkivpost
pdf   * R6.10     041010  Henter fra ASP arkiv. Mulighet for valg mellom BSA  *
pdf   *                   ASP og Multiarkiv gjøres i AP622C. Det finnes  BSA  *
pdf   *                   to varianter av dette programmet.              BSA  *
6.20  * R6.20     210711  Legger ut firmanummer i skjultfelt for         bsa  *
6.20  *                   spørring mot ASP-arkiv fra Jacada              bsa  *
6.21  * R6.20     040113  Endret metode for oppslag mot arkiv            bsa  *
6.22  * R6.20 060314 nho  Sjekker om post finnes et år tilbake                *
6.nu  * R6.30 270514 bsa  Programmet gjennomgått mtp nummerutvidelser.
6.23  * R6.30 290814 bkv  Vis ordrehode
6.30  * R6.30 081015 bsa  Ikke beregn beløpet basert på avrundet rabatt
6.31  * R6.30 020317 nho  Siste siffer og minus beløp vises ikke
6.31  *                   Endret div posisjoner
      *
7.xx  * 7.00  010216 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 7.00  070319 bkv  Rettet feil hvor - ikke ble vist
7.02  *       170220 nho  Ligger 2019 på transaksjonen... men i SOHELU ligger
7.02  *                   2020, lagt inn ny test
      *
8.01  *K0000  030123 bsa  Endret ledetekst i skjermbilde fra "Nettbut. ref"
8.01  *K0000              til "Eksternreferanse", siden det er mange systemer
8.01  *K0000              som skriver hit. (Bla PDA, POS, Smartkalk, NGN)
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsohel1    if   e           k disk    rename(sohepfr:sohel1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fsodtl1    if   e           k disk    rename(sodtpfr:sodtl1r)
     fvpkol1    if   e           k disk    rename(vpkopfr:vpkol1r)
3.32 fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
5.31 ffnufl1    if   e           k disk    rename(fnufpfr:fnufl1r)
5.31 ffmftl1    if   e           k disk    rename(fmftpfr:fmftl1r)
5.31 ffntrl1    if   e           k disk    rename(fntrpfr:fntrl1r)
      *
     fso510d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av datastrukturer
      *
      *  Datastruktur for detaljlinje
      *
     d                 ds
7.01 d d_text                        68
7.01 d  d_vare                        8    overlay(d_text)
7.01 d* d_tek1                       28    overlay(d_text:12)
7.01 d  d_tek1                       25    overlay(d_text:10)
7.01 d* d_anta                       13    overlay(d_text:37)
7.01 d  d_anta                       13    overlay(d_text:32)
7.01 d* d_enh1                        3    overlay(d_text:50)
7.01 d  d_enh1                        3    overlay(d_text:45)
7.01 d* d_raba                        6    overlay(d_text:53)
7.01 d  d_raba                        6    overlay(d_text:48)
7.01 d* d_salg                       13    overlay(d_text:58)
7.01 d  d_salg                       13    overlay(d_text:53)
      *
7.01 d*                 ds
7.01 d* d_text                        70
7.01 d*  d_lety                        1    overlay(d_text)
7.01 d*  d_vare                        8    overlay(d_text:3)
7.01 d** d_tek1                       28    overlay(d_text:12)
7.01 d*  d_tek1                       26    overlay(d_text:12)
7.01 d** d_anta                       13    overlay(d_text:37)
7.01 d*  d_anta                       13    overlay(d_text:35)
7.01 d** d_enh1                        3    overlay(d_text:50)
7.01 d*  d_enh1                        3    overlay(d_text:48)
7.01 d** d_raba                        6    overlay(d_text:53)
7.01 d*  d_raba                        6    overlay(d_text:51)
7.01 d** d_salg                       13    overlay(d_text:58)
7.01 d*  d_salg                       13    overlay(d_text:56)
7.01  *
7.01 d* d_text                        70
7.01 d*  d_lety                        1    overlay(d_text)
7.01 d*  c_sep0                        1    overlay(d_text:2)  inz(x'20')
7.01 d*  d_vare                        8    overlay(d_text:3)
7.01 d*  c_sep1                        1    overlay(d_text:11)  inz(x'20')
7.01 d*  d_tek1                       21    overlay(d_text:12)
7.01 d*  c_sep2                        1    overlay(d_text:33)  inz(x'20')
7.01 d*  d_anta                       11    overlay(d_text:34)
7.01 d*  c_sep3                        1    overlay(d_text:46)  inz(x'20')
7.01 d*  d_enh1                        3    overlay(d_text:47)
7.01 d*  c_sep4                        1    overlay(d_text:50)  inz(x'20')
7.01 d*  d_raba                        5    overlay(d_text:51)
7.01 d*  c_sep5                        1    overlay(d_text:55)  inz(x'20')
7.01 d*  d_salg                       12    overlay(d_text:56)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(sofirm)
     d p_aarr          s                   like(soaarr)
5.30 d p_kund          s                   like(sokund)
6.nu d p_binr          s                   like(sobinr)
      *
      * Definering av variabler i nøkler
      *
     d rkunl1_kund     s                   like(rkkund)
     d vpkol1_pkod     s                   like(vapkod)
3.32 d votyl1_otyp     s                   like(vaotyp)
5.31 d fnufl1_aarr     s                   like(fnuaar)
5.31 d fnufl1_binr     s                   like(fnubin)
5.31 d fmftl1_faty     s                   like(fmtfat)
5.31 d fntrl1_numm     s                   like(fnnumm)
5.31 d fntrl1_suff     s                   like(fnsuff)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
6.22 d w_tell          s              1  0
      *
     d w_dato          s               d   datfmt(*iso)
     d w_raba          s              5  2
7.xx d w_sumb          s                   like(sdsalg)
     d w_csts          s              1
     d w_ctx2          s             20
      *
     d w_firm          s                   like(sofirm)
     d w_aarr          s                   like(soaarr)
     d w_kund          s                   like(sokund)
6.nu d w_binr          s                   like(sobinr)
5.20  *
5.20 d w_retk          s              1
5.20 d w_itxt          s             20
5.60  *
5.60  * Definering av parametre MultiArcive
5.60  *
6.21 d w_type          s             50
pdf  d w_modu          s             10    inz('PROA    ')
5.60 d*w_modu*pdf      s             10    inz('MSA     ')
5.60 d w_disp          s              1    inz('1')
5.60 d w_tilg          s              1
5.60 d w_pgrp          s              2
5.60 d w_aara          s              4
5.60 d w_bila          s              6
5.60 d w_afir          s              3
6.21 d w_refn          s             50
      *
      * Definering av konstanter
      *
7.xx d c_sfil          s              3  0 inz(8)
7.01 d space           s              1    inz(x'20')
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Fyller control-bildet
      *
     c                   exsr      control_init
      *
      * Fyller subfile
      *
     c     sodtl1_key    setll     sodtl1
     c                   exsr      crt_subfile
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      dsp_subfile
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
7.xx c*                  when      *in21
7.xx c*                  eval      *in22 = *on
7.xx c*                  goto      b2taga
     c                   endsl
3.32  *
3.32  * F15=Generere ordre utfra historisk faktura-register
3.32  *
3.32 c                   if        *inkp = *on
3.32 c                   exsr      xh1win
3.32 c                   goto      b2taga
3.32 c                   endif
      *
      * F20=Utskrift av fakturakopi
      *
     c                   if        *inku = *on
     c                   call      'FO681R'
     c                   parm                    w_firm
     c                   parm                    soaarr
     c                   parm                    sobinr
     c                   parm                    sootyp
     c                   goto      b2taga
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
3.32  *
3.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
3.32  * xh1win  Bilde for generering av ordre fra historisk fakt.reg.
3.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
3.32  *
3.32 c     xh1win        begsr
3.32  *
3.32 c     h1taga        tag
3.32  *
3.32 c                   write     h1win
3.32 c                   exfmt     h1win
3.32  *
3.32  * Behandling av funksjonstaster
3.32  *
3.32 c                   select
3.32 c                   when      *inkc
3.32 c                   goto      end_h1win
3.32 c                   when      *inkd
3.32 c                   exsr      spØrring
3.32 c                   goto      h1taga
3.32 c                   endsl
3.32  *
3.32  * Ordretype må være oppgitt
3.32  *
3.32 c                   if        h1otyp = *blank
3.32 c                   eval      *in31  = *on
3.32 c                   goto      h1taga
3.32 c                   endif
3.32  *
3.32  * Ordretype må være gyldig
3.32  *
3.32 c                   eval      votyl1_otyp = h1otyp
3.32 c     votyl1_key    chain     votyl1r                            92
3.32 c                   if        *in92  = *on
3.32 c                   eval      *in32  = *on
3.32 c                   goto      h1taga
3.32 c                   endif
3.32  *
3.32  * Systemkode må være 0 på ordretypen
3.32  *
3.32 c                   if        vaosys <> 0
3.32 c                   eval      *in33  = *on
3.32 c                   goto      h1taga
3.32 c                   endif
3.32  *
3.32  * Generer ordre
3.32  *
3.32 c                   call      'SO700R'
3.32 c                   parm                    w_firm
3.32 c                   parm                    soaarr
3.32 c                   parm                    sobinr
3.32 c                   parm                    h1otyp
3.32  *
3.32 c     end_h1win     endsr
3.32  *
3.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
3.32  * Subrutine for behandling av spørring
3.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
3.32  *
3.32 c     spØrring      begsr
3.32  *
3.32  * Ordre-type
3.32  *
3.32 c                   if        w_afld = 'H1OTYP'
3.32 c                   call      'VA550R'
3.32 c                   parm                    w_firm
3.32 c                   parm                    h1otyp
3.32 c                   goto      end_spØrr
3.32 c                   endif
3.32  *
3.32 c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å initierer/fylle opp control-bildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     control_init  begsr
      *
      * Finner siste faktura-hode for faktura
      *
6.22 c     nyles         tag
6.22  *
     c     sohel1_key    setll     sohel1
     c     sohel1_key    reade     sohel1                                 90
6.22  * Sjekker om post finnes på fjoråret
6.22 c                   if        *in90 = *on and
6.22 c                             w_tell = 0
6.22 c                   add       1             w_tell
6.22 c     w_aarr        sub       1             w_aarr
6.22 c                   goto      nyles
6.22 c                   endif
6.22  *
7.02  * Sjekker om post finnes på + 2 år
7.02 c                   if        *in90 = *on and w_tell = 1
7.02 c                   add       1             w_tell
7.02 c     w_aarr        add       2             w_aarr
7.02 c                   goto      nyles
7.02 c                   endif
7.02  *
      *
     c                   if        *in90 = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   dow       *in90 = *off
     c     sohel1_key    reade     sohel1                                 90
     c                   enddo
5.30  *
5.30  * Kontroller om det er riktig kunde
5.30  *
5.30 c                   if        sokund <> p_kund and
5.30 c                             p_kund <> 0
5.30 c                   goto      avslutt
5.30 c                   endif
      *
     c                   eval      b2kund = sokund
     c                   eval      b2navn = sonavn
     c                   eval      b2adr1 = soadr1
     c                   eval      b2sted = sosted
     c                   eval      b2binr = sobinr
     c                   eval      b2lmtx = solmtx
     c                   eval      b2fsum = sofsum
5.20 c                   eval      b2selg = soselg
5.20 c                   eval      b2nref = sonref
5.52 c                   eval      b2kidd = sokidd
5.52 c                   eval      b2kidr = sokidr
5.31  *
5.31  * Hent fakturatype hvis fakt.nr på utgående fakturalogg
5.31  *
5.31 c                   eval      b2faty = *blank
5.31 c                   eval      fnufl1_aarr = soaarr
5.31 c                   eval      fnufl1_binr = sobinr
5.31 c     fnufl1_key    chain     fnufl1
5.31 c                   if        %found
5.31 c                   eval      fmftl1_faty = fnufat
5.31 c     fmftl1_key    chain     fmftl1
5.31 c                   if        %found
5.31 c                   eval      b2faty = '* ' + %trim(fmtbes) + ' *'
5.31 c                   endif
5.31 c                   endif
5.31  *
5.31 c                   if        b2faty = *blank
5.31 c                   eval      fntrl1_numm = sonumm
5.31 c                   eval      fntrl1_suff = sosuff
5.31 c     fntrl1_key    chain     fntrl1
5.31 c                   if        %found
5.31 c                   if        soaarr = fnaarr
5.31 c                   if        sobinr = fnbinr
5.31 c                   eval      b2faty = '* Kort/purchasing *'
5.31 c                   endif
5.31 c                   endif
5.31 c                   endif
5.31 c                   endif
5.20  *
5.20  * Hent tekst for selger
5.20  *
5.20 c                   call      'RS709R'
5.20 c                   parm                    w_retk
5.20 c                   parm                    soselg
5.20 c                   parm                    w_itxt
5.20 c                   movel     w_itxt        b2snav
      *
3.31 c                   if        sofkda <> *loval
     c     *dmy          move      sofkda        b2fkda
3.31 c                   endif
3.31 c                   if        soffda <> *loval
     c     *dmy          move      soffda        b2ffda
3.31 c                   endif
      *
      * Henter kundenavn
      *
     c                   if        sokund <> 0
     c                   eval      rkunl1_kund = sokund
     c     rkunl1_key    chain     rkunl1                             90
     c                   if        *in90 = *off
     c                   eval      b2knav = rknavn
     c                   eval      b2kgat = rkgate
     c                   eval      b2kste = rksted
     c                   endif
     c                   endif
      *
      * Hente inn betalingsbetingelser
      *
     c                   call      'FÅ703R'
     c                   parm                    w_firm
     c                   parm                    sobetb
     c                   parm                    w_dato
     c                   parm                    w_csts
     c                   parm                    w_dato
     c                   parm                    b2ctxt
     c                   parm                    w_ctx2
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c     sodtl1_key    setll     sodtl1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   call      'SO511R'
     c                   parm                    w_firm
     c                   parm                    w_aarr
6.nu c                   parm                    w_binr
6.nu c                   parm                    b1numm
     c                   parm                    b1suff
     c                   parm                    b1ktxt
     c                   parm                    b1line
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
6.23  *
6.23  * Vis ordrehode
6.23  *
6.23 c                   if        b1valg = 8
6.23 c                   call      'SO515R'
6.23 c                   parm                    w_firm
6.23 c                   parm                    w_aarr
6.23 c                   parm                    w_binr
6.23 c                   parm                    b1numm
6.23 c                   parm                    b1suff
6.23 c                   eval      b1valg = 0
6.23 c                   update    b1sfl
6.23 c                   iter
6.23 c                   endif
5.60  *
5.60  * Vis arkiv
5.60  *
5.60 c                   if        b1valg = 9
5.60  *
5.60  * Kontrollerer gyldig tilgang
5.60  *
5.60 c                   call      'AX700R'
5.60 c                   parm                    w_modu
5.60 c                   parm                    w_disp
5.60 c                   parm                    w_tilg
5.60  *
5.60 c                   if        w_tilg = '0'
5.60  *
pdf  c                   eval      w_type = 'PAKKSEDDEL'
5.60 c                   move      w_firm        w_afir
5.60 c                   eval      w_pgrp = '20'
5.60 c                   eval      w_aara = %char(w_aarr)
5.60 c                   eval      w_bila = %char(b1numm)
6.21 c                   eval      w_refn = *blank
6.21 c                   eval      w_refn = %char(w_aarr) + %trim(%char(b1numm))
5.60  *
pdf  c                   call      'AP622C'
pdf  c                   parm                    w_type
6.21 c                   parm                    w_refn
6.21 c**                 parm                    w_aara
6.21 c**                 parm                    w_bila
6.21 c**                 parm                    w_pgrp
pdf  c**                 call      'AH720C'
pdf  c**                 parm                    w_pgrp
pdf  c**                 parm                    w_afir
pdf  c**                 parm                    w_aara
pdf  c**                 parm                    w_bila
5.60  *
5.60 c                   endif
5.60 c                   eval      b1valg = 0
5.60 c                   update    b1sfl
5.60 c                   iter
5.60 c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c     sodtl1_key    reade     sodtl1                                 15
      *
     c                   if        *in15
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1numm = sdnumm
     c                   eval      b1suff = sdsuff
     c                   eval      b1ktxt = sdktxt
     c                   eval      b1line = sdline
     c                   eval      b1text = *blank
      *
      * Tekstlinje
      *
     c                   if        sdktxt = 1 or
     c                             sdktxt = 5
     c                   eval      b1text = sdtek1 + sdtek2
7.xx c                   eval      b1tex2 = %trimr(sdtek1) + %trimr(sdtek2)
7.01 c                   elseif    sdltyp = 'TX'
7.01 c                   eval      b1text = sdtek1 + sdtek2
7.01  *
      * Varelinje
      *
     c                   else
      *
     c                   eval(h)   w_raba = sdrab1 +
     c                                      ((100 - sdrab1) * sdrab2 / 100)
6.30 c**                 eval(h)   sdsalg = sdsalg - (sdsalg * w_raba / 100)
6.30 c                   eval(h)   sdsalg = sdsalg - sdrkr1 - sdrkr2
      *
     c                   eval      d_anta = %editc(sdanta : 'M')
     c                   eval      d_salg = %editc(sdsalg : 'M')
      *
7.01 c*                   if        sdlety = 0
7.01 c**                   eval      d_lety = '.'
7.01 c*                   eval      d_lety = x'20' + x'20'
7.01 c*                   eval      d_lety = space + space
7.01 c*                   else
7.01 c*                   eval      d_lety = %editc(sdlety : 'Z' )
7.01 c*                   endif
     c                   eval      d_vare = sdvare
     c                   eval      d_tek1 = sdtek1
     c                   eval      d_enh1 = sdenh1
     c                   eval      d_raba = *blank
7.xx c                   eval      b1raba = *blank
      *
     c                   if        w_raba <> 0
     c                   eval      d_raba = %editc(w_raba : '4')
7.xx c                   eval      b1raba = %editc(w_raba : '4')
     c                   endif
      *
     c                   if        w_raba = 0
     c                   eval      vpkol1_pkod = sdkpri
     c     vpkol1_key    chain     vpkol1                             91
     c                   if        *in91 = *off
     c                   move      vaptxt        d_raba
7.xx c                   move      vaptxt        b1raba
     c                   endif
     c                   endif
      *
     c                   eval      b1text = d_text
      *
7.01 c*                   eval      b1lety = %editc(sdlety : 'Z' )
7.01 c                   eval      b1lety = sdlety
7.xx c                   eval      b1vare = sdvare
7.xx c                   eval      b1enhe = sdenh1
7.xx c                   eval      b1anta = %editc(sdanta : 'M')
7.xx c                   eval      b1sumn = %editc(sdsalg : 'M')
7.xx c                   eval      w_sumb = sdsalg + (sdsalg*(sdmpro/100))
7.xx c                   eval      b1sumb = %editc(w_sumb : 'M')
7.xx c                   eval      b1sumk = %editc(sdkost : 'M')

7.xx c                   if        sdtek2 = *blank
7.xx c                   eval      b1tex2=%trimr(sdtek1)
7.xx c                   else
7.xx c                   eval      b1tex2=%trimr(sdtek1)+' : '+%trimr(sdtek2)
7.xx c                   endif
      *
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_aarr
5.30 c                   parm                    p_kund
6.nu c                   parm                    p_binr
      *
      * Key for oppslag på faktura-hode-register
      *
     c     sohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_aarr
     c                   kfld                    w_binr
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for les på faktura-detalj-register
      *
     c     sodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_aarr
     c                   kfld                    w_binr
      *
      * Key for oppslag på priskode-register
      *
     c     vpkol1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vpkol1_pkod
3.32  *
3.32  * Key for ordretyperegister
3.32  *
3.32 c     votyl1_key    klist
3.32 c                   kfld                    w_firm
3.32 c                   kfld                    votyl1_otyp
5.31  *
5.31  * Key for les på fakturalogg
5.31  *
5.31 c     fnufl1_key    klist
5.31 c                   kfld                    w_firm
5.31 c                   kfld                    fnufl1_aarr
5.31 c                   kfld                    fnufl1_binr
5.31  *
5.31  * Key for les av fakturatype
5.31  *
5.31 c     fmftl1_key    klist
5.31 c                   kfld                    w_firm
5.31 c                   kfld                    fmftl1_faty
5.31  *
5.31  * Key for les av korttrans.
5.31  *
5.31 c     fntrl1_key    klist
5.31 c                   kfld                    w_firm
5.31 c                   kfld                    fntrl1_numm
5.31 c                   kfld                    fntrl1_suff
      *
      *
      * Henter parameter-verdier
      *
     c                   eval      w_firm = p_firm
6.20 c                   eval      b2firm = w_firm
     c                   eval      w_aarr = p_aarr
5.30 c                   eval      w_kund = p_kund
6.nu c                   eval      w_binr = p_binr
      *
     c                   eval      *in22 = *on
      *
     c                   endsr
