      /title  ASOFAK Lev.vare, henter vareregister - Farvemiljø Nord
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FW709R
      * Beskrivelse . . : Danning av leverandør vareregister, Farvemiljø
      *                   Nord
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       060699 HKO  Nytt program
      *       290200 HKO  Fjernet avrunding ved uttrekk av mva
      *       021000 HKO  Tilpasset nytt record-format
      *       181207 bof  utvidet feltet d_anta med 3 pos. forskyver resten
      *       260508 bof  Konverterer ÆØÅ                              sten
      * 6.10  230609 BSA  Oppdatert med sporingsinformasjon
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fflvwpf    if   e           k disk
     ffenkl1    if   e           k disk    rename(fenkpfr:fenkl1r)
     fflvapf    o  a e             disk
      *
6.10  * Local data area
6.10  *
6.10 d                uds
6.10 d l_user                911    920
      *
      * Definering av datastruktur
      *
      *  Recordbeskrivelse - Farvemiljø Nord
      *
     d                 ds
     d d_inpu                       256
     d  d_vare                       10    overlay(d_inpu:1)
     d  d_fill1                       4    overlay(d_inpu:11)
     d  d_eann                       13  0 overlay(d_inpu:15)
     d  d_fill2                       1    overlay(d_inpu:28)
     d  d_tek1                       25    overlay(d_inpu:29)
     d  d_anta                        8    overlay(d_inpu:54)
     d  d_fill3                       3    overlay(d_inpu:62)
     d  d_enhe                        3    overlay(d_inpu:65)
     d  d_fill4                       1    overlay(d_inpu:68)
     d  d_inpr                        8    overlay(d_inpu:69)
     d   d_inpr_kr                    5  0 overlay(d_inpr:1)
     d   d_inpr_Øre                   2  2 overlay(d_inpr:7)
     d  d_fill5                       1    overlay(d_inpu:77)
     d  d_sapr                        8    overlay(d_inpu:78)
     d   d_sapr_kr                    5  0 overlay(d_sapr:1)
     d   d_sapr_Øre                   2  2 overlay(d_sapr:7)
     d  d_fill6                       1    overlay(d_inpu:86)
     d  d_vgrp                        2    overlay(d_inpu:87)
     d  d_fill7                       1    overlay(d_inpu:89)
     d  d_antp                        6    overlay(d_inpu:90)
     d  d_fill8                       1    overlay(d_inpu:96)
     d  d_nobb                        8    overlay(d_inpu:97)
     d  d_fill9                       5    overlay(d_inpu:105)
     d  d_nvgr                        4    overlay(d_inpu:110)
     d  d_fill10                      1    overlay(d_inpu:114)
     d  d_rkod                        3    overlay(d_inpu:115)
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_ldor          s              6  0
      *
      * Definering av variable i nøkler
      *
     d fenkl1_enhf     s                   like(feenhf)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_udat          s               d   datfmt(*iso)
     d w_mvap          s              6  2
     d w_mvat          s              5  4
     d w_mvaf          s             10  9
     d w_bmen          s             11
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser input-fil, og legger data ut på leverandør vareregister
      *
     c                   read      flvwpf                                 90
     c                   dow       *in90 = *off
      *
     c                   eval      d_inpu = fwirad
      *
      * Utleder varetekst
      *
     c     X'46':'Æ'     xlate     d_tek1        d_tek1
     c     X'A0':'æ'     xlate     d_tek1        d_tek1
     c     X'77':'Ø'     xlate     d_tek1        d_tek1
     c     X'90':'ø'     xlate     d_tek1        d_tek1
     c     X'2A':'Å'     xlate     d_tek1        d_tek1
     c     X'EF':'å'     xlate     d_tek1        d_tek1
      *
      * Skriver til leverandør vareregister
      *
     c                   eval      fwfirm = p_firm
     c                   eval      fwldor = p_ldor
     c                   eval      fwekod = *blank
     c                   eval      fwtek1 = d_tek1
     c                   eval      fwtek2 = *blank
     c                   eval      fwlvar = d_vare
     c                   eval      fweann = d_eann
     c                   eval      fwnobb = 0
     c                   eval      fwenhe = %trim(d_enhe)
     c                   eval      fwinpr = 0
     c                   eval      fwsapr = 0
     c                   eval      fwdato = *loval
     c                   eval      fwbmen = 0
     c                   eval      fwhkod = 1
      *
     c                   eval      fwline = fwline + 1
      *
     c                   if        d_anta <> *blank
     c                   eval      fwtek1 = %trim(%trim(fwtek1) + ' ' +
     c                                      d_anta)
     c                   endif
      *
     c                   if        d_nobb <> *blank
     c                   move      d_nobb        fwnobb
     c                   endif
      *
     c                   eval      fenkl1_enhf = %trim(d_enhe)
     c     fenkl1_key    chain     fenkl1                             91
     c                   if        *in91 = *off
     c                   eval      fwenhe = feenht
     c                   endif
      *
     c                   eval      fwinpr = d_inpr_kr + d_inpr_Øre
      *
     c                   eval      fwsapr = d_sapr_kr + d_sapr_Øre
     c                   eval      fwsapr = fwsapr * w_mvaf
      *
     c                   time                    fwdato
      *
     c                   if        d_antp <> *blank
     c                   eval      w_bmen = %trim('0000' +
     c                                            %subst(d_antp:1:4) +
     c                                            %subst(d_antp:6:1) + '00')
     c                   move      w_bmen        fwbmen
     c                   endif
      *
6.10 c                   time                    fwodat
6.10 c                   time                    fwotim
6.10 c                   eval      fwousr = l_user
6.10 c                   time                    fwedat
6.10 c                   time                    fwetim
6.10 c                   eval      fweusr = l_user
     c                   write     flvapfr
      *
     c                   read      flvwpf                                 90
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ldor
      *
      * Key for oppslag på enhet-konvertering
      *
     c     fenkl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fenkl1_enhf
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
      * Henter dagens dato
      *
     c                   time                    w_udat
      *
      * Henter mva-faktor
      *
     c                   call      'FÅ705R'
     c                   parm                    w_firm
     c                   parm                    w_udat
     c                   parm                    w_mvap
     c                   parm                    w_mvat
     c                   parm                    w_mvaf
      *
     c                   endsr
