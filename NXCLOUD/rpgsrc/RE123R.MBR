     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
      *                                                                       *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RE123R                                              *
      * Beskrivelse . . : Tilrettelegger trans fra Q-FREE                     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
6.20  * v6.20     220212  Endret pga alfanumerisk prosjeknummer               *
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     fre0ipf    ip  ae           k disk
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     fraa3l1    if   e           k disk    rename(raa3pfr:raa3l1r)
     fraa4l1    if   e           k disk    rename(raa4pfr:raa4l1r)
     fre0tpf    o    e           k disk
      *
     d per             s              2  0 dim(13)                              Perioder
     d txt             s             20    dim(5) ctdata perrcd(1)
      *
     d                 ds
     d rpcin1                       256                                         Input
6.NU d  wdsbil                        8  0 overlay(rpcin1)                      Bilags
6.NU d  wdsbnr                        4  0 overlay(rpcin1:9)                    Billet
6.NU d  wdsbse                        4  0 overlay(rpcin1:13)                   Sessio
6.NU d  wdsbss                        4  0 overlay(rpcin1:17)                   Ukjent
6.NU d  wdsbda                        6  0 overlay(rpcin1:21)                   Dato (
6.NU d  wdsrun                        6  0 overlay(rpcin1:27)                   Utsted
6.NU d  wdsrty                        2  0 overlay(rpcin1:33)                   Rutety
6.NU d  wdsrnr                        4  0 overlay(rpcin1:35)                   Rute n
6.NU d  wdsfnr                        4  0 overlay(rpcin1:39)                   Ferge/
6.NU d  wdskto                        6  0 overlay(rpcin1:43)                   Konton
6.NU d  wdsbel                        9  2 overlay(rpcin1:49)                   Beløp
6.NU d  wdsftg                        1    overlay(rpcin1:58)                   Forteg
6.NU d  wdsrkt                        6  0 overlay(rpcin1:59)                   Operat
     d rpcin2                       256                                         Input
     d rpcin3                       256                                         Input
      *
      * LOCAL DATA
      *
     d                uds
     d pfirm                 300    302  0                                      Firma
     d pnavn                         30                                         Navn
     d puser                         10                                         BruerI
     d pdato                          6  0                                      Pr. da
     d praar                          4  0                                      Pr. år
     d pperf                          2  0                                      Period
     d ppert                          2  0                                      Period
     d psyst                 471    472  0                                      System
      *
     d l_user                911    920
     d l_firm                944    946  0
     d l_navn                951    980
      *
     d pdatx           s              8  0
     d wfelt           s                   like(rpcin1)
     d w_raar          s              4  0
     d wkp             s              2  0
     d wkper           s              8  0
     d wlinje          s              8  0
6.NU d wnota           s              8  0
     d wtpre           s             10  2
     d x               s              3  0
     d y               s              3  0
     d w_bunt          s                   like(rtbunt)
     d w_firm          s                   like(l_firm)
      *
     d raa4l1_4aar     s                   like(ra4aar)
      *
      *  Statusopplysninger - avvikende perioder
      *
     iraa3l1r
     i              ra3p01                      per(01)
     i              ra3p02                      per(02)
     i              ra3p03                      per(03)
     i              ra3p04                      per(04)
     i              ra3p05                      per(05)
     i              ra3p06                      per(06)
     i              ra3p07                      per(07)
     i              ra3p08                      per(08)
     i              ra3p09                      per(09)
     i              ra3p10                      per(10)
     i              ra3p11                      per(11)
     i              ra3p12                      per(12)
     i              ra3p13                      per(13)
      *
B001 c                   if        *in15 = *off
 001 c                   eval      rtraar = praar                               ÅR på trans
 001 c                   eval      rtrper = ppert                               ÅR på trans
 001 c                   move      *on           *in15
E001 c                   endif
      *
     c     start         tag
      *
     c                   eval      rtbiln = wdsbil                              Bilag-nr.
      *
     c     *dmy          move      wdsbda        rtbdat                         Bilag-dato
      *
     c                   eval      rtbilk = 01                                  Bilagskode
      *
     c                   move      txt(1)        rttext                         Bilagstekst
      *
     c                   eval      rtbelØ = wdsbel                              Bruttobeløp
      *
B001 c                   if        wdsftg = '+'                                 +/-
 001 c                   eval      rtdavd = wdsfnr                              Debet-avd.
 001 c                   eval      rtdkto = wdskto                              Debet-kto.
X001 c                   else
 001 c                   eval      rtcavd = wdsfnr                              Kredit-avd.
 001 c                   eval      rtckto = wdskto                              Kredit-kto.
E001 c                   endif
      *
6.20 c**                 eval      rtpros = wdsfnr                              Prosjekt-nr.
6.20 c                   movel     wdsfnr        rtpros                         Prosjekt-nr.
     c                   move      wdsrnr        rtakti                         Aktivitet
      *
     c                   eval      rtfdat = *loval                              Forfallsdato
      *
      * Legg ut postering
      *
     c                   add       rtbelØ        wtpre
     c                   add       1             wlinje                         Linjenr.
     c                   eval      rtreid = 'T'                                 Rec.-id
     c                   eval      rtfirm = l_firm                              Firma
     c                   eval      rtbunt = w_bunt                              Bunt-nr.
     c                   eval      rtlinj = wlinje                              Bunt-nr.
     c                   eval      rtdsps = *zero                               Debet-spes.
     c                   eval      rtdmva = '3'                                 Debet-mva.
     c                   eval      rtcsps = *zero                               Kredit-spes.
     c                   eval      rtcmva = '3'                                 Kredit-mva.
     c                   eval      rtrefn = *zero                               Referanse
     c                   eval      rtkrab = *zero                               Rabatt
     c                   eval      rtvalk = *blank                              Valuta-kode
     c                   eval      rtvalb = *zero                               Valuta-beløp
     c                   eval      rtmngd = *zero                               Mengde
     c                   eval      rtmngk = *zero                               Mengde-kode
     c                   eval      rtresn = *zero                               Reskontronr.
     c                   eval      rtvirk = *blank                              Virksomhet
     c                   eval      rtatte = *zero                               Attestert
     c                   eval      rtautx = *blank                              Avkryssing
     c                   eval      rtkrys = *blank                              Avkryssing
     c                   eval      rtkidi = *blank                              KID-referanse
     c                   eval      rtbetb = *zero                               Betalingbet.
     c                   eval      rtbref = *blank                              Bank-referanse
      *
     c                   write     re0tpfr                                      Skriv post
      *
     c     slutt         tag
     c*
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c                   eval      w_firm = l_firm                              Firma
      *
     c                   eval      wtpre = *zeros
     c                   eval      wkp = *zeros
     c                   eval      x = 0
      *
     c                   z-add     1             w_bunt
     c     uyear         sub       1             w_raar
      *
     c     afirl1_key    chain     afirl1                             10
      *
     c     raa3l1_key    chain     raa3l1                             10         Avvikende perioder
      *
     c                   eval      raa4l1_4aar = *year
     c     raa4l1_key    chain     raa4l1                             10
      *
B001 c                   if        %found
 001 c                   z-add     ra4sgb        wnota
E001 c                   else
 001 c                   z-add     0             wnota
E001 c                   endif
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
     c     raa3l1_key    klist
     c                   kfld                    w_firm
      *
     c     raa4l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    raa4l1_4aar
      *
     c                   endsr
      * ----------------------------------------
**   Bilagstekster
Faktura
Kreditnota
Øreavrunding
Gebyr
Utg. MVA
