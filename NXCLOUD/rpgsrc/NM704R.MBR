     h option(*nodebugio) datedit(*dmy)
     h alwnull(*usrctl)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : NM704R
      * Beskrivelse . . : Ehandel MGR, eksport av lager
      *
      * Spesialversjon. : MGR Ehandelsløsning
      * Tilpasset for . : Mestergruppen
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       090316 ASK  Nytt program
9.01  * 9.01  210917 ASK  Sjekker om lager skal utelates fra ehandel
9.02  * 9.02  101117 ASK  Legger ut kjedekode og butikkid på filen
9.03  * 9.03  110220 ASK  Utvidet filen med orgnr. på WHS segmentet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fra91l1    if   e           k disk    rename(ra91pfr:ra91l1r)
     fanbll3    if   e           k disk    rename(anblpfr:anbll3r)
     fanbhl1    if   e           k disk    rename(anbhpfr:anbhl1r)
      *
     fnetlager  uf a e           k disk
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
     d l_fgr                 931    933
      *
      * Definering av datastrukturer
      *
      * Recordbeskrivelse - meldingshode
      *
     d                 ds
     d d_unh                         41
     d  d_unhrcd                      3    overlay(d_unh: 1) inz('UNH')
     d  d_unhfill1                    1    overlay(d_unh: 4) inz(';')
     d  d_unhrefno                   26    overlay(d_unh: 5)
     d  d_unhfill2                    1    overlay(d_unh:31) inz(';')
     d  d_unhtypeid                   6    overlay(d_unh:32) inz('IMLIMP')
     d  d_unhfill3                    1    overlay(d_unh:38) inz(';')
     d  d_unhversion                  3    overlay(d_unh:39) inz('1.0')
      *
      * Recordbeskrivelse - meldingsstart
      *
     d                 ds
     d d_bgm                         20
     d  d_bgmrcd                      3    overlay(d_bgm: 1) inz('BGM')
     d  d_bgmfill1                    1    overlay(d_bgm: 4) inz(';')
     d  d_bgmetypekod                10    overlay(d_bgm: 5) inz('IMLDATAWHS')
     d  d_bgmfill2                    2    overlay(d_bgm:15) inz(';;')
     d  d_bgmfunction                 1    overlay(d_bgm:17) inz('1')
      *
      * Recordbeskrivelse - organisasjon
      *
     d                 ds
     d d_org                        126
     d  d_orgrcd                      3    overlay(d_org: 1) inz('ORG')
     d  d_orgfill1                    1    overlay(d_org: 4) inz(';')
     d  d_orgid                      30    overlay(d_org: 5)
     d  d_orgfill2                    1    overlay(d_org:35) inz(';')
     d  d_orgstoreid                 30    overlay(d_org:36)
     d  d_orgfill3                    1    overlay(d_org:66) inz(';')
     d  d_orgname                    60    overlay(d_org:67)
      *
      * Recordbeskrivelse - lager
      *
     d                 ds
     d d_whs                        100
     d  d_whsrcd                      3    overlay(d_whs:  1) inz('WHS')
     d  d_whsfill1                    1    overlay(d_whs:  4) inz(';')
     d  d_whslage                     2  0 overlay(d_whs:  5)
     d  d_whsfill2                    1    overlay(d_whs:  7) inz(';')
     d  d_whslnav                    20    overlay(d_whs:  8)
     d  d_whsfill3                    1    overlay(d_whs: 28) inz(';')
     d  d_whsregion                   2  0 overlay(d_whs: 29)
     d  d_whsfill4                    1    overlay(d_whs: 31) inz(';')
9.02 d  d_whskjede                    2    overlay(d_whs: 32)
     d  d_whsfill5                    1    overlay(d_whs: 34) inz(';')
9.02 d  d_whsorgid                   25    overlay(d_whs: 35)
9.03 d  d_whsfill6                    1    overlay(d_whs: 60) inz(';')
9.03 d  d_whsorgno                    9    overlay(d_whs: 61)
      *
      * Recordbeskrivelse - meldingshale
      *
     d                 ds
     d d_unt                         43
     d  d_untrcd                      3    overlay(d_unt: 1) inz('UNT')
     d  d_untfill1                    1    overlay(d_unt: 4) inz(';')
     d  d_untnosegmen                12  0 overlay(d_unt: 5)
     d  d_untfill2                    1    overlay(d_unt:17) inz(';')
     d  d_untrefno                   26    overlay(d_unt:18)
      *
      * Definering av variabler i nøkler
      *
     d anbll3_fgr      s                   like(aolfgr)
     d anbll3_fir      s                   like(aolfir)
      *
      * Definering av variable
      *
     d w_firm          s                   like(rrfirm)
     d w_tell          s             12  0
     d w_tmst          s               z
     d w_dato          s               d
      *
      * Definering av parametre
      *
     d p_stat          s              1
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      p_stat = *blank
      *
      * Henter data om nettbutikk og eier. Avslutter hvis ingen finnes.
      *
     c                   eval      anbll3_fgr = l_fgr
     c                   eval      anbll3_fir = l_firm
     c     anbll3_key    chain     anbll3r
     c                   if        not %found
     c                   eval      p_stat = 'E'
     c                   goto      avslutt
     c                   endif
     c     aoleie        chain     anbhl1r
     c                   if        not %found
     c                   eval      p_stat = 'E'
     c                   goto      avslutt
     c                   endif
      *
      * Meldingshode
      *
     c                   time                    w_tmst
     c                   time                    w_dato
     c                   move      w_tmst        d_unhrefno
      *
     c                   eval      data = d_unh
     c                   write     netlagerr
      *
      * Meldingsstart
      *
     c                   eval      data = d_bgm
     c                   write     netlagerr
      *
      * Organisasjon
      *
     c                   eval      d_orgid = aoheie
     c                   eval      d_orgstoreid = aolbut
     c                   eval      d_orgname = aohnav
      *
     c                   eval      data = d_org
     c                   write     netlagerr
      *
      * Lager
      *
     c     ra91l1_key    setll     ra91l1
     c     ra91l1_key    reade     ra91l1r
     c                   dow       %eof = *off
      *
9.01 c                   if        rrlage <> 0  and
9.01 c                             rrebiz <> 1
     c                   eval      d_whslage = rrlage
     c                   eval      d_whslnav = rrltek
     c                   eval      d_whsregion = rrregi
9.02 c                   eval      d_whskjede = rrkjed
9.02 c                   eval      d_whsorgid = aoheie
9.03 c                   eval      d_whsorgno = aoheie
      *
     c                   eval      data = d_whs
     c                   write     netlagerr
     c                   eval      w_tell = w_tell + 1
     c                   endif
      *
     c     ra91l1_key    reade     ra91l1r
     c                   enddo
      *
      * Meldingshale
      *
     c                   eval      d_untnosegmen = w_tell
     c                   eval      d_untrefno = d_unhrefno
      *
     c                   eval      data = d_unt
     c                   write     netlagerr
      *
      *
      * Avslutt
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_stat
      *
      * Key for les på regionskobling
      *
     c     ra91l1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på nettbutikk
      *
     c     anbll3_key    klist
     c                   kfld                    anbll3_fgr
     c                   kfld                    anbll3_fir
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
