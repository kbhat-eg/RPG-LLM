     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : LL502R
      * Beskrivelse . . : Vise lagerbeholdning alle lager
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
5.70  * PRGR  191208 bof Utvidet med varetype på lager
6.30  * PRGR  311014 bkv Feltet VLPAKK er ikke i bruk
7.xx  * 7.00  170117 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  *       090519 bof Spørring mot andre lager
7.02  *       160615 ask Viser lagerinfo bare fra lagere innenfor samme region
8.01  * 800   220422 ask Lagt inn switchstyrt kobling til spørring på lager andre filgrupper
      *                  Dette overstyrer bruken av u_701.
8.02  * 800   190623 bof Endret i skjermbilde vedr. F8  - skal alltid vises
8.03  * 800   211124 ask lagt inn mulighet for å vise
8.03  *                  disponibel beholdning med bekr. NXS-20463
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = avslutt
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
7.02 fausrl1    if   e           k disk    rename(ausrpfr:ausrl1r)
7.02 fra91l2    if   e           k disk    rename(ra91pfr:ra91l2r)
      *
     fll502d    cf   e             workstn sfile(d1sfl:w_srrn)
      *
      * Definering av Local data area
      *
     d                uds
     d l_wsid                901    910
     d l_user                        10
7.00 d l_filg                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variable i parametre
      *
     d p_firm          s                   like(vlfirm)
     d p_vare          s                   like(vlvare)
8.03 d p_inlr          s              1
      *
      * Definering av variabler i nøkler
      *
     d vlagl1_vare     s                   like(vlvare)
     d vvarl1_vare     s                   like(vvvare)
7.02 d ausrl1_user     s                   like(abuser)
7.02 d ra91l2_lage     s                   like(rrlage)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vlfirm)
     d w_vare          s                   like(vlvare)
     d w_lage          s                   like(vllage)
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
7.02 d b_alle          s              1    inz(*off)
      *
     d w_latx          s             20
     d w_adrs          s             30
     d w_ponr          s              4  0
     d w_pste          s             30
     d w_avde          s              4  0
     d w_valg          s              1  0
     d w_dato          s                   like(vledat)
     d w_dato_6        s              6  0
     d w_lag_alf       s              2
7.02 d w_regi          s                   like(rrregi)
8.02 d w_bbek          s                   like(vlbbes)
8.02 d w_bbes          s                   like(vlbbes)
      *
7.00   dcl-s co402_verdi1  char(1);
7.00   dcl-s co402_verdi2  char(2048);
7.00   DCL-PR CO402R EXTPGM('CO402R');
7.00     p_filgrp            char(3)     const;
7.00     p_firm              packed(3:0) const;
7.00     p_lager             packed(2:0) const;
7.00     p_nokkel            char(50)    const;
7.00     p_verdi1            char(1);
7.00     p_verdi2            char(2048);
7.00   END-PR;
      *
7.01 d u_701           s              1    inz(*off)
7.02 d u_s702          s              1    inz(*off)
8.01 d u_s801          s              1    inz(*off)
8.03 d u_s803          s              1    inz(*off)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_srrn - Relativt recordnummer i subfile
      * w_spge - Sidestørrelse i subfile (page)
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * w_virn - Vis den side som inneholder denne record.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * D1SFL  - D1 Subfile - record
      * D2CTL  - D2 Subfile - control record
      * D2CMD  - D2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * D2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     d2taga        tag
8.01 c                   if        u_s801 = *on
8.01 c                   eval      *in30 = *on
8.01 c                   endif
8.01  *
     c                   write     d2cmd
      *
      * Bare data
      *
     c     d2tagb        tag
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_pgm
     c                   when      *inke
     c                   exsr      forny
     c                   goto      d2tagb
     c                   when      *inkh
7.02 c                   if        u_s702 = *on
7.02 c                   eval      b_alle = *on
7.02 c                   else
     c                   exsr      andre_lager
     c                   goto      d2tagb
7.02 c                   endif
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      d2tagb
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      d2taga
      *
      * Avslutt program
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c     vlagl1_key    setll     vlagl1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
      * Beholder eksisterende posisjonering i subfilen
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     d1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      w_virn = w_srrn
      *
      * Lagerdetaljer
      *
     c                   if        d1valg = 5
     c                   exsr      xb5win
     c                   eval      d1valg = 0
     c                   update    d1sfl
     c                   iter
     c                   endif
      *
      * Beholdning pr. dato
      *
     c                   if        d1valg = 6
     c                   exsr      xb6win
     c                   eval      d1valg = 0
     c                   update    d1sfl
     c                   iter
     c                   endif
      *
      * Historisk lagerbok
      *
     c                   if        d1valg = 7
     c                   exsr      xb7win
     c                   eval      d1valg = 0
     c                   update    d1sfl
     c                   iter
     c                   endif
      *
      * Inkjøpsplanlegging
      *
     c                   if        d1valg = 9
     c                   exsr      xb9win
     c                   eval      d1valg = 0
     c                   update    d1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   exsr      forny
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å se på lagerdetaljer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb5win        begsr
      *
     c                   call      'LL504R'
     c                   parm                    w_vare
     c                   parm                    d1lage
      *
     c     end_xb5win    endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å se på datodetaljer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb6win        begsr
      *
     c                   call      'LL506R'
     c                   parm                    w_vare
     c                   parm                    d1lage
      *
     c     end_xb6win    endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å se på historisk lagerbok
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb7win        begsr
      *
     c                   move      d1lage        w_lag_alf
      *
      *  Legger inn 01.01 dette år
      *
     c                   time                    w_dato
     c     *ymd          move      w_dato        w_dato_6
     c                   move      0101          w_dato_6
     c     *ymd          move      w_dato_6      w_dato
      *
     c                   eval      w_valg = 1
      *
     c                   call      'LH502R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_lag_alf
     c                   parm                    w_dato
     c                   parm                    w_valg
      *
     c     end_xb7win    endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for inkjøpsplanlegging
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb9win        begsr
      *
     c                   call      'LL700R'
     c                   parm                    w_vare
     c                   parm                    d1lage
      *
     c     end_xb9win    endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å se på lager i andre filgrupper
8.01  * Denne rutinen er nå switchstyrt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     andre_lager   begsr
      *
8.01 c                   if        u_s801 = *on
7.01 c                   call      'LQ502C'
7.01 c                   parm                    w_vare
7.01 c                   else
     c                   exfmt     d8win
     c                   goto      end_andre
7.01 c                   endif
      *
      *
     c     end_andre     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     d2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c     vlagl1_key    reade     vlagl1                                 15
      *
     c                   if        *in15
     c                   goto      end_crt
     c                   endif
      *
5.70 c                   if        vlvtyp <> 'L'
5.70 c                   iter
5.70 c                   endif
7.02  *
7.02  * Sjekker om lager er fra samme region dersom ikke alle er valgt
7.02  *
7.02 c                   if        u_s702 = *on
7.02 c                   if        b_alle = *off
7.02 c                   eval      ra91l2_lage = vllage
7.02 c     ra91l2_key    chain     ra91l2
7.02 c                   if        not %found  or
7.02 c                             rrregi <> w_regi
7.02 c                   iter
7.02 c                   endif
7.02 c                   endif
7.02 c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      d1valg = 0
     c                   eval      d1lage = vllage
     c                   eval      d1behl = vlbehl
6.30 c*                  eval      d1pakk = vlpakk * -1
     c                   eval      d1oord = vloord * -1
8.03 c                   if        u_s803 = *off
     c                   eval      d1bbek = vlbbes
     c                   eval      d1disp = vlbehl + vlbbes -
     c                                      vlpakk - vloord
8.03 c                   eval      *in31 = *off
8.03 c                   else
8.03 c                   eval      p_inlr = *off
8.03 c                   call      'LL508R'
8.03 c                   parm                    p_vare
8 03 c                   parm                    vllage
8.03 c                   parm                    w_bbek
8.03 c                   parm                    w_bbes
8.03 c                   parm                    p_inlr
8.03 c                   eval      d1bbek = w_bbek
8.03 c                   eval      d1bbes = w_bbes
8.03 c                   eval      d1disp = vlbehl + w_bbek -
8.03 c                                      vlpakk - vloord
8.03 c                   eval      *in31 = *on
8.03 c                   endif
      *
     c                   if        d1disp <= 0
     c                   eval      *in41 = *on
     c                   else
     c                   eval      *in41 = *off
     c                   endif
      *
     c                   exsr      hent_lager
      *
     c                   write     d1sfl
      *
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
8.03  *
8.03  * En siste "runde" for å lukke filer
8.03  *
8.03 c                   eval      p_inlr = *on
8.03 c                   call      'LL508R'
8.03 c                   parm                    p_vare
8 03 c                   parm                    vllage
8.03 c                   parm                    w_bbek
8.03 c                   parm                    w_bbes
8.03 c                   parm                    p_inlr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     d2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     d2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å hente lagertekst
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_lager    begsr
      *
     c                   call      'FÅ720R'
     c                   parm                    w_firm
     c                   parm                    d1lage
     c                   parm                    w_latx
     c                   parm                    w_adrs
     c                   parm                    w_ponr
     c                   parm                    w_pste
     c                   parm                    w_avde
     c                   parm                    b_feil
     c                   movel     w_latx        d1ltek
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for les av LAGER
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl1_vare
      *
      *  Key for les av VARE
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
7.02  *
7.02  * Key for brukerregister
7.02  *
7.02 c     ausrl1_key    klist
7.02 c                   kfld                    ausrl1_user
7.02  *
7.02  * Key for oppslag på regionskobling
7.02  *
7.02 c     ra91l2_key    klist
7.02 c                   kfld                    w_firm
7.02 c                   kfld                    ra91l2_lage
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
      *
      *  Legger inn startverdier
      *
     c                   eval      w_firm = p_firm
     c                   eval      vlagl1_vare = p_vare
     c                   eval      vvarl1_vare = p_vare
     c                   eval      w_vare = p_vare
7.01  *
7.01  * Endring 7.01
7.01  *
7.01   CallP CO402R(l_filg:w_firm:0:'LL502R_701':
7.01                    co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01     u_701 = *on;
7.01   endif;
7.02  *
7.02  * Endring 7.02
7.02  *
7.02   CallP CO402R(l_filg:w_firm:0:'BRUK_REGIONSLAGER':
7.02                    co402_verdi1:co402_verdi2);
7.02   if co402_verdi1 = '1';
7.02     u_s702 = *on;
7.02   endif;
8.01  *
8.01  * Endring 8.01
8.01  *
8.01   CallP CO402R(l_filg:w_firm:0:'LAGER_FILGRUPPER':
8.01                    co402_verdi1:co402_verdi2);
8.01   if co402_verdi1 = '1';
8.01     u_s801 = *on;
8.01   endif;
8.03  *
8.03  * Endring 8.03
8.03  *
8.03   CallP CO402R(l_filg:w_firm:0:'BESTILLING_DISP':
8.03                    co402_verdi1:co402_verdi2);
8.03   if co402_verdi1 = '1';
8.03     u_s803 = *on;
8.03   endif;
      *
      *  Finn varetekst og legg ut i bilde
      *
     c     vvarl1_key    chain     vvarl1r                            90
     c                   movel     w_vare        d2vare
     c                   eval      d2tek1 = vvtek1
     c                   eval      d2enhl = vvenhl
      *
7.02  *
7.02  * Hent regionskode på brukerens lager
7.02  *
7.02 c                   eval      ausrl1_user = l_user
7.02 c     ausrl1_key    chain     ausrl1
7.02  *
7.02 c                   eval      ra91l2_lage = ablagr
7.02 c     ra91l2_key    chain     ra91l2
7.02 c                   if        not %found  or
7.02 c                             rrregi = 0
7.02 c                   eval      b_alle = *on
7.02 c                   else
7.02 c                   eval      w_regi = rrregi
7.02 c                   endif
      * Posisjonerer databasen, og fyller opp subfile
      *
     c     vlagl1_key    setll     vlagl1
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
