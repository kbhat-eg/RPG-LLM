     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASSTAT
      * Program . . . . : SF500R
      * Beskrivelse . . : Salgsstatistikk, spørring på salg - parameter
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       150198 HKO  Nytt program
3.31  * R3.31 280501 AMD  Åpnet for å kunne spørre på varer som ikke
3.31  *                   finnes i eget vare-register.
      *       141005 HKO  Modernisert programmet
7.xx  * 7.00  010216 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
8.01  *PRG2   220622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner - COMP
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsstapf    if   e           k disk
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvasl1    if   e           k disk    rename(vvaspfr:vvasl1r)
      *
     fsf500d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variabler i nøkler
      *
     d rkunl1_kund     s                   like(rkkund)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
     d vvarl1_vare     s                   like(vvvare)
     d vvasl1_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
     d w_dato          s               d   datfmt(*iso)
     d w_dato_alf      s             10
      *
     d w_firm          s                   like(sffirm)
     d w_vare          s                   like(sfvare)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer bildet
      *
     c                   time                    w_dato
     c                   move      w_dato        w_dato_alf
     c                   movel     w_dato_alf    b1paar
      *
      * Behandler bildet
      *
     c     b1taga        tag
      *
     c                   exfmt     b1bld
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b1taga
     c                   endsl
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   eval      b_feil = *off
     c                   goto      b1taga
     c                   endif
      *
      * Kaller spørreprogram
      *
     c                   eval      w_dato = *loval
     c                   if        b1bida <> 0
     c     *dmy          move      b1bida        w_dato
     c                   endif
      *
     c                   if        b1vare = 0
      *
     c                   call      'SF501R'
     c                   parm                    w_firm
     c                   parm                    b1paar
     c                   parm                    b1vkun
     c                   parm                    b1kpro
     c                   parm                    w_dato
      *
     c                   else
      *
     c                   movel     b1vare        w_vare
      *
     c                   call      'SF502R'
     c                   parm                    w_firm
     c                   parm                    b1paar
     c                   parm                    w_vare
     c                   parm                    b1vkun
     c                   parm                    b1kpro
     c                   parm                    w_dato
      *
     c                   endif
      *
     c                   goto      b1taga
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Kunde
      *
     c                   if        wactfe = 'B1VKUN'
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    b1vkun
     c                   parm                    b1knav
     c                   leavesr
     c                   endif
      *
      * Kundeprosjekt
      *
     c                   if        wactfe = 'B1KPRO'
     c                   CALL      'FL500R'
     c                   parm                    w_firm
     c                   parm                    b1vkun
     c                   parm                    b1kpro
     c                   parm                    b1knav
     c                   parm                    b1pnav
     c                   leavesr
     c                   endif
      *
      * Varenummer
      *
     c                   if        wactfe = 'B1VARE'
     c                   movel     b1vare        w_vare
     c                   CALL      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    b1tek1
     c                   movel     w_vare        b1vare
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
     c                   eval      b1knav = *blank
     c                   eval      b1pnav = *blank
     c                   eval      b1tek1 = *blank
      *
      * År
      *
     c                   if        b1bida <> 0
     c     *dmy          move      b1bida        w_dato
     c                   move      w_dato        w_dato_alf
     c                   movel     w_dato_alf    b1paar
     c                   endif
      *
     c                   if        b1paar = 0
     c                   eval      *in31  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      * Dato
      *
     c                   if        b1bida <> 0
     c     *dmy          test(d)                 b1bida                 32
     c                   if        *in32
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
      * Kunde / vare
      *
     c                   if        b1vkun = 0 and
     c                             b1vare = 0
     c                   eval      *in33  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      * Kunde / kundeprosjekt
      *
     c                   if        b1vkun <> 0
     c                   eval      rkunl1_kund = b1vkun
     c     rkunl1_key    chain     rkunl1
     c                   if        not %found
     c                   eval      *in34  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      b1knav = rknavn
     c                   endif
      *
     c                   if        b1vkun = 0 and
     c                             b1kpro <> 0
     c                   eval      *in35  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   if        b1kpro <> 0
     c                   eval      fkprl1_kund = b1vkun
     c                   eval      fkprl1_kpro = b1kpro
     c     fkprl1_key    chain     fkprl1
     c                   if        not %found
     c                   eval      *in36  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      b1pnav = flnavn
     c                   endif
      *
      * Varenummer
      *
     c                   if        b1vare <> 0
      *
     c                   movel     b1vare        vvarl1_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   movel     b1vare        vvasl1_vare
     c     vvasl1_key    chain     vvasl1
     c                   if        not %found
3.31 c                   eval      vvtek1 = 'Vare ble ikke funnet +
3.31 c                                       i varereg'
     c                   endif
     c                   endif
      *
     c                   eval      b1tek1 = vvtek1
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på kunde/kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      * Key for oppslag på vareregister
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på register over slettede varer
      *
     c     vvasl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvasl1_vare
      *
      *
      * Henter firmakode fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
