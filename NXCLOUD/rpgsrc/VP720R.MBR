     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VP720R
      * Beskrivelse . . : Vare-pris, henter prissammenligningsenhet
      *
      *                   Input:  Firma, vare, enhet og salgspris
      *                   Output: Prissammenligningsenhet og salgpris i
      *                           forhold til prissammenligningsenhet
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       110607 HKO  Nytt program
6.30  * 6.30  270113 bof  Omskriving i forb. endring pakning VA
      *
7.01  * 6.30  170719 bsa  Henter pris på enhet, hvis finnes.
      *
8.01  * 8.xx  240323 bof  Test at p_pspr ikke > 99999999,99
8.02  *PRG2   150622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     fjvpkl2    if   e           k disk    rename(jvpkpfr:jvpkl2r)
      *
      * Definering av pamametre
      *
     d p_firm          s                   like(vvfirm)
     d p_vare          s                   like(vvvare)
     d p_enhe          s                   like(veenhe)
     d p_sapr          s              9  2
     d p_enps          s                   like(veenhe)
     d p_pspr          s              9  2
8.02 d p_prgr          s              2
      *
      * Definering av variable i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d vvenl1_vare     s                   like(vevare)
     d vvenl1_enhe     s                   like(veenhe)
     d jvpkl2_vare     s                   like(jwvare)
6.30 d jvpkl2_ldor     s                   like(jwldor)
     d jvpkl2_enhe     s                   like(jwenhe)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vvfirm)
     d w_omre          s                   like(veomre)
7.01 d w_vare          s                   like(vvvare)
8.02 d w_prgr          s              2
7.01 d w_enps          s                   like(vvenhe)
7.01 d w_lety          s              1  0
7.01 d w_dato          s                   like(vvodat)
7.01 d w_time          s                   like(vvotim)
7.01 d w_inlr          s              1    inz(*off)
7.01 d w_sapr          s              9  2
7.01 d w_bupr          s              9  2
7.01 d w_tipr          s              9  2
7.01 d w_kopr          s              9  2
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter vareinformasjon
      *
     c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      p_enps = vvenps
      *
     c                   if        p_enps <> *blank
     c                   goto      pse_pris
     c                   endif
      *
      * Dersom prissammenligningenhet ikke er oppgitt på vare hentes denne
      * fra undergruppe
      *
     c                   eval      vugrl1_uogr = vvogrp
     c                   eval      vugrl1_uhgr = vvhgrp
     c                   eval      vugrl1_uugr = vvugrp
     c     vugrl1_key    chain     vugrl1
     c                   if        %found
      *
     c                   if        vguep1 <> *blank
      *
     c                   eval      vvenl1_vare = p_vare
     c                   eval      vvenl1_enhe = vguep1
     c     vvenl1_key    chain     vvenl1
     c                   if        %found
     c                   eval      p_enps = vguep1
     c                   goto      pse_pris
     c                   endif
      *
     c                   eval      jvpkl2_vare = p_vare
6.30 c                   eval      jvpkl2_ldor = vvnlev
     c                   eval      jvpkl2_enhe = vguep1
     c     jvpkl2_key    chain     jvpkl2
     c                   if        %found
     c                   eval      p_enps = vguep1
     c                   goto      pse_pris
     c                   endif
      *
     c                   endif
      *
     c                   if        vguep2 <> *blank
      *
     c                   eval      vvenl1_vare = p_vare
     c                   eval      vvenl1_enhe = vguep2
     c     vvenl1_key    chain     vvenl1
     c                   if        %found
     c                   eval      p_enps = vguep2
     c                   goto      pse_pris
     c                   endif
      *
     c                   eval      jvpkl2_vare = p_vare
6.30 c                   eval      jvpkl2_ldor = vvnlev
     c                   eval      jvpkl2_enhe = vguep2
     c     jvpkl2_key    chain     jvpkl2
     c                   if        %found
     c                   eval      p_enps = vguep2
     c                   goto      pse_pris
     c                   endif
      *
     c                   endif
      *
     c                   endif
      *
      * Avbryter dersom varen ikke har prissammenligningsenhet
      *
     c                   if        p_enps = *blank
     c                   goto      avslutt
     c                   endif
      *
      * Henter salgspris i forhold til prissammenligningsenhet
      *
     c     pse_pris      tag
7.01  *
7.01  * Sjekk først om pris finnes for enheten. Hvis 0, bruk omregningsenhet
7.01  *
7.01 c                   eval      w_vare = p_vare
7.01 c                   eval      w_enps = p_enps
7.01 c                   eval      w_lety = 0
7.01 c                   time                    w_dato
7.01 c                   time                    w_time
7.01 c                   eval      w_sapr = 0
7.01 c                   eval      w_bupr = 0
7.01 c                   eval      w_tipr = 0
7.01 c                   eval      w_kopr = 0
7.01  *
7.01 c                   call      'VP700R'
7.01 c                   parm                    w_firm
7.01 c                   parm                    w_vare
7.01 c                   parm                    w_prgr
7.01 c                   parm                    w_enps
7.01 c                   parm                    w_lety
7.01 c                   parm                    w_dato
7.01 c                   parm                    w_time
7.01 c                   parm                    w_inlr
7.01 c                   parm                    w_sapr
7.01 c                   parm                    w_bupr
7.01 c                   parm                    w_tipr
7.01 c                   parm                    w_kopr
7.01  *
7.01 c                   if        w_sapr <> 0
7.01 c                   eval      p_pspr = w_sapr
7.01 c                   goto      avslutt
7.01 c                   endif
7.01  *
     c                   eval      vvenl1_vare = p_vare
     c                   eval      vvenl1_enhe = p_enhe
     c     vvenl1_key    chain     vvenl1
     c                   if        not %found
     c                   eval      p_enps = *blank
     c                   eval      p_pspr = 0
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      w_omre = veomre
      *
     c                   eval      vvenl1_vare = p_vare
     c                   eval      vvenl1_enhe = p_enps
     c     vvenl1_key    chain     vvenl1
     c                   if        %found
8.01 c                   if        (p_sapr * veomre / w_omre) < 9999999,99
     c                   eval(h)   p_pspr = p_sapr * veomre / w_omre
8.01 c                   else
8.01 c                   eval      p_pspr = 0
8.01 c                   endif
     c                   goto      avslutt
     c                   endif
      *
      * Henter informasjon fra VA når prissammenligningsenhet ikke
      * finnes på varen i EVR
      *
     c                   eval      jvpkl2_vare = p_vare
6.30 c                   eval      jvpkl2_ldor = vvnlev
     c                   eval      jvpkl2_enhe = p_enhe
     c     jvpkl2_key    chain     jvpkl2
     c                   if        not %found
     c                   eval      p_enps = *blank
     c                   eval      p_pspr = 0
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      w_omre = jwofak
      *
     c                   eval      jvpkl2_vare = p_vare
6.30 c                   eval      jvpkl2_ldor = vvnlev
     c                   eval      jvpkl2_enhe = p_enps
     c     jvpkl2_key    chain     jvpkl2
     c                   if        %found
8.01 c                   if        (p_sapr * jwofak/w_omre) < 9999999,99
     c                   eval(h)   p_pspr = p_sapr * jwofak / w_omre
8.01 c                   else
8.01 c                   eval      p_pspr = 0
8.01 c                   endif
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      p_enps = *blank
     c                   eval      p_pspr = 0
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_enhe
     c                   parm                    p_sapr
     c                   parm                    p_enps
     c                   parm                    p_pspr
7.01 c                   parm                    p_prgr
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag på vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
     c                   kfld                    vvenl1_enhe
      *
      * Key for oppslag på vare-pakning (VA)
      *
     c     jvpkl2_key    klist
     c                   kfld                    jvpkl2_vare
6.30 c                   kfld                    jvpkl2_ldor
     c                   kfld                    jvpkl2_enhe
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
7.01 c                   eval      w_prgr = p_prgr
      *
      * Initierer output-parametre
      *
     c                   eval      p_enps = *blank
     c                   eval      p_pspr = 0
      *
     c                   endsr
