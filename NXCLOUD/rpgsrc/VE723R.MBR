     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VE723R
      * Beskrivelse . . : Henter GTIN nr ut fra gitt varenr + enhet + leverandør
      *                   Baserer seg på VE713R
      *
      *                   Input : firma, leverandør, varenr., enhet
      *                   Output: GTIN nr  status (1 = ok)
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       121022 bsa  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarlr    if   e           k disk    rename(vvarpfr:vvarlrr)
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     fvveplr    if   e           k disk    rename(vveppfr:vveplrr)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjvpkl1    if   e           k disk    rename(jvpkpfr:jvpkl1r)
     fjlevl3    if   e           k disk    rename(jlevpfr:jlevl3r)
      *
      * Definering av variable i nøkler
      *
     d vvarlr_firm     s                   like(vvfirm)
     d vvarlr_vare     s                   like(vvvare)
     d vlagl1_vare     s                   like(vlvare)
     d vlagl1_lage     s                   like(vllage)
     d vveplr_pfir     s                   like(vepfir)
     d vveplr_pvar     s                   like(vepvar)
     d vveplr_penh     s                   like(vepenh)
     d vveplr_pldo     s                   like(vepldo)
     d vvepl1_pfir     s                   like(vepfir)
     d vvepl1_pvar     s                   like(vepvar)
     d vvepl1_penh     s                   like(vepenh)
     d jvpkl1_vare     s                   like(jwvare)
     d jvpkl1_ldor     s                   like(jwldor)
     d jvarl1_vare     s                   like(jvvare)
     d jlevl3_enum     s                   like(jlenum)
      *
      * Definering av pamametre
      *
     d p_firm          s                   like(vepfir)
     d p_eann          s                   like(vepgti)
     d p_vare          s                   like(vepvar)
     d p_ldor          s                   like(vvldor)
     d p_lage          s                   like(vllage)
     d p_enhe          s                   like(vepenh)
     d p_stat          s              1
      *
     d w_ldor          s                   like(vvldor)
      *
     d                uds
     d l_user                911    920
     d l_sfir                944    946  0
     d l_savd                947    950  0
     d l_lagr               1001   1002  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram - følgende prioritering
      *  - Finn GTIN basert på vare, enhet og leverandør
      *  - Hvis ikke utfylt leverandør får treff, finn leverandør
      *    fra lager, eller hovedleverandør, før vi sjekker med
      *    0 i leverandør.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer output-parametre
      *
     c                   eval      p_eann = 0
     c                   eval      p_stat = *blank
      *
     c                   eval      vvarlr_firm = p_firm
     c                   eval      vvarlr_vare = p_vare
     c     vvarlr_key    chain     vvarlr
     c                   if        not %found(vvarlr)
     c                   goto      sjk_VA
     c                   endif
      *
      * Henter GTIN kode fra VVEP på angitt enhet og leverandør
      *
     c                   eval      vveplr_pfir = p_firm
     c                   eval      vveplr_pvar = p_vare
     c                   eval      vveplr_penh = p_enhe
     c                   eval      vveplr_pldo = p_ldor
     c     vveplr_key    chain     vveplr
     c                   if        %found(vveplr) and
     c                             vepgti <> 0
     c                   eval      p_eann = vepgti
     c                   eval      p_stat = '1'
     c                   goto      avslutt
     c                   endif
      *
      * Hent overstyrt leverandør fra lager
      *
     c                   eval      vlagl1_vare = p_vare
     c                   eval      vlagl1_lage = p_lage
     c     vlagl1_key    chain     vlagl1
     c                   if        %found(vlagl1)
     c                   eval      vveplr_pldo = vlbldo
     c     vveplr_key    chain     vveplr
     c                   if        %found(vveplr) and
     c                             vepgti <> 0
     c                   eval      p_eann = vepgti
     c                   eval      p_stat = '1'
     c                   goto      avslutt
     c                   endif
     c                   endif
      *
      * Hent leverandør fra vare
      *
     c                   eval      vveplr_pldo = vvldor
     c     vveplr_key    chain     vveplr
     c                   if        %found(vveplr) and
     c                             vepgti <> 0
     c                   eval      p_eann = vepgti
     c                   eval      p_stat = '1'
     c                   goto      avslutt
     c                   endif
     c
      *
      * Sjekk en gang til på VVEP uavhengig av leverandør, men med angitt enhet
      *
     c                   eval      vvepl1_pfir = p_firm
     c                   eval      vvepl1_pvar = p_vare
     c                   eval      vvepl1_penh = p_enhe
     c     vvepl1_key    setll     vveplr
     c     vvepl1_key    reade     vveplr                                 91
     c                   dow       *in91 = *off
      * Har vi match på leverandør ?
     c                   if        p_enhe = vepenh and
     c                             vepgti <> 0
     c                   eval      p_eann = vepgti
     c                   eval      p_stat = '1'
     c                   goto      avslutt
     c                   endif
     c     vvepl1_key    reade     vveplr                                 91
     c                   enddo
      *
     c                   goto      sjk_gtinVA
      *
      * -----------------------------------------
      * VA håndtering                           *
      * -----------------------------------------
      *
     c     sjk_VA        tag
      *
     c                   eval      jvarl1_vare = p_vare
     c     jvarl1_key    chain     jvarl1
     c                   if        not %found(jvarl1)
     c                   goto      avslutt
     c                   endif
      *
     c     sjk_gtinVA    tag
      *
      * Hvis ikke funnet - sjekk mot vareadministrasjon.
      * Først forutsetter vi match på leverandør
      *
     c                   eval      jvpkl1_vare = p_vare
     c                   eval      jvpkl1_ldor = w_ldor
     c     jvpkl1_key    setll     jvpkl1
     c     jvpkl1_key    reade     jvpkl1
     c                   dow       not %eof(jvpkl1)
     c                   if        jwldor = w_ldor and
     c                             jwenhe = p_enhe and
     c                             jwgtin <> 0
      * Har vi match på enhet, hent enheten fra
     c                   eval      p_eann = jwgtin
     c                   eval      p_stat = '1'
     c                   goto      avslutt
     c                   endif
     c     jvpkl1_key    reade     jvpkl1
     c                   enddo
      *
      * Prøver til slutt uten krav om leverandør
      *
     c                   eval      jvpkl1_vare = p_vare
     c     jvpkl1_key2   setll     jvpkl1
     c     jvpkl1_key2   reade     jvpkl1
     c                   dow       not %eof(jvpkl1)
     c                   if        jwgtin <> 0   and
     c                             jwenhe = p_enhe
     c                   eval      p_eann = jwgtin
     c                   eval      p_stat = '1'
     c                   goto      avslutt
     c                   endif
     c     jvpkl1_key2   reade     jvpkl1
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
      *
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ldor
     c                   parm                    p_lage
     c                   parm                    p_vare
     c                   parm                    p_enhe
     c                   parm                    p_eann
     c                   parm                    p_stat
      *
      * Key for les på vareenhets-register
      *
     c     vveplr_key    klist
     c                   kfld                    vveplr_pfir
     c                   kfld                    vveplr_pvar
     c                   kfld                    vveplr_penh
     c                   kfld                    vveplr_pldo
      *
      * Key for les på vareregister
      *
     c     vvarlr_key    klist
     c                   kfld                    vvarlr_firm
     c                   kfld                    vvarlr_vare
      *
      * Key for les på lagerregister
      *
     c     vlagl1_key    klist
     c                   kfld                    p_firm
     c                   kfld                    vlagl1_vare
     c                   kfld                    vlagl1_lage
      *
      * Key for les på VVEP-register
      *
     c     vvepl1_key    klist
     c                   kfld                    vvepl1_pfir
     c                   kfld                    vvepl1_pvar
     c                   kfld                    vvepl1_penh
      *
      * Key for les på JVPK-register
      *
     c     jvpkl1_key    klist
     c                   kfld                    jvpkl1_vare
     c                   kfld                    jvpkl1_ldor
      * Key for les på JVPK-register
      *
     c     jvpkl1_key2   klist
     c                   kfld                    jvpkl1_vare
      *
      * Key for les på JVAR-register
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for les på leverandør i VA
      *
     c     jlevl3_key    klist
     c                   kfld                    jlevl3_enum
      *
      * leser leverandør i VA
     c                   eval      w_ldor = p_ldor
     c                   eval      jlevl3_enum = p_ldor
     c     jlevl3_key    chain     jlevl3
     c                   if        %found
     c                   eval      w_ldor = jlldor
     c                   endif
      *
     c                   endsr
      *
