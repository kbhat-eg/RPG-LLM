     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RA910R
      * Beskrivelse . . : Regioner og regionskoblinger av lager
      *
      * Spesialversjon. : Mestergruppen
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       051219 ASK  Nytt program
      *                   Programmet håndterer både vedlikehold av regioner
      *                   og kobling av region/lager.
7.01  *       101117 ASK Lagt inn kjedekode på lager for overføring til Ehandel
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fra91l1    if   e           k disk    rename(ra91pfr:ra91l1r)
     fra91lr    if   e           k disk    rename(ra91pfr:ra91lrr)
     fra91lu    uf a e           k disk    rename(ra91pfr:ra91lur)
     fra10lr    if   e           k disk    rename(ra10pfr:ra10lrr)
      *
     fra910d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av parametre
      *
     d p_lage          s                   like(rrlage)
     d p_tx20          s             20
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d ra91l1_regi     s                   like(rrregi)
     d ra91l1_lage     s                   like(rrlage)
     d ra91lr_regi     s                   like(rrregi)
     d ra91lr_lage     s                   like(rrlage)
     d ra91lu_regi     s                   like(rrregi)
     d ra91lu_lage     s                   like(rrlage)
     d ra10lr_jkod     s                   like(rajkod)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_vedl          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(rrfirm)
      *
     d s_jkod          s                   like(rrregi)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   eval      b_oppr = *on
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *off
     c                   goto      b2taga
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_sfrn <> 0
     c     w_sfrn        chain     b1sfl
     c                   endif
      *
     c                   eval      ra91l1_regi = b1regi
     c     ra91l1_key    setll     ra91l1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre regions beskrivelse eller kobling
      *
     c                   if        b1valg = 2
     c                   if        b1lage = 0
     c                   eval      b_oppr = *off
     c                   exsr      xc1bld
     c                   else
     c                   eval      b_vedl = *on
     c                   exsr      xc2bld
     c                   endif
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Koble lager til region
      *
     c                   if        b1valg = 3
     c                   eval      c2regi = b1regi
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett region og kobling
      *
     c                   if        b1valg = 4
     c                   if        b1lage = 0
     c                   exsr      xd1win
     c                   else
     c                   exsr      xd2win
     c                   endif
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for nyregistrering og vedlikehold av region
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_forn = *on
     c                   if        b_oppr = *on
     c                   eval      c1regi = 0
     c                   eval      c1rtek = *blank
     c                   eval      *in30 = *off
     c                   else
     c                   eval      c1regi = b1regi
     c                   eval      c1rtek = b1rtek
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
     c                   if        c1regi = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Sjekk om region finnes fra før dersom det er nyoppreting
      *
     c                   if        b_oppr = *on
     c                   eval      ra91lr_regi = c1regi
     c                   eval      ra91lr_lage = 0
     c     ra91lr_key    chain     ra91lur
     c                   if        %found
     c                   exsr      xc1msg
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   ra91lur
      *
     c                   eval      ra91lu_regi = c1regi
     c                   eval      ra91lu_lage = 0
     c     ra91lu_key    chain     ra91lur
      *
     c                   if        %found
     c                   eval      rrrtek = c1rtek
     c                   update    ra91lur
     c                   else
     c                   eval      rrfirm = w_firm
     c                   eval      rrregi = c1regi
     c                   eval      rrrtek = c1rtek
     c                   eval      rrlage = 0
     c                   eval      rrltek = *blank
     c                   write     ra91lur
     c                   endif
      *
     c                   if        b_oppr = *on
     c                   eval      c1regi = 0
     c                   eval      c1rtek = *blank
     c                   goto      c1taga
     c                   endif
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c*                  exsr      forny
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å varsle om at region allerede finnes
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å opprette kobling mellom region og lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      c2regi = b1regi
     c                   eval      c2rtek = b1rtek
      *
     c                   if        b_vedl = *off
     c                   eval      c2lage = 0
     c                   eval      c2ltek = *blank
     c                   eval      c2ebiz = 0
7.01 c                   eval      c2kjed = *blank
     c                   else
     c                   eval      ra91lr_regi = c2regi
     c                   eval      ra91lr_lage = b1lage
     c     ra91lr_key    chain     ra91lur
     c                   if        %found
     c                   eval      c2ebiz = rrebiz
7.01 c                   eval      c2kjed = rrkjed
     c                   endif
     c                   eval      c2lage = b1lage
     c                   eval      ra10lr_jkod = c2lage
     c     ra10lr_key    chain     ra10lr
     c                   eval      c2ltek = rajtxt
     c                   eval      *in40 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Call program for spørring på lager
      *
     c                   if        *inkd = *on and                                       .....
     c                             w_afld = 'C2LAGE'                                         .
     c                   call      'RA510R'                                                  .
     c                   parm                    w_firm                                      .
     c                   parm                    p_lage                                      .
     c                   parm                    p_tx20                                      .
      *                                                                 .
     c                   if        p_lage <> *zero                                           .
     c                   eval      c2lage = p_lage                                           .
     c                   eval      c2ltek = p_tx20                                           .
     c                   endif                                                               .
      *                                                                 .
     c                   goto      c2taga
     c                   endif                                                               .
      *
      * Validering av input
      *
     c                   if        c2lage = 0
     c                   goto      end_c2bld                                                 .
     c                   endif
      *
      * Sjekk om gyldig lager
      *
     c                   eval      ra10lr_jkod = c2lage
     c     ra10lr_key    chain     ra10lr
     c                   if        %found
     c                   eval      c2ltek = rajtxt
     c                   else
     c                   eval      c2ltek = 'Ikke gyldig lager'
     c                   goto      c2taga
     c                   endif
      *
      * Sjekk om kobling finnes fra før
      *
     c                   if        b_vedl = *off
     c                   eval      ra91lr_regi = c2regi
     c                   eval      ra91lr_lage = c2lage
     c     ra91lr_key    chain     ra91lur
     c                   if        %found
     c                   exsr      xc2msg
     c                   goto      c2taga
     c                   endif
     c                   endif
7.01  *
7.01  * Sjekk om kjedkode er utfylt
7.01  *
7.01 c                   if        c2kjed <> 'XL' and
7.01 c                             c2kjed <> 'BT'
7.01 c                   exsr      xc3msg
7.01 c                   goto      c2taga
7.01 c                   endif
      *
      * Oppdatering
      *
     c                   eval      ra91lu_regi = c2regi
     c                   eval      ra91lu_lage = c2lage
     c     ra91lu_key    chain     ra91lur
      *
     c                   eval      rrltek = c2ltek
     c                   eval      rrebiz = c2ebiz
7.01 c                   eval      rrkjed = c2kjed
      *
     c                   if        %found
     c                   update    ra91lur
     c                   else
     c                   eval      rrfirm = w_firm
     c                   eval      rrregi = c2regi
     c                   eval      rrrtek = c2rtek
     c                   eval      rrlage = c2lage
     c                   write     ra91lur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 3
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b_vedl = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *                                                                 .
     c                   if        b_vedl = *off
     c                   eval      c2lage = 0
     c                   eval      c2ltek = *blank                                           .
     c                   goto      c2taga
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
     c                   eval      *in40  = *off
     c                   eval      b_vedl  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å varsle om at kobling allerede finnes
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c2msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c2msg
     c                   endif
      *
     c     end_c2msg     endsr
7.01  *
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  * Subrutine for å varsle om at kjedekode må utfylles
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *
7.01 c     xc3msg        begsr
7.01  *
7.01 c                   exfmt     c3msg
7.01  *
7.01 c     end_c3msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sletting av regioner
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      d1regi = b1regi
     c                   eval      d1rtek = b1rtek
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rader på valgt region
      *
     c                   eval      ra91l1_regi = d1regi
     c                   eval      ra91l1_lage = 0
     c     ra91l1_key    setll     ra91l1
      *
     c     d1taga        tag
     c                   read      ra91l1
      *
     c                   if        %eof or
     c                             rrfirm <> w_firm or
     c                             rrregi <> d1regi
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      ra91lu_regi = d1regi
     c                   eval      ra91lu_lage = rrlage
     c     ra91lu_key    chain     ra91lur
      *
     c                   delete    ra91lur
     c                   goto      d1taga
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sletting av regions koblinger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd2win        begsr
      *
     c                   eval      d2regi = b1regi
     c                   eval      d2rtek = b1rtek
     c                   eval      d2lage = b1lage
     c                   eval      d2ltek = b1ltek
      *
     c                   exfmt     d2win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d2win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      ra91lu_regi = d2regi
     c                   eval      ra91lu_lage = d2lage
     c     ra91lu_key    chain     ra91lur
      *
     c                   delete    ra91lur
      *
     c     end_d2win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      ra91l1
      *
     c                   if        %eof or
     c                             rrfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      ra91l1_regi = rrregi
      *
     c                   eval      b1valg = 0
     c                   eval      b1regi = rrregi
     c                   eval      b1lage = rrlage
     c                   if        rrlage = 0
     c                   eval      b1rtek = rrrtek
     c                   eval      b1ltek = *blank
     c                   eval      b1ebiz = 0
     c                   else
     c                   eval      b1rtek = *blank
     c                   eval      b1ltek = rrltek
     c                   eval      b1ebiz = rrebiz
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     ra91l1_key    setgt     ra91l1
     c                   readp     ra91l1
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     ra91l1
      *
     c                   if        %eof or
     c                             rrfirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   eval      ra91l1_regi = rrregi
      *
     c                   enddo
      *
     c                   if        %eof
     c     ra91l1_key    setll     ra91l1
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for posisjonering på region/kobling
      *
     c     ra91l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra91l1_regi
     c                   kfld                    ra91l1_lage
      *
      * Key for les region/lager
      *
     c     ra91lr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra91lr_regi
     c                   kfld                    ra91lr_lage
      *
      * Key for oppdatering av region/lager
      *
     c     ra91lu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra91lu_regi
     c                   kfld                    ra91lu_lage
      *
      * Key for les på lager
      *
     c     ra10lr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra10lr_jkod
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      ra91l1_regi = 0
     c                   eval      ra91l1_lage = 0
     c     ra91l1_key    setll     ra91l1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
