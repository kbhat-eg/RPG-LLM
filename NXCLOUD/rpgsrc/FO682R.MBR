     h decedit('0,') datedit(*dmy.) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO682R
      * Beskrivelse . . : Inntasting av parametre ved faktura-kopier
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
6.20  * R6.20 030112 BSA  Vi lager egen rutine for kopi utskrift. Legger på et 1-tall bak
6.20  *                   rutinenavnet som ligger ordretypen. NB Forutsetter at ikke mer enn
6.20  *                   9 posisjoner er benyttet til rutinenavnet fra før.
6.nu  * R6.30 171014 bsa  Programmet gjennomgått mtp nummerutvidelser.
7.xx  * 7.00  010216 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     ffo682d    cf   e             workstn
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
6.20 faforl1    if   e           k disk    rename(aforpfr:aforl1r)
      *
      * Definering av parametere
      *
6.nu d p_parm          s             34
     d p_pf03          s              1  0
      *
      * Definering av data struktur
      *
     d                 ds
6.nu d d_parm                        34
     d  d_aarr                        4  0 overlay(d_parm)
     d  d_otyp                        2    overlay(d_parm:5)
6.nu d  d_fbin                        8  0 overlay(d_parm:7)
6.nu d  d_tbin                        8  0 overlay(d_parm:15)
6.nu d  d_prog                       10    overlay(d_parm:23)
6.nu d  d_kopi                        1    overlay(d_parm:33)
6.nu d  d_dumy                        1    overlay(d_parm:34)
      *
      * Definering av local data area
      *
     d                uds
     d l_ruti                800    809
     d l_sfir                944    946  0
      *                                                                     -
      * Definering av variable                                              -
      *                                                                     -
     d w_ofir          s                   like(vaofir)
     d w_otyp          s                   like(vaotyp)
6.20 d aforl1_ruti     s                   like(afruti)
6.20 d d_orut          s                   like(vaorut)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * A1 Behandler valg for utskrift av FAKTURA/KREDITNOTA-kopier
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     a1tag         tag
      *
      * Nullstiller felter som benyttes
      *
     c                   move      uyear         a1aarr
     c                   eval      a1fbin = *zero
     c                   eval      a1tbin = *zero
     c                   eval      a1otyp = *blank
      *
     c                   if        a1aarr > 80
     c                   add       1900          a1aarr
     c                   else
     c                   add       2000          a1aarr
     c                   endif
      *
     c     a1taga        tag
     c                   exfmt     a1bld
      *
      * F3=Avslutt
      *
     c                   if        *inkc = *on
     c                   z-add     1             p_pf03
     c                   goto      xslutt
     c                   endif
      *
      * F4=Spørring
      *
     c                   if        *inkd = *on
     c                   call      'VA550R'
     c                   parm                    w_ofir
     c                   parm                    a1otyp
      *
     c                   goto      a1taga
     c                   endif
      *
      * Kontroll om det
      * er oppgitt riktig
      * ordretype
      *
     c                   eval      w_ofir = l_sfir
     c                   eval      w_otyp = a1otyp
     c     otykey        chain     votyl1r                            61
     c                   if        *in61 = *on
     c                   eval      *in31 = *on
     c                   goto      a1taga
     c                   endif
      *
      * Kontroll om ordre-
      * type er faktura-
      * ordretype
      *
     c                   if        vaoakk <> 3
     c                   eval      *in32 = *on
     c                   goto      a1taga
     c                   endif
      *
      * Kontroll om fra
      * er større enn til
      * dersom til er oppgitt
      *
     c                   if        a1tbin <> *zero
     c                   if        a1fbin > a1tbin
     c                   eval      *in33 = *on
     c                   goto      a1taga
     c                   endif
     c                   endif
      *
      * Dersom til ikke er
      * oppgitt, legges fra
      * ut i til
      *
     c                   if        a1tbin = *zero
     c                   z-add     a1fbin        a1tbin
     c                   endif
6.20  * Hent "kopi" rutine hvis den finnes - hvis ikke benyttes den fra ordretypen
6.20 c                   eval      d_orut = vaorut
6.20 c                   eval      aforl1_ruti = %trim(vaorut) + '1'
6.20 c     aforl1_key    chain     aforl1                             61
6.20 c                   if        *in61 = *off
6.20 c                   eval      d_orut = aforl1_ruti
6.20 c                   endif
      *
     c                   eval      d_otyp = a1otyp
     c                   eval      d_aarr = a1aarr
     c                   eval      d_fbin = a1fbin
     c                   eval      d_tbin = a1tbin
     c                   eval      d_kopi = 'K'
     c                   eval      d_dumy = '*'
     c                   eval      p_parm = d_parm
      *
6.20 c**                 eval      l_ruti = vaorut
6.20 c                   eval      l_ruti = d_orut
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Definering av nøkler
      *
      *
      * Key for file : ORDRE-TYPE-REGISTER
      *
     c     otykey        klist
     c                   kfld                    w_ofir
     c                   kfld                    w_otyp
      *
      *  Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_parm
     c                   parm                    p_pf03
6.20  *
6.20  * Key for oppslag på rutineregisteret
6.20  *
6.20 c     aforl1_key    klist
6.20 c                   kfld                    aforl1_ruti
      *
     c                   endsr
