     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RK677R                                              *
      * Beskrivelse . . : Utskrift av overskredet kredittgrense               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * V5.50 080807 HFL  Nytt program.                                       *
6.20  * V6.20 140611 bsa  Memosaldo er lagt ut på egen fil                    *
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
6.20 frkmel1    if   e           k disk    rename(rkmepfr:rkmel1r)
     frk677p    o    e             printer oflind(*in30)
      *
     d                uds
     d  l_wsid               901    910
     d  l_user               911    920
     d  l_fgrp               931    933
     d  l_firm               944    946  0
     d  l_fnav               951    980
      *
      * Definering av variabler i nøkler
      *
6.20 d rkmel1_kund     s                   like(rkkund)
      *
     d w_firm          s              3  0
      *
      * Parametre til RS205R - hent mva %
      *
     d w_stat          s              1
     d w_mvak          s              1
     d w_mvtx          s             30
     d w_mvas          s              6  2
     d w_mvat          s              5  4
     d w_mvaf          s             10  9
     d w_bpro          s              5  2
     d w_date          s               d   datfmt(*iso)
      *
      * Les kunder
      *
     c     rkunl1_key    setll     rkunl1r                                60
     c     rkunl1_key    reade     rkunl1r                                60
      *
     c                   dow       *in60 = *off
6.20  * Hent inn informasjon om memosaldo
6.20 c                   eval      rkmel1_kund = rkkund
6.20 c     rkmel1_key    chain     rkmel1                             61
      *
      * Sjekk om kredittgrense
      *
     c                   if        rkgrns <= *zero
     c                   goto      neste
     c                   endif
      *
      * Gjør om beløp til hele kroner
      *
     c                   eval(h)   wkgrns = rkgrns * 1000
     c                   eval(h)   wkmems = rkmems * w_mvat
     c                   eval(h)   wksald = rksald
     c                   eval      wsumsa = wkmems + wksald
      *
      * Sjekker om overskredet kr.grense
      *
     c                   if        wsumsa <= wkgrns
     c                   goto      neste
     c                   endif
      *
      * Sjekker om overflow
      *
     c                   if        *in30 = *on                                               .
     c                   write     a1hdr                                                     .
     c                   eval      *in30 = *off                                              .
     c                   endif                                                           .....
      *
      *
     c                   write     d1det
      *
     c     neste         tag
      *
     c     rkunl1_key    reade     rkunl1r                                60
     c                   enddo
      *
     c     slutt         tag
     c                   eval      *inlr = *on
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
6.20  *
6.20 c     rkmel1_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    rkmel1_kund
      *
     c                   move      l_firm        w_firm
      *
      * Hent mva % for beregning av memosaldo inkl. mva
      *
     c                   eval      w_stat = *blank
     c                   eval      w_mvak = '3'
     c                   time                    w_date
      *
     c                   call      'RS205R'
     c                   parm                    w_stat
     c                   parm                    w_mvak
     c                   parm                    w_mvtx
     c                   parm                    w_date
     c                   parm                    w_mvas
     c                   parm                    w_mvat
     c                   parm                    w_mvaf
     c                   parm                    w_bpro
      *
     c                   write     a1hdr                                                     .
      *
     c                   endsr
      *
