     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LI740R
      * Beskrivelse . . : Forsøker å linke bestilllinger med EDI meldinger
      *
      * Spesialversjon. : Mestergruppen
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
7.00  *       171019 bsa  Nytt program
      *
8.01  *K0000  240621 BSA  Utvidet meldingsnummer fra 6-8 siffer
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fledilr    uf   e           k disk    rename(ledipfr:ledilrr)
     fledil1    if   e           k disk    rename(ledipfr:ledil1r)
     f                                     prefix(xx:2)
     fledil3    if   e           k disk    rename(ledipfr:ledil3r)
     flohelr    if   e           k disk    rename(lohepfr:lohelrr)
      *
      * Definering av Local Data Arae (LDA)
      *
     d                uds
     d l_user                911    920
     d l_fnav                951    980
      *
      * Parameter-inn
      *
     d p_inlr          s              1
     d p_stat          s              1
     d p_type          s                   like(litype)
     d p_numm          s                   like(lonumm)
     d p_suff          s                   like(losuff)
      *
      * FAKTURA Hode
      *
     d d_fakh1         ds
     d  d_fh_dtyp                     3
     d  d_fh_fanr                    35
     d  d_fh_fako                     3
     d  d_fh_ldat                      d   datfmt(*iso)
     d  d_fh_best                    20
     d  d_fh_lord                    20
     d  d_fh_lfak                    20
     d  d_fh_sean                    13  0
     d  d_fh_snav                    30
     d  d_fh_fean                    13  0
     d  d_fh_fnav                    30
     d  d_fh_fad1                    30
     d  d_fh_fad2                    30
     d  d_fh_fste                    30
     d  d_fh_forg                    20
     d  d_fh_kref                    20
     d  d_fh_lref                    20
     d  d_fh_valk                     3
     d  d_fh_ffda                      d   datfmt(*iso)
     d  d_fh_levb                     3
     d  d_fh_forf                      d   datfmt(*iso)
     d  d_fh_bean                    13  0
     d  d_fh_biln                     8  0
      *
      * Tillegg/fradragslinjer
      *
     d d_fakh2         ds
     d  d_fh_tsig                     3
     d  d_fh_tkod                     3
     d  d_fh_ttek                    30
     d  d_fh_tpro                     4  2
     d  d_fh_tbel                     8  2
     d  d_fh_apro                     4  2
      *
      * FAKTURA Linje
      *
     d d_fakl          ds
     d  d_fl_llin                     6  0
     d  d_fl_vean                    13  0
     d  d_fl_vnob                     8  0
     d  d_fl_vlev                    15
     d  d_fl_tek1                    35
     d  d_fl_tek2                    35
     d  d_fl_bkva                    15  3
     d  d_fl_benh                     3
     d  d_fl_lkva                    15  3
     d  d_fl_lenh                     3
     d  d_fl_fkva                    15  3
     d  d_fl_fenh                     3
     d  d_fl_bnet                    15  2
     d  d_fl_bbto                    15  2
     d  d_fl_prkv                     3
     d  d_fl_pris                    15  3
     d  d_fl_penh                     3
     d  d_fl_prpr                     9  0
     d  d_fl_bnum                    20
     d  d_fl_blin                     6  0
     d  d_fl_mvap                     4  2
     d  d_fl_sig1                     3
     d  d_fl_kod1                     3
     d  d_fl_bel1                     9  2
     d  d_fl_pro1                     5  2
     d  d_fl_sig2                     3
     d  d_fl_kod2                     3
     d  d_fl_bel2                     9  2
     d  d_fl_pro2                     5  2
     d  d_fl_sig3                     3
     d  d_fl_kod3                     3
     d  d_fl_bel3                     9  2
     d  d_fl_pro3                     5  2
     d  d_fl_sig4                     3
     d  d_fl_kod4                     3
     d  d_fl_bel4                     9  2
     d  d_fl_pro4                     5  2
     d  d_fl_sig5                     3
     d  d_fl_kod5                     3
     d  d_fl_bel5                     9  2
     d  d_fl_pro5                     5  2
     d  d_fl_sig6                     3
     d  d_fl_kod6                     3
     d  d_fl_bel6                     9  2
     d  d_fl_pro6                     5  2
     d  d_fl_vevr                    15
      *
      * Fakturatotallinjer
      *
     d d_fakt          ds
     d  d_ft_ftot                    15  2
     d  d_ft_teks                    15  2
     d  d_ft_tmva                    15  2
     d  d_ft_mvgr                    15  2
     d  d_ft_veks                    15  2
     d  d_ft_vink                    15  2
     d  d_ft_stil                    15  2
     d  d_ft_sfra                    15  2
      *
      * ORDREBEKREFTELSE Hode
      *
     d d_obkh          ds
     d  d_oh_obnr                    35
     d  d_oh_olda                      d   datfmt(*iso)
     d  d_oh_llda                      d   datfmt(*iso)
     d  d_oh_sean                    13  0
     d  d_oh_bean                    13  0
     d  d_oh_fean                    13  0
     d  d_oh_kref                    30
     d  d_oh_aksp                     3
     d  d_oh_valk                     3
     d  d_oh_ttyp                     3
     d  d_oh_fmat                     3
     d  d_oh_lbet                     3
     d  d_oh_lste                    13  0
     d  d_oh_txt1                    70
     d  d_oh_txt2                    70
     d  d_oh_txt3                    70
      *
      * ORDREBEKREFTELSE Tilegg/fradragslinjer
      *
     d d_obkh2         ds
     d  d_oh_ftkv                     3
     d  d_oh_tsek                     3
     d  d_oh_ftty                     3
     d  d_oh_fttx                    30
     d  d_oh_tbel                    17  2
      *
      * ORDREBEKREFTELSE Linje
      *
     d d_obkl          ds
     d  d_ol_llin                     6  0
     d  d_ol_laks                     3
     d  d_ol_vean                    14  0
     d  d_ol_vnob                     8  0
     d  d_ol_vlev                    15
     d  d_ol_bkva                    15  3
     d  d_ol_benh                     3
     d  d_ol_lkva                    15  3
     d  d_ol_lenh                     3
     d  d_ol_llda                      d   datfmt(*iso)
     d  d_ol_pris                    15  3
     d  d_ol_penh                     3
     d  d_ol_prpr                     9  0
     d  d_ol_prkv                     3
     d  d_ol_epri                    15  3
     d  d_ol_evar                     8  0
     d  d_ol_linr                     6  0
     d  d_ol_linb                    15  3
     d  d_ol_mvap                     4  2
     d  d_ol_tkv1                     3
     d  d_ol_tty1                     3
     d  d_ol_ttx1                    35
     d  d_ol_tse1                     3
     d  d_ol_tme1                    11  3
     d  d_ol_ten1                     3
     d  d_ol_tpr1                     5  2
     d  d_ol_tbe1                    15  2
     d  d_ol_tkv2                     3
     d  d_ol_tty2                     3
     d  d_ol_ttx2                    35
     d  d_ol_tse2                     3
     d  d_ol_tme2                    11  3
     d  d_ol_ten2                     3
     d  d_ol_tpr2                     5  2
     d  d_ol_tbe2                    15  2
     d  d_ol_txt1                    35
     d  d_ol_txt2                    35
      *
      * ORDREBEKREFTELSE Ordretotaler
      *
     d d_obks          ds
     d  d_os_suml                    17  2
     d  d_os_ntot                    17  2
     d  d_os_mvag                    17  2
     d  d_os_mvat                    17  2
     d  d_os_btot                    17  2
     d  d_os_tilt                    17  2
     d  d_os_frat                    17  2
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d lohelr_numm     s                   like(lonumm)
     d lohelr_suff     s                   like(losuff)
     d ledil3_type     s                   like(litype)
     d ledil3_numm     s                   like(linumm)
     d ledil3_suff     s                   like(lisuff)
     d ledil3_line     s                   like(liline)
     d ledil3_mnum     s                   like(limnum)
     d ledil3_mlin     s                   like(limlin)
     d ledil1_type     s                   like(litype)
     d ledil1_mnum     s                   like(limnum)
     d ledil1_mlin     s                   like(limlin)
     d ledilr_type     s                   like(litype)
     d ledilr_mnum     s                   like(limnum)
     d ledilr_mlin     s                   like(limlin)
      *
      * Definering av variable i parametre
      *
      *
      * Definering av variable
      *
     d w_mnum          s                   like(limnum)
     d w_mlin          s                   like(limlin)
     d w_firm          s                   like(lifirm)
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_year          s              4  0
     d w_diff          s             15  2
     d w_etot          s             15  3
     d w_btot          s             15  3
     d x               s              3  0
      *
      * Definering av konstanter
      *
     d lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
     d digit           c                   '0123456789 '
     d null            c                   '0000000000000'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * D1 Behandle åpningsopplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   if        p_inlr = *on
     c                   goto      avslutt
     c                   endif
      *
      * Les alle edi meldinger som har dette bestillingsnummeret som referanse
      *
     c                   eval      ledil3_type = p_type
     c                   eval      ledil3_numm = p_numm
     c                   eval      ledil3_suff = 0
     c                   eval      ledil3_line = 0
     c                   eval      ledil3_mnum = 0
     c                   eval      ledil3_mlin = 0
     c     ledil3_key    setll     ledil3
     c     ledil3_key2   reade     ledil3
     c                   dow       not %eof
      *
      * Matcher kun edi meldinger som ikke er ferdigbehandlet
      *
     c                   if        %trim(likode) = *blanks and
     c                             limlin = 100001
      *
      * Hent ut totalbeløp
      *
     c                   eval      x = 0
     c                   eval      w_etot = 0
     c                   eval      ledil1_mnum = limnum
     c                   eval      ledil1_mlin = 900001
     c     ledil1_key    chain     ledil1
     c                   if        %found
     c                   if        p_type = 'FAKT'
     c                   eval      d_fakt = xxmeld
     c                   eval      w_etot = d_ft_teks
     c                   else
     c                   eval      d_obks = xxmeld
     c                   if        d_os_ntot <> 0
     c                   eval      w_etot = d_os_ntot
     c                   else
     c                   eval      w_etot = d_os_suml
     c                   endif
     c                   endif
     c                   else
     c                   eval      w_etot = 0
     c                   endif
      *
      * Les bestillinger på samme bestillingsnummer
      *
     c                   eval      w_btot = 0
     c                   eval      w_diff = 0
     c                   eval      lohelr_numm = p_numm
     c                   eval      lohelr_suff = 0
     c     lohelr_key    setll     lohelr
     c     lohelr_key2   reade     lohelr
     c                   dow       not %eof
     c                   eval      x = x + 1
     c                   eval      w_btot = lototi - lototr
     c                   eval      w_diff = w_etot - w_btot
     c                   if        w_diff < 0
     c                   eval      w_diff = w_diff * -1
     c                   endif
      *
      * Hvis differansen er < 10 kroner, sier vi det er match
      *
     c                   if        w_diff < 10
     c                   eval      p_numm = lonumm
     c                   eval      p_suff = losuff
     c                   goto      opd_edi
     c                   endif
      *
     c     lohelr_key2   reade     lohelr
     c                   enddo
      *
     c                   endif
      *
     c     ledil3_key2   reade     ledil3
     c                   enddo
      *
     c                   goto      avslutt
      *
     c     opd_edi       tag
     c                   exsr      opd_edi_sub
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   if        p_inlr = *on
     c                   eval      *inlr = *on
     c                   endif
      *
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdater EDI melding med ny info
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     opd_edi_sub   begsr
      *
     c                   eval      ledilr_mnum = limnum
     c                   eval      ledilr_mlin = 0
     c     ledilr_key    setll     ledilr
     c     ledilr_key2   reade     ledilr
     c                   dow       not %eof
     c                   eval      linumm = lonumm
     c                   eval      lisuff = losuff
     c                   update    ledilrr
     c     ledilr_key2   reade     ledilr
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_type
     c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_stat
     c                   parm                    p_inlr
      *
      *  Key for les av MELDINGSLINJER
      *
     c     ledil1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ledil1_type
     c                   kfld                    ledil1_mnum
     c                   kfld                    ledil1_mlin
      *
      *  Key for les av MELDINGSLINJER
      *
     c     ledil3_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    ledil3_type
     c                   kfld                    ledil3_numm
      *
      *  Key for les av MELDINGSLINJER
      *
     c     ledil3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ledil3_type
     c                   kfld                    ledil3_numm
     c                   kfld                    ledil3_suff
     c                   kfld                    ledil3_line
     c                   kfld                    ledil3_mnum
     c                   kfld                    ledil3_mlin
      *
      *  Key for les av MELDINGSLINJER
      *
     c     ledilr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ledilr_type
     c                   kfld                    ledilr_mnum
     c                   kfld                    ledilr_mlin
      *
      *  Key for les av MELDINGSLINJER
      *
     c     ledilr_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    ledilr_type
     c                   kfld                    ledilr_mnum
      *
      *  Key for les av Bestillinger
      *
     c     lohelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelr_numm
     c                   kfld                    lohelr_suff
      *
      *  Key for les av Bestillinger
      *
     c     lohelr_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    lohelr_numm
      *
      * Henter verdier fra parmametre
      *
     c                   time                    w_date
     c                   time                    w_time
     c                   eval      w_year = %subdt(w_date:*years)
      *
     c                   eval      ledilr_type = p_type
     c                   eval      ledil1_type = p_type
     c                   eval      ledil3_type = p_type
      *
     c                   endsr
