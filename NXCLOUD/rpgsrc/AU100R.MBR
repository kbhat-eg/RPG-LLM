      /title  Notat, vedlikehold
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASPGPL
      * Program . . . . : AU100R
      * Beskrivelse . . : Notat, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       220200 HKO  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fanotl1    if   e           k disk    rename(anotpfr:anotl1r)
     fanotl2    if   e           k disk    rename(anotpfr:anotl2r)
     fanotlu    uf a e           k disk    rename(anotpfr:anotlur)
      *
     fau100d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
      *
      * Definering av parametre
      *
     d p_firm          s                   like(aufirm)
     d p_syst          s                   like(ausyst)
     d p_noid          s                   like(aunoid)
      *
      * Definering av variabler i nøkler
      *
     d anotlu_line     s                   like(auline)
      *
      * Definering av variable
      *
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d b_forn          s              1    inz(*off)
     d w_edat_alf      s              6
      *
     d w_firm          s                   like(aufirm)
     d w_syst          s                   like(ausyst)
     d w_noid          s                   like(aunoid)
     d w_line          s                   like(auline)
     d w_line_max      s                   like(auline)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(44)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_srrn - Relativt recordnummer i subfile
      * w_spge - Sidestørrelse i subfile (page)
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * wvisrn - Vis den side som inneholder denne record.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      dsp_subfile                                  DSP SFL
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile                                  CRT SFL
     c                   goto      b2tagb
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   if        *in80 = *off
     c                   exsr      subfile
     c                   endif
      *
      * F6=Sett inn rader
      *
     c                   if        *inkf = *on
     c                   exsr      insert
     c                   goto      b2tagb
     c                   endif
      *
      * F23=Vis signatur
      *
     c                   if        *inkx = *on
     c                   if        *in80 = *off
     c                   eval      *in80 = *on
     c                   else
     c                   eval      *in80 = *off
     c                   endif
     c                   exsr      forny
     c                   goto      b2taga
     c                   endif
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c     anotl1_key    setll     anotl1
      *
     c                   exsr      clr_subfile                                  TØM SFL
     c                   exsr      crt_subfile                                  CRT SFL
      *
     c     end_forny     endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
      * Initierer linjenummer
      *
     c                   eval      w_line = 0
      *
     c     anotl2_key    chain     anotl2                             91
     c                   if        *in91 = *off
     c                   eval      w_line = auline
     c                   endif
      *
      * Beholder eksisterende posisjonering i subfilen
      *
     c                   if        wcurrn > 0
     c                   eval      wvisrn = wcurrn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   if        b1nota = b1work
     c                   iter
     c                   endif
      *
     c                   eval      wvisrn = w_srrn
      *
      * Sletter notat
      *
     c                   if        b1nota = *blank
     c                   eval      anotlu_line = b1line
     c     anotlu_key    chain     anotlur                            91
     c                   if        *in91 = *off
     c                   delete    anotlur
     c                   endif
     c                   eval      b_forn = *on
     c                   iter
     c                   endif
      *
      * Endrer notat
      *
     c                   if        b1work <> *blank
     c                   eval      anotlu_line = b1line
     c     anotlu_key    chain     anotlur                            91
     c                   if        *in91 = *off
     c                   eval      aunota = b1nota
     c                   eval      aueusr = l_user
     c                   time                    auetim
     c                   time                    auedat
     c                   update    anotlur
     c                   endif
     c                   eval      b1work = b1nota
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Oppretter notat
      *
     c                   eval      w_line = w_line + 10
      *
     c                   eval      aufirm = w_firm
     c                   eval      ausyst = w_syst
     c                   eval      aunoid = w_noid
     c                   eval      auline = w_line
     c                   eval      aunota = b1nota
     c                   eval      aueusr = l_user
      *
     c                   time                    auetim
     c                   time                    auedat
      *
     c                   write     anotlur
      *
     c                   eval      b1work = b1nota
     c                   eval      b1line = auline
     c                   update    b1sfl
     c                   iter
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil                     SFL END
     c                   eval      w_srrn = w_ssrn
      *
     c                   dow       w_srrn < c_sfil
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c     anotl1_key    reade     anotl1                                 15
     c                   if        *in15 = *off                                 EOF
     c                   eval      b1nota = aunota
     c                   eval      b1work = aunota
     c                   eval      b1line = auline
     c                   eval      b1eusr = aueusr
     c     *dmy          move      auedat        b1edat
     c                   if        aunota = *blank
     c                   eval      b1work = 'ny linje'
     c                   endif
     c                   if        *in80 = *on
     c                   move      b1edat        w_edat_alf
     c                   eval      b1nota = %subst(b1nota:1:22) + ' ' +
     c                                      w_edat_alf + ' ' + b1eusr
     c                   endif
     c                   else
     c                   eval      b1nota = *blank
     c                   eval      b1work = *blank
     c                   eval      b1line = 0
     c                   eval      b1eusr = *blank
     c                   eval      b1edat = 0
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   eval      *in15 = *on
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      wvisrn = w_sfrn
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
     c                   eval      *in22 = *off
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for innsetting av rader i notatet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     insert        begsr
      *
     c                   if        wcurrn > 0
     c                   eval      wvisrn = wcurrn
     c                   endif
      *
     c     wcurrn        chain     b1sfl                              91
     c                   if        *in91 = *on or
     c                             b1line = 0
     c                   goto      end_insert
     c                   endif
      *
      * Setter inn rader
      *
     c                   eval      b1line = b1line + 1
      *
     c                   eval      aufirm = w_firm
     c                   eval      ausyst = w_syst
     c                   eval      aunoid = w_noid
     c                   eval      auline = b1line
     c                   eval      aunota = *blank
     c                   eval      aueusr = l_user
     c                   time                    auetim
     c                   time                    auedat
      *
     c                   write     anotlur
      *
     c                   eval      b1line = b1line + 1
     c                   eval      auline = b1line
     c                   write     anotlur
      *
     c                   eval      b1line = b1line + 1
     c                   eval      auline = b1line
     c                   write     anotlur
      *
      * Renumererer linjenummer i notat-filen
      *
     c                   eval      w_line_max = 10
      *
     c     anotl2_key    chain     anotl2                             91
     c                   if        *in91 = *off
     c                   eval      w_line_max = auline
     c                   endif
      *
     c                   eval      w_line = w_line_max
      *
     c     anotlu_key2   setll     anotlu
     c     anotlu_key2   reade     anotlur                                91
     c                   dow       *in91 = *off and
     c                             auline <= w_line_max
     c                   eval      w_line = w_line + 10
     c                   eval      auline = w_line
     c                   update    anotlur
     c     anotlu_key2   reade     anotlur                                91
     c                   enddo
      *
      * Fornyer subfile
      *
     c                   exsr      forny
      *
     c     end_insert    endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_syst
     c                   parm                    p_noid
      *
      * Key for les på notat-register
      *
     c     anotl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_syst
     c                   kfld                    w_noid
      *
      * Key for oppslag på notat-register m/synkende linjenummer
      *
     c     anotl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_syst
     c                   kfld                    w_noid
      *
      * Key for oppdatering av notat-register
      *
     c     anotlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_syst
     c                   kfld                    w_noid
     c                   kfld                    anotlu_line
      *
     c     anotlu_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    w_syst
     c                   kfld                    w_noid
      *
      *
      * Henter firma og notat-id fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_syst = p_syst
     c                   eval      w_noid = p_noid
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c     anotl1_key    setll     anotl1                                                    .
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
