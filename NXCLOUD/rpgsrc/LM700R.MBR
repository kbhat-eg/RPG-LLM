     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : LM700R
      * Beskrivelse . . : Henter inn siste status fra loggfilen
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       160307 ASK  Nytt program
6.nu  *       110614 bof  Sjekket mtp. nummerutvidelse
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
3.32 fllogl2    if   e           k disk    rename(llogpfr:llogl2r)
      *
      * Definering av parametre
      *
     d p_numm          s                   like(lmnumm)
     d p_suff          s                   like(lmsuff)
     d p_kode          s                   like(lmkode)
      *
      * Definering av Local data area (LDA)
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d llogl2_numm     s                   like(lmnumm)
     d llogl2_suff     s                   like(lmsuff)
     d llogl2_date     s                   like(lmdate)
     d llogl2_time     s                   like(lmtime)
      *
      * Definering av variable
      *
     d w_firm          s                   like(lmfirm)
      *
      * --------------------------------------------------------------------
      * Hoved - logikk
      * --------------------------------------------------------------------
      *
      * Finner siste transaksjon i loggfilen
      * og returnerer tilhørende statuskode
      *
     c                   exsr      hent_logg
      *
     c                   goto      avslutt
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av siste transaksjon i logg - file
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_logg     begsr
      *
      * Henter siste post fra logg - filen
      *
     c                   eval      llogl2_numm = p_numm
     c                   eval      llogl2_suff = p_suff
     c                   eval      llogl2_date = *hival
     c                   eval      llogl2_time = *hival
      *
     c     llogl2_key    setll     llogl2r
     c                   readp     llogl2r
      *
     c                   if        %eof
     c                   eval      p_kode = *blank
     c                   goto      end_sr
     c                   endif
      *
     c                   if        lmnumm = p_numm and
     c                             lmsuff = p_suff
     c                   eval      p_kode = lmkode
     c                   endif
      *
     c     end_sr        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_kode
      *
      * Key for logg - filen
      *
     c     llogl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    llogl2_numm
     c                   kfld                    llogl2_suff
     c                   kfld                    llogl2_date
     c                   kfld                    llogl2_time
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
