      /TITLE  ASOFAK Salgs-statistikk, bytter enhet på vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : SF750R
      * Beskrivelse . . : Salgs-statistikk, bytter enhet på vare
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       081199 HKO  Nytt program
8.01  *PRG2   220622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner - COMP
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsstal2    if   e           k disk    rename(sstapfr:sstal2r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     fsstal1    uf   e           k disk    rename(sstapfr:sstal1r)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(sffirm)
     d p_vare          s                   like(sfvare)
     d p_enhe          s                   like(sfenhe)
      *
      * Definering av variable i nøkler
      *
     d sstal2_vare     s                   like(sfvare)
     d vvenl1_vare     s                   like(vevare)
     d vvenl1_enhe     s                   like(veenhe)
     d sstal1_paar     s                   like(sfpaar)
     d sstal1_binr     s                   like(sfbinr)
     d sstal1_line     s                   like(sfline)
     d sstal1_bida     s                   like(sfbida)
      *
      * Definering av variable
      *
     d w_firm          s                   like(sffirm)
     d w_omrs          s                   like(veomre)
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Finner omregningsfaktor volum for ny statistikkenhet
      *
     c                   eval      vvenl1_vare = p_vare
     c                   eval      vvenl1_enhe = p_enhe
     c     vvenl1_key    chain     vvenl1                             90
     c                   if        *in90 = *on or
     c                             veomre = 0
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      w_omrs = veomre
      *
      * Leser alle statistikkrader for vare, og omregner antall i.h.t.
      * statistikkenhet
      *
     c                   eval      sstal2_vare = p_vare
     c     sstal2_key    setll     sstal2
     c     sstal2_key    reade     sstal2                                 90
     c                   dow       *in90 = *off
      *
     c                   if        sfenhe <> p_enhe
      *
     c                   eval      vvenl1_enhe = sfenho
     c     vvenl1_key    chain     vvenl1                             91
     c                   if        *in91 = *off and
     c                             veomre <> 0
      *
     c                   eval      sstal1_paar = sfpaar
     c                   eval      sstal1_binr = sfbinr
     c                   eval      sstal1_line = sfline
     c                   eval      sstal1_bida = sfbida
     c     sstal1_key    chain     sstal1r                            91
     c                   if        *in91 = *off
     c                   eval      sfenhe = p_enhe
     c                   eval(h)   sfanta = sfanto * veomre / w_omrs
     c                   update    sstal1r
     c                   endif
      *
     c                   endif
     c                   endif
      *
     c     sstal2_key    reade     sstal2                                 90
     c                   enddo
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_enhe
      *
      * Key for les på statistikk
      *
     c     sstal2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sstal2_vare
      *
      * Key for oppslag på vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
     c                   kfld                    vvenl1_enhe
      *
      * Key for oppdatering av statistikk
      *
     c     sstal1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sstal1_paar
     c                   kfld                    sstal1_binr
     c                   kfld                    sstal1_line
     c                   kfld                    sstal1_bida
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
