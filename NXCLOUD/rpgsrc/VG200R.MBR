     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : VG200R
      * Beskrivelse . . : Gj.kostpris pr.vare/lager
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       410182 NHO  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvgkpi1    if a e           k disk    rename(vgkpst:vgkpi1r)
     fvgkpir    if a e           k disk    rename(vgkpst:vgkpirr)
     fvgkpiu    uf a e           k disk    rename(vgkpst:vgkpiur)
     fvvarl1    if   e           K disk    rename(vvarpfr:vvarl1r)
     fvgkkir    if   e           k disk    rename(vgkkst:vgkkirr)
      *
     fvg200d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d vgkpiu_lage     s              2  0
     d vgkpiu_hkod     s                   like(vghkod)
     d vgkpiu_vare     s                   like(vgvare)
     d vgkpi1_lage     s              2  0
     d vgkpi1_hkod     s                   like(vghkod)
     d vgkpi1_vare     s                   like(vgvare)
     d vgkpir_lage     s              2  0
     d vgkpir_hkod     s                   like(vghkod)
     d vgkpir_vare     s                   like(vgvare)
     d vvarl1_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_ttim          s              6  0
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(vgfirm)
     d w_vare          s                   like(vgvare)
     d w_var1          s                   like(vvvare)
     d w_tek1          s             35
     d w_lage          s                   like(vglage)
     d w_ltek          s             30
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste recordnummer på siden
      * w_curn - Current recordnummer på siden
      * w_virn - Siden som skal vises
      * w_cfld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * C1MSG  - C1 Meldingsbilde
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      sok
     c                   goto      b2tagb
      *
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   eval      c2vare = 0
     c                   exsr      xc2bld
     c                   exsr      forny
     c                   goto      b2taga
     c                   when      *inkg
     c                   exsr      kp_fakt
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2vare <> *blanks
     c                   exsr      posisjoner
     c                   goto      b2taga
     c                   endif
      *
     c                   if        b2lage <> 0 and
     c                             b2lage <> w_lage
     c                   eval      w_lage = b2lage
     c                   exsr      posisjoner
     c                   goto      b2taga
     c                   endif
      *
      * Setter lagerverdi i nøkler
      *
     c                   eval      vgkpiu_lage = w_lage
     c                   eval      vgkpi1_lage = w_lage
     c                   eval      vgkpir_lage = w_lage
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_sfrn <> 0
     c     w_sfrn        chain     b1sfl
     c                   endif
      *
     c                   eval      vgkpi1_lage = b2lage
     c                   eval      vgkpi1_vare = b1vare
     c     vgkpi1_key    setll     vgkpi1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Varenummer
      *
     c                   if        b2vare <> *blanks
     c                   eval      w_seqe = *blank
     c                   eval      vgkpi1_vare = b2vare
     c     vgkpi1_key    setll     vgkpi1
     c                   goto      end_pos
     c                   endif
      *
     c                   if        b2lage <> 0
     c                   eval      w_seqe = *blank
     c                   eval      vgkpi1_lage = b2lage
     c     vgkpi1_key    setll     vgkpi1
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2vare = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      * Endre kostpris
     c                   if        b1valg = 2
     c                   movel     b1vare        c2vare
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   eval      b2vare = ' '
     c                   update    b1sfl
     c                   iter
     c                   endif
      * Vis
     c                   if        b1valg = 5
     c                   movel     b1vare        c2vare
     c                   eval      *in31 = *on
     c                   eval      *in30 = *on
     c                   exsr      xc2bld
     c                   eval      *in31 = *off
     c                   eval      *in30 = *off
     c                   eval      b1valg = 0
     c                   eval      b2vare = ' '
     c                   update    b1sfl
     c                   iter
     c                   endif
      * Vis historikk
     c                   if        b1valg = 7
     c                   movel     b1vare        w_vare
     c                   exsr      vis_historikk
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      vgkpir_hkod = *blank
     c                   movel     c2vare        vgkpir_vare
     c     vgkpir_key    chain     vgkpir
      *
     c                   if        %found
     c                   movel     vgvare        c2vare
     c                   eval      c2busr = vgbusr
     c                   eval      c2enhe = vgenhe
     c                   eval      c2type = vgtype
     c                   eval      c2cost = vgcost
     c                   eval      c2best = vgbest
     c                   eval      c2suff = vgsuff
     c                   eval      c2line = vgline
     c                   eval      c2gmkp = vggmkp
     c                   eval      c2inkp = vginkp
     c                   eval      c2pkod = '(' + vgpkod + ')'
     c                   eval      c2behl = vgbehl
     c                   eval      c2anta = vganta
     c     *hms          move      vgttim        w_ttim
     c                   movel     w_ttim        c2ttim
     c                   eval      c2type = vgtype
      *
      * Hent varetekst
      *
     c                   movel     c2vare        w_var1
     c                   exsr      les_vare
     c                   eval      c2vtek = vvtek1
      *
      * Dato gjeldene fra
      *
     c                   if        vggdat = *loval
     c                   eval      c2gdat = *zeros
     c                   else
     c     *dmy          move      vggdat        c2gdat
     c                   endif
      *
      * Dato beregnet
     c                   if        vgbdat = *loval
     c                   eval      c2bdat = *zeros
     c                   else
     c     *dmy          move      vgbdat        c2bdat
     c                   endif
      *
     c                   else
     c                   eval      c2vare = 0
     c                   eval      c2gdat = *zeros
     c                   eval      c2enhe = *blank
     c                   eval      c2vtek = *blank
     c                   eval      c2cost = *zeros
     c                   eval      c2bdat = *zeros
     c                   eval      c2type = *blank
     c                   eval      c2ttim = *zeros
     c                   eval      c2busr = *blank
     c                   endif
      *
     c                   if        b1valg = 2
     c                   eval      *in30 = *on
     c                   eval      c2cost = 0
     c                   eval      c2gdat = *zeros
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in31 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in31
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
     c                   if        *inkd
      * Varenummer
      *
     c                   if        w_afld = 'C2VARE'
     c                   exsr      sok_vare
     c                   movel     w_vare        c2vare
     c                   movel     c2vare        w_vare
     c                   movel     c2vare        w_var1
     c                   exsr      les_vare
     c                   eval      c2vtek = vvtek1
     c                   eval      c2enhe = vvenhl
     c                   eval      c2type = 'M'
     c                   goto      c2taga
     c                   endif
      *
     c                   endif
      *
      *  Sjekk, Finnes vare i vare registeret
      *
     c                   movel     c2vare        w_var1
     c                   exsr      les_vare
     c                   if        b_feil = *on
     c                   exfmt     c1msg
     c                   goto      c2taga
     c                   else
     c                   eval      c2enhe = vvenhl
     c                   eval      c2type = 'M'
     c                   endif
      *
      *  Sjekk,   dato, kan ikke være blank
      *
     c                   if        c2gdat = 0
     c                   exfmt     c2msg
     c                   goto      c2taga
     c                   endif
      *
      * Sjekk  kostpris
      *
     c                   if        c2cost = *zeros
     c                   exfmt     c3msg
     c                   goto      c2taga
     c                   endif
      *
      * Oppdatering
      *
     c                   eval      vgkpiu_hkod = *blank
     c                   movel     c2vare        vgkpiu_vare
     c     vgkpiu_key    chain     vgkpiu
      *
     c                   if        %found
     c                   exsr      pris_hist
     c                   end
      *
     c                   clear                   vgkpiur
     c                   eval      vgfirm = w_firm
     c                   eval      vglage = w_lage
     c                   eval      vghkod = ' '
     c                   movel     c2vare        vgvare
     c     *dmy          move      c2gdat        vggdat
     c                   time                    vggtim
     c                   eval      vgenhe = c2enhe
     c                   eval      vgtype = c2type
     c                   eval      vgcost = c2cost
      *
     c                   time                    vgttim
     c                   time                    vgbdat
     c                   eval      vgbusr = l_user
      *
     c                   write     vgkpiur
      *
      * Oppdater subfile
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 2
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      vgkpi1
      *
     c                   if        %eof or
     c                             vgfirm <> w_firm or
     c                             vglage <> w_lage or
     c                             vghkod = 'H'
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      vgkpi1_vare = vgvare
      *
     c                   eval      b1valg = 0
     c                   eval      b1vare = vgvare
     c                   movel     b1vare        w_var1
     c                   exsr      les_vare
     c                   movel     vvtek1        b1vtek
      * Dato gjeldene fra
     c                   if        vggdat <> *loval
     c     *dmy          move      vggdat        b1gdat
     c                   endif
      *
     c                   eval      b1enhe = vgenhe
     c                   eval      b1type = vgtype
     c                   eval      b1cost = vgcost
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     vgkpi1_key    setgt     vgkpi1
     c                   readp     vgkpi1
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     vgkpi1
      *
     c                   if        %eof or
     c                             vgfirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   eval      vgkpi1_vare = vgvare
      *
     c                   enddo
      *
     c                   if        %eof
     c     vgkpi1_key    setll     vgkpi1
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppslag på vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_vare      begsr
      *
     c                   eval      vvarl1_vare = w_var1
     c     vvarl1_key    chain     vvarl1r
     c                   if        %found
     c                   eval      b_feil = *off
     c                   else
     c                   eval      b_feil = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutiner for søking
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sok           begsr
     c                   if        w_afld = 'B2VARE'
     c                   eval      w_vare = *blank
     c                   exsr      sok_vare
     c                   movel     w_vare        b2vare
     c                   endif
      *
     c                   if        w_afld = 'B2LAGE'
     c                   eval      w_lage = 0
     c                   exsr      sok_lager
     c                   eval      b2lage = w_lage
     c                   eval      b2latx = w_ltek
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
     c     sok_vare      begsr
      *
     c                   call      'VV500R'
     C                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_tek1
      *
     c                   endsr
      *
     c     sok_lager     begsr
      *
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    w_lage
     c                   parm                    w_ltek
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kostpris historikk
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     pris_hist     begsr
      *
      * Sletter opprinnelig post
      *
     c                   delete    vgkpiur
      *
      * Oppretter historikk post
      *
     c                   eval      vghkod = 'H'
      *
     c                   write     vgkpiur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å vise kostpris historikk
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     vis_historikk begsr
      *
     c                   call      'VG201R'
     c                   parm                    w_vare
     c                   parm                    w_lage
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for vedlikehold av kostprisfaktorer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kp_fakt       begsr
      *
     c                   call      'VG220R'
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for oppdatering av kostpris
      *
     c     vgkpiu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vgkpiu_lage
     c                   kfld                    vgkpiu_hkod
     c                   kfld                    vgkpiu_vare
      *
      * Key for oppslag av kostpris
      *
     c     vgkpi1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vgkpi1_lage
     c                   kfld                    vgkpi1_hkod
     c                   kfld                    vgkpi1_vare
      *
      * Key for les av kostpris
      *
     c     vgkpir_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vgkpir_lage
     c                   kfld                    vgkpir_hkod
     c                   kfld                    vgkpir_vare
      *
      *  Key for file les av vareregister
      *
     c     vvarl1_key    KLIST
     c                   KFLD                    w_firm
     c                   KFLD                    vvarl1_vare
      *
      * Key for kostpris parameter
      *
     c     vgkkir_key    klist
     c                   kfld                    w_firm
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
     c                   eval      vgkpiu_lage = 0
     c                   eval      vgkpi1_lage = 0
     c                   eval      vgkpir_lage = 0
     c                   eval      w_lage = 0
      *
     c                   eval      *in22 = *on
      *
      * Slår opp i koderegister og henter standardverdier
      *
     c     vgkkir_key    chain     vgkkir
      *
     c                   if        vgprlg = 'N'
     c                   eval      *in35 = *on
     c                   else
     c                   eval      *in35 = *off
     c                   endif
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      vgkpi1_vare = *blank
     c                   eval      vgkpi1_hkod = *blank
     c     vgkpiu_key    setll     vgkpiu
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
