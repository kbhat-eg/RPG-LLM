     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : HR101R
      * Beskrivelse . . : Radioterminal trans, ekspedisjon - registrer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       250900 HKO  Nytt program
      *       121200 HKO  Lagt inn kundeprosjekt og "field exit"
      *       210301 HKO  Lagt inn lev.adr, kundens ref. og rekvisisjonsnr.
      *       230301 HKO  Tar vare på søkestring mot vare
      *       150601 HKO  Rettet feil ved endring av enhet på salg
      *       111001 HKO  Lagt inn logikk rundt selgernummer
3.31  *       190901 AMD  Funksjon for å kunne kjøre pakkseddel direkte
3.32  *       150402 AMD  Lagt inn kontroll på at kundenr. må være oppgitt
3.32  *                   ved direkte pakkseddel.
      *       161203 HKO  Endret parameter ved kall til VP900R
      *       120804 HKO  Endret parameter ved kall til VP900R
      *       041104 HKO  Endret parameter ved kall til VP900R
      *       201204 HKO  Lagt inn ny logikk for scanning
      *       060405 HKO  Viser antall varelinjer
      *       020805 HKO  Endret les mot vare-enhet for å finne salgsenheter
      *       060307 bø   Hvis enhet på ean - skal denne foreslås.
      *       011107 bø   Hvis vare ikke finnes ved scanning - legg i JVSCAPF
      *       181207 bø   Tester på selger og kundens ref.
5.70  * PRGR  021208 bof  Utvidet med prisgruppe
5.61  *       080109 bø   Kaller mot cl ved scanne-sjekk
6.11  * 6.10  130709 bof  Utivdet eannr til 14 og brukt std. oppslag
6.13  * 6.10  151210 ask  Lagt inn transer til hjelpefil for å kunne
      *                   selektere ekspedisjoner pr. lager i kassa.
6.14  * 6.10  080911 bof  Ekstra sjekk før pakkseddel kjøre om ref.finnes
6.15  * 6.10  290911 amd  Variabel sprekker når saldo og memosaldo blir
6.15  *                   større enn 9.999.999,99 Øker størrelsen
6.21  * 6.20  101110 bof  Utvidet med leverandør på lager
6.22  * 6.20  061210 bof  Gir melding hvis skaffevare
6.23  * 6.20  100111 bof  Godtar 0 i spm. om pakkseddel
6.24  * 6.20  080311 nho  Sjekk av skaffevare (Felt FAALBE)
      *                   0 = ingen sjekk
      *                   1 = melding, men kan komme videre
      *                   2 = melding, kommer ikke videre
      *                   3 = melding, kommer videre i HR og kasse, ikke ordre
6.25  * 6.20  190511 bof  Utvidet vl710r med utg.dato og lev.nr
6.26  * V6.20 040511 AMD  Flere muligheter rundt kreditt-alternativer
6.27  * V6.20 140611 bsa  Memosaldo ligger på eget register
6.28  * V6.20 210212 bsa  Legger ut avd og lager på HOHE. Fjerner bruk
6.28  *                   av HOHX.(ikke ferdigstilt enda)
6.29  * 6.20  290212 bof  Justert hente pris ihht. lev.nr på lager
6.2A  * 6.20  060612 amd  Lagt inn firmastyrt validering på felt.
6.nu  * 6.30  170614 bof  Endring vedr. nummerutvidelse
6.30  * 6.30  080814 bsa  Sjekker om det er kortkunde med fullmakt.
6.30  *                   Da kan det ikke skrives pakkseddel. Gjør opp
6.30  *                   i kassa.
6.31  * 6.30  261115 bsa  Sjekker mot feil kortkode.
6.32  *       070916 nho  Rekompilert pga rabattfelt på bildet endret
6.32  *                   til 5 posisjoner
6.33  * 6.30  170918 bkv  Utvidet med trelast sortiment
8.01  *PRG2   130622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.2A favall1    if   e           k disk    rename(avalpfr:avall1r)
     fhrtrl2    if   e           k disk    rename(hrtrpfr:hrtrl2r)
     fhrtrlr    if   e           k disk    rename(hrtrpfr:hrtrlrr)
     fhrtrlu    uf a e           k disk    rename(hrtrpfr:hrtrlur)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvarl4    if   e           k disk    rename(vvarpfr:vvarl4r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fvvenl2    if   e           k disk    rename(vvenpfr:vvenl2r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     ffusrl1    if   e           k disk    rename(fusrpfr:fusrl1r)
3.31 fbwsdl1    if   e           k disk    rename(bwsdpfr:bwsdl1r)
3.31 frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
6.27 frkmel1    if   e           k disk    rename(rkmepfr:rkmel1r)
6.14 fhohel1    if   e           k disk    rename(hohepfr:hohel1r)
     fhohelu    uf a e           k disk    rename(hohepfr:hohelur)
     fhodtlu    uf a e           k disk    rename(hodtpfr:hodtlur)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
6.13 fausrlr    if   e           k disk    rename(ausrpfr:ausrlrr)
6.13 fhohxlu    uf a e           k disk    rename(hohxpf:hohxpfr)
6.30 frukrl5    if   e           k disk    rename(rukrpfr:rukrl5r)
6.33 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
     fhr101d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(hrfirm)
     d p_kode          s                   like(hrkode)
     d p_tims          s                   like(hrtims)
     d p_kund          s                   like(hokund)
     d p_kpro          s                   like(hokpro)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
6.21 d l_lage               1001   1002  0
      *
      * Datastruktur - EAN128
      *
     d                 ds
     d d_e128                        26
6.11 d  d_fast                        2  0 overlay(d_e128)
6.11 d  d_eann                       14  0 overlay(d_e128:3)
     d  d_kode                        4  0 overlay(d_e128:17)
     d  d_anta                        6  2 overlay(d_e128:21)
      *
      * Datastruktur - parametre inn til FOHNPR
      *
     d                 ds
     d hirec                        120
     d  hifirm                        3  0 overlay(hirec)
     d  hirkat                        3  0 overlay(hirec:4)
     d  hikund                        6  0 overlay(hirec:7)
     d  hikpro                        6  0 overlay(hirec:13)
     d  hivare                       15    overlay(hirec:19)
     d  hilety                        1  0 overlay(hirec:34)
     d  hienhe                        3    overlay(hirec:35)
     d  hidato                         d   overlay(hirec:38) datfmt(*iso)
     d  hitime                         t   overlay(hirec:48) timfmt(*iso)
6.nu d  hinumm                        8  0 overlay(hirec:56)
6.nu d  hikode                        1    overlay(hirec:64)
6.nu d  hidekn                        5  2 overlay(hirec:65)
6.nu d  hiavgf                        1  0 overlay(hirec:70)
6.nu d  hipriv                        1  0 overlay(hirec:71)
6.nu d  hitilb                        1    overlay(hirec:72)
6.nu d  hiskaf                        1    overlay(hirec:73)
6.nu d  hildor                        6  0 overlay(hirec:74)
6.nu d  hisapr                        9  2 overlay(hirec:80)
6.nu d  hikopr                        9  2 overlay(hirec:89)
8.01 d  hiprgr                        2    overlay(hirec:98)
     d  hiinlr                        1    overlay(hirec:120)
      *
      * Datastruktur - parametre ut fra VP900R
      *
     d                 ds
     d horec                         80
     d  hokpri                        1    overlay(horec:2)
     d  hosapr                        9  2 overlay(horec:12)
     d  horab1                        5  2 overlay(horec:30)
     d  horab2                        5  2 overlay(horec:35)
8.01 d  hoprgr                        2    overlay(horec:71)
      *
      * Definering av variabler i nøkler
      *
6.2A d avall1_apgm     s                   like(avapgm)
     d hrtrlr_vare     s                   like(hrvare)
     d hrtrlr_enhe     s                   like(hrenhe)
     d hrtrlu_vare     s                   like(hrvare)
     d hrtrlu_enhe     s                   like(hrenhe)
6.14 d hohel1_refn     s                   like(horefn)
     d vvarl1_vare     s                   like(vvvare)
     d vvarl4_lvar     s                   like(vvlvar)
     d rlevl1_levr     s                   like(rllevr)
     d vvenl2_vare     s                   like(vevare)
     d vvenl2_saen     s                   like(vesaen)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
     d fusrl1_user     s                   like(fbuser)
3.31 d bwsdl1_wsid     s                   like(bwwsid)
3.31 d rkunl1_kund     s                   like(rkkund)
6.13 d ausrlr_user     s                   like(abuser)
6.30 d rukrl5_ktyp     s                   like(ruktyp)
6.30 d rukrl5_kkun     s                   like(rukkun)
6.30 d rukrl5_kpro     s                   like(rukpro)
      *
      * Definering av variable
      *
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
3.31 d w_stat          s              1    inz(*off)
     d b_scan          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_oppr          s              1    inz(*off)
     d b_skaf          s              1    inz(*off)
     d b_inlr          s              1    inz(*off)
      *
     d w_udat          s               d   datfmt(*iso)
     d w_scan          s             26
5.61 d w_sctx          s             30
     d w_leng          s              2  0
     d w_posi          s              2  0
     d w_vare          s             15
6.11 d w_eanx          s             14
     d w_mvap          s              6  2
     d w_mvat          s              5  4
     d w_mvaf          s             10  9
     d w_mvak          s              1  0
     d w_kod2          s              1
     d w_fell          s              6
     d w_type          s              2
     d w_fast          s             10
     d w_stek          s             35
3.31 d w_gtxt          s             20
3.31 d w_itxt          s             20
3.31 d w_jtxt          s             20
8.01 d w_prgr          s              2
6.21 d w_lage          s              2  0
     d x               s              1  0
6.22 d w_vtyp          s                   like(vvvtyp)
6.nu d w_numm          s              8  0
      *
     d w_firm          s                   like(hrfirm)
     d w_kode          s                   like(hrkode)
     d w_tims          s                   like(hrtims)
     d w_enhe          s                   like(hrenhe)
     d w_anta          s                   like(hranta)
     d w_dato          s                   like(hrdato)
     d w_time          s                   like(hrtime)
6.15 d w_grns          s             11  2
6.15 d w_sald          s             11  2
     d w_anta_kalk     s                   like(hranta)
6.11 d w_xenhe         s                   like(hrenhe)
6.11 d w_eann          s             14  0
6.11 d w_eava          s                   like(vvvare)
6.11 d w_eaen          s                   like(vvenhe)
6.11 d w_east          s              1
6.21 d w_ldor          s                   like(vvldor)
6.25 d w_utda          s               d   datfmt(*iso)
6.33 d w_lv_nobb       s                   like(vvlnob)
6.33 d w_lv_modn       s                   like(vvlmod)
6.33 d w_lv_lldo       s                   like(vvlnle)
6.33 d w_lv_llva       s                   like(vvllva)
6.33 d w_lv_vare       s                   like(vvlvnr)
6.2A  *
6.2A  * Firmastyrt validering av felt
6.2A  *
6.2A d u_dref          s              1    inz(*off)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(6)
     d c_digi          c                   '0123456789 '
     d c_null          c                   '0000000000000'
      *
     d lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_srrn - Relativt recordnummer i subfile
      * w_spge - Sidestørrelse i subfile (page)
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * wvisrn - Vis den side som inneholder denne record.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1BLD  - D1 Skjermbilde for slette (Delete)
      * E1BLD  - E1 Skjermbilde for innlegging av tilleggsinformasjon
      * F1BLD  - F1 Skjermbilde for avslutting av ekspedisjon
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.33 C/Exec SQL
6.33 C+  Set Option DatFmt=*ISO
6.33 C/End-Exec
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      summer_salg
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   eval      *in22 = *on
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   eval      *in22 = *on
     c                   goto      b2tagb
     c                   endsl
      *
      * F4=Spørring på vare
      *
     c                   if        *inkd
     c                   eval      b2sca1 = *blank
     c                   eval      b2sca2 = *blank
     c                   call      'VV580R'
     c                   parm                    w_firm
     c                   parm                    b2sca1
     c                   parm                    w_stek
     c                   eval      *in22 = *on
     c                   if        b2sca1 = *blank
     c                   goto      b2tagb
     c                   endif
     c                   endif
      *
      * Behandling av scanning
      *
     c                   if        b2sca1 <> *blank or
     c                             b2sca2 <> *blank
     c                   exsr      scanning
     c                   eval      *in22 = *on
     c                   if        b_feil = *on
     c                   goto      b2tagb
     c                   else
     c                   goto      b2taga
     c                   endif
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
      * Oppretter ekspedisjon i trans-fil
      *
     c                   exsr      opprett
     c                   if        b_oppr = *off
     c                   goto      b2taga
     c                   endif
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av scanning
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     scanning      begsr
      *
      * Identifisering av vare foregår etter følgende rekkefølge, hvor
      * lengden på varenummeret, og om varenummer er alfa eller numerisk
      * er avgjørende:
      *
      * 1) alfa:
      *      - Lev. varenummer (første 15 pos.)
      * 2) num + lengde = 26:
      *      - EAN-128
      * 3) num + lengde <= 8:
      *      - Varenr
6.11  * 4) num + lengde <= 14:
      *      - EAN-nummer
      * 5) num + lengde < 26:
      *      - Lev. varenummer (første 15 pos.)
      *
     c                   eval      b_feil = *off
     c                   eval      b_scan = *on
      *
     c                   eval      c1enhe = *blank
     c                   eval      w_anta = 0
      *
     c                   eval      w_scan = %trim(b2sca1) + %trim(b2sca2)
      *
     c                   eval      w_leng = %len(%trim(w_scan))
      *
      * Sjekker om scannefelt inneholder alfa-verdi
      *
     c     c_digi        check     w_scan        w_posi
      *
      * Behandling dersom alfa
      *
     c                   if        w_posi > 0
      *
6.33  * sjekker om logistikk-vare
6.33 c                   eval      w_lv_vare = *blank
6.33 c/exec sql
6.33 c+ select vvlvnr
6.33 c+ into   :w_lv_vare
6.33 c+ from   vvlepf
6.33 c+ where  vvllva = :w_scan
6.33 c+ fetch first 1 rows only
6.33 c/end-exec
6.33 c                   if        w_lv_vare <> *blank
6.33 c                   eval      b_skaf  = *off
6.33 c                   eval      vvvare = w_lv_vare
6.33 c                   exsr      sjk_skaff
6.33 c                   if        b_skaf  = *on
6.33 c                   goto      end_scan
6.33 c                   endif
6.33 c                   endif
      *
     c                   eval      vvarl4_lvar = w_scan
     c     vvarl4_key    chain     vvarl4
     c                   if        %found
6.22 c                   eval      b_skaf  = *off
6.22 c                   exsr      sjk_skaff
6.22 c                   if        b_skaf  = *on
6.22 c                   goto      end_scan
6.22 c                   endif
     c***                eval      c1vare = vvvare
     c                   movel     vvvare        c1vare
     c                   exsr      xc2bld
     c                   goto      end_scan
     c                   endif
      *
5.61 c                   call      'LL871C '
     c                   parm                    w_scan
5.61 c                   parm                    w_sctx
      *
     c                   eval      b_feil = *on
     c                   eval      *in31 = *on
     c                   goto      end_scan
      *
     c                   endif
      *
      * Behandling dersom lengde = 26
      *
     c                   if        w_leng = 26
      *
     c                   eval      w_xenhe = *blank
     c                   eval      d_e128 = w_scan
6.11 c                   eval      w_eann = d_eann
6.11 c                   call      'VE710R  '
6.11 c                   parm                    w_firm
6.11 c                   parm                    w_eann
6.11 c                   parm                    w_eava
6.11 c                   parm                    w_eaen
6.11 c                   parm                    w_east
6.11 c                   if        w_east = '1'
6.11 c                   eval      w_xenhe    =  w_eaen
6.11 c                   eval      vvarl1_vare = w_eava
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
6.22 c                   eval      b_skaf  = *off
6.22 c                   exsr      sjk_skaff
6.22 c                   if        b_skaf  = *on
6.22 c                   goto      end_scan
6.22 c                   endif
     c***                eval      c1vare = vvvare
     c                   movel     vvvare        c1vare
     c                   eval      w_anta = d_anta
     c                   exsr      xc2bld
     c                   goto      end_scan
     c                   endif
     c                   endif
      *
5.61 c                   call      'LL871C '
     c                   parm                    w_scan
5.61 c                   parm                    w_sctx
      *
     c                   eval      b_feil = *on
     c                   eval      *in31 = *on
     c                   goto      end_scan
      *
     c                   endif
      *
      * Behandling dersom lengde <= 8
      *
     c                   if        w_leng <= 8
      *
     c                   eval      w_vare = %subst(c_null:1:8-w_leng) +
     c                                      w_scan
      *
     c                   eval      vvarl1_vare = w_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
6.22 c                   eval      b_skaf  = *off
6.22 c                   exsr      sjk_skaff
6.22 c                   if        b_skaf  = *on
6.22 c                   goto      end_scan
6.22 c                   endif
     c***                eval      c1vare = vvvare
     c                   movel     vvvare        c1vare
     c                   exsr      xc2bld
     c                   goto      end_scan
     c                   endif
      *
     c                   endif
      *
6.11  * Behandling dersom lengde <= 14
      *
6.11 c                   if        w_leng <= 14
      *
6.11 c                   eval      w_eanx = %subst(c_null:1:14-w_leng) +
     c                                      w_scan
      *
     c                   eval      w_xenhe = *blank
6.11 c                   movel     w_eanx        w_eann
6.11 c                   call      'VE710R  '
6.11 c                   parm                    w_firm
6.11 c                   parm                    w_eann
6.11 c                   parm                    w_eava
6.11 c                   parm                    w_eaen
6.11 c                   parm                    w_east
6.11 c                   if        w_east = '1'
6.11 c                   eval      w_xenhe     = w_eaen
6.11 c                   eval      vvarl1_vare = w_eava
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
6.22 c                   eval      b_skaf  = *off
6.22 c                   exsr      sjk_skaff
6.22 c                   if        b_skaf  = *on
6.22 c                   goto      end_scan
6.22 c                   endif
     c***                eval      c1vare = vvvare
     c                   movel     vvvare        c1vare
     c                   exsr      xc2bld
     c                   goto      end_scan
     c                   endif
     c                   endif
      *
     c                   endif
      *
      * Behandling dersom lengde < 26 (samlepost)
      *
6.33  * sjekker om logistikk-vare
6.33 c                   eval      w_lv_vare = *blank
6.33 c/exec sql
6.33 c+ select vvlvnr
6.33 c+ into   :w_lv_vare
6.33 c+ from   vvlepf
6.33 c+ where  vvllva = :w_scan
6.33 c+ fetch first 1 rows only
6.33 c/end-exec
6.33 c                   if        w_lv_vare <> *blank
6.33 c                   eval      b_skaf  = *off
6.33 c                   eval      vvvare = w_lv_vare
6.33 c                   exsr      sjk_skaff
6.33 c                   if        b_skaf  = *on
6.33 c                   goto      end_scan
6.33 c                   endif
6.33 c                   endif

     c                   eval      vvarl4_lvar = w_scan
     c     vvarl4_key    chain     vvarl4
     c                   if        %found
6.22 c                   eval      b_skaf  = *off
6.22 c                   exsr      sjk_skaff
6.22 c                   if        b_skaf  = *on
6.22 c                   goto      end_scan
6.22 c                   endif
     c***                eval      c1vare = vvvare
     c                   movel     vvvare        c1vare
     c                   exsr      xc2bld
     c                   goto      end_scan
     c                   endif
      *
      * Setter feil-indikator dersom vare ikke ble funnet
      *
5.61 c                   call      'LL871C '
     c                   parm                    w_scan
5.61 c                   parm                    w_sctx
      *
     c                   eval      b_feil = *on
     c                   eval      *in31 = *on
     c                   goto      end_scan
      *
     c     end_scan      tag
      *
     c                   eval      b_scan = *off
      *
     c                   if        b_feil = *off
     c                   eval      b2sca1 = *blank
     c                   eval      b2sca2 = *blank
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   eval      b2sca1 = *blank
     c                   eval      b2sca2 = *blank
      *
     c     hrtrl2_key    setll     hrtrl2
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
      * Beholder eksisterende posisjonering i subfilen
      *
     c                   if        wcurrn > 0
     c                   eval      wvisrn = wcurrn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      wvisrn = w_srrn
     c                   eval      *in22 = *off
      *
      * Endre post
      *
     c                   if        b1valg = 2
     c                   eval      c1vare = b1vare
     c                   eval      c1enhe = b1enhe
6.25 c                   eval      w_lage = l_lage
6.25 c                   call      'VL721R'
6.25 c                   parm                    w_firm
6.25 c                   parm                    c1vare
6.25 c                   parm                    w_lage
6.25 c                   parm                    w_ldor
6.25 c                   parm                    b_inlr
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slette post
      *
     c                   if        b1valg = 4
     c                   eval      d1vare = b1vare
     c                   eval      d1enhe = b1enhe
     c                   exsr      xd1bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      *in85 = *off
     c                   eval      c2akku = 0
     c                   eval      w_enhe = c1enhe
      *
      * Henter vareinformasjon
      *
     c***                eval      vvarl1_vare = c1vare
     c                   eval      vvarl1_vare = *blank
     c                   movel     c1vare        vvarl1_vare
     c     vvarl1_key    chain     vvarl1                             90
     c                   if        *in90 = *on
     c                   goto      end_c2bld
     c                   endif
      *
     c                   eval      vvtek1 = %trim(vvtek1)
     c                   eval      c2tek1 = %subst(vvtek1:1:19)
     c                   eval      c2tek2 = %subst(vvtek1:20:16)
      *
     c                   eval      c2lnav = *blank
      *
6.21 c                   if        w_ldor <> 0
6.21 c                   eval      rlevl1_levr = w_ldor
     c     rlevl1_key    chain     rlevl1                             90
     c                   if        *in90 = *off
     c                   eval      c2lnav = rlnavn
     c                   endif
     c                   endif
      *
      * Henter tre salgsenheter
      *
     c                   eval      *in80 = *on
     c                   eval      *in81 = *off
     c                   eval      *in82 = *off
      *
     c                   eval      x = 0
     c                   eval      c2enh1 = *blank
     c                   eval      c2enh2 = *blank
     c                   eval      c2enh3 = *blank
      *
     c                   eval      vvenl2_vare = vvvare
     c                   eval      vvenl2_saen = 1
     c     vvenl2_key    setll     vvenl2
     c     vvenl2_key2   reade     vvenl2                                 90
     c                   dow       *in90 = *off and
     c                             x < 3
      *
     c                   eval      x = x + 1
      *
     c                   select
     c                   when      x = 1
     c                   eval      c2enh1 = veenhe
     c                   when      x = 2
     c                   eval      c2enh2 = veenhe
     c                   when      x = 3
     c                   eval      c2enh3 = veenhe
     c                   endsl
      *
     c     vvenl2_key2   reade     vvenl2                                 90
     c                   enddo
      *
     c                   if        w_xenhe <> *blank
     c                   select
     c                   when      w_xenhe = c2enh1
     c                   eval      *in80  = *on
     c                   eval      *in81  = *off
     c                   eval      *in82  = *off
     c                   when      w_xenhe = c2enh2
     c                   eval      *in80  = *off
     c                   eval      *in81  = *on
     c                   eval      *in82  = *off
     c                   when      w_xenhe = c2enh3
     c                   eval      *in80  = *off
     c                   eval      *in81  = *off
     c                   eval      *in82  = *on
     c                   endsl
     c                   endif
      *
      * Antall
      *
     c                   eval      c2anta = w_anta
      *
      * Transaksjon
      *
     c                   if        b_scan = *off
      *
     c                   eval      hrtrlr_vare = c1vare
     c                   eval      hrtrlr_enhe = c1enhe
     c     hrtrlr_key    chain     hrtrlr                             90
     c                   if        *in90 = *off
      *
     c                   eval      *in80 = *off
      *
     c                   select
     c                   when      hrenhe = c2enh1
     c                   eval      *in80 = *on
     c                   when      hrenhe = c2enh2
     c                   eval      *in81 = *on
     c                   when      hrenhe = c2enh3
     c                   eval      *in82 = *on
     c                   endsl
      *
     c                   eval      c2anta = hranta
      *
     c                   endif
     c                   endif
      *
      * Finner pris, priskode og rabatt for kunde på enhet
      *
     c                   exsr      hent_pris
      *
      * Viser bildet
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        c2akku <> 0
     c                   eval      c2anta = c2anta + c2akku
     c                   eval      c2akku = 0
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        *inkc
     c                   goto      end_c2bld
     c                   endif
      *
      * F1=Bytting av enhet
      *
     c                   if        *inka
     c                   select
     c                   when      *in80 = *on and
     c                             c2enh2 <> *blank
     c                   eval      *in80 = *off
     c                   eval      *in81 = *on
     c                   when      *in81 = *on
     c                   eval      *in81 = *off
     c                   if        c2enh3 <> *blank
     c                   eval      *in82 = *on
     c                   else
     c                   eval      *in80 = *on
     c                   endif
     c                   when      *in82 = *on
     c                   eval      *in82 = *off
     c                   eval      *in80 = *on
     c                   endsl
     c                   exsr      hent_pris
     c                   goto      c2tagb
     c                   endif
      *
      * F2=Kalkulator
      *
     c                   if        *inkb
     c                   call      'HR200R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   parm                    w_anta_kalk
     c                   eval      b2anta = w_anta_kalk
     c                   goto      c2tagb
     c                   endif
      *
      * F5=Forny
      *
     c                   if        *inke
     c                   eval      *in80 = *on
     c                   eval      *in81 = *off
     c                   eval      *in82 = *off
     c                   eval      *in85 = *off
     c                   eval      c2anta = 0
     c                   eval      c2akku = 0
     c                   exsr      hent_pris
     c                   goto      c2tagb
     c                   endif
      *
      * F6=Akkumulator
      *
     c                   if        *inkf
     c                   if        *in85 = *off
     c                   eval      *in85 = *on
     c                   else
     c                   eval      *in85 = *off
     c                   endif
     c                   goto      c2tagb
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2anta <= 0
     c                   eval      *in31 = *on
     c                   goto      c2tagb
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   hrtrlur
      *
     c                   select
     c                   when      *in80 = *on
     c                   eval      c1enhe = c2enh1
     c                   when      *in81 = *on
     c                   eval      c1enhe = c2enh2
     c                   when      *in82 = *on
     c                   eval      c1enhe = c2enh3
     c                   endsl
      *
     c                   if        b1valg = 2 and
     c                             w_enhe <> c1enhe
     c                   eval      hrtrlu_vare = c1vare
     c                   eval      hrtrlu_enhe = w_enhe
     c     hrtrlu_key    chain     hrtrlur                            90
     c                   if        *in90 = *off
     c                   delete    hrtrlur
     c                   endif
     c                   endif
      *
     c                   eval      hrtrlu_vare = c1vare
     c                   eval      hrtrlu_enhe = c1enhe
     c     hrtrlu_key    chain     hrtrlur                            90
      *
     c                   eval      hrvare = c1vare
     c                   eval      hrenhe = c1enhe
      *
     c                   if        *in90 = *off and
     c                             b_scan = *on
     c                   eval      hranta = hranta + c2anta
     c                   else
     c                   eval      hranta = c2anta
     c                   endif
      *
     c                   eval      hrsaim = c2saim * hranta
      *
     c                   time                    hrdato
     c                   time                    hrtime
      *
     c                   if        *in90 = *off
     c                   update    hrtrlur
     c                   else
     c                   eval      hrfirm = w_firm
     c                   eval      hrkode = w_kode
     c                   eval      hrtims = w_tims
     c                   write     hrtrlur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1enhe = hrenhe
     c     hranta        mult      1             b1anta
     c                   endif
      *
     c     end_c2bld     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (d1bld)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1bld        begsr
      *
     c                   eval      hrtrlr_vare = d1vare
     c                   eval      hrtrlr_enhe = d1enhe
     c     hrtrlr_key    chain     hrtrlr                             90
      *
     c                   eval      d1anta = hranta
     c                   eval      d1tek1 = *blank
     c                   eval      d1tek2 = *blank
      *
     c***                eval      vvarl1_vare = hrvare
     c                   eval      vvarl1_vare = *blank
     c                   movel     hrvare        vvarl1_vare
     c     vvarl1_key    chain     vvarl1                             90
     c                   if        *in90 = *off
     c                   eval      d1tek1 = %subst(vvtek1:1:19)
     c                   eval      d1tek2 = %subst(vvtek1:20:16)
     c                   endif
      *
     c                   exfmt     d1bld
      *
     c                   if        *inkc
     c                   goto      end_d1bld
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      hrtrlu_vare = d1vare
     c                   eval      hrtrlu_enhe = d1enhe
     c     hrtrlu_key    chain     hrtrlur                            90
      *
     c                   delete    hrtrlur
      *
     c     end_d1bld     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c     hrtrl2_key    reade     hrtrl2                                 15
      *
     c                   if        *in15
     c                   goto      end_crt
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1tek1 = *blank
     c                   eval      b1enhe = hrenhe
     c                   eval      b1anta = 0
     c                   eval      b1vare = hrvare
      *
     c     hranta        mult      1             b1anta
      *
     c***                eval      vvarl1_vare = hrvare
     c                   eval      vvarl1_vare = *blank
     c                   movel     hrvare        vvarl1_vare
     c     vvarl1_key    chain     vvarl1                             91
     c                   if        *in91 = *off
     c                   eval      b1tek1 = vvtek1
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      wvisrn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for hetning av pris, priskode og rabatt for kunde/enhet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_pris     begsr
      *
     c                   eval      c2saim = 0
     c                   eval      c2rab1 = 0
     c                   eval      c2rab2 = 0
     c                   eval      c2kpri = *blank
      *
     c                   eval      hifirm = w_firm
     c                   eval      hirkat = 0
     c                   eval      hikund = p_kund
     c                   eval      hikpro = p_kpro
     c                   eval      hivare = vvvare
     c                   eval      hilety = 0
     c                   eval      hienhe = *blank
     c                   eval      hidato = *loval
     c                   eval      hitime = *loval
     c                   eval      hinumm = 0
     c                   eval      hikode = *blank
     c                   eval      hidekn = 0
     c                   eval      hitilb = *blank
     c                   eval      hiavgf = 0
     c                   eval      hipriv = 0
     c                   eval      hiskaf = *off
6.29 c                   eval      hildor = w_ldor
     c                   eval      hisapr = 0
     c                   eval      hikopr = 0
5.70 c                   eval      hiprgr = w_prgr
     c                   eval      hiinlr = *blank
      *
     c                   select
     c                   when      *in80 = *on
     c                   eval      hienhe = c2enh1
     c                   when      *in81 = *on
     c                   eval      hienhe = c2enh2
     c                   when      *in82 = *on
     c                   eval      hienhe = c2enh3
     c                   endsl
      *
     c                   time                    hidato
     c                   time                    hitime
      *
     c                   call      'VP900R'
     c                   parm                    hirec
     c                   parm                    horec
      *
     c                   eval      c2saim = hosapr
     c                   eval      c2kpri = hokpri
     c                   eval      c2rab1 = horab1
     c                   eval      c2rab2 = horab2
      *
     c                   if        horab1 > 0
     c                   eval(h)   c2saim = c2saim - (c2saim * horab1 / 100)
     c                   endif
     c                   if        horab2 > 0
     c                   eval(h)   c2saim = c2saim - (c2saim * horab2 / 100)
     c                   endif
      *
     c                   if        c2saim <> 0
     c                   eval      w_mvak = 4
     c                   eval      c2saim = c2saim * w_mvat
     c                   call      'FA909R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   parm                    w_mvak
     c                   parm                    c2saim
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for summering av salg
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     summer_salg   begsr
      *
     c                   eval      b2antv = 0
     c                   eval      b2saim = 0
      *
     c     hrtrl2_key    setll     hrtrl2
     c     hrtrl2_key    reade     hrtrl2                                 90
     c                   dow       *in90 = *off
      *
     c                   eval      b2antv = b2antv + 1
     c                   eval      b2saim = b2saim + hrsaim
      *
     c     hrtrl2_key    reade     hrtrl2                                 90
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting av ekspedisjon i trans-fil
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     opprett       begsr
      *
     c                   eval      b_oppr = *off
      *
     c     hrtrl2_key    chain     hrtrl2                             90
     c                   if        *in90 = *on
     c                   eval      b_oppr = *on
     c                   goto      end_opprett
     c                   endif
      *
      * Tilleggsinformasjon
      *
     c     e1taga        tag
      *
     c                   eval      e1selg = fbselg
     c                   eval      e1nav1 = *blank
     c                   eval      e1nav2 = *blank
     c                   eval      e1dref = *blank
     c                   eval      e1rekv = *blank
      *
     c                   if        p_kpro <> 0
     c                   eval      fkprl1_kund = p_kund
     c                   eval      fkprl1_kpro = p_kpro
     c     fkprl1_key    chain     fkprl1                             91
     c                   if        *in91 = *Off
     c                   eval      flnavn = %trim(flnavn)
     c                   eval      e1nav1 = %subst(flnavn:1:19)
     c                   eval      e1nav2 = %subst(flnavn:20:11)
     c                   endif
     c                   endif
      *
      * Oversetter kundeprosjekt til store bokstaver dersom dette er valgt
      *
     c                   if        faahoy = 1
     c     lo:up         xlate     e1nav1        e1nav1
     c     lo:up         xlate     e1nav2        e1nav2
     c                   endif
      *
     c     e1tagb        tag
      *
     c                   exfmt     e1bld
      *
     c                   if        *inkc
     c                   goto      end_opprett
     c                   endif
      *
     c                   if        *inke
     c                   goto      e1taga
     c                   endif
      *
3.31  * validering av e1bilde
      *
      * selger
3.31 c                   if        e1selg = 0
3.31 c                   eval      *in35  = *on
3.31 c                   goto      e1tagb
3.31 c                   end
3.31  *
3.31 c                   call      'RS709R'
3.31 c                   parm                    w_stat
3.31 c                   parm                    e1selg
3.31 c                   parm                    w_itxt
3.31  *
3.31 c                   if        w_stat <> *blank
3.31 c                   eval      *in35 = *on
3.31 c                   goto      e1tagb
3.31 c                   end
3.32  * kundens ref.
3.32 c                   if        p_kund = 0 and
     c                             e1dref = *blank
3.32 c                   eval      *in39  = *on
3.32 c                   goto      e1tagb
3.32 c                   endif
6.2A  *
6.2A  * Er det krav til Deres referanse
6.2A  * (Kan kontrollere selv om kunde <> 0)
6.2A  *
6.2A c                   if        u_dref = *on  and
6.2A c                             e1dref = *blank
6.2A c                   eval      *in39  = *on
6.2A c                   goto      e1tagb
6.2A c                   endif
6.12  *
6.12  * krav til rekvisisjon ?
6.12  *
6.12 c                   if        p_kund <> 0
6.12 c                   eval      rkunl1_kund = p_kund
6.12 c     rkunl1_key    chain     rkunl1
6.12 c                   if        %found and
6.12 c                             rkrekv = 1 and
6.12 c                             e1rekv = *blank
6.12 c                   eval      *in34  = *on
6.12 c                   goto      e1tagb
6.12 c                   endif
6.12 c                   endif
      *
      * Avslutning
      *
      * Henter neste referansenummer
      *
     c                   eval      w_kod2 = '0'
     c                   eval      w_fell = 'LRADIO'
     c                   eval      w_type = *blank
     c                   eval      w_fast = *blank
      *
     c                   call      'AS100R'
     c                   parm                    w_kod2
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
6.nu c                   parm                    w_numm
      *
6.nu c                   eval      f1refn = w_numm
      *
      * Oppretter håndterminal ordre-hode
      *
     c                   eval      hofirm = w_firm
     c                   eval      horefn = f1refn
     c                   eval      hokund = p_kund
     c                   eval      hokpro = p_kpro
     c                   eval      hoselg = e1selg
     c                   eval      honavn = %trim(e1nav1) + %trim(e1nav2)
     c                   eval      hodref = e1dref
     c                   eval      horekv = e1rekv
     c                   eval      hostat = 0
6.28 c**                 eval      hoavde = absavd
6.28 c**                 eval      holage = ablagr
     c                   time                    hodato
     c                   time                    hotime
      *
      * Oversetter navn til store bokstaver dersom dette er valgt
      *
     c                   if        faahoy = 1
     c     lo:up         xlate     honavn        honavn
     c                   endif
      *
     c                   write     hohelur
      *
      * Flytter varesalg fra radioterminal til håndterminal ordre-linje
      *
     c     hrtrlu_key2   setll     hrtrlu
     c     hrtrlu_key2   reade     hrtrlur                                90
     c                   dow       *in90 = *off
      *
     c                   eval      hdfirm = hrfirm
     c                   eval      hdrefn = f1refn
     c                   eval      hdvare = hrvare
     c                   eval      hdenhe = hrenhe
     c                   eval      hdanta = hranta
     c                   eval      hddato = hrdato
     c                   eval      hdtime = hrtime
      *
     c                   write     hodtlur
      *
     c                   delete    hrtrlur
      *
     c     hrtrlu_key2   reade     hrtrlur                                90
     c                   enddo
      *
      * Viser avslutningsbildet
      *
3.31 c     f1tagb        tag
     c                   eval      b_oppr = *on
3.31 c                   eval      f1paks = ' '
      *
     c                   exfmt     f1bld
      *
3.31  * Tester om gyldig valg
3.31  *
3.31 c                   if        f1paks <> '1' and
6.23 c                             f1paks <> '0' and
6.23 c                             f1paks <> ' '
3.31 c                   eval      *in38  = *on
3.31 c                   goto      f1tagb
     c                   endif
      *
3.31  * Dersom pakkseddel skal kjøres direkte
3.31  * må det foretas en del kontroller før
3.31  * rutinen blir satt i gang
3.31  *
3.31 c                   if        f1paks = '1'
6.14  *
6.14  * Hvis hjelpefil finnes, og den har status skal den bare hopp ut
6.14  * Da er pakkseddel kjørt i kassa
6.14 c                   eval      hohel1_refn = f1refn
6.14 c     hohel1_key    chain     hohel1
6.14 c                   if        %found(hohel1)
6.14  * her finnes håndterminal ordre, men status sier at den er kjørt
6.14 c                   if        hostat = 1
6.14 c                   goto      end_opprett
6.14 c                   endif
6.14 c                   else
6.14  * her finnes ikke håndterminal ordre,   avslutter
6.14 c                   goto      end_opprett
6.14 c                   endif
3.32  *
3.32  * Er kundenummer oppgitt ?
3.32  *
3.32 c                   if        p_kund = 0
3.32 c                   eval      *in37  = *on
3.32 c                   goto      f1tagb
3.32 c                   endif
3.31  *
3.31  * Er kasseregister opprettet ?
3.31  *   (RT + bruker-id)
3.31  *
3.31 c                   eval      bwsdl1_wsid = %trim('RT' + l_user)
3.31 C     bwsdl1_key    chain     bwsdl1r                            90
3.31 c                   if        *in90 = *on
3.31 c                   eval      *in31 = *on
3.31 c                   goto      f1tagb
3.31 c                   endif
3.31  *
3.31  * Er kunde sperret for kreditt ?
3.31  *
3.31 c                   eval      rkunl1_kund = p_kund
3.31 c     rkunl1_key    chain     rkunl1                             90
3.31 c                   if        rkkres = 1
3.31 c                   eval      *in32  = *on
3.31 c                   goto      f1tagb
3.31 c                   endif
6.26  *
6.26  * Er kunde satt opp med kun kontant ?
6.26  *
6.26 c                   if        f1paks = '1' and
6.26 c                             rkkres = 2
6.26 c                   eval      *in41  = *on
6.26 c                   goto      f1tagb
6.26 c                   endif
6.26  *
6.26  * Er kunde satt opp med ikke kontant, kun faktura ?
6.26  *
6.26 c                   if        f1paks = ' ' and
6.26 c                             rkkres = 3
6.26 c                   eval      *in42  = *on
6.26 c                   goto      f1tagb
6.26 c                   endif
6.27  * Henter inn informasjon om memosaldo
6.27 c     rkunl1_key    chain     rkmel1                             90
3.31  *
3.31  * Er kreditt-grense overskredet ?
3.31  *
3.31 c                   eval      w_grns = rkgrns * 1000
3.31 c                   eval      w_sald = 0
3.31 c                   eval      w_sald = w_sald + rksald
3.31 c                   eval      w_sald = w_sald + rkmems
3.31 c                   eval      w_sald = w_sald + b2saim
3.31  *
3.31 c                   if        w_grns <> 0
3.31 c                   if        w_sald >  w_grns
3.31 c                   eval      *in33  = *on
3.31 c                   goto      f1tagb
3.31 c                   endif
3.31 c                   endif
3.31  *
3.31  *
3.31  * Finnes avdeling i kode-register ?
3.31  *
3.31 c                   if        bwavde <> 0
3.31 c                   call      'RS707R'
3.31 c                   parm                    w_stat
3.31 c                   parm                    bwavde
3.31 c                   parm                    w_gtxt
3.31  *
3.31 c                   if        w_stat <> *blank
3.31 c                   eval      *in34 = *on
3.31 c                   goto      f1tagb
3.31 c                   end
3.31 c                   end
3.31  *
3.31  * Finnes selger i kode-register ?
3.31  *
3.31 c                   if        e1selg = 0
3.31 c                   eval      *in35  = *on
3.31 c                   goto      f1tagb
3.31 c                   end
3.31  *
3.31 c                   call      'RS709R'
3.31 c                   parm                    w_stat
3.31 c                   parm                    e1selg
3.31 c                   parm                    w_itxt
3.31  *
3.31 c                   if        w_stat <> *blank
3.31 c                   eval      *in35 = *on
3.31 c                   goto      f1tagb
3.31 c                   end
3.31  *
3.31  * Finnes lager i kode-register ?
3.31  *
3.31 c                   call      'RS710R'
3.31 c                   parm                    w_stat
3.31 c                   parm                    bwlage
3.31 c                   parm                    w_jtxt
3.31  *
3.31 c                   if        w_stat <> *blank
3.31 c                   eval      *in36  = *on
3.31 c                   goto      f1tagb
3.31 c                   endif
6.30  *
6.30  * Sjekk om kunden har kort, og at dette har fullmakt. Foreløpig
6.30  * gjelder dette kun GE kort. (Korttype 6 - integrasjon mot origo)
6.30  *
6.31 c**                 eval      rukrl5_ktyp = 6
6.31 c                   eval      rukrl5_ktyp = 4
6.30 c                   eval      rukrl5_kkun = p_kund
6.30 c                   eval      rukrl5_kpro = 0
6.30 c     rukrl5_key    chain     rukrl5
6.30 c                   if        %found     and
6.30 c                             rukobl = 1
6.30 c                   eval      *in43  = *on
6.30 c                   goto      f1tagb
6.30 c                   endif
      *
3.31  *
3.31  *
3.31  * Pakkseddel skal kjøres direkte
3.31  *
3.31 c                   call      'BO760C'
3.31 c                   parm                    w_firm
3.31 c                   parm                    f1refn
3.31  *
3.31 c                   endif
6.13  *
6.13  * Legger ut informasjon i hjelpefil for lager/avdelings skille
6.28  * Fjernes når HOHE er utvidet med felter for avdeling og lager
6.13  *
6.13 c                   eval      hxfirm = hdfirm
6.13 c                   eval      hxrefn = hdrefn
6.13 c                   eval      hxavde = absavd
6.13 c                   eval      hxlage = ablagr
6.13 c                   eval      hxeoda = hrdato
6.13 c                   eval      hxeoti = hrtime
6.13 c                   eval      hxeous = l_user
6.13 c                   eval      hxeeda = hrdato
6.13 c                   eval      hxeeti = hrtime
6.13 c                   eval      hxeeus = l_user
6.13  *
6.13 c                   write     hohxpfr
6.13  *
3.31  *
     c     end_opprett   endsr
6.22  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.22  *   Subrutine som sjekker om varen er skaffevare for lageret
6.22  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.22 c     sjk_skaff     begsr
6.22  *
6.22  * kaller denne her fordi man også trenger lev.nr senere
6.22 c                   eval      w_lage = l_lage
6.22 c                   call      'VL710R'
6.22 c                   parm                    w_firm
6.22 c                   parm                    vvvare
6.22 c                   parm                    w_lage
6.22 c                   parm                    w_vtyp
6.25 c                   parm                    w_utda
6.25 c                   parm                    w_ldor
6.25 c                   parm                    b_inlr
6.33 c                   parm                    w_lv_nobb
6.33 c                   parm                    w_lv_modn
6.33 c                   parm                    w_lv_lldo
6.33 c                   parm                    w_lv_llva
6.24  *
6.24  * Ingen sjekk dersom kode = 0
6.24 c                   if        faalbe = 0
6.24 c                   goto      end_skaf
6.24 c                   endif
6.22  *
6.24  * Kode = 1
6.22 c                   if        w_vtyp = 'S' and
6.24 c                             faalbe = 1
6.22 c                   exfmt     m1msg
6.22  *
6.22 c                   if        *inkc
6.22 c                   eval      b_skaf =*on
6.22 c                   leavesr
6.22 c                   endif
6.22 c                   endif
6.22  *
6.24  * Kode = 2
6.24 c                   if        w_vtyp = 'S' and
6.24 c                             faalbe = 2
6.24 c                   exfmt     m2msg
6.24  *
6.24 c                   eval      *inkc  =*on
6.24 c                   eval      b_skaf =*on
6.24 c                   leavesr
6.24 c                   endif
6.22  *
6.24 c     end_skaf      tag
6.24  *
6.22 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_kode
     c                   parm                    p_tims
     c                   parm                    p_kund
     c                   parm                    p_kpro
6.2A  *
6.2A  * Key for file : Firmastyrt validering
6.2A  *
6.2A c     avall1_key    klist
6.2A c                   kfld                    w_firm
6.2A c                   kfld                    avall1_apgm
      *
      * Key for les på salg
      *
     c     hrtrl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_kode
     c                   kfld                    w_tims
      *
      * Key for oppslag på salg
      *
     c     hrtrlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_kode
     c                   kfld                    w_tims
     c                   kfld                    hrtrlr_vare
     c                   kfld                    hrtrlr_enhe
      *
      * Key for oppdatering av salg
      *
     c     hrtrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_kode
     c                   kfld                    w_tims
     c                   kfld                    hrtrlu_vare
     c                   kfld                    hrtrlu_enhe
      *
     c     hrtrlu_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    w_kode
     c                   kfld                    w_tims
6.14  *
6.14  * Key for oppslag på hohepf
6.14  *
6.14 c     hohel1_key    klist
6.14 c                   kfld                    w_firm
6.14 c                   kfld                    hohel1_refn
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på vare m/lev.varenummer
      *
     c     vvarl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl4_lvar
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for les på vare-enhet-register
      *
     c     vvenl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
     c                   kfld                    vvenl2_saen
      *
     c     vvenl2_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
      *
      * Key for oppslag på kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      * Key for oppslag på bruker
      *
     c     fusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fusrl1_user
3.31  *
3.31  * Key for file : SKJERM/KASSE-REGISTER
3.31  *
3.31 c     bwsdl1_key    klist
3.31 c                   kfld                    w_firm
3.31 c                   kfld                    bwsdl1_wsid
3.31  *
3.31  * Key for oppslag på kunde
3.31  *
3.31 c     rkunl1_key    klist
3.31 c                   kfld                    w_firm
3.31 c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på status
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
6.13  *
6.13  * Key for les av bruker
6.13  *
6.13 c     ausrlr_key    klist
6.13 c                   kfld                    ausrlr_user
6.30  *
6.30  * Key for les på kortregister pr. kunde
6.30  *
6.30 c     rukrl5_key    klist
6.30 c                   kfld                    w_firm
6.30 c                   kfld                    rukrl5_ktyp
6.30 c                   kfld                    rukrl5_kkun
6.30 c                   kfld                    rukrl5_kpro
      *
      * Henter firma, timestamp og kunde parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_kode = p_kode
     c                   eval      w_tims = p_tims
      *
      * Finner moms-sats
      *
     c                   time                    w_udat
      *
     c                   call      'FÅ705R'
     c                   parm                    w_firm
     c                   parm                    w_udat
     c                   parm                    w_mvap
     c                   parm                    w_mvat
     c                   parm                    w_mvaf
5.70  *
5.70  * Henter prisgruppe
5.70  *
5.70 c                   call      'VL711R  '
5.70 c                   parm                    w_prgr
      *
      * Henter bruker-info, og setter indikator 89 dersom selgernummer er
      * utfylt
      *
     c                   eval      fusrl1_user = l_user
     c     fusrl1_key    chain     fusrl1                             90
     c                   if        *in90 = *off and
     c                             fbselg <> 0
     c                   eval      *in89 = *on
     c                   endif
      *
5.61 c                   eval      w_sctx = 'HR101R Ekspedisjon'
      *
      * Hent opplysninger fra ofak koderegister
      *
     c     fstsl1_key    chain     fstsl1r
      *
6.13  *
6.13  * Henter info fra bruker
6.13  *
6.13 c                   eval      ausrlr_user = l_user
6.13 c     ausrlr_key    chain     ausrlr
6.13 c
6.2A  *
6.2A  * Firmastyrt validering av utvalgte felter
6.2A  *
6.2A c                   eval      avall1_apgm = 'HR101R'
6.2A c     avall1_key    setll     avall1
6.2A c     avall1_key    reade     avall1
6.2A c                   dow       not %eof(avall1)
6.2A c                   if        avaflt = 'HODREF'  and
6.2A c                             avaval = 1
6.2A c                   eval      u_dref = *on
6.2A c                   endif
6.2A c     avall1_key    reade     avall1
6.2A c                   enddo
      *
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      *in22 = *on
      *
     c     hrtrl2_key    setll     hrtrl2
      *
     c                   exsr      crt_subfile
     c                   endsr
