     h/TITLE  Stikkprøver,hovedbok - Rapport
     h datedit(*dmy.)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RH654R                                              *
      * Beskrivelse . . : Stikkprøver,hovedbok - Rapport                      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *       KA = F1  = Valg av firma                                  *
      *       KC = F3  = Exit                                           *
      *       LR = Avslutt program                                      *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     frhtrl3    if   e           k disk    rename(rhtrpfr:rhtrl3r)
     frh654p    o    e             printer oflind(*in30)
      *
     d                 ds
     d nybrd                         13  0                                      Bruddb
     d  nyavd                         4  0 overlay(nybrd)                       Avdeli
     d  nykto                         5  0 overlay(nybrd:5)                     Konto
     d   nygrp                        2  0 overlay(nykto:2)                     Gruppe
     d    nykla                       1  0 overlay(nygrp)                       Klasse
     d  nysps                         4  0 overlay(nybrd:10)                    Spesif
      *
     d                 ds
     d gmbrd                         13  0                                      Bruddb
     d  gmavd                         4  0 overlay(gmbrd)                       Avdeli
     d  gmkto                         5  0 overlay(gmbrd:5)                     Konto
     d   gmgrp                        3  0 overlay(gmkto)                       Gruppe
     d    gmkla                       1  0 overlay(gmgrp:2)                     Klasse
     d  gmsps                         4  0 overlay(gmbrd:10)                    Spesif
      *
      *  Parameter i LDA
      *
     d                uds
     d rhpfir                300    302  0
     d rhpnav                        30
     d rhpusr                        10
     d rhpkda                         6  0
     d rhprÅr                         4  0
     d rhpper                         2  0
     d rhppef                         2  0
     d rhppet                         2  0
     d rhpavf                         4  0
     d rhpavt                         4  0
     d rhpktf                         5  0
     d rhpktt                         5  0
     d rhpsrt                         1
     d rhpbel                384    394  2
     d rhpran                         4  0
      *
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d antskr          s              7  0
     d anttra          s              7  0
     d a07txt          s             20
      *
     d gavde           s                   like(rbavde)
     d gbilk           s                   like(rbbilk)
     d gbiln           s                   like(rbbiln)
     d gfirm           s                   like(rbfirm)
     d ghmrk           s                   like(rbhmrk)
     d gjour           s                   like(rbjour)
     d gkont           s                   like(rbkont)
     d gmvak           s                   like(rbmvak)
     d gmvao           s                   like(rbmvao)
     d gneto           s                   like(rbneto)
     d graar           s                   like(rbraar)
     d grper           s                   like(rbrper)
     d gspes           s                   like(rbspes)
6.NU d*gtext           s                   like(rbtext)
      *
     d lrbeve          s                   like(rbneto)
     d lrdebe          s                   like(rbneto)
     d lrgdeb          s                   like(rbneto)
     d lrgkre          s                   like(rbneto)
     d lrgsal          s                   like(rbneto)
     d lrkred          s                   like(rbneto)
     d lrndeb          s                   like(rbneto)
     d lrnkre          s                   like(rbneto)
     d lrnsal          s                   like(rbneto)
      *
     d l1beve          s                   like(rbneto)
     d l1debe          s                   like(rbneto)
     d l1gsal          s                   like(rbneto)
     d l1kred          s                   like(rbneto)
     d l1nsal          s                   like(rbneto)
      *
     d l2beve          s                   like(rbneto)
     d l2debe          s                   like(rbneto)
     d l2gsal          s                   like(rbneto)
     d l2kred          s                   like(rbneto)
     d l2nsal          s                   like(rbneto)
      *
     d m               s              2  0
     d n               s              2  0
     d whpbel          s                   like(rhpbel)
     d whpran          s                   like(rhpran)
     d x               s              2  0
      *
     d w_firm          s                   like(rbfirm)
      *
     d rhtrl3_raar     s                   like(rbraar)
      *
     d p_stat          s              1
     d p_kont          s                   like(rbkont)
     d p_spes          s                   like(rbspes)
     d p_avde          s                   like(rbavde)
     d p_text          s             30
     d p_gkod          s                   like(rbavde)
     d p_gtxt          s             20
      *
      * Les hovedboks-transaksjoner
      *
     c     les           tag
      *
     c     rhtrl3_key    setll     rhtrl3r                                60
     c     rhtrl3_key    reade     rhtrl3r                                60
      *
B001 c                   if        *in60 = *on                                           .....
 001 c                   move      *hival        nybrd                                       .
 001 c                   move      *on           *inlr                                       .
 001 c                   goto      brudd                                                     .
E001 c                   endif                                                           .....
      *
B001 c                   dow       *in60 = *off                                          .....
      *
     c                   eval      nyavd = rbavde
     c                   eval      nykto = rbkont
     c                   move      rbspes        nysps
      *
      * Sjekk om transaksjonen skal behandles
      *
      *
      * Sjekk periode er > valgt periode
      *
B001 c                   if        rbrper > rhppet                                       .....
 001 c                   goto      neste                                                     .
E001 c                   endif                                                           .....
      *
      * Sjekk avdeling fra/til
      *
B001 c                   if        rbavde < rhpavf or                                    .....
B001 c                             rbavde > rhpavt                                       .....
 001 c                   goto      neste                                                     .
E001 c                   endif                                                           .....
      *
      * Sjekk kontonr. fra/til
      *
B001 c                   if        rbkont < rhpktf or                                    .....
B001 c                             rbkont > rhpktt                                       .....
 001 c                   goto      neste                                                     .
E001 c                   endif                                                           .....
      *
      * Test mot utvalg  - hver N. gang
      *
B001 c                   if        rhpran > 0                                            .....
 001 c                   add       1             whpran                                      .
B002 c                   if        rhpran = whpran                                       ... .
 002 c                   z-add     0             whpran                                    . .
 002 c                   goto      posmed                                                  . .
E002 c                   endif                                                           ... .
E001 c                   endif                                                           .....
      *
      * Test mot utvalg  - større enn angitt beløp
      *
B001 c                   if        rhpbel <> 0                                           .....
      *
B002 c                   if        rbneto > 0 and                                        ... .
B003 c                             rbneto >= rhpbel                                        . .
 003 c                   goto      posmed                                                  . .
E003 c                   endif                                                             . .
      *
B002 c                   if        rbneto < 0 and                                        ... .
B003 c                             rbneto <= whpbel                                        . .
 003 c                   goto      posmed                                                  . .
E003 c                   endif                                                             . .
      *
E001 c                   endif                                                           .....
      *
     c                   goto      neste
      *
     c     posmed        tag
      *
      * Brudd i kontrollbegrepet
      *
     c     brudd         tag
      *
     c                   eval      nyavd = *zeros
      *
      * Første gang - ikke test på brudd
      *
B001 c                   if        gmbrd = *zeros                                        .....
 001 c                   exsr      flytt                                                     .
 001 c                   goto      neste                                                     .
E001 c                   endif                                                           .....
      *
      * Summer antall poster
      *
     c                   add       1             anttra
      *
B001 c                   if        anttra = 1                                            .....
 001  *                                                                 .
 001  * 1. record på konto                                              .
 001  *                                                                 .
 001  * Hent tekstrecord fra KONTOPLAN                                  .
 001  *                                                                 .
 001 c                   move      *blank        wtekst                                      .
 001 c                   move      *blank        wtxt2                                       .
 001 c                   move      gmkto         p_kont                                      .
 001 c                   move      gmsps         p_spes                                      .
 001 c                   move      gmavd         p_avde                                      .
      *
 001 c                   call      'RS740R'                                      kontoplan-
     c                   parm                    p_stat
     c                   parm                    p_kont
     c                   parm                    p_spes
     c                   parm                    p_avde
     c                   parm                    p_text
      *
 002 c                   movel     p_text        wtekst                          finnes      .
 001  *                                                                 .
B002 c                   if        *in30 = *off
 002 c                   write     ktotxt                                                    .
E002 c                   endif
 001  *                                                                 .
E001 c                   endif                                                           .....
      *
      * Summer til inngående saldo (Hovedbok)
      *                                                             .....
B001 c                   if        grper < rhppef                                            .
 001 c                   add       gneto         l1gsal                                      .
 001 c                   goto      brudde                                                    .
E001 c                   endif                                                           .....
      *
      * Skriv heading
      *
B001 c   30              do                                                              .....
 001 c                   exsr      skriv                                                     .
     c                   eval      *in30 = *off
E001 c                   enddo                                                           .....
      *
      * Skriv linje
      *
     c                   add       1             antskr
     c                   write     linje
      *
      * Summer til debet eller kredit
      *
B001 c                   if        gneto < 0                                             .....
 001 c                   add       gneto         l1kred                                      .
X001 c                   else                                                                .
 001 c                   add       gneto         l1debe                                      .
E001 c                   endif                                                           .....
      *
     c     brudde        tag
      *
      * Brudd på konto
      *
B001 c                   if        nykto <> gmkto or                                     .....
 001 c                             nyavd <> gmavd                                            .
 001 c                   exsr      brkto                                                     .
E001 c                   endif                                                           .....
      *
      * Brudd på avdeling -
      * kun hvis sortert på avdeling
      *
B001 c                   if        rhpsrt = 'A' and                                      .....
 001 c                             nyavd <> gmavd                                            .
 001 c                   exsr      bravd                                                     .
E001 c                   endif                                                           .....
      *
     c                   exsr      flytt
      *
     c     neste         tag
      *
     c     rhtrl3_key    reade     rhtrl3r                                60
E001 c                   enddo
      *
     c     slutt         tag
      *
     c                   write     firsum
     c                   seton                                        lr
      *
      ****************************************************************
      *   SUBRUTINE FOR FLYTTING AV FELT FRA NY TIL GAMMEL           *
      ****************************************************************
      *
     c     flytt         begsr
      *
      *  Flytt felt fra ny transaksjon til gammel transaksjon
      *
     c                   eval      gfirm = rbfirm
     c                   eval      graar = rbraar
     c                   eval      gkont = rbkont
     c                   eval      gavde = rbavde
     c                   eval      gspes = rbspes
     c                   eval      grper = rbrper
     c                   eval      gbiln = rbbiln
     c     *dmy          move      rbbdat        gbdat
     c                   eval      gbilk = rbbilk
6.NU c*                  eval      gtext = rbtext
6.NU c                   movel     rbtext        gtext
     c                   eval      gmvao = rbmvao
     c                   eval      gmvak = rbmvak
     c                   eval      gneto = rbneto
     c                   eval      gjour = rbjour
     c                   eval      ghmrk = rbhmrk
      *
      * Flytt nytt brudd-begrep til gammelt brudd-begrep
      *
     c                   eval      gmbrd = nybrd
      *
     c                   endsr
      *
     c/eject
      ****************************************************************
      *   SUBRUTINE FOR BEREGNING VED BRUDD PÅ KONTO                 *
      ****************************************************************
      *
     c     brkto         begsr
      *
      * Beregn differanser
      *
     c     l1debe        add       l1kred        l1beve
     c     l1gsal        add       l1beve        l1nsal
      *
      * Skriv heading
      *
B001 c   30              do                                                              .....
 001 c                   exsr      skriv                                                     .
     c                   eval      *in30 = *off
E001 c                   enddo                                                           .....
      *
B001 c                   if        antskr > *zero                                        .....
 001 c                   write     blank                                                     .
E001 c                   endif                                                           .....
      *
      * Skriv linje
      *
     c                   write     ktosum
      *
      * Adder til neste nivå
      *
     c                   add       l1gsal        l2gsal
     c                   add       l1nsal        l2nsal
     c                   add       l1debe        l2debe
     c                   add       l1kred        l2kred
     c                   add       l1beve        l2beve
      *
      * Summer for siste side
      *
     c                   add       l1gsal        lrgsal
     c                   add       l1nsal        lrnsal
     c                   add       l1debe        lrdebe
     c                   add       l1kred        lrkred
     c                   add       l1beve        lrbeve
      *
      * Nullstill arbeidsfelt på L1-nivå
      *
     c                   eval      anttra = *zeros
     c                   eval      antskr = *zeros
      *
     c                   eval      l1gsal = *zeros
     c                   eval      l1nsal = *zeros
     c                   eval      l1debe = *zeros
     c                   eval      l1kred = *zeros
     c                   eval      l1beve = *zeros
      *
     c                   endsr
      *
      ****************************************************************
      *   SUBRUTINE FOR BEREGNING VED BRUDD PÅ AVDELING              *
      ****************************************************************
      *
     c     bravd         begsr
      *
      * Beregn differanser
      *
     c     l2debe        add       l2kred        l2beve
     c     l2gsal        add       l2beve        l2nsal
      *
      * Hent avdelingstekst fra koderegister
      *
     c                   move      gmavd         p_gkod
      *
     c                   call      'RS707R'                                      avdelings-
     c                   parm                    p_stat
     c                   parm                    p_gkod
     c                   parm                    p_gtxt
      *
 001 c                   move      p_gtxt        a07txt
      *
      * Skriv heading
      *
B001 c   30              do                                                              .....
 001 c                   exsr      skriv                                                     .
     c                   eval      *in30 = *off
E001 c                   enddo                                                           .....
      *
      * Skriv linje
      *
     c                   write     avdsum
      *
      * Hent ny avdelingstekst fra koderegister og skriv ny heading
      *
B001 c  n60              do                                                              .....
 001  *                                                                 .
 001 c                   move      nyavd         p_gkod                                      .
      *
 001 c                   call      'RS707R'                                      avdelings-  .
     c                   parm                    p_stat
     c                   parm                    p_gkod
     c                   parm                    p_gtxt
      *
 002 c                   move      p_gtxt        a07txt                                      .
 001  *                                                                 .
 001 c                   exsr      skriv                                                     .
 001  *                                                                 .
E001 c                   enddo                                                           .....
      *
      * Nullstill arbeidsfelt på L1-nivå
      *
     c     l2null        tag
      *
     c                   eval      l2gsal = *zeros
     c                   eval      l2nsal = *zeros
     c                   eval      l2debe = *zeros
     c                   eval      l2kred = *zeros
     c                   eval      l2beve = *zeros
      *
     c                   endsr
      *
      ****************************************************************
      *   SUBRUTINE FOR Å SKRIVE HEADING                             *
      ****************************************************************
      *
     c     skriv         begsr
      *
     c                   write     head1
     c                   write     head4
      *
B001 c                   if        anttra > *zero                                        .....
 001 c                   write     ktotxt
E001 c                   endif                                                           .....
      *
B001 c                   if        antskr > *zero                                        .....
 001 c                   write     transp                                                    .
E001 c                   endif                                                           .....
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Sett startverdier
      *
     c                   eval      nybrd = *zeros
     c                   eval      gmbrd = *zeros
      *
     c                   eval      w_firm = l_firm
      *
     c                   move      rhprÅr        rhtrl3_raar
     c     rhtrl3_key    setll     rhtrl3r
      *
      * Hent avdelingstekst far koderegister
      *
     c                   move      rhpavf        p_gkod
      *
     c                   call      'RS707R'                                      avdelings-
     c                   parm                    p_stat
     c                   parm                    p_gkod
     c                   parm                    p_gtxt
      *
 002 c                   move      p_gtxt        a07txt                                      .
      *
     c                   z-sub     rhpbel        whpbel
      *
      * Skriv heading på første side
      *
     c                   write     head1
     c                   write     head2
     c                   write     head4
      *
     c                   eval      m = *zeros
     c                   eval      n = *zeros
     c                   eval      x = *zeros
      *
     c                   add       1             m
     c                   add       1             n
      *
     c                   eval      anttra = *zeros
     c                   eval      antskr = *zeros
      *
      * Key for kontorekkefølge
      *
     c     rhtrl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhtrl3_raar
      *
     c                   endsr
