      /TITLE  ASSHOP: Henter inn fra ordre-register
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASSHOP
      * Program . . . . : BOHHOR
      * Beskrivelse . . : Hentet inn fra ordre-register
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffohela    if   e           k disk    rename(fohepfr:fohelar)
     ffohelk    if   e           k disk    rename(fohepfr:fohelkr)
     ffohelo    if   e           k disk    rename(fohepfr:fohelor)
     ffohelv    if   e           k disk    rename(fohepfr:fohelvr)
     fbohhod    cf   e             workstn
     f                                     sfile(b1sfl:w_srrn)
      *
      * Definering av Local data area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
      *
      * Definering av parametere
      *
     d p_retu          s                   like(w_alf1)
     d p_firm          s                   like(fofirm)
     d p_numm          s                   like(fonumm)
     d p_suff          s                   like(fosuff)
      *
      * Definering av variabler i nøkler
      *
     d w_firm          s                   like(fofirm)
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d fohelk_kund     s                   like(fokund)
     d fohelk_numm     s                   like(fonumm)
     d fohelk_suff     s                   like(fosuff)
     d fohela_kuna     s                   like(fokuna)
     d fohela_navn     s                   like(fonavn)
     d fohela_numm     s                   like(fonumm)
     d fohela_suff     s                   like(fosuff)
     d fohelo_numm     s                   like(fonumm)
     d fohelo_otyp     s                   like(footyp)
     d fohelo_suff     s                   like(fosuff)
     d fohelv_navn     s                   like(fonavn)
     d fohelv_numm     s                   like(fonumm)
     d fohelv_suff     s                   like(fosuff)
     d rkunl1_kund     s                   like(rkkund)
     d votyl1_otyp     s                   like(vaotyp)
      *
      * Definering av variable
      *
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d w_alf1          s              1
     d w_test          s              1  0
     d w_numm          s                   like(fonumm)
     d w_suff          s                   like(fosuff)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(9)
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inka = *on
     c                   exsr      xh1win
     c                   goto      b2tagb
     c                   when      *inkc = *on
     c                   eval      p_retu = '9'
     c                   goto      avslutt
     c                   when      *inke = *on
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10 = *on
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2numm <> *zero  or
     c                             b2kuna <> *blank or
     c                             b2navn <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   if        p_retu = ' '
     c                   goto      b2taga
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   select
     c                   when      w_seqe = 'A'
     c     fohela_key    setll     fohela
     c                   when      w_seqe = 'V'
     c     fohelv_key    setll     fohelv
     c                   when      w_seqe = 'K'
     c     fohelk_key    setll     fohelk
     c                   when      w_seqe = 'O'
     c     fohelo_key    setll     fohelo
     c                   when      w_seqe = 'B'
     c     fohel1_key    setll     fohel1
     c                   other
     c     fohel1_key    setll     fohel1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner
      * startverdi
      * på ordrenummer, men rutinen blar bakfra
      *
     c                   if        b2numm <> *zero
     c                   if        w_seqe = 'B'
     c                   eval      fohel1_numm = b2numm
     c                   eval      fohel1_suff = 99
     c     fohel1_key    setll     fohel1
     c                   goto      end_pos
     c                   endif
     c                   endif
      *
      * Posisjoner
      * startverdi
      * på ordretype + ordrenummer
      *
     c                   if        b2numm <> *zero
     c                   if        w_seqe = 'O'
     c                   eval      fohelo_numm = b2numm
     c                   eval      fohelo_suff = *zero
     c     fohelo_key    setll     fohelo
     c                   goto      end_pos
     c                   endif
     c                   endif
      *
      * Posisjoner
      * startverdi
      * på kundenummer + ordrenummer
      *
     c                   if        b2numm <> *zero
     c                   if        w_seqe = 'K'
     c                   eval      fohelk_numm = b2numm
     c                   eval      fohelk_suff = *zero
     c     fohelk_key    setll     fohelk
     c                   goto      end_pos
     c                   endif
     c                   endif
      *
      * Posisjoner
      * startverdi
      * på ordrenummer
      *
     c                   if        b2numm <> *zero
     c                   eval      w_seqe = ' '
     c                   eval      fohel1_numm = b2numm
     c                   eval      fohel1_suff = *zero
     c     fohel1_key    setll     fohel1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner
      * startverdi
      * på kundenavn+vareadresse
      *
     c                   if        b2kuna <> *blank
     c                   eval      w_seqe = 'A'
     c                   eval      fohela_numm = *zero
     c                   eval      fohela_suff = *zero
     c                   movel     b2kuna        fohela_kuna
     c                   move      b2navn        fohela_navn
     c     fohela_key    setll     fohela
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner
      * startverdi
      * på vareadresse
      *
     c                   if        b2navn <> *blank
     c                   eval      w_seqe = 'V'
     c                   eval      fohelv_numm = *zero
     c                   eval      fohelv_suff = *zero
     c                   eval      fohelv_navn = b2navn
     c     fohelv_key    setll     fohelv
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2numm = *zero
     c                   eval      b2kuna = *blank
     c                   eval      b2navn = *blank
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16 = *on
     c                   leave
     c                   endif
      *
      * Sjekk, er denne posten allerede til behandling
      *
     c                   exsr      xaktiv
     c                   if        w_test <> *zero
     c                   eval      b1valg = *blank
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Valgt post taes med tilbake til hovedprogram
      *
     c                   if        b1valg = '1' or
     c                             b1valg = '2'
     c                   eval      p_retu = b1valg
     c                   eval      p_numm = b1numm
     c                   eval      p_suff = b1suff
     c                   leave
     c                   endif
      *
      * Vise ordre
      *
     c                   if        b1valg = '5'
     c                   eval      w_numm = b1numm
     c                   eval      w_suff = b1suff
      *
     c                   call      'FO505R'
     c                   parm                    w_firm
     c                   parm                    w_numm
     c                   parm                    w_suff
     c                   eval      b1valg = ' '
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
     c     end_subfile   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * H1 Behandle bilde for bla-alternativer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xh1win        begsr
      *
      * Send Alt
      *
     c                   write     h1bld
     c                   exfmt     h1bld
      *
      * F3=Avslutt subrutinen
      *
     c     *inkc         cabeq     *on           xh1slt
      *
      * Ved blaing i ordre-register skal det blas bakfra
      *
     c                   if        h1valg = '1'
     c                   eval      w_seqe = 'B'
     c                   eval      fohel1_numm = 999999
     c                   eval      fohel1_suff = 99
     c     fohel1_key    setll     fohel1
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
     c                   eval      b2numm = *zero
     c                   eval      b2kuna = *blank
     c                   eval      b2navn = *blank
     c                   eval      h1valg = *blank
     c                   goto      xh1slt
     c                   endif
      *
      * Kun en bestemt ordre-type
      *
     c                   if        h1otyp <> *blank
     c                   eval      w_seqe = 'O'
     c                   eval      fohelo_numm = *zero
     c                   eval      fohelo_suff = *zero
     c                   eval      fohelo_otyp = h1otyp
     c     fohelo_key    setll     fohelo
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
     c                   eval      b2numm = *zero
     c                   eval      b2kuna = *blank
     c                   eval      b2navn = *blank
     c                   goto      xh1slt
     c                   endif
      *
      * Kun ordre for denne kunde skal vises
      *
     c                   if        h1kund <> *zero
     c                   eval      w_seqe = 'K'
     c                   eval      fohelk_numm = *zero
     c                   eval      fohelk_suff = *zero
     c                   eval      fohelk_kund = h1kund
     c     fohelk_key    setll     fohelk
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
     c                   eval      b2numm = *zero
     c                   eval      b2kuna = *blank
     c                   eval      b2navn = *blank
     c                   goto      xh1slt
     c                   endif
      *
      * Dersom ingen er valgt, men ENTER er trykket
      * skal startverdi settes til ordrenummer
      *
     c                   eval      w_seqe = ' '
     c                   eval      fohel1_numm = *zero
     c                   eval      fohel1_suff = *zero
     c     fohel1_key    setll     fohel1
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
     c                   eval      b2numm = *zero
     c                   eval      b2kuna = *blank
     c                   eval      b2navn = *blank
      *
     c     xh1slt        endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine som sjekker om ordren allerede er til behandling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xaktiv        begsr
      *
     c                   eval      w_test = *zero
      *
      * Sjekk,  Er denne posten allerede til behandling,
      *         Denne sjekken må utføres fordi subfilen
      *         ikke alltid er oppdatert
      *
     c                   eval      fohel1_numm = b1numm
     c                   eval      fohel1_suff = b1suff
     c     fohel1_key    chain     fohel1                             90
     c                   if        *in90 = *off
     c                   if        fokode <> *zero
     c                   eval      w_test = 1
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
      * Velg logisk fil
      *
     c                   select
     c                   when      w_seqe = 'A'
     c                   read      fohela                                 15
     c                   when      w_seqe = 'V'
     c                   read      fohelv                                 15
     c                   when      w_seqe = 'K'
     c                   read      fohelk                                 15
     c                   when      w_seqe = 'O'
     c                   read      fohelo                                 15
     c                   when      w_seqe = 'B'
     c                   readp     fohel1                                 15
     c                   other
     c                   read      fohel1                                 15
     c                   endsl
      *
      * End of file
      *
     c                   if        *in15
     c                   goto      end_crt
     c                   endif
      *
      * Kommet til neste firma ?
      *
     c                   if        fofirm <> w_firm
     c                   eval      *in15  = *on
     c                   goto      end_crt
     c                   endif
      *
      * Skal denne ordretype vises ?
      *
     c                   eval      votyl1_otyp = footyp
     c     votyl1_key    chain     votyl1r                            90
     c                   if        vaobut = *zero
     c                   iter
     c                   endif
      *
      * Uaktuell ordretype ?
      *
     c                   if        h1otyp <> *blank and
     c                             h1otyp <> footyp
     c                   iter
     c                   endif
      *
      * Uaktuell kunde ?
      *
     c                   if        h1kund <> 0 and
     c                             h1kund <> fokund
     c                   iter
     c                   endif
      *
      * Fyll opp subfile
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1numm = fonumm
     c                   eval      b1suff = fosuff
     c                   eval      b1otyp = footyp
     c                   eval      b1navn = fonavn
      *
     c                   eval      b1kuna = *blank
     c                   eval      rkunl1_kund = fokund
     c     rkunl1_key    chain     rkunl1r                            90
     c                   if        *in90 = *off
     c                   movel     rknavn        b1kuna
     c                   else
     c                   movel     fokuna        b1kuna
     c                   endif
      *
     c                   write     b1sfl
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      wvisrn = w_sfrn
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for file : KUNDE-REGISTER
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for file : ORDRE-TYPE-REGISTER
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for file : ORDRE-HODE-REGISTER
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      * Key for file : ORDRE-HODE-KUNDENAVN-VAREADRESSE-REGISTER
      *
     c     fohela_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohela_kuna
     c                   kfld                    fohela_navn
     c                   kfld                    fohela_numm
     c                   kfld                    fohela_suff
      *
      * Key for file : ORDRE-HODE-KUNDENUMMER-REGISTER
      *
     c     fohelk_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelk_kund
     c                   kfld                    fohelk_numm
     c                   kfld                    fohelk_suff
      *
      * Key for file : ORDRE-HODE-ORDRETYPE-REGISTER
      *
     c     fohelo_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelo_otyp
     c                   kfld                    fohelo_numm
     c                   kfld                    fohelo_suff
      *
      * Key for file : ORDRE-HODE-VAREADRESSE-REGISTER
      *
     c     fohelv_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelv_navn
     c                   kfld                    fohelv_numm
     c                   kfld                    fohelv_suff
      *
      * Tar inn parameter fra forrige program
      *
     c     *entry        plist
     c                   parm                    p_retu
     c                   parm                    p_firm
     c                   parm                    p_numm
     c                   parm                    p_suff
      *
      * Legger inn firma i nøkklene
      *
     c                   eval      w_firm = p_firm
      *
      * Nullstiller felter som alltid skal være blanke/null
      *
     c                   eval      fohel1_numm = *zero
     c                   eval      fohel1_suff = *zero
     c                   eval      fohela_numm = *zero
     c                   eval      fohela_suff = *zero
     c                   eval      fohelk_numm = *zero
     c                   eval      fohelk_suff = *zero
     c                   eval      fohelo_numm = *zero
     c                   eval      fohelo_suff = *zero
     c                   eval      fohelv_numm = *zero
     c                   eval      fohelv_suff = *zero
      *
      * Fyller opp skjermbildet for første visning
      *
     c     fohel1_key    setll     fohel1
     c                   exsr      crt_subfile
      *
     c                   endsr
