     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : EM101R
      * Beskrivelse . . : Bonus, Maler, vedlikehold - parameter (liste)
      *
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       150611 bsa  Nytt program
6.20  *       210212 bsa  Fjernet krav til kundeinfo. Spes for MGR
6.21  *       081113 bsa  Fjernet spesial for MGR
6.22  *       221113 bsa  Problemer med retur etter opprettelse av ny
6.22  *                   mal.
6.23  *       251113 bsa  Lagt inn mulighet for kopiering av mal til
6.23  *                   enkeltkunder.
6.nu  *       251115 bsa  Utvidet nummer
7.xx  * 7.00  110316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.00  * 7.00  110316 bsa  Returnerer ikke til riktig sted etter opprettelse.
7.01  * 7.00  160420 bsa  Skal ikke kunne opprette mer en en mal pr bonustype.
7.02  * 7.000 220413 bsa  Lagt til bonustype 05
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fembhir    if   e           k disk    rename(embhst:embhirr)
     fembhi1    if   e           k disk    rename(embhst:embhi1r)
     fembhiu    uf a e           k disk    rename(embhst:embhiur)
     fvbotl1    if   e           k disk    rename(vbotpfr:vbotl1r)
     fra11l1    if   e           k disk    rename(ra11pfr:ra11l1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     f*ekboiu    uf a e           k disk    rename(ekbost:ekboiur)
     f*ekboic    uf a e           k disk    rename(ekbost:ekboicr)
     f*ekbeiu    uf a e           k disk    rename(ekbest:ekbeiur)
     f*ekbtiu    uf a e           k disk    rename(ekbtst:ekbtiur)
     fembeiu    uf a e           k disk    rename(embest:embeiur)
     fembeir    if   e           k disk    rename(embest:embeirr)
     f*                                     prefix(xe:2)
     fembdiu    uf a e           k disk    rename(embdst:embdiur)
     fembdir    if   e           k disk    rename(embdst:embdirr)
     f*                                     prefix(xd:2)
     fembkiu    uf a e           k disk    rename(embkst:embkiur)
      *
     fem101d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d  l_dato               101    108
     d  l_tek1               201    270
     d  l_tek2               301    370
     d  l_tek3               401    470
     d  l_user               911    920
     d  l_firm               944    946  0
     d  l_fnav               951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d embhir_hbot     s                   like(emhbot)
      *
     d embhi1_hbot     s                   like(emhbot)
     d embhi1_hmnr     s                   like(emhmnr)
      *
     d embhiu_hbot     s                   like(emhbot)
     d embhiu_hmnr     s                   like(emhmnr)
      *
     d embeir_ebot     s                   like(emebot)
     d embeir_emnr     s                   like(ememnr)
      *
     d embeiu_ebot     s                   like(emebot)
     d embeiu_emnr     s                   like(ememnr)
     d embeiu_eogr     s                   like(emeogr)
     d embeiu_ehgr     s                   like(emehgr)
     d embeiu_eugr     s                   like(emeugr)
     d embeiu_eldo     s                   like(emeldo)
     d embeiu_emod     s                   like(ememod)
     d embeiu_evar     s                   like(emevar)
     d embeiu_eenh     s                   like(emeenh)
     d embeiu_elet     s                   like(emelet)
     d embeiu_efda     s                   like(emefda)
     d embeiu_etda     s                   like(emetda)
      *
     d embdir_dbot     s                   like(emdbot)
     d embdir_dmnr     s                   like(emdmnr)
      *
     d embdiu_dbot     s                   like(emdbot)
     d embdiu_dmnr     s                   like(emdmnr)
     d embdiu_dogr     s                   like(emdogr)
     d embdiu_dhgr     s                   like(emdhgr)
     d embdiu_dugr     s                   like(emdugr)
     d embdiu_dldo     s                   like(emdldo)
     d embdiu_dmod     s                   like(emdmod)
     d embdiu_dvar     s                   like(emdvar)
     d embdiu_denh     s                   like(emdenh)
     d embdiu_dlet     s                   like(emdlet)
     d embdiu_dfda     s                   like(emdfda)
     d embdiu_dtda     s                   like(emdtda)
     d embdiu_dbgf     s                   like(emdbgf)
     d embdiu_dbgt     s                   like(emdbgt)
      *
     d*embdi2_dbot     s                   like(emdbot)
     d*embdi2_dmnr     s                   like(emdmnr)
     d*embdi2_dogr     s                   like(emdogr)
     d*embdi2_dhgr     s                   like(emdhgr)
     d*embdi2_dugr     s                   like(emdugr)
     d*embdi2_dldo     s                   like(emdldo)
     d*embdi2_dmod     s                   like(emdmod)
     d*embdi2_dvar     s                   like(emdvar)
     d*embdi2_denh     s                   like(emdenh)
     d*embdi2_dlet     s                   like(emdlet)
     d*embdi2_dfda     s                   like(emdfda)
     d*embdi2_dtda     s                   like(emdtda)
      *
     d*ekboiu_bkun     s                   like(ekbkun)
     d*ekboiu_rkat     s                   like(ekrkat)
     d*ekboiu_kund     s                   like(ekkund)
     d*ekboiu_kpro     s                   like(ekkpro)
     d*ekboiu_ogrp     s                   like(ekogrp)
     d*ekboiu_hgrp     s                   like(ekhgrp)
     d*ekboiu_ugrp     s                   like(ekugrp)
     d*ekboiu_ldor     s                   like(ekldor)
     d*ekboiu_modn     s                   like(ekmodn)
     d*ekboiu_vare     s                   like(ekvare)
     d*ekboiu_enhe     s                   like(ekenhe)
     d*ekboiu_lety     s                   like(eklety)
     d*ekboiu_boty     s                   like(ekboty)
     d*ekboiu_fdat     s                   like(ekfdat)
     d*ekboiu_tdat     s                   like(ektdat)
      *
     d*ekboic_bkun     s                   like(ekbkun)
     d*ekboic_boty     s                   like(ekboty)
      *
     d*ekbeiu_ebku     s                   like(ekebku)
     d*ekbeiu_erka     s                   like(ekerka)
     d*ekbeiu_ekun     s                   like(ekekun)
     d*ekbeiu_ekpr     s                   like(ekekpr)
     d*ekbeiu_eogr     s                   like(ekeogr)
     d*ekbeiu_ehgr     s                   like(ekehgr)
     d*ekbeiu_eugr     s                   like(ekeugr)
     d*ekbeiu_eldo     s                   like(ekeldo)
     d*ekbeiu_emod     s                   like(ekemod)
     d*ekbeiu_evar     s                   like(ekevar)
     d*ekbeiu_eenh     s                   like(ekeenh)
     d*ekbeiu_elet     s                   like(ekelet)
     d*ekbeiu_ebot     s                   like(ekebot)
     d*ekbeiu_efda     s                   like(ekefda)
     d*ekbeiu_etda     s                   like(eketda)
      *
     d*ekbtiu_tbku     s                   like(ektbku)
     d*ekbtiu_tbot     s                   like(ektbot)
      *
     d vbotl1_tbot     s                   like(vatbot)
     d ra11l1_kkod     s                   like(rakkod)
     d rkunl1_kund     s                   like(rkkund)
      *
     d embkiu_kbot     s                   like(emkbot)
     d embkiu_kmnr     s                   like(emkmnr)
     d embkiu_kkat     s                   like(emkkat)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
     d b_oppr          s              1    inz(*off)
     d b_bonus         s              1    inz(*off)
     d b_betb          s              1    inz(*off)
7.01 d b_newm          s              1    inz(*off)
      *
     d w_udat          s               d   datfmt(*iso)
     d w_udat_8        s              8
     d w_firm_alf      s              3
     d w_kund_alf      s              6
     d w_paar_alf      s              4
     d w_m1ar          s              4  0
     d w_naar          s              4  0
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_type          s              2
     d w_fast          s             10
6.nu d w_numm          s              8  0
     d w_kode          s              1
     d w_fell          s              6
7.01 d w_mld1          s             50
      *
     d w_firm          s                   like(emhfir)
     d k_hmnr          s                   like(emhmnr)
     d d_hmnr          s                   like(emhmnr)
      *
     d p_hbot          s                   like(emhbot)
     d p_ok            s              1
      *
     d b_kuti          s              1    inz(*off)
      *
      * Definering av konstanter
      *
7.xx d c_sfil          s              2  0 inz(13)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * K1WIN  - K1 Vindu for kopiere
      * O1WIN  - O1 Vindu for starte beregnign av årsbonus for kunde
      * M1WIN  - M1 Vindu for innlegging av prosenter for masseoppdatering
      * L1WIN  - L1 Vindu for innlegging av tidsrom for masseoppdatering
      * P1WIN  - P1 Vindu for starte beregnign av årsbonus for alle kunder
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
7.01 c                   eval      b_newm = *on
7.01 c                   exsr      xc1sjk
7.01 c                   if        b_newm = *off
7.01 c                   goto      b2taga
7.01 c                   endif
     c                   exsr      xc1bld
7.00 c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c     embhir_key    setll     embhir
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Vedlikehold av bonus
      *
     c                   if        b1valg = 2
     c                   eval      c1tbot = b2tbot
     c                   eval      c1tbes = b2tbes
     c                   eval      c1hmnr = b1hmnr
     c                   eval      embhi1_hbot = b2tbot
     c                   eval      embhi1_hmnr = b1hmnr
     c                   exsr      xc2bld
     c                   eval      b_forn = *on
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier bonus
      *
     c                   if        b1valg = 3
     c                   exsr      xk1win
     c                   eval      b_forn = *on
     c                   eval      b1valg = 0
     c                   iter
     c                   endif
      *
      * Slette bonus
      *
     c                   if        b1valg = 4
     c                   exsr      xd1win
     c                   eval      b_forn = *on
     c                   eval      b1valg = 0
     c                   iter
     c                   endif
      *
      * Vise bonus
      *
     c                   if        b1valg = 5
     c                   eval      c1tbot = b2tbot
     c                   eval      c1tbes = b2tbes
     c                   eval      c1hmnr = b1hmnr
     c                   eval      embhi1_hbot = b2tbot
     c                   eval      embhi1_hmnr = b1hmnr
     c                   exsr      xc2bld
     c                   eval      b_forn = *on
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Bonus
      *
     c                   if        b1valg = 7
     c                   call      'EM102R'
     c                   parm                    w_firm
     c                   parm                    p_hbot
     c                   parm                    b1hmnr
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier avtale til kundekategori
      *
     c                   if        b1valg = 9
     c                   exsr      xm1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
6.23  *
6.23  * Kopier avtale til kunde
6.23  *
6.23 c                   if        b1valg = 10
6.23 c                   exsr      xe1win
6.23 c                   eval      b1valg = 0
6.23 c                   update    b1sfl
6.23 c                   iter
6.23 c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      embhir
      *
     c                   if        %eof or
     c                             emhfir <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   if        emhbot <> p_hbot
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c**                 select
     c**                 when      w_seqe = 'K'
     c**                 eval      rkunl1_kund = rkkund
     c**                 other
     c**                 eval      rkunl2_alfa = rkalfa
     c**                 endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1hbes = emhbes
     c                   eval      b1hmnr = emhmnr
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     embhir_key    setgt     embhir
     c                   readp     embhir
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     embhir
      *
     c                   if        %eof or
     c                             emhfir <> w_firm
     c                   leave
     c                   endif
      *
     c                   if        emhbot <> p_hbot
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   enddo
      *
     c                   if        %eof
     c     embhir_key    setll     embhir
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  * Subrutine for sjekk om det skal være mulig å legge til mal
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *
7.01 c     xc1sjk        begsr
7.01  *
7.01 c                   eval      w_mld1 = *blanks
7.01 c                   eval      embhir_hbot = b2tbot
7.01 c     embhir_key    chain     embhir
7.01 c                   if        %found
7.01 c                   eval      w_mld1 = 'Kun 1 mal tillat pr bonustype'
7.01 c                   call      'AA005R'
7.01 c                   parm                    w_mld1
7.01 c                   eval      b_newm = *off
7.01 c                   endif
7.01  *
7.01 c                   endsr
7.01  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1tbot = b2tbot
6.22 c**                 eval      c1tbes = b2tbes
6.22 c                   eval      c1tbes = *blanks
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
     c                   if        c1hbes = *blank
     c                   goto      end_c1bld
     c                   endif
      *
     c                   exsr      hent_malnr
      *
     c                   eval      c1hmnr = w_numm
     c                   eval      c2hbes = c1hbes
     c                   eval      embhi1_hbot = c1tbot
     c                   eval      embhi1_hmnr = c1hmnr
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
6.22 c**                 eval      c1tbot = *blank
6.22 c                   eval      c1tbes = *blanks
      *
7.01 c**                 goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c     embhi1_key    chain     embhi1
      *
     c                   if        %found
     c                   eval      c2hbes = emhbes
     c                   eval      c2hbka = emhbka
     c                   if        emhfra <> *loval
     c     *dmy          move      emhfra        c2hfra
     c                   endif
     c                   if        emhtil <> *loval
     c     *dmy          move      emhtil        c2htil
     c                   endif
     c                   eval      c2heus = emheus
     c                   if        emheda <> *loval
     c     *dmy          move      emheda        c2heda
     c                   endif
     c                   if        emhoda <> *loval
     c     *dmy          move      emhoda        c2hoda
     c                   endif
     c                   else
     c                   eval      c2hbes = *blank
     c                   eval      c2hbka = 0
     c                   if        c1hbes <> *blank and
     c                             k1hbes = *blank
     c                   eval      c2hbes = c1hbes
     c                   endif
     c                   if        k1hbes <> *blank and
     c                             c1hbes = *blank
     c                   eval      c2hbes = k1hbes
     c                   endif
     c                   eval      c2heus = *blank
     c                   eval      c2heda = 0
     c                   eval      c2hoda = 0
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Validering av input
      *
     c**                 if        c2hbes = *blank
     c*                  eval      *in31 = *on
     c*                  goto      c2taga
     c*                  endif
      *
      * Validering av input
      *
     c                   if        c2hbka < 0 or
6.21 c*                            c2hbka > 1
7.02 c                             c2hbka > 6
     c                   eval      *in33 = *on
     c                   goto      c2taga
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   embhiur
      *
     c                   eval      embhiu_hbot = c1tbot
     c                   eval      embhiu_hmnr = c1hmnr
     c     embhiu_key    chain     embhiur
      *
     c                   eval      emhbes = c2hbes
     c                   eval      emhbka = c2hbka
     c                   if        c2hfra <> 0
     c     *dmy          move      c2hfra        emhfra
     c                   else
     c                   eval      emhfra = *loval
     c                   endif
     c                   if        c2htil <> 0
     c     *dmy          move      c2htil        emhtil
     c                   else
     c                   eval      emhtil = *loval
     c                   endif
      *
     c                   time                    emheda
     c                   time                    emheti
     c                   eval      emheus = l_user
      *
     c                   if        %found
     c                   update    embhiur
     c                   else
     c                   eval      emhfir = w_firm
     c                   eval      emhbot = c1tbot
     c                   eval      emhmnr = c1hmnr
     c                   time                    emhoda
     c                   time                    emhoti
     c                   eval      emhous = l_user
     c                   write     embhiur
     c                   endif
      *
      * Oppdater subfile
      *
     c*                  if        b1valg = 2
     c*                  eval      b1tbes = vatbes
     c*                  endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kopiering av bonus
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Fyller kopierings-bildet
      *
     c                   eval      k1hbot = b2tbot
     c                   eval      k1tbes = b2tbes
     c                   eval      k1hbes = *blank
     c                   eval      emhbot = b2tbot
     c                   eval      vatbes = b2tbes
     c                   eval      emhbes = b1hbes
     c                   eval      k_hmnr = b1hmnr
      *
     c     k1tagb        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * Sjekker input
      *
     c                   if        k1hbes = *blank
     c                   eval      *in31 = *on
     c                   goto      k1tagb
     c                   endif
      *
      * Hent inn nytt nummer
      *
     c                   exsr      hent_malnr
      *
     c                   eval      c1tbot = k1hbot
     c                   eval      c1tbes = k1tbes
     c                   eval      c1hmnr = w_numm
     c                   eval      c2hbes = k1hbes
     c                   eval      embhi1_hbot = k1hbot
     c                   eval      embhi1_hmnr = w_numm
      *
      * Kopiere med underliggende bonuselementer
      *
     c                   if        *inkg
     c                   exsr      kopi_belem
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      b_oppr = *off
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter inn neste ledige nummer for mal
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kopi_belem    begsr
      *
      * Opprett først ny post
      *
     c                   clear                   embhiur
      *
     c                   eval      embhiu_hbot = k1hbot
     c                   eval      embhiu_hmnr = w_numm
     c     embhiu_key    chain     embhiur
      *
     c                   eval      emhbes = k1hbes
      *
     c                   time                    emheda
     c                   time                    emheti
     c                   eval      emheus = l_user
      *
     c                   if        %found
     c                   update    embhiur
     c                   else
     c                   eval      emhfir = w_firm
     c                   eval      emhbot = k1hbot
     c                   eval      emhmnr = w_numm
     c                   time                    emhoda
     c                   time                    emhoti
     c                   eval      emhous = l_user
     c                   write     embhiur
     c                   endif
      *
      * Finn avtalen som skal kopieres, og legg den over
      *
      * Les først fra bonuselementer
      *
     c                   eval      embeir_ebot = k1hbot
     c                   eval      embeir_emnr = k_hmnr
     c     embeir_key    setll     embeir
     c     embeir_key    reade     embeir
     c                   dow       not %eof
     c                   eval      embeiu_ebot = emebot
     c                   eval      embeiu_emnr = w_numm
     c                   eval      embeiu_eogr = emeogr
     c                   eval      embeiu_ehgr = emehgr
     c                   eval      embeiu_eugr = emeugr
     c                   eval      embeiu_eldo = emeldo
     c                   eval      embeiu_emod = ememod
     c                   eval      embeiu_evar = emevar
     c                   eval      embeiu_eenh = emeenh
     c                   eval      embeiu_elet = emelet
     c                   eval      embeiu_efda = emefda
     c                   eval      embeiu_etda = emetda
     c     embeiu_key    chain     embeiur
      *
     c                   if        not %found
     c                   clear                   embeiur
     c                   eval      emefir = w_firm
     c                   eval      emebot = k1hbot
     c                   eval      ememnr = w_numm
     c                   eval      emeogr = emeogr
     c                   eval      emehgr = emehgr
     c                   eval      emeugr = emeugr
     c                   eval      emeldo = emeldo
     c                   eval      ememod = ememod
     c                   eval      emevar = emevar
     c                   eval      emeenh = emeenh
     c                   eval      emelet = emelet
     c                   eval      emefda = emefda
     c                   eval      emetda = emetda
      *
     c                   time                    emeoda
     c                   time                    emeoti
     c                   eval      emeous = l_user
     c                   endif
      *
     c                   eval      ememrk = ememrk
     c                   time                    emdeda
     c                   time                    emdeti
     c                   eval      emdeus = l_user
      *
     c                   if        %found
     c                   update    embeiur
     c                   else
     c                   write     embeiur
     c                   endif
      *
     c     embeir_key    reade     embeir
     c                   enddo
      *
      * Les deretter fra elementdetaljer
      *
     c                   eval      embdir_dbot = k1hbot
     c                   eval      embdir_dmnr = k_hmnr
     c     embdir_key    setll     embdir
     c     embdir_key    reade     embdir
     c                   dow       not %eof
     c                   eval      embdiu_dbot = emdbot
     c                   eval      embdiu_dmnr = w_numm
     c                   eval      embdiu_dogr = emdogr
     c                   eval      embdiu_dhgr = emdhgr
     c                   eval      embdiu_dugr = emdugr
     c                   eval      embdiu_dldo = emdldo
     c                   eval      embdiu_dmod = emdmod
     c                   eval      embdiu_dvar = emdvar
     c                   eval      embdiu_denh = emdenh
     c                   eval      embdiu_dlet = emdlet
     c                   eval      embdiu_dfda = emdfda
     c                   eval      embdiu_dtda = emdtda
     c                   eval      embdiu_dbgf = emdbgf
     c                   eval      embdiu_dbgt = emdbgt
     c     embdiu_key    chain     embdiur
      *
     c                   if        not %found
     c                   clear                   embdiur
     c                   eval      emdfir = w_firm
     c                   eval      emdbot = k1hbot
     c                   eval      emdmnr = w_numm
     c                   eval      emdogr = emdogr
     c                   eval      emdhgr = emdhgr
     c                   eval      emdugr = emdugr
     c                   eval      emdldo = emdldo
     c                   eval      emdmod = emdmod
     c                   eval      emdvar = emdvar
     c                   eval      emdenh = emdenh
     c                   eval      emdlet = emdlet
     c                   eval      emdfda = emdfda
     c                   eval      emdtda = emdtda
     c                   eval      emdbgf = emdbgf
     c                   eval      emdbgt = emdbgt
      *
     c                   time                    emdoda
     c                   time                    emdoti
     c                   eval      emdous = l_user
     c                   endif
      *
     c                   eval      emdmrk = emdmrk
     c                   eval      emdktb = emdktb
     c                   eval      emdbpr = emdbpr
     c                   time                    emdeda
     c                   time                    emdeti
     c                   eval      emdeus = l_user
      *
     c                   if        %found
     c                   update    embdiur
     c                   else
     c                   write     embdiur
     c                   endif
      *
     c     embdir_key    reade     embdir
     c                   enddo
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sletting av bonus
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
      * Sletter bonus
      *
     c                   eval      d1tbot = b2tbot
     c                   eval      d1tbes = b2tbes
     c                   eval      d1hbes = b1hbes
     c                   eval      d_hmnr = b1hmnr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * Sletter bonusnivå
      *
     c                   eval      b_forn = *on
      *
     c                   eval      embhiu_hbot = d1tbot
     c                   eval      embhiu_hmnr = d_hmnr
     c     embhiu_key    chain     embhiur
     c                   delete    embhiur
      *
      * Slette bonuselementer til malen
      *
     c                   eval      embeir_ebot = d1tbot
     c                   eval      embeir_emnr = d_hmnr
      *
     c     embeir_key    setll     embeiu
     c     embeir_key    reade     embeiu
     c                   dow       not %eof(embeiu)
     c                   delete    embeiur
     c     embeir_key    reade     embeiu
     c                   enddo
      *
      * Slette bonusdetaljer til malen
      *
     c                   eval      embdir_dbot = d1tbot
     c                   eval      embdir_dmnr = d_hmnr
      *
     c     embdir_key    setll     embdiu
     c     embdir_key    reade     embdiu
     c                   dow       not %eof(embdiu)
     c                   delete    embdiur
     c     embdir_key    reade     embdiu
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Kopiere inn underliggende elementer fra avtale
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_malnr    begsr
      *
      * Hent inn neste ledige nummer for mal
      *
     c                   eval      w_kode = '0'
     c                   eval      w_firm = l_firm
     c                   eval      w_fell = 'BONUSM'
     c                   eval      w_type = *blank
     c                   eval      w_fast = *blank
     c                   eval      w_numm = *zero
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
     c                   parm                    w_numm
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kopiering av bonusmal til kundekategori
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xm1win        begsr
      *
     c                   eval      m1kkat = 0
     c                   eval      m1ktxt = *blank
      *
     c     m1tagb        tag
      *
     c                   exfmt     m1win
      *
      * Spørring
      *
     c                   if        *inkd = *on
     c                   exsr      spØrring
     c                   goto      m1tagb
     c                   endif
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   if        m1kkat = 0
     c                   goto      m1tagb
     c                   endif
      *
     c                   eval      ra11l1_kkod = m1kkat
     c     ra11l1_key    chain     ra11l1                             36
6.23 c                   if        *in36 = *on
     c                   goto      m1tagb
     c                   endif
      *
      * Leser alle kunder i en kundekategori
      *
     c     rkunl1_key2   setll     rkunl1
     c     rkunl1_key2   reade     rkunl1
     c                   dow       not %eof(rkunl1)
     c                   if        rkkatg = m1kkat
     c                   exsr      kopier
     c                   endif
     c     rkunl1_key2   reade     rkunl1
     c                   enddo
      *
      * Oppdater fil med info om link mellom maler og kundekategorier
      *
     c                   eval      embkiu_kbot = p_hbot
     c                   eval      embkiu_kmnr = b1hmnr
     c                   eval      embkiu_kkat = m1kkat
     c     embkiu_key    chain     embkiu
     c                   time                    emkdat
     c                   time                    emkeda
     c                   time                    emketi
     c                   eval      emkeus = l_user
     c                   if        %found
     c                   update    embkiur
     c                   else
     c                   eval      emkfir = w_firm
     c                   eval      emkbot = p_hbot
     c                   eval      emkmnr = b1hmnr
     c                   eval      emkkat = m1kkat
     c                   time                    emkoda
     c                   time                    emkoti
     c                   eval      emkous = l_user
     c                   write     embkiur
     c                   endif
      *
     c                   eval      b_forn = *on
     c                   eval      m1kkat = 0
      *
     c                   endsr
      *
6.23  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.23  * Subrutine for kopiering av bonusmal til enkeltkunde
6.23  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.23  *
6.23 c     xe1win        begsr
6.23  *
6.23 c                   eval      e1kund = 0
6.23 c                   eval      e1ktxt = *blank
6.23  *
6.23 c     e1tagb        tag
6.23  *
6.23 c                   exfmt     e1win
6.23  *
6.23  * Spørring
6.23  *
6.23 c                   if        *inkd = *on
6.23 c                   exsr      spØrring
6.23 c                   goto      e1tagb
6.23 c                   endif
6.23  *
6.23 c                   if        *inkc or *inkl
6.23 c                   leavesr
6.23 c                   endif
6.23  *
6.23 c                   if        e1kund = 0
6.23 c                   goto      e1tagb
6.23 c                   endif
6.23  *
6.23 c                   eval      rkunl1_kund = e1kund
6.23 c     rkunl1_key    chain     rkunl1                             36
6.23 c                   if        *in36 = *on
6.23 c                   goto      e1tagb
6.23 c                   endif
6.23  *
6.23  * Start kopiering
6.23  *
6.23 c                   exsr      kopier
6.23  *
6.23 c                   eval      b_forn = *on
6.23 c                   eval      e1kund = 0
6.23  *
6.23 c                   endsr
6.23  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Kundekategori
      *
     c                   if        w_afld = 'M1KKAT'
     c                   call      'RA511R'
     c                   parm                    w_firm
     c                   parm                    m1kkat
     c                   parm                    m1ktxt
     c                   leavesr
     c                   endif
6.23  *
6.23  * Kunde
6.23  *
6.23 c                   if        w_afld = 'E1KUND'
6.23 c                   call      'RK500R'
6.23 c                   parm                    w_firm
6.23 c                   parm                    e1kund
6.23 c                   parm                    e1ktxt
6.23 c                   leavesr
6.23 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å kopiere av bonusmal
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kopier        begsr
      * Vi må finne bonustypen
     c                   eval      b_betb = *off
     c                   eval      embhi1_hbot = b2tbot
     c                   eval      embhi1_hmnr = b1hmnr
     c     embhi1_key    chain     embhi1
     c                   if        %found and
     c                             emhbka = 3
     c                   eval      b_betb = *on
     c                   endif
      * Sjekk om kunden oppfyller krav til kopiering av valgt bonusmal
6.20 c                   if        1=2
     c                   eval      p_ok = '0'
     c                   call      'EM114R'
     c                   parm                    rkkund
     c                   parm                    b2tbot
     c                   parm                    b1hmnr
     c                   parm                    p_ok
6.20 c                   endif
      *
6.20 c**                 if        p_ok = '1'
     c                   if        b_betb = *off
     c                   call      'EM111R'
     c                   parm                    rkkund
     c                   parm                    b2tbot
     c                   parm                    b1hmnr
      *
     c                   else
      * Betalingsbonus
     c                   call      'EM115R'
     c                   parm                    rkkund
     c                   parm                    b2tbot
     c                   parm                    b1hmnr
     c                   endif
6.20 c**                 endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Tar inn parameter fra forrige program
      *
     c     *entry        plist
     c                   parm                    p_hbot
      *
      * Key for oppslag på bonusregister
      *
     c     embhir_key    klist
     c                   kfld                    w_firm
     c                   kfld                    embhir_hbot
      *
      * Key for oppslag på bonusregister
      *
     c     embhi1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    embhi1_hbot
     c                   kfld                    embhi1_hmnr
      *
      * Key for oppslag på bonusregister
      *
     c     embhiu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    embhiu_hbot
     c                   kfld                    embhiu_hmnr
      *
      * Key for oppslag på bonusregister - elementer
      *
     c     embeiu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    embeiu_ebot
     c                   kfld                    embeiu_emnr
     c                   kfld                    embeiu_eogr
     c                   kfld                    embeiu_ehgr
     c                   kfld                    embeiu_eugr
     c                   kfld                    embeiu_eldo
     c                   kfld                    embeiu_emod
     c                   kfld                    embeiu_evar
     c                   kfld                    embeiu_eenh
     c                   kfld                    embeiu_elet
     c                   kfld                    embeiu_efda
     c                   kfld                    embeiu_etda
      *
      * Key for oppslag på bonusregister - elementer
      *
     c     embeir_key    klist
     c                   kfld                    w_firm
     c                   kfld                    embeir_ebot
     c                   kfld                    embeir_emnr
      *
      * Key for oppslag på bonusregister - elementdetaljer
      *
     c     embdiu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    embdiu_dbot
     c                   kfld                    embdiu_dmnr
     c                   kfld                    embdiu_dogr
     c                   kfld                    embdiu_dhgr
     c                   kfld                    embdiu_dugr
     c                   kfld                    embdiu_dldo
     c                   kfld                    embdiu_dmod
     c                   kfld                    embdiu_dvar
     c                   kfld                    embdiu_denh
     c                   kfld                    embdiu_dlet
     c                   kfld                    embdiu_dfda
     c                   kfld                    embdiu_dtda
     c                   kfld                    embdiu_dbgf
     c                   kfld                    embdiu_dbgt
      *
      * Key for oppslag på bonusregister - elementdetaljer
      *
     c*    embdir_key2   klist
     c*                  kfld                    w_firm
     c*                  kfld                    embdi2_dbot
     c*                  kfld                    embdi2_dmnr
     c*                  kfld                    embdi2_dogr
     c*                  kfld                    embdi2_dhgr
     c*                  kfld                    embdi2_dugr
     c*                  kfld                    embdi2_dldo
     c*                  kfld                    embdi2_dmod
     c*                  kfld                    embdi2_dvar
     c*                  kfld                    embdi2_denh
     c*                  kfld                    embdi2_dlet
     c*                  kfld                    embdi2_dfda
     c*                  kfld                    embdi2_dtda
      *
      * Key for oppslag på bonusregister - elementdetaljer
      *
     c     embdir_key    klist
     c                   kfld                    w_firm
     c                   kfld                    embdir_dbot
     c                   kfld                    embdir_dmnr
      *
      * Key for oppslag på kundebonus
      *
     c*    ekboiu_key2   klist
     c*                  kfld                    w_firm
     c*                  kfld                    ekboiu_bkun
     c*                  kfld                    ekboiu_rkat
     c*                  kfld                    ekboiu_kund
     c*                  kfld                    ekboiu_kpro
     c*                  kfld                    ekboiu_ogrp
     c*                  kfld                    ekboiu_hgrp
     c*                  kfld                    ekboiu_ugrp
     c*                  kfld                    ekboiu_ldor
     c*                  kfld                    ekboiu_modn
     c*                  kfld                    ekboiu_vare
     c*                  kfld                    ekboiu_enhe
     c*                  kfld                    ekboiu_lety
     c*                  kfld                    ekboiu_boty
     c*                  kfld                    ekboiu_fdat
     c*                  kfld                    ekboiu_tdat
      *
      * Key for oppslag på kundebonus
      *
     c*    ekboic_key    klist
     c*                  kfld                    w_firm
     c*                  kfld                    ekboic_bkun
     c*                  kfld                    ekboic_boty
      *
      * Key for skriv på kundebonus-elment
      *
     c*    ekbeiu_key    klist
     c*                  kfld                    w_firm
     c*                  kfld                    ekbeiu_ebku
     c*                  kfld                    ekbeiu_erka
     c*                  kfld                    ekbeiu_ekun
     c*                  kfld                    ekbeiu_ekpr
     c*                  kfld                    ekbeiu_eogr
     c*                  kfld                    ekbeiu_ehgr
     c*                  kfld                    ekbeiu_eugr
     c*                  kfld                    ekbeiu_eldo
     c*                  kfld                    ekbeiu_emod
     c*                  kfld                    ekbeiu_evar
     c*                  kfld                    ekbeiu_eenh
     c*                  kfld                    ekbeiu_elet
     c*                  kfld                    ekbeiu_ebot
     c*                  kfld                    ekbeiu_efda
     c*                  kfld                    ekbeiu_etda
      *
      * Key for oppslag på kunde typeregister
      *
     c*    ekbtiu_key    klist
     c*                  kfld                    w_firm
     c*                  kfld                    ekbtiu_tbku
     c*                  kfld                    ekbtiu_tbot
      *
      * Key for oppslag på bonusregister
      *
     c     vbotl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vbotl1_tbot
      *
      * Key for oppslag på kundekategori
      *
     c     ra11l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra11l1_kkod
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key2   klist
     c                   kfld                    w_firm
6.23  *
6.23  * Key for oppslag på kunde
6.23  *
6.23 c     rkunl1_key    klist
6.23 c                   kfld                    w_firm
6.23 c                   kfld                    rkunl1_kund
      *
      * Key for link mellom mal og kundekategori
      *
     c     embkiu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    embkiu_kbot
     c                   kfld                    embkiu_kmnr
     c                   kfld                    embkiu_kkat
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
     c                   time                    w_date
     c                   time                    w_time
      *
      * Initierer skjermbildet
      *
     c                   eval      b2tbot = *blank
     c                   eval      b2tbes = *blank
      *
     c                   eval      vbotl1_tbot = p_hbot
     c     vbotl1_key    chain     vbotl1
     c                   if        %found
     c                   eval      b2tbot = vatbot
     c                   eval      b2tbes = vatbes
     c                   endif
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      embhir_hbot = p_hbot
     c     embhir_key    setll     embhir
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
