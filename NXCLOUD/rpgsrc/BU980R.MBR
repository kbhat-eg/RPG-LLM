     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASSHOP
      * Program . . . . : BU980R
      * Beskrivelse . . : Butikk, henter ordrenummer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       091104 HKO  Nytt program
      *       030106 HKO  Sjekker om ordrenr allerede finnes som ordre
6.nu  * 6.30  170614 bof  Endring ihht. nummerutvidelse
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fbstsl1    if   e           k disk    rename(bstspfr:bstsl1r)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
      *
      * Definering av parametre
      *
     d p_firm          s              3
     d p_osum          s             11  2
6.nu d p_ornr          s              8  0
     d p_stat          s              8
      *
      * Definering av variable i nøkler
      *
     d fohel1_numm     s                   like(fonumm)
      *
      * Definering av variable
      *
     d b_ornr          s              1    inz(*off)
      *
     d w_firm          s              3  0
     d w_otyp          s              2
     d w_fell          s              6
     d w_type          s              2
     d w_fast          s             10
     d w_kode          s              1
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      b_ornr = *off
      *
     c                   eval      p_ornr = 0
      *
      * Avbryter dersom butikk kode-register ikke ble funnet
      *
     c     bstsl1_key    chain     bstsl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
      * Finner ordretype avhengig av ordresum
      *
     c                   if        p_osum >= 0
     c                   eval      w_otyp = baaocd
     c                   else
     c                   eval      w_otyp = baaock
     c                   endif
      *
      * Hente inn eventuell overstyrt ordretype for nummerserie
      *
     c                   eval      w_type = *blank
      *
     c                   call      'VA752R'
     c                   parm                    w_firm
     c                   parm                    w_otyp
     c                   parm                    w_type
      *
      * Henter neste nummer fra nummer-registeret
      *
     c                   eval      w_kode = '0'
     c                   eval      w_fell = baaord
     c                   eval      w_fast = *blank
      *
     c                   if        w_type = *blank
     c                   eval      w_type = w_otyp
     c                   endif
      *
     c                   dou       b_ornr = *on
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
     c                   parm                    p_ornr
      *
     c                   eval      fohel1_numm = p_ornr
     c     fohel1_key    chain     fohel1
     c                   if        not %found
     c                   eval      b_ornr = *on
     c                   leave
     c                   endif
      *
     c                   enddo
      *
      * Avslutt
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ERROR-rutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *pssr         begsr
      *
     c                   eval      p_stat = 'ERROR'
     c                   goto      avslutt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameter
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_osum
     c                   parm                    p_ornr
     c                   parm                    p_stat
      *
      * Key for oppslag på Butikk koderegister
      *
     c     bstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på Ordre-hode
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
      *
      *
      * Henter firma og kasse fra parameter
      *
     c                   move      p_firm        w_firm
      *
     c                   endsr
