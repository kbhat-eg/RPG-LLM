     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : RY898R
      * Beskrivelse . . : Kopierer priskode/varetype fra EVR til LVR
      *
      * Programmet skal kjøres etter at vare-detalj-register er opprettet
      * for alle varer i LVR
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       140498 HKO  Nytt program
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     fjvdtlu    uf   e           k disk    rename(jvdtpfr:jvdtlur)
      *
      * Lokal data area (*LDA)
      *
     d                uds
     d l_firm                944    946  0
     d l_lage               1001   1002  0
      *
      * Definering av variable i nøkler
      *
     d vlagl1_vare     s                   like(vlvare)
     d vlagl1_lage     s                   like(vllage)
     d jvdtlu_dvar     s                   like(jvdvar)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vvfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     vvarl1_key    setll     vvarl1
     c     vvarl1_key    reade     vvarl1
     c                   dow       not %eof
      *
     c                   eval      jvdtlu_dvar = vvvare
     c     jvdtlu_key    chain     jvdtlu
     c                   if        %found
      *
     c                   eval      jvdpko = vvkpri
     c                   eval      jvdsko = 0
      *
     c                   eval      vlagl1_vare = vvvare
     c                   eval      vlagl1_lage = l_lage
     c     vlagl1_key    chain     vlagl1
     c                   if        %found
     c                   if        vlvtyp = 'S'
     c                   eval      jvdsko = 1
     c                   endif
     c                   else
     c                   if        vvvtyp = 'S'
     c                   eval      jvdsko = 1
     c                   endif
     c                   endif
      *
     c                   update    jvdtlur
      *
     c                   endif
      *
     c     vvarl1_key    reade     vvarl1
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for sekvensiell les av EVR-vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på vare-lager
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl1_vare
     c                   kfld                    vlagl1_lage
      *
      * Key for oppdatering av vare-detalj
      *
     c     jvdtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jvdtlu_dvar
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
