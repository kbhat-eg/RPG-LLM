     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO808R
      * Beskrivelse . . : Behandling av ordre, resting og sortering mm
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * 8.xx  020522 bsa  Nytt program basert på FO614R
8.01  * 8.xx  210622 bsa  Må hente antall fra utplukkstabell.
8.02  * 8.xx  120822 bsa  Sjekk ikke mot brukere.
8.03  * 8.xx  120822 bsa  Bruk antall fra utplukket register uavhengig av
8.03  * 8.xx              om det ikke restes.
8.04  * 8.xx  011122 bsa  Blir feil ved resting av linjer.
8.05  * 8.xx  110123 bsa  Hvis resting, sjekk at det sendes ny transport
8.05  * 8.xx              request på de nye varene.
8.06  *K000   180123 bsa  Flagg om skaffetekst i transportlogg.
8.07  *K000   190123 bsa  Oppdater også orginal ordre, ikke bare ny ved
8.07  *K000               resting.
8.08  *K000   190123 bsa  På nytt suffiks skal eksternordre referanse og
8.08  *K000               eksternsystem nulles.
8.09  *K000   140223 bsa  Flagg om logging skal sjekkes.
8.10  *K000   190124 bsa  Sjekk ekstra gang mot flåtestyring.
8.11  *K000   230124 bsa  Problemer med logging ved bruk av neste ordretype
8.12  *K000   090424 bsa  Kaller program for å hente høyeste suffiks på ordrenummer
8.13  *K000   120424 bsa  Hvis bytte av suffiks, sjekk transporttracking og
8.13  *K000               oppdater med nytt suffiks.
8.14  *K000   290824 bsa  Hvis nytt sufiks, oppdater tilhørende bestillingslinjer
8.15  *       140524 ask  Fjernet skriv til ByggDok
8.16  *K000   261124 bsa  Legger inn ekstra sjekk mot sletting av ordrehode.
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffpm2lu    ip   e           k disk    rename(fpm2pfr:fpm2lur)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     ffohelu    uf a e           k disk    rename(fohepfr:fohelur)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffodtlu    uf a e           k disk    rename(fodtpfr:fodtlur)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     ffodtlr    if   e           k disk    rename(fodtpfr:fodtlrr)
     ffotxl1    if   e           k disk    rename(fotxpfr:fotxl1r)
     ffplol1    if   e           k disk    rename(fplopfr:fplol1r)
     ffplolu    uf   e           k disk    rename(fplopfr:fplolur)
     ffplllu    uf   e           k disk    rename(fpllpfr:fplllur)
8.01 ffplll1    if   e           k disk    rename(fpllpfr:fplll1r)
     ffpsolu    uf a e           k disk    rename(fpsopfr:fpsolur)
     fvinfl1    if   e           k disk    rename(vinfpfr:vinfl1r)
     ffusrl1    if   e           k disk    rename(fusrpfr:fusrl1r)
     ffntrl1    if   e           k disk    rename(fntrpfr:fntrl1r)
     ffmkal1    if   e           k disk    rename(fmkapfr:fmkal1r)
     flwpllu    uf   e           k disk    rename(lwplpfr:lwpllur)
     fsdepl1    if   e           k disk    rename(sdeppfr:sdepl1r)
     fafpslr    if   e           k disk    rename(afpspfr:afpslrr)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     frukpl6    if   e           k disk    rename(rukppfr:rukpl6r)
     famodl1    if   e           k disk    rename(amodpfr:amodl1r)
     fsodtpf    if   e           k disk    rename(sodtpfr:sodtpfr)
8.13 fftrpi2    if   e           k disk    rename(ftrpst:ftrpi2r)
8.13 fftrpiu    uf   e           k disk    rename(ftrpst:ftrpiur)
8.14 flodtlu    uf   e           k disk    rename(lodtpfr:lodtlur)
     ffbdopf    o  a e             disk
      *
      * Definering av data-struktur
      * for lageroppdatering
      *
     d                 ds
     d hlrec                        128
     d  hlvare                       15    overlay(hlrec)
     d  hllage                        2  0 overlay(hlrec:16)
     d  hldato                         d   overlay(hlrec:18) datfmt(*iso)
     d  hltime                         t   overlay(hlrec:28) timfmt(*iso)
     d  hlotyp                        2    overlay(hlrec:36)
     d  hlltyp                        2    overlay(hlrec:38)
     d  hlanta                       11  3 overlay(hlrec:40)
     d  hlkopr                        9  2 overlay(hlrec:51)
     d  hlrefr                       20    overlay(hlrec:60)
     d  hlsyst                        1    overlay(hlrec:80)
     d  hlaltk                        2    overlay(hlrec:81)
     d  hlrest                        1    overlay(hlrec:83)
     d  hltype                        1    overlay(hlrec:84)
     d  hlenhe                        3    overlay(hlrec:85)
     d  hlinlr                        1    overlay(hlrec:88)
     d  hllety                        1    overlay(hlrec:89)
     d  hlonum                        8  0 overlay(hlrec:90)
     d  hlolin                        4  0 overlay(hlrec:98)
      *
      * Definering av data-struktur
      * for sorteringsbegrep
      *
     d                 ds
     d sorter                       114
     d  sor001                        8  0 overlay(sorter)
     d  sor002                       60    overlay(sorter:9)
     d  sor003                       16    overlay(sorter:69)
     d  sor004                        3    overlay(sorter:85)
     d  sor005                       17    overlay(sorter:88)
     d  sor006                        8  0 overlay(sorter:105)
     d  sor007                        2  0 overlay(sorter:113)
      *
     d                 ds
     d d_urec                        80
     d  d_ufir                        3  0 overlay(d_urec)
     d  d_aarr                        4  0 overlay(d_urec:4)
     d  d_unum                        8  0 overlay(d_urec:8)
     d  d_usuf                        2  0 overlay(d_urec:16)
     d  d_ukod                        1    overlay(d_urec:18)
     d  d_date                         d   overlay(d_urec:19) datfmt(*iso)
     d  d_time                         t   overlay(d_urec:29) timfmt(*iso)
     d  d_user                       10    overlay(d_urec:37)
     d  d_wsid                       10    overlay(d_urec:47)
      *
      *
      * Definering av local data area
      *
     d lda            uds
     d l_alt1                540    540  0
     d l_alt2                541    541  0
     d l_peri                561    566  0
     d l_dato                567    576d   datfmt(*iso)
      *
     d l_ruti                800    809
     d l_outq                810    819
     d l_acpi                830    833  0
     d l_chgv                844    844
     d l_prog                871    880
     d l_outm                899    899
     d l_wsid                901    910
     d l_user                911    920
     d l_filg                931    933
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d w_firm          s                   like(fofirm)
     d rkunl1_kund     s                   like(rkkund)
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d fodtl1_numm     s                   like(fdnumm)
     d fodtl1_suff     s                   like(fdsuff)
     d fodtl1_line     s                   like(fdline)
     d fodtlr_numm     s                   like(fdnumm)
     d fodtlr_suff     s                   like(fdsuff)
     d fodtlr_line     s                   like(fdline)
8.01 d fodtlu_numm     s                   like(fdnumm)
8.01 d fodtlu_suff     s                   like(fdsuff)
8.01 d fodtlu_line     s                   like(fdline)
     d votyl1_otyp     s                   like(vaotyp)
     d vinfl1_ityp     s                   like(vaityp)
     d fusrl1_user     s                   like(fbuser)
     d fplllu_lonr     s                   like(fklonr)
     d fplllu_lsuf     s                   like(fklsuf)
     d fplllu_llin     s                   like(fkllin)
8.01 d fplll1_lonr     s                   like(fklonr)
8.01 d fplll1_lsuf     s                   like(fklsuf)
8.01 d fplll1_llin     s                   like(fkllin)
     d fplolu_ponr     s                   like(fkponr)
     d fplolu_psuf     s                   like(fkpsuf)
     d fpsolu_slin     s                   like(fkslin)
     d fpsolu_sonr     s                   like(fksonr)
     d fpsolu_ssuf     s                   like(fkssuf)
     d fpsolu_stll     s                   like(fkstll)
     d rohel1_numm     s                   like(fonumm)
     d rohel1_suff     s                   like(fosuff)
     d fotxl1_numm     s                   like(ftnumm)
     d fotxl1_suff     s                   like(ftsuff)
     d fotxl1_line     s                   like(ftline)
     d fntrl1_numm     s                   like(fnnumm)
     d fntrl1_suff     s                   like(fnsuff)
     d fmkal1_kund     s                   like(fmkund)
     d fmkal1_kpro     s                   like(fmkpro)
     d fmkal1_kftp     s                   like(fmkftp)
     d fmkal1_ktsq     s                   like(fmktsq)
     d lwpllu_numm     s                   like(mknumm)
     d lwpllu_suff     s                   like(mksuff)
     d sdepl1_numm     s                   like(sknumm)
     d sdepl1_kund     s                   like(skkund)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
     d amodl1_modu     s                   like(axmodu)
     d afpslr_ufil     s                   like(l_filg)
     d afpslr_uide     s                   like(afuide)
     d rukpl6_kund     s                   like(rukund)
8.13 d ftrpi2_rnum     s                   like(ftrnum)
8.13 d ftrpi2_rsuf     s                   like(ftrsuf)
8.13 d ftrpiu_roid     s                   like(ftroid)
8.14 d lodtlu_numm     s                   like(ldnumm)
8.14 d lodtlu_suff     s                   like(ldsuff)
8.14 d lodtlu_line     s                   like(ldline)
      *
      * Definering av variable
      *
     d w_kode          s              1
     d w_fell          s              6
     d w_type          s              2
     d w_fast          s             10
     d w_numm          s                   like(fonumm)
8.12 d w_suff          s                   like(fosuff)
      *
     d w_lrec          s            128
     d w_tonr          s                   like(fkponr)
     d w_tsuf          s                   like(fkpsuf)
     d w_otyp          s                   like(footyp)
     d w_otyp_sav      s                   like(footyp)
     d w_lage          s                   like(folage)
     d w_lage_sav      s                   like(folage)
     d w_date          s                   like(foldat)
     d w_oakk          s                   like(vaoakk)
     d w_rest          s                   like(fdanta)
     d w_test          s              1  0
     d w_enor          s              1
     d w_in03          s              1
     d w_tvbt          s              1
     d w_btch          s              1
     d w_ruti          s             10
     d w_sald          s             11  2
     d w_sal2          s             11  2
     d w_stat          s              1
     d w_fkda          s              6  0
     d w_ffda          s              6  0
     d w_olag          s                   like(foolag)
     d w_onum          s                   like(vaonum)
     d w_spgm          s             10
     d w_aarr          s              4  0
     d w_kod2          s              1
     d w_sva2          s              1
     d w_lnum          s                   like(fonumm)
     d w_lsuf          s                   like(fosuff)
     d w_numa          s              8
     d w_lina          s              4
     d w_btms          s               Z
     d w_ti26          s             26
     d w_bnum          s              6
     d w_samf          s                   like(rksamf)
     d w_disp          s              1
     d w_tilg          s              1
     d w_modu          s             10
8.01 d w_anta          s                   like(fdanta)
8.05 d w_reqs          s              5
8.06 d w_skaf          s              1  0
8.09 d w_logg          s              1  0
      *
     d b_dell          s              1    inz(*off)
     d b_kjko          s              1    inz(*off)
     d b_bdo1          s              1    inz(*off)
     d b_bdo2          s              1    inz(*off)
     d b_bdo3          s              1    inz(*off)
     d b_bdo4          s              1    inz(*off)
     d b_kper          s              1    inz(*off)
8.01 d b_rest          s              1    inz(*off)
8.01 d b_ordr          s              1    inz(*off)
8.10 d b_rtry          s              1    inz(*on)
8.15 d b_bygd          s              1    inz(*off)
      *
     d p_urec          s             80
     d p_stat          s              1
      *
      * Definering av konstanter
      *
     d lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
      *
     d u_s701          s              1    inz(*off)                            Klient
8.05 d u_flat          s              1    inz(*off)                            Klient
      *
      * Definering av eksterne prg
      *
       dcl-s co402_verdi1  char(1);
       dcl-s co402_verdi2  char(2048);
       DCL-PR CO402R EXTPGM('CO402R');
         p_filgrp            char(3)     const;
         p_firm              packed(3:0) const;
         p_lager             packed(2:0) const;
         p_nokkel            char(50)    const;
         p_verdi1            char(1);
         p_verdi2            char(2048);
       END-PR;
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Beskrivelse av spesielle felter som er benyttet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  w_test  Benyttes til og sjekkke om det er linjer igjen
      *  (0,1)   på ordren etter at den er restet. Hvis det ikke
      *          er varelinjer igjen på ordren slettes heading-record
      *
      *
      *  Hent opplysninger fra OFAK-KODE-REGISTER
      *
     c                   eval      w_firm = fk2fir
     c     fstsl1_key    chain     fstsl1r                            90
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Utplukk av ordre til utskrift og oppdatering
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Legger inn verdier fra PLUKK-2-LINJE-REGISTER (Parametre)
      * Dersom ett ordrenummer er valgt vil fra/til være like
      *
     c                   eval      w_firm      = fk2fir
     c                   eval      fplolu_ponr = fk2onr
     c                   eval      fplolu_psuf = fk2suf
     c                   eval      w_tonr      = fk2onr
     c                   eval      w_tsuf      = fk2suf
      *
      * Legger inn 99999999/99 som sluttverdi
      * dersom sluttverdi ikke er utfylt
      *
     c                   if        w_tonr = *zero
     c                   eval      w_tonr = 99999999
     c                   eval      w_tsuf = 99
     c                   endif
      *
      * Setter start for lesing i UTPLUKK-PARAM-REGISTER
      *
     c     fplol1_key    setll     fplol1r
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av utplukk
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      * Leser record fra UTPLUKK-PARAM-REGISTER
      *
     c     xstart        tag
     c                   read      fplol1r                                90
     c     *in90         cabeq     *on           xslutt
      *
      * Avslutter dersom ordrenummer lest
      * er større en testverdi
      *
     c     fkponr        cabgt     w_tonr        xslutt
      *
      * Avslutter dersom ordrenummer
      * og suffix er for stort
      *
     c                   if        fkponr = w_tonr
     c     fkpsuf        cabgt     w_tsuf        xslutt
     c                   endif
      *
      * Sjekk, Dersom uaktuelt firma
      *
     c                   if        fkpfir <> l_firm
     c                   goto      xstart
     c                   endif
      *
      * Sjekk, ordretype plukket er lik
      * ordretype fra parameter
      *
     c     fkpoty        cabne     fk2oty        xstart
      *
      * Sjekk, Dersom ordre bare fra denne skjerm
      *
     c                   if        fk2vg1 = 1
     c     fkpwsd        cabne     fk2wsd        xstart
     c                   endif
      *
      * Sjekk, Dersom ordre bare for denne bruker
      *
8.02 c**                 if        fk2vg2 = 1
8.02 c**   fkpusr        cabne     fk2usr        xstart
8.02 c**                 endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Ett ordrenummer er funnet som skal plukkes ut
      * Dette ordrenummer skal behandles helt ferdig før neste
      * ordrenummer hentes fra UTPLUKK-PARAM-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Legger inn firmanummer i nøkklene
      *
     c                   eval      w_firm = fkpfir
      *
      * Legger inn ordrenummer og suffix i nøkklene
      *
     c                   eval      rohel1_numm = fkponr
     c                   eval      fohel1_numm = fkponr
     c                   eval      fohel1_suff = fkpsuf
      *
     c                   eval      fodtl1_numm = fkponr
     c                   eval      fodtl1_suff = fkpsuf
     c                   eval      fodtl1_line = *zero
      *
8.01 c                   eval      fodtlu_numm = fkponr
8.01 c                   eval      fodtlu_suff = fkpsuf
8.01 c                   eval      fodtlu_line = *zero
      *
     c                   eval      fotxl1_numm = fkponr
     c                   eval      fotxl1_suff = fkpsuf
     c                   eval      fotxl1_line = *zero
      *
     c                   eval      fplolu_ponr = fkponr
     c                   eval      fplolu_psuf = fkpsuf
      *
     c                   eval      fplllu_lonr = fkponr
     c                   eval      fplllu_lsuf = fkpsuf
     c                   eval      fplllu_llin = *zero
      *
8.01 c                   eval      fplll1_lonr = fkponr
8.01 c                   eval      fplll1_lsuf = fkpsuf
8.01 c                   eval      fplll1_llin = *zero
      *
     c                   eval      fpsolu_sonr = fkponr
     c                   eval      fpsolu_ssuf = fkpsuf
      *
      * Legger inn ordretype og alternativ i PLUKK-S-LINJE-felter
      *
     c                   eval      fksoty = fkpoty
     c                   eval      fksalt = fkpalt
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Bygger opp standardverdier for denne utskriften
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      * Hent opplysninger fra ORDRETYPE-REGISTER
      *
     c                   eval      votyl1_otyp = fk2oty
     c     votyl1_key    chain     votyl1r                            90
     c                   eval      w_ruti = vaorut
     c                   eval      w_oakk = vaoakk
      *
      * Legg inn verdier for henting av utskrifts-opplysninger
      *
     c                   eval      l_chgv = *blank
     c                   eval      l_ruti = w_ruti
      *
      * Er det valgt alternativt tilbud, overstyr rutine
      *
     c                   if        vaoakk = 0
      *
     c                   if        fkpalt = 1
     c                   eval      l_ruti = %trim(w_ruti) + '1'
     c                   endif
      *
     c                   if        fkpalt = 2
     c                   eval      l_ruti = %trim(w_ruti) + '2'
     c                   endif
      *
     c                   if        fkpalt = 3
     c                   eval      l_ruti = %trim(w_ruti) + '3'
     c                   endif
      *
     c                   if        fkpalt = 4
     c                   eval      l_ruti = %trim(w_ruti) + '4'
     c                   endif
      *
     c                   endif
      *
      * Er det valgt plukkliste fra ordre, overstyr rutine
      *
     c                   if        vaoakk = 1
     c                   if        fkpalt = 1
     c                   eval      l_ruti = %trim(w_ruti) + '1'
     c                   endif
     c                   endif
      *
      * Er det valgt plukkliste fra ordre, overstyr rutine
      *
     c                   if        vaoakk = 1
     c                   if        fkpalt = 1
     c                   eval      l_ruti = %trim(w_ruti) + '1'
     c                   endif
     c                   endif
      *
      *
      * Er det valgt mail, overstyr rutine  (Tilbud)
      *
     c                   if        vaoakk = 0
     c                   if        fk2utq = 'E_MAIL_ASP'
      *
     c                   if        fkpalt = 0
     c                   eval      l_ruti = %trim(w_ruti) + '5'
     c                   endif
      *
     c                   if        fkpalt = 1
     c                   eval      l_ruti = %trim(w_ruti) + '6'
     c                   endif
      *
     c                   if        fkpalt = 2
     c                   eval      l_ruti = %trim(w_ruti) + '7'
     c                   endif
      *
     c                   if        fkpalt = 3
     c                   eval      l_ruti = %trim(w_ruti) + '8'
     c                   endif
      *
     c                   if        fkpalt = 4
     c                   eval      l_ruti = %trim(w_ruti) + '9'
     c                   endif
      *
     c                   endif
     c                   endif
      *
      * Er det valgt mail, overstyr rutine  (Ordrebekr/plukkliste)
      *
     c                   if        vaoakk = 1
     c                   if        fk2utq = 'E_MAIL_ASP'
      *
     c                   if        fkpalt = 0
     c                   eval      l_ruti = %trim(w_ruti) + '5'
     c                   endif
      *
     c                   if        fkpalt = 1
     c                   eval      l_ruti = %trim(w_ruti) + '6'
     c                   endif
      *
     c                   endif
     c                   endif
      *
      * Dersom OUTQ er overstyrt manuelt,
      * sett kode som varsler dette i AP600r
      *
     c                   if        fk2utq <> *blank
     c                   eval      l_outq = fk2utq
     c                   eval      l_outm = '1'
     c                   endif
      *
      * Oppdaterer LDA
      *
     c                   out       *dtaara
      *
      * Kaller opp program før utskrift
      *
     c                   call      'AP800R'
     c                   parm                    l_ruti
     c                   parm                    l_chgv
     c                   parm                    w_tvbt
     c                   parm                    w_in03
     c                   parm                    w_btch
      *
      * Henter inn igjen LDA
      *
     c                   in        *dtaara
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av de enkelte utskrifter/formularer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Legger inn verdi for at ordre/ordrelinjer er valgt
      *
     c                   eval      l_alt1 = 1
      *
      * Faktura/Kreditnota :
      *
     c                   if        vaoakk = 3
     c                   eval      l_alt1 = 2
     c                   move      fk2per        l_peri
     c                   move      fk2dat        l_dato
     c                   endif
      *
      * Tilbakestill kode for manuelt overstyrt OUTQ
      *
     c                   eval      l_outm = ' '
      *
      * Oppdaterer LDA
      *
     c                   out       *dtaara
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Arbeider med opplysninger i tilknyttning til denne ordren
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      * Henter inn opplysninger fra ORDRE-HODE-REGISTER
      *
     c     fohel1_key    chain     fohel1r                            90
     c                   eval      w_otyp_sav = footyp
     c                   eval      w_lage_sav = folage
     c                   eval      w_olag     = foolag
      *
 .    * Sjekker om kundeprosjekt skal legge ut ByggDok
      *
     c                   eval      b_bdo2 = *off
     c                   eval      b_bdo4 = *off
     c                   if        b_bdo1 = *on
     c                   eval      fkprl1_kund = fokund
     c                   eval      fkprl1_kpro = fokpro
     c     fkprl1_key    chain     fkprl1r
     c                   if        %found(fkprl1)
     c                   if        flbdok = 1 or
     c                             flcobu = 1
     c                   time                    w_btms
     c                   eval      b_bdo2 = *on
     c                   endif
     c                   endif
     c                   endif
     c                   eval      b_bdo3 = *off
     c                   if        vaoakk = 2 and
     c                             vaokre = *blank
     c                   eval      b_bdo3 = *on
     c                   endif
      *
      * Hopper forbi denne ordre dersom OBEK ikke skal skrives ut
      *
     c                   if        vaoakk = 1
     c     foksob        cabeq     1             tag300
     c                   endif
      *
      * Hopper forbi denne faktura dersom fakturadato på ordre
      * er større enn periodens fakturadato (oppgitt i bildet)
      *
     c                   if        vaoakk = 3
     c                   if        fofkda <> *loval
     c                   if        fofkda > l_dato
     c                   goto      xstart
     c                   endif
     c                   endif
     c                   endif
      *
      * Hopper forbi denne ordren dersom det ikke finnes noen
      * varelinjer på den.
      *
     c                   if        vaoakk = 3
     c                   eval      fodtlr_numm = fonumm
     c                   eval      fodtlr_suff = fosuff
     c                   eval      fodtlr_line = 0
     c     fodtlr_key    setll     fodtlrr
     c                   read      fodtlrr                                90
     c                   if        %eof
     c                   goto      xstart
     c                   endif
     c                   if        fonumm = fdnumm
     c                   if        fosuff = fdsuff
     c                   else
     c                   goto      xstart
     c                   endif
     c                   endif
     c                   endif
      *
      * Hent inn ordre-info-kode i inforegister
      *
     c                   eval      vinfl1_ityp = foityp
     c     vinfl1_key    chain     vinfl1r                            98
     c                   if        *in98  = *on
     c                   eval      vaifak = *blank
     c                   endif
      *
      * Dersom ordre har en infokode, og dette er fakturering,
      * sjekkes det om fakturering skal stoppes.
      *
     c                   if        foityp <> *blank
     c                   if        vaoakk = 3
     c                   if        vaifak = '1'
     c                   goto      xstart
     c                   endif
     c                   endif
     c                   endif
      *
      *  Kontroll om bruker har lov til å sette igang fakturering.
      *
     c                   if        fbko08 <> 1
     c                   if        vaoakk =  3
     c                   goto      xstart
     c                   endif
     c                   endif
      *
      *  Kontroll på om pakkseddel allerede er skrevet
      *  + sjekk mot bruker om gjenutskrift er tillatt
      *
     c                   if        fbko09 <> 1
     c                   if        fopaks =  1
     c                   if        vaoakk =  2
     c                   goto      xstart
     c                   endif
     c                   endif
     c                   endif
      *
      * Legger inn sorteringsbegrep fra ordre-register
      *
     c                   eval      sorter = *blank
     c                   eval      sor001 = *zero
     c                   eval      sor006 = *zero
     c                   eval      sor007 = *zero
      *
     c                   move      fokund        sor001
     c                   move      fkponr        sor006
     c                   move      fkpsuf        sor007
     c                   eval      sor002 = fonavn + foadr1
      *
      * Oppsamling av flere ordre på samme blankett
      *
     c                   if        vaosam = 1
      *
     c                   eval      sor006 = *zero
     c                   eval      sor007 = *zero
      *
      * Overstyring av samlefaktura
      *
     c                   if        vaoakk = 3
      *
      * Hent opplysninger fra KUNDE-REGISTER
      *
     c                   eval      rkunl1_kund = fokund
     c     rkunl1_key    chain     rkunl1                             90
      *
     c                   eval      w_samf = rksamf
      *
     c                   eval      fkprl1_kund = fokund
     c                   eval      fkprl1_kpro = fokpro
     c     fkprl1_key    chain     fkprl1r
     c                   if        %found(fkprl1)
     c                   eval      w_samf = flsamf
     c                   endif

      *
      * Dersom dette er en ordre som har depositum,
      * skal det bare være en faktura pr. ordre
      *
     c                   eval      sdepl1_numm = fonumm
     c                   eval      sdepl1_kund = fokund
     c     sdepl1_key    chain     sdepl1r
     c                   if        %found
     c                   eval      w_samf = 1
     c                   endif
      *
      * En faktura pr. ordre
      *
     c                   if        w_samf = 1
     c                   move      fkponr        sor006
     c                   move      fkpsuf        sor007
     c                   endif
      *
      * Flere ordre pr. faktura dersom
      * prosjektnummer er det samme
      *
     c                   if        w_samf = 2
     c                   move      *blank        sor002
     c                   movel     fokpro        sor002
     c                   endif
      *
      * Flere ordre pr. faktura dersom
      * prosjektnummer er det samme, men ...
      * egen faktura dersom oppgitt leveringsadresse.
      *
     c                   if        w_samf = 3
     c                   move      *blank        sor002
     c                   movel     fokpro        sor002
     c                   if        fokpro = 0
     c                   eval      sor002 = fonavn + foadr1
     c                   endif
     c                   endif
      *
      * Flere ordre pr. faktura uansett om
      * leveringsadresse/prosjekt er forskjellig
      *
     c                   if        w_samf = 4
     c                   eval      sor002 = *blank
     c                   endif
      *
     c                   endif
     c                   endif
      *
      *  Kontroll om korttrans. - kortnr. inn i sorteringsbegrep
      *
     c                   eval      fntrl1_numm = fonumm
     c                   eval      fntrl1_suff = fosuff
     c     fntrl1_key    chain     fntrl1r
     c                   if        %found
     c                   eval      fmkal1_kund = fnkund
     c                   eval      fmkal1_kpro = fnkpro
     c                   eval      fmkal1_kftp = fnkftp
     c                   eval      fmkal1_ktsq = fnktsq
     c     fmkal1_key    chain     fmkal1r
     c                   if        %found
     c                   move      fmkrnr        sor003
     c                   endif
     c                   endif
      *
      * Legger inn forskjellig bet.bet på forskjellig faktura
      *
     c                   move      fobetb        sor004
      *
      * Dersom fakturadato eller forfallsdato er overstyrt,
      * skal ikke ordre slås sammen på en faktura,
      * men komme på hver sin.
      * (Samme plassering benyttes for fakturadato og
      *  forfallsdato, da jeg anser at det sjelden vil
      *  bli noe sammenfall her.)
      * Fakturadato legges ut først (dersom oppgitt),
      * og forfallsdato legges over (dersom oppgitt)
      * Dette for å få brudd på sorteringsbegrepet.
      *
     c                   eval      w_fkda = 0
     c                   eval      w_ffda = 0
      *
     c                   if        fofkda <> *loval
     c     *ymd          move      fofkda        w_fkda
     c                   endif
      *
     c                   if        foffda <> *loval
     c     *ymd          move      foffda        w_ffda
     c                   endif
      *
     c                   if        w_fkda <> 0
     c                   movel     w_fkda        sor005
     c                   endif
      *
     c                   if        w_ffda <> 0
     c                   movel     w_ffda        sor005
     c                   endif
      *
      *
      * Legger over hele sorteringsbegrepet til register-feltet
      *
     c                   movel     sorter        fkssrt
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Arbeider med henting av heading/linjer/tekster og resting
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Hent opplysninger fra ORDRETYPE-REGISTER
      *
     c                   eval      votyl1_otyp = footyp
     c     votyl1_key    chain     votyl1r                            90
      *
      * Hopp til plukk linjer dersom dette bare er utskrift
      *
     c     footyp        cabeq     fk2oty        tag200
      *
      * Legger inn stanard suffix dersom ikke resting av linjer
      *
     c                   eval      rohel1_suff = fohel1_suff
      *
      * Nullstiller hjelpevariable for test på om restordre benyttes
      *
     c                   eval      w_test = *zero
8.01  *
8.01  * Sjekk om alle linjer er med i plukkeregistrene. Hvis nei, sett restkode.
8.01  *
8.01 c                   eval      b_rest = *off
8.01 c                   eval      fodtlr_numm = fonumm
8.01 c                   eval      fodtlr_suff = fosuff
8.01 c                   eval      fodtlr_line = 0
8.01 c     fodtlr_key    setll     fodtlr
8.01 c     fodtlr_key2   reade     fodtlr
8.01 c                   dow       not %eof
8.01 c                   eval      fplll1_llin = fdline
8.01 c     fplll1_key    chain     fplll1
8.01 c                   if        not %found
8.01 c                   eval      b_rest = *on
8.01 c                   endif
8.04 c                   if        %found     and
8.04 c                             fkpres = 1 and
8.04 c                             fdvare <> *blanks and
8.04 c                             fdanta > fklant
8.04 c                   eval      b_rest = *on
8.04 c                   endif
8.01 c     fodtlr_key2   reade     fodtlr
8.01 c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av resting    Danner nye linjer og heading
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Sjekk, Resting av ordre, utfør dette først
      *
8.01 c**                 if        fkpres <> *zero
8.01 c                   if        b_rest = *on
      *
      * Kaller opp subrutine for henting av neste suffix
      *
     c                   exsr      hntsuf
      *
      * Kaller opp subrutiner for behandling av resting
      *
     c                   exsr      olrest
      *
      * Danner ny ordreheading/sletter eventuellt gammel
      *
     c     fohel1_key    chain     fohel1r                            90
      *
      * Det finnes ikke linjer som er restet igjen så slett ordre
      *
8.16 c                   if        w_test = *zero and
8.16 c                             b_rest <> '1'
     c     fohel1_key    chain     fohelur
     c                   if        %found(fohelu)
     c                   delete    fohelur
     c                   endif
     c                   endif
      *
      * Danner ny ordre med ny suffix
      *
     c                   call      'FO709R'
     c                   parm                    rohel1_numm
     c                   parm                    rohel1_suff
      *
     c     rohel1_key    chain     fohelur                            90
     c                   if        *in90 = *on
     c                   eval      fonumm = rohel1_numm
     c                   eval      fosuff = rohel1_suff
     c                   eval      fokode = 3
     c                   eval      forest = 0
8.08 c                   eval      fonref = *blanks
8.08 c                   eval      fofsys = *blanks
     c                   eval      fooous = l_user
8.13 c                   eval      footyp = fkpoty
     c                   time                    foedat
     c                   time                    foetim
     c                   eval      foeusr = l_user
     c                   write     fohelur
     c                   endif
8.13  *
8.13  * Hvis flåtestyring oppdater transportfila med nytt suffiks.
8.13  *
8.13 c                   if        u_flat = *on
8.13  *
8.13 c                   eval      ftrpi2_rnum = fohel1_numm
8.13 c                   eval      ftrpi2_rsuf = fohel1_suff
8.13 c     ftrpi2_key    chain     ftrpi2
8.13 c                   if        %found
8.13 c                   eval      ftrpiu_roid = ftroid
8.13 c     ftrpiu_key    chain     ftrpiu
8.13 c                   if        %found
8.13 c                   eval      ftrsuf = rohel1_suff
8.13 c                   update    ftrpiur
8.13 c                   endif
8.13 c                   endif
8.13  *
8.13 c                   endif
8.05  *
8.05  * Hvis flåtestyring send, request for nytt suffiks
8.05  * Alle sjekker på om ordren tilfredsstiller krav, ligger i AP058.
8.11  * Sjekk ikke her, da ordren ikke har riktig ordretype her.
8.05  *
8.11 c*                  if        u_flat = *on
8.05  *
8.11 c*                  eval      b_rtry = *off
8.11 c*                  eval      w_reqs = *blanks
8.11 c*                  eval      w_skaf = 1
8.11 c*                  eval      w_logg = 1
8.11 c*                  call      'AP058R '
8.11 c*                  parm                    w_firm
8.11 c*                  parm                    rohel1_numm
8.11 c*                  parm                    rohel1_suff
8.11 c*                  parm                    w_skaf
8.11 c*                  parm                    w_logg
8.11 c*                  parm                    w_reqs
8.15  *
8.11 c*                  endif
      *
     c                   if        w_oakk = 1
     c                   eval      w_lnum = fohel1_numm
     c                   eval      w_lsuf = fohel1_suff
     c                   exsr      sjekk_lkode
     c                   if        b_kjko = *on
      * oppdat. status hvis plukkstyring er valg
     c                   eval      d_ufir = fofirm
     c                   eval      d_unum = fonumm
     c                   eval      d_usuf = fosuff
     c                   eval      d_ukod = '2'
     c                   exsr      upd_logg
     c                   endif
      *
     c                   endif
      *
     c                   call      'FO190R'
     c                   parm                    w_stat
     c                   parm                    w_firm
     c                   parm                    rohel1_numm
     c                   parm                    rohel1_suff
     c                   parm                    fokund
     c                   parm                    fokpro
      *
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * SPESIAL FOR LAGER :  behandling av ordre som ikke restes
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   if        faalag <> *zero
8.03 c**                 if        fkpres = *zero
     c                   if        w_olag = *zero
     c                   exsr      laglag
     c                   endif
8.03 c**                 endif
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Etterbehandling av "gammel" ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Hopper til etterbehandling ny ordre dersom ikke
      * Heading finnes på forrige /fra) ordre
      *
      * Trekker ut hele beløpet fra fra ordre derosm ikke restlinjer
      *
     c                   if        w_test = *zero
      *
      * Beregner justeringsbeløp
      *
     c                   eval      w_sald = fototr - fotots
     c                   eval      w_sal2 = fobrra * -1
      *
      * Kaller opp subrutine for oppdatering av memosaldo
      *
     c                   call      'FA922R'
     c                   parm                    w_firm
     c                   parm                    footyp
     c                   parm                    fokund
     c                   parm                    w_sald
     c                   parm                    w_sal2
      *
     c                   goto      tag100
     c                   endif
      *
      * Beregner nye summer for "GAMMEL" ordre
      *
     c                   eval      w_enor = *blank
     c                   call      'FO730R'
     c                   parm                    w_enor
     c                   parm                    w_firm
     c                   parm                    fohel1_numm
     c                   parm                    fohel1_suff
      *
      * Oppdaterer ordren slik at den kan endres etc.etc.
      *
     c     fohel1_key    chain     fohelur                            90
      *
      * Legger inn kode for at beholdning er oppdatert
      *
     c                   if        w_oakk = 2 or
     c                             w_oakk = 3
     c                   if        w_test = 0
     c                   eval      foolag = 1
     c                   endif
     c                   endif
      *
      * Legger inn kode for restnotering
      *
     c                   if        w_oakk = 2
     c                   eval      forest = 1
     c                   eval      d_ufir = w_firm
     c                   eval      d_unum = fohel1_numm
     c                   eval      d_usuf = fohel1_suff
     c                   eval      d_ukod = '8'
     c                   exsr      upd_logg
     c                   endif
     c                   if        w_oakk = 1
     c                   eval      d_ufir = w_firm
     c                   eval      d_unum = fohel1_numm
     c                   eval      d_usuf = fohel1_suff
     c                   eval      d_ukod = '9'
     c                   exsr      upd_logg
     c                   eval      b_dell = *on
     c                   endif
      *
     c                   if        *in90 = *off
     c                   if        b_dell = *on
      * det er suff. som skal ha lev.dato og info. suff lik 00 har hatt
      * info midlertidig.
     c                   eval      fopada = *loval
     c                   eval      foinf1 = *blank
     c                   eval      foinf2 = *blank
     c                   endif
     c                   eval      fokode = *zero
     c                   move      *loval        fokoda
     c                   move      *loval        fokoti
     c                   eval      fokous = *blank
     c                   eval      fokows = *blank
     c                   time                    foedat
     c                   time                    foetim
     c                   eval      foeusr = l_user
     c                   update    fohelur
     c                   endif
      *
      * Sletter evt. arbeidsregister for plukking for denne ordre/suff
      *
     c                   eval      lwpllu_numm = fohel1_numm
     c                   eval      lwpllu_suff = fohel1_suff
     c                   exsr      slett_plukk
8.07  *
8.07  * Hvis flåtestyring send, oppdater request for gammelt suffiks
8.07  * Alle sjekker på om ordren tilfredsstiller krav, ligger i AP058.
8.07  *
8.07 c                   if        u_flat = *on
8.13  * Logg linje med kode 6.
8.13 c                   if        w_oakk = 2
8.13 c                   eval      d_ufir = fofirm
8.13 c                   eval      d_unum = rohel1_numm
8.13 c                   eval      d_usuf = rohel1_suff
8.13 c                   eval      d_ukod = '6'
8.13 c                   exsr      upd_logg
8.13 c                   endif
8.07  *
8.10 c                   eval      b_rtry = *off
8.07 c                   eval      w_reqs = *blanks
8.07 c                   eval      w_skaf = 1
8.09 c                   eval      w_logg = 1
8.07 c                   call      'AP058R '
8.07 c                   parm                    w_firm
8.13 c*                  parm                    fohel1_numm
8.13 c                   parm                    rohel1_numm
8.13 c*                  parm                    fohel1_suff
8.13 c                   parm                    rohel1_suff
8.07 c                   parm                    w_skaf
8.09 c                   parm                    w_logg
8.07 c                   parm                    w_reqs
8.07  *
8.07 c                   endif
      *
      * Tilbakestill kode for restnotering
      *
     c                   eval      forest = 0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Etterbehandling av "ny" ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     tag100        tag
      *
      * Legger inn rett ordrenummer etter plukking
      *
     c                   eval      fkponr      = rohel1_numm
     c                   eval      fohel1_numm = rohel1_numm
     c                   eval      fodtl1_numm = rohel1_numm
     c                   eval      fotxl1_numm = rohel1_numm
     c                   eval      fpsolu_sonr = rohel1_numm
      *
      * Legger inn rett suffix etter plukking
      *
     c                   eval      fkpsuf      = rohel1_suff
     c                   eval      fohel1_suff = rohel1_suff
     c                   eval      fodtl1_suff = rohel1_suff
     c                   eval      fotxl1_suff = rohel1_suff
     c                   eval      fpsolu_ssuf = rohel1_suff
      *
      * Oppdaterer ordren med nye verdier fra utplukk
      *
     c     fohel1_key    chain     fohelur                            90
     c                   if        *in90 = *off
     c                   eval      footyp = fkpoty
     c                   eval      folage = fkplag
     c                   eval      fotots = *zero
     c                   eval      fototr = *zero
     c                   eval      fobrra = *zero
     c                   eval      forest = *zero
     c                   if        w_oakk = 2 or
     c                             w_oakk = 3
     c                   eval      foolag = 1
     c                   endif
      *
      * Dersom leveringsdato ikke er registert og dette er en
      * pakkseddel, legges dagens dato ut i leveringsdato.
      *
      *    w_oakk        ifeq      2
      *    foldat        ifeq      *loval
      *                  move      w_date        foldat
      *                  end
      *                  end
      *
      * Når en ordre plukkes til pakkseddel, oppdateres pakks.dato
      * med dag.dato. Leveringsdato blir liggende som før.
      * Den dato som lå i feltet fra før,
      * tas vare på i planlagt levering, slik at man i ettertid
      * kan se planlagt levering opp mot virkelig levering.
      *
     c                   if        w_oakk = 2
     c                   if        foldat = *loval
     c                   eval      foldat = w_date
     c                   endif
     c                   if        fopdat = *loval
     c                   eval      fopdat = foldat
     c                   endif
     c                   eval      fopada = w_date
     c                   endif
      *
      * Oppdatering av bruker/skjerm/dato/klokkeslett
      *
     c                   eval      fowsid = l_wsid
      *
     c                   time                    foedat
     c                   time                    foetim
     c                   eval      foeusr = l_user
     c                   update    fohelur
     c                   endif
      *
      * Beregner nye summer for "NY" ordre
      *
     c                   eval      w_enor = *blank
     c                   call      'FO730R'
     c                   parm                    w_enor
     c                   parm                    w_firm
     c                   parm                    rohel1_numm
     c                   parm                    rohel1_suff
8.10  *
8.10  * Hvis flåtestyring send, oppdater request for gammelt suffiks
8.10  * Alle sjekker på om ordren tilfredsstiller krav, ligger i AP058.
8.10  *
8.10 c                   if        u_flat = *on and
8.10 c                             b_rtry = *on
8.13  * Logg linje med kode 6.
8.13 c                   if        w_oakk = 2
8.13 c                   eval      d_ufir = fofirm
8.13 c                   eval      d_unum = rohel1_numm
8.13 c                   eval      d_usuf = rohel1_suff
8.13 c                   eval      d_ukod = '6'
8.13 c                   exsr      upd_logg
8.13 c                   endif
8.10  *
8.10 c                   eval      w_reqs = *blanks
8.10 c                   eval      w_skaf = 1
8.10 c                   eval      w_logg = 1
8.10 c                   call      'AP058R '
8.10 c                   parm                    w_firm
8.13 c*                  parm                    fohel1_numm
8.13 c                   parm                    rohel1_numm
8.13 c*                  parm                    fohel1_suff
8.13 c                   parm                    rohel1_suff
8.10 c                   parm                    w_skaf
8.10 c                   parm                    w_logg
8.10 c                   parm                    w_reqs
8.10  *
8.10 c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Etterbehandling av ordre som skal skrives ut  (henting m.m.)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter ordre/-linjer fra valgt ordre til PLUKK-S-LINJE-REGISTER
      *
     c     tag200        tag
      *
      * Oppdater kode som sier at pakkseddel er skrevet,
      * dersom dette er en pakkseddel.
      *
     c     fohel1_key    chain     fohelur                            90
     c                   if        *in90 = *off
     c                   if        w_oakk = 2
     c                   eval      fopaks = 1
     c                   endif
     c                   time                    foedat
     c                   time                    foetim
     c                   eval      foeusr = l_user
     c                   update    fohelur
     c                   endif
      *
      * Dersom det er direkte utskrift, skal ordre ikke
      * kjøres via logic
      *
     c                   if        w_oakk = 3
     c                   if        fk2vg0 = 1
     c     fohel1_key    chain     fohelur
     c                   if        %found(fohelu)
     c                   if        fofaty <> 0
     c                   eval      fofaty =  0
     c                   endif
     c                   update    fohelur
     c                   endif
     c                   endif
     c                   endif
      *
     c                   exsr      ohead
     c                   if        fkptxt = *zero
     c                   exsr      foran
     c                   endif
     c                   exsr      linje
     c                   if        fkptxt = *zero
     c                   exsr      etter
     c                   endif
      *
      * Sletter record fra UTPLUKK-PARAM-REGISTER
      *
     c     tag300        tag
      *
     c     fplol1_key    chain     fplolur                            90
     c                   if        *in90 = *off
     c                   delete    fplolur
     c                   endif
8.01  *
8.01  * Slett også evnt linjer i plukkeregisteret.
8.01  *
8.01 c                   eval      fplllu_llin = 0
8.01 c     fplllu_key    setll     fplllu
8.01 c     fplllu_key2   reade     fplllu
8.01 c                   dow       not %eof
8.01 c                   delete    fplllu
8.01 c     fplllu_key2   reade     fplllu
8.01 c                   enddo
      *
      *  Sjekker om kostprisbokføring skal gjøres
      *
     c                   eval      w_modu = 'GJKOST'
     c                   call      'AX700R'
     c                   parm                    w_modu
     c                   parm                    w_disp
     c                   parm                    w_tilg
      *
     c                   if        w_tilg = '0'
     c                   call      'VG802R'
     c                   parm                    rohel1_numm
     c                   parm                    rohel1_suff
     c                   endif
      *
     c                   goto      xstart
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xslutt        tag
      *
     clr                 return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter neste ledige suffix
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hntsuf        begsr
8.12  *
8.12  * Hent høyeste brukte suffiks på ordrenummer
8.12  *
8.12 c                   eval      w_suff = 0
8.12 c                   call      'AS110R'
8.12 c                   parm                    fonumm
8.12 c                   parm                    w_suff
      *
8.12 c*                  eval      rohel1_suff = 49
8.12 c*    rohel1_key    setll     fohel1r
      *
8.12 c*                  eval      *in90 = *off
8.12 c*                  readp     fohel1r                                90
      *
8.12 c*                  eval      rohel1_suff = fosuff + 1
8.12 c                   eval      rohel1_suff = w_suff
      *
      * Dersom dette er plukking fra et tilbud (behandlingskode = 0)
      * og til en ordretype som ikke er tilbud (behandlingskode > 0)
      * skal det skapes en helt ny ordre i den nummerserie som
      * tilhører ordretypen det plukkes til.
      * På dette tidspunkt i programmet skal behandlingskode for
      * gammel ordretype være = VAOAKK, og ny ordretype = w_oakk.
      *
     c                   if        vaoakk = 0 and w_oakk > 0
     c                   exsr      hntnum
     c                   eval      rohel1_numm = w_numm
     c                   eval      rohel1_suff = 0
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter/Oppdatering av NUMMER-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hntnum        begsr
      *
     c                   eval      w_fell = faaork
     c                   eval      w_type = fkpoty
     c                   eval      w_fast = l_wsid
     c                   eval      w_kode = '0'
      *
      * Hente inn eventuell overstyrt
      * ordretype for nummerserie
      *
     c                   call      'VA752R'
     c                   parm                    w_firm
     c                   parm                    fkpoty
     c                   parm                    w_onum
      *
     c                   if        w_onum <> *blank
     c                   eval      w_type = w_onum
     c                   endif
      *
      * Kaller opp subprogram for henting av nummer fra NUMMER-REGISTER
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
     c                   parm                    w_numm
      *
      * sjekker at ordrenr ikke har tidligere relasjoner liggende
      *
     c                   call      'FO415R '
     c                   parm                    w_firm
     c                   parm                    w_numm
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Legger ut ordre-linjer til resting
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Forklaring på hva som skjer i denne subrutine:
      * ----------------------------------------------
      *
      * I skjermbildet for utplukk finnes tre alternativer:
      *
      *    0 = Ingen spesifisering av linjer
      *    1 = Plukk bare linjer som oppgis
      *    2 = Plukk alle linjer, men unntak
      *        (Feltet for denne koden heter FKPRES)
      *
      * Kode 0 kommer ikke innom denne rutine, bare kode 1 eller 2.
      * Det settes lower limit på varelinjeregister (som inneholder varer
      * og tekstlinjer) og det leses så lenge man befinner seg på samme
      * firmanummer / ordrenummer / suffix.
      * Samtidig gjøres det oppslag mot utplukksregister for å se om det
      * skal gjøres endringer i forhold til opprinnelig varelinje.
      *
      * Disse koder er tilgjengelige:
      *    1 = Plukkes
      *    3 = Kopieres                 (Kun tekstlinjer)
      *    4 = Holdes tilbake
      *    5 = Ingen resting av antall  (Kun varelinjer)
      *        (Feltet for denne koden heter FKLKOD)
      *
      * Utplukks(linje)register inneholder derfor poster bare der hvor
      * det er registrert enten et antall eller en kode for utplukk.
      * Vær også oppmerksom på at selv om det ikke blir funnet en
      * post i utplukksregisteret, kan det legges inn verdier i feltene
      * FKLANT og FKLKOD
      * F. eks. settes FKLKOD til 1 når hele ordren skal plukkes, men
      * unntak (FKPRES=2)
      *
      * Rutinen tar seg av restnotering, oppdatering av lager, kopiering/
      * plukking av tekslinjer etc.  En ordre som kommer innom her vil
      * alltid få et nytt suffix, mens resten blir liggende igjen på
      * gammel ordre eller slettes dersom den plukkes tom.
      *
     c     olrest        begsr
      *
     c                   eval      fodtl1_line = *zero
     c     fodtl1_key    setll     fodtl1r
      *
      * Leser inn en record fra ORDRE-LINJE-REGISTER
      *
     c     olres1        tag
     c                   eval      *in90 = *off
     c                   read      fodtl1r                                90
     c                   if        *in90 = *off
     c                   if        fdfirm = w_firm
     c                   if        fdnumm = fodtl1_numm
     c                   if        fdsuff = fodtl1_suff
      *
      * Hent opplysninger fra UTPLUKK-LINJE-REGISTER (rest/Tekst-linje)
      *
     c                   eval      fplllu_llin = fdline
     c     fplllu_key    chain     fplllur                            92
     c                   if        *in92 = *off
     c                   delete    fplllur
     c                   endif
      *
      * Dersom alle linjer skal plukkes, men unntak kan gjøres
      * Legges hele antallet inn som antall plukket
      * dersom linjen ikke finnes i ORDRE-PLUKK-LINJE-REGISTER
      *
     c                   if        *in92 = *on
     c                   eval      fklant = *zero
     c                   eval      fklkod = *blank
      *
      * Plukke alt, men unntak
      *
     c                   if        fkpres = 2
     c                   eval      fklant = fdanta
     c                   eval      fklkod = '1'
     c                   endif
      *
     c                   endif
      *
      * Dersom det er oppgitt kode 1 for utplukk, og antall
      * er lik 0 i plukkfilen, legges hele antallet over.
      *
     c                   if        *in92 = *off
     c                   if        fklkod = '1'
     c                   if        fklant = *zero
     c                   eval      fklant = fdanta
     c                   endif
     c                   endif
     c                   endif
      *
      * Dersom tekstlinjer, gå til eget avsnitt
      *
     c     fdvare        cabeq     *blank        xtxlin
      *
      * Dersom antall levert=0 skal ikke ORDRE-LINJE-REGISTER oppdat.
      *
     c                   if        fklkod = *blank
     c                   if        fklant = *zero
     c                   eval      w_test = 1
     c                   goto      olres1
     c                   endif
     c                   endif
      *
      * Dersom kode 4 er oppgitt, skal varelinje vente,
      * og ordrelinjeregister skal ikke oppdateres.
      *
     c                   if        fklkod = '4'
     c                   eval      w_test = 1
     c                   goto      olres1
     c                   endif
      *
      * Danner ny linjer for RESTORDRE
      * Legger ut nye linjer for "NY" ordre  ): NY SUFFIX
      *
     c                   eval      fodtl1_numm = fdnumm
     c                   eval      fodtl1_line = fdline
     c     rodtlu_key    chain     fodtlur                            94
     c                   if        *in94 = *on
     c                   eval      fdnumm = rohel1_numm
     c                   eval      fdsuff = rohel1_suff
     c                   eval      fdanta = fklant
     c                   time                    fdodat
     c                   time                    fdotim
     c                   eval      fdoous = l_user
     c                   time                    fdedat
     c                   time                    fdetim
     c                   eval      fdeusr = l_user
     c                   write     fodtlur
8.14  *
8.14  * Oppdater tilhørende bestillinhslinje
8.14  *
8.14 c                   if        fdbenr <> 0
8.14 c                   eval      lodtlu_numm = fdbenr
8.14 c                   eval      lodtlu_suff = fdbsuf
8.14 c                   eval      lodtlu_line = fdblin
8.14 c                   eval      lodtlu_line = fdblin
8.14 c     lodtlu_key    chain     lodtlu
8.14 c                   if        %found
8.14 c                   eval      ldorsu = fdsuff
8.14 c                   time                    ldedat
8.14 c                   time                    ldetim
8.14 c                   eval      ldeusr = l_user
8.14 c                   update    lodtlur
8.14 c                   endif
8.14 c                   endif
      *
     c                   if        footyp = 'HO'
     c                             and (%subst(fdtek1:1:9) = 'Sluttfakt'
     c                              or %subst(fdtek1:1:9) = 'SLUTTFAKT'
     c                              or %subst(fdtek1:1:9) = 'SluttFakt')
     c                   call      'FO615R'
     c                   parm                    fdnumm
     c                   parm                    fdsuff
     c                   parm                    fdline
     c                   parm                    foproj
     c                   parm                    foupro
     c                   parm                    foakti
     c                   endif
      *
      * Kaller opp subrutine for oppdatering av lager
      *
     c                   if        faalag <> *zero
     c                   if        w_olag =  *zero
     c                   eval      w_lage = fkplag
     c                   eval      w_otyp = fkpoty
     c                   exsr      opplag
     c                   endif
     c                   endif
      *
     c                   endif
      *
      * Oppdaterer linjer for "GAMMEL" ordre
      *
     c     fodtl1_key    chain     fodtlur                            94
     c                   if        *in94 = *off
      *
      * Kaller opp subrutine for oppdatering av lager
      *
     c                   if        faalag <> *zero
     c                   if        w_olag =  *zero
     c                   eval      w_lage = w_lage_sav
     c                   eval      w_otyp = w_otyp_sav
     c                   eval      fdanta = fdanta * -1
     c                   exsr      opplag
     c                   eval      fdanta = fdanta * -1
     c                   endif
     c                   endif
      *
      * Beregn rest    (REST=BESTILT-LEVERT)
      * Kode for og bevare tidligere linjeantall =0
      *
     c                   if        vaoboo = 0
     c                   eval      w_rest = fdanta - fklant
     c                   endif
      *
      * Det skal ikke restnoteres
      *
     c                   if        vaoboo = 1
     c                   eval      w_rest = 0
     c                   endif
      *
      * Resten skal ikke beholdes, den skal slettes
      *
     c                   if        fklkod = '5'
     c                   eval      w_rest = 0
     c                   endif
      *
      * Behold alltid hele det opprinnelige antallet
      *
     c                   if        vaoboo = 2
     c                   eval      w_rest = fdanta
     c                   endif
      *
      * Behandler oppdatering av ordren utfra resultatet av restingen
      *
     c                   if        w_rest > 0
     c                   eval      w_test = 1
     c                   eval      fdanta = w_rest
     c                   time                    fdedat
     c                   time                    fdetim
     c                   eval      fdeusr = l_user
     c                   update    fodtlur
     c                   else
      *
      * Logg ordrelinje før den blir slettet
      *
     c                   eval      w_spgm      = 'FO614R'
     c                   call      'FS710R'
     c                   parm                    w_firm
     c                   parm                    fdnumm
     c                   parm                    fdsuff
     c                   parm                    fdline
     c                   parm                    w_spgm
      *
     c                   eval      fdanta = 0
     c                   delete    fodtlur
     c                   endif
      *
      * Kaller opp subrutine for oppdatering av lager
      *
     c                   if        faalag <> *zero
     c                   if        w_olag =  *zero
     c                   eval      w_lage = w_lage_sav
     c                   eval      w_otyp = w_otyp_sav
     c                   exsr      opplag
     c                   endif
     c                   endif
      *
     c                   endif
      *
      * Ferdig med behandling av varelinjer
      *
     c                   goto      xneste
      *
      * -----------------------------------
      * Behandling av tekstlinjer
      *
     c     xtxlin        tag
      *
      * Gå ut med linjer som ikke skal behandles
      *
     c                   if        fklkod = *blank
     c                   eval      w_test = 1
     c                   goto      xneste
     c                   endif
      *
     c                   if        fklkod = '4'
     c                   eval      w_test = 1
     c                   goto      xneste
     c                   endif
      *
      * Legge ut tekstlinje på ny suffix
      *
     c                   eval      fodtl1_numm = fdnumm
     c                   eval      fodtl1_line = fdline
     c     rodtlu_key    chain     fodtlur                            94
     c                   if        *in94 = *on
     c                   eval      fdnumm = rohel1_numm
     c                   eval      fdsuff = rohel1_suff
     c                   eval      fdanta = 0
     c                   time                    fdodat
     c                   time                    fdotim
     c                   eval      fdoous = l_user
     c                   time                    fdedat
     c                   time                    fdetim
     c                   eval      fdeusr = l_user
     c                   write     fodtlur
8.14  *
8.14  * Oppdater tilhørende bestillinhslinje
8.14  *
8.14 c                   if        fdbenr <> 0
8.14 c                   eval      lodtlu_numm = fdbenr
8.14 c                   eval      lodtlu_suff = fdbsuf
8.14 c                   eval      lodtlu_line = fdblin
8.14 c                   eval      lodtlu_line = fdblin
8.14 c     lodtlu_key    chain     lodtlu
8.14 c                   if        %found
8.14 c                   eval      ldorsu = fdsuff
8.14 c                   time                    ldedat
8.14 c                   time                    ldetim
8.14 c                   eval      ldeusr = l_user
8.14 c                   update    lodtlur
8.14 c                   endif
8.14 c                   endif
     c                   endif
      *
      * Dersom det er igjen linjer på "gammel ordre"
      * kan ikke ordrehode slettes (kopiering)
      *
     c                   if        fklkod = '3'
     c                   eval      w_test = 1
     c                   goto      xneste
     c                   endif
      *
      * Slette eventuelle tekstlinjer fra "gammel ordre"
      *
     c     fodtl1_key    chain     fodtlur                            94
     c                   if        *in94 = *off
     c                   delete    fodtlur
     c                   endif
      *
     c     xneste        tag
      *
     c                   goto      olres1
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer PLUKK-S-LINJE-REGISTER med sorterings-felter m.m.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ohead         begsr
      *
     c                   eval      fpsolu_stll = *zero
     c                   eval      fpsolu_slin = *zero
     c     fpsolu_key    chain     fpsolur                            92
      *
     c                   if        *in92 = *off
     c                   update    fpsolur
     c                   else
     c                   eval      fksfir = fkpfir
     c                   eval      fksonr = fkponr
     c                   eval      fkssuf = fkpsuf
     c                   eval      fkslin = *zero
     c                   eval      fkstll = *zero
     c                   eval      fkskod = 'H'
     c                   write     fpsolur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter ordre-linjer og legger ut til PLUKK-S-LINJE-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     linje         begsr
      *
     c                   eval      fodtl1_line = *zero
      *
     c     fodtl1_key    setll     fodtl1r
      *
     c     linje1        tag
     c                   eval      *in90 = *off
     c                   read      fodtl1r                                90
     c                   if        *in90 = *off
     c                   if        fdfirm = w_firm
     c                   if        fdnumm = fodtl1_numm
     c                   if        fdsuff = fodtl1_suff
      *
      * Kaller opp subrutine for oppdatering av lager
      *
     c                   if        faalag <> *zero
     c                   if        w_olag =  *zero
     c                   if        vaoakk = 3
     c                   if        footyp = fk2oty
     c                   eval      w_lage = w_lage_sav
     c                   eval      w_otyp = w_otyp_sav
     c                   exsr      opplag
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
      * legger evt. linjer til BygDok
      *
     c                   if        b_bdo1 = *on and
     c                             b_bdo2 = *on and
     c                             b_bdo3 = *on and
     c                             ((fdnobb <> 0) or
     c                             (u_s701 = *on and
     c                              fdvare <> *blank))
     c                   eval      rkunl1_kund = fokund
     c     rkunl1_key    chain     rkunl1
      * finner kontaktperson for kunde som har rolle BYGGDOK
     c                   eval      b_kper  = *off
     c                   eval      rukpl6_kund = fokund
     c     rukpl6_key    setll     rukpl6
     c     rukpl6_key    reade     rukpl6
     c                   dow       not %eof
     c     lo:up         xlate     rukrol        rukrol
     c                   if        rukrol  = 'BYGGDOK' or rukrol = 'COBUILDER'
     c                   eval      b_kper  = *on
     c                   goto      fbdo_tag
     c                   endif
     c     rukpl6_key    reade     rukpl6
     c                   enddo
     c     fbdo_tag      tag
     c                   eval      fwbfir = w_firm
     c                   eval      fwbkun = fokund
     c                   eval      fwbkpr = fokpro
     c                   movel     fonumm        fwbnum
     c                   eval      fwbnav = rknavn
     c                   if        b_kper  = *on and
     c                             ruenav <> *blank and
     c                             rumail <> *blank
     c                   eval      fwbper = %trim(ruenav) + ',' +
     c                                      %trim(rufnav)
     c                   eval      fwbmai = rumail
     c                   else
     c                   eval      fwbper = rknavn
     c                   eval      fwbmai = rkemal
     c                   endif
      * hvis mail på kunde-prosjekt overstyrer det alt
     c                   if        flmail <> *blank
     c                   eval      fwbmai = flmail
     c                   if        flkprs <> *blank
     c                   eval      fwbper = flkprs
     c                   endif
     c                   endif
     c                   eval      fwbftn = rkftnr
     c                   move      w_btms        w_ti26
     c                   eval      fwbtms = w_ti26
     c                   if        u_s701 = *on
     c                   movel     fdvare        fwbnob
     c                   else
     c                   eval      fwbnob = fdnobb
     c                   endif
     c                   time                    fwboda
     c                   time                    fwboti
     c                   time                    fwbeda
     c                   time                    fwbeti
     c                   eval      fwbous = l_user
     c                   eval      fwbeus = l_user
8.15 c                   if        b_bygd = *on
     c                   write     fbdopfr
8.15 c                   endif
     c                   move      fwbnum        w_bnum
     c                   eval      b_bdo4 = *on
     c                   endif
      *
     c                   eval      fpsolu_stll = 1
     c                   eval      fpsolu_slin = fdline
     c     fpsolu_key    chain     fpsolur                            92
      *
     c                   if        *in92 = *off
     c                   update    fpsolur
     c                   else
     c                   eval      fkstll = 3
     c                   eval      fkslin = fdline
     c                   eval      fkskod = 'V'
     c                   write     fpsolur
     c                   endif
      *
     c                   goto      linje1
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter ordre-tekst (FORAN) og legger ut til PLUKK-S-LINJE-REG.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     foran         begsr
      *
     c                   eval      fotxl1_line = *zero
      *
     c     fotxl1_key    setll     fotxl1r
      *
     c     foran1        tag
     c                   eval      *in90 = *off
     c                   read      fotxl1r                                90
     c                   if        *in90 = *off
     c                   if        ftfirm = w_firm
     c                   if        ftnumm = fotxl1_numm
     c                   if        ftsuff = fotxl1_suff
     c                   if        ftline <= 4999
      *
     c                   eval      fpsolu_stll = 2
     c                   eval      fpsolu_slin = ftline
     c     fpsolu_key    chain     fpsolur                            92
      *
     c                   if        *in92 = *off
     c                   update    fpsolur
     c                   else
     c                   eval      fkstll = 2
     c                   eval      fkslin = ftline
     c                   eval      fkskod = 'K'
     c                   write     fpsolur
     c                   endif
      *
     c                   goto      foran1
     c                   endif
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter ordre-tekst (ETTER) og legger ut til PLUKK-S-LINJE-REG.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     etter         begsr
      *
     c                   eval      fotxl1_line = 5000
      *
     c     fotxl1_key    setll     fotxl1r
      *
     c     etter1        tag
     c                   eval      *in90 = *off
     c                   read      fotxl1r                                90
     c                   if        *in90 = *off
     c                   if        ftfirm = w_firm
     c                   if        ftnumm = fotxl1_numm
     c                   if        ftsuff = fotxl1_suff
     c                   if        ftline <= 9999
      *
     c                   eval      fpsolu_stll = 4
     c                   eval      fpsolu_slin = ftline
     c     fpsolu_key    chain     fpsolur                            92
      *
     c                   if        *in92 = *off
     c                   update    fpsolur
     c                   else
     c                   eval      fkstll = 4
     c                   eval      fkslin = ftline
     c                   eval      fkskod = 'K'
     c                   write     fpsolur
     c                   endif
      *
     c                   goto      etter1
     c                   endif
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Leser igjenom ordrelinjer for oppdatering av lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     laglag        begsr
      *
8.01 c                   eval      b_ordr = *off
      *
     c                   eval      fodtl1_line  = *zero
     c     fodtl1_key    setll     fodtl1r
      *
     c     lanje1        tag
     c                   eval      *in90 = *off
     c                   read      fodtl1r                                90
     c                   if        *in90 = *off
     c                   if        fdfirm = w_firm
     c                   if        fdnumm = fodtl1_numm
     c                   if        fdsuff = fodtl1_suff
8.01  *
8.01  * Hent informasjon fra plukkeregisteret. Hvis den ikke ligger her, skal den heller
8.01  * ikke oppdateres.
8.01  *
8.01 c                   eval      fplll1_llin = fdline
8.01 c     fplll1_key    chain     fplll1
8.01 c                   if        %found
      *
      * Behandler fra antall på ordretype FRA
      *
     c                   eval      w_lage = w_lage_sav
     c                   eval      w_otyp = w_otyp_sav
     c                   eval      fdanta = fdanta * -1
     c                   exsr      opplag
     c                   eval      fdanta = fdanta * -1
      *
      * Behandler til antall på ordretype TIL
      *
     c                   eval      w_lage = fkplag
     c                   eval      w_otyp = fkpoty
8.01 c                   eval      w_anta = fdanta
8.01 c                   eval      fdanta = fklant
     c                   exsr      opplag
8.01  *
8.01  * Hvis det er avvik mellom antall på ordreslinje og pakkseddel
8.01  * oppdater med plukket antall.
8.01  *
8.01 c                   if        w_anta <> fdanta
8.01 c                   eval      fodtlu_line = fdline
8.01 c     fodtlu_key    chain     fodtlu
8.01 c                   if        %found
8.01 c                   eval      b_ordr = *on
8.01 c                   eval      fdanta = fklant
8.01 c                   time                    fdedat
8.01 c                   time                    fdetim
8.01 c                   eval      fdeusr = l_user
8.01 c                   update    fodtlur
8.01 c                   endif
8.01 c                   endif
      *
8.01 c                   endif
      *
     c                   goto      lanje1
     c                   endif
     c                   endif
     c                   endif
     c                   endif
8.01  *
8.01  *  Beregner nye summer hvis det er endret antall
8.01  *
8.01 c                   if        b_ordr = *on
8.01 c                   eval      w_enor = *blank
8.01 c                   call      'FO730R'
8.01 c                   parm                    w_enor
8.01 c                   parm                    w_firm
8.01 c                   parm                    fohel1_numm
8.01 c                   parm                    fohel1_suff
8.01 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdatering av Lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     opplag        begsr
      *
      * Legger over verdier i overførings-felter
      *
     c                   eval      hlvare = fdvare
     c                   eval      hllage = fdlage
     c                   eval      hlenhe = fdenh1
     c                   eval      hldato = *loval
     c                   eval      hltime = *loval
     c                   eval      hlotyp = w_otyp
     c                   eval      hlltyp = fdltyp
      *
     c                   eval      hlanta = fdanta
     c                   eval      hlkopr = fdkopr
     c                   eval      hlonum = fdnumm
     c                   eval      hlolin = fdline
     c                   eval      hlsyst = 'F'
     c                   eval      hlaltk = *blank
     c                   eval      hlrest = *blank
     c                   move      fdnumm        w_numa
     c                   move      fdline        w_lina
     c                   eval      hlrefr = 'O:' + w_numa +
     c                                      ' L:' + w_lina
     c                   move      fdlety        hllety
      *
      * Ved oppdatering av fakturering,
      * skal fakturadato benyttes
      * (ikke dagens dato)
      *
     c                   if        vaoakk = 3
     c                   eval      hldato = fk2dat
     c                   endif
      *
      * Kaller opp lageroppdaterings-program
      *
     c                   eval      w_lrec = hlrec
      *
     c                   call      'VL001R'
     c                   parm                    w_stat
     c                   parm                    w_lrec
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for slette plukk-reg. på RadioTerm.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     slett_plukk   begsr
      *
     c     lwpllu_key    setll     lwpllu
     c     lwpllu_key    reade     lwpllu
     c                   dow       not %eof(lwpllu)
      *
     c                   delete    lwpllur
      *
     c     lwpllu_key    reade     lwpllu
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for slette plukk-reg. på RadioTerm.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     upd_logg      begsr
      *
      *
     c                   time                    d_date
     c                   time                    d_time
     c                   eval      d_user = l_user
     c                   eval      d_wsid = l_wsid
     c                   eval      d_aarr = %subdt(fobdat:*years)
     c                   eval      p_urec = d_urec
     c                   call      'FU900R  '
     c                   parm                    p_stat
     c                   parm                    p_urec
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekke om kjørekontor er valgt (loggkode = 0)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_lkode   begsr
     c                   eval      b_kjko = *off
     c                   eval      w_kod2 = '0'
      *
     c                   eval      w_aarr = %subdt(fobdat:*years)
     c                   call      'FU702R'
     c                   parm                    w_aarr
     c                   parm                    w_lnum
     c                   parm                    w_lsuf
     c                   parm                    w_kod2
     c                   parm                    w_sva2
      *
     c                   if        w_sva2 = 'J'
     c                   eval      b_kjko= *on
     c                   endif
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Definisjon av local data area for output
      *
     c     *dtaara       define    *lda          lda
      *
     c                   eval      w_in03 = *blank
      *
      * Nullstilling av felter som ikke kan redefineres
      *
     c                   eval      w_test = *zero
      *
     c                   eval      w_sald = *zero
     c                   eval      w_sal2 = *zero
      *
     c                   eval      w_enor = *blank
     c                   eval      w_ruti = *blank
      *
      * Key for file : KUNDE-REGISTER
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for file : ORDRETYPE-REGISTER
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for file : OFAK-KODE-REGISTER
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for file : ORDRE-HODE-REGISTER
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      * Key for file : ORDRE-LINJE-REGISTER
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
     c                   kfld                    fodtl1_line
8.01  *
8.01  * Key for file : ORDRE-LINJE-REGISTER
8.01  *
8.01 c     fodtlu_key    klist
8.01 c                   kfld                    w_firm
8.01 c                   kfld                    fodtlu_numm
8.01 c                   kfld                    fodtlu_suff
8.01 c                   kfld                    fodtlu_line
      *
      * Key for file : ORDRE-LINJE-REGISTER (les)
      *
     c     fodtlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtlr_numm
     c                   kfld                    fodtlr_suff
     c                   kfld                    fodtlr_line
8.01  *
8.01  * Key for file : ORDRE-LINJE-REGISTER (les)
8.01  *
8.01 c     fodtlr_key2   klist
8.01 c                   kfld                    w_firm
8.01 c                   kfld                    fodtlr_numm
8.01 c                   kfld                    fodtlr_suff
      *
      * Key for file : ORDRE-TEKST-REGISTER
      *
     c     fotxl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fotxl1_numm
     c                   kfld                    fotxl1_suff
     c                   kfld                    fotxl1_line
      *
      * Key for file : ORDRE-HODE-REGISTER, for resting
      *
     c     rohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rohel1_numm
     c                   kfld                    rohel1_suff
      *
      * Key for file : ORDRE-LINJE-REGISTER, for resting
      *
     c     rodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rohel1_numm
     c                   kfld                    rohel1_suff
     c                   kfld                    fodtl1_line
      *
      * Key for file : PLUKK-S-LINJE-REGISTER
      *
     c     fpsolu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fpsolu_sonr
     c                   kfld                    fpsolu_ssuf
     c                   kfld                    fpsolu_stll
     c                   kfld                    fpsolu_slin
      *
      * Key for file : UTPLUKK-PARAM-REGISTER
      *
     c     fplol1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fplolu_ponr
     c                   kfld                    fplolu_psuf
      *
      * Key for file : UTPLUKK-LINJE-REGISTER
      *
     c     fplllu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fplllu_lonr
     c                   kfld                    fplllu_lsuf
     c                   kfld                    fplllu_llin
8.01  *
8.01  * Key for file : UTPLUKK-LINJE-REGISTER
8.01  *
8.01 c     fplllu_key2   klist
8.01 c                   kfld                    w_firm
8.01 c                   kfld                    fplllu_lonr
8.01 c                   kfld                    fplllu_lsuf
8.01  *
8.01  * Key for file : UTPLUKK-LINJE-REGISTER
8.01  *
8.01 c     fplll1_key    klist
8.01 c                   kfld                    w_firm
8.01 c                   kfld                    fplll1_lonr
8.01 c                   kfld                    fplll1_lsuf
8.01 c                   kfld                    fplll1_llin
      *
      * Key for oppslag på ordre-info-type
      *
     c     vinfl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vinfl1_ityp
      *
      * Key for brukerregister
      *
     c     fusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fusrl1_user
      *
      * Key for kort/fakt trans/numm
      *
     c     fntrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fntrl1_numm
     c                   kfld                    fntrl1_suff
      *
      * Key for oppslag på kort/fakt.avtale-register
      *
     c     fmkal1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fmkal1_kund
     c                   kfld                    fmkal1_kpro
     c                   kfld                    fmkal1_kftp
     c                   kfld                    fmkal1_ktsq
      *
      * Key for file : Arbeidsregister utplukk
      *
     c     lwpllu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lwpllu_numm
     c                   kfld                    lwpllu_suff
      *
      * Key for oppslag på depositum-fil
      *
     c     sdepl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sdepl1_numm
     c                   kfld                    sdepl1_kund
      *
      * Key for les av moduler Nexstep
      *
     c     amodl1_key    klist
     c                   kfld                    amodl1_modu
      *
      * Key for les av kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      * Key for propertiesfil
      *
     c     afpslr_key    klist
     c                   kfld                    afpslr_ufil
     c                   kfld                    afpslr_uide
      *
      * Key for kontakpersonregister
      *
     c     rukpl6_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rukpl6_kund
8.13  *
8.13  * Key for les av transportlogging
8.13  *
8.13 c     ftrpi2_key    klist
8.13 c                   kfld                    w_firm
8.13 c                   kfld                    ftrpi2_rnum
8.13 c                   kfld                    ftrpi2_rsuf
8.13  *
8.13  * Key for les av transportlogging
8.13  *
8.13 c     ftrpiu_key    klist
8.13 c                   kfld                    w_firm
8.13 c                   kfld                    ftrpiu_roid
8.14  *
8.14  * Key for file : Bestilling-LINJE-REGISTER
8.14  *
8.14 c     lodtlu_key    klist
8.14 c                   kfld                    w_firm
8.14 c                   kfld                    lodtlu_numm
8.14 c                   kfld                    lodtlu_suff
8.14 c                   kfld                    lodtlu_line
      *
      * Blanker hjelpefelter som benyttes i Local Data Area
      *
     c                   eval      l_alt1 = *zero
     c                   eval      l_alt2 = *zero
     c                   eval      l_peri = *zero
     c                   eval      l_dato = *loval
     c                   eval      l_prog = *blank
      *
      * Legger inn type for oppdatering av lager  R=Registrering
      *                                             =Utskrift/Utplukk
      *
     c                   eval      hltype = *blank
      *
      * Hente inn brukerregister
      *
     c                   eval      w_firm      = l_firm
     c                   eval      fusrl1_user = l_user
     c     fusrl1_key    chain     fusrl1r                            90
      *
      * Legger inn dagens dato
      *
     c                   time                    w_date
      *
      * Sjekker om Byggdok skal kjøres forutsetter at
      * modul er på plass og at
      *
     c                   eval      b_bdo1 = *off
     c                   eval      amodl1_modu = 'BYGGDOK'
     c     amodl1_key    chain     amodl1
     c                   if        %found(amodl1)
     c                   eval      afpslr_ufil = l_filg
     c                   eval      afpslr_uide = 'BYGGDOK   '
      *
      * Sjekk om byggdok eller cobuilder aktivering er på/av
      *
     c     afpslr_key    chain     afpslrr
     c                   if        %found(afpslr)
     c                   eval      b_bdo1 = *on
     c                   else
     c                   eval      afpslr_uide = 'COBUILDER '
     c     afpslr_key    chain     afpslrr
     c                   if        %found(afpslr)
     c                   eval      b_bdo1 = *on
     c                   endif
     c                   endif
     c                   endif
      *
      * Sjekker om det ikke er krav til nobbnummer
      *
       CallP CO402R(l_filg:w_firm:0:'COBU_IKKENOBB':
       co402_verdi1:co402_verdi2);
       if co402_verdi1 = '1';
         u_s701 = *on;
       endif;
8.05  *
8.05  * Endring 8.05 - Koblet til ekstern flåtestyring
8.05  *
8.05   CallP CO402R(l_filg:w_firm:0:'EKSTERN_FLATESTYRING':
8.05                    co402_verdi1:co402_verdi2);
8.05   if co402_verdi1 = '1';
8.05     u_flat = *on;
8.05   endif;
      *
     c                   endsr
