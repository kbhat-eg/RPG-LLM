     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : EK720R
      * Beskrivelse . . : Kunde bonus, beregner og oppdaterer bonus - alle
      *                   kunder
      *
      *                   Er laget for å kjøres som batch-jobb i jobbplan
      *
      * Spesialversjon. : Hardkodet årstall for engangskjøring
      * Tilpasset for . : Degernes
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       261005 HKO  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fekboi1    if   e           k disk    rename(ekbost:ekboi1r)
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variabler i nøkler
      *
     d ekboi1_bkun     s                   like(ekbkun)
      *
      * Definering av variable
      *
     d w_firm          s                   like(ekfirm)
      *
     d w_udat          s               d   datfmt(*iso)
     d w_udat_8        s              8
      *
     d w_firm_alf      s              3
     d w_bkun_alf      s              6
     d w_paar_alf      s              4
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Utleder år
      *
     c                   time                    w_udat
     c     *iso0         move      w_udat        w_udat_8
     c****               movel     w_udat_8      w_paar_alf
     c                   movel     '2007'        w_paar_alf
      *
     c                   movel     w_firm        w_firm_alf
      *
      * Kaller kalkulasjonsprogram for alle kunder det er knyttet
      * bonusavtale i mot
      *
     c     rkunl1_key    setll     rkunl1
     c     rkunl1_key    reade     rkunl1
     c                   dow       not %eof
      *
     c                   eval      ekboi1_bkun = rkkund
     c     ekboi1_key    chain     ekboi1
     c                   if        %found
     c                   movel     ekbkun        w_bkun_alf
     c                   call      'EK711R'
     c                   parm                    w_firm_alf
     c                   parm                    w_bkun_alf
     c                   parm                    w_paar_alf
     c                   endif
      *
     c     rkunl1_key    reade     rkunl1
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for les på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på kundebonus
      *
     c     ekboi1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ekboi1_bkun
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
