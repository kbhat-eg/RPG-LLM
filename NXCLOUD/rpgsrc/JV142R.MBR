     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JV142R
      * Beskrivelse . . : Overføring til EVR, kalkulasjon av vare
      *
      * NB! Hardkodet moms på 25 %
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       241196 HKO  Nytt program
      *       091297 HKO  Lagt inn avrunding av salspris inkl. mva
      *                   Lagt inn automatisk oppdatering ved endring av
      *                   salgspriser inkl. mva
      *       170698 HKO  Tilpasset ny datamodell for betingelser
      *       120898 HKO  Forbedret endring av betingelses-tekst, og
      *                   tilpasset programmet til ny prog.standard.
      *       261098 HKO  Forbedret programlogikk
      *                   Regner om nettopris og påslagsbeløp kostpris til
      *                   prisenhet før lagring i tabeller
      *       040299 HKO  Rettet feil med at nettopris og påslagsbeløp
      *                   kostpris ikke ble beregnet til prisenhet når
      *                   denne ikke lå som enhet 1, 2 eller 3 på varen
      *       150102 HKO  Lagt inn firmnummer ved oppretting av rabatt-
      *                   element
      *       101202 HKO  Lagt inn grossistløsning
      *       191003 HKO  Modul som nivå under påslag
      *       170904 HKO  Viser info om slettet i Nobb
      *       071204 HKO  Endret parameter ved kall av VP716R
      *       171204 HKO  Rettet gammel morro ved kall av VP716R
      *       100605 HKO  Viser varetype og leveringstype fra påslag og
      *                   betingelse
      *       150605 HKO  Lagt inn valg av leveringstype og endring av
      *                   varetype samt funksjon for vedlikehold av vare
      *       130905 HKO  Overstyring til skaffevare som parameter
      *       150905 HKO  Lagrer kalkyle iht. varetype og leveringstype
      *                   Lagt inn kalkulering av salgspris fra Nobbpris
      *       280508 bof  Utvidet parm. til JV750R
5.70  *       171108 bof  Utvidet med prisgruppe
6.10  *       260809 bof  Endret statuskode fra num. til alfa.
6.20  *       061011 bof  Eget pris tas med valgt leverandør VP713R
6.21  *       080114 bof  Hvis bytte av hovedlev.,bruk hlev. pris fra EVR
6.30  *       280114 bof  Utvidet nøkkel med lev.nr i JVPKPF
6.31  *       270214 bof  endret kall til vedlikehold av pakning
6.32  *       140314 bof  Lagt inn test på fra /til dato på pakning
6.33  *       020714 bof  Ny popup med kostpris og innkjøpspris pr enhet
6.34  *       140714 bof  Utvidet med prisendringsdato + parameter for utg.priser
6.35  * 6.30  221014 Bsa  Innfører F tast for visning av NOBB info. Erstatter
6.35  *                   koding i jacada.
6.3x  * 6.30  040615 bof  utvidet parm i forb. med prgr. på betingelse
6.3y  * 6.30  050815 bof  utv. parm i forb. med prgr. på frakt + oppdat.
6.36  *       160916 bof  Ny datostyring på prisdato i EVR og prisdato l.dor
6.37  * 6.30  221118 bof  Utvidet med logistikk-vare
6.38  * 6.30  181218 bkv  Rettelse påslag på varenivå for TLS
6.39  * 6.30  250319 bof  Feilretting vedr. F22 på logistikk-varer
8.01  *PRG2   140622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjvdtl1    if   e           k disk    rename(jvdtpfr:jvdtl1r)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fjvpkl2    if   e           k disk    rename(jvpkpfr:jvpkl2r)
     fjwprlu    uf a e           k disk    rename(jwprpfr:jwprlur)
     fjvdtlu    uf a e           k disk    rename(jvdtpfr:jvdtlur)
     fjbetlu    uf a e           k disk    rename(jbetpfr:jbetlur)
     fjraelu    uf a e           k disk    rename(jraepfr:jraelur)
     fjpfalu    uf a e           k disk    rename(jpfapfr:jpfalur)
     fvltpl1    if   e           k disk    rename(vltppfr:vltpl1r)
5.70 fra30l1    if   e           k disk    rename(ra30pfr:ra30l1r)
      *
     fjv142d    cf   e             workstn
      *
      * Definering av parametre
      *
     d p_firm          s                   like(jvdfir)
     d p_vare          s                   like(jvvare)
     d p_pgiv          s                   like(jvldor)
     d p_lety          s                   like(jplety)
     d p_skaf          s              1  0
5.70 d p_prgr          s                   like(jcprgr)
6.21 d p_hlev          s              1  0
6.36 d p_pdat          s               d   datfmt(*iso)
6.36 d p_edat          s               d   datfmt(*iso)
6.34 d p_utgp          s              1  0
6.35 d p_nobb          s              8
      *
      * Definering av LDA
      *
     d                uds
     d l_user                911    920
     d l_fnav                951    980
      *
      * Definering av variable i nøkler
      *
     d jlevl1_ldor     s                   like(jlldor)
6.30 d jvpkl2_ldor     s                   like(jwldor)
     d jvpkl2_enhe     s                   like(jwenhe)
     d jbetlu_ldor     s                   like(jpldor)
6.3x d jbetlu_prgr     s                   like(jpprgr)
     d jbetlu_ogrp     s                   like(jpogrp)
     d jbetlu_hgrp     s                   like(jphgrp)
     d jbetlu_ugrp     s                   like(jpugrp)
     d jbetlu_modn     s                   like(jpmodn)
     d jbetlu_vare     s                   like(jpvare)
     d jbetlu_vtyp     s                   like(jpvtyp)
     d jbetlu_lety     s                   like(jplety)
     d jraelu_eldo     s                   like(jpeldo)
6.3x d jraelu_eprg     s                   like(jpeprg)
     d jraelu_eogr     s                   like(jpeogr)
     d jraelu_ehgr     s                   like(jpehgr)
     d jraelu_eugr     s                   like(jpeugr)
     d jraelu_emod     s                   like(jpemod)
     d jraelu_evar     s                   like(jpevar)
     d jraelu_evty     s                   like(jpevty)
     d jraelu_elet     s                   like(jpelet)
     d jraelu_ebrs     s                   like(jpebrs)
     d jraelu_ebli     s                   like(jpebli)
5.70 d jpfalu_prgr     s                   like(jcprgr)
     d jpfalu_ogrp     s                   like(jcogrp)
     d jpfalu_hgrp     s                   like(jchgrp)
     d jpfalu_ugrp     s                   like(jcugrp)
     d jpfalu_ldor     s                   like(jcldor)
     d jpfalu_modn     s                   like(jcmodn)
     d jpfalu_vare     s                   like(jcvare)
     d jpfalu_kpri     s                   like(jckpri)
     d jpfalu_vtyp     s                   like(jcvtyp)
     d jpfalu_lety     s                   like(jclety)
     d vltpl1_lety     s                   like(vylety)
5.70 d ra30l1_dkod     s                   like(redkod)
      *
      * Definering av variable
      *
     d b_bet_oppd      s              1    inz(*off)
     d b_endr_bet      s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
6.32 d b_pakn          s              1    inz(*off)
      *
     d w_firm          s              3  0
     d w_udat          s               d   datfmt(*iso)
     d w_utim          s               t   timfmt(*iso)
     d w_pgiv          s              6  0
6.34 d w_lvar          s                   like(jvlvar)
     d w_gdat          s               d   datfmt(*iso)
     d w_eldo          s              6  0
     d w_ofp1          s             12  6
     d w_ofp2          s             12  6
     d w_ofp3          s             12  6
     d w_ofv2          s             12  6
     d w_ofv3          s             12  6
     d w_ofi1          s             12  6
     d w_ofi2          s             12  6
     d w_ofi3          s             12  6
     d w_bvar          s             15
     d w_pvar          s             15
     d w_midl          s              1  0
     d w_enh1          s              3
     d w_enh2          s              3
     d w_enh3          s              3
     d w_eni1          s              3
     d w_eni2          s              3
     d w_eni3          s              3
     d w_enp1          s              3
     d w_enp2          s              3
     d w_enp3          s              3
     d w_bup1          s              9  2
     d w_pda1          s               d   datfmt(*iso)
     d w_bup2          s              9  2
     d w_omr2          s             12  6
     d w_pda2          s               d   datfmt(*iso)
     d w_bup3          s              9  2
     d w_omr3          s             12  6
     d w_pda3          s               d   datfmt(*iso)
     d w_fdat          s               d   datfmt(*iso)
     d w_ftid          s               t   timfmt(*iso)
     d w_tdat          s               d   datfmt(*iso)
     d w_ttid          s               t   timfmt(*iso)
     d w_kamp          s              5
     d w_tip1          s              9  2
     d w_tkp1          s              9  2
     d w_tip2          s              9  2
     d w_tip3          s              9  2
     d w_enin          s              3
     d w_enps          s              3
     d w_pspr          s              9  2
     d w_valg          s              1  0
      *
     d w_mva_fakt      s              5  2
     d w_dekn_wrk      s              5  2
     d w_kode          s              1  0
     d w_gprk          s              1  0
     d w_dekn          s              5  2
6.34 d w_ffak          s              8  6
6.34 d w_inp2          s              9  2
6.34 d w_inpr          s              9  2
6.3y d w_oppd          s              1
      *
     d w_ntpr_wrk      s              9  2
     d w_ifak_wrk      s              8  6
      *
     d w_vare          s                   like(jvvare)
     d w_vtyp          s                   like(jcvtyp)
     d w_lbes          s                   like(vybesk)
     d w_kbel          s                   like(jckobe)
     d w_ofak          s                   like(jwofak)
      *
     d w_kfak_wrk      s                   like(jckofa)
     d w_kbel_wrk      s                   like(jckobe)
     d w_sfak_wrk      s                   like(jcsafa)
     d w_inpr_wrk      s                   like(jbinpr)
     d w_kopr_wrk      s                   like(jbkopr)
     d w_s1im_wrk      s                   like(jbs1pr)
     d w_s2im_wrk      s                   like(jbs2pr)
     d w_s3im_wrk      s                   like(jbs3pr)
     d w_s1pr_wrk      s                   like(jbs1pr)
     d w_s2pr_wrk      s                   like(jbs2pr)
     d w_s3pr_wrk      s                   like(jbs3pr)
6.37 d b_logv          s              1    inz(*off)
6.37 d w_lv_vare       s                   like(jvvare)
6.37 d w_lv_modn       s                   like(jvmodn)
6.37 d w_lv_kldo       s                   like(jvldor)
6.37 d w_lv_nobb       s                   like(jvnobb)
6.37 d w_lv_ofak       s                   like(jwofak)
      *
     d s_ntpr          s              9  2
     d s_ifak          s              8  6
      *
     d s_pkod          s                   like(jvdpko)
     d s_skod          s                   like(jvdsko)
5.70 d s_prgr          s                   like(jcprgr)
     d s_lety          s                   like(jplety)
     d s_kfak          s                   like(jckofa)
     d s_kbel          s                   like(jckobe)
     d s_sfak          s                   like(jcsafa)
     d s_inpr          s                   like(jbinpr)
     d s_kopr          s                   like(jbkopr)
     d s_s1pr          s                   like(jbs1pr)
     d s_s2pr          s                   like(jbs2pr)
     d s_s3pr          s                   like(jbs3pr)
      *
6.37 C/Exec SQL
6.37 C+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR
6.37 C/End-Exec
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer skjermbilde
      *
     c                   eval      b1lety = p_lety
5.70 c                   eval      b1prgr = p_prgr
      *
     c     b1tag         tag
      *
      * Henter vareinformasjon og varedetaljer
      *
     c     jvarl1_key    chain     jvarl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
6.37 c                   exsr      finn_logivar
6.37  *
     c                   eval      b1vare = jvvare
     c                   eval      b1vtek = jvtek1
6.37 c                   if        w_lv_vare <> *blank
6.37 c                   eval      b1nobb = w_lv_nobb
6.37 c                   eval      b1modn = w_lv_modn
6.37 c                   else
     c                   eval      b1nobb = jvnobb
     c                   eval      b1modn = jvmodn
6.37 c                   endif
     c                   eval      b1ogrp = jvogrp
     c                   eval      b1hgrp = jvhgrp
     c                   eval      b1ugrp = jvugrp
      *
     c     jvdtl1_key    chain     jvdtl1
     c                   if        %found
     c                   eval      b1pkod = jvdpko
     c                   eval      b1skod = jvdsko
     c                   endif
      *
     c                   if        p_skaf = 1
     c                   eval      b1skod = 1
     c                   endif
6.20  *
6.20 c                   eval      jlevl1_ldor = p_pgiv
6.20 c     jlevl1_key    chain     jlevl1
      *
     c     b1taga        tag
      *
      * Henter prisinformasjon fra eget vareregister
      *
     c                   call      'VP713R'
     c                   parm                    w_firm
5.70 c                   parm                    w_vare
6.20 c                   parm                    jlenum
5.70 c                   parm                    b1prgr
     c                   parm                    b1lety
     c                   parm                    w_udat
     c                   parm                    w_utim
     c                   parm                    w_enh1
     c                   parm                    b1s1gm
     c                   parm                    w_bup1
     c                   parm                    b1k1gm
     c                   parm                    b1i1gm
     c                   parm                    w_pda1
     c                   parm                    w_enh2
     c                   parm                    b1s2gm
     c                   parm                    w_bup2
     c                   parm                    w_omr2
     c                   parm                    w_pda2
     c                   parm                    w_enh3
     c                   parm                    b1s3gm
     c                   parm                    w_bup3
     c                   parm                    w_omr3
     c                   parm                    w_pda3
     c                   parm                    w_fdat
     c                   parm                    w_ftid
     c                   parm                    w_tdat
     c                   parm                    w_ttid
     c                   parm                    w_kamp
     c                   parm                    w_tip1
     c                   parm                    w_tkp1
     c                   parm                    w_tip2
     c                   parm                    w_tip3
     c                   parm                    w_enin
     c                   parm                    w_enps
     c                   parm                    w_pspr
6.21 c                   parm                    p_hlev
      *
      * Hent leverandørinformasjon
      *
     c                   if        b1ldor <> 0
     c                   eval      jlevl1_ldor = b1ldor
     c     jlevl1_key    chain     jlevl1
     c                   if        %found
     c                   eval      b1navn = jlnavn
     c                   endif
     c                   endif
      *
      * Gjør om skaffekode til varetype
      *
     c                   if        b1skod = 1
     c                   eval      w_vtyp = 'S'
     c                   else
     c                   eval      w_vtyp = *blank
     c                   endif
      *
      * Henter prisinformasjon om vare
      *
     c                   exsr      hent_pris
      *
      * Tar vare på eksisterende verdier
      *
     c                   eval      s_pkod = b1pkod
     c                   eval      s_skod = b1skod
     c                   eval      s_lety = b1lety
5.70 c                   eval      s_prgr = b1prgr
      *
     c                   eval      s_ntpr = b1ntpr
     c                   eval      s_ifak = b1ifak
     c                   eval      s_kfak = b1kfak
     c                   eval      s_kbel = b1kbel
     c                   eval      s_sfak = b1sfak
      *
     c                   eval      s_inpr = b1inpr
     c                   eval      s_kopr = b1kopr
     c                   eval      s_s1pr = b1s1pr
     c                   eval      s_s2pr = b1s2pr
     c                   eval      s_s3pr = b1s3pr
      *
     c                   eval      *in80 = jvstat = 4
     c                   eval      *in81 = jlkkal = 1
      *
     c     b1tagb        tag
      *
      * Beregner prosentvis endring av pris + DG
      *
     c                   exsr      endring
      *
      * Tar vare på verdier i felter som kan endres
      *
     c                   eval      w_ntpr_wrk = b1ntpr
     c                   eval      w_ifak_wrk = b1ifak
     c                   eval      w_inpr_wrk = b1inpr
     c                   eval      w_kfak_wrk = b1kfak
     c                   eval      w_kbel_wrk = b1kbel
     c                   eval      w_kopr_wrk = b1kopr
      *
     c                   eval      w_sfak_wrk = b1sfak
     c                   eval      w_dekn_wrk = b1dekn
     c                   eval      w_s1pr_wrk = b1s1pr
     c                   eval      w_s2pr_wrk = b1s2pr
     c                   eval      w_s3pr_wrk = b1s3pr
     c                   eval      w_s1im_wrk = b1s1im
     c                   eval      w_s2im_wrk = b1s2im
     c                   eval      w_s3im_wrk = b1s3im
      *
     c     b1tagc        tag
      *
      * Legger ut bildet
      *
     c                   exfmt     b1bld
      *
      * Standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b1tagc
     c                   when      *inke = *on
     c                   goto      b1tag
     c                   endsl
      *
      * F13=Statuskoder
      *
     c                   if        *inkm = *on
     c                   call      'JV140R'
     c                   goto      b1tagc
     c                   endif
6.33  *
6.33  * F15=Popup med kostpris og innkjøpspris
6.33  *
6.33 c                   if        *inkp = *on
6.33  *
6.33 c                   exsr      xb3win
6.33 c                   goto      b1tagb
6.33  *
6.33 c                   endif
      *
      * F17=Vare-pakning
      *
     c                   if        *inkr = *on
     c                   call      'JW100R'
     c                   parm                    w_firm
     c                   parm                    w_vare
6.31 c                   parm                    p_pgiv
     c                   goto      b1tag
     c                   endif
      *
     c                   if        b1pkod <> s_pkod or
     c                             b1skod <> s_skod or
5.70 c                             b1lety <> s_lety or
5.70 c                             b1prgr <> s_prgr
     c                   goto      b1taga
     c                   endif
      *
      * Sjekk input og gyldige kombinasjoner av faktor/pris
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1tagc
     c                   endif
      *
      * Kalkulerer t.o.m. kostpris ved endring av faktorer og/eller beløp
      *
     c                   if        b1ntpr <> w_ntpr_wrk or
     c                             b1ifak <> w_ifak_wrk or
     c                             b1inpr <> w_inpr_wrk or
     c                             b1kfak <> w_kfak_wrk or
     c                             b1kbel <> w_kbel_wrk or
     c                             b1kopr <> w_kopr_wrk
     c                   exsr      kalk_kopr
     c                   endif
      *
      * Kalkulerer t.o.m. salgspris dersom F20, eller ved endring av
      * faktorer og/eller beløp. Kalkulerer salgspris eksl. moms fra
      * salgspris inkl. moms ved endring av salgspris inkl. mva.
      *
     c                   select
     c                   when      *inku = *on
     c                   if        jlkkal = 1
     c                   eval(h)   b1s1pr = b1nopr * b1sfak * w_ofp1
     c                   else
     c                   eval(h)   b1s1pr = b1kopr * b1sfak * w_ofp1
     c                   endif
     c                   exsr      kalk_saim
     c                   when      b1sfak <> w_sfak_wrk or
     c                             b1dekn <> w_dekn_wrk or
     c                             b1s1pr <> w_s1pr_wrk or
     c                             b1s2pr <> w_s2pr_wrk or
     c                             b1s3pr <> w_s3pr_wrk
     c                   exsr      kalk_saim
     c                   when      b1s1im <> w_s1im_wrk or
     c                             b1s2im <> w_s2im_wrk or
     c                             b1s3im <> w_s3im_wrk
     c                   exsr      kalk_sapr
     c                   endsl
      *
      * Sjekk om gyldig pris
      *
     c                   exsr      sjekk_pris
     c                   if        b_feil = *on
     c                   goto      b1tagc
     c                   endif
6.35  *
6.35  * F18=Viser NOBB informasjon
6.35  *
6.35 c                   if        *inks = *on
6.35 c                   exsr      vis_nobb
6.35 c                   goto      b1tagc
6.35 c                   endif
      *
      * F19=Vedlikehold av vare
      *
     c                   if        *inkt = *on
     c                   call      'JV101R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_valg
     c                   goto      b1tag
     c                   endif
      *
      * F21=Lagrer pris-endringer midlertidig
      *
     c                   if        *inkv = *on
      *
     c                   exsr      xb1win
     c                   if        b_anul = *on
     c                   goto      b1tagb
     c                   else
     c                   goto      b1tag
     c                   endif
      *
     c                   endif
      *
      * F22=Lagrer faktor-edringer permanent
      *
     c                   if        *inkw = *on
      *
     c                   exsr      xb2win
     c                   if        b_anul = *on
     c                   goto      b1tagb
     c                   else
     c                   goto      b1tag
     c                   endif
      *
     c                   endif
      *
      * Viser bildet på nytt
      *
     c                   goto      b1tagb
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av prisinformasjon om vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_pris     begsr
      *
      * Henter prisinformasjon fra fellesprogram
      *
     c                   eval      w_gprk = 0
     c                   eval      w_pgiv = p_pgiv
6.3y c                   eval      w_oppd = *blank
      *
     c                   call      'JV750R'
     c                   parm                    p_firm
     c                   parm                    p_vare
5.70 c                   parm                    b1prgr
     c                   parm                    w_pgiv
     c                   parm                    b1pkod
     c                   parm                    b1skod
     c                   parm                    b1lety
     c                   parm                    w_gprk
6.36 c                   parm                    p_pdat
6.34 c                   parm                    p_utgp
      *
     c                   parm                    b1osts
      *
     c                   parm                    b1ldor
     c                   parm                    w_lvar
     c                   parm                    w_eldo
     c                   parm                    b1nopr
     c                   parm                    w_gdat
      *
     c                   parm                    b1enhe
     c                   parm                    b1enh1
     c                   parm                    b1enh2
     c                   parm                    b1enh3
     c                   parm                    w_eni1
     c                   parm                    w_eni2
     c                   parm                    w_eni3
     c                   parm                    w_enp1
     c                   parm                    w_enp2
     c                   parm                    w_enp3
     c                   parm                    w_ofp1
     c                   parm                    w_ofp2
     c                   parm                    w_ofp3
     c                   parm                    w_ofv2
     c                   parm                    w_ofv3
     c                   parm                    w_ofi1
     c                   parm                    w_ofi2
     c                   parm                    w_ofi3
      *
     c                   parm                    b1bldo
     c                   parm                    b1bogr
     c                   parm                    b1bhgr
     c                   parm                    b1bugr
     c                   parm                    b1bmod
     c                   parm                    w_bvar
     c                   parm                    b1bvty
     c                   parm                    b1blet
     c                   parm                    b1bbet
     c                   parm                    b1ntpr
     c                   parm                    b1ifak
      *
     c                   parm                    b1pogr
     c                   parm                    b1phgr
     c                   parm                    b1pugr
     c                   parm                    b1pldo
     c                   parm                    b1pmod
     c                   parm                    w_pvar
     c                   parm                    b1ppko
     c                   parm                    b1pvty
     c                   parm                    b1plet
     c                   parm                    b1kfak
     c                   parm                    b1kbel
     c                   parm                    b1sfak
      *
     c                   parm                    w_midl
6.34 c                   parm                    b1inpr
     c                   parm                    b1kopr
     c                   parm                    b1s1pr
     c                   parm                    b1s1im
     c                   parm                    b1s2pr
     c                   parm                    b1s2im
     c                   parm                    b1s3pr
     c                   parm                    b1s3im
     c                   parm                    w_dekn
     c                   parm                    b1prut
6.34 c                   parm                    b1ffak
6.34 c                   parm                    b1inp2
6.3x c                   parm                    b1pgbe
6.3y c                   parm                    b1pgfr
6.3y c                   parm                    w_oppd
      *
6.34 c*                  eval(h)   b1inp2 = w_inpr * w_ffak
6.34 c*                  eval(h)   b1ffak = w_ffak
      *
      * Leverandør i påslag
      *
     c                   eval      b1bvar = w_bvar
     c                   eval      b1pvar = w_pvar
      *
      * Snur gyldig fra-dato for prisgivende leverandør
      *
     c                   eval      b1gdat = 0
     c                   if        w_gdat <> *loval
     c     *dmy          move      w_gdat        b1gdat
     c                   endif
      *
      * Flagg for midlertidig pris
      *
     c                   if        w_midl = 1
     c                   eval      b1midl = 'M'
     c                   else
     c                   eval      b1midl = *blank
     c                   endif
6.33  *
6.33  * beregner kostpris og innkjøpspris
6.33  *
6.33 c                   eval      b3kop1 = b1kopr
6.33 c                   eval      b3inp1 = b1inpr
6.33 c                   eval      b3in21 = b1inp2
6.33 c                   eval(h)   b3inp2 = b3inp1 * w_ofp2
6.33 c                   eval(h)   b3in22 = b3in21 * w_ofp2
6.33 c                   eval(h)   b3kop2 = b3kop1 * w_ofp2
6.33 c                   eval(h)   b3inp3 = b3inp1 * w_ofp3
6.33 c                   eval(h)   b3in23 = b3in21 * w_ofp3
6.33 c                   eval(h)   b3kop3 = b3kop1 * w_ofp3
6.33 c                   eval      b3enh1 = b1enh1
6.33 c                   eval      b3enh2 = b1enh2
6.33 c                   eval      b3enh3 = b1enh3
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Leveringstype
      *
     c                   if        w_afld = 'B1LETY'
     c                   call      'VY500R'
     c                   parm                    w_firm
     c                   parm                    b1lety
     c                   parm                    w_lbes
     c                   leavesr
     c                   endif
5.70  *
5.70  * Prisgruppe
5.70  *
5.70 c                   if        w_afld = 'B1PRGR'
5.70 c                   call      'RA530R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    b1prgr
5.70 c                   parm                    w_lbes
5.70 c                   leavesr
5.70 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for beregning av prosentvis endring av pris og
      * dekningsgrad (DG)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     endring       begsr
      *
     c                   if        b1i1gm <> 0
     c                   if        (100 * b1inpr / b1i1gm) - 100 > 999,99
     c                   eval      b1inen = 999,99
     c                   else
     c                   eval(h)   b1inen = (100 * b1inpr / b1i1gm) - 100
     c                   endif
     c                   endif
      *
     c                   if        b1k1gm <> 0
     c                   if        (100 * b1kopr / b1k1gm) - 100 > 999,99
     c                   eval      b1koen = 999,99
     c                   else
     c                   eval(h)   b1koen = (100 * b1kopr / b1k1gm) - 100
     c                   endif
     c                   endif
      *
     c                   if        b1s1gm <> 0
     c                   if        (100 * b1s1pr / b1s1gm) - 100 > 999,99
     c                   eval      b1s1en = 999,99
     c                   else
     c                   eval(h)   b1s1en = (100 * b1s1pr / b1s1gm) - 100
     c                   endif
     c                   endif
      *
     c                   if        b1s2gm <> 0
     c                   if        (100 * b1s2pr / b1s2gm) - 100 > 999,99
     c                   eval      b1s2en = 999,99
     c                   else
     c                   eval(h)   b1s2en = (100 * b1s2pr / b1s2gm) - 100
     c                   endif
     c                   endif
      *
     c                   if        b1s3gm <> 0
     c                   if        (100 * b1s3pr / b1s3gm) - 100 > 999,99
     c                   eval      b1s3en = 999,99
     c                   else
     c                   eval(h)   b1s3en = (100 * b1s3pr / b1s3gm) - 100
     c                   endif
     c                   endif
      *
      * Dekningsgrad (DG)
      *
     c                   if        b1kopr <> 0
     c                   eval(h)   b1dekn = 100 - ((b1kopr * 100) / b1s1pr)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kalkulasjon t.o.m. kostpris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kalk_kopr     begsr
      *
      * Innkjøpspris
      *
     c                   if        b1inpr <> w_inpr_wrk
      *
     c                   if        b1ntpr <> 0
     c                   eval      b1ntpr = b1inpr
     c                   else
     c                   eval(h)   b1ifak = b1inpr / b1nopr
     c                   endif
      *
     c                   else
      *
     c                   if        b1ntpr <> 0
     c                   eval      b1inpr = b1ntpr
     c                   else
6.33 c*                  eval(h)   b1inpr = b1nopr * b1ifak * b1ffak
6.33 c                   eval(h)   b1inpr = b1nopr * b1ifak
     c                   endif
      *
     c                   endif
6.33  *
6.33  * Innkjøpspris 2
6.33 c                   eval(h)   b1inp2 = b1inpr * b1ffak
      *
      *
      * Kostpris
      *
     c                   if        b1kopr <> w_kopr_wrk
6.33 c                   eval(h)   b1kfak = (b1kopr - b1kbel) / b1inp2
     c                   else
6.33 c                   eval(h)   b1kopr = (b1inp2 * b1kfak) + b1kbel
     c                   endif
6.33  *
6.33  * beregner felter for B3 bildet
6.33  *
6.33 c                   eval      b3kop1 = b1kopr
6.33 c                   eval      b3inp1 = b1inpr
6.33 c                   eval      b3in21 = b1inp2
6.33 c                   eval(h)   b3inp2 = b3inp1 * w_ofp2
6.33 c                   eval(h)   b3in22 = b3in21 * w_ofp2
6.33 c                   eval(h)   b3kop2 = b3kop1 * w_ofp2
6.33 c                   eval(h)   b3inp3 = b3inp1 * w_ofp3
6.33 c                   eval(h)   b3in23 = b3in21 * w_ofp3
6.33 c                   eval(h)   b3kop3 = b3kop1 * w_ofp3
6.33 c                   eval      b3enh1 = b1enh1
6.33 c                   eval      b3enh2 = b1enh2
6.33 c                   eval      b3enh3 = b1enh3
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kalkulasjon t.o.m. salgspris inkl.moms
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kalk_saim     begsr
      *
      * Behandling avhengig av hva som er endret
      *
     c                   select
     c                   when      b1sfak <> w_sfak_wrk
     c                   if        jlkkal = 1
     c                   eval(h)   b1s1pr = b1nopr * b1sfak * w_ofp1
     c                   else
     c                   eval(h)   b1s1pr = b1kopr * b1sfak * w_ofp1
     c                   endif
     c                   eval(h)   b1s2pr = b1s1pr * w_ofp2
     c                   eval(h)   b1s3pr = b1s1pr * w_ofp3
     c                   when      b1dekn <> w_dekn_wrk
     c                   eval(h)   b1s1pr = (100 * b1kopr) / (100 - b1dekn)
     c                   if        jlkkal = 1
     c                   eval(h)   b1sfak = b1s1pr / (b1nopr * w_ofp1)
     c                   else
     c                   eval(h)   b1sfak = b1s1pr / (b1kopr * w_ofp1)
     c                   endif
     c                   eval(h)   b1s2pr = b1s1pr * w_ofp2
     c                   eval(h)   b1s3pr = b1s1pr * w_ofp3
     c                   when      b1s1pr <> w_s1pr_wrk
     c                   if        jlkkal = 1
     c                   eval(h)   b1sfak = b1s1pr / (b1nopr * w_ofp1)
     c                   else
     c                   eval(h)   b1sfak = b1s1pr / (b1kopr * w_ofp1)
     c                   endif
     c                   eval(h)   b1s2pr = b1s1pr * w_ofp2
     c                   eval(h)   b1s3pr = b1s1pr * w_ofp3
     c                   when      b1s2pr <> w_s2pr_wrk
     c                   eval(h)   b1s1pr = b1s2pr / w_ofp2
     c                   if        jlkkal = 1
     c                   eval(h)   b1sfak = b1s1pr / (b1nopr * w_ofp1)
     c                   else
     c                   eval(h)   b1sfak = b1s1pr / (b1kopr * w_ofp1)
     c                   endif
     c                   eval(h)   b1s3pr = b1s1pr * w_ofp3
     c                   when      b1s3pr <> w_s3pr_wrk
     c                   eval(h)   b1s1pr = b1s3pr / w_ofp3
     c                   if        jlkkal = 1
     c                   eval(h)   b1sfak = b1s1pr / (b1nopr * w_ofp1)
     c                   else
     c                   eval(h)   b1sfak = b1s1pr / (b1kopr * w_ofp1)
     c                   endif
     c                   eval(h)   b1s2pr = b1s1pr * w_ofp2
     c                   endsl
      *
      * Beregner salgspriser inkl. moms
      *
     c                   eval      b1s1im = b1s1pr * w_mva_fakt
      *
     c                   if        b1s1im <> 0
     c                   eval      w_kode = 4
     c                   call      'FA909R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_kode
     c                   parm                    b1s1im
     c                   endif
      *
     c                   eval      b1s2im = b1s2pr * w_mva_fakt
      *
     c                   if        b1s2im <> 0
     c                   eval      w_kode = 4
     c                   call      'FA909R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_kode
     c                   parm                    b1s2im
     c                   endif
      *
     c                   eval      b1s3im = b1s3pr * w_mva_fakt
      *
     c                   if        b1s3im <> 0
     c                   eval      w_kode = 4
     c                   call      'FA909R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_kode
     c                   parm                    b1s3im
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kalkulasjon av salgspris eksl. moms på bakgrunn av
      * manuelt endret salgspris inkl. moms
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kalk_sapr     begsr
      *
      * Behandling avhengig av hva som er endret
      *
     c                   select
     c                   when      b1s1im <> w_s1im_wrk
     c                   eval(h)   b1s1pr = b1s1im / w_mva_fakt
     c                   eval(h)   b1s2pr = b1s1pr * w_ofp2
     c                   eval(h)   b1s3pr = b1s1pr * w_ofp3
     c                   when      b1s2im <> w_s2im_wrk
     c                   eval(h)   b1s2pr = b1s2im / w_mva_fakt
     c                   eval(h)   b1s1pr = b1s2pr / w_ofp2
     c                   eval(h)   b1s3pr = b1s1pr * w_ofp3
     c                   when      b1s3im <> w_s3im_wrk
     c                   eval(h)   b1s3pr = b1s3im / w_mva_fakt
     c                   eval(h)   b1s1pr = b1s3pr / w_ofp3
     c                   eval(h)   b1s2pr = b1s1pr * w_ofp2
     c                   endsl
      *
      * Beregner ny salgspris-faktor
      *
     c                   if        jlkkal = 1
     c                   eval(h)   b1sfak = b1s1pr / (b1nopr * w_ofp1)
     c                   else
     c                   eval(h)   b1sfak = b1s1pr / (b1kopr * w_ofp1)
     c                   endif
      *
      * Beregner salgspriser inkl. moms
      *
     c                   eval      b1s1im = b1s1pr * w_mva_fakt
      *
     c                   if        b1s1im <> 0
     c                   eval      w_kode = 4
     c                   call      'FA909R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_kode
     c                   parm                    b1s1im
     c                   endif
      *
     c                   eval      b1s2im = b1s2pr * w_mva_fakt
      *
     c                   if        b1s2im <> 0
     c                   eval      w_kode = 4
     c                   call      'FA909R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_kode
     c                   parm                    b1s2im
     c                   endif
      *
     c                   eval      b1s3im = b1s3pr * w_mva_fakt
      *
     c                   if        b1s3im <> 0
     c                   eval      w_kode = 4
     c                   call      'FA909R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_kode
     c                   parm                    b1s3im
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input og gyldige kombinasjoner av
      * endringer i faktor/pris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
      * Leveringstype
      *
     c                   if        b1lety <> 0
     c                   eval      vltpl1_lety = b1lety
     c     vltpl1_key    chain     vltpl1                             39
     c                   if        *in39 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
5.70  *
5.70  * prisgruppe
5.70  *
5.70 c                   if        b1prgr <> *blank
5.70 c                   eval      ra30l1_dkod = b1prgr
5.70 c     ra30l1_key    chain     ra30l1                             47
5.70 c                   if        *in47 = *on
5.70 c                   eval      b_feil = *on
5.70 c                   leavesr
5.70 c                   endif
5.70 c                   endif
      *
     c                   if        b1nopr <= 0
     c                   eval      *in31  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   if        b1enhe = *blank or
     c                             b1enh1 = *blank
     c                   eval      *in32  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   if        b1inpr <> w_inpr_wrk
     c                   if        b1ntpr <> w_ntpr_wrk or
     c                             b1ifak <> w_ifak_wrk
     c                   eval      *in33  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        b1kopr <> w_kopr_wrk
     c                   if        b1kfak <> w_kfak_wrk or
     c                             b1kbel <> w_kbel_wrk
     c                   eval      *in34  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        b1sfak <> w_sfak_wrk and
     c                             b1dekn <> w_dekn_wrk
     c                   eval      *in35  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   if        b1s1pr <> w_s1pr_wrk
     c                   if        b1sfak <> w_sfak_wrk or
     c                             b1dekn <> w_dekn_wrk
     c                   eval      *in36  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine som sjekker om prisene er OK
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_pris    begsr
      *
     c                   eval      b_feil = *off
      *
     c                   if        b1pkod <> ' ' and
     c                             b1pkod <> 'N' and
     c                             b1pkod <> 'B'
     c                   eval      *in40 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   if        b1kopr = 0
     c                   eval      *in41 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   if        b1kopr < b1inpr
     c                   eval      *in42 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      * Sjekker salgspris og enhet for enhet-1
      *
     c                   if        b1enh1 = *blank
     c                   eval      *in43 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   if        b1s1pr = 0
     c                   eval      *in44 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      * Sjekker salgspris og enhet for enhet-2
      *
     c                   if        b1enh2 = *blank and
     c                             b1s2pr <> 0 or
     c                             b1enh2 <> *blank and
     c                             b1s2pr = 0
     c                   eval      *in45 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      * Sjekker salgspris og enhet for enhet-3
      *
     c                   if        b1enh3 = *blank and
     c                             b1s3pr <> 0 or
     c                             b1enh3 <> *blank and
     c                             b1s3pr = 0
     c                   eval      *in46 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av midlertidig pris register
      * Registeret blir kun oppdatert dersom det har vært endring i en av
      * prisene.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb1win        begsr
      *
     c                   eval      b_anul = *off
      *
      * Behandling av bildet
      *
     c     w1tag         tag
      *
     c                   exfmt     b1win
      *
     c                   if        *inkc = *on or
     c                             *inkl = *on
     c                   eval      b_anul = *on
     c                   leavesr
     c                   endif
      *
      * Oppdaterer/oppretter midlertidig pris
      *
     c                   if        b1inpr <> s_inpr or
     c                             b1kopr <> s_kopr or
     c                             b1s1pr <> s_s1pr or
     c                             b1s2pr <> s_s2pr or
     c                             b1s3pr <> s_s3pr
      *
     c     jwprlu_key    chain     jwprlur
      *
     c                   eval      jbinpr = b1inpr
     c                   eval      jbkopr = b1kopr
     c                   eval      jbs1pr = b1s1pr
     c                   eval      jbs2pr = b1s2pr
     c                   eval      jbs3pr = b1s3pr
      *
     c                   if        %found(jwprlu)
     c                   update    jwprlur
     c                   else
     c                   eval      jbfirm = p_firm
     c                   eval      jbvare = p_vare
     c                   write     jwprlur
     c                   endif
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering/oppretting av betingelse og påslags-
      * faktorer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb2win        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   eval      b_endr_bet = *off
     c                   eval      *in30      = *off
     c                   eval      bwbeti     = *blank
      *
     c                   if        b1ifak <> s_ifak or
     c                             b1ntpr <> s_ntpr or
     c                             w_midl = 1
     c                   eval      b_endr_bet = *on
     c                   eval      *in30      = *on
     c                   eval      bwbeti     = b1bbet
     c                   endif
      *
      * Finner omregningsfaktor volum for enhet-1
      *
6.30 c                   eval      jvpkl2_ldor = p_pgiv
     c                   eval      jvpkl2_enhe = b1enh1
6.32 c     jvpkl2_key    setll     jvpkl2
6.32 c     jvpkl2_key    reade     jvpkl2
6.32 c                   dow       not %eof
6.32  *
6.32  * sjekker fra / tildato
6.32 c                   exsr      sjekk_pkdato
6.32 c                   if        b_pakn = *on
6.32 c                   eval      w_ofak = jwofak
6.32 c                   endif
6.32  *
6.32 c     jvpkl2_key    reade     jvpkl2
6.32 c                   enddo
      *
     c                   if        w_ofak = 0
     c                   eval      w_ofak = 1
     c                   endif
      *
      * Behandling av bildet
      *
     c     w2tag         tag
      *
     c                   exfmt     b2win
      *
     c                   if        *inkc = *on or
     c                             *inkl = *on
     c                   eval      b_anul = *on
     c                   leavesr
     c                   endif
      *
      * Oppdatering av vare-detaljer
      *
     c                   if        b1pkod <> s_pkod or
     c                             b1skod <> s_skod
     c                   exsr      oppd_detaljer
     c                   endif
      *
      * Oppdatering av endrede betingelser
      *
     c                   if        b_endr_bet = *on
      *
     c                   if        bwbeti = *blank
     c                   eval      *in31 = *on
     c                   goto      w2tag
     c                   endif
      *
     c                   exsr      oppd_beti
      *
     c                   endif
      *
      * Oppdatering av endrede påslag
      *
     c                   if        b1kfak <> s_kfak or
     c                             b1kbel <> s_kbel or
     c                             b1sfak <> s_sfak or
     c                             w_midl = 1
     c                   exsr      oppd_fakt
     c                   endif
      *
      * Fjerner midlertidig pris fra varen
      *
     c                   if        w_midl = 1
     c     jwprlu_key    chain     jwprlur
     c                   if        %found
     c                   delete    jwprlur
     c                   endif
     c                   endif
      *
     c                   endsr
      *
6.33  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.33  * Subrutine for popup med kostpris og innkjøpspris
6.33  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.33  *
6.33 c     xb3win        begsr
6.33  *
6.33  *
6.33  * Behandling av bildet
6.33  *
6.33 c     w3tag         tag
6.33  *
6.33 c                   exfmt     b3win
6.33  *
6.33 c                   if        *inkc = *on or
6.33 c                             *inkl = *on
6.33 c                   eval      b_anul = *on
6.33 c                   leavesr
6.33 c                   endif
6.33  *
6.33 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av varedetaljer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_detaljer begsr
      *
     c                   clear                   jvdtlur
      *
     c     jvdtlu_key    chain     jvdtlur
      *
     c                   eval      jvdpko = b1pkod
     c                   eval      jvdsko = b1skod
      *
     c                   if        %found
     c                   update    jvdtlur
     c                   else
     c                   eval      jvdfir = w_firm
     c                   eval      jvdvar = w_vare
     c                   eval      jvdhko = 1
     c                   write     jvdtlur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting/oppdatering av betingelse på varenivå
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_beti     begsr
      *
      * Oppretter/endrer betingelse for vare dersom endring av betingelse.
      * Dersom eksisterende betingelse finnes på varenivå, og er lagt inn
      * lokalt, endres denne. Ellers opprettes betingelsen.
      *
      * Behandling dersom eksisterende betingelse på varenivå.
      * Dersom betingelsen er lagt inn lokalt, endres faktorene, ellers
      * passiviseres betingelsen. Aktiv betingelse skal være den nye
      * betingelsen som opprettes.
      *
     c                   eval      b_bet_oppd = *off
      *
      * Eksisterende betingelse
      *
     c                   if        b1bvar <> *blank
      *
     c                   eval      jbetlu_ldor = b1bldo
6.3x c                   eval      jbetlu_prgr = b1prgr
     c                   eval      jbetlu_ogrp = b1bogr
     c                   eval      jbetlu_hgrp = b1bhgr
     c                   eval      jbetlu_ugrp = b1bugr
     c                   eval      jbetlu_modn = b1bmod
     c                   eval      jbetlu_vare = b1bvar
     c                   eval      jbetlu_vtyp = w_vtyp
     c                   eval      jbetlu_lety = b1lety
     c     jbetlu_key    setll     jbetlu
     c     jbetlu_key    reade     jbetlur
     c                   dow       not %eof
      *
     c                   if        jprsts = 1 and
     c                             jpakti = 1 and
     c                             jpbeti = b1bbet
     c                   eval      jpbeti = bwbeti
     c                   update    jbetlur
     c                   exsr      oppd_element
     c                   eval      b_bet_oppd = *on
     c                   else
     c                   eval      jpakti = 0
     c                   update    jbetlur
     c                   endif
      *
     c     jbetlu_key    reade     jbetlur
     c                   enddo
      *
     c                   endif
      *
     c                   if        b_bet_oppd = *on
     c                   leavesr
     c                   endif
      *
      * Ny betingelse
      *
     c                   clear                   jbetlur
      *
     c                   eval      jpfirm = w_firm
     c                   eval      jpldor = b1ldor
6.3x c                   eval      jpprgr = b1prgr
     c                   eval      jpogrp = jvogrp
     c                   eval      jphgrp = jvhgrp
     c                   eval      jpugrp = jvugrp
6.37 c                   if        w_lv_vare <> *blank
6.39 c                   movel     w_lv_nobb     jpvare
6.39 c                   eval      jpmodn = w_lv_modn
6.37 c                   else
     c                   eval      jpmodn = jvmodn
     c                   eval      jpvare = jvvare
6.37 c                   endif
     c                   eval      jpvtyp = w_vtyp
     c                   eval      jplety = b1lety
     c                   eval      jprsts = 1
     c                   eval      jpline = 1
     c                   eval      jpbeti = bwbeti
     c                   eval      jpakti = 1
     c                   eval      jpeusr = l_user
      *
     c                   time                    jpfdat
     c                   time                    jpodat
     c                   time                    jpedat
     c                   time                    jpetim
      *
     c                   write     jbetlur
      *
     c                   exsr      oppd_element
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av rabattelement
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_element  begsr
      *
      * Sletter eventuelt eksisterende rabattelement
      *
     c                   eval      jraelu_eldo = jpldor
6.3x c                   eval      jraelu_eprg = jpprgr
     c                   eval      jraelu_eogr = jpogrp
     c                   eval      jraelu_ehgr = jphgrp
     c                   eval      jraelu_eugr = jpugrp
     c                   eval      jraelu_emod = jpmodn
     c                   eval      jraelu_evar = jpvare
     c                   eval      jraelu_evty = jpvtyp
     c                   eval      jraelu_elet = jplety
     c                   eval      jraelu_ebrs = jprsts
     c                   eval      jraelu_ebli = jpline
     c     jraelu_key    setll     jraelu
     c     jraelu_key    reade     jraelur
     c                   dow       not %eof
     c                   delete    jraelur
     c     jraelu_key    reade     jraelur
     c                   enddo
      *
      * Oppretter rabattelemet for betingelse
      *
     c                   eval      jpefir = w_firm
     c                   eval      jpeldo = jpldor
6.3x c                   eval      jpeprg = b1prgr
     c                   eval      jpeogr = jpogrp
     c                   eval      jpehgr = jphgrp
     c                   eval      jpeugr = jpugrp
     c                   eval      jpemod = jpmodn
     c                   eval      jpevar = jpvare
     c                   eval      jpevty = jpvtyp
     c                   eval      jpelet = jplety
     c                   eval      jpebrs = jprsts
     c                   eval      jpebli = jpline
      *
     c                   eval      jperst = 1
     c                   eval      jpelin = 1
     c                   eval      jpeakt = 0
     c                   eval      jperae = jpbeti
     c                   eval      jpefda = jpfdat
     c                   eval      jpetda = jptdat
     c                   eval      jpeoda = jpodat
     c                   eval      jpeeda = jpedat
     c                   eval      jpeeti = jpetim
     c                   eval      jpeeus = jpeusr
      *
     c                   if        b1ntpr <> 0
     c                   eval      jpereg = 'N'
     c                   eval(h)   jpever = b1ntpr / w_ofak
     c                   else
     c                   eval      jpereg = '1'
     c                   eval      jpever = (1 - b1ifak) * 100
     c                   endif
      *
     c                   write     jraelur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting/oppdatering av påslag på varenivå
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_fakt     begsr
      *
      * Regner kostpris påslagsbeløp om til prisenhet
      *
     c                   eval(h)   w_kbel = b1kbel / w_ofak
      *
      * Dersom eksisterende faktorer er hentet fra vare-nivå og hvor
      * kalkylens varetype og leveringstype er lik den valgte, oppdateres
      * kalkylen på varen.
      *
     c                   if        b1pvar <> *blank
      *
5.70 c                   eval      jpfalu_prgr = b1prgr
     c                   eval      jpfalu_ogrp = b1pogr
     c                   eval      jpfalu_hgrp = b1phgr
     c                   eval      jpfalu_ugrp = b1pugr
     c                   eval      jpfalu_ldor = b1pldo
     c                   eval      jpfalu_modn = b1pmod
     c                   eval      jpfalu_vare = b1pvar
     c                   eval      jpfalu_kpri = b1ppko
     c                   eval      jpfalu_vtyp = w_vtyp
     c                   eval      jpfalu_lety = b1lety
     c     jpfalu_key    chain     jpfalur
     c                   if        %found
     c                   eval      jckofa = b1kfak
     c                   eval      jckobe = w_kbel
     c                   eval      jcsafa = b1sfak
     c                   eval      jceusr = l_user
     c                   time                    jcedat
     c                   time                    jcetim
     c                   update    jpfalur
     c                   leavesr
     c                   endif
      *
     c                   endif
      *
      * Oppretter påslagsfaktorer på varenivå
      *
5.70 c                   eval      jpfalu_prgr = b1prgr
     c                   eval      jpfalu_ogrp = jvogrp
     c                   eval      jpfalu_hgrp = jvhgrp
     c                   eval      jpfalu_ugrp = jvugrp
     c                   eval      jpfalu_ldor = b1ldor
6.37 c                   if        w_lv_vare <> *blank
6.38 c                   movel     w_lv_nobb     jpfalu_vare
6.38 c                   eval      jpfalu_modn = w_lv_modn
6.37 c                   else
     c                   eval      jpfalu_modn = jvmodn
     c                   eval      jpfalu_vare = jvvare
6.37 c                   endif
     c                   eval      jpfalu_kpri = b1pkod
     c                   eval      jpfalu_vtyp = w_vtyp
     c                   eval      jpfalu_lety = b1lety
     c     jpfalu_key    chain     jpfalur
      *
     c                   eval      jckofa = b1kfak
     c                   eval      jckobe = w_kbel
     c                   eval      jcsafa = b1sfak
     c                   eval      jceusr = l_user
      *
     c                   time                    jcedat
     c                   time                    jcetim
      *
     c                   if        %found(jpfalu)
     c                   update    jpfalur
     c                   else
     c                   eval      jcfirm = w_firm
5.70 c                   eval      jcprgr = b1prgr
     c                   eval      jcogrp = jvogrp
     c                   eval      jchgrp = jvhgrp
     c                   eval      jcugrp = jvugrp
     c                   eval      jcldor = b1ldor
6.37 c                   if        w_lv_vare <> *blank
6.39 c                   movel     w_lv_nobb     jcvare
6.39 c                   eval      jcmodn = w_lv_modn
6.37 c                   else
     c                   eval      jcmodn = jvmodn
     c                   eval      jcvare = jvvare
6.37 c                   endif
     c                   eval      jckpri = b1pkod
     c                   eval      jcvtyp = w_vtyp
     c                   eval      jclety = b1lety
     c                   time                    jcodat
     c                   write     jpfalur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ERROR-rutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *pssr         begsr
      *
     c                   eval      *in49 = *on
     c                   goto      b1tagc
      *
     c                   endsr
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  * Subrutine for å sjekke om dato er innenfor fra /til dato
6.32  * hvis *loval i begge datoer = ok
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  *
6.32 c     sjekk_pkdato  begsr
6.32  *
6.32 c                   eval      b_pakn = *on
6.32  *
6.32 c                   if        jwfdat = *loval and
6.32 c                             jwtdat = *loval
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.36 c                   if        jwfdat <= w_udat and
6.36 c                             jwtdat >  w_udat
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   if        jwfdat = *loval  and
6.36 c                             jwtdat >  w_udat
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.36 c                   if        jwfdat <= w_udat and
6.32 c                             jwtdat =  *loval
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   eval      b_pakn = *off
6.32  *
6.32 c                   endsr
6.32  *
6.35  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.35  * Vis NOBB informasjon for varen. Samme prinsipp som preview av utskrifter
6.35  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.35  *
6.35 c     vis_nobb      begsr
6.35 c
6.35 c                   eval      p_nobb = %char(b1nobb)
6.35 c                   call      'AP633R'
6.35 c                   parm                    p_nobb
6.35  *
6.35 c                   endsr
6.37  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.37  * Finn info om logistikkvare for riktig prisgiver
6.37  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.37  *
6.37 c     finn_logivar  begsr
6.37  *
6.37 c                   eval      b_logv = *off
6.37 c                   eval      w_lv_vare = *blank
6.37 c                   eval      w_lv_kldo = 0
6.37 c                   eval      w_lv_nobb = 0
6.37 c                   eval      w_lv_ofak = 0
6.37 c                   eval      w_lv_modn = 0
6.37  *
6.37 c/exec sql
6.37 c+ select a.jvvare,
6.37 c+        b.jvkldo,
6.37 c+        c.jvquno,
6.37 c+        c.jvqofa,
6.37 c+    (select d.jvmodn from jvarpf d where d.jvnobb = c.jvquno)
6.37 c+ into   :w_lv_vare,
6.37 c+        :w_lv_kldo,
6.37 c+        :w_lv_nobb,
6.37 c+        :w_lv_ofak,
6.37 c+        :w_lv_modn
6.37 c+ from   jvarpf a
6.37 c+ inner join jvkhpf b on jvkvnr = jvvare and jvkfir = :w_firm
6.37 c+ inner join jvklpf c on jvqvnr = jvvare and jvqfir = :w_firm
6.37 c+ where  jvqvnr = :jvvare
6.37 c+    and jvqldo = :p_pgiv fetch first 1 rows only
6.37 c/end-exec
6.37  *
6.37 c                   if        sqlcod = 100 or sqlstt = '02000'
6.37 c                   leavesr
6.37 c                   endif
6.37 c                   if        sqlcod <> 0
6.37 c                   leavesr
6.37 c                   endif
6.37  *
6.37 c                   eval      b_logv = *on
6.37  *
6.37 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_pgiv
     c                   parm                    p_lety
     c                   parm                    p_skaf
5.70 c                   parm                    p_prgr
6.21 c                   parm                    p_hlev
6.36 c                   parm                    p_edat
6.36 c                   parm                    p_pdat
6.34 c                   parm                    p_utgp
      *
      * Key for oppsalg på vare
      *
     c     jvarl1_key    klist
     c                   kfld                    w_vare
      *
      * Key for oppsalg på vare-detalj
      *
     c     jvdtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for oppsalg på vare-pakning
      *
     c     jvpkl2_key    klist
     c                   kfld                    w_vare
6.30 c                   kfld                    jvpkl2_ldor
     c                   kfld                    jvpkl2_enhe
      *
      * Key for oppdatering av midlertidig pris
      *
     c     jwprlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
      *
      * Key for oppdatering av vare-detalj
      *
     c     jvdtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
      *
      * Key for oppdatering av betingelser
      *
     c     jbetlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jbetlu_ldor
6.3x c                   kfld                    jbetlu_prgr
     c                   kfld                    jbetlu_ogrp
     c                   kfld                    jbetlu_hgrp
     c                   kfld                    jbetlu_ugrp
     c                   kfld                    jbetlu_modn
     c                   kfld                    jbetlu_vare
     c                   kfld                    jbetlu_vtyp
     c                   kfld                    jbetlu_lety
      *
      * Key for oppdatering av rabattelementer
      *
     c     jraelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jraelu_eldo
6.3x c                   kfld                    jraelu_eprg
     c                   kfld                    jraelu_eogr
     c                   kfld                    jraelu_ehgr
     c                   kfld                    jraelu_eugr
     c                   kfld                    jraelu_emod
     c                   kfld                    jraelu_evar
     c                   kfld                    jraelu_evty
     c                   kfld                    jraelu_elet
     c                   kfld                    jraelu_ebrs
     c                   kfld                    jraelu_ebli
      *
      * Key for oppdatering av påslagsfaktor
      *
     c     jpfalu_key    klist
     c                   kfld                    w_firm
5.70 c                   kfld                    jpfalu_prgr
     c                   kfld                    jpfalu_ogrp
     c                   kfld                    jpfalu_hgrp
     c                   kfld                    jpfalu_ugrp
     c                   kfld                    jpfalu_ldor
     c                   kfld                    jpfalu_modn
     c                   kfld                    jpfalu_vare
     c                   kfld                    jpfalu_kpri
     c                   kfld                    jpfalu_vtyp
     c                   kfld                    jpfalu_lety
      *
      * Key for oppslag på leveringstype
      *
     c     vltpl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltpl1_lety
5.70  *
5.70  * Key for oppslag på prisgrupe
5.70  *
5.70 c     ra30l1_key    klist
5.70 c                   kfld                    w_firm
5.70 c                   kfld                    ra30l1_dkod
      *
      *
      * Henter firma og vare fra parmeter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_vare = p_vare
      *
     c                   eval      w_mva_fakt = 1,25
      *
      * Henter current dato og klokkelsett
      *
     c                   time                    w_udat
     c                   time                    w_utim
      *
     c                   endsr
