     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RL108R
      * Beskrivelse . . : Flytting av leverandører og posteringer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       130802 KOB  Nytt program
T5.30 * v5.30 140704 HFL  Firmanavn i skjermbildet
T5.40 * v5.40 020205 HFL  Sjekker om notatblokk.
T5.30 * V5.40 140705 KOB  Lagt inn muligheter til å vise bare dubletter
6.10  * V6.10 230909 BSA  Lagt inn sporingsfelter
6.11  * V6.10 070510 BSA  Endret i sporingsfelter
6.30  * V6.30 120517 bof  Sjekk av alle tabeller som skal kopieres
7.00  *K00    220321 sst  Sperre denne rutina via NX
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     frlevl2    if   e           k disk    rename(rlevpfr:rlevl2r)
     frlevlu    uf a e           k disk    rename(rlevpfr:rlevlur)
     frlbalu    uf a e           k disk    rename(rlbapfr:rlbalur)
     frlsolu    uf   e           k disk    rename(rlsopfr:rlsolur)
     frltrlu    uf a e           k disk    rename(rltrpfr:rltrlur)
T5.40frklalu    uf   e           k disk    rename(rklapfr:rklalur)
      *
     frl108d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av array
      *
     d a_meld          s             11    dim(3) ctdata perrcd(1)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d rlevl1_levr     s                   like(rllevr)
     d rlevl2_alfa     s                   like(rlalfa)
     d rlevlu_levr     s                   like(rllevr)
     d rlbalu_levr     s                   like(rvlevr)
     d rlsolu_slev     s                   like(rlslev)
     d rltrlu_levr     s                   like(rolevr)
     d rltrlu_bdat     s                   like(robdat)
     d rltrlu_tist     s                   like(rotist)
      *
T5.40d rklalu_syst     s                   like(rxsyst)
T5.40d rklalu_pros     s                   like(rxpros)
T5.40d rklalu_akti     s                   like(rxakti)
T5.40d rklalu_levr     s                   like(rxkont)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(rlfirm)
     d w_levr_gml      s                   like(rllevr)
     d w_sald_gml      s                   like(rlsald)
     d w_kjop_gml      s                   like(rlkjØp)
     d w_kjof_gml      s                   like(rlkjØf)
      *
     d w_tmst          s               z
     d w_tmst_20       s             20
     d w_tell          s              6  0
     d w_tell_alf      s              6
      *
     d ant             s              2  0
     d w_ikke          s              1
T5.00d w_navk          s              1
T5.00d w_gatk          s              1
T5.00d w_ponk          s              1
T5.00d w_tlfk          s              1
T5.00d w_mobk          s              1
T5.00d xsrobk          s              1
6.30 d w_mld1          s             50
6.30 d w_stat          s              1
      *
     d w_levr          s                   like(rllevr)
     d w_alfa          s                   like(rlalfa)
     d w_navn          s                   like(rlnavn)
     d w_gate          s                   like(rlgate)
     d w_sted          s                   like(rlsted)
     d w_ponr          s                   like(rlponr)
     d w_tlfn          s                   like(rltlfn)
     d w_tlfx          s                   like(rltlfx)
7.00 d w_sp            s              1
7.00 d w_rut           s             50
7.00 d w_l             s              3  0
7.00 d w_p             s              3  0
7.00 d w_m1            s             30
7.00 d w_m2            s             30
      *
     d p_valg          s              1  0
     d p_levr          s                   like(rllevr)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(13)
      *
      /eject
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_srrn - Relativt recordnummer i subfile
      * w_spge - Sidestørrelse i subfile (page)
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * w_virn - Vis den side som inneholder denne record.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * K1WIN  - K1 Vindu for kopiere
      * S1WIN  - S1 Standardvindu som inneholder definisjon av
      *             størrelse og utseende.  Bilde K1WIN
      *             er referert til dette.
      * V1WIN  - V1 Vindu for utvalg av dubletter
      *
7.00  * ___________________________________________________________________
7.00  *  Sjekk om filgruppa er sperret for bruk av dette programmet
7.00  *
7.00 c                   eval      w_rut = 'SPERRE_FLYTTING_KUND/LEV'
7.00 c                   call      'TESTSPERRE'
7.00 c                   parm                    w_sp
7.00 c                   parm                    w_rut
7.00 c                   if        w_sp = '1'
7.00 c                   eval      w_m1 = 'Denne rutina er sperret for'
7.00 c                   eval      w_m2 = 'denne filgruppa, Trykk Ok'
7.00 c                   eval      w_l  = 5
7.00 c                   eval      w_p  = 30
7.00 c                   call      'AA011C'
7.00 c                   parm                    w_l
7.00 c                   parm                    w_p
7.00 c                   parm                    w_m1
7.00 c                   parm                    w_m2
7.00 c                   goto      avslutt
7.00 c                   endif
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * F11=Bytter på å vise informasjon i subfien
      * skifter mellom navn/gateadresse og Poststed/Telefon/Fax
      *
     c                   if        *inkk = *on
     c                   if        *in80 = *on
     c                   eval      *in80 = *off
T5.30c                   else
     c                   eval      *in80 = *on
T5.30c                   endif
     c                   exsr      forny
     c                   goto      b2taga
     c                   endif
      *
      * F7=Bytter på å vise alle eller bare leverandører med dubletter
      *
     c                   if        *inkg = *on
      *
     c                   write     s1win
      *
     c                   exfmt     v1win
     c                   eval      w_navk = v1navn
     c                   eval      w_gatk = v1gate
     c                   eval      w_ponk = v1ponr
     c                   eval      w_tlfk = v1tlfn
     c                   eval      w_mobk = v1mobn
      *
     c                   if        *in81 = *on
     c                   eval      *in81 = *off
T5.30c                   else
     c                   eval      *in81 = *on
T5.30c                   endif
     c                   exsr      forny
     c                   goto      b2taga
     c                   endif
      *
      * Posisjonering
      *
     c                   if        b2levr <> 0 or
     c                             b2alfa <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   select
     c                   when      w_seqe = 'B'
     c     rlevl2_key    setll     rlevl2
     c                   other
     c     rlevl1_key    setll     rlevl1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på leverandørnummer
      *
     c                   if        b2levr <> 0
     c                   eval      w_seqe = ' '
     c                   eval      rlevl1_levr = b2levr
     c     rlevl1_key    setll     rlevl1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på leverandørnavn
      *
     c                   if        b2alfa <> *blank
     c                   eval      w_seqe = 'B'
     c                   eval      rlevl2_alfa = b2alfa
     c     rlevl2_key    setll     rlevl2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2levr = 0
     c                   eval      b2alfa = *blank
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
      * Beholder eksisterende posisjonering i subfilen
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      w_virn = w_srrn
      *
      * Endre post
      *
     c                   if        b1valg = 2
     c                   eval      p_valg = 2
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vise post
      *
     c                   if        b1valg = 5
     c                   eval      p_valg = 5
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Flytte post
      *
     c                   if        b1valg = 7
     c                   eval      k1levr = b1levr
T5.40c                   exsr      sjekk_no
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
T5.40 * Subrutine for sjekk om det finnes notatblokk på leverandøren
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
T5.40c     sjekk_no      begsr
      *
T5.40c                   eval      b_anul = *off
T5.40c                   eval      *in79 = *off
      *
T5.40 * Finnes notatblokk på leverandøren?
      *
T5.40c                   eval      rklalu_syst = 'L'                                         .
T5.40c                   eval      rklalu_pros = *zeros                                      .
T5.40c                   eval      rklalu_akti = *blanks                                     .
T5.40c                   eval      rklalu_levr = k1levr                                      .
T5.40c     rklalu_key    setll     rklalu                                 94
      *
T5.40c                   if        *in94 = *on
T5.40c                   eval      *in79 = *on                                               .
T5.40c                   endif                                                           .....
      *
T5.40c                   if        *in79 = *on
T5.40c                   exfmt     c2msg
T5.40c                   endif
      *
T5.40 * Behandling av funksjonstaster
      *
T5.40c                   if        *inkc or *inkl
T5.40c                   eval      b_anul = *on
T5.40c                   goto      end_sjekkno
T5.40c                   endif
      *
T5.40c     end_sjekkno   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
      * Vedlikehold av leverandørregisteret
      *
     c                   if        b1valg = 2 or
     c                             b1valg = 5
      *
     c                   eval      p_levr = b1levr
     c                   call      'RL101R'
     c                   parm                    p_levr
     c                   parm                    p_valg
      *
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2 or
     c                             b1valg = 7
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      w_levr_gml = k1levr
      *
      * Fyller kopierings-bildet
      *
     c                   eval      rlevl1_levr = k1levr
     c     rlevl1_key    chain     rlevl1                             90
      *
      * Ta vare på saldi m.v. fra gammel leverandør
      *
     c                   eval      w_sald_gml = rlsald
     c                   eval      w_kjop_gml = rlkjØp
     c                   eval      w_kjof_gml = rlkjØf
      *
     c                   write     s1win
      *
     c     k1tagb        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
6.30  *
6.30  * Rutine som sjekker alle tabeller som blir behandlet
6.30  *
6.30 c                   call      'RL109R'
6.30 c                   parm                    w_firm
6.30 c                   parm                    w_levr_gml
6.30 c                   parm                    k1levr
6.30 c                   parm                    w_stat
6.30 c                   if        w_stat = '1'
6.30 c                   eval      w_mld1 = 'Leverandør kan ikke b+
6.30 c                                       yttes.Finnes fra før +
6.30 c                                       i tabell.'
6.30 c                   call      'AA005R'
6.30 c                   parm                    w_mld1
6.30 c                   goto      k1tagb
6.30 c                   endif
      *
     c                   if        *inke
     c                   goto      k1tagc
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1levr = 0
     c                   eval      *in31 = *on
     c                   goto      k1tagb
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      rlevl1_levr = k1levr
     c     rlevl1_key    chain     rlevl1                             90
     c                   if        *in90 = *off
     c                   eval      *in32 = *on
     c                   goto      k1tagb
     c                   endif
      *
      * Kopier til nytt leverandørnummer
      *
     c                   eval      rllevr = k1levr
6.10 c                   time                    rlndat
6.10 c                   time                    rlntim
6.10 c                   eval      rlnsig = l_user
6.10 c                   time                    rledat
6.10 c                   time                    rletim
6.10 c                   eval      rlesig = l_user
6.11 c                   time                    rl2dpt
6.11 c                   eval      rlepgm = 'RL108R    '
     c                   write     rlevlur
      *
      * Flytt bankinformasjon (kun hvis ny leverandør)
      *
     c                   exsr      xk2bank
      *
      * Oppdater søketekst
      *
     c                   call      'RL750R'
     c                   parm                    w_firm
     c                   parm                    k1levr
      *
     c                   goto      k1tagd
      *
      * Kopiering til eksisterende leverandør
      *
     c     k1tagc        tag
      *
     c                   eval      rlevlu_levr = k1levr
     c     rlevlu_key    chain     rlevlu                             90
     c                   if        *in90 = *off
     c                   eval      rlsald = rlsald + w_sald_gml
     c                   eval      rlkjØp = rlkjØp + w_kjop_gml
     c                   eval      rlkjØf = rlkjØf + w_kjof_gml
6.10 c                   time                    rledat
6.10 c                   time                    rletim
6.10 c                   eval      rlesig = l_user
6.11 c                   time                    rl2dpt
6.11 c                   eval      rlepgm = 'RL108R    '
     c                   update    rlevlur
     c                   endif
      *
     c     k1tagd        tag
      *
      * Flytt poster i andre delsystemer
      *
     c                   call      'RL791C'
     c                   parm                    w_firm
     c                   parm                    w_levr_gml
     c                   parm                    k1levr
      *
      * Flytt transaksjoner
      *
     c                   exsr      xk2tra
      *
      * Slett gammel leverandør med søketekst
      *
     c                   eval      rlevlu_levr = w_levr_gml
     c     rlevlu_key    chain     rlevlu                             90
     c                   if        *in90 = *off
      * Søketekst
     c                   eval      rlsolu_slev = w_levr_gml
     c     rlsolu_key    chain     rlsolu                             91
     c                   if        *in91 = *off
     c                   delete    rlsolur
     c                   endif
      *
T5.40 * Notatblokk
      *
T5.40c                   eval      rklalu_syst = 'L'                                         .
T5.40c                   eval      rklalu_pros = *zeros                                      .
T5.40c                   eval      rklalu_akti = *blanks                                     .
T5.40c                   eval      rklalu_levr = w_levr_gml                                  .
T5.40c     rklalu_key    setll     rklalu                                 94
      *
T5.40c                   if        *in94 = *on
T5.40c     rklalu_key    reade     rklalu                                 95
T5.40c                   dow       *in95 = *off
T5.40c                   delete    rklalur
T5.40c     rklalu_key    reade     rklalu                                 95
T5.40c                   enddo                                                           .....
T5.40c                   endif
      *
      * Sletter leverandøren
      *
     c                   delete    rlevlur
     c                   endif
      *
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle posteringer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk2tra        begsr
      *
      * Leser leverandørposteringer
      *
     c                   eval      rltrlu_levr = w_levr_gml
     c     rltrlu_key    setll     rltrlu
     c     rltrlu_key    reade     rltrlu                                 91
     c                   eval      w_tell = *zeros
      *
     c                   dow       *in91 = *off
      *
      * Teller/Timestamp
      *
     c                   if        w_tell = 999999
     c                   eval      w_tell = 0
     c                   endif
      *
     c                   eval      w_tell = w_tell + 1
     c                   move      w_tell        w_tell_alf
     c                   time                    rotist
     c     *iso0         move      rotist        w_tmst_20
     c                   move      w_tell_alf    w_tmst_20
     c     *iso0         move      w_tmst_20     rotist
      *
     c                   eval      rolevr = k1levr
      *
6.10 c                   time                    roedat
6.10 c                   time                    roetim
6.10 c                   eval      roeusr = l_user
     c                   update    rltrlur
      *
     c     rltrlu_key    reade     rltrlu                                 91
      *
     c                   enddo
      *
     c     end_k2tra     endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bankinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk2bank       begsr
      *
      * Flytt bankinformasjon til ny leverandør
      *
     c                   eval      rlbalu_levr = w_levr_gml
     c     rlbalu_key    chain     rlbalu                             92
      *
     c                   if        *in92 = *off
      *
     c                   delete    rlbalur
     c                   eval      rvlevr = k1levr
6.10 c                   time                    rvodat
6.10 c                   time                    rvotim
6.10 c                   eval      rvousr = l_user
6.10 c                   time                    rvedat
6.10 c                   time                    rvetim
6.10 c                   eval      rveusr = l_user
     c                   write     rlbalur
      *
     c                   endif
      *
     c     end_k2bank    endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      rlevl2                                 15
     c                   other
     c                   read      rlevl1                                 15
     c                   endsl
      *
     c                   if        *in15
     c                   goto      end_crt
     c                   endif
      *
     c                   if        rlfirm <> w_firm
     c                   eval      *in15 = *on
     c                   goto      end_crt
     c                   endif
      *
     c   81              do
     c                   if        rlalfa = w_alfa
     c                   exsr      xsrkont
      *
     c                   if        w_ikke = ' '
     c                   eval      ant = ant + 1
     c                   else
     c                   eval      ant = 1
     c                   endif
      *
     c                   else
      *
     c                   eval      ant = 1
     c                   endif
      *
     c                   select
     c                   when      ant = 1
     c                   eval      w_levr = rllevr
     c                   eval      w_navn = rlnavn
     c                   eval      w_gate = rlgate
     c                   eval      w_sted = rlsted
     c                   eval      w_ponr = rlponr
     c                   eval      w_tlfn = rltlfn
     c                   eval      w_tlfx = rltlfx
      *
     c                   when      ant = 2
     c                   eval      w_srrn = w_srrn + 1
     c                   eval      b1alfa = w_alfa
     c                   eval      b1levr = w_levr
     c  n80              eval      b1navn = w_navn
     c   80              eval      b1navn = w_gate
     c  n80              eval      b1sted = w_sted
     c   80              eval      b1sted = %trim(w_tlfn)+'  '+%trim(w_tlfx)
     c                   write     b1sfl
      *
     c                   eval      w_srrn = w_srrn + 1
     c                   eval      b1alfa = rlalfa
     c                   eval      b1levr = rllevr
     c  n80              eval      b1navn = rlnavn
     c   80              eval      b1navn = rlgate
     c  n80              eval      b1sted = rlsted
     c   80              eval      b1sted = %trim(rltlfn)+'  '+%trim(rltlfx)
     c                   write     b1sfl
      *
     c                   when      ant > 2
     c                   eval      w_srrn = w_srrn + 1
     c                   eval      b1alfa = rlalfa
     c                   eval      b1levr = rllevr
     c  n80              eval      b1navn = rlnavn
     c   80              eval      b1navn = rlgate
     c  n80              eval      b1sted = rlsted
     c   80              eval      b1sted = %trim(rltlfn)+'  '+%trim(rltlfx)
     c                   write     b1sfl
     c                   endsl
      *
     c                   eval      w_alfa = rlalfa
     c                   enddo
      *
     c  n81              do
     c                   eval      w_srrn = w_srrn + 1
     c                   eval      b1valg = 0
     c                   eval      b1alfa = rlalfa
     c                   eval      b1levr = rllevr
     c  n80              eval      b1navn = rlnavn
     c   80              eval      b1navn = rlgate
     c  n80              eval      b1sted = rlsted
     c   80              eval      b1sted = %trim(rltlfn)+'  '+%trim(rltlfx)
      *
     c                   write     b1sfl
     c                   enddo
      *
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   select
     c                   when      w_seqe = 'A' and
     c                             %eof(rlevl2)
     c     rlevl2_key    setgt     rlevl2
     c                   readp     rlevl2
     c                   when      w_seqe = *blank and
     c                             %eof(rlevl1)
     c     rlevl1_key    setgt     rlevl1
     c                   readp     rlevl1
     c                   endsl
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'A'
     c                   readp     rlevl2
     c                   other
     c                   readp     rlevl1
     c                   endsl
      *
     c                   if        %eof or
     c                             rlfirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'A'
     c                   eval      rlevl2_alfa = rlalfa
     c                   other
     c                   eval      rlevl1_levr = rllevr
     c                   endsl
      *
     c                   enddo
      *
     c                   select
     c                   when      w_seqe = 'A' and
     c                             %eof(rlevl2)
     c     rlevl2_key    setll     rlevl2
     c                   when      w_seqe = *blank and
     c                             %eof(rlevl1)
     c     rlevl1_key    setll     rlevl1
     c                   endsl
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
     c                   eval      *in22 = *off
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kontroll
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xsrkont       begsr
      *
     c                   eval      w_ikke = ' '
      *
      * Sjekk navn
      *
     c                   if        w_navk = 'J' and
     c                             rlnavn <> w_navn
     c                   eval      w_ikke = 'J'
     c                   endif
      *
      * Sjekk gateadresse
      *
     c                   if        w_gatk = 'J' and
     c                             rlgate <> w_gate
     c                   eval      w_ikke = 'J'
     c                   endif
      *
      * Sjekk postnummer
      *
     c                   if        w_ponk = 'J' and
     c                             rlponr <> w_ponr
     c                   eval      w_ikke = 'J'
     c                   endif
      *
      * Sjekk telefonnummer
      *
     c                   if        w_tlfk = 'J' and
     c                             rltlfn <> w_tlfn
     c                   eval      w_ikke = 'J'
     c                   endif
      *
      * Sjekk telefax
      *
     c                   if        w_tlfx = 'J' and
     c                             rltlfx <> w_tlfx
     c                   eval      w_ikke = 'J'
     c                   endif
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på leverandørnummer
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for posisjonering på alfa
      *
     c     rlevl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl2_alfa
      *
      * Key for oppdatering av leverandørnummer
      *
     c     rlevlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevlu_levr
      *
      * Key for oppdatering av leverandørbank
      *
     c     rlbalu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlbalu_levr
      *
      * Key for oppdatering av leverandørsøketekstregister
      *
     c     rlsolu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlsolu_slev
      *
      * Key for oppdatering av leverandørposter
      *
     c     rltrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rltrlu_levr
      *
T5.40 * Key for notatblokk
      *
T5.40c     rklalu_key    klist
T5.40c                   kfld                    w_firm
T5.40c                   kfld                    rklalu_syst
T5.40c                   kfld                    rklalu_pros
T5.40c                   kfld                    rklalu_akti
T5.40c                   kfld                    rklalu_levr
      *
      * Henter firmanummer fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      rlevl1_levr = 0
     c     rlevl1_key    setll     rlevl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
**
  Ny post                  001
  Endring                  002
  Vise                     003
