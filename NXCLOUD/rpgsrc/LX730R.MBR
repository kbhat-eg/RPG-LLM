     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LX730R
      * Beskrivelse . . : Ajourfører lokasjonsbeskrivelse fra lager
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       091107 TSV  Nytt program
      * 6.10  170609 BSA  Oppdatert med sporingsinformasjon
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     fmdlol1    uf a e           k disk    rename(mdlopfr:mdlol1r)
      * Definering av Local data area
     d                uds
     d l_user                911    920
      * Definering av variable i nøkler
     d mdlol1_mdfirm   s                   like(mdfirm)
     d mdlol1_mdlage   s                   like(mdlage)
     d mdlol1_mdloka   s                   like(mdloka)

      * Arbeidsvariable
     d w_udat          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
      ** HOVEDPROGRAM **
     c                   read      vlagl1
     c                   dow       not %eof
     c                   if        vlplas <> ' '
     c                   eval      mdlol1_mdfirm = vlfirm
     c                   eval      mdlol1_mdlage = vllage
     c                   eval      mdlol1_mdloka = vlplas
     c     mdlol1_key    chain     mdlol1                             90
     c                   if        *in90 = *on
     c                   clear                   mdlol1r
     c                   eval      mdfirm = mdlol1_mdfirm
     c                   eval      mdlage = mdlol1_mdlage
     c                   eval      mdloka = mdlol1_mdloka
     c                   eval      mdeusr = l_user
     c                   eval      mdodat = w_udat
     c                   eval      mdedat = w_udat
     c                   eval      mjodat = w_udat
     c                   eval      mdetim = w_time
6.10 c                   time                    mdodat
6.10 c                   time                    mdotim
6.10 c                   eval      mdousr = l_user
6.10 c                   time                    mdedat
6.10 c                   time                    mdetim
6.10 c                   eval      mdeusr = l_user
     c                   write     mdlol1r
     c                   endif
     c                   endif
     c                   read      vlagl1r
     c                   enddo
      * Avslutt program
     c                   eval      *inlr = *on
     c                   return
      ** Subrutine for initiering av program **
     c     *inzsr        begsr
      *  Key for oppslag og evt oppdatering av tabell (lokasjon)
     c     mdlol1_key    klist
     c                   kfld                    mdlol1_mdfirm
     c                   kfld                    mdlol1_mdlage
     c                   kfld                    mdlol1_mdloka
     c                   time                    w_udat
     c                   time                    w_time
     c                   endsr
