     h option(*nodebugio) datedit(*dmy)
     h dftactgrp(*no)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : VG711R
      * Beskrivelse . . : Henter ut gjeldende glidende gennomsnittlige kospris pr. dato
      *                   Batch versjon for bruk i VG715R
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       231118 ASK  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvgkpi3    if a e           k disk    rename(vgkpst:vgkpi2r)
     fvvarlr    if a e           k disk    rename(vvarpfr:vvarlrr)
     fvgkkir    if   e           k disk    rename(vgkkst:vgkkirr)
      *
      * Definering av parametre
      *
     d p_vare          s                   like(vgvare)
     d p_lage          s                   like(vglage)
     d p_dato          s              8  0
     d p_cost          s                   like(vgcost)
     d p_enhe          s              3
     d p_inlr          s              1
     d p_prgr          s              1
     d p_lety          s              1  0
     d p_sapr          s              9  2
     d p_gmpr          s              9  2
     d p_kopr          s              9  2
     d p_inpr          s              9  2
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variabler i nøkler
      *
     d vgkpi3_lage     s              2  0
     d vgkpi3_vare     s                   like(vgvare)
     d vgkpi3_gdat     s                   like(vggdat)
     d vgkpi3_gtim     s                   like(vggtim)
     d vgkpi3_hkod     s                   like(vghkod)
     d vvarlr_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vgfirm)
     d w_date          s                   like(vggdat)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedrutine - finner aktuell gjennomsnittlig kostpris ut fra dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Sjekker om siste runde for å stenge tabeller
      *
     c                   if        p_inlr = '1'
     c                   eval      *inlr = *on
     c                   goto      avslutt
     c                   endif
      *
      * Slår opp og forsøker å finne gj.sn.kostpris, hvis ikke funnet - settes kostpris til 0
      *
     c                   eval      vgkpi3_vare = p_vare
     c                   eval      vgkpi3_lage = p_lage
     c                   eval      vgkpi3_gdat = %date(p_dato:*iso)
     c                   eval      vgkpi3_gtim = *hival
     c                   eval      vgkpi3_hkod = ' '
     c     vgkpi3_key    setll     vgkpi3
      *
     c                   read      vgkpi3
      *
     c                   if        %eof or
     c                             vgfirm <> w_firm or
     c                             vgvare <> p_vare or
     c                             vglage <> p_lage
     c                   eval      p_cost = 0
     c                   else
     c                   eval      p_cost = vgcost
     c                   eval      p_enhe = vgenhe
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_vare
     c                   parm                    p_lage
     c                   parm                    p_dato
     c                   parm                    p_cost
     c                   parm                    p_enhe
     c                   parm                    p_inlr
      *
      * Key for oppslag av kostpris
      *
     c     vgkpi3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vgkpi3_lage
     c                   kfld                    vgkpi3_vare
     c                   kfld                    vgkpi3_gdat
     c                   kfld                    vgkpi3_gtim
     c                   kfld                    vgkpi3_hkod
      *
      * Key for oppslag på vare
      *
     c     vvarlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlr_vare
      *
      * Key for posisjonering på kodetabell
      *
     c     vgkkir_key    klist
     c                   kfld                    w_firm
      *
     c                   eval      w_firm = l_firm
      *
      *
      * Sjekk om kostpris pr. lager.
      *
     c     vgkkir_key    chain     vgkkir
     c                   if        vgprlg = 'N'
     c                   eval      p_lage = 0
     c                   endif
      *
     c                   endsr
