     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : rp550R
      * Beskrivelse . . : Spørring ordre / bestilling pr. prosjekt
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       030315 bof  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = avslutt
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     frp550d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      *  Definering av datastrukturer
      *
      * Transaksjons-array
     d                 ds
     d tr_info                      139    dim(500)
     d  tr_numm                       8  0 overlay(tr_info:1)
     d  tr_otyp                       2    overlay(tr_info:9)
     d  tr_navn                      30    overlay(tr_info:11)
     d  tr_stat                      10    overlay(tr_info:41)
     d  tr_user                      10    overlay(tr_info:51)
     d  tr_dato                        d   datfmt(*iso)
     d                                     overlay(tr_info:61)
     d  tr_time                        t   timfmt(*iso)
     d                                     overlay(tr_info:71)
     d  tr_sort                      18    overlay(tr_info:61)
     d  tr_btyp                       1    overlay(tr_info:79)
     d  tr_kund                       6  0 overlay(tr_info:80)
     d  tr_kass                      10    overlay(tr_info:86)
     d  tr_adr1                      30    overlay(tr_info:96)
     d  tr_suff                       2  0 overlay(tr_info:126)
     d  tr_salg                      11  2 overlay(tr_info:128)
      *
6.22 d                 ds
6.22 d d_prec                        20
6.22 d  d_firm                        3  0 overlay(d_prec)
6.22 d  d_type                        4    overlay(d_prec:4)
6.22 d  d_mnum                        6  0 overlay(d_prec:8)
6.22 d  d_mlin                        6  0 overlay(d_prec:14)
6.22 d  d_kode                        1    overlay(d_prec:20)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(fofirm)
     d p_proj          s                   like(foproj)
6.24 d p_ntyp          s              1
      *
      * Definering av Local data area
      *
6.24 dlda             uds
6.24 d l_vise                700    700
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d lohel1_numm     s                   like(loornr)
     d lohel1_suff     s                   like(losuff)
     d rlevl1_levr     s                   like(rllevr)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
     d ind             s              3  0
      *
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
6.29 d b_best          s              1    inz(*off)
6.2a d b_bes2          s              1    inz(*off)
6.2c d b_bes3          s              1    inz(*off)
      *
     d w_firm          s                   like(fofirm)
     d w_numm          s                   like(fonumm)
     d w_snum          s                   like(fonumm)
     d w_best          s                   like(lonumm)
6.22 d w_mnum          s                   like(lomnum)
     d w_date          s               d   datfmt(*iso)
     d w_ddat          s               d   datfmt(*iso)
     d w_aarr          s              4  0
6.2b d w_baar          s              4  0
     d w_daar          s              4  0
     d w_faar          s              4  0
     d w_binr          s              6  0
     d w_bong          s             12
     d w_sbon          s             12
     d w_tell          s              3  0
6.2b d w_anta          s              3  0
6.22 d w_prec          s             45
6.23 d w_type          s              4
     d w_proj          s                   like(foproj)
6.2b d w_salg          s             11  2
     d w_navn          s                   like(lonavn)
     d w_adr1          s                   like(loadr1)
      *
     d s_onum          s              6  0
     d s_fakt          s              6  0
     d s_bong          s             12
     d s_best          s              6  0
     d s_lonr          s             15
     d s_lfak          s             15
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(13)
      *
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_srrn - Relativt recordnummer i subfile
      * w_spge - Sidestørrelse i subfile (page)
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * w_virn - Vis den side som inneholder denne record.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Setter startverdier
      *
     c                   eval      s_onum = 0
     c                   eval      s_fakt = 0
     c                   eval      s_bong = *blank
     c                   eval      s_best = 0
     c                   eval      s_lonr = *blank
     c                   eval      s_lfak = *blank
      *
      * Send startbilde
      *
     c     b2taga        tag
     c                   write     b2cmd
     c     b2tagb        tag
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_pgm
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2taga
     c                   when      *in21
     c                   eval      *in22  = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile    ,
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Vise detaljinformasjon
      *
     c                   if        b1valg = 5
     c     *dmy          move      b1dato        w_date
     c                   eval      w_aarr = %subdt(w_date:*years)
      *
      * Vis ordre
      *
     c                   if        b1btyp = 'O'
     c                   call      'FO505R'
     c                   parm                    w_firm
     c                   parm                    b1numm
     c                   parm                    b1suff
     c                   endif
      *
      * Vis bestilling
      *
     c                   if        b1btyp = 'B'
     c                   call      'LO505R'
     c                   parm                    w_firm
     c                   parm                    b1numm
     c                   parm                    b1suff
     c                   endif
      *
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   exsr      null_tab
     c                   exsr      finn_ordre
     c                   exsr      finn_bestill
      *
     c                   exsr      sort_tab
      *
     c                   eval      ind = 1
     c                   eval      w_spge = w_spge + c_sfil
      *
     c                   dou       ind > 500
      *
     c                   if        tr_sort(ind) <> *blank
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1numm = tr_numm(ind)
     c                   eval      b1suff = tr_suff(ind)
     c                   eval      b1otyp = tr_otyp(ind)
     c                   eval      b1lnav = tr_navn(ind)
     c                   eval      b1vadr = tr_adr1(ind)
     c                   eval      b1salg = tr_salg(ind)
     c                   eval      b1stat = tr_stat(ind)
     c                   eval      b1user = tr_user(ind)
     c                   if        tr_dato(ind) > *loval
     c     *dmy          move      tr_dato(ind)  b1dato
     c                   else
     c                   eval      b1dato = 0
     c                   endif
     c                   if        tr_time(ind) > *loval
     c     *hms          move      tr_time(ind)  b1time
     c                   else
     c                   eval      b1time = 0
     c                   endif
     c                   eval      b1btyp = tr_btyp(ind)
     c                   eval      b1kund = tr_kund(ind)
     c                   eval      b1kass = tr_kass(ind)
      *
     c                   write     b1sfl
     c                   endif
      *
     c                   eval      ind = ind + 1
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   eval      *in15 = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for nullstilling av tabell
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     null_tab      begsr
      *
      * Nullstiller tabell
      *
     c                   movea     *blank        tr_info
     c                   eval      ind = 1
      *
     c                   dou       ind > 500
     c                   eval      tr_numm(ind) = 0
     c                   eval      tr_suff(ind) = 0
     c                   eval      tr_otyp(ind) = *blank
     c                   eval      tr_navn(ind) = *blank
     c                   eval      tr_stat(ind) = *blank
     c                   eval      tr_user(ind) = *blank
     c                   eval      tr_dato(ind) = *loval
     c                   eval      tr_time(ind) = *loval
     c                   eval      tr_sort(ind) = *blank
     c                   eval      tr_btyp(ind) = *blank
     c                   eval      tr_kund(ind) = 0
     c                   eval      tr_kass(ind) = *blank
     c                   eval      ind = ind + 1
     c                   enddo
     c                   eval      ind = 1
      *
     c     end_null      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for uthenting av aktive ordres
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_ordre    begsr
      *
     c/exec sql
     c+                  declare sok_o cursor for
     c+                  select fofirm,
     c+                         fonumm,
     c+                         fosuff
     c+                  from   fohepf
     c+                    where foproj = :w_proj  and
     c+                          fofirm = :w_firm
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_o
     c/end-exec
     c/exec sql
     c+                  open sok_o
     c/end-exec
      *

     c                   dou       ind    > 500
      *
     c/exec sql
     c+                  fetch next
     c+                  from  sok_o
     c+                  into  :fofirm,
     c+                        :fonumm,
     c+                        :fosuff
     c/end-exec
      *
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   leavesr
     c                   endif
      *
      *
     c                   if        fofirm <> w_firm
     c                   leavesr
     c                   endif
      *
     c                   eval      fohel1_numm = fonumm
     c                   eval      fohel1_suff = fosuff
     c     fohel1_key    chain     fohel1
     c                   if        %found(fohel1)
      *
     c                   eval      w_salg = (fotots - fototr)
     c                   eval      ind = ind + 1
     c                   eval      tr_numm(ind) = fonumm
     c                   eval      tr_suff(ind) = fosuff
     c                   eval      tr_otyp(ind) = footyp
     c                   eval      tr_navn(ind) = fonavn
     c                   eval      tr_user(ind) = fouser
     c                   eval      tr_dato(ind) = fodate
     c                   eval      tr_time(ind) = fotime
     c                   eval      tr_btyp(ind) = 'O'
     c                   eval      tr_kund(ind) = fokund
     c                   eval      tr_adr1(ind) = foadr1
     c                   eval      tr_salg(ind) = w_salg
      *
     c                   endif
      *
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne bestilling koblet til ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_bestill  begsr
      *
     c/exec sql
     c+                  declare sok_l cursor for
     c+                  select lofirm,
     c+                         lonumm,
     c+                         losuff
     c+                  from   lohepf
     c+                    where loproj = :w_proj  and
     c+                          lofirm = :w_firm
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_l
     c/end-exec
     c/exec sql
     c+                  open sok_l
     c/end-exec
      *

     c                   dou       ind    > 500
      *
     c/exec sql
     c+                  fetch next
     c+                  from  sok_l
     c+                  into  :lofirm,
     c+                        :lonumm,
     c+                        :losuff
     c/end-exec
      *
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   leavesr
     c                   endif
      *
      *
     c                   if        lofirm <> w_firm
     c                   leavesr
     c                   endif
      *
     c                   eval      w_navn = rlnavn
     c                   eval      w_adr1 = rlgate
      *
     c                   eval      lohel1_numm = lonumm
     c                   eval      lohel1_suff = losuff
     c     lohel1_key    chain     lohel1
     c                   if        %found(lohel1)
     c                   if        lonavn = *blank and
     c                             loadr1 = *blank
     c                   eval      rlevl1_levr = loldor
     c     rlevl1_key    chain     rlevl1
     c                   if        %found(rlevl1)
     c                   eval      w_navn = rlnavn
     c                   eval      w_adr1 = rlgate
     c                   else
     c                   eval      w_navn = lonavn
     c                   eval      w_adr1 = loadr1
     c                   endif
     c                   endif
      *
     c                   eval      w_salg = (lototi - lototr)
     c                   eval      ind = ind + 1
     c                   eval      tr_numm(ind) = lonumm
     c                   eval      tr_suff(ind) = losuff
     c                   eval      tr_otyp(ind) = lootyp
     c                   eval      tr_navn(ind) = w_navn
     c                   eval      tr_user(ind) = louser
     c                   eval      tr_dato(ind) = lodate
     c                   eval      tr_time(ind) = lotime
     c                   eval      tr_btyp(ind) = 'B'
     c                   eval      tr_kund(ind) = loldor
     c                   eval      tr_adr1(ind) = w_adr1
     c                   eval      tr_salg(ind) = w_salg
      *
     c                   endif
      *
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sortering av tabell
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sort_tab      begsr
      *
     c                   sorta     tr_sort
      *
     c     end_sort      endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *dtaara       define    *lda          lda
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
6.24 c                   parm                    p_proj
      *
      *  Key les av AKTIVE-ORDRES
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      *  Key les av BESTILLING på bestnr
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
     c                   kfld                    lohel1_suff
      *
      *  Key les av leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Setter inn startverdier
      *
     c                   eval      w_proj = p_proj
     c                   eval      b2proj = p_proj
     c                   eval      w_firm = p_firm
     c                   eval      *in22  = *on
      *
     c                   time                    w_ddat
     c                   eval      w_daar = %subdt(w_ddat:*years)
     c                   eval      w_faar = w_daar - 1
      *
     c                   eval      *in22 = *on
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
