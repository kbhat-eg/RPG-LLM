      /TITLE  Utskrift av kundeetiketter
     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RK623R                                        *
      * Beskrivelse . . : Utskrift av kundeetiketter                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
T5.00 * R5.00     270902  Bruker eksterntdefinert printerfil.      HFL  *
      * R5.20     131003  Utelater passive kunder                  KOB  *
      * R5.40     230305  Lagt inn utskrift til laser              KOB  *
T5.40 * R5.40     151205  Feil når 3 i bredden, er rettet          HFL  *
T5.40 * R5.40     190406  Eget format for laser-etiketter          HFL  *
T5.50 * R5.50     200607  Kan velge inntil 21 enkeltkunder.        HFL  *
      *                                                                 *
6.20  * V6.20     090907  Endret lengde på distrikt                NHO  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KA = F1  = Valg av firma                                  *
      *       KC = F3  = Exit                                           *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     frkunl2    if   e           k disk    rename(rkunpfr:rkunl2r)
     frk623p    o    e             printer oflind(*in30)
      *
      *************************  T A B E L L E R  ************************
      *
     d f               s              1    dim(30)
     d knr             s              6  0 dim(3)                               Kundenummer
     d kna             s             30    dim(3)                               Navn
     d ka1             s             30    dim(3)                               Adresse 1
     d ka2             s             30    dim(3)                               Adresse 2
     d kst             s             30    dim(3)                               Poststed
     d kpe             s             30    dim(3)                               Kontaktperson
      *
T5.40 * Definering av variable
      *
T5.40d w_kper          s             30
      *
     d                 ds
     d w_pdat                         6  0
     d  w_pdag                        2  0 overlay(w_pdat)
     d  w_pmnd                        2  0 overlay(w_pdat:3)
     d  w_paar                        2  0 overlay(w_pdat:5)
      *
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      * LOCAL DATA
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      *
T5.50 *RESKONTRO - Datastruktur for parameter
     d                uds
     d pdato                 300    305  0                                      Pr. dato
     d paarf                          4  0                                      Pr. år
     d paart                          4  0                                      Pr. år
     d pperf                          2  0                                      Periode f.o.m.
     d ppert                          2  0                                      Periode t.o.m.
     d pavdf                          4  0                                      Avdeling f.o.m.
     d pavdt                          4  0                                      Avdeling t.o.m.
     d pkatf                          4  0                                      Kategori f.o.m.
     d pkatt                          4  0                                      Kategori t.o.m.
     d pself                          4  0                                      Selger f.o.m.
     d pselt                          4  0                                      Selger t.o.m.
     d pknrf                          6  0                                      Nr. f.o.m.
     d pknrt                          6  0                                      Nr. t.o.m.
     d kun                            6  0 dim(21)                              Tabell-enkeltkunder
6.20 d*pdisf                          2  0                                      Distrikt f.o.m.
6.20 d pdisf                          4  0                                      Distrikt f.o.m.
6.20 d*pdist                          2  0                                      Distrikt t.o.m.
6.20 d pdist                          4  0                                      Distrikt t.o.m.
     d plagf                          2  0                                      Lager f.o.m
     d plagt                          2  0                                      Lager t.o.m
     d psagr                          1                                         Saldotest(J/N)
     d psabe                          7  0                                      Saldogrense i kroner
     d pktid                          1                                         Kredittest(J/N)
     d pktdg                          5  0                                      Kreditgrense i dager
     d pkgrn                          1                                         Kredit overskredet
     d pedat                         10d   datfmt(*iso)                         Endringsdato
     d ppass                          1                                         Passive
      *
      *  Hva skal skrives på liste                                Valg: J/N=Ja/N
     d pvalu                          1                                         Valutabeløp
     d pkper                          1                                         K.person
     d ptlf                           1                                         Tlf.nr
     d pkomp                          1                                         Komprimert
     d psald                          1                                         Kun saldo
     d pkjop                          1                                         Kun saldo
      *
      *  Hvordan skal liste sorteres                              Valg: J/N=Ja/N
     d pkats                          1                                         Kategori
     d pavds                          1                                         Avdeling
     d psels                          1                                         Selger
     d psort                          1                                         Alfa/Numerisk
     d petik                          1  0                                      Etikettype
     d ptype                          1                                         Listetype
     d prabk                          1                                         Rabattkategori
     d ppsla                          1                                         Påslagstabell
     d pssal                          1                                         Stigende saldo
      *
     d l_wsid                901    910                                         Bruker
     d l_user                911    920                                         Bruker
     d l_firm                944    946  0                                      Firma
     d l_fnav                951    980                                         Navn
      *
     d n               s              2  0
T5.50d m               s              2  0
     d wantre          s              5  0
     d wantdl          s              5  0
     d w_alfa          s                   like(rkalfa)
     d w_firm          s                   like(rkfirm)
     d w_kund          s                   like(rkkund)
T5.50d tstkt           s              7  0
      *
      *
      * Les kunderegister
      * avhengig av valg num./alfa
      *
     c     lesk          tag
      *
     c                   if        psort = ' '                                           .....
     c                   read      rkunl1                                 10                 .
     c                   else                                                                .
     c                   read      rkunl2                                 10                 .
     c                   endif                                                           .....
      *
     c                   if        rkfirm > l_firm                                       .....
     c                   eval      *in10 = *on                                               .
     c                   endif                                                           .....
      *
     c                   if        *in10 = *on
     c                   goto      slutt                                        FERDIG
     c                   endif
      *
      * Sjekk om passiv
      *
     c                   if        ppass  = ' ' and                                      .....
     c                             rkpass = 'J'                                          .....
     c                   goto      neste                                                     .
     c                   endif                                                           .....
      *
      * Sjekk kundenr. fra/til
      *
     c                   if        rkkund < pknrf or                                     .....
     c                             rkkund > pknrt                                        .....
     c                   goto      neste                                                     .
     c                   endif                                                           .....
      *
      * Sjekk avdeling fra/til
      *
     c                   if        rkavde < pavdf or                                     .....
     c                             rkavde > pavdt                                        .....
     c                   goto      neste                                                     .
     c                   endif                                                           .....
      *
      * Sjekk kundekategori fra/til
      *
     c                   if        rkkatg < pkatf or                                     .....
     c                             rkkatg > pkatt                                        .....
     c                   goto      neste                                                     .
     c                   endif                                                           .....
      *
      * Sjekk lager fra/til
      *
     c                   if        rklagr < plagf or                                     .....
     c                             rklagr > plagt                                        .....
     c                   goto      neste                                                     .
     c                   endif                                                           .....
      *
      * Sjekk selger fra/til
      *
     c                   if        rkslgr < pself or                                     .....
     c                             rkslgr > pselt                                        .....
     c                   goto      neste                                                     .
     c                   endif                                                           .....
      *
      * Sjekk distrikt fra/til
      *
     c                   if        rkdist < pdisf or                                     .....
     c                             rkdist > pdist                                        .....
     c                   goto      neste                                                     .
     c                   endif                                                           .....
      *
T5.50 * Sjekk om enkeltkunder er valgt
      *
T5.50c                   if        tstkt = *zeros                                        .....
T5.50c                   goto      skriv_kund                                                .
T5.50c                   endif                                                           .....
      *
T5.50 * Kundenr. ligger utenfor fra - til
T5.50 * sjekk mot tabell
      *
T5.50c                   eval      m = 1
T5.50c                   dou       m > 21                                                .....
      *                                                                 .
T5.50c                   if        kun(m) <> *zeros                                          .
      *                                                                 .
T5.50c                   if        rkkund = kun(m)                                           .
T5.50c                   goto      skriv_kund                                                .
T5.50c                   endif                                                               .
      *                                                                 .
T5.50c                   endif                                                               .
      *                                                                 .
T5.50c                   eval      m = m + 1                                                 .
T5.50c                   enddo                                                           .....
      *
T5.50 * Konto heller ikke i tabell -
T5.50 * les neste postering
      *
T5.50c                   goto      neste
      *
T5.50c     skriv_kund    tag
      *
     c                   eval      n = n + 1
     c                   move      rkkund        knr(n)
     c                   movel     rknavn        kna(n)
     c                   movel     rkgate        ka1(n)
     c                   movel     rkadr2        ka2(n)
     c                   movel     rksted        kst(n)
      *
      * Skal kontaktperson skrives?
      *
T5.40c                   if        pkper  = 'J' and                                      .....
T5.40c                             rkpers <> *blank                                      .....
T5.40c                   eval      w_kper = 'Attn. ' +
T5.40c                                       %subst(rkpers:1:30)
T5.40c                   else                                                                .
T5.40c                   eval      w_kper = *blank
T5.40c                   endif                                                           .....
E5.40c                   movel     w_kper        kpe(n)
      *
      * Skriv enkeltetiketter
      *
     c                   eval      knr1  = knr(1)
     c                   eval      kna1  = kna(1)
     c                   eval      ka11  = ka1(1)
     c                   eval      ka21  = ka2(1)
     c                   eval      kst1  = kst(1)
T5.40c                   eval      kpe1  = kpe(1)
      *
     c                   if        petik = 1                                             .....
     c                   eval      wantdl = wantdl + 1
     c                   write     mlapp1                                                    .
     c                   exsr      xnull                                                     .
     c                   endif                                                           .....
      *
      * Skriv 3 i bredden eller laser
      *
E5.40c                   if        petik = 3                                             .....
E5.40c                             and n = 3 or                                              .
     c                             petik = 4                                             .....
     c                             and n = 3                                                 .
     c                   eval      knr2  = knr(2)
     c                   eval      kna2  = kna(2)
     c                   eval      ka12  = ka1(2)
     c                   eval      ka22  = ka2(2)
     c                   eval      kst2  = kst(2)
T5.40c                   eval      kpe2  = kpe(2)
      *
     c                   eval      knr3  = knr(3)
     c                   eval      kna3  = kna(3)
     c                   eval      ka13  = ka1(3)
     c                   eval      ka23  = ka2(3)
     c                   eval      kst3  = kst(3)
T5.40c                   eval      kpe3  = kpe(3)
     c                   eval      wantdl = wantdl + n
      *
T5.40c                   if        petik = 3                                             .....
     c                   write     mlapp3                                                    .
T5.40c                   else                                                            .....
      *
T5.40 * Sjekker om overflow
      *
T5.40c                   if        *in30 = *on                                               .
T5.40c                   write     head                                                      .
T5.40c                   eval      *in30 = *off                                              .
T5.40c                   endif                                                           .....
      *
T5.40c                   write     mlapp4                                                    .
T5.40c                   endif                                                           .....
     c                   exsr      xnull                                                     .
     c                   endif                                                           .....
      *
     c     neste         tag
      *
     c                   eval      wantre = wantre + 1
      *
     c                   goto      lesk
      *
     c     slutt         tag
      *
      * Skriv gjenstående etiketter
      *
     c                   if        petik = 3                                             .....
E5.40c                             and n > *zeros or                                         .
T5.40c                             petik = 4                                             .....
T5.40c                             and n > *zeros                                            .
E5.40c                   if        n     = 2                                             .....
     c                   eval      knr2  = knr(2)
     c                   eval      kna2  = kna(2)
     c                   eval      ka12  = ka1(2)
     c                   eval      ka22  = ka2(2)
     c                   eval      kst2  = kst(2)
T5.40c                   eval      kpe2  = kpe(2)
     c                   else                                                            .....
     c                   eval      knr2  = *zeros
     c                   eval      kna2  = *blanks
     c                   eval      ka12  = *blanks
     c                   eval      ka22  = *blanks
     c                   eval      kst2  = *blanks
T5.40c                   eval      kpe2  = *blanks
     c                   endif                                                           .....
      *
     c                   eval      knr3  = *zeros
     c                   eval      kna3  = *blanks
     c                   eval      ka13  = *blanks
     c                   eval      ka23  = *blanks
     c                   eval      kst3  = *blanks
T5.40c                   eval      kpe3  = *blanks
      *
     c                   eval      wantdl = wantdl + n
T5.40c                   if        petik = 3                                             .....
     c                   write     mlapp3                                                    .
T5.40c                   else                                                            .....
      *
T5.40 * Sjekker om overflow
      *
T5.40c                   if        *in30 = *on                                               .
T5.40c                   write     head                                                      .
T5.40c                   eval      *in30 = *off                                              .
T5.40c                   endif                                                           .....
      *
T5.40c                   write     mlapp4                                                    .
T5.40c                   endif                                                           .....
     c                   exsr      xnull                                                     .
     c                   endif                                                           .....
      *
     c                   eval      *inlr = *on
      *
     clr                 if        petik = 1                                             .....
     clr                 write     total1                                                    .
     clr                 else                                                                .
     clr                 write     total1                                                    .
     clr                 endif                                                           .....
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for nullstilling av tabeller                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xnull         begsr
      *                                                                 .
      * Nullstiller                                                     .
      *                                                                 .
     c                   eval      n = *zeros                                                .
     c                   eval      knr = *zeros                                              .
     c                   eval      kna = *blanks                                             .
     c                   eval      ka1 = *blanks                                             .
     c                   eval      ka2 = *blanks                                             .
     c                   eval      kst = *blanks                                             .
     c                   eval      kpe = *blanks                                             .
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Sette startverdier
      *
     c                   if        petik = 1 or                                          .....
     c                             petik = 3                                             .....
     c                   write     kinst1                                                    .
     c                   endif                                                           .....
      *
     c                   move      l_firm        w_firm
     c                   eval      w_kund = *zeros
     c                   eval      w_alfa = *blank
T5.50c                   xfoot     kun           tstkt
      *
     c                   if        psort = ' '                                           .....
     c     key01         setll     rkunl1                                                    .
     c                   else                                                                .
     c     key02         setll     rkunl2                                                    .
     c                   endif                                                           .....
      *
      * Key for kunderegister
      *
     c     key01         klist
     c                   kfld                    w_firm
     c                   kfld                    w_kund
      *
      * Key for kunderegister
      * via alfafeltet
      *
     c     key02         klist
     c                   kfld                    w_firm
     c                   kfld                    w_alfa
     c                   kfld                    w_kund
      *
T5.40 * Posisjonerer første linje
      *
T5.40c                   if        petik = 4                                             .....
T5.40c                   write     head                                                      .
T5.40c                   endif                                                           .....
      *
     c                   endsr
