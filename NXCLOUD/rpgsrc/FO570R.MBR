     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FO570R
      * Beskrivelse . . : Spørring på ordre/restet - til levering
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       261005 GRo  Nytt program
      *       040206 HKO  Utvidet logikk i bildet
      *       091107 bof  Viser alle restede med bla-alt de som er lev.dykt.
      *                   (skrevet om fordi blaing ikke fungerte)
5.60  * R5.60 070108 bof  Utvidet logg med år
5.60  * R5.61 280208 bof  Utvidet med valg av lager i FIlter + default lager
5.63  * R5.63 090708 Amd  Har foretatt en liten justering, ordre ble
      *                   liggende igjen på restede ordre til levering
      *                   selv etter at de var sendt til plukking igjen
      *                   I tillegg er det opprettet en ny kode 9
5.64  * R5.64 110708 Amd  Lagt inn mulighet for å se loggfilen
5.65  *       090212 bof  Skal kunne se kode 9 her
5.66  *       280212 bof  Må også teste på restkode = 0 + 9 - skal sees
6.nu  * R6.30 280514 bsa  Programmet gjennomgått mtp nummerutvidelser.
7.xx  * 7.00  190216 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 7.00  310519 bkv  Vasker vekk probl Newlook tegn
7.02  * 7.00  300120 bkv  Flyttet FU706 pga hastighet
      *
8.01  * 8.xx  150322 bsa  Feil kode blir satt når en klikker av i cloud.
8.02  *K000   140122 bsa  Sjekker at varekunde vises, og ikke fakturakunde.
8.03  *K000   180321 bsa  Hvis vi kjører med ekstern flåtestyring, kjør
8.03  *K000               request til API for opprettelse av transport.
8.03  *K000               Flåtestyring på lagernivå.
8.04  *K00    111122 bsa  Kaller nytt program som håndterer alt rundt flåtestyring.
8.05  *K000   180123 bsa  Flagg om skaffetekst i transportlogg.
8.06  *K000   060223 bsa  Flagg om sjekk på loggkode i transportlogging
8.07  *PRG2   200622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = avslutt
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffoheld    if   e           k disk    rename(fohepfr:foheldr)
     ffohelr    if   e           k disk    rename(fohepfr:fohelrr)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     fvinfl1    if   e           k disk    rename(vinfpfr:vinfl1r)
     fausrlr    if   e           k disk    rename(ausrpfr:ausrlrr)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     ffohelu    uf   e           k disk    rename(fohepfr:fohelur)
8.04 f*ftrpi2    if   e           k disk    rename(ftrpst:ftrpi2r)
8.04 f*raf2l1    if   e           k disk    rename(raf2pfr:raf2l1r)
8.04 f*ftrli1    if   e           k disk    rename(ftrlst:ftrli1r)
      *
     ffo570d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
8.03 d l_filg                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d fodtl1_numm     s                   like(fdnumm)
     d fodtl1_suff     s                   like(fdsuff)
     d rkunl1_kund     s                   like(rkkund)
     d votyl1_otyp     s                   like(vaotyp)
     d vlagl1_vare     s                   like(vlvare)
     d vlagl1_lage     s                   like(vllage)
     d vinfl1_ityp     s                   like(vaityp)
     d fohelu_numm     s                   like(fonumm)
     d fohelu_suff     s                   like(fosuff)
     d foheld_bdat     s                   like(fobdat)
     d ausrlr_user     s                   like(abuser)
8.04 d*raf2l1_2kod     s                   like(ra2kod)
8.04 d*ftrli1_llag     s                   like(ftllag)
8.04 d*ftrpi2_rnum     s                   like(ftrnum)
8.04 d*ftrpi2_rsuf     s                   like(ftrsuf)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_dekn          s              1    inz(*off)
     d b_subf          s              1    inz(*off)
5.62 d b_funn          s              1    inz(*off)
      *
     d w_firm          s                   like(fofirm)
6.nu d w_ornr          s                   like(fonumm)
     d w_lpro          s              1
6.nu d w_numm          s                   like(fonumm)
     d w_suff          s                   like(fosuff)
5.54 d w_stat          s              1
5.54 d w_kode          s              1
5.54 d w_aarr          s              4  0
7.01 d w_len           s              3  0
7.01 d w_teller        s              3  0
8.02 d w_kund          s                   like(fokund)
8.03 d w_reqs          s              5
8.03 d w_roid          s             35
8.05 d w_skaf          s              1  0
8.06 d w_logg          s              1  0
      *
6.nu d s_numm          s                   like(fonumm)
5.54  *
5.54  * Definering av datastruktur
5.54  *
5.54 d                 ds
5.54 d d_urec                        80
5.54 d  d_firm                        3  0 overlay(d_urec)
5.60 d  d_aarr                        4  0 overlay(d_urec:4)
6.nu d  d_numm                        8  0 overlay(d_urec:8)
6.nu d  d_suff                        2  0 overlay(d_urec:16)
6.nu d  d_kode                        1    overlay(d_urec:18)
6.nu d  d_date                         d   overlay(d_urec:19) datfmt(*iso)
6.nu d  d_time                         t   overlay(d_urec:29) timfmt(*iso)
6.nu d  d_user                       10    overlay(d_urec:37)
6.nu d  d_wsid                       10    overlay(d_urec:47)
      *
      * Definering av konstanter
      *
7.xx d c_sfil          s              2  0 inz(13)
      *
8.03 d u_803           s              1    inz(*off)
      *
8.03   dcl-s co402_verdi1  char(1);
8.03   dcl-s co402_verdi2  char(2048);
8.03   DCL-PR CO402R EXTPGM('CO402R');
8.03     p_filgrp            char(3)     const;
8.03     p_firm              packed(3:0) const;
8.03     p_lager             packed(2:0) const;
8.03     p_nokkel            char(50)    const;
8.03     p_verdi1            char(1);
8.03     p_verdi2            char(2048);
8.03   END-PR;
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inka
     c                   exsr      xh1win
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
     c                   exsr      xsjekk_inp
     c                   if        b_feil = *on
     c                   eval      b_feil = *off
     c                   goto      b2tagb
     c                   endif
      *
      * Posisjonering
      *
     c                   if        b2numm <> 0   or
     c                             b2dato <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      foheld_bdat = b1bdat
     c     foheld_key    setll     foheld
     c                   other
     c                   eval      fohel1_numm = b1numm
     c*                  eval      fohel1_suff = b1suff
     c     fohel1_key    setll     fohel1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Rabattgruppe
      *
     c                   if        b2numm <> 0
     c                   eval      w_seqe = *blank
     c                   eval      fohel1_numm = b2numm
     c*                  eval      fohel1_suff = 0
     c     fohel1_key    setll     fohel1
     c                   goto      end_pos
     c                   endif
      *
      * Bilagsdato
      *
     c                   if        b2dato <> 0
     c                   eval      w_seqe = 'B'
     c     *dmy          move      b2dato        foheld_bdat
     c     foheld_key    setll     foheld
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2numm = 0
     c                   eval      b2dato = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre ordreinfo
      *
     c                   if        b1valg = 2
     c                   call      'FO150R'
     c                   parm                    w_firm
6.nu c                   parm                    b1numm
     c                   parm                    b1suff
     c                   eval      b1valg = 0
     c                   eval      b_forn = *on
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   exsr      vis_olin
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis kunde/kundeprosjekt
      *
     c                   if        b1valg = 6
8.02 c                   eval      w_kund = b1kund
8.02 c                   if        b1lkun <> 0 and
8.02 c                             b1kund <> 0 and
8.02 c                             b1kund <> b1lkun
8.02 c                   eval      w_kund = b1lkun
8.02 c                   endif
     c                   call      'FL510R'
     c                   parm                    w_firm
8.02 c**                 parm                    b1kund
8.02 c                   parm                    w_kund
     c                   parm                    b1kpro
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Behandle ordre
      *
     c                   if        b1valg = 7
     c                   exsr      ovg_ordre
     c                   eval      b1valg = 0
     c                   eval      b_forn = *on
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Hvem behandler ordre
      *
     c                   if        b1valg = 8
     c                   call      'FO710R'
     c                   parm                    w_firm
6.nu c                   parm                    b1numm
     c                   parm                    b1suff
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Sende til plukking
      *
     c                   if        b1valg = 9
     c                   exsr      klar_plukking
     c                   eval      b1valg = 0
     c                   eval      b_forn = *on
     c                   update    b1sfl
     c                   iter
     c                   endif
5.64  *
5.64  * Viser loggfilen med ordrehistorikk
5.64  *
5.64 c                   if        b1valg = 60
5.64 c                   eval      w_aarr = %subdt(b1bdat:*years)
5.64 c                   eval      w_numm = b1numm
5.64 c                   eval      w_suff = b1suff
5.64 c                   call      'FU500R'
5.64 c                   parm                    w_firm
5.64 c                   parm                    w_aarr
6.nu c                   parm                    w_numm
5.64 c                   parm                    w_suff
5.64 c                   eval      b1valg = 0
5.64  ***                exsr      xfarge
5.64 c                   update    b1sfl
5.64 c                   iter
5.64 c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for overgang til endr. ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ovg_ordre     begsr
      *
     c                   eval      w_ornr = b1numm
     c                   eval      w_lpro = *blank
      *
     c                   call      'FO500R'
6.nu c                   parm                    w_ornr
     c                   parm                    w_lpro
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å vise ordrelinjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     vis_olin      begsr
      *
     c                   eval      w_numm = b1numm
     c                   eval      w_suff = b1suff
      *
     c                   call      'FO575R'
     c                   parm                    w_firm
6.nu c                   parm                    w_numm
     c                   parm                    w_suff
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * xh1win  Bilde for behandling av bla-alternativer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xh1win        begsr
      *
     c     h1taga        tag
      *
     c                   write     h1win
     c                   exfmt     h1win
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc
     c                   goto      end_h1win
     c                   endsl
      *
     c     end_h1win     endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      foheld
     c                   other
     c                   read      fohel1
     c                   endsl
      *
     c                   if        %eof or
     c                             fofirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   exsr      chk_subfile
     c                   if        b_subf = *on
     c                   iter
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      foheld_bdat = fobdat
     c                   other
     c                   eval      fohel1_numm = fonumm
     c*                  eval      fohel1_suff = fosuff
     c                   endsl
      *
     c                   eval      b1valg = 0
      *
     c                   eval      *in30 = *off
     c                   eval      b1kund = fokund
     c                   eval      b1kpro = fokpro
     c                   eval      b1numm = fonumm
     c                   eval      b1suff = fosuff
     c                   eval      b1otyp = footyp
     c                   eval      b1bdat = fobdat
     c                   eval      b1odat = 0
8.02 c                   eval      b1lkun = folkun
      *
     c                   if        fobdat <> *loval
     c     *dmy          move      fobdat        b1odat
     c                   endif
      *
     c                   eval      rkunl1_kund = fokund
     c     rkunl1_key    chain     rkunl1r
     c                   if        %found
     c                   eval      b1knav = rknavn
     c                   else
     c                   eval      b1knav = *blank
     c                   endif
8.02  *
8.02  * Hvis varekunde, fakturakunde og prosjekt, legg ut varekunde
8.02  *
8.02 c                   if        fokund <> 0 and
8.02 c                             folkun <> 0 and
8.02 c                             fokund <> folkun and
8.02 c                             fokpro <> 0
8.02 c                   eval      rkunl1_kund = folkun
8.02 c     rkunl1_key    chain     rkunl1r
8.02 c                   if        %found
8.02 c                   eval      b1knav = rknavn
8.02 c                   endif
8.02 c                   endif
      *
     c                   eval      b1vadr = fonavn
     c                   eval      b1osum = fotots - fototr
     c                   eval      b1ityp = foityp
     c                   eval      b1kode = *blank
      *
     c                   if        foityp <> *blank
      *
     c                   eval      vinfl1_ityp = foityp
     c     vinfl1_key    chain     vinfl1
      *
3.34 c                   eval      *in41  = *off
3.34 c                   eval      *in42  = *off
3.34 c                   eval      *in43  = *off
3.34 c                   eval      *in44  = *off
      *
     c                   select
3.34 c                   when      vaifrg = 'H'
3.34 c                   eval      *in41  = *on
3.34 c                   when      vaifrg = 'G'
3.34 c                   eval      *in42  = *on
3.34 c                   when      vaifrg = 'B'
3.34 c                   eval      *in43  = *on
3.34 c                   when      vaifrg = 'R'
3.34 c                   eval      *in44  = *on
3.34 c                   endsl
3.34  *
     c                   endif
      *
     c                   select
3.12 c                   when      fokode = 1
3.12 c                   eval      b1kode = '**'
3.12 c                   when      fokode = 2
3.12 c                   eval      b1kode = 'UT'
3.12 c                   when      fokode = 3
3.12 c                   eval      b1kode = 'OK'
3.12 c                   when      fokode = 5
3.12 c                   eval      b1kode = 'VE'
3.12 c                   when      fokode = 7
3.12 c                   eval      b1kode = 'BU'
3.12 c                   endsl
      *
7.01 c                   exsr      newlo_vask
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'B'
     c     foheld_key    setgt     foheld
     c                   readp     foheld
     c                   other
     c     fohel1_key    setgt     fohel1
     c                   readp     fohel1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   readp     foheld
     c                   other
     c                   readp     fohel1
     c                   endsl
      *
     c                   if        %eof or
     c                             fofirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   exsr      chk_subfile
     c                   if        b_subf = *on
     c                   iter
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      foheld_bdat = fobdat
     c                   other
     c                   eval      fohel1_numm = fonumm
     c*                  eval      fohel1_suff = fosuff
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'B'
     c     foheld_key    setll     foheld
     c                   other
     c     fohel1_key    setll     fohel1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekk av dekn./lagerbeholdning på varenivå
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_lager     begsr
      *
     c                   eval      b_dekn = *off
      *
     c                   eval      fodtl1_numm = fonumm
     c                   eval      fodtl1_suff = fosuff
      *
     c     fodtl1_key    setll     fodtl1
     c     fodtl1_key    reade     fodtl1
     c                   dow       not %eof
      *
     c                   eval      vlagl1_vare = fdvare
     c                   eval      vlagl1_lage = fdlage
     c     vlagl1_key    chain     vlagl1
      *
     c                   if        %found
     c                   if        vlbehl >= fdanta
     c                   eval      b_dekn = *on
     c                   leave
     c                   endif
     c                   endif
      *
     c     fodtl1_key    reade     fodtl1
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å skrive plukkliste samt å bestemme hvem som skal
      * plkke ordren
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     klar_plukking begsr
      *
      * Tar bort rest kode på ordre når den sendes til plukking igjen
      *
     c***                eval      fohelu_numm = b1numm
     c***                eval      fohelu_suff = b1suff
     c***  fohelu_key    chain     fohelu
     c***                if        not %found
     c***                leavesr
     c***                endif
     c*****              eval      forest = 0
      *
     c***                update    fohelur
      *
5.63  *
5.63  * Oppdaterer loggfilen først med "Tilbakestilling"
5.63  *
5.63 c                   eval      d_firm = l_firm
5.63 c                   eval      d_user = l_user
5.63 c                   eval      d_wsid = l_wsid
5.63 c                   eval      d_numm = b1numm
5.63 c                   eval      d_suff = b1suff
5.63 c                   eval      d_kode = '9'
5.63 c                   time                    d_date
5.63 c                   time                    d_time
5.63 c                   eval      d_aarr = %subdt(b1bdat:*years)
5.63  *
5.63  * Kaller program for oppdatering
5.63  *
5.63 c                   call      'FU900R'
5.63 c                   parm                    w_stat
5.63 c                   parm                    d_urec
5.54  *
5.54  * Oppdaterer loggfilen med "Klar til plukking"
5.54  *
5.54 c                   eval      d_firm = l_firm
5.54 c                   eval      d_user = l_user
5.54 c                   eval      d_wsid = l_wsid
5.54 c                   eval      d_numm = b1numm
5.54 c                   eval      d_suff = b1suff
5.54 c                   eval      d_kode = '2'
5.54 c                   time                    d_date
5.54 c                   time                    d_time
5.60 c                   eval      d_aarr = %subdt(b1bdat:*years)
5.54  *
5.54  * Kaller program for oppdatering
5.54  *
5.54 c                   call      'FU900R'
5.54 c                   parm                    w_stat
5.54 c                   parm                    d_urec
8.03  *
8.03  * Hvis vi kjører med integrasjon til transportstyrer, må vi sende request.
8.03  * - sjekk at forsendelsesmåten har flåtestyring.
8.03  *
8.03 c                   if        u_803 = *on
8.03  *
8.04 c*                  eval      fohel1_numm = b1numm
8.04 c*                  eval      fohel1_suff = b1suff
8.04 c*    fohel1_key    chain     fohel1
8.04 c*                  if        %found
8.03  *
8.04 c*                  eval      raf2l1_2kod = folmat
8.04 c*    raf2l1_key    chain     raf2l1r
8.04 c*                  if        %found and
8.04 c*                            ra2flt = 1
8.03  * Styres pr lager
8.04 c*                  eval      ftrli1_llag = folage
8.04 c*    ftrli1_key    chain     ftrli1r
8.04 c*                  if        %found and
8.04 c*                            ftlflt = 1
8.03  *
8.03  * Hent info/id om ordren, hvis den allerde er sendt
8.03  *
8.04 c*                  eval      w_roid = *blanks
8.03  *
8.04 c*                  eval      ftrpi2_rnum = b1numm
8.04 c*                  eval      ftrpi2_rsuf = b1suff
8.04 c*    ftrpi2_key    chain     ftrpi2r
8.04 c*                  if        %found
8.04 c*                  eval      w_roid = ftroid
8.04 c*                  endif
8.04 c*                  call      'AP055R '
8.04 c*                  parm                    fofirm
8.04 c*                  parm                    b1numm
8.04 c*                  parm                    b1suff
8.04 c*                  parm                    w_roid
8.04 c*                  parm                    ra2fid
8.04 c*                  parm                    w_reqs
8.05 c                   eval      w_skaf = 0
8.06 c                   eval      w_logg = 0
8.04 c                   call      'AP058R '
8.04 c                   parm                    fofirm
8.04 c                   parm                    b1numm
8.04 c                   parm                    b1suff
8.05 c                   parm                    w_skaf
8.06 c                   parm                    w_logg
8.04 c                   parm                    w_reqs
8.04 c*                  endif
8.04 c*                  endif
8.04 c*                  endif
8.03  *
8.03 c                   endif
      *
     c                   eval      b_forn = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xsjekk_inp    begsr
      *
     c                   eval      b_feil = *off
      *
      * Dato
      *
     c                   if        b2dato <> 0
     c     *dmy          test(d)                 b2dato                 34
     c                   if        *in34  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sjekke om rad skal være med i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     chk_subfile   begsr
      *
     c                   eval      b_subf = *off
7.02  **
7.02  ** Kun de med forest kode lik 1 skal med
7.02  **
7.02 c*                   eval      w_aarr = %subdt(fobdat:*years)
7.02 c*                   eval      w_kode = *blank
7.02 c*                   call      'FU706R'
7.02 c*                   parm                    w_aarr
7.02 c*                   parm                    fonumm
7.02 c*                   parm                    fosuff
7.02 c*                   parm                    w_kode
7.02 c*                   if        forest <> 1  and
7.02 c*                             w_kode <> '9'
7.02 c*                   goto      xdropp
7.02 c*                   endif
7.02  **
7.02  ** Sjekker om siste kode = 8 eller 9
7.02  **
7.02 c*                   if        w_kode < '8'
7.02 c*                   goto      xdropp
7.02 c*                   endif
5.62  *
5.62  * Sjekker lagerfilter
5.62  *
5.62 c                   if        h1lage <> 0
5.62 c                   exsr      xsjekk_lag
5.62 c                   if        b_funn = ' '
3.31 c                   goto      xdropp
5.62 c                   endif
5.62 c                   endif
      *
     c                   eval      votyl1_otyp = footyp
     c     votyl1_key    chain     votyl1
     c                   if        not %found or
     c                             vaoakk <> 1
3.31 c                   goto      xdropp
     c                   endif
      *
5.42  * ta med bare de med lagerbeholdning
      *
     c                   if        h1levk = '1'
     c                   exsr      sjk_lager
     c                   if        b_dekn = *off
3.31 c                   goto      xdropp
     c                   endif
     c                   endif
7.02  *
7.02  * Kun de med forest kode lik 1 skal med
7.02  *
7.02 c                   eval      w_aarr = %subdt(fobdat:*years)
7.02 c                   eval      w_kode = *blank
7.02 c                   call      'FU706R'
7.02 c                   parm                    w_aarr
7.02 c                   parm                    fonumm
7.02 c                   parm                    fosuff
7.02 c                   parm                    w_kode
7.02 c                   if        forest <> 1  and
7.02 c                             w_kode <> '9'
7.02 c                   goto      xdropp
7.02 c                   endif
7.02  *
7.02  * Sjekker om siste kode = 8 eller 9
7.02  *
7.02 c                   if        w_kode < '8'
7.02 c                   goto      xdropp
7.02 c                   endif
      *
3.31 c     xtamed        tag
3.39 c                   eval      b_subf = *off
     c                   leavesr
      *
5.20 c     xdropp        tag
3.31  *
3.31 c                   eval      b_subf = *on
     c                   leavesr
3.31  *
      *
     c                   endsr
5.62  *
5.62  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.62  * Sjekker om lager finnes på ordrelinje
5.62  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.62  *
5.62 c     xsjekk_lag    begsr
5.62  *
5.62  * Sjekker om oppgitt lagernummer er representert på
5.62  * en av varelinjene på ordren
5.62  *
5.62 c                   eval      b_funn = ' '
5.62 c                   eval      fodtl1_numm = fonumm
5.62 c                   eval      fodtl1_suff = fosuff
5.62 c     fodtl1_key    setll     fodtl1
5.62 c     fodtl1_key    reade     fodtl1
5.62  *
5.62 c                   dow       not %eof
5.62 c                   if        fdlage = h1lage
5.62 c                   eval      b_funn = '1'
5.62 c                   leave
5.62 c                   endif
5.62 c     fodtl1_key    reade     fodtl1
5.62 c                   enddo
5.62  *
5.62 c                   endsr
      *
      *
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  * Newlook vask av data
7.01  *  Fjerner tegn som gjør at Newlook feiltolker
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *
7.01   begsr newlo_vask;
7.01     for w_teller = 1 to 5 ;
7.01       w_len = %len(%trim(b1knav));
7.01       if (w_len > 0);
7.01       b1knav = %ScanRpl('.':'':b1knav:w_len); // newlook tåler ikke . på slutten
7.01       endif;
7.01       w_len = %len(%trim(b1knav));
7.01       if (w_len > 0);
7.01       b1knav = %ScanRpl(':':'':b1knav:w_len); // newlook tåler ikke : på slutten
7.01       endif;
7.01       w_len = %len(%trim(b1vadr));
7.01       if (w_len > 0);
7.01       b1vadr = %ScanRpl('.':'':b1vadr:w_len); // newlook tåler ikke . på slutten
7.01       endif;
7.01       w_len = %len(%trim(b1vadr));
7.01       if (w_len > 0);
7.01       b1vadr = %ScanRpl(':':'':b1vadr:w_len); // newlook tåler ikke : på slutten
7.01       endif;
7.01     endfor ;
7.01   endsr;
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppdatering av ordre-hode
      *
     c     fohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelu_numm
     c                   kfld                    fohelu_suff
      *
      *  Key for les av ordrehode
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c*                  kfld                    fohel1_suff
      *
      * Key for posisjonering på ordre-dato
      *
     c     foheld_key    klist
     c                   kfld                    w_firm
     c                   kfld                    foheld_bdat
      *
      *  Key for les av ordrelinje
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
      *
      *  Key for les av kundereg.
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for les av ordretype-register
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for oppslag på lagerregister
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl1_vare
     c                   kfld                    vlagl1_lage
      *
      * Key for oppslag på ordre informasjonskode
      *
     c     vinfl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vinfl1_ityp
      *
      * Key for oppslag på ofak-koderegister
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
5.55  *
5.55  * Key for les av bruker
5.55  *
5.55 c     ausrlr_key    klist
5.55 c                   kfld                    ausrlr_user
8.03  *
8.03  * Key for oppslag på odre-hode - transport info
8.03  *
8.04 c*    ftrpi2_key    klist
8.04 c*                  kfld                    w_firm
8.04 c*                  kfld                    ftrpi2_rnum
8.04 c*                  kfld                    ftrpi2_rsuf
8.03  *
8.04 c*    raf2l1_key    klist
8.04 c*                  kfld                    w_firm
8.04 c*                  kfld                    raf2l1_2kod
8.03  *
8.03  * Key for oppslag på flåtestyring pr lager
8.03  *
8.04 c*    ftrli1_key    klist
8.04 c*                  kfld                    w_firm
8.04 c*                  kfld                    ftrli1_llag
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
      * hente lager fra bruker
      *
     c                   eval      ausrlr_user = l_user
     c     ausrlr_key    chain     ausrlr
     c                   if        %found(ausrlr)
     c                   eval      h1lage = ablagr
     c                   endif
     c
      *
      * Henter firmanummer/bruker fra LDA
      *
     c                   eval      w_firm = l_firm
     c                   eval      h1levk = *blank
      *
     c                   eval      *in22 = *on
      *
      * Henter statusopplysninger fra status-register
      *
     c     fstsl1_key    chain     fstsl1
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      fohel1_numm = 0
     c*                  eval      fohel1_suff = 0
     c     fohel1_key    setll     fohel1
8.03  *
8.03  * Endring 8.03 - Koblet til ekstern flåtestyring
8.03  *
8.03   CallP CO402R(l_filg:w_firm:0:'EKSTERN_FLATESTYRING':
8.03                    co402_verdi1:co402_verdi2);
8.03   if co402_verdi1 = '1';
8.03     u_803 = *on;
8.03   endif;
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
