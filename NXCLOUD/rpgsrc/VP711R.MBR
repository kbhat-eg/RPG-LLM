     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VP711R
      * Beskrivelse . . : Vare-pris, henter salgsenhet-1 m/priser på hovedleverandør
      *
      *                   Input:  Firma, vare, prisgruppe, lev.type,
      *                            dato og tidspunkt
      *                   Output: Enhet, salgspris, ret.kode
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       050511 bof  Nytt program
      *
8.01  *PRG2   150622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvprl1    if   e           k disk    rename(vvprpfr:vvprl1r)
     fvvenl2    if   e           k disk    rename(vvenpfr:vvenl2r)
6.20 fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
      *
      * Definering av pamametre
      *
     d p_firm          s                   like(vpfirm)
     d p_vare          s                   like(vpvare)
5.70 d p_prgr          s                   like(vpprgr)
     d p_lety          s                   like(vplety)
     d p_dato          s                   like(vppdat)
     d p_enhe          s                   like(vpenhe)
     d p_sapr          s                   like(vpsapr)
     d p_retk          s              1
      *
      * Definering av variable i nøkler
      *
     d vvprl1_vare     s                   like(vpvare)
6.20 d vvprl1_ldor     s                   like(vpldor)
5.70 d vvprl1_prgr     s                   like(vpprgr)
     d vvprl1_enhe     s                   like(vpenhe)
     d vvprl1_lety     s                   like(vplety)
     d vvenl2_vare     s                   like(vevare)
     d vvenl2_saen     s                   like(vesaen)
6.20 d vvarl1_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d b_pris          s              1    inz(*off)
6.20 d b_inlr          s              1    inz(*off)
      *
     d w_firm          s                   like(vpfirm)
     d w_dato          s                   like(vppdat)
6.20 d w_ldor          s                   like(vvldor)
6.20 d w_lage          s              2  0
6.21 d w_lety          s                   like(vplety)
6.21 d w_prgr          s                   like(vpprgr)
6.21 d w_pldo          s                   like(vpldor)
      *
      *
6.20 d                uds
6.20 d l_lage               1001   1002  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20 c                   eval      w_lage = l_lage
      *
6.20 c                   eval      vvarl1_vare = p_vare
6.20 c     vvarl1_key    chain     vvarl1
      *
      * Leser salgsenheter på vare, og henter prisinformasjon
      *
     c                   eval      b_pris = *off
      *
     c                   eval      vvenl2_vare = p_vare
     c                   eval      vvenl2_saen = 1
     c     vvenl2_key    setll     vvenl2
     c     vvenl2_key2   reade     vvenl2                                 90
     c                   dow       *in90 = *off and
     c                             b_pris = *off
      *
6.20  * 1. leverandør for lageret
6.20 c                   eval      vvprl1_ldor = vvldor
6.20 c                   exsr      hent_pris
     c     vvenl2_key2   reade     vvenl2                                 90
     c                   enddo
      *
     c                   if        b_pris = *off
     c                   eval      p_retk = 'F'
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for hendting av prisinformasjon for salgsenhet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_pris     begsr
      *
     c                   eval      vvprl1_vare = p_vare
5.70 c                   eval      vvprl1_prgr = p_prgr
     c                   eval      vvprl1_enhe = veenhe
     c                   eval      vvprl1_lety = p_lety
     c     vvprl1_key    setll     vvprl1
     c     vvprl1_key    reade     vvprl1                                 91
     c                   dow       *in91 = *off
      *
     c                   eval      b_pris = *on
     c                   eval      p_enhe = vpenhe
     c                   eval      p_sapr = vpsapr
      *
     c                   if        w_dato >= vppdat
     c                   leave
     c                   endif
      *
     c     vvprl1_key    reade     vvprl1                                 91
     c                   enddo
      *
      * Dersom pris ikke finnes for oppgitt leveringstype, hentes pris fra
      * leveringstype 0
      *
     c                   if        b_pris = *off and
     c                             p_lety <> 0
      *
     c                   eval      vvprl1_vare = p_vare
5.70 c                   eval      vvprl1_prgr = p_prgr
     c                   eval      vvprl1_enhe = veenhe
     c                   eval      vvprl1_lety = 0
     c     vvprl1_key    setll     vvprl1
     c     vvprl1_key    reade     vvprl1                                 91
     c                   dow       *in91 = *off
      *
     c                   eval      b_pris = *on
     c                   eval      p_enhe = vpenhe
     c                   eval      p_sapr = vpsapr
      *
     c                   if        w_dato >= vppdat
     c                   leave
     c                   endif
      *
     c     vvprl1_key    reade     vvprl1                                 91
     c                   enddo
      *
     c                   endif
5.70  *
5.70  * Dersom pris ikke finnes for oppgitt prisgruppe, hentes pris fra
5.70  * prisgruppe = blank
5.70  *
5.70 c                   if        b_pris = *off and
5.70 c                             p_prgr <> *blank
5.70 c                   eval      vvprl1_vare = p_vare
5.70 c                   eval      vvprl1_prgr = *blank
5.70 c                   eval      vvprl1_enhe = veenhe
5.70 c                   eval      vvprl1_lety = p_lety
5.70 c     vvprl1_key    setll     vvprl1
5.70 c     vvprl1_key    reade     vvprl1                                 91
5.70 c                   dow       *in91 = *off
5.70  *
5.70 c                   eval      b_pris = *on
5.70 c                   eval      p_enhe = vpenhe
5.70 c                   eval      p_sapr = vpsapr
5.70  *
5.70 c                   if        w_dato >= vppdat
5.70 c                   leave
5.70 c                   endif
5.70  *
5.70 c     vvprl1_key    reade     vvprl1                                 91
5.70 c                   enddo
5.70  *
5.70  * Dersom pris ikke finnes for oppgitt leveringstype, hentes pris fra
5.70  * leveringstype 0
5.70  *
5.70 c                   if        b_pris = *off and
5.70 c                             p_lety <> 0
5.70  *
5.70 c                   eval      vvprl1_vare = p_vare
5.70 c                   eval      vvprl1_prgr = *blank
5.70 c                   eval      vvprl1_enhe = veenhe
5.70 c                   eval      vvprl1_lety = 0
5.70 c     vvprl1_key    setll     vvprl1
5.70 c     vvprl1_key    reade     vvprl1                                 91
5.70 c                   dow       *in91 = *off
5.70  *
5.70 c                   eval      b_pris = *on
5.70 c                   eval      p_enhe = vpenhe
5.70 c                   eval      p_sapr = vpsapr
5.70  *
5.70 c                   if        w_dato >= vppdat
5.70 c                   leave
5.70 c                   endif
5.70  *
5.70 c     vvprl1_key    reade     vvprl1                                 91
5.70 c                   enddo
5.70  *
5.70 c                   endif
5.70  *
5.70 c                   endif
6.12  *
6.21 c                   eval      w_lety = vplety
6.21 c                   eval      w_prgr = vpprgr
6.21 c                   eval      w_pldo = vpldor
5.70  *
     c                   endsr
6.21  *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
5.70 c                   parm                    p_prgr
     c                   parm                    p_lety
     c                   parm                    p_dato
     c                   parm                    p_enhe
     c                   parm                    p_sapr
     c                   parm                    p_retk
      *
      * Key for les på vare-pris-register
      *
     c     vvprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvprl1_vare
6.20 c                   kfld                    vvprl1_ldor
5.70 c                   kfld                    vvprl1_prgr
     c                   kfld                    vvprl1_enhe
     c                   kfld                    vvprl1_lety
      *
      * Key for les på vare-enhet-register
      *
     c     vvenl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
     c                   kfld                    vvenl2_saen
      *
     c     vvenl2_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
6.20  *
6.20  * Key for les på vare
6.20  *
6.20 c     vvarl1_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    vvarl1_vare
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
      * Initierer dato og tidspunkt
      *
     c                   eval      w_dato = p_dato
      *
     c                   if        w_dato = *loval
     c                   time                    w_dato
     c                   endif
      *
      * Initierer output-parametre
      *
     c                   eval      p_enhe = *blank
     c                   eval      p_sapr = 0
      *
     c                   endsr
