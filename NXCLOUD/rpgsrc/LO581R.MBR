     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LO581R
      * Beskrivelse . . : Skriver logg, og sender denne til angitt e-post
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       110411 bsa  Nytt program
6.20  *       050112 bsa  Hvis det ikke er logget linjer, skal vi ikke sende
6.20  *                   info.
6.nu  *       230614 bof  Endret ihht. nummerutvidelse
6.30  *       240516 bsa  Bruker avsender hvis finnes.
6.31  * R6.30 170117 bsa  Rokkert på rekkefølge av close printerfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffwbolu    uf   e           k disk    rename(fwbopfr:fwbolur)
     Flo581p    o    e             printer
     f                                     usropn
      *
      *  Definering av datastrukturer
      *
      * Definering av variable i nøkler
      *
     d fwbolu_numm     s                   like(wfnumm)
     d fwbolu_suff     s                   like(wfsuff)
      *
      * Definering av øvrige variable
      *
     d w_firm          s                   like(wffirm)
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_fra           s             50
     d w_subj          s            200
     d w_mtxt          s            200
     d w_tell          s              3  0
      *
6.nu d p_numm          s              8  0
     d p_suff          s              2  0
     d p_emai          s             50
     d p_ema2          s             50
     d p_emne          s             50
     d p_txt1          s             50
     d p_txt2          s             50
     d p_txt3          s             50
     d p_txt4          s             50
     d p_navn          s             30
     d p_pers          s             30
      *
     d b_prnt          s              1    inz(*off)
6.20 d b_send          s              1    inz(*off)
      *
     d qsprilsp        pr                  extpgm('QSPRILSP')
     d rcvvar                     32767a   options(*varsize)
     d rcvvarlen                     10i 0 const
     d format                         8a   const
     d errorcode                  32767a   options(*varsize)
      *
     d sprl0100        ds
     d  bytesrtn                     10i 0
     d  bytesavl                     10i 0
     d  splfname                     10a
     d  jobname                      10a
     d  username                     10a
     d  jobnbr                        6a
     d  splfnbr                      10i 0
     d  systemname                    8a
     d  createdate                    7a
     d                                1a
     d  createtime                    6a
      *
     d errorcode       ds
     d  bytesprov                    10i 0 inz(0)
     d  byteavail                    10i 0 inz(0)
      *
      * Local Data Area
      *
     d                uds
     d l_arch                636    636  0
     d l_skje                637    637  0
     d l_mail                640    640  0
     d l_ruti                800    809
     d l_wsid                901    910
     d l_user                        10
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram - leser rader fra vare for utplukks-test
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hvis mailkode er satt til 0, skal det kun ryddes i loggen
     c                   if        l_mail = 1
      *
     c     fwbolu_key    reade     fwbolu                                 91
      *
     c                   dow       *in91 = *off
      *
     c                   exsr      prt_line
      *
     c     fwbolu_key    reade     fwbolu                                 91
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Generer xml for videre utskrift
      *
     c                   eval      t1post = w_tell
     c                   write     t1tot
      *
6.20 c                   if        b_send = *on
      *
     c                   exsr      spoolnbr
6.31 c                   close     lo581p
     c                   exsr      xml_create
      *
     c                   endif
      *
6.20 c                   endif
      * Rydd opp i workfile
     c                   exsr      rydd_wf
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Skriv linja som "burde" vært laget
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     prt_line      begsr
      * Første gang skrives heading, deretter etter behov
     c                   if        b_prnt = *off
     c                   eval      a1firm = l_firm
     c                   eval      a1fnav = l_fnav
     c                   eval      a1onum = wfnumm
     c                   eval      a1bnum = wfbnum
     c                   write     a1hdr
     c                   eval      b_prnt = *on
     c                   endif
      * Klargjør linjene
     c                   eval      d1vare = wfvare
     c                   eval      d1type = wftype
     c                   eval      d1tek1 = wftek1
     c                   eval      d1anta = wfanta
     c                   eval      d1enh1 = wfenhe
     c                   eval      d1sapr = wfsapr
     c                   eval      d1sums = wfsums
      *
     c                   write     d1det                                30
6.20 c                   eval      b_send = *on
     c                   if        wftype = 'FRA'
     c                   eval      w_tell = w_tell +1
     c                   endif
      *
      * Er det overflow ?
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Finner spool som er generert av jobben
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     c     spoolnbr      begsr
      /Free

               QSPRILSP( SPRL0100
                       : %size(SPRL0100)
                       : 'SPRL0100'
                       : errorcode );
      /End-free
     c*
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Slett i arbeidsregister
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     rydd_wf       begsr
      *
     c                   eval      fwbolu_numm = p_numm
     c                   eval      fwbolu_suff = p_suff
     c     fwbolu_key    setll     fwbolur
     c     fwbolu_key    reade     fwbolur
     c                   dow       not %eof
     c                   delete    fwbolur
     c     fwbolu_key    reade     fwbolur
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Kaller eget program for generering av xml fil. Eget pgm         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     c     xml_create    begsr
      *
     c                   eval      l_ruti = 'LO580'
6.30 c                   if        %trim(p_ema2) = *blanks
     c                   eval      w_fra = 'kvittering@asp-as.no'
6.30 c                   else
6.30 c                   eval      w_fra = %trim(p_ema2)
6.30 c                   endif
     c                   eval      w_subj = 'Endringer i ordre '+ %char(wfnumm)
     c**                 eval      w_mtxt = 'Vedlagt PDF inneholder endringer +
     c**                           av ordrelinjer ved oppdateirng av bestilling'
      *
     c                   call      'AP626R'
     c                   parm                    splfname
     c                   parm                    jobname
     c                   parm                    username
     c                   parm                    jobnbr
     c                   parm                    splfnbr
     c                   parm                    p_emai
     c                   parm                    w_fra
     c                   parm                    p_emne
     c                   parm                    p_txt1
     c                   parm                    p_txt2
     c                   parm                    p_txt3
     c                   parm                    p_txt4
     c                   parm                    p_pers
     c                   parm                    p_ema2
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_emai
     c                   parm                    p_emne
     c                   parm                    p_txt1
     c                   parm                    p_txt2
     c                   parm                    p_txt3
     c                   parm                    p_txt4
     c                   parm                    p_pers
     c                   parm                    p_ema2
      *
     c                   in        *dtaara
      *
      * Key for les av leverandør distribusjon
      *
     c     fwbolu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fwbolu_numm
     c                   kfld                    fwbolu_suff
      *
      *  Legge inn startverdier
      *
     c                   eval      w_firm = l_firm
     c                   time                    w_date
     c                   time                    w_time
     c                   eval      w_tell = 0
     c                   open      lo581p
      *
      *  Legger inn startverdi for lesingen
      *
     c                   eval      fwbolu_numm = p_numm
     c                   eval      fwbolu_suff = p_suff
     c     fwbolu_key    setll     fwbolur
      *
     c                   endsr
