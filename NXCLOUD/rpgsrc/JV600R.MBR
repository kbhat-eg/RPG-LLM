     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JV600R
      * Beskrivelse . . : Utskrift av vareregister, parameterprogram
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       130596 HKO  Nytt program
7.xx  * 7.00  010816 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
      *
     Fjv600d    cf   e             workstn
      *
      * Local data area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Datastruktur for parameterliste -ut
      *
     d                 ds
     d d_list                       120
     d  d_firm                        3  0 overlay(d_list)
     d  d_valg                        1  0 overlay(d_list:4)
     d  d_ldor                        6  0 overlay(d_list:5)
     d  d_eusr                       10    overlay(d_list:11)
     d  d_ogrf                        2  0 overlay(d_list:21)
     d  d_hgrf                        2  0 overlay(d_list:23)
     d  d_ugrf                        3  0 overlay(d_list:25)
     d  d_ogrt                        2  0 overlay(d_list:28)
     d  d_hgrt                        2  0 overlay(d_list:30)
     d  d_ugrt                        3  0 overlay(d_list:32)
     d  d_varf                       15    overlay(d_list:35)
     d  d_vart                       15    overlay(d_list:50)
     d  d_edaf                         d   overlay(d_list:65) datfmt(*iso)
     d  d_edat                         d   overlay(d_list:75) datfmt(*iso)
      *
      * Datastruktur for varegruppe
      *
     d                 ds
     d d_vgrp                         7  0
     d  d_ogrp                        2  0 overlay(d_vgrp)
     d  d_hgrp                        2  0 overlay(d_vgrp:3)
     d  d_ugrp                        3  0 overlay(d_vgrp:5)
      *
      * Definering av variable i nøkler
      *
     d jlevl1_ldor     s                   like(jlldor)
      *
      * Definering av variable
      *
     d b_feil          s              1
      *
     d w_firm          s              3  0
     d w_navn          s             30
     d w_otxt          s             35
     d w_htxt          s             35
     d w_utxt          s             35
     d w_tek1          s             35
     d w_vgrf          s              7  0
     d w_vgrt          s              7  0
     d w_vare          s             15
     d w_edaf          s               d   datfmt(*iso)
     d w_edat          s               d   datfmt(*iso)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Behandler bildet
      *
     c     b1tag         tag
      *
     c                   exfmt     b1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b1tag
     c                   endsl
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1tag
     c                   endif
      *
     c                   exsr      flytt_par
      *
      * Kaller utskriftsprogram
      *
     c                   call      'JV600C'
     c                   parm                    d_list
      *
     c                   goto      b1tag
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørringer (FD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
     c                   if        w_afld = 'B1LDOR'
     c                   call      'JL500R'
     c                   parm                    b1ldor
     c                   parm                    w_navn
     c                   goto      end_spØrr
     c                   endif                                                           .....
      *
     c                   if        w_afld = 'B1OGRF'
     c                   eval      b1hgrf = 0
     c                   eval      b1ugrf = 0
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b1ogrf
     c                   parm                    w_otxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'B1HGRF'
     c                   eval      b1ugrf = 0
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    b1ogrf
     c                   parm                    b1hgrf
     c                   parm                    w_htxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'B1UGRF'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    b1ogrf
     c                   parm                    b1hgrf
     c                   parm                    b1ugrf
     c                   parm                    w_utxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'B1OGRT'
     c                   eval      b1hgrt = 0
     c                   eval      b1ugrt = 0
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b1ogrt
     c                   parm                    w_otxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'B1HGRT'
     c                   eval      b1ugrt = 0
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    b1ogrt
     c                   parm                    b1hgrt
     c                   parm                    w_htxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'B1UGRT'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    b1ogrt
     c                   parm                    b1hgrt
     c                   parm                    b1ugrt
     c                   parm                    w_utxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'B1VARF'
     c                   movel     b1varf        w_vare
     c                   call      'JV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_tek1
     c                   movel     w_vare        b1varf
     c                   goto      end_spØrr
     c                   endif                                                           .....
      *
     c                   if        w_afld = 'B1VART'
     c                   movel     b1vart        w_vare
     c                   call      'JV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_tek1
     c                   movel     w_vare        b1vart
     c                   goto      end_spØrr
     c                   endif                                                           .....
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
      * Leverandør
      *
     c                   if        b1ldor <> 0
     c                   eval      jlevl1_ldor = b1ldor
     c     jlevl1_key    chain     jlevl1                             31
     c                   if        *in31 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
      * Varegruppe
      *
     c                   if        b1ogrf = 0 and
     c                             b1hgrf <> 0
     c                   eval      *in32 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c                   if        b1hgrf = 0 and
     c                             b1ugrf <> 0
     c                   eval      *in33 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c                   if        b1ogrt = 0 and
     c                             b1hgrt <> 0
     c                   eval      *in34 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c                   if        b1hgrt = 0 and
     c                             b1ugrt <> 0
     c                   eval      *in35 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c                   eval      d_ogrp = b1ogrf
     c                   eval      d_hgrp = b1hgrf
     c                   eval      d_ugrp = b1ugrf
     c                   eval      w_vgrf = d_vgrp
      *
     c                   eval      d_ogrp = b1ogrt
     c                   eval      d_hgrp = b1hgrt
     c                   eval      d_ugrp = b1ugrt
     c                   eval      w_vgrt = d_vgrp
      *
     c                   if        w_vgrf <> 0 and
     c                             w_vgrt = 0
     c                   eval      b1ogrt = b1ogrf
     c                   eval      b1hgrt = b1hgrf
     c                   eval      b1ugrt = b1ugrf
     c                   eval      w_vgrt = w_vgrf
     c                   endif
      *
     c                   if        w_vgrf > w_vgrt
     c                   eval      *in36 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * Vare
      *
     c                   if        b1vart = 0 and
     c                             b1varf <> 0
     c                   eval      b1vart = b1varf
     c                   endif
      *
     c                   if        b1varf > b1vart
     c                   eval      *in37 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * Endringsdato
      *
     c                   if        b1edaf <> 0
     c     *dmy          test(d)                 b1edaf                 38
     c                   if        *in38 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
     c                   if        b1edat <> 0
     c     *dmy          test(d)                 b1edat                 39
     c                   if        *in39 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
     c                   if        b1edaf <> 0 and
     c                             b1edat = 0
     c                   eval      b1edat = b1edaf
     c                   endif
      *
     c                   if        b1edaf <> 0
     c     *dmy          move      b1edaf        w_edaf
     c     *dmy          move      b1edat        w_edat
     c                   if        w_edaf > w_edat
     c                   eval      *in40 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for flytting av felter til parametre                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     flytt_par     begsr
      *
     c                   eval      d_firm = l_firm
      *
     c                   eval      d_valg = b1valg
     c                   eval      d_ldor = b1ldor
     c                   eval      d_eusr = b1eusr
     c                   eval      d_ogrf = b1ogrf
     c                   eval      d_hgrf = b1hgrf
     c                   eval      d_ugrf = b1ugrf
     c                   eval      d_ogrt = b1ogrt
     c                   eval      d_hgrt = b1hgrt
     c                   eval      d_ugrt = b1ugrt
     c                   eval      d_varf = *blank
     c                   eval      d_vart = *blank
     c                   eval      d_edaf = *loval
     c                   eval      d_edat = *loval
      *
     c                   if        b1varf <> 0
     c                   movel     b1varf        d_varf
     c                   endif
      *
     c                   if        b1vart <> 0
     c                   movel     b1vart        d_vart
     c                   endif
      *
     c                   if        b1edaf <> 0
     c     *dmy          move      b1edaf        d_edaf
     c                   endif
      *
     c                   if        b1edat <> 0
     c     *dmy          move      b1edat        d_edat
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Henter firma fra LDA, og initiering av skjermfelter
      *
     c                   eval      b1valg = 1
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
