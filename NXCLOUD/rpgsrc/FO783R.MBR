     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FO783R
      * Beskrivelse . . : ByggDOk, omkjøring av kunde/kundeprosjekt
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       040113 bof  Nytt program
7.xx  * 7.00  010416 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
6.31  *       190516 bof  Utvidet med parameter for Byggdok eller Cobuilder
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
      *
     fFO783d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d rkunl1_kund     s                   like(rkkund)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(rkfirm)
     d w_paar          s              4  0
     d w_kund          s                   like(rkkund)
     d w_kpro          s                   like(flkpro)
     d w_knav          s                   like(rknavn)
     d w_pnav          s                   like(flnavn)
6.31 d w_bdok          s              1  0
6.31 d w_cobu          s              1  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send bilde
      *
     c     b1taga        tag
      *
     c                   exfmt     b1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc = *on or *inkl = *on
     c                   goto      avslutt
     c                   when      *inkd = *on
     c                   exsr      spØrring
     c                   goto      b1taga
     c                   endsl
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   eval      b_feil = *off
     c                   goto      b1taga
     c                   endif
      *
      * Kaller oppdateringsprogram
      *
     c                   move      b1paar        w_paar
     c                   move      b1vkun        w_kund
     c                   move      b1kpro        w_kpro
6.31 c                   move      b1bdok        w_bdok
6.31 c                   move      b1cobu        w_cobu
      *
     c                   call      'FO784R'
     c                   parm                    w_paar
     c                   parm                    w_kund
     c                   parm                    w_kpro
6.31 c                   parm                    w_bdok
6.31 c                   parm                    w_cobu
      *
      * Viser bildet på nytt
      *
     c                   eval      b1paar = 0
     c                   eval      b1vkun = 0
     c                   eval      b1kpro = 0
      *
     c                   goto      b1taga
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Varenummer
      *
     c                   if        w_afld = 'B1VKUN'
      *
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    b1vkun
     c                   parm                    w_knav
      *
     c                   endif
      * Kundeprosjekt
      *
     c                   if        w_afld = 'B2KPRO'
     c                   call      'FL500R'
     c                   parm                    w_firm
     c                   parm                    b1vkun
     c                   parm                    b1kpro
     c                   parm                    w_knav
     c                   parm                    w_pnav
     c                   leavesr
     c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
      *
      * År
      *
     c                   if        b1paar = 0
     c                   eval      *in31  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * kundenr
      *
     c                   if        b1vkun = 0
     c                   eval      *in33  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c                   move      b1vkun        rkunl1_kund
     c     rkunl1_key    chain     rkunl1
     c                   if        not %found(rkunl1)
     c                   eval      *in33  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * kundeprosjekt
      *
     c                   if        b1kpro = 0
     c                   eval      *in35  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c                   movel     b1vkun        fkprl1_kund
     c                   movel     b1kpro        fkprl1_kpro
     c     fkprl1_key    chain     fkprl1                             90
     c                   if        not %found(fkprl1)
     c                   eval      *in35 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på kundeprosj
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
