     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASPGPL
      * Program . . . . : AN050R
      * Beskrivelse . . : Oppdatering av historikk ved nattjobb
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * R3.10 040400 AMD  Lagt inn mulighet for å oppdatere historikken
      *                   med informasjon som på forhånd er lagt opp i
      *                   LDA posisjon 701 - 740.  Kode for melding må
      *                   da settes til 99
6.11  *  6.10 070113 AMD  Utvidet med 3 nye meldinger for savefile
6.21  *  6.21 111213 AMD  Utvidet med 2 nye meldinger for savefile
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     fahstlu    uf a e           k disk    rename(ahstpfr:ahstlur)
      *
      * Definering av tabeller
      *
6.21 d txt             s             40    dim(30) ctdata perrcd(1)              Tekster
      *
      * Definering av datastrukturer
      *
     d                 ds
     d d_fdat                         8  0
     d  d_faar                        4  0 overlay(d_fdat)
     d  d_fmnd                        2  0 overlay(d_fdat:5)
     d  d_fdag                        2  0 overlay(d_fdat:7)
3.10  *
3.10  * Definering av local data area
3.10  *
3.10 d                uds
     d l_meld                701    740
      *                                                                     -
      * Definering av parametere
      *                                                                     -
     d p_bibl          s             10
     d p_kode          s              2  0
      *                                                                     -
      * Definering av variable
      *                                                                     -
     d n               s              2  0
     d ahstlu_dato     s                   like(ahdato)
     d ahstlu_ttid     s                   like(ahttid)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer historikkregister med status fra nattjobben
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Legger inn forsinkelse ved å kalle opp AN050C
      *
     c                   call      'AN050C'
      *
      * Tar hensyn til år
      * 2000.
      *
     c                   if        uyear > 80                                            .....
     c                   movel     19            d_faar                                      .
     c                   else                                                                .
     c                   movel     20            d_faar                                      .
     c                   endif                                                           .....
     c                   move      uyear         d_faar
     c                   eval      d_fmnd = umonth
     c                   eval      d_fdag = uday
      *
     c                   z-add     d_fdat        ahstlu_dato
     c                   time                    ahstlu_ttid
     c     ahstlu_key    chain     ahstlur                            60
      *
      * Oppdaterer historikkregister fra kode
      *
3.10 c                   if        n <> 99                                               .....
     c                   if        *in60 = *on                                           .....
     c                   eval      ahdato = ahstlu_dato                                      .
     c                   eval      ahttid = ahstlu_ttid                                      .
     c                   movel     txt(n)        ahtxt1                                      .
     c                   if        n = 15                                                ... .
     c                   movel     p_bibl        ahtxt1                                    . .
     c                   endif                                                           ... .
     c                   write     ahstlur                                                   .
     c                   endif                                                           .....
3.10 c                   endif                                                           .....
3.10  *
3.10  * Oppdaterer historikkregister fra LDA
3.10  *
3.10 c                   if        n = 99                                                .....
3.10 c                   if        *in60 = *on                                           .....
3.10 c                   eval      ahdato = ahstlu_dato                                      .
3.10 c                   eval      ahttid = ahstlu_ttid                                      .
3.10 c                   eval      ahtxt1 = l_meld                                           .
3.10 c                   write     ahstlur                                                   .
3.10 c                   endif                                                           .....
3.10 c                   endif                                                           .....
      *
      * Skal normalt ikke kunne komme inn her ...
      *
     c                   if        *in60 = *off                                          .....
     c                   movel     '*?*'         ahtxt1                                      .
     c                   update    ahstlur                                                   .
     c                   endif                                                           .....
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  END OF JOB
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
      *
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Definering av felter som benyttes i systemet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c                   z-add     0             n
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_kode
     c                   parm                    p_bibl
      *
      * Initiering av felter
      *
     c                   z-add     0             d_fdat
      *
      * Kode benyttes som index
      *
     c                   z-add     p_kode        n
      *
     c                   if        p_kode < 1
     c                   z-add     1             n
     c                   endif
      *
3.10 c                   if        p_kode <> 99
6.21 c                   if        p_kode > 30
     c                   z-add     1             n
     c                   endif
3.10 c                   endif
      *
      * Key for file med
      * historikkinformasjon
      *
     c     ahstlu_key    klist
     c                   kfld                    ahstlu_dato
     c                   kfld                    ahstlu_ttid
      *
     c                   endsr
**
Udefinert rutine . . . . . . . starter   TXT,1
Nattjobben . . . . . . . . . . starter * TXT,2
Standard backup. . . . . . . . starter   TXT,3
Reorganisering  av filer . . . starter   TXT,4
Ekstrajobb . . . . . . . . . . starter   TXT,5
Ekstrajobb fra andre system. . starter   TXT,6
PWRDWNSYS (IPL)  . . . . . . . starter   TXT,7
Backup av server (komplett). . starter   TXT,8
Backup av server (endringer) . starter   TXT,9
Nattjobben er avsluttet. . . . normalt   TXT,10
Backup-tape var ikke satt inn. *** obs ! TXT,11
Backup-tape var ikke ASBACK. . *** obs ! TXT,12
Backup ble ikke fullført . . . *** obs ! TXT,13
Klarte ikke initiere tapen . . *** obs ! TXT,14
xxxxxxxxxx ble ikke funnet . . *** obs ! TXT,15
Backup av mapper . . . . . . . starter   TXT,16
Backup av mapper ikke iorden . *** obs ! TXT,17
Backup av mapper er fullført . normalt   TXT,18
Backup av alle brukerbibl. . . starter   TXT,19
Backup avsluttet ikke normalt. *** obs ! TXT,20
Backup ble fullført. . . . . . normalt   TXT,21
Egen startrutine . . . . . . . starter   TXT,22
Egen sluttrutine . . . . . . . starter   TXT,23
Ekstrajobb ikke fullført . . . *** obs ! TXT,24
Ekstrajobb andre syst. feil. . *** obs ! TXT,25
Savefile renamet til ADB.SAVF1 . . .ok   TXT,26
Savefile renamet til ADB.SAVF2 . . .ok   TXT,27
Savefile renamet til ADB.SAVF3 . . .ok   TXT,28
Savefile er kjørt ut på tape . starter   TXT,29
ADB.SAVF er tømt igjen ! . . . starter   TXT,30
