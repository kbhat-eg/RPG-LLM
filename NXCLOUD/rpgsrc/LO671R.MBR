     H datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * System  . . . . : ASLAGR                                        *
      * Program . . . . : LO642R                                        *
      * Beskrivelse . . : Utskrift av øre-differanse liste              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * Spesialversjon. :                                               *
      * Tilpasset Lor . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
6.10  *           300113  Hvis fri ordre, bruk netto og ikke brutto bsa *
7.01  *           180915  Sjekker på valgte ordretyper              ask *
7.02  *           151118  Mva må beregnes fra linje, ikke bare brukebsa *
7.02  *                   flat prosentsats.                         bsa *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flodtlr    if   e           k disk    rename(lodtpfr:lodtlrr)
      *
     flo670p    o    e             printer
      *
      * Definering av Local Data Arae (LDA)
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d lodtlr_numm     s                   like(ldnumm)
     d lodtlr_suff     s                   like(ldsuff)
      *
      * Definering av variable
      *
     d w_firm          s                   like(lofirm)
     d w_toti          s             11  2
     d w_totr          s             11  2
7.02 d w_mvag          s             11  2
7.02 d w_mvab          s             11  2
7.02 d w_mvbb          s             11  2
7.02 d w_stat          s              1    inz(*off)
7.02 d w_mvak          s              1
7.02 d w_mdat          s               d   datfmt(*iso)
7.02 d w_mvtx          s             30
7.02 d w_mvap          s              6  2
7.02 d w_mvat          s              5  4
7.02 d w_mvaf          s             10  9
7.02 d w_bpro          s              5  2
     d w_grns          s              2  0
7.01 d w_otyp          s             12
7.01 d w_oty1          s              2
7.01 d w_oty2          s              2
7.01 d w_oty3          s              2
7.01 d w_oty4          s              2
7.01 d w_oty5          s              2
7.01 d w_oty6          s              2
7.01 d b_type          s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * A1HDR Skriv heading
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      a1grns = w_grns
     c                   write     a1hdr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * D1DET Behandle file
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Lese file
      *
     c     lohel1_key    setll     lohel1
     c     lohel1_key    reade     lohel1
      *
     c                   dow       not %eof
      *
7.01 c                   if        b_type = *on
7.01 c                   if        lootyp <> w_oty1 and
7.01 c                             lootyp <> w_oty2 and
7.01 c                             lootyp <> w_oty3 and
7.01 c                             lootyp <> w_oty4 and
7.01 c                             lootyp <> w_oty5 and
7.01 c                             lootyp <> w_oty6
7.01 c                   goto      les_neste
7.01 c                   endif
7.01 c                   endif
7.01  *
     c                   eval      d1numm = lonumm
     c                   eval      d1suff = losuff
     c                   eval      d1otyp = lootyp
     c                   eval      d1ldor = loldor
     c                   eval      d1navn = lolena
     c                   eval      d1lfak = losfak
      *
     c                   exsr      hent_total
      *
     c                   if        %abs(d1diff) > w_grns
     c                   write     d1det                                30
     c                   endif
      *
      * Er det overflow ?
      *
     c     *in30         ifeq      *on
     c                   write     a1hdr
     c                   endif
      *
7.01 c     les_neste     tag
     c     lohel1_key    reade     lohel1
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * T1TOT Behandle total T1
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Skrive total
      *
     c                   write     t1tot                                30
     c                   goto      avslutt
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av bestillingstotal
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_total    begsr
      *
     c                   eval      w_toti = 0
     c                   eval      w_totr = 0
7.02 c                   eval      w_mvbb = 0
     c                   eval      d1nett = 0
     c                   eval      d1mvab = 0
     c                   eval      d1lsum = 0
     c                   eval      d1totf = 0
     c                   eval      d1diff = 0
      *
     c                   eval      lodtlr_numm = lonumm
     c                   eval      lodtlr_suff = losuff
      *
     c     lodtlr_key    setll     lodtlr
     c     lodtlr_key    reade     lodtlr
     c                   dow       not %eof
     c                   eval      w_toti = w_toti + ldsumi
     c                   eval      w_totr = w_totr + ldsumr
7.02 c                   exsr      kalk_mva
7.02 c                   eval      w_mvbb = w_mvbb + w_mvab
     c     lodtlr_key    reade     lodtlr
     c                   enddo
      *
     c                   eval      d1nett = w_toti - w_totr
      *
      * Beregner faktura-diff
      *
6.10 c                   if        lomkod = 0
     c                   eval      d1totf = lototf
6.10 c                   else
6.10 c                   eval(h)   d1totf = d1nett
6.10 c                   endif
      *
     c                   if        lomkod = 0
7.02 c                   eval      d1mvab = w_mvbb
7.02 c**                 eval(h)   d1mvab = (d1nett * 25) / 100
     c                   else
     c                   eval      d1mvab = 0
     c                   endif
      *
     c                   eval      d1lsum = d1nett + d1mvab
     c                   eval(h)   d1diff = d1lsum - d1totf
      *
     c                   eval      t1diff = t1diff + d1diff
      *
     c                   endsr
      *
7.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.02  * Subrutine for beregning av mva beløp
7.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.02  *
7.02 c     kalk_mva      begsr
7.02  *
7.02 c                   eval      w_mvab = 0
7.02 c                   eval      w_mvap = 0
7.02  *
7.02 c                   if        ldomva <> *blank
7.02 c                   eval      w_stat = *blank
7.02 c                   eval      w_mvak = ldomva
7.02 c                   time                    w_mdat
7.02  *
7.02 c                   call      'RS205R'
7.02 c                   parm                    w_stat
7.02 c                   parm                    w_mvak
7.02 c                   parm                    w_mvtx
7.02 c                   parm                    w_mdat
7.02 c                   parm                    w_mvap
7.02 c                   parm                    w_mvat
7.02 c                   parm                    w_mvaf
7.02 c                   parm                    w_bpro
7.02  *
7.02 c                   if        w_mvap = 0
7.02 c                   eval      w_mvap = 1
7.02 c                   endif
7.02  *
7.02 c                   eval      w_mvag = ldsumi - ldsumr
7.02 c                   eval      w_mvab = ((w_mvag * w_mvap)/100)
7.02  *
7.02 c                   endif
7.02  *
7.02 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Tar inn parameter fra forrige program
      *
     c     *entry        plist
     c                   parm                    w_grns
7.01 c                   parm                    w_otyp
      *
      * Key for oppslag på odre-hode
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
      *
      * Key for les/oppslag på bestilling-linje
      *
     c     lodtlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlr_numm
     c                   kfld                    lodtlr_suff
      *
      * Key for oppslag på koderegister
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Legge inn key
      *
     c                   eval      a1fnav = l_fnav
     c                   eval      w_firm = l_firm
     c                   eval      a1firm = l_firm
7.01  *
7.01  *
7.01 c                   eval      w_oty1 = %subst(w_otyp:1:2)
7.01 c                   eval      w_oty2 = %subst(w_otyp:3:2)
7.01 c                   eval      w_oty3 = %subst(w_otyp:5:2)
7.01 c                   eval      w_oty4 = %subst(w_otyp:7:2)
7.01 c                   eval      w_oty5 = %subst(w_otyp:9:2)
7.01 c                   eval      w_oty6 = %subst(w_otyp:11:2)
7.01  *
7.01 c                   if        w_oty1 <> *blank or
7.01 c                             w_oty2 <> *blank or
7.01 c                             w_oty3 <> *blank or
7.01 c                             w_oty4 <> *blank or
7.01 c                             w_oty5 <> *blank or
7.01 c                             w_oty6 <> *blank
7.01 c                   eval      b_type = *on
7.01 c                   endif
7.01  *
      *
      * Henter statusopplysninger fra status-register
      *
     c     lstsl1_key    chain     lstsl1
      *
     c                   endsr
