     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JU510R
      * Beskrivelse . . : Modultekst, vise
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       280197 HKO  Nytt program
      *
6.30  *       9.02   NHO  Også vise varetekst fra JVTXPF
6.31  *       120418 bkv  Overskrift for vare og modul
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       21 = Home
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjmtxl1    if   e           k disk    rename(jmtxpfr:jmtxl1r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
6.30 fjvtxl1    if   e           k disk    rename(jvtxpfr:jvtxl1r)
      *
     fju510d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_vare          s                   like(jvvare)
      *
      * Definering av Local data area
      *
     d                uds
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av datastruktur, parameterliste -ut
      *
     d                 ds
     d d_list                        32
     d  d_firm                        3  0 overlay(d_list:1)
     d  d_vare                       15    overlay(d_list:4)
      *
      * Definering av variable i nøkler
      *
     d jmtxl1_tmod     s                   like(jutmod)
     d jvarl1_vare     s                   like(jvvare)
6.30 d jvtxl1_tvar     s                   like(jvtvar)
6.30 d jvtxl1_tlin     s                   like(jvtlin)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_firm          s              3  0
      *
6.31 d w_ovskv         s              1    inz(*off)                            overskrift vare
6.31 d w_ovskm         s              1    inz(*off)                            overskrift modul
      *
      * Konstanter
      *
     d c_sfil          s              2  0 inz(17)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile                                  DSP SFL
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * F7=Skriv ut
      *
     c                   if        *inkg
     c                   eval      d_firm = p_firm
     c                   eval      d_vare = p_vare
     c                   call      'JU600C'
     c                   parm                    d_list
     c                   goto      b2tagb
     c                   endif
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c     jmtxl1_key    setll     jmtxl1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
      * Fyller opp subfile
      *
     c                   dou       w_srrn = w_spge
      *
6.31  * Overskrift vare
6.31 c                   if        w_ovskv = *off
6.31 c                   eval      w_ovskv = *on
6.31 c                   eval      w_srrn = w_srrn + 1
6.31 c                   eval      b1ttxt = '*Produktinfo:'
6.31 c                   goto      etles
6.31 c                   endif
6.30  * Vis først tekst fra JVTXL1
6.30 c                   if        *in16 = *off
6.30 c                   read      jvtxl1                                 16
6.30 c*                                                                   15
6.30 c                   if        *in16 = *off and
6.30 c                             jvtvar = p_vare
6.30 c                   eval      w_srrn = w_srrn + 1
6.30 c                   eval      b1ttxt = jvttxt
6.30 c                   goto      etles
6.30 c                   else
6.30 c                   eval      *in16 = *on
6.31 c                   eval      w_srrn = w_srrn + 1
6.31 c                   eval      b1ttxt = ''                                  blank linje
6.31 c                   goto      etles
6.30 c                   endif
6.30 c                   endif
      *
6.31  * Overskrift modul
6.31 c                   if        w_ovskm = *off
6.31 c                   eval      w_ovskm = *on
6.31 c                   eval      w_srrn = w_srrn + 1
6.31 c                   eval      b1ttxt = '*Modulinfo:'
6.31 c                   goto      etles
6.31 c                   endif
      *
     c     jmtxl1_key    reade     jmtxl1                                 15
      *
     c                   if        *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
     c                   eval      b1ttxt = juttxt
6 30  *
6.30 c     etles         tag
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
      *
      * Key for les på modultekst
      *
     c     jmtxl1_key    klist
     c                   kfld                    jmtxl1_tmod
      *
6.30  * Key for les på varetekst
6.30  *
6.30 c     jvtxl1_key    klist
6.30 c                   kfld                    jvtxl1_tvar
6.30 c                   kfld                    jvtxl1_tlin
      *
      * Key for oppslag på vare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   eval      *in22 = *on
6.30  *
6.30  * Henter vareteks fra JVTXPF, og posisjonerer i databasen
6.30 c                   eval      jvtxl1_tvar = p_vare
6.30 c                   eval      jvtxl1_tlin = *zeros
6.30 c     jvtxl1_key    setll     jvtxl1                                                    .
      *
      * Henter modulnummer fra vare, og posisjonerer i databasen
      *
     c                   eval      jvarl1_vare = p_vare
     c     jvarl1_key    chain     jvarl1                             91                     .
      *
     c                   eval      jmtxl1_tmod = jvmodn
     c     jmtxl1_key    setll     jmtxl1                                                    .
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
