     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JM110R
      * Beskrivelse . . : Vedlikehold av kampanjepriser
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       270997 HKO  Nytt program
      *       130605 HKO  Endret parameter ved kall av JP500R
      *       190905 HKO  Fjernet test på at innkjøpspris i kampanje-
      *                   perioden må være utfylt
      *                   Viser Nobb grunnenhet
6.10  *       250809 bof  Fjernet 000 fra pakning, ligger på vvarpf
6.30  *       190214 bof  Utvidet med innkjøpsdato fra /til
6.31  *       270214 bof  Utvidet med lev.nr i kall til JW100R
6.32  *       130314 bof  Legge fra til dato fra hode,default på innkj.dato
6.33  *       070714 bkv  Låst lokalt og opprettet dato
6.34  * 6.30  201014 Bsa  Innfører F tast for visning av NOBB info. Erstatter
6.34  *                   koding i jacada.
6.3x  * 6.30  040615 bof  utvidet parm i forb. med prgr. på betingelse
6.35  * 6.30  240516 bof  liten justering ved utlegg av prisenhet.
6.36  * 6.30  180517 bof  Tillate å endre prisgiver
7.xx  * 7.00  171116 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 7.00  150120 bkv  Tillate at kostpris er = 0. Gipling
7.02  * 7.00  120220 bkv  Byttet om *on / *off i setting av switch u_701
      *                   (veldig rart valg gjort i 7.01.....)
7.04  *       110516 bof  Utvidet med prisgruppe
7.05  *       181116 bof  ta bort automatisk fra/til innkjøpsdato.
7.06  *       240417 bof  Feilretting vedr. prisgruppe
7.14  *(7.04) 120321 bof  NXS-1063 kunne se DG kalkulert fra VA
7.14  *(7.04) 070521 bof  Skal hent lev.nr fra lager evt. vare hvis lagervare
8.01  *PRG2   130622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjkavlr    if   e           k disk    rename(jkavpfr:jkavlrr)
     fjkavlu    uf a e           k disk    rename(jkavpfr:jkavlur)
     fjkavl1    if   e           k disk    rename(jkavpfr:jkavlr1)
     fjkavl2    if   e           k disk    rename(jkavpfr:jkavl2r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
6.32 fjkahl1    if   e           k disk    rename(jkahpfr:jkahl1r)
6.36 fjvlel1    if   e           k disk    rename(jvlepfr:jvlel1r)
7.14 fjvdtl1    if   e           k disk    rename(jvdtpfr:jvdtl1r)
7.14 fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
7.14 fvvprl1    if   e           k disk    rename(vvprpfr:vvprl1r)
      *
     fjm110d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(jmvfir)
     d p_kamp          s                   like(jmvkam)
6.34 d p_nobb          s              8
7.04 d p_prgr          s                   like(jmvprg)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
7.01 d l_filg                931    933
7.01 d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d jkavlu_vvar     s                   like(jmvvar)
     d jkavlr_vvar     s                   like(jmvvar)
     d jkavl1_vvar     s                   like(jmvvar)
     d jkavl2_vldo     s                   like(jmvldo)
     d jkavl2_vvar     s                   like(jmvvar)
     d jvarl1_vare     s                   like(jvvare)
     d jlevl1_ldor     s                   like(jlldor)
6.36 d jvlel1_lvnr     s                   like(jvlvnr)
6.36 d jvlel1_lldo     s                   like(jvlldo)
7.14 d jvdtl1_dvar     s                   like(jvdvar)
7.14 d vvarl1_vare     s                   like(vvvare)
7.14 d vvprl1_vare     s                   like(vpvare)
7.14 d vvprl1_ldor     s                   like(vpldor)
7.14 d vvprl1_prgr     s                   like(vpprgr)
7.14 d vvprl1_enhe     s                   like(vpenhe)
7.14 d vvprl1_lety     s                   like(vplety)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
7.05 d u_utgd          s              1    inz(*off)                            Sjekk utgårdato
      *
     d w_valg          s              1  0
     d w_lety          s              1  0
8.01 d w_prgr          s              2
      *
     d w_navn          s                   like(jlnavn)
     d w_tek1          s                   like(jvtek1)
     d w_vare          s                   like(jvvare)
     d w_pldo          s                   like(jvldor)
     d w_firm          s                   like(jmvfir)
     d w_kamp          s                   like(jmvkam)
6.30 d w_fdat          s               d   datfmt(*iso)
6.30 d w_tdat          s               d   datfmt(*iso)
6.31 d w_vldo          s                   like(jvldor)
      *
7.14 d w_pgiv          s                   like(jvldor)
7.14 d w_gprk          s              1  0
7.14 d w_bldo          s                   like(jvldor)
7.14 d w_bogr          s                   like(jvogrp)
7.14 d w_bhgr          s                   like(jvhgrp)
7.14 d w_bugr          s                   like(jvugrp)
7.14 d w_bmod          s                   like(jvmodn)
7.14 d w_bvar          s                   like(jvvare)
7.14 d w_eldo          s                   like(jvldor)
7.14 d w_ldor          s                   like(jvldor)
7.14 d w_lvar          s                   like(jvlvar)
7.14 d w_pogr          s                   like(jvogrp)
7.14 d w_phgr          s                   like(jvhgrp)
7.14 d w_pugr          s                   like(jvugrp)
7.14 d w_pmod          s                   like(jvmodn)
7.14 d w_pvar          s                   like(jvvare)
7.14 d w_nlev          s                   like(jvldor)
7.14 d w_levr          s                   like(jvldor)
7.14 d w_osts          s              1
7.14 d w_midl          s              1  0
7.14 d w_bvty          s              1
7.14 d w_blet          s              1  0
7.14 d w_bbet          s             35
7.14 d w_dekn          s              5  2
7.14 d w_enh           s              3
7.14 d w_enh1          s              3
7.14 d w_enh2          s              3
7.14 d w_enh3          s              3
7.14 d w_eni1          s              3
7.14 d w_eni2          s              3
7.14 d w_eni3          s              3
7.14 d w_enp1          s              3
7.14 d w_enp2          s              3
7.14 d w_enp3          s              3
7.14 d w_gdat          s               d   datfmt(*iso)
7.14 d w_grpr          s              9  2
7.14 d w_ifak          s              8  6
7.14 d w_inpr          s              9  2
7.144d w_kbel          s              7  2
7.14 d w_kfak          s              8  6
7.14 d w_kopr          s              9  2
7.14 d w_ntpr          s              8  2
7.14 d w_ofp1          s             12  6
7.14 d w_ofp2          s             12  6
7.14 d w_ofp3          s             12  6
7.14 d w_ofv2          s             12  6
7.14 d w_ofv3          s             12  6
7.14 d w_ofi1          s             12  6
7.14 d w_ofi2          s             12  6
7.14 d w_ofi3          s             12  6
7.14 d w_ppko          s              1
7.14 d w_pvty          s              1
7.14 d w_plet          s              1  0
7.14 d w_sfak          s              8  6
7.14 d w_s1im          s              9  2
7.14 d w_s1pr          s              9  2
7.14 d w_s2im          s              9  2
7.14 d w_s2pr          s              9  2
7.14 d w_s3im          s              9  2
7.14 d w_s3pr          s              9  2
7.14 d w_prut          s              1
7.14 d w_pgbe          s              1
7.14 d w_pgfr          s              1
7.14 d w_ffak          s              8  6
7.14 d w_inp2          s              9  2
7.14 d w_pdat          s                   like(jvodat)
7.14 d w_utgp          s              1  0
7.14 d w_oppd          s              1
7.14 d w_udat          s                   like(jvudat)
7.14 d w_mvap          s              6  2
7.14 d w_mvat          s              5  4
6.31 d w_mvaf          s             10  9
7.14 d w_saum          s              9  2
7.14 d w_glag          s              2  0
7.14 d w_enum          s              6  0
      *
     d s_vvar          s                   like(jmvvar)
7.14 d s_vkpr          s                   like(jmvkpr)
7.14 d s_kopr          s                   like(jmvkpr)
7.14 d s_kpim          s                   like(jmvkpr)
7.14 d s_vldo          s                   like(jmvldo)
7.14 d s_vkaa          s                   like(jmvkaa)
      *
7.01 d u_701           s              1    inz(*off)
7.14 d u_702           s              1    inz(*off)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(12)
      *
7.00  *
7.00  * Definering av eksterne prg
7.00  *
7.00   dcl-s co402_verdi1  char(1);
7.00   dcl-s co402_verdi2  char(2048);
7.00   DCL-PR CO402R EXTPGM('CO402R');
7.00     p_filgrp            char(3)     const;
7.00     p_firm              packed(3:0) const;
7.00     p_lager             packed(2:0) const;
7.00     p_nokkel            char(50)    const;
7.00     p_verdi1            char(1);
7.00     p_verdi2            char(2048);
7.00   END-PR;
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
7.14 C/Exec SQL
7.14 C+  Set Option DatFmt=*ISO, commit=*none
7.14 C/End-Exec
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2taga
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * F14=Påslag til kostpris
      *
     c                   if        *inkn
     c                   call      'JM113R'
     c                   parm                    p_firm
     c                   parm                    p_kamp
     c                   parm                    l_user
     c                   exsr      forny
     c                   goto      b2tagb
     c                   endif
      *
      * F13=Statuskoder
      *
     c                   if        *inkm
     c                   call      'JM112R'
     c                   goto      b2tagb
     c                   endif
      *
      * F16=Gruppebehandling - nye varer for leverandør
      *
     c                   if        *inkq
     c                   call      'JM114R'
     c                   parm                    p_firm
     c                   parm                    p_kamp
     c                   parm                    l_user
     c                   exsr      forny
     c                   goto      b2tagb
     c                   endif
      *
      * F17=Gruppebehandling - søk og velg nye varer
      *
     c                   if        *inkr
     c                   call      'JM115R'
     c                   parm                    p_firm
     c                   parm                    p_kamp
     c                   parm                    l_user
     c                   exsr      forny
     c                   goto      b2tagb
     c                   endif
      *
      * F18=Utplukk fra kampanje
      *
     c                   if        *inks
     c                   call      'JM550R'
     c                   parm                    p_firm
     c                   parm                    p_kamp
     c                   parm                    l_user
     c                   exsr      forny
     c                   goto      b2tagb
     c                   endif
      *
      * F20=Start overføring
      *
     c                   if        *inku
     c                   call      'JM119R'
     c                   parm                    p_firm
     c                   parm                    p_kamp
7.06 c                   parm                    p_prgr
     c                   goto      avslutt
     c                   endif
      *
      * Posisjonering
      *
     c                   if        b2vldo <> 0 or
     c                             b2vvar <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'V'
     c                   eval      jkavl1_vvar = b1vvar
     c     jkavl1_key    setll     jkavl1
     c                   other
     c                   eval      jkavl2_vldo = b1vldo
     c     jkavl2_key    setll     jkavl2
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Leverandørnavn
      *
     c                   if        b2vldo <> 0
     c                   eval      w_seqe = *blank
     c                   eval      jkavl2_vldo = b2vldo
     c                   eval      jkavl2_vvar = b2vvar
     c     jkavl2_key    setll     jkavl2
     c                   goto      end_pos
     c                   endif
      *
      * Varenummer
      *
     c                   if        b2vvar <> *blank
     c                   eval      w_seqe = 'V'
     c                   eval      jkavl1_vvar = b2vvar
     c     jkavl1_key    setll     jkavl1
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2vldo = 0
     c                   eval      b2vvar = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1vvar = b1vvar
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1vvar = b1vvar
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1vvar = b1vvar
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1vvar = b1vvar
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vedlikehold av vare
      *
     c                   if        b1valg = 7
     c                   eval      w_vare = b1vvar
     c                   eval      w_valg = 2
     c                   call      'JV101R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_valg
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vedlikehold av betingelser
      *
     c                   if        b1valg = 8
     c                   eval      w_vare = b1vvar
     c                   eval      w_pldo = 0
     c                   eval      w_lety = 0
6.3x c                   eval      w_prgr = *blank
     c                   call      'JP500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_pldo
6.3x c                   parm                    w_prgr
     c                   parm                    w_lety
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vedlikehold av Nobb-priser
      *
     c                   if        b1valg = 9
     c                   eval      w_vare = b1vvar
     c                   call      'JX100R'
     c                   parm                    w_vare
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vedlikehold av vare-pakning
      *
     c                   if        b1valg = 10
     c                   eval      w_vare = b1vvar
6.31 c                   eval      w_vldo = b1vldo
     c                   call      'JW100R'
     c                   parm                    w_firm
     c                   parm                    w_vare
6.31 c                   parm                    w_vldo
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1vvar = b2vvar
     c                   eval      c1tek1 = *blank
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Spørring
      *
     c                   if        *inkd
     c                   exsr      spØrring
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1vvar = *blank
     c                   goto      end_c1bld
     c                   endif
      *
      * Validering
      *
     c                   eval      jvarl1_vare = c1vvar
     c     jvarl1_key    chain     jvarl1
     c                   if        not %found
     c                   eval      *in32 = *on
     c                   goto      c1taga
     c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      jkavlr_vvar = c1vvar
     c     jkavlr_key    chain     jkavlr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1vvar = *blank
7.14 c                   eval      c1tek1 = *blank
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2vvar = *blank
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbilde legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle skjermbilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
7.04 c                   eval      s_kopr = 0
7.04 c                   eval      s_vkpr = 0
7.04 c                   eval      s_kpim = 0
7.04 c                   eval      s_vldo = 0
7.04 c                   eval      s_vkaa = 0
7.04 c                   eval      w_dekn = 0
7.04 c                   eval      c2kopr = 0
7.04 c                   eval      c2kpim = 0
      *
     c                   eval      jkavlr_vvar = c1vvar
     c     jkavlr_key    chain     jkavlr
      *
     c                   if        %found
     c                   eval      c2vldo = jmvldo
     c                   eval      c2vkai = jmvkai
     c                   if        jmvkpr <> 0
     c                   eval      c2vkpr = jmvkpr
     c                   else
7.01 c                   if        u_701 = *on
7.01 c                   eval      c2vkpr = 0
7.01 c                   else
     c                   eval      c2vkpr = jmvkai
7.01 c                   endif
     c                   endif
6.33 c                   eval      c2vlas = jmvlas
     c                   eval      c2vkau = jmvkau
     c                   eval      c2vkaa = jmvkaa
7.04 c                   if        u_702 = *on
7.04 c                   eval(h)   c2kpim = jmvkaa * w_mvat
7.04 c                   endif
     c                   eval      c2veus = jmveus
     c                   eval      c2veda = 0
6.33 c                   if        jmvoda <> *loval
6.33 c     *dmy          move      jmvoda        c2voda
6.33 c                   else
6.33 c                   eval      c2voda = 0
6.33 c                   endif
     c                   if        jmveda <> *loval
     c     *dmy          move      jmveda        c2veda
6.33 c                   else
6.33 c                   eval      c2veda = 0
6.33 c                   endif
6.30 c                   if        jmvidf <> *loval
6.30 c     *dmy          move      jmvidf        c2vidf
6.30 c                   else
6.30 c                   eval      c2vidf = 0
6.30 c                   endif
6.30 c                   if        jmvidt <> *loval
6.30 c     *dmy          move      jmvidt        c2vidt
6.30 c                   else
6.30 c                   eval      c2vidt = 0
6.30 c                   endif
     c                   else
6.33 c                   eval      c2vlas = 1
     c                   eval      c2vldo = 0
     c                   eval      c2vkai = 0
     c                   eval      c2vkpr = 0
     c                   eval      c2vkau = 0
     c                   eval      c2vkaa = 0
     c                   eval      c2vkaa = 0
     c                   eval      c2veda = 0
7.04 c                   eval      c2kopr = 0
7.04 c                   eval(h)   c2kpim = 0
6.32  * setter default fra heading
7.05 c                   if        u_utgd = *on
6.32 c                   if        jmfdat <> *loval
6.32 c     *dmy          move      jmfdat        c2vidf
6.32 c                   else
6.32 c                   eval      c2vidf = 0
6.32 c                   endif
6.32 c                   if        jmtdat <> *loval
6.32 c     *dmy          move      jmtdat        c2vidt
6.32 c                   else
6.32 c                   eval      c2vidt = 0
6.32 c                   endif
7.05 c                   else
7.05 c                   eval      c2vidf = 0
7.05 c                   eval      c2vidt = 0
7.05 c                   endif
     c                   eval      c2veus = *blank
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1vvar = k1vvar
     c                   eval      jkavlr_vvar = k1vvar
     c                   endif
      *
6.33 c                   if        b1valg = 2
6.33 c                   if        jmvlas = 0
6.33 c                   eval      c2vlas = 1
6.33 c                   endif
6.33 c                   endif
      *
      * Henter vareinformasjon, leverandørnavn og grunnenhet
      *
     c                   eval      jvarl1_vare = c1vvar
     c     jvarl1_key    chain     jvarl1
     c                   eval      c1tek1 = jvtek1
     c                   eval      c2nobb = jvnobb
     c                   if        c2vldo = 0 or
     c                             b1valg = 3
     c                   eval      c2vldo = jvldor
     c                   endif
6.35 c                   eval      c2enhe = jvpenh
      *
     c                   eval      c2lnav = *blank
     c                   eval      jlevl1_ldor = c2vldo
     c     jlevl1_key    chain     jlevl1
     c                   if        %found
     c                   eval      c2lnav = jlnavn
7.04 c                   eval      c2levr = jlenum
     c                   endif
7.04 c                   if        u_702 = *on
7.04 c                   exsr      hent_pris

7.04 c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
7.04  *
7.04  * beregner DG enten ut fra Kostpris eller Kostpris fra VA og
7.04  * anbefalt kamppris
7.04 c                   eval      w_dekn = 0
7.04 c                   if        u_702 = *on
7.04 c                   eval      w_saum = c2vkaa
7.04 c                   if        (c2vkpr <> 0 or
7.04 c                             c2kopr <> 0) and
7.04 c                             c2vkaa <> 0
7.04 c                   select
7.04 c                   when      c2vkpr <> 0
7.04 c                   eval(h)   w_dekn = 100 - ((c2vkpr * 100) / w_saum)
7.04 c                   when      c2kopr <> 0
7.04 c                   eval(h)   w_dekn = 100 - ((c2kopr * 100) / w_saum)
7.04 c                   endsl
7.04 c                   endif
7.04 c                   endif
7.04 c                   eval      c2dg = w_dekn
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
6.34  *
6.34  * F18=Utplukk fra kampanje
6.34  *
6.34 c                   if        *inks = *on
6.34 c                   exsr      vis_nobb
6.34 c                   goto      c2taga
6.34 c                   endif
      *
      * Validering av input
      *
     c***                if        c2vkai = 0
     c***                eval      *in31 = *on
     c***                goto      c2taga
     c***                endif
      *
6.36  *
6.36  *
6.36 c                   eval      jlevl1_ldor = c2vldo
6.36 c     jlevl1_key    chain     jlevl1
6.36 c                   if        not %found(jlevl1)
6.36 c                   eval      *in40 = *on
6.36 c                   goto      c2taga
6.36 c                   endif
7.04 c                   eval      c2lnav = jlnavn
6.36 c                   eval      jvlel1_lvnr = c1vvar
6.36 c                   eval      jvlel1_lldo = c2vldo
6.36 c     jvlel1_key    chain     jvlel1
6.36 c                   if        not %found(jvlel1)
6.36 c                   eval      *in40 = *on
6.36 c                   goto      c2taga
6.36 c                   endif
7.04  *
7.04  * test på endring av leverandør
7.04 c                   if        u_702 = *on
7.04 c                   if        c2vldo <> s_vldo
7.04 c                   exsr      hent_pris

7.04 c                   endif
7.04 c                   endif
7.04 c                   eval      s_vldo = c2vldo
7.04  *
7.04  * skal vise bildet en gang til ved endring av kostpris
7.04  *
7.04 c                   if        u_702 = *on
7.04 c                   if        s_vkpr <> c2vkpr or
7.04 c                             s_kpim <> c2kpim or
7.04 c                             s_vkaa <> c2vkaa or
7.04 c                             s_kopr <> c2kopr
7.04  * endret anb.kamppris
7.04 c                   if        s_vkaa <> c2vkaa
7.04 c                   eval(h)   c2kpim = c2vkaa * w_mvat
7.04 c                   endif
7.04 c                   if        c2kpim <> 0
7.04 c                   eval(h)   c2vkaa = c2kpim * w_mvaf
7.04 c                   endif
7.04 c                   eval      s_vkpr = c2vkpr
7.04 c                   eval      s_kpim = c2kpim
7.04 c                   eval      s_kopr = c2kopr
7.04  *
7.04 c                   eval      w_saum = c2vkaa
7.04 c                   eval      s_vkaa = c2vkaa
7.04 c                   if        (c2vkpr <> 0 or
7.04 c                             c2kopr <> 0) and
7.04 c                             c2vkaa <> 0
7.04 c                   select
7.04 c                   when      c2vkpr <> 0
7.04 c                   eval(h)   w_dekn = 100 - ((c2vkpr * 100) / w_saum)
7.04 c                   when      c2kopr <> 0
7.04 c                   eval(h)   w_dekn = 100 - ((c2kopr * 100) / w_saum)
7.04 c                   endsl
7.04 c                   eval      c2dg = w_dekn
7.04 c                   goto      c2taga
7.04 c                   endif
7.04 c                   endif
7.04 c                   endif
     c                   if        c2vkaa = 0 and
     c                             c2vkau = 0
     c                   eval      *in32 = *on
     c                   goto      c2taga
     c                   endif
      *
     c                   if        c2vkaa <> 0 and
     c                             c2vkau <> 0
     c                   eval      *in33 = *on
     c                   goto      c2taga
     c                   endif
6.30  *
6.30  *  validering innkjøpsdato fra / til
6.30  *
6.30 c                   if        c2vidf <> 0 or
6.30 c                             c2vidt <> 0
6.30 c     *dmy          test (d)                c2vidf                 38
6.30 c                   if        *in38 = *on
6.30 c                   goto      c2taga
6.30 c                   endif
6.30  *
6.30 c     *dmy          test (d)                c2vidt                 39
6.30 c                   if        *in39 = *on
6.30 c                   goto      c2taga
6.30 c                   endif
6.30  *
6.30 c     *dmy          move      c2vidf        w_fdat
6.30 c     *dmy          move      c2vidt        w_tdat
6.30 c                   if        w_fdat> w_tdat
6.30 c                   eval      *in41 = *on
6.30 c                   goto      c2taga
6.30 c                   endif
6.30  *
6.30 c                   if        c2vidf = 0
6.30 c                   goto      c2taga
6.30 c                   endif
6.30  *
6.30 c                   endif
      *
     c                   clear                   jkavlur
      *
     c                   eval      jkavlu_vvar = c1vvar
     c     jkavlu_key    chain     jkavlur
      *
     c                   eval      jmvldo = c2vldo
     c                   eval      jmvkai = c2vkai
     c                   if        c2vkpr <> 0
     c                   eval      jmvkpr = c2vkpr
     c                   else
7.01 c                   if        u_701 = *on
7.01 c                   eval      jmvkpr = 0
7.01 c                   else
     c                   eval      jmvkpr = c2vkai
7.01 c                   endif
     c                   endif
6.30 c                   if        c2vidf <> 0
6.30 c     *dmy          move      c2vidf        jmvidf
6.30 c                   else
6.30 c                   eval      jmvidf = *loval
6.30 c                   endif
6.30 c                   if        c2vidt <> 0
6.30 c     *dmy          move      c2vidt        jmvidt
6.30 c                   else
6.30 c                   eval      jmvidt = *loval
6.30 c                   endif
     c                   eval      jmvkau = c2vkau
     c                   eval      jmvkaa = c2vkaa
6.33 c                   eval      jmvlas = c2vlas
     c                   eval      jmveus = l_user
     c                   time                    jmveda
     c                   time                    jmveti
      *
     c                   if        %found(jkavlu)
     c                   update    jkavlur
     c                   else
     c                   eval      jmvfir = w_firm
     c                   eval      jmvkam = w_kamp
7.04 c                   eval      jmvprg = w_prgr
     c                   eval      jmvvar = c1vvar
6.33 c                   time                    jmvoda
     c                   write     jkavlur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
6.36 c                   eval      b1vldo = jmvldo
7.04 c                   eval      b1lnav = c2lnav
     c                   if        jmvkpr <> 0
     c                   eval      b1vkpr = jmvkpr
     c                   else
7.01 c                   if        u_701 = *on
7.01 c                   eval      b1vkpr = 0
7.01 c                   else
     c                   eval      b1vkpr = jmvkai
7.01 c                   endif
     c                   endif
     c                   if        jmvkau <> 0
     c                   eval      b1pris = jmvkau
     c                   eval      b1last = 1
     c                   else
     c                   eval      b1pris = jmvkaa
     c                   eval      b1last = 0
     c                   endif
     c                   call      'JM111R'
     c                   parm                    jmvfir
     c                   parm                    jmvkam
7.04 c                   parm                    jmvprg
     c                   parm                    jmvvar
     c                   parm                    b1stat
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      jkavlr_vvar = d1vvar
     c     jkavlr_key    chain     jkavlr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   eval      b_forn = *on
      *
     c                   eval      jkavlu_vvar = d1vvar
     c     jkavlu_key    chain     jkavlur
     c                   delete    jkavlur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_vvar = k1vvar
      *
      * Fyller kopierings-bildet
      *
     c                   eval      jkavlr_vvar = k1vvar
     c     jkavlr_key    chain     jkavlr
      *
     c     k1tagb        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   exsr      spØrring
     c                   goto      k1tagb
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1vvar = *blank
     c                   eval      *in31 = *on
     c                   goto      k1tagb
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      jkavlr_vvar = k1vvar
     c     jkavlr_key    chain     jkavlr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1tagb
     c                   endif
      *
      * Sjekker om vare finnes
      *
     c                   eval      jvarl1_vare = k1vvar
     c     jvarl1_key    chain     jvarl1
     c                   if        not %found
     c                   eval      *in33 = *on
     c                   goto      k1tagb
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1vvar = s_vvar
     c                   exsr      xc2bld
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   if        w_seqe = 'V'
     c                   read      jkavl1
     c                   else
     c                   read      jkavl2
     c                   endif
      *
     c                   if        %eof or
     c                             jmvfir <> p_firm or
     c                             jmvkam <> p_kamp
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
7.04  *
7.04 c                   if        jmvprg <> p_prgr
7.04 c                   iter
7.04 c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'V'
     c                   eval      jkavl1_vvar = jmvvar
     c                   other
     c                   eval      jkavl2_vldo = jmvldo
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1vvar = jmvvar
     c                   eval      b1vldo = jmvldo
     c                   eval      b1enhe = *blank
     c                   if        jmvkpr <> 0
     c                   eval      b1vkpr = jmvkpr
     c                   else
7.01 c                   if        u_701 = *on
7.01 c                   eval      b1vkpr = 0
7.01 c                   else
     c                   eval      b1vkpr = jmvkai
7.01 c                   endif
     c                   endif
     c                   if        jmvkau <> 0
     c                   eval      b1pris = jmvkau
     c                   eval      b1last = 1
     c                   else
     c                   eval      b1pris = jmvkaa
     c                   eval      b1last = 0
     c                   endif
      *
      * Henter varetekst-1 og leverandørnavn
      *
     c                   eval      b1tek1      = *blank
     c                   eval      jvarl1_vare = b1vvar
     c     jvarl1_key    chain     jvarl1
     c                   if        %found
     c                   eval      b1tek1 = jvtek1
6.10 c                   eval      b1enhe = jvpenh
     c                   endif
      *
     c                   eval      b1lnav = *blank
     c                   if        b1vldo <> 0
     c                   eval      jlevl1_ldor = b1vldo
     c     jlevl1_key    chain     jlevl1
     c                   if        %found
     c                   eval      b1lnav = jlnavn
     c                   endif
     c                   endif
      *
      * Henter status-kode
      *
     c                   call      'JM111R'
     c                   parm                    jmvfir
     c                   parm                    jmvkam
7.04 c                   parm                    jmvprg
     c                   parm                    jmvvar
     c                   parm                    b1stat
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'V'
     c     jkavl1_key    setgt     jkavl1
     c                   readp     jkavl1
     c                   other
     c     jkavl2_key    setgt     jkavl2
     c                   readp     jkavl2
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'V'
     c                   readp     jkavl1
     c                   other
     c                   readp     jkavl2
     c                   endsl
      *
     c                   if        %eof or
     c                             jmvfir <> p_firm or
     c                             jmvkam <> p_kamp
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'V'
     c                   eval      jkavl1_vvar = jmvvar
     c                   other
     c                   eval      jkavl2_vldo = jmvldo
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'V'
     c     jkavl1_key    setll     jkavl1
     c                   other
     c     jkavl2_key    setll     jkavl2
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Vare-nummer
      *
     c                   if        w_afld = 'B2VVAR'
     c                   eval      w_vare = *blank
     c                   eval      w_tek1 = *blank
     c                   call      'JV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_tek1
     c                   if        w_vare <> *blank
     c                   eval      b2vvar = w_vare
     c                   endif
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1VVAR'
     c                   eval      w_vare = *blank
     c                   eval      w_tek1 = *blank
     c                   call      'JV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_tek1
     c                   if        w_vare<> *blank
     c                   eval      c1vvar = w_vare
     c                   eval      c1tek1 = w_tek1
     c                   endif
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1VVAR'
     c                   eval      w_vare = *blank
     c                   eval      w_tek1 = *blank
     c                   call      'JV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_tek1
     c                   if        w_vare <> *blank
     c                   eval      k1vvar = w_vare
     c                   endif
     c                   leavesr
     c                   endif
      *
      * Leverandør-navn
      *
     c                   if        w_afld = 'B2VLDO'
     c                   call      'JL500R'
     c                   parm                    b2vldo
     c                   parm                    w_navn
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
6.34  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.34  * Vis NOBB informasjon for varen. Samme prinsipp som preview av utskrifter
6.34  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.34  *
6.34 c     vis_nobb      begsr
6.34 c
6.34 c                   eval      p_nobb = %char(c2nobb)
6.34 c                   call      'AP633R'
6.34 c                   parm                    p_nobb
6.34  *
6.34 c                   endsr
7.14  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.14  * Subrutine for å hente data/kalkyle og status på vare/leveringstype
7.14  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.14  *
7.14 c     hent_pris     begsr
7.14  *
7.14 c                   eval      vvarl1_vare = c1vvar
7.14 c     vvarl1_key    chain     vvarl1
7.14 c                   if        %found(vvarl1)
7.14 c                   exsr      hent_evr
7.14 c                   if        w_kopr <> 0
7.14 c                   leavesr
7.14 c                   endif
7.14 c                   endif
7.14 c                   eval      jvdtl1_dvar = c1vvar
7.14 c     jvdtl1_key    chain     jvdtl1
7.14  *
7.14 c                   eval      w_gprk = 0
7.14 c                   eval      w_oppd = '1'
7.14 c                   eval      w_pgiv = c2vldo
7.14  *
7.14 c                   call      'JV750R'
7.14  *
7.14 c                   parm                    w_firm
7.14 c                   parm                    jvdvar
7.14 c                   parm                    w_prgr
7.14 c                   parm                    w_pgiv
7.14 c                   parm                    jvdpko
7.14 c                   parm                    jvdsko
7.14 c                   parm                    w_lety
7.14 c                   parm                    w_gprk
7.14 c                   parm                    w_pdat
7.14 c                   parm                    w_utgp
7.14  *
7.14 c                   parm                    w_osts
7.14  *
7.14 c                   parm                    w_ldor
7.14 c                   parm                    w_lvar
7.14 c                   parm                    w_eldo
7.14 c                   parm                    w_grpr
7.14 c                   parm                    w_gdat
7.14  *
7.14 c                   parm                    w_enh
7.14 c                   parm                    w_enh1
7.14 c                   parm                    w_enh2
7.14 c                   parm                    w_enh3
7.14 c                   parm                    w_eni1
7.14 c                   parm                    w_eni2
7.14 c                   parm                    w_eni3
7.14 c                   parm                    w_enp1
7.14 c                   parm                    w_enp2
7.14 c                   parm                    w_enp3
7.14 c                   parm                    w_ofp1
7.14 c                   parm                    w_ofp2
7.14 c                   parm                    w_ofp3
7.14 c                   parm                    w_ofv2
7.14 c                   parm                    w_ofv3
7.14 c                   parm                    w_ofi1
7.14 c                   parm                    w_ofi2
7.14 c                   parm                    w_ofi3
7.14  *
7.14 c                   parm                    w_bldo
7.14 c                   parm                    w_bogr
7.14 c                   parm                    w_bhgr
7.14 c                   parm                    w_bugr
7.14 c                   parm                    w_bmod
7.14 c                   parm                    w_bvar
7.14 c                   parm                    w_bvty
7.14 c                   parm                    w_blet
7.14 c                   parm                    w_bbet
7.14 c                   parm                    w_ntpr
7.14 c                   parm                    w_ifak
7.14  *
7.14 c                   parm                    w_pogr
7.14 c                   parm                    w_phgr
7.14 c                   parm                    w_pugr
7.14 c                   parm                    w_pldo
7.14 c                   parm                    w_pmod
7.14 c                   parm                    w_pvar
7.14 c                   parm                    w_ppko
7.14 c                   parm                    w_pvty
7.14 c                   parm                    w_plet
7.14 c                   parm                    w_kfak
7.14 c                   parm                    w_kbel
7.14 c                   parm                    w_sfak
7.14  *
7.14 c                   parm                    w_midl
7.14 c                   parm                    w_inpr
7.14 c                   parm                    w_kopr
7.14 c                   parm                    w_s1pr
7.14 c                   parm                    w_s1im
7.14 c                   parm                    w_s2pr
7.14 c                   parm                    w_s2im
7.14 c                   parm                    w_s3pr
7.14 c                   parm                    w_s3im
7.14 c                   parm                    w_dekn
7.14 c                   parm                    w_prut
7.14 c                   parm                    w_ffak
7.14 c                   parm                    w_inp2
7.14 c                   parm                    w_pgbe
7.14 c                   parm                    w_pgfr
7.14 c                   parm                    w_oppd
7.14  *
7.14 c                   eval(h)   w_inp2 = w_inpr * w_ffak
7.14  *
7.14  * regner om til enhet på kampanje
7.14 c                   select
7.14 c                   when      w_enh2 = c2enhe
7.14 c                   eval(h)   c2kopr = w_kopr * w_ofv2
7.14 c                   when      w_enh3 = c2enhe
7.14 c                   eval(h)   c2kopr = w_kopr * w_ofv3
7.14 c                   other
7.14 c                   eval      c2kopr = w_kopr
7.14 c                   endsl
7.14 c
7.14  *
7.14 c                   endsr
7.14  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.14  * Subrutine for å hente kostpris fra EVR
7.14  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.14  *
7.14 c     hent_evr      begsr
7.14  *
7.14 c                   eval      w_kopr = 0
7.14  * henter resk.levnr
7.14 c                   eval      w_enum = 0
7.14 c/exec sql
7.14 c+                  select jlenum
7.14 c+                  into :w_enum
7.14 c+                  from jlevpf
7.14 c+                  where jlldor = :c2vldo
7.14 c+                  fetch first 1 rows only
7.14 c/end-exec
7.14  *
7.14 c                   eval      vvprl1_vare = c1vvar
7.14 c                   eval      vvprl1_ldor = w_enum
7.14 c                   eval      vvprl1_prgr = w_prgr
7.14 c                   eval      vvprl1_enhe = c2enhe
7.14 c                   eval      vvprl1_lety = 0
7.14 c     vvprl1_key    setll     vvprl1
7.14 c     vvprl1_key    reade     vvprl1
7.14 c                   dow       not %eof(vvprl1)
7.14 c                   eval      w_kopr = vpkopr
7.14 c                   if        w_pdat >= vppdat
7.14 c                   leave
7.14 c                   endif
7.14 c     vvprl1_key    reade     vvprl1
7.14 c                   enddo
7.14  *
7.14  * hvis  pris ikke finnes på prisgruppe, prøv blank
7.14  *
7.14 c                   if        w_kopr = 0
7.14 c                   eval      vvprl1_prgr = *blank
7.14 c     vvprl1_key    setll     vvprl1
7.14 c     vvprl1_key    reade     vvprl1
7.14 c                   dow       not %eof(vvprl1)
7.14 c                   eval      w_kopr = vpkopr
7.14 c                   if        w_pdat >= vppdat
7.14 c                   leave
7.14 c                   endif
7.14 c     vvprl1_key    reade     vvprl1
7.14 c                   enddo
7.14 c                   endif
7.14 c                   eval      c2kopr = w_kopr
7.14 c                   endsr
7.14  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.14  * Subrutine for å hente lagerets leverandør, evt. varens lev - lagervarer
7.14  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.14  *
7.14 c     hent_laglev   begsr
7.14  *
7.14 c                   eval      vvarl1_vare = c1vvar
7.14 c     vvarl1_key    chain     vvarl1
7.14 c                   if        %found(vvarl1)
7.14  * henter std. lager
7.14 c                   eval      w_levr = 0
7.14 c/exec sql
7.14 c+                  select vlbldo
7.14 c+                  into :w_levr
7.14 c+                  from vlagpf
7.14 c+                  where vlfirm = :w_firm and
7.14 c+                        vlvare = :c1vvar and
7.14 c+                        vllage = :w_glag
7.14 c+                  fetch first 1 rows only
7.14 c/end-exec
7.14  *
7.14 c                   if        w_levr = 0
7.14 c                   eval      w_levr = vvldor
7.14 c                   endif
7.14 c                   endif
7.14  * henter VA lev
7.14 c                   if        w_levr > 0
7.14 c                   eval      w_nlev = 0
7.14 c/exec sql
7.14 c+                  select jlldor
7.14 c+                  into :w_nlev
7.14 c+                  from jlevpf
7.14 c+                  where jlenum = :w_levr
7.14 c+                  fetch first 1 rows only
7.14 c/end-exec
7.14 c                   eval      c2vldo = w_nlev
7.14 c                   endif
7.14  *
7.14 c                   endsr
7.14  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_kamp
7.04 c                   parm                    p_prgr
      *
      * Key for posisjonering på varenummer
      *
     c     jkavl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_kamp
7.04 c                   kfld                    w_prgr
     c                   kfld                    jkavl1_vvar
      *
      * Key for posisjonering på leverandør
      *
     c     jkavl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_kamp
     c                   kfld                    jkavl2_vldo
7.04 c                   kfld                    w_prgr
     c                   kfld                    jkavl2_vvar
      *
      * Key for les på kampanje-vare
      *
     c     jkavlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_kamp
7.04 c                   kfld                    w_prgr
     c                   kfld                    jkavlr_vvar
      *
      * Key for oppdatering av kampanje-vare
      *
     c     jkavlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_kamp
7.04 c                   kfld                    w_prgr
     c                   kfld                    jkavlu_vvar
      *
      * Key for oppslag på vare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
6.32  *
6.32  * Key for les av kampanje hode
6.32  *                              h
6.32 c     jkahl1_key    klist
6.32 c                   kfld                    w_firm
6.32 c                   kfld                    w_kamp
7.04 c                   kfld                    w_prgr
6.36  *
6.36  * Key for les av vare-leverandør
6.36  *
6.36 c     jvlel1_key    klist
6.36 c                   kfld                    jvlel1_lvnr
6.36 c                   kfld                    jvlel1_lldo
7.14  *
7.14  * Key for oppslag på vare-detalj
7.14  *
7.14 c     jvdtl1_key    klist
7.14 c                   kfld                    w_firm
7.14 c                   kfld                    jvdtl1_dvar
7.14  *
7.14  *
7.14  * Key for les på vare
7.14  *
7.14 c     vvarl1_key    klist
7.14 c                   kfld                    w_firm
7.14 c                   kfld                    vvarl1_vare
7.14  *
7.14  * Key for les på vare-pris-register
7.14  *
7.14 c     vvprl1_key    klist
7.14 c                   kfld                    w_firm
7.14 c                   kfld                    vvprl1_vare
7.14 c                   kfld                    vvprl1_ldor
7.14 c                   kfld                    vvprl1_prgr
7.14 c                   kfld                    vvprl1_enhe
7.14 c                   kfld                    vvprl1_lety
7.14  *
      *
      *
      * Henter verdier fra paraemter, og initierer skjermbildet
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_kamp = p_kamp
7.04 c                   eval      w_prgr = p_prgr
      *
7.01  *
7.01  * Konfiguerte tilpasninger
7.04  *
7.01  * Test om Tillate at kostpris er = 0 er lov
7.01  *
7.02   //u_701 = *on;
7.01   CallP CO402R(l_filg:w_firm:0:'JM110R_701':
7.01                    co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.02   //  u_701 = *off;
7.02     u_701 = *on;
7.01   endif;
      *
     c                   eval      b2kamp = p_kamp
7.04 c                   eval      b2prgr = p_prgr
7.14 c                   time                    w_pdat
7.14  *
      *
     c                   eval      *in22 = *on
7.14  * henter lagernr.
7.14 c                   eval      w_glag = 0
7.14 c/exec sql
7.14 c+                  select vpglag
7.14 c+                  into :w_glag
7.14 c+                  from vpgrpf
7.14 c+                  where vpgprg = :w_prgr
7.14 c+                  and vpgfir = :w_firm
7.14 c+                  fetch first 1 rows only
7.14 c/end-exec
7.14  * Finner mva-sats
7.14  *
7.14 c                   time                    w_udat
7.14 c                   call      'FÅ705R'
7.14 c                   parm                    w_firm
7.14 c                   parm                    w_udat
7.14 c                   parm                    w_mvap
7.14 c                   parm                    w_mvat
7.14 c                   parm                    w_mvaf
7.14  *
6.32  *
6.32  * Les kampanjehode
6.32  *
6.32 c     jkahl1_key    chain     jkahl1
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      jkavl2_vldo = 0
     c                   eval      jkavl2_vvar = *blank
     c     jkavl2_key    setll     jkavl2
      *
     c                   exsr      crt_subfile
7.05  *
7.05  * Test om vi skal  fra shop er lov
7.05  *
7.05   CallP CO402R(l_filg:w_firm:0:'SJEKK_INNKJOPSINTERVALL':
7.05                    co402_verdi1:co402_verdi2);
7.05   if co402_verdi1 = '1';
7.05     u_utgd = *on;
7.05   endif;
      *
7.01a * Konfiguerte tilpasninger
7.01a *
7.01a * Test om Tillate at kostpris er = 0 er lov
7.01a *
7.01a  CallP CO402R(l_filg:w_firm:0:'JM110R_701':
7.01a                   co402_verdi1:co402_verdi2);
7.01a  if co402_verdi1 = '1';
7.01a    u_701 = *on;
7.01a  endif;
7.01  *
7.04  * Test om Se DG
7.04  *
7.04   CallP CO402R(l_filg:w_firm:0:'JM110R_702':
7.04                    co402_verdi1:co402_verdi2);
7.04   if co402_verdi1 = '1';
7.04     u_702 = *on;
7.04   endif;
7.04
      *
     c                   endsr
