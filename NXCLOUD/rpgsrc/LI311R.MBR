     H/TITLE  ASLAGR: Inntasting av parameter for utskrift
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  System. . . . . : ASLAGR                                       *
      *  Program . . . . : LI311R                                       *
      *  Beskrivelse . . : Automatisk lagerlegging av EDI pakkseddel    *
      *                    NB Hardkodet rutine "LPAKKEDI"               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  Spesialversjon. :                                              *
      *  Tilpasset for . :                                              *
      *                                                                 *
      * Ref.   Dato  Sign Endringsbeskrivelse                           *
      * ----- ------ ---- ----------------------------------------------*
7.01  *  7.00 150520 bsa  Nytt program
7.02  *  7.00 051119 bsa  Skal ikke vise skjermbildet
7.xx  *  K000 140520 Bsa  Innfører logging pga feilsøk                 *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     flohelu    uf   e           k disk    rename(lohepfr:lohelur)
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flplolu    uf a e           k disk    rename(lplopfr:lplolur)
     flplol1    if   e           k disk    rename(lplopfr:lplol1r)
7.xx f*li301d    cf   e             workstn
      *
      * Datastruktur
      *
     D                UDS
     d l_poff                584    584
     d l_ruti                800    809
     d l_wsid                901    910
     d l_user                911    920
     d l_filg                931    933
     d l_avde                947    950  0
      *
      * Definering av øvrige variable
      *
     d w_stat          s              1
     d w_parm          s             80
     d w_ruti          s                   like(l_ruti)
      *
     d p_ruti          s                   like(l_ruti)
     d p_pdf           s              1
      *
     d lohelu_numm     s                   like(lonumm)
     d lohelu_suff     s                   like(losuff)
     d lohel1_numm     s                   like(lonumm)
     d lohel1_suff     s                   like(losuff)
     d votyl1_otyp     s                   like(vaotyp)
     d lplol1_ponr     s                   like(lkponr)
     d lplol1_psuf     s                   like(lkpsuf)
      *
     d w_firm          s                   like(lofirm)
     d w_numm          s                   like(lonumm)
     d w_suff          s                   like(losuff)
     d w_otyp          s                   like(vaotyp)
7.xx d w_svar          s              1
     d w_skor          s              1
     d w_ipor          s              1
     d wsjekk          s              1
     d wpskor          s              1
     d wwoutq          s             10
     d w_palt          s                   like(lkpalt)
7.xx d w_jnav          s             15
7.xx d w_jkey          s             41
7.xx d w_jtxt          s             35
7.xx d w_jstat         s              2  0
7.xx d w_jtext         s            200
7.xx d w_errt          s            200
7.xx d w_debg          s              1    inz(*on)
      *
      * Datastruktur for informasjon ved exceptions
      *
     d PgmError       sds
     d  proc_name        *proc
     d  pgm_status       *STATUS
     d  prv_status            16     20s 0
     d  line_numm             21     28
     d  routine          *ROUTINE
     d  parms            *PARMS
     d  excp_type             40     42
     d  excp_numm             43     46
     d  pgm_lib               81     90
     d  excp_data             91    170
     d  excp_id              171    174
     d  date                 191    198
     d  year                 199    200s 0
     d  last_file            201    208
     d  file_info            209    243
     d  job_name             244    253
     d  job_user             254    263
     d  job_numb             264    269s 0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Velg utfra ORDRETYPE hvilket bilde som skal vises              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      *  Hent opplysninger fra BESTILLING-HODE-REGISTER
      *
     c     lohel1_key    chain     lohel1
     c                   if        %found
     c                   eval      w_otyp = lootyp
     c                   endif
      *
      *  Hent opplysninger fra UTPLUKK-PARAM-REGISTER
      *       (Ordretype hentes kun om den finnes plukket tidligere)
      *
     c     lplol1_key    chain     lplol1
     c                   eval      w_otyp = lkpoty
      *
      *  Hent opplysninger fra ORDRETYPE-REGISTER
      *
     c                   eval      votyl1_otyp = w_otyp
     c     votyl1_key    chain     votyl1r
      *
      *  Utfra ordretype (akk.type) hopp til riktig bilde
      *
     c     vaoakk        cabeq     2             e4tag
      *
     c                   goto      xslutt
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  E4 Behandler valg for utskrift av INNGÅENDE PAKKSEDDEL         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     e4tag         tag
      *
     c                   eval      w_ruti = 'LPAKKEDI '
     c                   exsr      sjk_proa
      *
      *  Legger opplysninger over i felles-felter
      *
     c                   eval      w_palt = *zero
7.xx c                   eval      w_svar = '1'
     c                   eval      wwoutq = *blanks
      *
     c                   goto      start
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  START Behandler oppdatering av BESTILLING-HODE-REGISTER        *
      *        samt utlegging/oppdatering av UTPLUKK-PARAM-REGISTER     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     start         tag
      *
      *  Oppdater BESTILLING-HODE-REGISTER, BESTILLINGEN er nå til behandling
      *
     c     lohelu_key    chain     lohelur
     c                   if        %found
      *
      *  Dersom bestillingen allerde er plukket, skal ikke koden endres
      *
     c                   if        lokode <> 3
     c                   eval      lokode = 2
     c                   endif
      *
     c                   time                    loedat
     c                   time                    loetim
     c                   eval      loeusr = l_user
     c                   update    lohelur
     c                   endif
      *
      *  Legg ut nye records til  BESTILLING-PLUKK-HODE-REGISTER
      *  Eventuelt oppdater, dersom bestillingen allerede er plukket
      *
     c     lplol1_key    chain     lplolur
     c                   if        not %found
     c                   eval      lkpfir = w_firm
     c                   eval      lkponr = lplol1_ponr
     c                   eval      lkpsuf = lplol1_psuf
     c                   eval      lkpoty = w_otyp
     c                   eval      lkpwsd = l_wsid
     c                   eval      lkpusr = l_user
     c                   eval      lkpalt = w_palt
7.xx c**                 eval      lkpdir = w_svar
7.xx c                   eval      lkpdir = 1
     c                   write     lplolur
     c                   else
     c                   eval      lkpalt = w_palt
7.xx c**                 eval      lkpdir = w_svar
7.xx c                   eval      lkpdir = 1
     c                   update    lplolur
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Kaller opp utskriftsprogrammet, dersom direkte utskrift        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Skal ikke kunne endres
      *
7.xx  *
7.xx  * Vi logger at utskriftprogrammet har startet
7.xx  *
7.xx c                   if        w_debg = *on
7.xx c                   eval      w_jstat = 10
7.xx c                   eval      w_jtext = 'EDI Pakkseddel-call-312'
7.xx c                   call      'AB705R'
7.xx c                   parm                    w_jkey
7.xx c                   parm                    w_jstat
7.xx c                   parm                    w_jtext
7.xx c                   endif
      *
7.xx c                   call      'LI312R'
     c                   parm                    w_skor
     c                   parm                    w_firm
     c                   parm                    w_numm
     c                   parm                    w_suff
     c                   parm                    w_otyp
     c                   parm                    w_svar
     c                   parm                    wwoutq
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     xslutt        tag
      *
7.xx c                   if        w_debg = *on
7.xx  * Avslutt debug - dette programmet
7.xx c                   call      'AB710R'
7.xx c                   parm                    w_jkey
7.xx c                   endif
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_proa      begsr
      *
     c                   eval      p_pdf ='0'
     c                   eval      p_ruti= w_ruti
      * Sjekk om PROA er aktivert
     c                   call      'AP608R'
     c                   parm                    p_ruti
     c                   parm                    p_pdf
      * p_pdf = '1' = PROA aktivert
     c                   if        p_pdf = '0'
     c                   eval      *in35 = *off
     c                   endif
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ERROR-rutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *pssr         begsr
      *
     c                   eval      w_errt = *blanks
     c                   eval      w_errt = 'Feil i LI311R! Info=' +
     c                             %trim(excp_data) + '. Line=' +
     c                             %trim(line_numm) + '. Job=' +
     c                             %trim(job_name) + '/' +
     c                             %trim(job_user) + '/' +
     c                             %char(job_numb)
      *
     c                   eval      w_jstat = 99
     c                   eval      w_jtext = w_errt
     c                   call      'AB705R'
     c                   parm                    w_jkey
     c                   parm                    w_jstat
     c                   parm                    w_jtext
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     *inzsr        begsr
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     c                   eval      wpskor = *blank
     c                   eval      w_ipor = *blank
     c                   eval      wsjekk = *blank
     c                   eval      wwoutq = *blanks
     c                   eval      *in35 = *on
      *
      *  Redefinering av nøkler
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for file : BESTILLING-HODE-REGISTER
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
     c                   kfld                    lohel1_suff
      *
      * Key for file : BESTILLING-HODE-REGISTER
      *
     c     lohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelu_numm
     c                   kfld                    lohelu_suff
      *
      * Key for file : BESTILLING-PLUKK-HODE-REGISTER
      *
     c     lplol1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lplol1_ponr
     c                   kfld                    lplol1_psuf
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     *entry        plist
     c                   parm                    w_ipor
     c                   parm                    w_firm
     c                   parm                    w_numm
     c                   parm                    w_suff
     c                   parm                    w_svar
7.xx  *
7.xx  * Logging av EDI Pakkseddel mottak
7.xx  *
7.xx c                   if        w_debg = *on
7.xx c                   eval      w_jnav = 'EDI Pakkseddel motttak'
7.xx c                   eval      w_jtxt = 'Scanning av SSCC koder på PDA-311'
7.xx c                   eval      w_jkey = *blanks
7.xx c                   call      'AB700R'
7.xx c                   parm                    w_jnav
7.xx c                   parm                    w_jkey
7.xx c                   parm                    w_jtxt
7.xx  *
7.xx  * Vi logger at utskriftprogrammet har startet
7.xx  *
7.xx c                   eval      w_jstat = 10
7.xx c                   eval      w_jtext = 'EDI Pakkseddel-LI311-inzsr'
7.xx c                   call      'AB705R'
7.xx c                   parm                    w_jkey
7.xx c                   parm                    w_jstat
7.xx c                   parm                    w_jtext
7.xx  *
7.xx c                   endif
      *
      *  Legger inn bestillingsnummer og suffix i nøkklene
      *
     c                   eval      lohel1_numm = w_numm
     c                   eval      lohel1_suff = w_suff
     c                   eval      lohelu_numm = w_numm
     c                   eval      lohelu_suff = w_suff
      *
     c                   eval      lplol1_ponr = w_numm
     c                   eval      lplol1_psuf = w_suff
      *
      *  Nullstiller returvalg
      *
     c                   eval      w_ipor = *blank
      *
     c                   endsr
