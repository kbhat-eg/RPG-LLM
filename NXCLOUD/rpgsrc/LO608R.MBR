     H DATEDIT(*DMY)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  System. . . . . : ASLAGR                                       *
      *  Program . . . . : LO608R                                       *
      *  Beskrivelse . . : Start utkjøring av bestilling/innkjøpspapirer*
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  Spesialversjon. :                                              *
      *  Tilpasset for . :                                              *
      *                                                                 *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.*
      *  --------- ------  ---------------------------------------------*
6.nu  *  R6.30     120614  Endret ihht. nummerutvidelse            bof  *
8.01  *  800       070422  Fyller utperiode ved plukk til IF       ask  *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     FVOTYL1    IF   E           K DISK    RENAME(VOTYPFR:VOTYL1R)
     FLSTSL1    IF   E           K DISK    RENAME(LSTSPFR:LSTSL1R)
     FLPM1LU    UF A E           K DISK    RENAME(LPM1PFR:LPM1LUR)
      *
     D                 DS
     D  LDAMBR                 1     10
     D  LDABOK                 1      1
     D  LDAFIR                 2      4  0
     D  LDALST                 5     10  0
      *
      *
     D                UDS
     D  DSWSID               901    910
     D  DSUSER               911    920
     D  DSSFIR               944    946  0
      *
      * Definering av variable
      *
     d wwdato_6        s              6  0
     d wwdato          s               d   datfmt(*iso)
     d WWPERI          s              4  0
     d WWPERI_ALF      s              8
     d W_OTYP          s              2
     d W_OT02          s              2
     d W_OT03          s              2
     d W_OT04          s              2
     d W_OT05          s              2
8.01 d w_peri          s              6  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Velg om det skal kjøres direkte utskrift                       *
      *  Direkte utskrift kjøres dersom bestillingsnummer er utfylt     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     WPNUMM        IFNE      *ZERO
     C                   MOVE      WPOTYP        W_OTYP
     C                   ELSE
     C                   GOTO      XSLUTT
     C                   END
      *
     C                   MOVE      *ZERO         WPDIRK
      *
      *  Hent opplysninger fra ORDRETYPE-REGISTER
      *
     C                   MOVE      W_OTYP        OTYTYP
     C     OTYKEY        CHAIN     VOTYL1R                            90
      *
      *  Kaller opp subrutine for utlegging av parameter, for
      *  start av utskrift (WWVALG=Internt listenummer fra 0 til 3)
      *
     C                   MOVE      *ZERO         WWVALG
     C                   EXSR      PARAM
      *
     C                   CALL      'LO112C'
     C                   PARM                    LDAMBR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     XSLUTT        TAG
      *
     C                   SETON                                        LR
     C                   RETURN
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Oppdaterer  BESTILLING-PLUKK-PARAM1-REGISTER                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     PARAM         BEGSR
      *
     C                   MOVE      WWVALG        PM1LST
     C     PM1KEY        CHAIN     LPM1LUR                            90
      *
     C                   MOVE      WPOUTQ        LK1UTQ
     C                   MOVE      W_OTYP        LK1OTY
     C                   MOVE      WPNUMM        LK1ONR
     C                   MOVE      WPSUFF        LK1SUF
     C                   MOVE      WPDIRK        LK1VG0
     C                   MOVE      WWVALG        LK1VG1
     C                   MOVE      0             LK1VG2
     C                   MOVE      0             LK1VG3

8.01   // Henter periode fra dagens dato
8.01
8.01       w_peri = (%subdt(%date():*years) * 100) +
8.01                 %subdt(%date():*months);
8.01
8.01 c                   eval      lk1per = w_peri
8.01 c                   eval      lk1dat = %date()
      *
     C     *IN90         IFEQ      *OFF
     C                   UPDATE    LPM1LUR
     C                   ELSE
     C                   MOVE      PM1FIR        LK1FIR
     C                   MOVE      DSWSID        LK1WSD
     C                   MOVE      DSUSER        LK1USR
     C                   MOVE      WWVALG        LK1LST
     C                   WRITE     LPM1LUR
     C                   END
      *
     C                   ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     CSR   *INZSR        BEGSR
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     C                   MOVE      *ZERO         WCQNNN            1
     C                   MOVE      *ZERO         WCNUMM            6 0
      *
      *  Redefinering av felter
      *
     C     *LIKE         DEFINE    LK1VG1        WWVALG
      *
      *  Redefinering av nøkler
      *
     C     *LIKE         DEFINE    LAAFIR        STSFIR
      *
     C     *LIKE         DEFINE    VAOFIR        OTYFIR
     C     *LIKE         DEFINE    VAOTYP        OTYTYP
      *
     C     *LIKE         DEFINE    LK1FIR        PM1FIR
     C     *LIKE         DEFINE    LK1WSD        PM1WSD
     C     *LIKE         DEFINE    LK1USR        PM1USR
     C     *LIKE         DEFINE    LK1LST        PM1LST
      *
      * Key for file : LAGER-STATUS-KODE-REGISTER
      *
     C     STSKEY        KLIST
     C                   KFLD                    STSFIR
      *
      * Key for file : ORDRETYPE-REGISTER
      *
     C     OTYKEY        KLIST
     C                   KFLD                    OTYFIR
     C                   KFLD                    OTYTYP
      *
      * Key for file : PLUKK-1-LINJE-REGISTER
      *
     C     PM1KEY        KLIST
     C                   KFLD                    PM1FIR
     C                   KFLD                    PM1WSD
     C                   KFLD                    PM1USR
     C                   KFLD                    PM1LST
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     *ENTRY        PLIST
     C                   PARM                    WPSKOR            1
     C                   PARM                    WPFIRM            3 0
6.NU C                   PARM                    WPNUMM            8 0
     C                   PARM                    WPSUFF            2 0
     C                   PARM                    WPOTYP            2
     C                   PARM                    WPDIRK            1 0
     C                   PARM                    WPOUTQ           10
      *
      *  Legger inn firmanummer i nøkklene
      *
     C                   MOVE      WPFIRM        STSFIR
     C                   MOVE      WPFIRM        OTYFIR
     C                   MOVE      WPFIRM        PM1FIR
     C                   MOVE      WPFIRM        LDAFIR
      *
      *  Legger inn opplysninger i nøkklene
      *
     C                   MOVE      DSWSID        PM1WSD
     C                   MOVE      DSUSER        PM1USR
      *
      *  Hent opplysninger fra LAGER-STATUS-KODE-REGISTER
      *
     C     STSKEY        CHAIN     LSTSL1R                            90
     C     *IN90         IFEQ      *OFF
     C                   MOVE      LAAOT2        W_OT02
     C                   MOVE      LAAOT3        W_OT03
     C                   MOVE      LAAOT4        W_OT04
     C                   MOVE      LAAOT5        W_OT05
     C                   END
      *
      *  Henter siste nummer for File-member
      *
     C                   CALL      'LÅ900R'
     C                   PARM                    WCQNNN
     C                   PARM                    WCNUMM
      *
     C                   MOVE      'A'           LDABOK
     C                   MOVE      WCNUMM        LDALST
      *
     CSR                 ENDSR
