     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FO711R
      * Beskrivelse . . : Vise informasjon om hvem som holder ordre. Fjerne kode
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       030511 bsa  Nytt program
6.nu  * R6.30 030614 bsa  Programmet gjennomgått mtp nummerutvidelser.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     fausrl1    if   e           k disk    rename(ausrpfr:ausrl1r)
     fra09l1    if   e           k disk    rename(ra09pfr:ra09l1r)
     ffusrl1    if   e           k disk    rename(fusrpfr:fusrl1r)
      *
     ffo711d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av parametre
      *
     d p_firm          s                   like(fofirm)
6.nu d p_numm          s                   like(fonumm)
     d p_suff          s                   like(fosuff)
     d p_stat          s              1
      *
      * Definering av variabler i nøkler
      *
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d ausrl1_user     s                   like(abuser)
     d ra09l1_ikod     s                   like(raikod)
     d fusrl1_user     s                   like(fbuser)
      *
      * Definering av variable
      *
     d w_firm          s                   like(fofirm)
     d w_kode          s              1
     d w_svar          s              1
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_user          s             10
     d w_wsid          s             10
     d w_aarr          s              4  0
     d w_retu          s              1
     d w_enor          s              1
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * X1 Behandler opplysning om hvem som holder ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter inn opplysninger fra Ordre-Hode-Tabell
      *
     c                   eval      fohel1_numm = p_numm
     c                   eval      fohel1_suff = p_suff
     c     fohel1_key    chain     fohel1r
     c                   if        %found
     c                   eval      x1kous = fokous
     c                   eval      x1kows = fokows
     c                   if        fokoda <> *loval
     c     *dmy          move      fokoda        x1koda
     c                   endif
     c                   if        fokoti <> *loval
     c     *hms          move      fokoti        x1koti
     c                   endif
     c                   if        fodate <> *loval
     c     *dmy          move      fodate        x1date
     c                   endif
     c                   if        fotime <> *loval
     c     *hms          move      fotime        x1time
     c                   endif
      *
      * Henter ut dato og kl. for
      * når ordren er sendt til plukking
      *
     c                   eval      w_kode = '2'
     c                   eval      w_aarr = %subdt(fobdat:*years)
     c                   call      'FU704R'
     c                   parm                    w_aarr
     c                   parm                    fonumm
     c                   parm                    fosuff
     c                   parm                    w_kode
     c                   parm                    w_svar
     c                   parm                    w_date
     c                   parm                    w_time
     c                   parm                    w_user
     c                   parm                    w_wsid
      *
     c                   if        w_svar = 'J'
      *
     c                   if        w_date <> *loval
     c     *dmy          move      w_date        x1plda
     c                   endif
     c                   if        w_time <> *loval
     c     *hms          move      w_time        x1plti
     c                   endif
     c                   endif
      *
      * Henter inn opplysninger fra Bruker-Tabell
      *
     c                   if        fokous <> *blank
     c                   eval      ausrl1_user = fokous
     c     ausrl1_key    chain     ausrl1r
     c                   if        %found
     c                   eval      x1knav = abnavn
     c                   endif
     c                   endif
      *
     c                   eval      ausrl1_user = fouser
     c     ausrl1_key    chain     ausrl1r
     c                   if        %found
     c                   eval      x1onav = abnavn
     c                   endif
      *
      * Henter inn opplysninger fra selger
      *
     c                   if        foplse <> 0
     c                   eval      ra09l1_ikod = foplse
     c     ra09l1_key    chain     ra09l1r
     c                   if        %found
     c                   eval      x1plna = raitxt
     c                   endif
     c                   endif
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *    Behandler skjembilde om hvem som holder ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hvis ikke ordren er sperret, skal ikke bilde sendes
     c                   if        fokode = 0
     c                   goto      xslutt
     c                   endif
      * Send bilde
      *
     c     e1tabg        tag
     c                   exfmt     x1bld
      *
     c                   eval      *in31 = *off
      * Gå tilbake til forrige bilde
      *
     c                   if        *inkc
     c                   eval      p_stat = '2'
     c                   goto      xslutt
     c                   endif
      *
      * Fjern sperrekode på ordren
      *
     c                   if        *inkh
      * Sjekk om brukeren har rettigheter til å fjerne koden
     c                   if        fbko03 = 0
     c                   eval      *in31 = *on
     c                   goto      e1tabg
     c                   endif
      *  Kaller opp subprogrammet for sletting av utplukk
      *  og blanking av behandlinskode i ORDRE-HODE-REGISTER
     c                   call      'FO412R'
     c                   parm                    w_retu
     c                   parm                    w_firm
6.nu c                   parm                    p_numm
     c                   parm                    p_suff
      *
      *  Beregner nye summer for ordre
      *
     c                   eval      w_enor = *blank
     c                   call      'FO730R'
     c                   parm                    w_enor
     c                   parm                    w_firm
6.nu c                   parm                    p_numm
     c                   parm                    p_suff
     c                   goto      xslutt
     c                   endif
      *
     c                   goto      e1tabg
      *
      * Avslutt program
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
6.nu c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_stat
      *
      * Key for Ordre-hode-tabell
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      * Key for brukerregister
      *
     c     ausrl1_key    klist
     c                   kfld                    ausrl1_user
      *
      * Key for selger-register
      *
     c     ra09l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra09l1_ikod
      *
      * Key for brukerregister
      *
     c     fusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fusrl1_user
      *
      *
      * Henter firmanummer fra LDA
      *
     c                   eval      w_firm = l_firm
     c                   eval      x1numm = p_numm
      *
     c                   eval      fusrl1_user = l_user
     c     fusrl1_key    chain     fusrl1r
     c                   if        not %found
     c                   eval      fbko03 = 0
     c                   endif
      *
     c                   endsr
