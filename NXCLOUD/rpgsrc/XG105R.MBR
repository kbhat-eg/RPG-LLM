     h option(*nodebugio) datedit(*dmy)
      *
      * System  . . . . : ASVGRP
      * Program . . . . : xg105R
      * Beskrivelse . . : Varer i varegr.konv, vedlikehold
      *                   Det er kun mulig å redigere de som ikke er ihht
      *                   konvertering.
      *                   Det er ikke mulig å legge inn nye, kun rette til varegr.
      *
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       121015 bof  Nytt program
      *
8.cl  *K0000  231023 bsa  Gjennomgått mtp cloud
8.02  *K0000  311023 bof  Ønsker å ha filter utfylt på 1
8.03  *K0000  071123 bof  utvidet med test mot xvgkl4
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fxvgvl1    if   e           k disk    rename(xvgvpfr:xvgvl1r)
     fxvgvl2    if   e           k disk    rename(xvgvpfr:xvgvl2r)
     fxvgvlr    if   e           k disk    rename(xvgvpfr:xvgvlrr)
     fxvgvlu    uf a e           k disk    rename(xvgvpfr:xvgvlur)
     fxvgrlr    if   e           k disk    rename(xvgrpfr:xvgrlrr)
8.03 fxvgkl2    if   e           k disk    rename(xvgkpfr:xvgkl2r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
      *
     fxg105d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av LDA (Local Data Area)
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av array
      *
      *
      * Definering av variabler i nøkler
      *
     d xvgvl1_nobb     s                   like(xvnobb)
     d xvgvl2_fogr     s                   like(xvfogr)
     d xvgvl2_fhgr     s                   like(xvfhgr)
     d xvgvl2_fugr     s                   like(xvfugr)
     d xvgvlr_nobb     s                   like(xvnobb)
     d xvgvlu_nobb     s                   like(xvnobb)
     d xvgrlr_ogrp     s                   like(xnogrp)
     d xvgrlr_hgrp     s                   like(xnhgrp)
     d xvgrlr_ugrp     s                   like(xnugrp)
8.03 d xvgkl2_ogrg     s                   like(xgogrg)
8.03 d xvgkl2_hgrg     s                   like(xghgrg)
8.03 d xvgkl2_ugrg     s                   like(xgugrg)
8.03 d xvgkl2_ogrp     s                   like(xgogrp)
8.03 d xvgkl2_hgrp     s                   like(xghgrp)
8.03 d xvgkl2_ugrp     s                   like(xgugrp)
     d vvarl1_vare     s                   like(vvvare)
     d jlevl1_ldor     s                   like(jlldor)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_oppr          s              1    inz(*off)
      *
     d w_firm          s                   like(vvfirm)
      *
     d s_nobb          s                   like(xvnobb)
      *
      * Definering av konstanter
      *
8.cl d c_sfil          s              2  0 inz(14)
      *
      /eject
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_srrn - Relativt recordnummer i subfile
      * w_spge - Sidestørrelse i subfile (page)
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * wvisrn - Vis den side som inneholder denne record.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meleingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      * S1WIN  - S1 Standardvindu som inneholder definisjon av
      *             størrelse og utseende.  Bildene D1WIN og K1WIN
      *             er referert til dette.
      *
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inka
     c                   exsr      xf1win
     c                   exsr      forny
     c                   goto      b2taga
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c*                  when      *inkf
     c*                  exsr      xc1bld
     c*                  goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2nobb <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
     c                   if        b2fogr <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      xvgvl2_fogr = b1fogr
     c     xvgvl2_key    setll     xvgvl2
     c                   other
     c                   eval      xvgvl1_nobb = b1nobb
     c     xvgvl1_key    setll     xvgvl1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Rabattgruppe
      *
      *
      * Posisjoner startverdi på varegruppe
      *
     c                   if        b2nobb <> 0
     c                   eval      w_seqe = *blank
     c                   eval      xvgvl1_nobb = b2nobb
     c     xvgvl1_key    setll     xvgvl1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på varegruppe
      *
     c                   if        b2fogr <> 0
     c                   eval      w_seqe = 'B'
     c                   eval      xvgvl2_fogr = b2fogr
     c                   eval      xvgvl2_fhgr = b2fhgr
     c                   eval      xvgvl2_fugr = b2fugr
     c     xvgvl2_key    setll     xvgvl2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2nobb = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1nobb = b1nobb
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1nobb = b1nobb
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1nobb = b1nobb
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1nobb = b1nobb
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1nobb = b2nobb
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1nobb = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Validering
      *
     c***                if        c1nobb = 0
     c***                eval      *in31  = *on
     c***                goto      c1taga
     c***                endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      xvgvlr_nobb = c1nobb
     c     xvgvlr_key    chain     xvgvlr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1nobb = 0
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2nobb = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c*                  eval      w_arow = 0
     c*                  eval      w_acol = 0
      *
     c                   eval      xvgvlr_nobb = c1nobb
     c     xvgvlr_key    chain     xvgvlr
      *
     c                   if        %found
     c                   eval      c2fogr = xvfogr
     c                   eval      c2fhgr = xvfhgr
     c                   eval      c2fugr = xvfugr
     c                   eval      c2fgrt = xvfgrt
     c                   eval      c2togr = xvtogr
     c                   eval      c2thgr = xvthgr
     c                   eval      c2tugr = xvtugr
     c                   eval      c2tgrt = xvtgrt
      *
     c                   else
     c                   eval      c2fogr = 0
     c                   eval      c2fhgr = 0
     c                   eval      c2fugr = 0
     c                   eval      c2fgrt = *Blank
     c                   eval      c2togr = 0
     c                   eval      c2thgr = 0
     c                   eval      c2tugr = 0
     c                   eval      c2tgrt = *blank
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1nobb = k1nobb
     c                   endif
      *
     c                   if        b1valg = 5  or
     c                             xvnova = 1
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c2taga
     c                   endif
      *
      * Validering av input
      *
      *sjekker at varegruppe finnes i nye varegrupper
     c                   eval      xvgrlr_ogrp = c2togr
     c                   eval      xvgrlr_hgrp = c2thgr
     c                   eval      xvgrlr_ugrp = c2tugr
     c     xvgrlr_key    chain     xvgrlr
     c                   if        not %found(xvgrlr)
8.03  *  sjekk også mot xvgkl4
8.03 c                   eval      xvgkl2_ogrg = c2fogr
8.03 c                   eval      xvgkl2_hgrg = c2fhgr
8.03 c                   eval      xvgkl2_ugrg = c2fugr
8.03 c                   eval      xvgkl2_ogrp = c2togr
8.03 c                   eval      xvgkl2_hgrp = c2thgr
8.03 c                   eval      xvgkl2_ugrp = c2tugr
8.03 c     xvgkl2_key    chain     xvgkl2
8.03 c                   if        not %found(xvgkl2)
8.03 c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
8.03 c                   endif
      *
      * Oppdatering
      *
     c                   clear                   xvgvlur
      *
     c                   eval      xvgvlu_nobb = c1nobb
     c     xvgvlu_key    chain     xvgvlur
      *
     c                   eval      xvfogr = c2fogr
     c                   eval      xvfhgr = c2fhgr
     c                   eval      xvfugr = c2fugr
     c                   eval      xvfgrt = c2fgrt
     c                   eval      xvtogr = c2togr
     c                   eval      xvthgr = c2thgr
     c                   eval      xvtugr = c2tugr
     c                   eval      xvtgrt = c2tgrt
      * aldri vedlikehold av nobbvare, altså en annen....
     c                   eval      xvnova = 0
      *
     c                   time                    xvodat
      *
     c                   if        %found
     c                   update    xvgvlur
     c                   else
     c                   eval      xvnobb = c1nobb
     c                   write     xvgvlur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1togr = xvtogr
     c                   eval      b1thgr = xvthgr
     c                   eval      b1tugr = xvtugr
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      xvgvlr_nobb = d1nobb
     c     xvgvlr_key    chain     xvgvlr
     c                   if        %found(xvgvlr) and
     c                             xvnova = 1
     c                   leavesr
     c                   endif
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      xvgvlu_nobb = d1nobb
     c     xvgvlu_key    chain     xvgvlur
      *
     c                   delete    xvgvlur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_nobb = k1nobb
      *
      * Fyller kopierings-bildet
      *
     c                   eval      xvgvlr_nobb = k1nobb
     c     xvgvlr_key    chain     xvgvlr
     c                   if        %found(xvgvlr) and
     c                             xvnova = 1
     c                   leavesr
     c                   endif
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1nobb = 0
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      xvgvlr_nobb = k1nobb
     c     xvgvlr_key    chain     xvgvlr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1nobb = s_nobb
     c                   exsr      xc2bld
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      xvgvl2
     c                   other
     c                   read      xvgvl1
     c                   endsl
      *
     c                   if        %eof
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
      *   skal ikke vise std. konvertering
      *
9.xx c**                 if        1=2
      *
     c                   if        xvnova = 1
     c                   iter
     c                   endif
6.30  *
6.30  * Filter
6.30  *
6.30 c                   if        f1ikke = 1 and
6.30 c                             xvtogr <> 0
6.30 c                   iter
6.30 c                   endif
      *
9.xx c**                 endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      xvgvl2_fogr = xvfogr
     c                   eval      xvgvl2_fhgr = xvfhgr
     c                   eval      xvgvl2_fugr = xvfugr
     c                   other
     c                   eval      xvgvl1_nobb = xvnobb
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1nobb = xvnobb
     c                   eval      b1tek1 = xvtek1
     c                   eval      b1ldor = xvldor
     c                   eval      jlevl1_ldor = xvldor
     c     jlevl1_key    chain     jlevl1
     c                   movel     jlnavn        b1ldox
     c                   eval      b1fogr = xvfogr
     c                   eval      b1fhgr = xvfhgr
     c                   eval      b1fugr = xvfugr
     c                   eval      b1togr = xvtogr
     c                   eval      b1thgr = xvthgr
     c                   eval      b1tugr = xvtugr
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'B'
     c     xvgvl2_key    setgt     xvgvl2
     c                   readp     xvgvl2
     c                   other
     c     xvgvl1_key    setgt     xvgvl1
     c                   readp     xvgvl1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   readp     xvgvl2
     c                   other
     c                   readp     xvgvl1
     c                   endsl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      xvgvl2_fogr = xvfogr
     c                   eval      xvgvl2_fhgr = xvfhgr
     c                   eval      xvgvl2_fugr = xvfugr
     c                   other
     c                   eval      xvgvl1_nobb = xvnobb
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'B'
     c     xvgvl2_key    setll     xvgvl2
     c                   other
     c     xvgvl1_key    setll     xvgvl1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
5.60  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.60  * F1 Behandle bilde for filter
5.60  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.60  *
6.30 c     xf1win        begsr
      *
     c     f1taga        tag
      *
     c                   exfmt     f1win
6.30  *
6.30  * Behandling av funksjonstaster
6.30  *
6.30 c                   select
6.30 c                   when      *inkc or *inkl
6.30 c                   goto      end_f1win
6.30 c                   endsl
6.30  *
6.30 c     end_f1win     endsr
5.55  *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for posisjonering på rabattgruppe
      *
     c     xvgvl1_key    klist
     c                   kfld                    xvgvl1_nobb
      *
      * Key for posisjonering på rabattgruppe
      *
     c     xvgvl2_key    klist
     c                   kfld                    xvgvl2_fogr
     c                   kfld                    xvgvl2_fhgr
     c                   kfld                    xvgvl2_fugr
      *
      * Key for les på rabattgruppe
      *
     c     xvgvlr_key    klist
     c                   kfld                    xvgvlr_nobb
      *
      * Key for oppdatering av rabattgruppe
      *
     c     xvgvlu_key    klist
     c                   kfld                    xvgvlu_nobb
      *
      * Key for les av nye varegrupper
      *
     c     xvgrlr_key    klist
     c                   kfld                    xvgrlr_ogrp
     c                   kfld                    xvgrlr_hgrp
     c                   kfld                    xvgrlr_ugrp
8.03  *
8.03  * Key for les av varekobling
8.03  *
8.03 c     xvgkl2_key    klist
8.03 c                   kfld                    xvgkl2_ogrg
8.03 c                   kfld                    xvgkl2_hgrg
8.03 c                   kfld                    xvgkl2_ugrg
8.03 c                   kfld                    xvgkl2_ogrp
8.03 c                   kfld                    xvgkl2_hgrp
8.03 c                   kfld                    xvgkl2_ugrp
      *
      * Key for les av nye varegrupper
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
8.02 c                   eval      f1ikke = 1
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      xvgvl1_nobb = 0
     c     xvgvl1_key    setll     xvgvl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
