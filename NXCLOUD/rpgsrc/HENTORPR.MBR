     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Følgende kolonner kan/skal forekomme i regnearket
      *    Kontonummer
      *    Kontonavn
      *    År               Ikke utfyllt
      *    Periode          Ikke utfyllt
      *    Bevegelser
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  2017_08_02 sst Nytt program
7.00  *  2020_05_11 sst Forsøker å hente opp div oppl. via skaffevarer
7.01  *  2020_05_25 sst Justering vedr 1. linje med 'V'
7.02  *  2020_06_12 sst Justering vedr pris fra rgneark u/rabatt
7.03  *  2020_06_22 sst Fjernet jvkll3 da denne ikke er tatt i bruk
7.04  *  2020_06_22 sst Forsøk på å finne rett pris vedr prisgruppe
8.01  *PRG2 130622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Felt 1:   T/V      T = alle tekstlinjer inkl Heading
      *                     V = varelinjer og tekstliinjer til Odre
      *  Felt 2:   NobbNr/TX
      *  Felt 3:   Vareteskt 1
      *  Felt 4:   Varetekst 2
      *  Felt 5:   Antall
      *  Felt 6:   Enhet
      *  Felt 7:   Leverandør
7.02  *  Felt 8:   Pris
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffalinj    uf   f  512        disk    usropn
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
7.03 f**jvkll3    if   e           k disk    rename(jvklpfr:jvkll3r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fvpgrl1    if   e           k disk    rename(vpgrpfr:vpgrl1r)
     fvmval1    if   e           k disk    rename(vmvapfr:vmval1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffohelu    uf a e           k disk    rename(fohepfr:fohelur)
     ffodtlu    uf a e           k disk    rename(fodtpfr:fodtlur)
7.02 ffodtlr    if   e           k disk    rename(fodtpfr:fodtlrr)
      *
      * Definering av Local data area
      *
     d                uds
     d l_feil                  1      1
     d l_ws                  901    910
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d fodtlu_firm     s                   like(fdfirm)
     d fodtlu_numm     s                   like(fdnumm)
     d fodtlu_suff     s                   like(fdsuff)
     d fodtlu_line     s                   like(fdline)
     d fohelu_firm     s                   like(fofirm)
     d fohelu_numm     s                   like(fonumm)
     d fohelu_suff     s                   like(fosuff)
     d vvarl1_firm     s                   like(vvfirm)
     d vvarl1_vare     s                   like(vvvare)
     d vpgrl1_firm     s                   like(vpgfir)
     d vpgrl1_lage     s                   like(vpglag)
     d vmval1_firm     s                   like(vamfir)
     d vmval1_mkod     s                   like(vamkod)
     d rlevl1_levr     s                   like(rllevr)
7.03 d** jvkll3_uvnr     s                   like(jvquno)
     d jvarl1_vare     s                   like(jvvare)
     d fodtlr_line     s                   like(fdline)
      *
     d c_digi          s             15    inz('0123456789')
     d c_dig2          s             15    inz('0123456789 ,-')
      *
      * Definering av variable
      *
     d w_otyp          s              2      inz('VF')
     d w_otypk         s              2      inz('VK')
     d w_faty          s              2  0   inz(21)
     d w_mvak          s              1      inz('3')
     d w_mvaf          s              1      inz('4')
     d w_numm          s                     like(fdnumm)
     d w_suff          s                     like(fdsuff)
     d w_numx          s              8
     d w_sufx          s              2
     d w_anta          s                     like(fdanta)
s    d w_pris          s                     like(fdsapr)
     d w_m1            s             50
     d w_m2            s             50
     d w_sv            s              1
     d w_in12          s              1  0
      *
     d i               s              3  0
     d j               s              3  0
     d w_amdpdat       s              6  0
     d w_amdfdat       s              6  0
     d w_amdidat       s              6  0
     d w_belo          s             11  2
     d w_rbel          s             11  2
     d w_salg          s             11  2
      *
      * Definering av variable for timestamp
      *
     d w_tmst          s               z
     d w_tmst_20       s             20
     d w_line          s              4  0  inz(0)
     d w_line_alf      s              4
     d w_datx          s              6
     d w_dat           s              6  0
     d w_bdat          s              6  0
     d w_fdat          s              6  0
     d w_iso           s               d   datfmt(*iso)
     d w_isu           s               d   datfmt(*iso)
      *
     d w_kode          s              1
     d w_firm          s                     like(fofirm)
     d w_fell          s              6
     d w_type          s              2
     d w_fast          s             10
     d w_stat          s             10
     d w_feil          s             15
     d w_feilMf        s              1    inz(' ')
     d w_tran          s            256
     d w_forst         s              1    inz('J')
      *
 s   d w_totk          s             11  2
 s   d w_tots          s             11  2
 s   d w_totr          s             11  2
 s   d w_tota          s             11  2
 s   d w_totn          s             11  2
 s   d w_tot1          s             11  2
s    d w_tot2          s             11  2
s    d w_sald          s             11  2
s    d w_sal2          s             11  2
s    d b_umva          s              1    inz(*off)
      *
     d w_6             s              6
     d d_vare          s             15
     d d_anta          s             11
     d d_enhe          s              3
     d d_levr          s              6
     d d_tek1          s             30
     d d_tek2          s             30
     d d_type          s              1
s    d d_pris          s             11
      *
     d s_kund          s              6
     d s_aar           s              4
     d s_per           s              2
      *
     d p_vare          s             15
8.01 d p_prgr          s              2
     d p_enhe          s                        like(vvenhe)
7.00 d p_lety          s                        like(fdlety)
7.00 d p_ltyp          s                        like(fdltyp)
7.00 d p_kode          s              1
     d p_dato          s                        like(fobdat)
     d p_time          s                        like(fdtime)
     d p_inlr          s              1
     d p_sapr          s                        like(fdsapr)
     d p_bupr          s                        like(fdkopr)
     d p_tipr          s                        like(fdkopr)
     d p_kopr          s                        like(fdkopr)
      *
     Ifalinj    ns  01
     I                                  1  256  fatran
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Finn sist brukte linjenr
     c                   eval      fodtlu_firm  = l_firm
     c                   eval      fodtlu_numm  = %dec(w_numm:8:0)
     c                   eval      fodtlu_suff  = %dec(w_suff:2:0)
     c                   eval      fodtlu_line  = 9999
     c     fodtlu_key    setgt     fodtlu
     c                   readp     fodtlu
     c                   if        not %eof(fodtlu)
     c                             and fdnumm = fodtlu_numm
     c                             and fdsuff = fodtlu_suff
     c                   eval      w_line = fdline
     c                   else
     c                   eval      w_line = *zero
     c                   endif
      *
     c                   open      falinj
     c                   eval      w_stat = 'Kontroll'
     c                   eval      l_feil = *blank
     c                   eval      fohelu_numm = %dec(w_numm:8:0)
     c                   eval      fohelu_suff = %dec(w_suff:2:0)
     c     fohelu_key    chain     fohel1
     c                   eval      w_salg = fotots
      *
      *
     c     start         tag
     c                   read      falinj
     c                   dow       not %eof(falinj)
      *
     c                   eval      w_tran = fatran
      *
     c                   if        %subst(fatran:1:1) <> 'V'
7.01 c                   goto      end_fodt
     c                   endif
      *
     c                   eval      i = %scan('            ':fatran)
     c                   eval      %subst(fatran:i:5) = ';;;;;'
      *
     c                   eval      d_vare = *blank
     c                   eval      d_tek1 = *blank
     c                   eval      d_tek2 = *blank
     c                   eval      d_anta = *blank
     c                   eval      d_enhe = *blank
     c                   eval      d_levr = *blank
     c                   eval      d_pris = *blank
      * Æ,Ø,Å
     c                   eval      fatran= %xlate(x'77':'Ø':fatran)
     c                   eval      fatran= %xlate(x'2A':'Å':fatran)
     c                   eval      fatran= %xlate(x'46':'Æ':fatran)
     c                   eval      fatran= %xlate(x'EF':'å':fatran)
     c                   eval      fatran= %xlate(x'90':'ø':fatran)
     c                   eval      fatran= %xlate(x'A0':'æ':fatran)
     c                   eval      fatran= %xlate(x'14':'Ø':fatran)
      *
      *  Type       Felt 1
     c                   eval      i = %scan(';':fatran)
     c                   eval      d_type = %trim(%subst(fatran:1:i-1))
     c                   eval      fatran = %subst(fatran:i+1:256-i)
      *
      *  Vare       Felt 2
     c                   eval      i = %scan(';':fatran)
     c                   eval      d_vare = %trim(%subst(fatran:1:i-1))
     c                   eval      fatran = %subst(fatran:i+1:256-i)
     c                   if        d_vare <> 'TX'
      *  Høyrejust av varenr innenfor 8 siffer etter møte m/mgr
     c                   dow       d_vare <> *blank
     c                             and %subst(d_vare:8:1) = *blank
     c                   eval      %subst(d_vare:2:7) =
     c                             %subst(d_vare:1:7)
     c                   eval      %subst(d_vare:1:1) = '0'
     c                   enddo
     c                   endif
      *  Varetekst1 Felt 3
     c                   eval      i = %scan(';':fatran)
     c                   eval      d_tek1 = %trim(%subst(fatran:1:i-1))
     c                   eval      fatran = %subst(fatran:i+1:256-i)
      *
      *  Varetekst2 Felt 4
     c                   eval      i = %scan(';':fatran)
     c                   eval      d_tek2 = %trim(%subst(fatran:1:i-1))
     c                   eval      fatran = %subst(fatran:i+1:256-i)
      *
     c                   if        d_vare <> 'TX'
      *
      *  Antall     Felt 5
     c                   eval      i = %scan(';':fatran)
     c                   eval      d_anta = %trim(%subst(fatran:1:i-1))
     c                   eval      fatran = %subst(fatran:i+1:256-i)
     c                   if        %check(c_dig2:d_anta) = 0
     c                   eval      w_anta = %dec(d_anta:11:3)
     c                   endif
      *
      *  Enhet      Felt 6
     c                   eval      i = %scan(';':fatran)
     c                   eval      d_enhe = %trim(%subst(fatran:1:i-1))
     c                   eval      fatran = %subst(fatran:i+1:256-i)
      *
      *
      *  Leverandør Felt 7
     c                   eval      i = %scan(';':fatran)
     c                   eval      d_levr = %trim(%subst(fatran:1:i-1))
     c                   eval      fatran = %subst(fatran:i+1:256-i)
7.02  *
7.02  *  Pris       Felt 8
7.02 c                   eval      i = %scan(';':fatran)
7.02 c                   eval      d_pris = %trim(%subst(fatran:1:i-1))
7.02 c                   eval      fatran = %subst(fatran:i+1:256-i)
7.02 c                   eval      w_pris = 0
7.02 c                   if        d_pris <> *blank
7.02 c                             and %check(c_dig2:d_pris) = 0
7.02 c                   eval      w_pris = %dec(d_pris:9:2)
7.02 c                   endif
      *
      *    Sjekk om gyldig leverandør
     c                   if        d_levr <> *blank
     c                   eval      rlevl1_levr = %dec(d_levr:6:0)
     c     rlevl1_key    chain     rlevl1
     c                   if        not %found(rlevl1)
     c                   eval      w_m1 = 'Ukjent leverandør ' + d_levr
     c                   eval      w_line = w_line + 10
     c                   eval      w_m2 = 'Det gjelder linje ' + %char(w_line)
     c                   eval      w_line = w_line - 10
     c                   call      'AA007R'
     c                   parm                    w_m1
     c                   parm                    w_m2
     c                   parm                    w_sv
     c                   parm                    w_in12
     c                   endif
     c                   endif
      *  end_Vare<>TX
     c                   endif
      *
      *
     c     Ikke_Fler     tag
      *
      *   Bygge detaljlinje
     c                   clear                   fodtlur
      *
     c                   if        d_vare <> 'TX'
      *   Hent vare
     c                   eval      vvarl1_firm = l_firm
     c                   eval      vvarl1_vare = d_vare
     c     vvarl1_key    chain     vvarl1
      *
      *    Hvis vare ikke finnes i VVAR sjekkes om den finnes som undervare i JVKL
7.03 c**                   if        not %found(vvarl1)
7.03 c**                   eval      jvkll3_uvnr = %dec(d_vare:8:0)
7.03 c**     vvarl1_key    chain     vvarl1
7.03 c**                   endif
      *
      *    Hvis vare ikke finnes i VVAR sjekkes om den finnes i jvar i xxxlLVR
     c                   if        not %found(vvarl1)
     c                   eval      jvarl1_vare = d_vare
     c     jvarl1_key    chain     jvarl1
     c                   if        %found(jvarl1)
     c                   eval      vvvare = jvvare
     c                   eval      vvogrp = jvogrp
     c                   eval      vvhgrp = jvhgrp
     c                   eval      vvugrp = jvugrp
     c                   eval      vvenhe = jvpenh
     c                   eval      vvtek1 = jvtek1
     c                   eval      vvtek2 = jvtek2
     c                   eval      vvnobb = jvnobb
     c                   endif
7.00  *  Hente opp vare m.m. dersom ukjent vare
7.00  *
7.00 c                   eval      p_kode = '1'
7.00 c                   eval      w_line = w_line + 10
7.00 c                   call      'FD120R'
7.00 c                   parm                    p_kode
7.00 c                   parm                    w_firm
7.00 c                   parm                    w_numm
7.00 c                   parm                    w_suff
7.00 c                   parm                    w_line
7.00 c                   parm                    p_ltyp
7.00 c                   parm                    p_lety
7.00 c                   parm                    d_vare
7.00 c                   parm                    w_anta
7.02  *   Dersom regnearket skal overstyre pris må dette gjøres her
7.02  *          Skaffevarer?
7.02 c                   if        w_pris > 0
7.02 c                   eval      fodtlu_firm = w_firm
7.02 c                   eval      fodtlu_numm = w_numm
7.02 c                   eval      fodtlu_suff = w_suff
7.02 c                   eval      fodtlu_line = w_line
7.02 c     fodtlu_key    chain     fodtlu
7.02 c                   if        %found(fodtlu)
7.02 c                   eval      fdsapr = w_pris
7.02 c                   eval      fdkpri = 'F'
7.02 c                   eval      fdrab1 = 0
7.02 c                   eval      fdrab2 = 0
7.02 c                   eval      fdrab3 = 0
7.02 c                   eval      fdrab4 = 0
7.02 c                   eval      fdrab5 = 0
7.02 c                   eval      fdrkr1 = 0
7.02 c                   eval      fdrkr2 = 0
7.02 c                   eval      fdsumr = 0
7.02 c                   eval      w_salg = w_salg + fdsapr * fdanta
7.02 c                   update    fodtlur
7.02 c                   endif
7.02 c                   endif
      *
     c                   goto      end_fodt
7.00  *
     c                   endif
      *
     c                   if        %found(vvarl1)
     c                             or %found(jvarl1)
     c                   eval      vmval1_firm = l_firm
     c                   eval      vmval1_mkod = vvkmva
     c     vmval1_key    chain     vmval1
     c                   if        %found(vmval1)
     c                   eval      fdomva = vamkof
     c                   else
     c                   eval      fdomva = w_mvak
     c                   endif
     c                   eval      fdogrp = vvogrp
     c                   eval      fdhgrp = vvhgrp
     c                   eval      fdugrp = vvugrp
     c                   else
      *
      *    Vare ikke funnet
     c                   eval      fdogrp = 99
     c                   eval      fdhgrp = 99
     c                   eval      fdugrp = 999
     c                   eval      fdomva = w_mvak
     c                   eval      w_m1 = 'En eller flere varer ble ikke funnet'
     c                   eval      w_line = w_line + 10
     c                   eval      w_m2 = '1. linje: ' + %char(w_line) +
     c                                    'vare ' + d_vare
     c                   eval      w_line = w_line - 10
     c                   if        w_forst = 'J'
     c                   call      'AA007R'
     c                   parm                    w_m1
     c                   parm                    w_m2
     c                   parm                    w_sv
     c                   parm                    w_in12
     c                   eval      w_forst = 'N'
     c                   endif
     c                   endif
      *   End_TX
     c                   endif
      *
     c                   eval      w_line = w_line + 10
     c                   eval      fdnumm = %dec(w_numm:8:0)
     c                   eval      fdsuff = %dec(w_suff:2:0)
     c                   eval      fdline = w_line
     c                   eval      fdotyp = footyp
      *
     c                   if        d_vare = 'TX'
     c                   eval      fdltyp = 'TX'
     c                   eval      fdtek1 = d_tek1
     c                   eval      fdtek2 = d_tek2
     c                   else
     c                   eval      fdvare = vvvare
     c                   eval      fdenh1 = d_enhe
     c                   if        fdenh1 = *blank
     c                   eval      fdenh1 = vvenhe
     c                   endif
     c                   eval      fdlage = folage
     c                   eval      fdkund = fokund
     c                   eval      fdtek1 = vvtek1
     c                   eval      fdtek2 = vvtek2
OkHitc                   eval      fdanta = w_anta
OkHitc                   eval      fdnobb = vvnobb
      *
      *   Finn Priser
      *
     c                   eval      p_vare = d_vare
     c                   eval      p_prgr = *blank
7.04  *    Finn riktig prisgruppe
7.04  * Foreløpig
7.04 c                   if        l_user = 'G0TEGSVSTR'
7.04  * Foreløpig hit
7.04 c                   eval      vpgrl1_firm = l_firm
7.04 c                   eval      vpgrl1_lage = folage
7.04 c     vpgrl1_key    chain     vpgrl1
7.04 c                   if        %found(vpgrl1)
7.04 c                   eval      p_prgr = vpgprg
7.04 c                   else
7.04 c                   eval      p_prgr = *blank
7.04 c                   endif
7.04  * Foreløpig
7.04 c                   endif
7.04  * Foreløpig hit
7.04  *
     c                   eval      p_enhe = d_enhe
     c                   eval      p_lety = *zero
     c                   time                    p_dato
     c                   time                    p_time
     c                   call      'VP700R'
     c                   parm                    w_firm
     c                   parm                    p_vare
     c                   parm                    p_prgr
     c                   parm                    p_enhe
     c                   parm                    p_lety
     c                   parm                    p_dato
     c                   parm                    p_time
     c                   parm                    p_inlr
     c                   parm                    p_sapr
     c                   parm                    p_bupr
     c                   parm                    p_tipr
     c                   parm                    p_kopr
     c                   eval      fdoppr = p_sapr
     c                   eval      fdsapr = p_sapr
     c                   eval      fdkopr = p_kopr
s    c                   if        w_pris <> 0
s    c                   eval      fdsapr = w_pris
s    c                   eval      fdkpri = 'F'
s    c                   endif
      *
     c                   eval      w_salg = w_salg + fdsapr * fdanta
      *
      *   Ev. overstyre momskode dersom kunde er avgiftsfri
     c**Hva er sammenhengen eval      fdomva = vvkmva
      *
     c                   eval      vpgrl1_firm = l_firm
     c                   eval      vpgrl1_lage = folage
     c     vpgrl1_key    chain     vpgrl1
     c                   if        %found(vpgrl1)
     c                   eval      fdprgr = vpgprg
     c                   else
     c                   eval      fdprgr = *blank
     c                   endif
      *    end_vare_TX
     c                   endif
      *
     c                   time                    fdedat
     c                   time                    fdetim
     c                   movel     l_user        fdeusr
     c                   time                    fdodat
     c                   time                    fdotim
     c                   movel     l_user        fdoous
      *
     c                   write     fodtlur
      *
     c     end_fodt      tag
      *
     c                   read      falinj
     c                   enddo
      *
s     *   Oppdatere totaler på ordre m.m.
s     *
s    c                   eval      w_totk = 0
s    c                   eval      w_tots = 0
s    c                   eval      w_totr = 0
s    c                   eval      w_tota = 0
s    c                   eval      w_totn = 0
s    c                   eval      w_tot1 = 0
s    c                   eval      w_tot2 = 0
s    c                   eval      b_umva = *off
s     *
s    c     fodtlr_key2   setll     fodtlr
s    c     fodtlr_key2   reade     fodtlr
s    c                   dow       not %eof
s    c                   eval      w_totk = w_totk + fdsumk
s    c                   eval      w_tots = w_tots + fdsums
s    c                   eval      w_totr = w_totr + fdsumr
s     *
s    c                   eval      w_totn = fdsums
s    c                   eval      w_tot1 = 0
s    c                   eval      w_tot2 = 0
s     *
s    c                   if        fdrab1 <> 0
s    c                   eval      w_tot1 = w_totn * fdrab1 / 100
s    c                   eval      w_totn = w_totn - w_tot1
s    c                   endif
s     *
s    c                   if        fdrab2 <> 0
s    c                   eval      w_tot2 = w_totn * fdrab2 / 100
s    c                   eval      w_totn = w_totn - w_tot2
s    c                   endif
s     *
s     * NB!  w_tot1
s     * og   w_tot2 brukes om
s     * igjen og null-stilles
s     *
s    c                   eval      w_tot1 = 0
s    c                   eval      w_tot2 = 0
s     *
s    c                   if        fdbrr1 <> 0
s    c                   eval      w_tot1 = w_totn * fdbrr1 / 100
s    c                   eval      w_totn = w_totn - w_tot1
s    c                   endif
s     *
s    c                   if        fdbrr2 <> 0
s    c                   eval      w_tot2 = w_totn * fdbrr2 / 100
s    c                   eval      w_totn = w_totn - w_tot2
s    c                   endif
s     *
s    c                   eval      w_tota = w_tota + w_tot1
s    c                   eval      w_tota = w_tota + w_tot2
s     *
s     *
s     * Finnes det varelinjer som ikke har moms ?
s     *
s    c                   if        fdvare <>' '
s    c                   if        fdomva = ' ' or
s    c                             fdomva = '0' or
s    c                             fdomva = '4'
s    c                   eval      b_umva = *on
s    c                   endif
s    c                   endif
s     *
s    c     fodtlr_key2   reade     fodtlr
s    c                   enddo
s     *
s     *
s     * Beregner justeringsbeløp, og oppdaterer memosaldo
s     *
s    c     fohel1_key    chain     fohel1
s     *
s    c                   eval      w_sald = (w_tots - w_totr) -
s    c                                      (fotots - fototr)
s     *
s    c                   eval      w_sal2 = (w_tota - fobrra)
s     *
s    c                   eval      w_stat = *blank
s     *
s    c                   call      'FA922R'
s    c                   parm                    w_firm
s    c                   parm                    footyp
s    c                   parm                    fokund
s    c                   parm                    w_sald
s    c                   parm                    w_sal2
s     *
s     * Oppdaterer ordre-hode med ny saldo
s     *
s    c     fohelu_key    chain     fohelur
s    c                   eval      fotots = w_tots
s    c                   eval      fototr = w_totr
s    c                   eval      fototk = w_totk
s    c                   eval      fobrra = w_tota
s     *
s    c                   time                    foedat
s    c                   time                    foetim
s    c                   eval      foeusr = l_user
s    c                   update    fohelur
s     *
s     *   Vise dekningsbidrag
s     *
s    c                   call      'FO522R'
s    c                   parm                    w_firm
s    c                   parm                    w_numm
s    c                   parm                    w_suff
s     *
      *   Oppdatere faktura heading
     c*                  eval      fohelu_firm = l_firm
     c*                  eval      fohelu_numm = %dec(w_numm:8:0)
     c*                  eval      fohelu_suff = %dec(w_suff:2:0)
     c*    fohelu_key    chain     fohelu
     c*                  if        %found(fohelu)
     c*                  eval      fotots = w_salg
     c*                  update    fohelur
     c*                  eval      w_salg = *zero
     c*                  endif
      *  _______________________________________________________________
     c     end           tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    w_numx
     c                   parm                    w_sufx
     c                   move      w_numx        w_numm
     c                   move      w_sufx        w_suff
      *
      * Key for posisjonering på bunts
      *
     c     fohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelu_numm
     c                   kfld                    fohelu_suff
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
      *
      * Key for posisjonering på trans
      *
     c     fodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtlu_numm
     c                   kfld                    fodtlu_suff
     c                   kfld                    fodtlu_line
      *
     c     fodtlr_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
      *
      * Key for posisjonering på Vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for posisjonering på Prisgruppe-knytning
      *
     c     vpgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vpgrl1_lage
      *
      * Key for posisjonering på Momskodeknytning
      *
     c     vmval1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vmval1_mkod
      *
      * Key for oppslag       på Leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for oppslag       på JVARL1
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
      *
     ofalinj    e            FaUppd
     o                       fatran
