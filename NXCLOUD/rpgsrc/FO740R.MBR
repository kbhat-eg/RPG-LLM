     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO740R
      * Beskrivelse . . : Automatisk generering av bestillinger fra ordre
      *                   Vise bestilling på ordre
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       040206 ASK  Nytt program
      *
5.70  * PRGR  231008 sst  Prisgruppe
6.21  * 6.20  190511 bø   Utvidet vl710r med div. info
6.22  * 6.20  081111 bof  Utvidet med parameter kall til LO740r
6.NU  * R6.30 260514 bsa  Programmet gjennomgått mtp nummerutvidelser.
6.30  * 6.30  170918 bkv  Utvidet med trelast sortiment
8.01  *PRG2   090622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     ffodtl2    if   e           k disk    rename(fodtpfr:fodtl2r)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
3.17 flohel1    if a e           k disk    rename(lohepfr:lohel1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
5.32 frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvltpl1    if   e           k disk    rename(vltppfr:vltpl1r)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
6.30 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
     ffo740d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av LDA
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av parametere
      *
     d p_firm          s              3  0
6.NU d p_numm          s                   like(fonumm)
     d p_suff          s                   like(fosuff)
     d p_vise          s              1
5.70 d p_vtyp          s                   like(vvvtyp)
      *
      * Definering av variabler i nøkler
      *
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d fodtl1_numm     s                   like(fdnumm)
     d fodtl1_suff     s                   like(fdsuff)
     d fodtl2_numm     s                   like(fdnumm)
     d fodtl2_suff     s                   like(fdsuff)
     d fodtl2_ldor     s                   like(fdldor)
3.17 d lohel1_numm     s                   like(lonumm)
3.17 d lohel1_suff     s                   like(losuff)
     d rlevl1_levr     s                   like(rllevr)
5.32 d rkunl1_kund     s                   like(rkkund)
     d vltpl1_lety     s                   like(vylety)
     d vvarl1_vare     s                   like(fdvare)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_srrn_w        s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
     d w_stel          s              1  0
      *
     d w_firm          s                   like(fofirm)
6.NU d w_numm          s                   like(fonumm)
     d w_suff          s                   like(fosuff)
     d w_bnum          s                   like(lonumm)
     d w_bsuf          s                   like(losuff)
     d w_ldor          s                   like(fdldor)
     d w_valg          s              2  0
     d w_vise          s              1
     d w_pval          s              1
     d w_lpro          s              1
     d w_retu          s              1
6.21 d w_pldo          s                   like(vvldor)
6.21 d w_utda          s                   like(vvudat)
6.22 d w_retk          s              1
      *
     d b_forn          s              1    inz(*off)
6.21 d b_inlr          s              1    inz(*off)
6.30 d w_lv_nobb       s                   like(vvlnob)
6.30 d w_lv_modn       s                   like(vvlmod)
6.30 d w_lv_lldo       s                   like(vvlnle)
6.30 d w_lv_llva       s                   like(vvllva)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(11)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Behandle åpningsopplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Hent opplysninger fra ordre
      *
     c                   exsr      hent_ordre
      *
     c     fodtl2_key    setll     fodtl2
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * D1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     d1taga        tag
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   eval      *in22 = *on
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile                                  DSP SFL
      *
     c                   eval      *in22 = *off
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *in10
     c                   exsr      crt_subfile                                  CRT SFL
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on                                  CRT SFL
     c                   goto      b2taga
     c                   endsl
      *
      * F3=avslutt
      *
     c                   if        *inkc = *on
     c                   goto      avslutt
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   goto      d1taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c     fodtl2_key    setll     fodtl2
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      w_virn = w_fcrn
     c                   eval      b_forn = *off
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      w_virn = w_srrn
      *
      * Endre bestilling
      *
     c                   if        b1valg = 2
     c                   exsr      endre_best
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   eval      b_forn = *on
     c                   iter
     c                   endif
      *
      * Slett bestilling
      *
     c                   if        b1valg = 4
     c                   exsr      slett_best
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   eval      b_forn = *on
     c                   iter
     c                   endif
      *
      * Skrive ut bestilling
      *
     c                   if        b1valg = 6
     c                   exsr      skriv_best
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   eval      b_forn = *on
     c                   iter
     c                   endif
      *
      * Gå til bestilling
      *
     c                   if        b1valg = 7
     c                   exsr      bestilling
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   eval      b_forn = *on
     c                   iter
     c                   endif
      *
      * Send bestilling
      *
     c                   if        b1valg = 8
     c                   exsr      send_best
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   eval      b_forn = *on
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å endre bestilling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     endre_best    begsr
      *
     c                   call      'FO741R'
     c                   parm                    w_firm
6.NU c                   parm                    w_numm
     c                   parm                    w_suff
     c                   parm                    b1best
     c                   parm                    b1ldor
      *
     c     end_endre     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sletting av bestilling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     slett_best    begsr
      *
     c     end_slett     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift av bestilling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_best    begsr
      *
     c                   if        b1best = 0
     c                   goto      end_skriv
     c                   endif
      *
     c                   eval      w_bnum = b1best
     c                   eval      w_bsuf = 0
     c                   eval      w_pval = '1'
      *
     c                   call      'LO600R'
     c                   parm                    w_retu
     c                   parm                    w_firm
     c                   parm                    w_bnum
     c                   parm                    w_bsuf
     c                   parm                    w_pval
      *
     c     end_skriv     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for overgang til bestillingssystem
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bestilling    begsr
      *
      *
     c                   eval      w_valg = 2
     c                   if        b1best <> 0
     c                   call      'LO110R'
     c                   parm                    w_valg
     c                   parm                    b1best
     c                   parm                    b1suff
     c                   else
     c                   call      'LO500R'
     c                   parm                    b1best
     c                   parm                    w_lpro
     c                   endif
      *
     c     end           endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sending av bestilling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     send_best     begsr
      *
     c                   call      'LO740R'
     c                   parm                    b1best
     c                   parm                    b1suff
6.22 c                   parm                    w_retk
      *
     c     end_send      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil                     SFL END
     c                   eval      w_srrn = w_ssrn
     c                   eval      w_ldor = 999999
      *
     c                   dou       w_srrn = w_spge
      *
     c     fodtl2_key    reade     fodtl2
      *
     c                   if        %eof or                                      EOF
     c                             fdfirm <> w_firm or
     c                             fdnumm <> w_numm or
     c                             fdsuff <> w_suff
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   if        fdldor = w_ldor
     c                   iter
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1line = fdline
     c                   eval      b1ldor = fdldor
     c                   eval      b1best = fdbenr
     c                   eval      b1suff = fdbsuf
     c                   eval      w_ldor = fdldor
      *
      * Hent leverandør
      *
     c                   eval      rlevl1_levr = fdldor
     c     rlevl1_key    chain     rlevl1
     c                   if        %found
     c                   eval      b1lnav = rlnavn
     c                   else
     c                   eval      b1lnav = 'LEVERANDØR IKKE VALGT'
     c                   endif
      *
      * Hent bestilling
      *
     c                   eval      lohel1_numm = fdbenr
     c                   eval      lohel1_suff = fdbsuf
     c     lohel1_key    chain     lohel1
     c                   if        %found
      *
     c                   eval      b1duse = lobsav
     c                   if        lobsda <> *loval
     c     *dmy          move      lobsda        b1ddat
     c                   else
     c                   eval      b1ddat = 0
     c                   endif
      *
     c                   select
     c                   when      lobsen = 1
     c                   eval      b1dist = 'EDI   '
     c                   when      lobsen = 2
     c                   eval      b1dist = 'E-mail'
     c                   when      lobsen = 3
     c                   eval      b1dist = 'FAX   '
     c                   other
     c                   eval      b1dist = *blank
     c                   eval      b1duse = *blank
     c                   eval      b1ddat = 0
     c                   endsl
     c                   endif
      *
      * Sjekk om leverandør må bestilles
      *
     c                   exsr      sjekk_lev
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     fodtl2_key    setgt     fodtl2
     c                   readp     fodtl2
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     fodtl2
      *
     c                   if        %eof or                                      EOF
     c                             fdfirm <> w_firm or
     c                             fdnumm <> w_numm or
     c                             fdsuff <> w_suff
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   enddo
      *
     c*                  if        %eof
     c*                  eval      fodtl1_line = fdline
     c*    fodtl1_key    setll     fodtl1
     c*                  endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekk om denne leverandør har varelinjer som må bestilles
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_lev     begsr
      *
     c                   eval      b1beko = *blank
     c                   eval      fodtl1_numm = fdnumm
     c                   eval      fodtl1_suff = fdsuff
     c     fodtl1_key    setll     fodtl1
     c     fodtl1_key    reade     fodtl1
      *
     c                   dow       not %eof
      *
      * Hent vareinfo
      *
     c                   eval      vvarl1_vare = fdvare
     c     vvarl1_key    chain     vvarl1r
      *
      * Skal linje bestilles?
      *
     c                   if        fdldor = w_ldor and
     c                             fdbenr = 0
5.70  *
5.70  * Kaller program for henting av prisgruppe
5.70  *
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    vvvare
5.70 c                   parm                    fdlage
5.70 c                   parm                    p_vtyp
6.21 c                   parm                    w_utda
6.21 c                   parm                    w_pldo
6.21 c                   parm                    b_inlr
6.30 c                   parm                    w_lv_nobb
6.30 c                   parm                    w_lv_modn
6.30 c                   parm                    w_lv_lldo
6.30 c                   parm                    w_lv_llva
     c                   if        p_vtyp = 'S' or
     c                             fdlety = 1 or
     c                             fdlvre = 1
     c                   eval      b1beko = 'B'
     c                   goto      end_lev
     c                   endif
     c                   endif
      *
     c     fodtl1_key    reade     fodtl1
     c                   enddo
      *
     c     end_lev       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hent opplysninger om ordren
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_ordre    begsr
      *
      * Hent opplysninger fra ordre-hode
      *
     c     fohel1_key    chain     fohel1r
      *
     c                   eval      b2onum = fonumm
     c                   eval      b2otyp = footyp
     c                   eval      b3navn = fonavn
     c                   eval      b3adr1 = foadr1
     c                   eval      b3sted = fosted
      *
     c                   eval      b2kund = fokund
     c                   eval      b2lkun = folkun
     c                   eval      b3lety = folety
      *
      * Henter beskrivelse av leveringstype
      *
     c                   eval      b2besk = *blank
      *
     c                   eval      vltpl1_lety = folety
     c     vltpl1_key    chain     vltpl1
     c                   if        %found
     c                   eval      b2besk = vybesk
     c                   endif
      *
      * Hent opplysninger fra kunde
      *
     c                   eval      b2navn = *blank
     c                   eval      b2adr1 = *blank
     c                   eval      b2sted = *blank
     c                   eval      b2tlfn = *blank
     c                   eval      b2mobn = *blank
      *
      *
     c                   eval      rkunl1_kund = fokund
     c     rkunl1_key    chain     rkunl1
     c                   if        %found
     c                   eval      b2navn = rknavn
     c                   eval      b2adr1 = rkgate
     c                   eval      b2sted = rksted
     c                   eval      b2tlfn = rktlfn
     c                   eval      b2mobn = rkmobn
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for file : Ordre-linje-register
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
      *
      * Key for file : Ordre-linje-register leverandørsortert
      *
     c     fodtl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl2_numm
     c                   kfld                    fodtl2_suff
      *
      * Key for file : Ordre-hode-register
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
3.17  *
3.17  * Key for lese : Ordre-hode-register, for bestilling
3.17  *
3.17 c     lohel1_key    klist
3.17 c                   kfld                    w_firm
3.17 c                   kfld                    lohel1_numm
3.17 c                   kfld                    lohel1_suff
      *
      * Key for file : Leverandør-register
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
5.32  * Key for file : KUNDE-REGISTER
5.32  *
5.32 c     rkunl1_key    klist
5.32 c                   kfld                    w_firm
5.32 c                   kfld                    rkunl1_kund
      *
      * Key for file : Vare-register
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på leveringstype
      *
     c     vltpl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltpl1_lety
      *
      * Key for file : Status-koder Ofak
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for file : Status-koder Lager
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    p_firm
6.NU c                   parm                    p_numm
     c                   parm                    p_suff
      *
      * Legger inn firmanummer
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_numm = p_numm
     c                   eval      w_suff = p_suff
     c                   eval      w_vise = p_vise
      *
      * Legger inn ordrenummer og suffix i nøklene
      *
     c                   eval      fohel1_numm = p_numm
     c                   eval      fohel1_suff = p_suff
      *
     c                   eval      fodtl2_numm = p_numm
     c                   eval      fodtl2_suff = p_suff
      *
      * Henter opplysninger fra Ofak-status-register
      *
     c     fstsl1_key    chain     fstsl1r
      *
      * Henter opplysninger fra Lager-status-register
      *
     c     lstsl1_key    chain     lstsl1r
      *
     c                   endsr
