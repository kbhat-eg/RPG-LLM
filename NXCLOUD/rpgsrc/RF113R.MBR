      /TITLE       Program : RF113R     ASOKON     Sist endret: 09.08.01  RF115R
     h datedit(*dmy.) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RF113R                                              *
      * Beskrivelse . . : Utskrift av etiketter pr bilag                      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
T5.10 * R5.40     030406  Nytt program                                    KOB *
T5.10 * R5.41     300606  Hentet firma fra local                          KOB *
T5.10 * R5.60     010508  Endret slik at etiketter skrives for lagertrans KOB *
7.01  *K00        060720  lagt på return + ab705r + switchstyrt           bof *
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
610  frjoupf    if   e           k disk
     fraa1l1    if   e           k disk    rename(raa1pfr:raa1l1r)
      *
     d                uds
     d l_user                911    920
7.01 d l_filg                931    933
     d  lcwwus                        7    overlay(l_user)
      *
     d l_firm                944    946  0
      *
     d wpuser          s              7
610  d w_mem3          s             10
      *
     d w_tell          s              6  0
610  d w_nivo          s                   like(rjnivo)
610  d w_memb          s                   like(rjmemb)
610  d w_sort          s                   like(rjsort)
     d w_firm          s                   like(rjfirm)
     d w_lagr          s              2
610  d w_rjfirm        s                   like(rjfirm)
610  d w_rjraar        s                   like(rjraar)
610  d w_rjrper        s                   like(rjrper)
610  d w_rjtype        s                   like(rjtype)
610  d w_rjbunt        s                   like(rjbunt)
610  d w_rjbiln        s                   like(rjbiln)
7.01 d w_jkey          s             41
7.01 d w_jnav          s             15
7.01 d w_jsta          s              2  0
7.01 d w_jmld          s            200
      *
7.01   dcl-s co402_verdi1  char(1);
7.01   dcl-s co402_verdi2  char(2048);
7.01   DCL-PR CO402R EXTPGM('CO402R');
7.01     p_filgrp            char(3)     const;
7.01     p_firm              packed(3:0) const;
7.01     p_lager             packed(2:0) const;
7.01     p_nokkel            char(50)    const;
7.01     p_verdi1            char(1);
7.01     p_verdi2            char(2048);
7.01   END-PR;
7.01 d u_logg          s              1    inz(*off)                            logging ajltpf
      *
     c/eject
7.01 c                   if        u_logg = *on
7.01 c                   eval      w_jsta = 10
7.01 c                   eval      w_jmld = 'Start RF113R        '
7.01 c                   eval      w_jnav = 'OKON_RF113R-'
7.01 c                   call      'AB700R'
7.01 c                   parm                    w_jnav
7.01 c                   parm                    w_jkey
7.01 c                   parm                    w_jmld
7.01 c                   eval      w_jsta = 10
7.01 c                   eval      w_jmld = 'Start RF113R       '
7.01 c                   call      'AB705R'
7.01 c                   parm                    w_jkey
7.01 c                   parm                    w_jsta
7.01 c                   parm                    w_jmld
7.01 c                   endif
7.01  *
610  c                   move      *blank        w_nivo
610  c                   eval      w_memb = w_mem3
610  c     rjoupf_key    setll     rjoupf
610   *
      *
     c     dummy         tag
610   *
610  c                   read      rjoupf                                 60
610  c                   if        *in60 = *on
7.01 c                   if        u_logg = *on
7.01 c                   eval      w_jsta = 10
7.01 c                   eval      w_jmld = 'Slutt RF113R før LR'
7.01 c                   call      'AB705R'
7.01 c                   parm                    w_jkey
7.01 c                   parm                    w_jsta
7.01 c                   parm                    w_jmld
7.01 c                   endif
7.01  *
610  c                   eval      *inlr = '1'
610  c                   goto      end1
610  c                   endif
      *
610   *
610  c                   if        rjmemb <> w_mem3
7.01 c                   if        u_logg = *on
7.01 c                   eval      w_jsta = 10
7.01 c                   eval      w_jmld = 'Slutt RF113R før LR'
7.01 c                   call      'AB705R'
7.01 c                   parm                    w_jkey
7.01 c                   parm                    w_jsta
7.01 c                   parm                    w_jmld
7.01 c                   endif
7.01  *
610  c                   move      *on           *inlr
610  c                   goto      end1
610  c                   endif
610   *
      *
      * Bare poster fra bilagsregistrering behandles
      *
N5.20c                   if        lcwwus <> wpuser and
N5.20c                             w_lagr <> 'LA'
     c                   goto      end
     c                   endif
      *
      * Brudd på firmakode
610  c                   if        rjfirm <> w_rjfirm
610  c                   eval      w_rjfirm = rjfirm
     c                   move      rjfirm        w_firm
     c     raa1l1_key    chain     raa1l1r
     c                   endif
      *
      * Brudd på bilagsnummer
      *
610  c                   if        rjbiln <> w_rjbiln
610  c                   eval      w_rjbiln = rjbiln
      *
      * Test posteringstype
      *
     c                   if        rjtype = 2 or
     c                             rjtype = 3
     c                   goto      end
     c                   endif
      *
      * Det finnes prosjektposter, men de behandles ikke
      *
     c                   if        rjreid = 'P'
     c                   goto      end
     c                   endif
      *
      * Skriv etikett for Multisupport
      *
     c                   if        ra1skd = 'J'
     c                   call      'RF011C'
     c                   parm                    w_firm
     c                   parm                    rjraar
     c                   parm                    rjbiln
      *
     c                   endif
      *
     c                   endif
      *
     c     end           tag
610   *
610  c                   goto      dummy
610   *
      *
610  c     end1          tag
7.01 c                   eval      *inlr = '1'
7.01 c                   if        u_logg = *on
7.01 c                   eval      w_jsta = 10
7.01 c                   eval      w_jmld = 'Slutt RF113R etter LR'
7.01 c                   call      'AB705R'
7.01 c                   parm                    w_jkey
7.01 c                   parm                    w_jsta
7.01 c                   parm                    w_jmld
7.01 c                   endif
7.01 c                   return
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    wpuser
610  c                   parm                    w_mem3
      *
     c                   move      l_firm        w_firm
     c                   movel     wpuser        w_lagr
      *
      * Key for statusrecord
      *
     c     raa1l1_key    klist
     c                   kfld                    w_firm
      *
610  c     rjoupf_key    klist
610  c                   kfld                    w_nivo
610  c                   kfld                    w_memb
610  c                   kfld                    w_sort
7.01  * Test om logging skal gjøres
7.01  *
7.01   CallP CO402R(l_filg:w_firm:0:'RF-PROG LOGGING':
7.01                    co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01     u_logg = *on;
7.01   endif;
610   *
     c                   endsr
