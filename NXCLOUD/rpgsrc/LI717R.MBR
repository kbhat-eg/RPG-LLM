     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LI717R
      * Beskrivelse . . : Summerer EDI medling
      *
      * Spesialversjon. : Mestergruppen
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       090916 bsa  Nytt program
7.01  *       070617 bsa  Vi flagger linjer med beløpsfeil
7.02  *       300119 bsa  Skille melom netto og brutto avhengig av
7.02  *                   mva kode.
7.03  *K0000  230821 bsa  GTIN nummer er lagt ut med 14 siffer.
      *
8.01  *K0000  230621 BSA  Utvidet meldingsnummer fra 6-8 siffer
8.02  *K0000  251121 BSA  Sjekker for å unngå at kalkulasjon feiler
8.03  *K0000  121222 BSA  Sørger for å nullstille mva sats.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fledil1    if   e           k disk    rename(ledipfr:ledil1r)
7.01 fledilr    uf   e           k disk    rename(ledipfr:ledilrr)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
      *
      * Definering av Local Data Arae (LDA)
      *
     d                uds
     d l_user                911    920
     d l_fnav                951    980
      *
      * Parameter-inn
      *
     d p_stat          s              1
     d p_type          s              4
8.01 d p_mnum          s              8  0
      *
      * FAKTURA Hode
      *
     d d_fakh1         ds
     d  d_fh_dtyp                     3
     d  d_fh_fanr                    35
     d  d_fh_fako                     3
     d  d_fh_ldat                      d   datfmt(*iso)
     d  d_fh_best                    20
     d  d_fh_lord                    20
     d  d_fh_lfak                    20
     d  d_fh_sean                    13  0
     d  d_fh_snav                    30
     d  d_fh_fean                    13  0
     d  d_fh_fnav                    30
     d  d_fh_fad1                    30
     d  d_fh_fad2                    30
     d  d_fh_fste                    30
     d  d_fh_forg                    20
     d  d_fh_kref                    20
     d  d_fh_lref                    20
     d  d_fh_valk                     3
     d  d_fh_ffda                      d   datfmt(*iso)
     d  d_fh_levb                     3
     d  d_fh_forf                      d   datfmt(*iso)
     d  d_fh_bean                    13  0
     d  d_fh_biln                     8  0
      *
      * Tillegg/fradragslinjer
      *
     d d_fakh2         ds
     d  d_fh_tsig                     3
     d  d_fh_tkod                     3
     d  d_fh_ttek                    30
     d  d_fh_tpro                     4  2
     d  d_fh_tbel                     8  2
     d  d_fh_apro                     4  2
      *
      * Fritekst/hodelinje
      *
     d d_fakh3         ds
     d  d_fh_ftxq                     3
     d  d_fh_ftxt                    70
      *
      * Faktura partsinfo./hode
      *
     d d_fakh5         ds
     d  d_fh_part                     3
     d  d_fh_pean                    13  0
     d  d_fh_pnav                    30
     d  d_fh_pad1                    30
     d  d_fh_pad2                    30
     d  d_fh_pste                    30
     d  d_fh_porg                    20
      *
      * Faktura transport/lev. - hode
      *
     d d_fakh6         ds
     d  d_fh_trmo                     3
     d  d_fh_trna                    35
     d  d_fh_lebe                     3
      *
      * FAKTURA Linje
      *
     d d_fakl          ds
     d  d_fl_llin                     6  0
7.03 d  d_fl_vean                    14  0
     d  d_fl_vnob                     8  0
     d  d_fl_vlev                    15
     d  d_fl_tek1                    35
     d  d_fl_tek2                    35
     d  d_fl_bkva                    15  3
     d  d_fl_benh                     3
     d  d_fl_lkva                    15  3
     d  d_fl_lenh                     3
     d  d_fl_fkva                    15  3
     d  d_fl_fenh                     3
     d  d_fl_bnet                    15  2
     d  d_fl_bbto                    15  2
     d  d_fl_prkv                     3
     d  d_fl_pris                    15  3
     d  d_fl_penh                     3
     d  d_fl_prpr                     9  0
     d  d_fl_bnum                    20
     d  d_fl_blin                     6  0
     d  d_fl_mvap                     4  2
     d  d_fl_sig1                     3
     d  d_fl_kod1                     3
     d  d_fl_bel1                     9  2
     d  d_fl_pro1                     5  2
     d  d_fl_sig2                     3
     d  d_fl_kod2                     3
     d  d_fl_bel2                     9  2
     d  d_fl_pro2                     5  2
     d  d_fl_sig3                     3
     d  d_fl_kod3                     3
     d  d_fl_bel3                     9  2
     d  d_fl_pro3                     5  2
     d  d_fl_sig4                     3
     d  d_fl_kod4                     3
     d  d_fl_bel4                     9  2
     d  d_fl_pro4                     5  2
     d  d_fl_sig5                     3
     d  d_fl_kod5                     3
     d  d_fl_bel5                     9  2
     d  d_fl_pro5                     5  2
     d  d_fl_sig6                     3
     d  d_fl_kod6                     3
     d  d_fl_bel6                     9  2
     d  d_fl_pro6                     5  2
     d  d_fl_vevr                    15
      *
      * Fakturatotallinjer
      *
     d d_fakt          ds
     d  d_ft_ftot                    15  2
     d  d_ft_teks                    15  2
     d  d_ft_tmva                    15  2
     d  d_ft_mvgr                    15  2
     d  d_ft_veks                    15  2
     d  d_ft_vink                    15  2
     d  d_ft_stil                    15  2
     d  d_ft_sfra                    15  2
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d ledilr_type     s                   like(litype)
     d ledilr_mnum     s                   like(limnum)
     d ledilr_mlin     s                   like(limlin)
     d ledil1_type     s                   like(litype)
     d ledil1_mnum     s                   like(limnum)
     d ledil1_mlin     s                   like(limlin)
     d rlevl1_levr     s                   like(rllevr)
      *
      * Definering av variable i parametre
      *
      *
      * Definering av variable
      *
     d b_forn          s              1    inz(*off)
     d b_vare          s              1    inz(*off)
      *
     d w_mnum          s                   like(limnum)
     d w_mlin          s                   like(limlin)
     d w_firm          s                   like(lifirm)
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_year          s              4  0
     d w_ldor          s                   like(lildor)
     d w_kund          s                   like(linumm)
     d w_ltot          s             15  2
7.02 d w_mtot          s             15  2
     d w_pris          s             15  3
7.02 d w_mpri          s             15  3
     d w_mvap          s              4  2
     d w_prig          s             15  3
     d w_frad          s             15  3
     d w_ftot          s             15  2
     d w_diff          s             15  2
7.01 d w_lint          s             15  3
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(10)
      *
     d lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
     d digit           c                   '0123456789 '
     d null            c                   '0000000000000'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * D1 Behandle åpningsopplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      p_stat = *blanks
      *
     c                   exsr      gen_diff
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Regner ut linjebeløp mot total basert på beløpskontrol
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     gen_diff      begsr
      *
     c                   eval      w_ltot = 0
     c                   eval      w_diff = 0
      *
     c                   if        d_fh_dtyp = '381' and
     c                             d_ft_ftot < 0
     c                   eval      p_stat = 'B'
     c                   goto      end_diff
     c                   endif
      *
     c                   eval      w_ftot = d_ft_ftot
     c                   exsr      finn_lint
     c                   exsr      finn_tifr
7.02 c**                 eval      w_diff = w_ftot - w_ltot
7.02 c                   eval      w_diff = w_ftot - w_mtot
      *
     c                   if        w_diff > 5 or
     c                             w_diff < -5
     c                   eval      p_stat = 'B'
     c                   endif
      *
     c     end_diff      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finne sum netto varelinjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_lint     begsr
      *
     c                   eval      w_ltot = 0
7.02 c                   eval      w_mtot = 0
      *
     c                   eval      ledilr_type = 'FAKT'
     c                   eval      ledilr_mnum = p_mnum
     c                   eval      ledilr_mlin = 500001
     c     ledilr_key    setll     ledilr
     c                   read      ledilr
      *
     c                   dow       not %eof and
     c                             limlin < 550000
      *
     c                   eval      d_fakl = limeld
      *
      * Sjekk om fritekstlinje
      *
     c                   if        d_fl_vnob = 0 and
     c                             d_fl_fenh = *blank and
     c                             d_fl_fkva = 0
     c                   goto      les_neste
     c                   endif
      *
      * Akkumuler netto linjebeløp
      *
     c                   exsr      finn_pris
     c                   eval      w_ltot = w_ltot + (w_pris * d_fl_fkva)
7.02 c                   eval      w_mtot = w_mtot + (w_mpri * d_fl_fkva)
7.01  * Er det beløpsavvik på linja ??
7.01 c                   eval(h)   w_lint = (w_pris * d_fl_fkva)
7.01  * Sjekk først mot linjenetto
7.01 c                   if        d_fl_bnet <> 0
7.01 c                   eval      w_diff = w_lint - d_fl_bnet
7.01 c                   if        w_diff > 5 or
7.01 c                             w_diff < -5
7.01 c                   eval      listat = 'B'
7.01 c                   eval      p_stat = 'B'
7.01 c                   endif
7.01  * Hvis ikke netto, forutsetter vi at det kun er brutto (dvs ingen rabatt)
7.01 c                   elseif    d_fl_bbto <> 0
7.01 c                   eval      w_diff = w_lint - d_fl_bbto
7.01 c                   if        w_diff > 5 or
7.01 c                             w_diff < -5
7.01 c                   eval      listat = 'B'
7.01 c                   eval      p_stat = 'B'
7.01 c                   endif
7.01 c                   endif
      *
     c     les_neste     tag
7.01  *
7.01 c                   update    ledilrr
7.01  *
     c                   read      ledilr
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å beregne nettopris for beløpskontroll
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_pris     begsr
      *
      * Nullstiller
      *
     c                   eval      w_prig = d_fl_pris
      *
      * Tillegg 1
      *
     c                   eval      w_frad = 0
7.01 c                   if        d_fl_bel1  <> 0
8.02 c                   if        d_fl_fkva = 0
8.02 c                   eval      d_fl_fkva = 1
8.02 c                   endif
7.01 c                   eval(h)   w_frad = d_fl_bel1 / d_fl_fkva
7.01 c                   else
     c                   if        d_fl_pro1 <> 0
     c                   eval(h)   w_frad = (w_prig * d_fl_pro1 ) / 100
     c                   endif
     c                   endif
      *
     c                   if        d_fl_sig1 = 'A  '
     c                   eval      w_prig = w_prig - w_frad
     c                   else
     c                   eval      w_prig = w_prig + w_frad
     c                   endif
      *
      * Tillegg 2
      *
     c                   eval      w_frad = 0
7.01 c                   if        d_fl_bel2  <> 0
7.01 c                   eval(h)   w_frad = d_fl_bel2 / d_fl_fkva
7.01 c                   else
     c                   if        d_fl_pro2 <> 0
     c                   eval(h)   w_frad = (w_prig * d_fl_pro2 ) / 100
     c                   endif
     c                   endif
      *
     c                   if        d_fl_sig2 = 'A  '
     c                   eval      w_prig = w_prig - w_frad
     c                   else
     c                   eval      w_prig = w_prig + w_frad
     c                   endif
      *
      * Tillegg 3
      *
     c                   eval      w_frad = 0
7.01 c                   if        d_fl_bel3  <> 0
7.01 c                   eval(h)   w_frad = d_fl_bel3 / d_fl_fkva
7.01 c                   else
     c                   if        d_fl_pro3 <> 0
     c                   eval(h)   w_frad = (w_prig * d_fl_pro3 ) / 100
     c                   endif
     c                   endif
      *
     c                   if        d_fl_sig3 = 'A  '
     c                   eval      w_prig = w_prig - w_frad
     c                   else
     c                   eval      w_prig = w_prig + w_frad
     c                   endif
      *
      * Tillegg 4
      *
     c                   eval      w_frad = 0
7.01 c                   if        d_fl_bel4  <> 0
7.01 c                   eval(h)   w_frad = d_fl_bel4 / d_fl_fkva
7.01 c                   else
     c                   if        d_fl_pro4 <> 0
     c                   eval(h)   w_frad = (w_prig * d_fl_pro4 ) / 100
     c                   endif
     c                   endif
      *
     c                   if        d_fl_sig4 = 'A  '
     c                   eval      w_prig = w_prig - w_frad
     c                   else
     c                   eval      w_prig = w_prig + w_frad
     c                   endif
      *
      * Tillegg 5
      *
     c                   eval      w_frad = 0
7.01 c                   if        d_fl_bel5  <> 0
7.01 c                   eval(h)   w_frad = d_fl_bel5 / d_fl_fkva
7.01 c                   else
     c                   if        d_fl_pro5 <> 0
     c                   eval(h)   w_frad = (w_prig * d_fl_pro5 ) / 100
     c                   endif
     c                   endif
      *
     c                   if        d_fl_sig5 = 'A  '
     c                   eval      w_prig = w_prig - w_frad
     c                   else
     c                   eval      w_prig = w_prig + w_frad
     c                   endif
      *
      * Tillegg 6
      *
     c                   eval      w_frad = 0
7.01 c                   if        d_fl_bel6  <> 0
7.01 c                   eval(h)   w_frad = d_fl_bel6 / d_fl_fkva
7.01 c                   else
     c                   if        d_fl_pro6 <> 0
     c                   eval(h)   w_frad = (w_prig * d_fl_pro6 ) / 100
     c                   endif
     c                   endif
      *
     c                   if        d_fl_sig6 = 'A  '
     c                   eval      w_prig = w_prig - w_frad
     c                   else
     c                   eval      w_prig = w_prig + w_frad
     c                   endif
      *
      * Nobb pris?
      *
8.03 c                   eval      w_mvap = 0
     c                   if        d_fl_mvap > 0
     c                   eval      w_mvap = d_fl_mvap
     c                   else
     c                   if        rlmkod = 0
     c                   eval      w_mvap = 25.00
     c                   endif
     c                   endif
      *
     c                   if        d_fl_prkv = 'AAA'
7.02 c                   eval(h)   w_pris = d_fl_pris
7.02 c                   eval(h)   w_mpri = d_fl_pris +
7.02 c                                      (d_fl_pris * w_mvap) / 100
     c                   else
7.02 c                   eval(h)   w_pris = w_prig
7.02 c                   eval(h)   w_mpri = w_prig +
7.02 c                                      (w_prig * w_mvap) / 100
     c                   endif
      *
     c     end_pris      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finne tillegg/fradrag på hodenivå
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_tifr     begsr
      *
     c                   eval      ledil1_type = 'FAKT'
     c                   eval      ledil1_mnum = p_mnum
     c                   eval      ledil1_mlin = 600001
     c     ledil1_key    setll     ledil1
     c                   read      ledil1
      *
     c                   dow       not %eof and
     c                             limlin < 650000
      *
     c                   eval      d_fakh2 = limeld
      *
      * Akkumuler tillegg/fradrag
      *
     c                   if        d_fh_apro > 0
     c                   eval      w_mvap = d_fh_apro
     c                   else
     c                   if        rlmkod = 0
     c                   eval      w_mvap = 25.00
     c                   endif
     c                   endif
      *
     c                   eval      w_pris = d_fh_tbel +
     c                                      ((d_fh_tbel * w_mvap)/100)
      * Gjør om til absoluttverdi
     c                   if        w_pris < 0
     c                   eval      w_pris = w_pris * -1
     c                   endif
      *
     c                   if        d_fh_tsig = 'C'
     c                   eval      w_ltot = w_ltot + w_pris
     c                   else
     c                   eval      w_ltot = w_ltot - w_pris
     c                   endif
      *
     c                   read      ledil1
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_type
     c                   parm                    p_mnum
     c                   parm                    p_stat
      *
      *  Key for les av MELDINGSLINJER
      *
     c     ledil1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ledil1_type
     c                   kfld                    ledil1_mnum
     c                   kfld                    ledil1_mlin
      *
      *  Key for les av MELDINGSLINJER
      *
     c     ledilr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ledilr_type
     c                   kfld                    ledilr_mnum
     c                   kfld                    ledilr_mlin
      *
      * Key for oppslag på RLEV
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Henter verdier fra parmametre
      *
     c                   time                    w_date
     c                   time                    w_time
     c                   eval      w_year = %subdt(w_date:*years)
      *
     c                   eval      ledilr_type = p_type
     c                   eval      ledilr_mnum = p_mnum
     c                   eval      ledil1_type = p_type
     c                   eval      ledil1_mnum = p_mnum
      * Finner info fra EDI mneldinga
     c                   eval      ledil1_mlin = 100001
     c     ledil1_key    chain     ledil1
     c                   if        %found
     c                   eval      d_fakh1 = limeld
     c                   eval      w_ldor = lildor
     c                   eval      w_kund = likund
     c                   endif
      *
     c                   eval      ledil1_mlin = 900001
     c     ledil1_key    chain     ledil1
     c                   if        %found
     c                   eval      d_fakt = limeld
     c                   endif
      * Hent info fra leverandør
     c                   eval      rlevl1_levr = w_ldor
     c     rlevl1_key    chain     rlevl1
      *
     c                   endsr
