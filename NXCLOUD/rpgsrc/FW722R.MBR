      /title  ASOFAK Lev.vare, henter vareregister - Sørbø
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FW722R
      * Beskrivelse . . : Danning av leverandør vareregister, Sørbø
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       100800 HKO  Nytt program
      * 6.10  230609 BSA  Oppdatert med sporingsinformasjon
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fflvwpf    if   e           k disk
     ffenkl1    if   e           k disk    rename(fenkpfr:fenkl1r)
     fflvapf    o  a e             disk
      *
6.10  * Local data area
6.10  *
6.10 d                uds
6.10 d l_user                911    920
      *
      * Definering av datastruktur
      *
      *  Recordbeskrivelse - Sørbø
      *
     d                 ds
     d d_inpu                       122
     d  d_ekod                        1    overlay(d_inpu)                      Endringskode
     d  d_vare                       15    overlay(d_inpu:2)                    Varenr
     d  d_tek1                       35    overlay(d_inpu:17)                   Varetekst
     d  d_eann                       13    overlay(d_inpu:52)                   EAN-nr
     d  d_ldor                        6    overlay(d_inpu:65)                   Leverandør
     d  d_dato                        6  0 overlay(d_inpu:71)                   Endringsdato
     d  d_enh1                        4    overlay(d_inpu:77)                   Enhet
     d  d_sapr                        9  2 overlay(d_inpu:81)                   Salgspris
     d  d_ipny                        9  2 overlay(d_inpu:90)                   Innkjøpspris
     d  d_enh2                        4    overlay(d_inpu:99)                   Enhet 2
     d  d_gpny                        9  2 overlay(d_inpu:103)                  Salgspris enhet 2
     d  d_bmen                       11    overlay(d_inpu:112)                  Bestillingsmengde
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_ldor          s              6  0
      *
      * Definering av variable i nøkler
      *
     d fenkl1_enhf     s                   like(feenhf)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser input-fil, og legger data ut på leverandør vareregister
      *
     c                   read      flvwpf                                 90
     c                   dow       *in90 = *off
      *
     c                   eval      d_inpu = fwirad
      *
      * Skriver til leverandør vareregister
      *
     c                   eval      fwfirm = p_firm
     c                   eval      fwldor = p_ldor
     c                   eval      fwekod = d_ekod
     c                   eval      fwtek1 = d_tek1
     c                   eval      fwtek2 = *blank
     c                   eval      fwlvar = d_vare
     c                   eval      fweann = 0
     c                   eval      fwnobb = 0
     c                   eval      fwenhe = d_enh1
     c                   eval      fwinpr = d_ipny
     c                   eval      fwsapr = d_sapr
     c                   eval      fwdato = *loval
     c                   eval      fwbmen = 0
     c                   eval      fwhkod = 1
      *
     c                   eval      fwline = fwline + 1
      *
     c                   move      d_eann        fweann
      *
     c                   eval      fenkl1_enhf = d_enh1
     c     fenkl1_key    chain     fenkl1                             91
     c                   if        *in91 = *off
     c                   eval      fwenhe = feenht
     c                   endif
      *
     c                   if        d_dato <> 0
     c     *ymd          move      d_dato        fwdato
     c                   endif
      *
     c                   if        d_bmen <> *blank
     c                   move      d_bmen        fwbmen
     c                   endif
      *
6.10 c                   time                    fwodat
6.10 c                   time                    fwotim
6.10 c                   eval      fwousr = l_user
6.10 c                   time                    fwedat
6.10 c                   time                    fwetim
6.10 c                   eval      fweusr = l_user
     c                   write     flvapfr
      *
     c                   read      flvwpf                                 90
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ldor
      *
      * Key for oppslag på enhet-konvertering
      *
     c     fenkl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fenkl1_enhf
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
