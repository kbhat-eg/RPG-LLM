      /TITLE  ASOFAK Lev.vare, henter vareregister - Arvid Nilsson
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FW706R
      * Beskrivelse . . : Danning av leverandør vareregister, Arvid Nilsson
      *
      *                   Prisdato settes lik dagens dato
      *                   Enhet hentes fra basisenhet på vareregisteret
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       060699 HKO  Nytt program
      *       180202 HKO  Merker enhet med 'CEN'
      * 6.10  230609 BSA  Oppdatert med sporingsinformasjon
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fflvwpf    if   e             disk
     fflvapf    uf a e             disk
      *
6.10  * Local data area
6.10  *
6.10 d                uds
6.10 d l_user                911    920
      *
      * Definering av datastruktur
      *
      *  Recordbeskrivelse - Arvid Nilsson
      *
     d                 ds
     d d_inpu                        85
     d  d_vare                        8    overlay(d_inpu:1)                    Varenr
     d  d_tek1                       32    overlay(d_inpu:9)                    Varetekst
     d  d_sapr                        9  2 overlay(d_inpu:41)                   Salgspris
     d  d_inpr                        9  2 overlay(d_inpu:50)                   Innkjøpspris
     d  d_anta                        7  0 overlay(d_inpu:59)                   Antall
     d  d_vekt                        7  0 overlay(d_inpu:66)                   Vekt
     d  d_eann                       13  0 overlay(d_inpu:73)                   EAN-nr
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_ldor          s              6  0
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_ldor          s              6  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser input-fil, og legger data ut på leverandør vareregister
      *
     c                   read      flvwpf                                 90
     c                   dow       *in90 = *off
      *
     c                   eval      d_inpu = fwirad
      *
      * Skriver til leverandør vareregister
      *
     c                   eval      fwfirm = p_firm
     c                   eval      fwldor = p_ldor
     c                   eval      fwtek1 = d_tek1
     c                   eval      fwtek2 = *blank
     c                   eval      fwlvar = d_vare
     c                   eval      fweann = d_eann
     c                   eval      fwnobb = 0
     c                   eval      fwenhe = 'CEN'
     c                   eval      fwinpr = d_inpr
     c                   eval      fwsapr = d_sapr
     c                   eval      fwvekt = d_vekt
     c                   eval      fwanta = d_anta
     c                   eval      fwhkod = 1
      *
     c                   eval      fwline = fwline + 1
      *
     c                   time                    fwdato
      *
6.10 c                   time                    fwodat
6.10 c                   time                    fwotim
6.10 c                   eval      fwousr = l_user
6.10 c                   time                    fwedat
6.10 c                   time                    fwetim
6.10 c                   eval      fweusr = l_user
     c                   write     flvapfr
      *
     c                   read      flvwpf                                 90
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ldor
      *
      *
      * Henter firma og leverandør fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_ldor = p_ldor
      *
     c                   endsr
