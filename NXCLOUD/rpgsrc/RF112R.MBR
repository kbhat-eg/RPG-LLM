      /TITLE       Program : RF112R     ASOKON     Sist endret: 09.08.01  RF115R
     h datedit(*dmy.) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RF112R                                              *
      * Beskrivelse . . : Utskrift av kontroll-liste                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
T5.00 * R5.00     111002  Overflow-ind. ikke satt av.                     HFL *
T5.00 * R5.00     151002  Journalnr. ble forrige journal.                 HFL *
T5.10 * R5.10     030703  Lagt ut refnr og avkryssing på liste            KOB *
T5.10 * R5.20     071103  Fjernet bruk av subrutiner for å hente tekst    KOB *
T5.10 * R5.40     130905  Endret utskrift av sumnivåer                    KOB *
T6.10 * R6.10     220610  Totallinjer må ikke skrives når flere kjørt     NHO *
      *                   samtidig som frembringer brudd                      *
pdf   * R6.10     080413  Innført PDF utskrift. Innebærer primært å finne BSA *
pdf   *                   generert spool, og kall av AP620R med dette som     *
pdf   *                   parametre.                                          *
6.11  *           170713  W_STAR som skal vise at mvakode er overstyrt    NHO
      *                   har ingen verdi.... felt hentes fra arb.fil
      *                   RJOUPF og heter (RJSTAR)
6.30  * R6.30 180816 bsa  Rokkert på rekkefølge av close printerfile
7.01  * R6.30 040117 nho  Hovedboksum ble ikke skrevet dersom brudd
      *                   på member og periode + år var likt med det
      *                   samme som den behandlede posten.
      *
8.01  * R8.xx 220821 bsa  Utvidet felt for mva grunnlag. Ref PRTF
8.02  * R8.xx 241121 bsa  Problemer med at teller sprekker.
      *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frjoupf    ip   e           k disk
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     frhovl1    if   e           k disk    rename(rhovpfr:rhovl1r)
     fraa4l1    if   e           k disk    rename(raa4pfr:raa4l1r)
     fra16l1    if   e           k disk    rename(ra16pfr:ra16l1r)
     frf112p    o    e             printer oflind(*in30)
pdf  f                                     usropn
      *
     d                 ds
     d umtid                         12  0
     d  umtime                        4  0 overlay(umtid)
     d  umdato                        6  0 overlay(umtid:7)
      *
     d                uds
pdf  d l_poff                584    584
pdf  d l_bach                595    635
pdf  d l_skje                637    637  0
     d l_wsid                901    910
     d l_user                911    920
      *
      * Definering av nøkkelfelt
      *
     d rkunl1_kund     s                   like(rkkund)
     d rlevl1_levr     s                   like(rllevr)
     d rhovl1_kont     s                   like(rhkont)
     d rhovl1_spes     s                   like(rhspes)
     d rhovl1_avde     s                   like(rhavde)
     d ra16l1_pkod     s                   like(rapkod)
      *
     d antpos          s              5  0
     d t1diff          s             13  2
     d t1dsum          s             13  2
     d t1ksum          s             13  2
     d t3kund          s             13  2
     d vatxt           s             20
6.NU d*wfbinr          s              7  0
8.02 d wfbinr          s              9  0
      *
6.10 d w_ant           s              1  0
6.10 d w_mem1          s                   like(rjmemb)
     d w_post          s                   like(rjneto)
     d w_firm          s                   like(rjfirm)
     d w_star          s                   like(rjstar)
     d w_bila          s             13  2
      *
     d p_stat          s              1
     d p_pkod          s              3
     d p_pmna          s             20
     d p_text          s             30
     d p_kund          s              6  0
     d p_levr          s              6  0
     d p_navn          s             30
     d p_kont          s              5  0
     d p_firm          s                   like(rjfirm)
     d p_avde          s                   like(rjavde)
     d p_spes          s                   like(rjspes)
pdf  d p_btyp          s            100
pdf   *
pdf  d splfname2       s             10a
pdf  d jobname2        s             10a
pdf  d username2       s             10a
pdf  d jobnbr2         s              6a
pdf  d splfnbr2        s             10i 0
pdf  d splfname3       s             10a
pdf  d jobname3        s             10a
pdf  d username3       s             10a
pdf  d jobnbr3         s              6a
pdf  d splfnbr3        s             10i 0
pdf  d p_tagg0         s             20
pdf  d p_tagg0val      s             50
pdf  d p_tagg1         s             20
pdf  d p_tagg1val      s             50
pdf  d p_tagg2         s             20
pdf  d p_tagg2val      s             50
pdf  d p_tagg3         s             20
pdf  d p_tagg3val      s             50
pdf  d p_tagg4         s             20
pdf  d p_tagg4val      s             50
pdf  d p_tagg5         s             20
pdf  d p_tagg5val      s             50
pdf  d p_tagg6         s             20
pdf  d p_tagg6val      s             50
pdf  d p_tagg7         s             20
pdf  d p_tagg7val      s             50
pdf  d p_tagg8         s             20
pdf  d p_tagg8val      s             50
pdf  d p_tagg9         s             20
pdf  d p_tagg9val      s             50
pdf  d p_refn          s             50
pdf  d p_dspl          s            100
pdf   *
pdf  d qsprilsp        pr                  extpgm('QSPRILSP')
pdf  d rcvvar                     32767a   options(*varsize)
pdf  d rcvvarlen                     10i 0 const
pdf  d format                         8a   const
pdf  d errorcode                  32767a   options(*varsize)
pdf   *
pdf  d sprl0100        ds
pdf  d  bytesrtn                     10i 0
pdf  d  bytesavl                     10i 0
pdf  d  splfname                     10a
pdf  d  jobname                      10a
pdf  d  username                     10a
pdf  d  jobnbr                        6a
pdf  d  splfnbr                      10i 0
pdf  d  systemname                    8a
pdf  d  createdate                    7a
pdf  d                                1a
pdf  d  createtime                    6a
pdf   *
pdf  d errorcode       ds
pdf  d  bytesprov                    10i 0 inz(0)
pdf  d  byteavail                    10i 0 inz(0)
      *
     irjoupfr
      *  Posteringer inn
     i                                          rjfirm        l6
     i                                          rjbdat
     i                                          rjbiln        l2
     i                                          rjmvab
     i                                          rjiavg
     i                                          rjmvak
     i                                          rjtype        l4
     i                                          rjraar        l5
     i                                          rjrper        l5
     i                                          rjbunt        l3
     i                                          rjfdat
     i                                          rjvalk
      *
     c     dummy         tag
6.10  *
6.10 c                   if        rjmemb > w_mem1
6.10 c                   move      *on           *inlr
6.10 c                   move      *on           *in86
6.10 c                   goto      end
6.10 c                   endif
6.10  *
6.10 c                   if        rjmemb <> w_mem1
6.10 c                   move      *on           *in86
6.10 c                   goto      end
6.10 c                   else
6.10 c                   if        w_ant = 0
6.10 c                   add       1             w_ant
6.10 c                   move      *on           *inl6
6.10 c                   move      *on           *inl5
6.10 c                   move      *on           *inl4
6.10 c                   move      *on           *inl3
6.10 c                   move      *on           *inl2
6.10 c                   move      *on           *inl1
6.10 c                   end
6.10 c                   move      *off          *in86
6.10 c                   endif
      *
      * Brudd på firma
      *
     c                   if        *inl6 = *on
      *
     c                   time                    umtid
     c                   move      rjfirm        w_firm                         FIRMA-OPPL.
      *
     c     afirl1_key    chain     afirl1                             61
     c                   eval      a1firm = aaffir
     c                   eval      a1fnav = aafnav
      *
     c                   eval      page = 0
      *
     c                   endif
      *
     c/eject
      *
      * Brudd på Periode/År
      *
     c                   if        *inl5 = *on
      *
     c                   move      rjraar        ra4aar
     c     raa4l1_key    chain     raa4l1                             61
      *
E5.00c                   eval      a1jonr = ra4sjÅ +1
     c                   eval      a1rper = rjrper
     c                   eval      a1raar = rjraar
      *
      * Skriv heading
      *
     c                   eval      a1bunt = rjbunt
     c                   write     a1hdr
      *
     c                   endif
      *
      * Brudd på bilagstype
      *
     c                   if        *inl4 = *on
     c                   eval      a1bunt = rjbunt
     c                   eval      t2dsum = *zeros
     c                   eval      t2ksum = *zeros
     c                   endif
      *
      * Brudd på bunt
      *
     c                   if        *inl3 = *on
     c                   eval      a1bunt = rjbunt
     c                   move      rjbiln        wfbinr
     c                   write     a1bun
     c                   endif
      *
      * Brudd på bilagsnummer
      *
     c                   eval      *in52 = *off
      *
     c                   if        *inl2 = *on and
     c                             rjbiln <> wfbinr
     c                   eval      *in52 = *on
     c                   move      rjbiln        wfbinr
     c                   endif
      *
     c/eject
      *
      * Test posteringstype
      *
     c                   eval      *in81 = *off
     c                   eval      *in82 = *off
     c                   eval      *in83 = *off
      *
     c                   if        rjtype = 1
     c                   eval      *in81 = *on
     c                   endif
      *
     c                   if        rjtype = 2
     c                   eval      *in82 = *on
     c                   endif
      *
     c                   if        rjtype = 3
     c                   eval      *in83 = *on
     c                   endif
      *
      * Det finnes prosjektposter, men de behandles ikke
      *
     c                   if        rjreid = 'P'
     c                   eval      *inu6 = *on
     c                   goto      end
     c                   endif
      *
     c                   eval      d1text = rjtext
6.11 c***                eval      rjstar = w_star
      *
     c                   if        rjbdat = *loval
     c                   eval      d1bdat = 0
     c                   else
     c     *dmy          move      rjbdat        d1bdat
     c                   endif
      *
     c                   eval      d1fdat = 0
     c                   eval      d1navn = *blanks
      *
     c/eject
      *
      * Behandle hovedboksposter
      *
     c                   if        rjreid = 'H'
      *
     c                   move      rjfirm        w_firm
     c                   eval      rhovl1_kont = rjkont
     c                   eval      rhovl1_spes = rjspes
     c                   eval      rhovl1_avde = rjavde
     c     rhovl1_key    chain     rhovl1
      *
     c                   if        not%found
     c                   move      *zeros        rhovl1_avde
     c     rhovl1_key    chain     rhovl1
      *
     c                   if        not%found
     c                   move      *zeros        rhovl1_spes
     c     rhovl1_key    chain     rhovl1
      *
     c                   if        not%found
     c                   eval      rhtext = *blank
     c                   endif
     c                   endif
     c                   endif
      *
     c                   movel     rhtext        d1navn
      *
     c                   if        rjneto > 0
     c                   eval      t3dsum = t3dsum + rjneto
     c                   else
     c                   eval      t3ksum = t3ksum + rjneto
     c                   endif
      *
     c                   endif
      *
     c/space 3
      *
      * Behandle kundeposter
      *
     c                   if        rjreid = 'K'
      *
      * Hent kundenavn
      *
     c                   eval      rkunl1_kund = rjkont
     c     rkunl1_key    chain     rkunl1
      *
     c                   if        %found
     c                   movel     rknavn        d1navn
     c                   endif
      *
     c                   if        rjfdat = *loval
     c                   eval      d1fdat = 0
     c                   else
     c     *dmy          move      rjfdat        d1fdat
     c                   endif
      *
     c                   eval      t3kund = t3kund + rjneto
      *
     c                   endif
      *
     c/space 3
      *
      * Behandle leverandørposter
      *
     c                   if        rjreid = 'L'
      *
      * Hent leverandørnavn
      *
     c                   eval      rlevl1_levr = rjkont
     c     rlevl1_key    chain     rlevl1
      *
     c                   if        %found
     c                   movel     rlnavn        d1navn
     c                   endif
      *
     c                   if        rjfdat = *loval
     c                   eval      d1fdat = 0
     c                   else
     c     *dmy          move      rjfdat        d1fdat
     c                   endif
      *
     c                   eval      t3levr = t3levr + rjneto
      *
     c                   endif
      *
     c/space 3
      *
      * Overflow ?
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   write     a1bun
T5.00c                   move      *off          *in30                                       .
     c                   endif
      *
      * Skriv linje
      *
     c                   if        rjneto > 0
     c                   eval      d1debe = rjneto + rjmvab - rjiavg
     c                   eval      t1dsum = t1dsum + rjneto
     c                   eval      d1kred = 0
     c                   eval      *in31 = *on
     c                   else
     c                   eval      d1kred = rjneto + rjmvab - rjiavg
     c                   eval      t1ksum = t1ksum + rjneto
     c                   eval      d1debe = 0
     c                   eval      *in31 = *off
     c                   endif
      *
     c                   write     d1det
      *
     c                   eval      d1fdat = 0
     c/space 3
      *
      * Henter valutatekst
      *
     c                   if        rjvalk <> *blanks
     c                   eval      ra16l1_pkod = rjvalk
      *
     c     ra16l1_key    chain     ra16l1
      *
     c                   if        %found
     c                   movel     rapmna        vatxt
     c                   endif
      *
      *
     c                   write     d1val
      *
     c                   endif                                                        ........
      *
     c/eject
      *
      * Summer debet/kredit
      *
     c                   eval      antpos = antpos + 1
      *
     c                   eval      w_post = rjneto + rjmvab - rjiavg
     c                   eval      w_bila = w_bila + w_post
      *
     c                   if        w_post > 0
     c                   eval      t2dsum = t2dsum + w_post
     c                   else
     c                   eval      t2ksum = t2ksum + w_post
     c                   endif
      *
     c     end           tag
      *
     c/eject
      *
      * Brudd på bilagnummer
      *
6.10 cl2N86              eval      wfbinr = wfbinr + 1
      *
      * Brudd på bilagstype
      *
6.10 cl4N86              write     t2tot
      *
      * Brudd på bunt
      *
6.10 cl3N86              eval      *in25 = *off
6.10 cl3N86              eval      t3diff = t3dsum + t3ksum
      *
6.10 cl3N86              if        t3diff <> 0
6.10 cl3N86              eval      *in25 = *on
6.10 cl3                 endif
      *
6.10 cl3 81              if        *in86 = *off
     cl3 81              write     t1tot
6.10 cl3                 endif
      *
6.10 cl3N86              eval      t1dsum = 0
6.10 cl3N86              eval      t1ksum = 0
      *
      * Brudd på periode
      *
6.10  * Dersom LR er på og summer er lik 0 er avslutning skrevet
6.10  * tidligere.
6.10 cl5                 if        *inlr = *on and
6.10 c                             t3dsum = *zeros and
6.10 c                             t3ksum = *zeros and
6.10 c                             t3kund = *zeros and
6.10 c                             t3levr = *zeros and
6.10 c                             antpos = *zeros
7.01 cl5                 else
7.01 cl5                 write     t3hdr
7.01 cl5                 eval      t3dsum = 0
7.01 cl5                 eval      t3ksum = 0
7.01 cl5                 eval      t3kund = 0
7.01 cl5                 eval      t3levr = 0
7.01 cl5                 eval      antpos = 0
7.01 cl5                 move      *on           *in86
6.10 cl5                 endif
      *
6.10 cl5N86              write     t3hdr
      *
6.10 cl5N86              eval      t3dsum = 0
6.10 cl5N86              eval      t3ksum = 0
6.10 cl5N86              eval      t3kund = 0
6.10 cl5N86              eval      t3levr = 0
6.10 cl5N86              eval      antpos = 0
      *
pdf   * Generer xml for videre utskrift
pdf  clr                 if        l_poff = '1'
pdf  clr                 exsr      spoolnbr
6.30 clr                 close     rf112p
6.30 clr                 exsr      xml_create
pdf  clr                 exsr      pdf_preview
pdf   * Avslutt jobbiden
pdf  clr                 call      'AB710R'
pdf  c                   parm                    l_bach
pdf  clr                 endif
     c/eject
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Finner spool som er generert av jobben
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     spoolnbr      begsr
pdf   /Free
pdf
pdf            QSPRILSP( SPRL0100
pdf                    : %size(SPRL0100)
pdf                    : 'SPRL0100'
pdf                    : errorcode );
pdf   /End-free
pdf  c*
pdf   *
pdf  c                   endsr
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Kaller eget program for generering av xml fil. Eget pgm         *
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     xml_create    begsr
pdf   *
pdf  c                   eval      splfname2 = *blanks
pdf  c                   eval      splfname3 = *blanks
pdf  c                   eval      jobname2 = *blanks
pdf  c                   eval      jobname3 = *blanks
pdf  c                   eval      username2 = *blanks
pdf  c                   eval      username3 = *blanks
pdf  c                   eval      jobnbr2 = *blanks
pdf  c                   eval      jobnbr3 = *blanks
pdf  c                   eval      splfnbr2 = *zeros
pdf  c                   eval      splfnbr3 = *zeros
pdf  c                   eval      p_btyp = 'KONTROLLISTE'
pdf  c                   eval      p_dspl = 'Kontrolliste - nummer: '+
pdf  c                                      %char(a1jonr)+
pdf  c                                      '   Periode ' +
pdf  c                                      %char(a1rper)+'/'+%char(a1raar)
pdf  c                   eval      p_refn = %char(a1raar) + %char(a1jonr)
pdf  c                   eval      p_tagg0 = 'KONTROLLISTE'
pdf  c                   eval      p_tagg0val = %char(a1jonr)
pdf  c                   eval      p_tagg1 = 'BUNT'
pdf  c                   eval      p_tagg1val = %char(a1bunt)
pdf  c                   eval      p_tagg2 = 'KONTROLLSUM'
pdf  c                   eval      p_tagg2val = %char(t3dsum)
pdf  c                   eval      p_tagg3 = *blanks
pdf  c                   eval      p_tagg3val = *blanks
pdf  c                   eval      p_tagg4 = *blanks
pdf  c                   eval      p_tagg4val = *blanks
pdf  c                   eval      p_tagg5 = *blanks
pdf  c                   eval      p_tagg5val = *blanks
pdf  c                   eval      p_tagg6 = *blanks
pdf  c                   eval      p_tagg6val = *blanks
pdf  c                   eval      p_tagg7 = *blanks
pdf  c                   eval      p_tagg7val = *blanks
pdf  c                   eval      p_tagg8 = *blanks
pdf  c                   eval      p_tagg8val = *blanks
pdf  c                   eval      p_tagg9 = *blanks
pdf  c                   eval      p_tagg9val = *blanks
pdf   *
pdf  c                   call      'AP627R'
pdf  c                   parm                    splfname
pdf  c                   parm                    jobname
pdf  c                   parm                    username
pdf  c                   parm                    jobnbr
pdf  c                   parm                    splfnbr
pdf  c                   parm                    splfname2
pdf  c                   parm                    jobname2
pdf  c                   parm                    username2
pdf  c                   parm                    jobnbr2
pdf  c                   parm                    splfnbr2
pdf  c                   parm                    splfname3
pdf  c                   parm                    jobname3
pdf  c                   parm                    username3
pdf  c                   parm                    jobnbr3
pdf  c                   parm                    splfnbr3
pdf  c                   parm                    p_tagg0
pdf  c                   parm                    p_tagg0val
pdf  c                   parm                    p_tagg1
pdf  c                   parm                    p_tagg1val
pdf  c                   parm                    p_tagg2
pdf  c                   parm                    p_tagg2val
pdf  c                   parm                    p_tagg3
pdf  c                   parm                    p_tagg3val
pdf  c                   parm                    p_tagg4
pdf  c                   parm                    p_tagg4val
pdf  c                   parm                    p_tagg5
pdf  c                   parm                    p_tagg5val
pdf  c                   parm                    p_tagg6
pdf  c                   parm                    p_tagg6val
pdf  c                   parm                    p_tagg7
pdf  c                   parm                    p_tagg7val
pdf  c                   parm                    p_tagg8
pdf  c                   parm                    p_tagg8val
pdf  c                   parm                    p_tagg9
pdf  c                   parm                    p_tagg9val
pdf  c                   parm                    l_bach
pdf  c                   PARM                    p_btyp
pdf  c                   PARM                    p_refn
pdf  c                   PARM                    p_dspl
pdf   *
pdf  c                   endsr
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Subrutiner for XML/jasper integrasjon - Skjermvisning           *
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     pdf_preview   begsr
pdf  c                   if        l_skje = 1
pdf   * Vi kaller program for launch av PDF i browser
pdf  c                   call      'AP621R'
pdf  c                   parm                    l_bach
pdf   *
pdf  c                   endif
pdf   *
pdf  c                   endsr
pdf   *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
6.10  *
6.10 c     *entry        plist
6.10 c                   parm                    w_mem1                         user
      *
      * Key for firma-opplysninger - firmaregister
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for statusopplysninger - koderegister
      *
     c     raa4l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra4aar
      *
      * Key for kontoplan
      *
     c     rhovl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhovl1_kont
     c                   kfld                    rhovl1_spes
     c                   kfld                    rhovl1_avde
      *
      * Key for kunder
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for leverandører
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for valutakoder
      *
     c     ra16l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra16l1_pkod
      *
pdf  c                   open      rf112p
      *
     c                   endsr
      *
