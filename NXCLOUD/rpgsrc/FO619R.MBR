     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO619R
      * Beskrivelse . . : Behandling av ordre, resting av plukk fra
      *                                        radioterminal
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       220806 bof  Nytt program - tatt utg.pkt i  FO614R
5.51  *       151107 bof  Hele plukket slettes fra RT-plukk-register
5.60  * R5.60 091107 AMD  Lagt inn løsning for kontroll av grense for
      *                   almenningsrabatter
5.60  *       080108 bof  Utvidet logg-fil med år
5.63  * R5.63 150208 AMD  Lagt inn logging av slettede ordrelinjer
5.64  * R5.64 220508 bof  utvidet i param. til av FA922R
      * 6.10  240609 BSA  Oppdatert med sporingsinformasjon
      * 6.10  220909 ASK  Nye parametre til VL001R
6.11  * 6.10  110610 bof  Justert i utvidelse av parm VL001R
6.12  * 6.10  050612 bof  Feilretting lageroppdat. ved resting
6.20  * 6.20  260511 bof  Oppdater pakks.dato i stedet for å endre lev.dato
6.22  * 6.20  190913 bof  Feilretting vedr. resting og oppdatering av lager
6.23  * R6.20 270415 bof  oprettet dato skal ikke oppdateres
6.nu  * R6.30 030614 bsa  Programmet gjennomgått mtp nummerutvidelser.
6.30  * 6.30  040615 bkv  Ordre økt fra 6 til 8
6.31  * 6.30  200116 bsa  Utsetter sletteing av transaksjon fra LWPL.
6.31  *                   Avventer til alle filer er oppdatert.
6.32  * 6.30  101116 bof  hvis LWPLPF er tom på valgt numm/suff,så avsl
6.33  * 6.30  300516 bsa  Legger ut suffix i LDA, brukes ved utskrift av
6.33  *                   kollietiketter.
6.34  * 6.30  060618 ask  Sjekker "Neste ordretype" i steden for å bruke
      *                   ordretype fra plukkfil. Dette for å håndtere
      *                   internordre plukking.
6.35  * 6.30  061219 bkv  Kommentert ut 6.34
7.01  *K000   061020 ask  Rekalkulering av rabatt ved overplukk på ordre fra B2C
7.02  *K000   030216 bof  Hvis PN legg *H i info-type
7.03  * 6.30  040119 sst  Hente ordretype vedr prosjektregnskap
7.04  * 6.30  300120 ask  Fjenet tilpasning fra 7.01
      *
8.01  *K000   230321 bsa  Sjekk om det er en transportforsendelse som ligger mot
8.01  *K000               gammelt suffiks. Hvis ja, så opdpater transportinfo.
8.02  *K000   250521 bsa  Sender over informasjon hvis statuskode settes til
8.02  *K000               ferdig plukket.
8.03  * K000  080621 bsa  Innfører flåtestyring på lagernivå.
8.04  * K000  141122 bsa  Endrer program som håndterer selve transport
8.04  * K000              grensesnittet.
8.05  *K000   180123 bsa  Flagg om skaffetekst i transportlogg.
8.06  *K000   060223 bsa  Flagg om sjekk på loggkode i transportlogging
8.07  *K000   170223 bsa  Blanker FOFSYS og FONREF siden det er nytt suffiks.
8.08  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
8.xx  *K000   120523 bsa  Implementerer logging.
8.08x *K000   150323 sst  Beregner/oppdaterer memosaldo med FO763R
8.09  *K000   090424 bsa  Kaller program for å hente høyeste suffiks på ordrenummer
8.10  *K0000  300724 bsa  Setter gammelt suffiks til 0, slik at det
8.10  *K0000              ikke feiler om det er bedt om kollietikett
8.10  *K0000              og det er feil i data.
8.12  *K000   031024 bsa  Skal ikke oppdatere lager hvis ikke ordretype har
8.12  *K000               akkumulatorkode > 1
8.13  *K000   290824 bsa  Hvis nytt sufiks, oppdater tilhørende bestillingslinjer
8.14  *K000   091224 bsa  Hvis vi leser forbi til neste ordre, kan vi ikke bruke
8.14  *K000               MKOTYP fra neste post som ikke tilhører ordren.
8.15  *K000   280125 bsa  Feilmerking ved sletting.
8.17  *K000   280125 bsa  Hvis vare ikke ligger på utplukk, skal ikke
8.17  *K000               etterfølgende tekstlinjer være med ut.
8.18  *K000   280425 bsa  Utvidet med å kunne legge til linjer som ikke finnes
8.18  *K000               på ordren.
8.19  *K000   140425 bsa  Java klarer ikke legge inn flere linjer med linjenummer
8.19  *K000               0 ifbm serviceartikler, så de opprettes med negative
8.19  *K000               linjenummer. Må ta høyde for dette når vi leser denne
8.19  *K000               typen linjer.
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     ffohelu    uf a e           k disk    rename(fohepfr:fohelur)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffodtlu    uf a e           k disk    rename(fodtpfr:fodtlur)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
5.42 ffodtlr    if   e           k disk    rename(fodtpfr:fodtlrr)
     ffotxl1    if   e           k disk    rename(fotxpfr:fotxl1r)
     flwpllu    uf   e           k disk    rename(lwplpfr:lwpllur)
6.32 flwpll1    if   e           k disk    rename(lwplpfr:lwpll1r)
3.33 fvinfl1    if   e           k disk    rename(vinfpfr:vinfl1r)
3.34 ffusrl1    if   e           k disk    rename(fusrpfr:fusrl1r)
     ffpsolu    uf a e           k disk    rename(fpsopfr:fpsolur)
7.02 fafpslr    if   e           k disk    rename(afpspfr:afpslrr)
7.03 frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
8.01 fftrpi2    if   e           k disk    rename(ftrpst:ftrpi2r)
8.01 fftrpiu    uf   e           k disk    rename(ftrpst:ftrpiur)
8.01 fraf2l1    if   e           k disk    rename(raf2pfr:raf2l1r)
8.03 fftrli1    if   e           k disk    rename(ftrlst:ftrli1r)
8.13 flodtlu    uf   e           k disk    rename(lodtpfr:lodtlur)
8.18 fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
8.18 fsdepl1    if   e           k disk    rename(sdeppfr:sdepl1r)
3.11  *
      * Definering av data-struktur
      * for lageroppdatering
      *
     d                 ds
6.nu d hlrec                        128
     d  hlvare                       15    overlay(hlrec)
     d  hllage                        2  0 overlay(hlrec:16)
     d  hldato                         d   overlay(hlrec:18) datfmt(*iso)
     d  hltime                         t   overlay(hlrec:28) timfmt(*iso)
     d  hlotyp                        2    overlay(hlrec:36)
     d  hlltyp                        2    overlay(hlrec:38)
     d  hlanta                       11  3 overlay(hlrec:40)
     d  hlkopr                        9  2 overlay(hlrec:51)
     d  hlrefr                       20    overlay(hlrec:60)
     d  hlsyst                        1    overlay(hlrec:80)
     d  hlaltk                        2    overlay(hlrec:81)
     d  hlrest                        1    overlay(hlrec:83)
     d  hltype                        1    overlay(hlrec:84)
     d  hlenhe                        3    overlay(hlrec:85)
     d  hlinlr                        1    overlay(hlrec:88)
     d  hllety                        1    overlay(hlrec:89)
6.nu d  hlonum                        8  0 overlay(hlrec:90)
6.nu d  hlolin                        4  0 overlay(hlrec:98)
      *
      * Definering av data-struktur
      * for sorteringsbegrep
      *
     d                 ds
     d sorter                        84
     d  sor001                        8  0 overlay(sorter)
     d  sor002                       30    overlay(sorter:9)
5.33 d  sor003                       16    overlay(sorter:39)
5.33 d  sor004                        3    overlay(sorter:55)
5.33 d  sor005                       17    overlay(sorter:58)
6.nu d  sor006                        8  0 overlay(sorter:75)
6.nu d  sor007                        2  0 overlay(sorter:83)
      *
      * Datastruktur for navn på member
      *
     d                 ds
     d d_memb                        10
     d  d_alf1                        1    overlay(d_memb)
     d  d_firm                        3  0 overlay(d_memb:2)
     d  d_numm                        6  0 overlay(d_memb:5)
      *
      * Definering av local data area
      *
     d lda            uds
6.32 d l_gsuf                538    539  0
     d l_alt1                540    540  0
     d l_alt2                541    541  0
     d l_peri                561    566  0
     d l_dato                567    576d   datfmt(*iso)
      *
     d l_ruti                800    809
     d l_outq                810    819
     d l_acpi                830    833  0
     d l_chgv                844    844
     d l_prog                871    880
5.22 d l_outm                899    899
     d l_wsid                901    910
3.34 d l_user                911    920
7.01 d l_filg                931    933
3.36 d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d w_firm          s                   like(fofirm)
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d fodtl1_numm     s                   like(fdnumm)
     d fodtl1_suff     s                   like(fdsuff)
     d fodtl1_line     s                   like(fdline)
5.42 d fodtlr_numm     s                   like(fdnumm)
5.42 d fodtlr_suff     s                   like(fdsuff)
5.42 d fodtlr_line     s                   like(fdline)
     d votyl1_otyp     s                   like(vaotyp)
3.33 d vinfl1_ityp     s                   like(vaityp)
3.34 d fusrl1_user     s                   like(fbuser)
     d rohel1_numm     s                   like(fonumm)
     d rohel1_suff     s                   like(fosuff)
     d fotxl1_numm     s                   like(ftnumm)
     d fotxl1_suff     s                   like(ftsuff)
     d fotxl1_line     s                   like(ftline)
     d lwpllu_numm     s                   like(mknumm)
     d lwpllu_suff     s                   like(mksuff)
     d lwpllu_line     s                   like(mkline)
5.51 d lwpllx_numm     s                   like(mknumm)
5.51 d lwpllx_suff     s                   like(mksuff)
     d fpsolu_slin     s                   like(fkslin)
     d fpsolu_sonr     s                   like(fksonr)
     d fpsolu_ssuf     s                   like(fkssuf)
     d fpsolu_stll     s                   like(fkstll)
7.02 d afpslr_ufil     s                   like(l_filg)
7.02 d afpslr_uide     s                   like(afuide)
7.03 d rkunl1_kund     s                   like(rkkund)
8.01 d ftrpi2_rnum     s                   like(ftrnum)
8.01 d ftrpi2_rsuf     s                   like(ftrsuf)
8.01 d ftrpiu_roid     s                   like(ftroid)
8.01 d raf2l1_2kod     s                   like(ra2kod)
8.03 d ftrli1_llag     s                   like(ftllag)
8.13 d lodtlu_numm     s                   like(ldnumm)
8.13 d lodtlu_suff     s                   like(ldsuff)
8.13 d lodtlu_line     s                   like(ldline)
8.18 d sdepl1_numm     s                   like(sknumm)
8.18 d sdepl1_kund     s                   like(skkund)
8.18 d sdepl1_tell     s                   like(sktell)
8.18 d vvarl1_vare     s                   like(vvvare)
      *
     d p_firm          s                   like(mkfirm)
     d p_numm          s                   like(mknumm)
     d p_suff          s                   like(mksuff)
     d p_urec          s             80
     d p_stat          s              1
8.08xd p_firx          s              3
8.08xd p_kunx          s              6
      *
3.11  * Definering av variable
3.11  *
3.11 d w_kode          s              1
3.11 d w_fell          s              6
3.11 d w_type          s              2
3.11 d w_fast          s             10
3.11 d w_numm          s                   like(fonumm)
3.11  *
6.nu d w_lrec          s            128
     d w_otyp          s                   like(footyp)
     d w_otyp_sav      s                   like(footyp)
     d w_lage          s                   like(folage)
     d w_lage_sav      s                   like(folage)
     d w_date          s                   like(foldat)
     d w_ruti          s                   like(vaorut)
5.21 d w_tvbt          s              1
5.21 d w_btch          s              1
     d w_oakk          s                   like(vaoakk)
     d w_rest          s                   like(fdanta)
     d w_test          s              1  0
     d w_enor          s              1
5.21 d w_in03          s              1
     d w_sald          s             11  2
5.60 d w_sal2          s             11  2
     d w_stat          s              1
5.02 d w_olag          s                   like(foolag)
5.32 d w_onum          s                   like(vaonum)
5.63 d w_spgm          s             10
6.30 d w_numa          s              8
5.02 d w_lina          s              4
7.02 d w_hkod          s              2
8.02 d w_roid          s             35
8.02 d w_reqs          s              5
8.05 d w_skaf          s              1  0
8.06 d w_logg          s              1  0
8.09 d w_suff          s                   like(fosuff)
8.14 d w_ktyp          s                   like(footyp)
8.18 d w_line          s              4  0
8.18 d w_rab1          s              5  2
      *
8.xx d w_jnav          s             15
8.xx d w_jkey          s             41
8.xx d w_jtxt          s             35
8.xx d w_jtext         s            200
8.xx d w_jstat         s              2  0
      *
8.18 d s_numm          s                   like(fonumm)
8.18 d s_suff          s                   like(fosuff)
      *
      * Datastruktur
      *
     d                 ds
     d d_urec                        80
     d  d_ufir                        3  0 overlay(d_urec)
5.60 d  d_aarr                        4  0 overlay(d_urec:4)
6.nu d  d_unum                        8  0 overlay(d_urec:8)
6.nu d  d_usuf                        2  0 overlay(d_urec:16)
6.nu d  d_ukod                        1    overlay(d_urec:18)
6.nu d  d_date                         d   overlay(d_urec:19) datfmt(*iso)
6.nu d  d_time                         t   overlay(d_urec:29) timfmt(*iso)
6.nu d  d_user                       10    overlay(d_urec:37)
6.nu d  d_wsid                       10    overlay(d_urec:47)
7.01  *
7.01 d w_linnet        s                   like(fdsums)
7.01 d w_nynet         s                   like(fdsums)
7.01 d w_linbrt        s                   like(fdsums)
7.01 d w_lindif        s                   like(fdsums)
7.01 d u_701           s              1    inz(*off)
8.01 d u_801           s              1    inz(*off)
8.xx d b_logg          s              1    inz(*on)
8.17 d b_notxt         s              1    inz(*off)
7.01  *
7.0l C/Exec SQL
7.0l C+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR,
7.0I C+             commit=*none
7.0l C/End-Exec
7.01  *
7.01  * Definering av eksterne prg
7.01  * BKV
7.01   dcl-s co402_verdi1  char(1);
7.01   dcl-s co402_verdi2  char(2048);
7.01   DCL-PR CO402R EXTPGM('CO402R');
7.01     p_filgrp            char(3)     const;
7.01     p_firm              packed(3:0) const;
7.01     p_lager             packed(2:0) const;
7.01     p_nokkel            char(50)    const;
7.01     p_verdi1            char(1);
7.01     p_verdi2            char(2048);
7.01   END-PR;
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Beskrivelse av spesielle felter som er benyttet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  w_test  Benyttes til og sjekkke om det er linjer igjen
      *  (0,1)   på ordren etter at den er restet. Hvis det ikke
      *          er varelinjer igjen på ordren slettes heading-record
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Utplukk av ordre til utskrift og oppdatering
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  *
6.32  * hvis ordre/suff ikke finnes i lwplpf, så avslutt
6.32  *
6.32 c     lwpllx_key    chain     lwpll1
5.32 c                   if        not %found(lwpll1)
6.32 c                   goto      avslutt
6.32 c                   endif
      *
      * Leser ordrehode
      *
     c     fohel1_key    chain     fohel1
6.12 c                   eval      w_otyp_sav = footyp
      *
      *  Hent opplysninger fra OFAK-KODE-REGISTER
      *
     c                   eval      w_firm = fofirm
     c     fstsl1_key    chain     fstsl1r                            90
      *
     c                   eval      w_firm      = fofirm
      *
      * behandler resting / oppdatering
      *
     c                   exsr      resting
      *
      * bygger std.verdier for denne utskriften
      *
     c                   exsr      std_verd
5.51 c                   exsr      slett_plukk
      *
     c     avslutt       tag
8.xx  *
8.xx  * Logg
8.xx  *
8.xx c                   if        b_logg = *on
8.xx c                   eval      w_jtext = 'Avslutter FO619, håper vi-
8.xx c                              er nærmere løsning'
8.xx c                   exsr      logg_debug
8.xx c                   endif
8.xx  *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av resting    Danner nye linjer og heading
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     resting       begsr
      *
      * Kaller opp subrutine for henting av neste suffix
      *
     c                   exsr      hntsuf
      *
      * Kaller opp subrutiner for behandling av resting
      *
     c                   exsr      olrest
      *
      * Danner ny ordreheading/sletter eventuellt gammel
      *
     c     fohel1_key    chain     fohel1r                            90
     c                   eval      w_otyp_sav = footyp
     c                   eval      w_lage_sav = folage
5.02 c                   eval      w_olag     = foolag
      *
      * Det finnes ikke linjer som er restet igjen så slett ordre
      *
     c                   if        w_test = *zero
     c     fohel1_key    chain     fohelur                            90
8.xx  *
8.xx  * Logg
8.xx  *
8.xx c                   if        b_logg = *on
8.xx c                   eval      w_jtext = 'Sletter ordrehode ' +
8.xx c                             %char(fohel1_numm) + '/' +
8.xx c                             %char(fohel1_suff) + '/' +
8.xx c                             ' ordretype ' + footyp + '/' + mkotyp
8.xx c                   exsr      logg_debug
8.xx c                   endif
8.xx  *
     c                   delete    fohelur
     c                   else
      *
      * Ellers oppdat. status på rest-ordre
      *
     c     fohel1_key    chain     fohelur                            90
8.07 c                   if        fofsys = 'AOR'
8.07 c                   eval      fofsys = *blanks
8.07 c                   eval      fonref = *blanks
8.07 c                   endif
     c                   eval      forest = 1
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = l_user
     c                   update    fohelur
8.xx  *
8.xx  * Logg
8.xx  *
8.xx c                   if        b_logg = *on
8.xx c                   eval      w_jtext = 'Oppdaterer rest-hode ' +
8.xx c                             %char(fohel1_numm) + '/' +
8.xx c                             %char(fohel1_suff) + '/' +
8.xx c                             ' ordretype ' + footyp + '/' + mkotyp
8.xx c                   exsr      logg_debug
8.xx c                   endif
8.xx  *
     c                   eval      d_ufir = w_firm
     c                   eval      d_unum = fohel1_numm
     c                   eval      d_usuf = fohel1_suff
     c                   eval      d_ukod = '8'
     c                   time                    d_date
     c                   time                    d_time
     c                   eval      d_user = l_user
     c                   eval      d_wsid = l_wsid
5.60 c                   eval      d_aarr = %subdt(fobdat:*years)
     c                   eval      p_urec = d_urec
     c                   call      'FU900R  '
     c                   parm                    p_stat
     c                   parm                    p_urec
      *
     c                   endif
8.01  *
8.01  * Hvis vi kjører med integrasjon til transportstyrer, må vi sende request.
8.01  * - sjekk at forsendelsesmåten har flåtestyring.
8.01  *
8.01 c                   if        u_801 = *on
8.01 c                   eval      raf2l1_2kod = folmat
8.01 c     raf2l1_key    chain     raf2l1r
8.01 c                   if        %found and
8.01 c                             ra2flt = 1
8.03  * Styres pr lager
8.03 c                   eval      ftrli1_llag = folage
8.03 c     ftrli1_key    chain     ftrli1r
8.03 c                   if        %found and
8.03 c                             ftlflt = 1
8.01  *
8.01  * Hent info/id om ordren, hvis den allerde er sendt
8.01  *
8.01 c                   eval      ftrpi2_rnum = fohel1_numm
8.01 c                   eval      ftrpi2_rsuf = fohel1_suff
8.01 c     ftrpi2_key    chain     ftrpi2r
8.01 c                   if        %found
8.01 c                   eval      ftrpiu_roid = ftroid
8.01 c     ftrpiu_key    chain     ftrpiur
8.01 c                   if        %found
8.01 c                   eval      ftrnum = rohel1_numm
8.01 c                   eval      ftrsuf = rohel1_suff
8.01 c                   time                    ftreda
8.01 c                   time                    ftreti
8.01 c                   eval      ftreus = '2DP'
8.01 c                   update    ftrpiur
8.01 c                   endif
8.01 c                   endif
8.01  *
8.03 c                   endif
8.01 c                   endif
8.01 c                   endif
      *
      *
      * Danner ny ordre med ny suffix
      *
     c                   call      'FO709R'
3.11 c                   parm                    rohel1_numm
     c                   parm                    rohel1_suff
      *
     c     rohel1_key    chain     fohelur                            90
     c                   if        *in90 = *on
3.11 c                   eval      fonumm = rohel1_numm
     c                   eval      fosuff = rohel1_suff
6.32 c                   eval      l_gsuf = rohel1_suff
     c                   eval      fokode = 3
7.02  *
7.02  * Henter inn opplysninger om ordretypen. Hvis pakkseddel negativ, settes
7.02  * evnt holdekode.
7.02  *
7.02 c                   if        vaoakk = 2   and
7.02 c                             vaokre = '-'
7.02 c                   eval      foityp = w_hkod
7.02 c                   endif
      *
6.23 c*                  time                    fodate
6.23 c*                  time                    fotime
6.10 c                   eval      fooous = l_user
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = l_user
     c                   write     fohelur
8.xx  *
8.xx  * Logg
8.xx  *
8.xx c                   if        b_logg = *on
8.xx c                   eval      w_jtext = 'Skrevet nytt ordrehode ' +
8.xx c                             %char(rohel1_numm) + '/' +
8.xx c                             %char(rohel1_suff) + '/' +
8.xx c                             ' ordretype ' + footyp + '/' + mkotyp
8.xx c                   exsr      logg_debug
8.xx c                   endif
8.xx  *
     c                   endif
      * oppdat. status
     c                   eval      d_ufir = fofirm
     c                   eval      d_unum = fonumm
     c                   eval      d_usuf = fosuff
     c                   eval      d_ukod = '6'
     c                   time                    d_date
     c                   time                    d_time
     c                   eval      d_user = l_user
     c                   eval      d_wsid = l_wsid
5.60 c                   eval      d_aarr = %subdt(fobdat:*years)
     c                   eval      p_urec = d_urec
     c                   call      'FU900R  '
     c                   parm                    p_stat
     c                   parm                    p_urec
8.xx  *
8.xx  * Logg
8.xx  *
8.xx c                   if        b_logg = *on
8.xx c                   eval      w_jtext = 'Lagt ut loggkode for ' +
8.xx c                             %char(fonumm) + '/' +
8.xx c                             %char(fosuff) + '/' +
8.xx c                             ' ordretype ' + footyp
8.xx c                   exsr      logg_debug
8.xx c                   endif
8.xx  *
8.02  *
8.02  * Hvis vi kjører med integrasjon til transportstyrer, må vi oppdatere
8.02  * Lognet
8.02  *
8.02 c                   if        u_801 = *on
8.02  *
8.04 c*                  eval      raf2l1_2kod = folmat
8.04 c*    raf2l1_key    chain     raf2l1r
8.04 c*                  if        %found and
8.04 c*                            ra2flt = 1
8.02  *
8.02  * Hent info/id om ordren, hvis den allerde er sendt
8.02  * Vi forutsetter at den finnes i forrige oppslag (ref 8.01)
8.02  *
8.04 c*                  eval      w_roid = *blanks
8.02  *
8.02 c     ftrpiu_key    chain     ftrpiur
8.02 c                   if        %found
8.04 c*                  eval      w_roid = ftroid
8.02 c                   eval      ftreus = '2DP'
8.02 c                   update    ftrpiur
8.02 c                   endif
8.02  *
8.04 c*                  call      'AP055R '
8.04 c*                  parm                    fofirm
8.04 c*                  parm                    fonumm
8.04 c*                  parm                    fosuff
8.04 c*                  parm                    w_roid
8.04 c*                  parm                    ra2fid
8.04 c*                  parm                    w_reqs
8.02  *
8.05 c                   eval      w_skaf = 0
8.06 c                   eval      w_logg = 0
8.04 c                   call      'AP058R '
8.04 c                   parm                    fofirm
8.04 c                   parm                    fonumm
8.04 c                   parm                    fosuff
8.05 c                   parm                    w_skaf
8.06 c                   parm                    w_logg
8.04 c                   parm                    w_reqs
8.04 c*                  endif
      *
8.02 c                   endif
      *
     c                   call      'FO190R'
     c                   parm                    w_stat
     c                   parm                    w_firm
     c                   parm                    rohel1_numm
     c                   parm                    rohel1_suff
     c                   parm                    fokund
     c                   parm                    fokpro
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * SPESIAL FOR LAGER :  behandling av ordre som ikke restes
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * * fjernet fordi vi ikke har noe parameter som evt. overstyrer resting
      *
     c*                  if        faalag <> *zero
5.02 c*                  if        w_olag = *zero
     c*                  exsr      laglag
     c*                  endif
5.02 c*                  endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Etterbehandling av "gammel" ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Hopper til etterbehandling ny ordre dersom ikke
      * Heading finnes på forrige /fra) ordre
      *
      * Trekker ut hele beløpet fra fra ordre derosm ikke restlinjer
      *
     c                   if        w_test = *zero
      *
      * Beregner justeringsbeløp
      *
     c                   eval      w_sald = fototr - fotots
5.60 c                   eval      w_sal2 = fobrra * -1
      *
      * Kaller opp subrutine for oppdatering av memosaldo
      *
8.08xc*                  call      'FA922R'
8.08xc*                  parm                    w_firm
8.08xc*                  parm                    footyp
8.08xc*                  parm                    fokund
8.08xc*                  parm                    w_sald
8.08xc*                  parm                    w_sal2
      *
8.08xc                   move      w_firm        p_firx
8.08xc                   move      fokund        p_kunx
8.08xc                   call      'FO763R'
8.08xc                   parm                    p_firx
8.08xc                   parm                    p_kunx
      *
     c                   goto      tag100
     c                   endif
      *
      * Beregner nye summer for "GAMMEL" ordre
      *
     c                   eval      w_enor = *blank
     c                   call      'FO730R'
     c                   parm                    w_enor
     c                   parm                    w_firm
     c                   parm                    fohel1_numm
     c                   parm                    fohel1_suff
      *
      * Oppdaterer ordren slik at den kan endres etc.etc.
      *
     c     fohel1_key    chain     fohelur                            90
5.02  *
5.02  * Legger inn kode for at beholdning er oppdatert
5.02  *
5.02 c                   if        w_oakk = 2 or
5.02 c                             w_oakk = 3
5.02 c                   if        w_test = 0
5.02 c                   eval      foolag = 1
5.02 c                   endif
5.02 c                   endif
5.02  *
     c                   if        *in90 = *off
     c                   eval      fokode = *zero
     c                   move      *loval        fokoda
     c                   move      *loval        fokoti
     c                   eval      fokous = *blank
     c                   eval      fokows = *blank
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = l_user
     c                   update    fohelur
8.xx  *
8.xx  * Logg
8.xx  *
8.xx c                   if        b_logg = *on
8.xx c                   eval      w_jtext = 'Oppdaterer lagerkode ' +
8.xx c                             %char(fonumm) + '/' +
8.xx c                             %char(fosuff) + '/' +
8.xx c                             ' ordretype ' + footyp + '/' + mkotyp
8.xx c                   exsr      logg_debug
8.xx c                   endif
8.xx  *
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Etterbehandling av "ny" ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     tag100        tag
3.11  *
3.11  * Legger inn rett ordrenummer etter plukking
3.11  *
3.11 c                   eval      fohel1_numm = rohel1_numm
3.11 c                   eval      fodtl1_numm = rohel1_numm
3.11 c                   eval      fotxl1_numm = rohel1_numm
3.11 c                   eval      fpsolu_sonr = rohel1_numm
      *
      * Legger inn rett suffix etter plukking
      *
     c                   eval      fohel1_suff = rohel1_suff
     c                   eval      fodtl1_suff = rohel1_suff
     c                   eval      fotxl1_suff = rohel1_suff
     c                   eval      fpsolu_ssuf = rohel1_suff
      *
      * Oppdaterer ordren med nye verdier fra utplukk
      *
     c     fohel1_key    chain     fohelur                            90
     c                   if        *in90 = *off
8.14 c*                  eval      footyp = mkotyp
8.14 c                   eval      footyp = w_ktyp
     c*                  eval      folage = fkplag
     c                   eval      fotots = *zero
     c                   eval      fototr = *zero
5.02 c                   if        w_oakk = 2 or
5.02 c                             w_oakk = 3
6.22 c*                  if        w_test = 0
5.02 c                   eval      foolag = 1
6.22 c*                  endif
5.02 c                   endif
      *
      * Dersom leveringsdato ikke er registert og dette er en
      * pakkseddel, legges dagens dato ut i leveringsdato.
      *
3.14  *    w_oakk        ifeq      2
3.14  *    foldat        ifeq      *loval
3.14  *                  move      w_date        foldat
3.14  *                  end
3.14  *                  end
3.14  *
6.20  * Når en ordre plukkes til pakkseddel, oppdateres pakks.dato
6.20  * med dag.dato. Leveringsdato blir liggende som før.
6.20  * Den dato som lå i feltet fra før,
3.14  * tas vare på i planlagt levering, slik at man i ettertid
3.14  * kan se planlagt levering opp mot virkelig levering.
3.14  *
3.14 c                   if        w_oakk = 2
3.14 c                   if        fopdat = *loval
3.14 c                   eval      fopdat = foldat
3.14 c                   endif
6.20 c                   eval      fopada = w_date
3.14 c                   endif
      *
5.23  * Setter inn til plukking dato
5.23  *
      *
5.23  * Oppdatering av bruker/skjerm/dato/klokkeslett
5.23  *
6.23 c*                  time                    fodate
6.23 c*                  time                    fotime
5.23 c                   eval      fowsid = l_wsid
5.43 c***                eval      fouser = l_user
5.23  *
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = l_user
     c                   update    fohelur
8.xx  *
8.xx  * Logg
8.xx  *
8.xx c                   if        b_logg = *on
8.xx c                   eval      w_jtext = 'Da har vi oppd. med pakks.info ' +
8.xx c                             %char(fonumm) + '/' +
8.xx c                             %char(fosuff) + '/' +
8.xx c                             ' ordretype ' + footyp + '/' + w_ktyp
8.xx c                   exsr      logg_debug
8.xx c                   endif
8.xx  *
     c                   endif
      *
8.18 c                   exsr      sjekk_newline
      *
      * Beregner nye summer for "NY" ordre
      *
     c                   eval      w_enor = *blank
     c                   call      'FO730R'
     c                   parm                    w_enor
     c                   parm                    w_firm
3.11 c                   parm                    rohel1_numm
     c                   parm                    rohel1_suff
      *
     c                   exsr      dann_pakks
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Etterbehandling - danne fil for utskrift pakkseddel
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dann_pakks    begsr
5.01  *
5.01  * Oppdater kode som sier at pakkseddel er skrevet,
5.01  * dersom dette er en pakkseddel.
5.01  *
5.01 c     fohel1_key    chain     fohelur                            90
5.01 c                   if        *in90 = *off
5.01 c                   if        w_oakk = 2
5.01 c                   eval      fopaks = 1
5.01 c                   endif
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = l_user
5.01 c                   update    fohelur
8.xx  *
8.xx  * Logg
8.xx  *
8.xx c                   if        b_logg = *on
8.xx c                   eval      w_jtext = 'Oppdaterer pakks.skrevet ' +
8.xx c                             %char(fohel1_numm) + '/' +
8.xx c                             %char(fohel1_suff) + '/' +
8.xx c                             ' ordretype ' + footyp + '/' + w_ktyp
8.xx c                   exsr      logg_debug
8.xx c                   endif
8.xx  *
5.01 c                   endif
      *
     c                   exsr      ohead
     c*                  if        fkptxt = *zero
     c*                  exsr      foran
     c*                  endif
     c                   exsr      linje
     c*                  if        fkptxt = *zero
     c*                  exsr      etter
     c*                  endif
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer PLUKK-S-LINJE-REGISTER med sorterings-felter m.m.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ohead         begsr
      *
     c                   eval      fpsolu_stll = *zero
     c                   eval      fpsolu_slin = *zero
     c     fpsolu_key    chain     fpsolur                            92
      *
     c                   if        *in92 = *off
     c                   update    fpsolur
     c                   else
     c                   eval      fksfir = p_firm
     c                   eval      fksonr = fpsolu_sonr
     c                   eval      fkssuf = fpsolu_ssuf
     c                   eval      fkslin = *zero
     c                   eval      fkstll = *zero
     c                   eval      fkskod = 'H'
     c                   write     fpsolur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter ordre-linjer og legger ut til PLUKK-S-LINJE-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     linje         begsr
      *
     c                   eval      fodtl1_line = *zero
      *
     c     fodtl1_key    setll     fodtl1r
      *
     c     linje1        tag
     c                   eval      *in90 = *off
     c                   read      fodtl1r                                90
     c                   if        *in90 = *off
     c                   if        fdfirm = w_firm
     c                   if        fdnumm = fodtl1_numm
     c                   if        fdsuff = fodtl1_suff
      *
      * Kaller opp subrutine for oppdatering av lager
      *
     c                   if        faalag <> *zero
5.02 c                   if        w_olag =  *zero
     c                   if        vaoakk = 3
     c                   if        footyp = mkotyp
     c                   eval      w_lage = w_lage_sav
     c                   eval      w_otyp = w_otyp_sav
     c                   exsr      opplag
8.xx  *
8.xx  * Logg
8.xx  *
8.xx c                   if        b_logg = *on
8.xx c                   eval      w_jtext = 'Oppdaterer lager 1 ' +
8.xx c                             %char(fdnumm) + '/' +
8.xx c                             %char(fdsuff) + '/' +
8.xx c                             ' ordretype '+footyp+'/'+%char(vaoakk)+
8.xx c                             ' varenr ' + fdvare + '/' + %char(fdanta)
8.xx c                   exsr      logg_debug
8.xx c                   endif
8.xx  *
     c                   endif
     c                   endif
     c                   endif
5.02 c                   endif
      *
     c                   eval      fpsolu_stll = 1
     c                   eval      fpsolu_slin = fdline
     c     fpsolu_key    chain     fpsolur                            92
      *
     c                   if        *in92 = *off
     c                   update    fpsolur
     c                   else
     c                   eval      fkstll = 3
     c                   eval      fkslin = fdline
     c                   eval      fkskod = 'V'
     c                   write     fpsolur
     c                   endif
      *
     c                   goto      linje1
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter ordre-tekst (FORAN) og legger ut til PLUKK-S-LINJE-REG.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     foran         begsr
      *
     c                   eval      fotxl1_line = *zero
      *
     c     fotxl1_key    setll     fotxl1r
      *
     c     foran1        tag
     c                   eval      *in90 = *off
     c                   read      fotxl1r                                90
     c                   if        *in90 = *off
     c                   if        ftfirm = w_firm
     c                   if        ftnumm = fotxl1_numm
     c                   if        ftsuff = fotxl1_suff
     c                   if        ftline <= 4999
      *
     c                   eval      fpsolu_stll = 2
     c                   eval      fpsolu_slin = ftline
     c     fpsolu_key    chain     fpsolur                            92
      *
     c                   if        *in92 = *off
     c                   update    fpsolur
     c                   else
     c                   eval      fkstll = 2
     c                   eval      fkslin = ftline
     c                   eval      fkskod = 'K'
     c                   write     fpsolur
     c                   endif
      *
     c                   goto      foran1
     c                   endif
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter ordre-tekst (ETTER) og legger ut til PLUKK-S-LINJE-REG.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     etter         begsr
      *
     c                   eval      fotxl1_line = 5000
      *
     c     fotxl1_key    setll     fotxl1r
      *
     c     etter1        tag
     c                   eval      *in90 = *off
     c                   read      fotxl1r                                90
     c                   if        *in90 = *off
     c                   if        ftfirm = w_firm
     c                   if        ftnumm = fotxl1_numm
     c                   if        ftsuff = fotxl1_suff
     c                   if        ftline <= 9999
      *
     c                   eval      fpsolu_stll = 4
     c                   eval      fpsolu_slin = ftline
     c     fpsolu_key    chain     fpsolur                            92
      *
     c                   if        *in92 = *off
     c                   update    fpsolur
     c                   else
     c                   eval      fkstll = 4
     c                   eval      fkslin = ftline
     c                   eval      fkskod = 'K'
     c                   write     fpsolur
     c                   endif
      *
     c                   goto      etter1
     c                   endif
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter neste ledige suffix
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hntsuf        begsr
8.09  *
8.09  * Hent høyeste brukte suffiks på ordrenummer
8.09  *
8.09 c                   eval      w_suff = 0
8.09 c                   call      'AS110R'
8.09 c                   parm                    fonumm
8.09 c                   parm                    w_suff
      *
8.09 c*                  eval      rohel1_suff = 49
8.09 c*    rohel1_key    setll     fohel1r
      *
8.09 c*                  eval      *in90 = *off
8.09 c*                  readp     fohel1r                                90
      *
     c*                  eval      rohel1_suff = fosuff + 1
8.09 c                   eval      rohel1_suff = w_suff
3.11  *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Legger ut ordre-linjer til resting
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Forklaring på hva som skjer i denne subrutine:
      * ----------------------------------------------
      *
      * I skjermbildet for utplukk finnes tre alternativer:
      *
      *    0 = Ingen spesifisering av linjer
      *    1 = Plukk bare linjer som oppgis
      *    2 = Plukk alle linjer, men unntak
      *        (Feltet for denne koden heter FKPRES)
      *
      * Kode 0 kommer ikke innom denne rutine, bare kode 1 eller 2.
      * Det settes lower limit på varelinjeregister (som inneholder varer
      * og tekstlinjer) og det leses så lenge man befinner seg på samme
      * firmanummer / ordrenummer / suffix.
      * Samtidig gjøres det oppslag mot utplukksregister for å se om det
      * skal gjøres endringer i forhold til opprinnelig varelinje.
      *
      * Disse koder er tilgjengelige:
      *    1 = Plukkes
      *    3 = Kopieres                 (Kun tekstlinjer)
      *    4 = Holdes tilbake
      *    5 = Ingen resting av antall  (Kun varelinjer)
      *        (Feltet for denne koden heter FKLKOD)
      *
      * Utplukks(linje)register inneholder derfor poster bare der hvor
      * det er registrert enten et antall eller en kode for utplukk.
      * Vær også oppmerksom på at selv om det ikke blir funnet en
      * post i utplukksregisteret, kan det legges inn verdier i feltene
      * FKLANT og FKLKOD
      * F. eks. settes FKLKOD til 1 når hele ordren skal plukkes, men
      * unntak (FKPRES=2)
      *
      * Rutinen tar seg av restnotering, oppdatering av lager, kopiering/
      * plukking av tekslinjer etc.  En ordre som kommer innom her vil
      * alltid få et nytt suffix, mens resten blir liggende igjen på
      * gammel ordre eller slettes dersom den plukkes tom.
      *
     c     olrest        begsr
      *
8.14 c                   eval      w_ktyp = *blanks
     c                   eval      fodtl1_line = *zero
     c     fodtl1_key    setll     fodtl1r
      *
      * Leser inn en record fra ORDRE-LINJE-REGISTER
      *
8.17 c                   eval      b_notxt = *off
      *
     c     olres1        tag
     c                   eval      *in90 = *off
     c                   read      fodtl1r                                90
     c                   if        *in90 = *off
     c                   if        fdfirm = w_firm
     c                   if        fdnumm = fodtl1_numm
     c                   if        fdsuff = fodtl1_suff
      *
      * Hent opplysninger fra UTPLUKK-LINJE-REGISTER (rest/Tekst-linje)
      *
     c                   eval      lwpllu_line = fdline
     c     lwpllu_key    chain     lwpllur
     c                   if        %found(lwpllu)
6.31 c**                 delete    lwpllur
6.31 c                   unlock    lwpllu
8.17 c                   eval      b_notxt = *off
8.17 c                   else
8.17 c                   if        fdvare <> *blanks
8.17 c                   eval      b_notxt = *on
8.17 c                   eval      w_test = 1
8.17 c                   goto      olres1
8.17 c                   endif
8.17  * NB Ikke kollilinjer 9000-9002
8.17 c                   if        b_notxt = *on and
8.17 c                             fdline <> 9000 and
8.17 c                             fdline <> 9001 and
8.17 c                             fdline <> 9002
8.17 c                   goto      olres1
8.17 c                   endif
     c                   endif
7.03  *  Sjekk om egen ordretype vedr prosjektregnskap
7.03 c                   eval      rkunl1_kund = fdkund
7.03 c     rkunl1_key    chain     rkunl1
7.03 c                   if        %found(rkunl1)
7.03 c                             and rkspfa <> 0
7.03 c                             and rkmkod <> 0
7.03 c                             and faaot6 <> ' '
7.04 c**                 eval      mkotyp = faaot6
7.03 c                   else
6.34  *
6.34  * Sjekker "Neste ordretype" i steden for å bruke fra parameterfil
6.34  *
6.35 c*                   eval      votyl1_otyp = fdotyp
6.35 c*     votyl1_key    chain     votyl1r                            90
6.35 c*                   eval      mkotyp = vaonxt
7.02 c                   endif
8.14  *
8.14  * Dersom tekstlinjer, gå til eget avsnitt - flyttet
8.14  *
8.14 c     fdvare        cabeq     *blank        xtxlin
8.14  *
      * Hent opplysninger fra ordretype
      *
     c                   eval      votyl1_otyp = mkotyp
8.14 c     votyl1_key    chain     votyl1r
8.14 c                   if        %found(votyl1)
     c                   eval      w_ruti = vaorut
     c                   eval      w_oakk = vaoakk
8.14 c                   eval      w_ktyp = mkotyp
8.14 c                   endif
      *
      * Dersom alle linjer skal plukkes, men unntak kan gjøres
      * Legges hele antallet inn som antall plukket
      * dersom linjen ikke finnes i ORDRE-PLUKK-LINJE-REGISTER
      *
     c                   if        not %found(lwpllu)
     c                   eval      mkantp = 0
     c                   endif
      *
      * Dersom antall levert=0 skal ikke ORDRE-LINJE-REGISTER oppdat.
      * ??
     c                   if        mkantp = 0
     c                   eval      w_test = 1
     c                   goto      olres1
     c                   endif
      *
      * Danner ny linjer for RESTORDRE
      * Legger ut nye linjer for "NY" ordre  ): NY SUFFIX
      *
3.11 c                   eval      fodtl1_numm = fdnumm
     c                   eval      fodtl1_line = fdline
     c     rodtlu_key    chain     fodtlur                            94
     c                   if        *in94 = *on
7.01  *
7.01 c                   if        u_701 = *on and
7.01 c                             fofsys = 'B2C' and
7.01 c                             mkantp > fdanta
7.01 c                   exsr      b2c_calc
7.01 c                   endif
7.01  *
3.11 c                   eval      fdnumm = rohel1_numm
     c                   eval      fdsuff = rohel1_suff
     c                   eval      fdanta = mkantp
6.10 c                   time                    fdodat
6.10 c                   time                    fdotim
6.10 c                   eval      fdoous = l_user
6.10 c                   time                    fdedat
6.10 c                   time                    fdetim
6.10 c                   eval      fdeusr = l_user
     c                   write     fodtlur
8.13  *
8.13  * Oppdater tilhørende bestillinhslinje
8.13  *
8.13 c                   if        fdbenr <> 0
8.13 c                   eval      lodtlu_numm = fdbenr
8.13 c                   eval      lodtlu_suff = fdbsuf
8.13 c                   eval      lodtlu_line = fdblin
8.13 c                   eval      lodtlu_line = fdblin
8.13 c     lodtlu_key    chain     lodtlu
8.13 c                   if        %found
8.13 c                   eval      ldorsu = fdsuff
8.13 c                   time                    ldedat
8.13 c                   time                    ldetim
8.13 c                   eval      ldeusr = l_user
8.13 c                   update    lodtlur
8.13 c                   endif
8.13 c                   endif
8.xx  *
8.xx  * Logg
8.xx  *
8.xx c                   if        b_logg = *on
8.xx c                   eval      w_jtext = 'Ordretype på varelinje ' +
8.xx c                             %char(fdline) + '/' +
8.xx c                             %trim(fdvare) + '/' +
8.xx c                             mkotyp
8.xx c                   exsr      logg_debug
8.xx c                   endif
      *
      * Kaller opp subrutine for oppdatering av lager
      *
     c                   if        faalag <> *zero
5.02 c                   if        w_olag =  *zero
8.12 c                   if        vaoakk > 1
     c                   eval      w_lage = fdlage
     c                   eval      w_otyp = mkotyp
     c                   exsr      opplag
8.xx  *
8.xx  * Logg
8.xx  *
8.xx c                   if        b_logg = *on
8.xx c                   eval      w_jtext = 'Oppdaterer lager 2 ' +
8.xx c                             %char(fdnumm) + '/' +
8.xx c                             %char(fdsuff) + '/' +
8.xx c                             ' ordretype ' + footyp + '/'+%char(vaoakk)+
8.xx c                             ' varenr ' + fdvare + '/' + %char(fdanta)
8.xx c                   exsr      logg_debug
8.xx c                   endif
8.xx  *
8.12 c                   endif
     c                   endif
5.02 c                   endif
      *
     c                   endif
      *
      * Oppdaterer linjer for "GAMMEL" ordre
      *
     c     fodtl1_key    chain     fodtlur                            94
     c                   if        *in94 = *off
      *
      * Kaller opp subrutine for oppdatering av lager
      *
     c                   if        faalag <> *zero
5.02 c                   if        w_olag =  *zero
8.12 c                   if        vaoakk > 1
6.12 c                   eval      w_otyp = w_otyp_sav
6.12 c*                  eval      w_lage = w_lage_sav
6.12 c*                  eval      w_otyp = w_otyp_sav
     c                   eval      fdanta = fdanta * -1
     c                   exsr      opplag
8.xx  *
8.xx  * Logg
8.xx  *
8.xx c                   if        b_logg = *on
8.xx c                   eval      w_jtext = 'Oppdaterer lager 3 ' +
8.xx c                             %char(fdnumm) + '/' +
8.xx c                             %char(fdsuff) + '/' +
8.xx c                             ' ordretype ' + footyp + '/'+%char(vaoakk)+
8.xx c                             ' varenr ' + fdvare + '/' + %char(fdanta)
8.xx c                   exsr      logg_debug
8.xx c                   endif
8.xx  *
     c                   eval      fdanta = fdanta * -1
8.12 c                   endif
     c                   endif
5.02 c                   endif
      *
      * Beregn rest    (REST=BESTILT-LEVERT)
      * Kode for og bevare tidligere linjeantall =0
      *
     c                   if        vaoboo = 0
     c                   eval      w_rest = fdanta - mkantp
     c                   endif
      *
      * Det skal ikke restnoteres
      *
     c                   if        vaoboo = 1
     c                   eval      w_rest = 0
     c                   endif
      *
      * Behold alltid hele det opprinnelige antallet
      *
     c                   if        vaoboo = 2
     c                   eval      w_rest = fdanta
     c                   endif
      *
      * Behandler oppdatering av ordren utfra resultatet av restingen
      *
     c                   if        w_rest > 0
     c                   eval      w_test = 1
     c                   eval      fdanta = w_rest
6.10 c                   time                    fdedat
6.10 c                   time                    fdetim
6.10 c                   eval      fdeusr = l_user
     c                   update    fodtlur
     c                   else
5.63  *
5.63  * Logg ordrelinje før den blir slettet
5.63  *
8.15 c*                  eval      w_spgm      = 'FO614R'
8.15 c                   eval      w_spgm      = 'FO619R'
5.63 c                   call      'FS710R'
5.63 c                   parm                    w_firm
5.63 c                   parm                    fdnumm
5.63 c                   parm                    fdsuff
5.63 c                   parm                    fdline
5.63 c                   parm                    w_spgm
5.63  *
     c                   eval      fdanta = 0
     c                   delete    fodtlur
     c                   endif
      *
      * Kaller opp subrutine for oppdatering av lager
      *
     c                   if        faalag <> *zero
5.02 c                   if        w_olag =  *zero
8.12 c                   if        vaoakk > 1
6.12 c*                  eval      w_lage = folage
6.12 c                   eval      w_otyp = w_otyp_sav
     c                   exsr      opplag
8.xx  *
8.xx  * Logg
8.xx  *
8.xx c                   if        b_logg = *on
8.xx c                   eval      w_jtext = 'Oppdaterer lager 4 ' +
8.xx c                             %char(fdnumm) + '/' +
8.xx c                             %char(fdsuff) + '/' +
8.xx c                             ' ordretype ' + footyp + '/'+%char(vaoakk)+
8.xx c                             ' varenr ' + fdvare + '/' + %char(fdanta)
8.xx c                   exsr      logg_debug
8.xx c                   endif
8.xx  *
8.12 c                   endif
     c                   endif
5.02 c                   endif
      *
     c                   endif
      *
      * Ferdig med behandling av varelinjer
      *
     c                   goto      xneste
      *
      * -----------------------------------
      * Behandling av tekstlinjer
      *
     c     xtxlin        tag
      *
      * Legge ut tekstlinje på ny suffix
      *
3.11 c                   eval      fodtl1_numm = fdnumm
     c                   eval      fodtl1_line = fdline
     c     rodtlu_key    chain     fodtlur                            94
     c                   if        *in94 = *on
3.11 c                   eval      fdnumm = rohel1_numm
     c                   eval      fdsuff = rohel1_suff
     c                   eval      fdanta = 0
6.10 c                   time                    fdodat
6.10 c                   time                    fdotim
6.10 c                   eval      fdoous = l_user
6.10 c                   time                    fdedat
6.10 c                   time                    fdetim
6.10 c                   eval      fdeusr = l_user
     c                   write     fodtlur
8.13  *
8.13  * Oppdater tilhørende bestillinhslinje
8.13  *
8.13 c                   if        fdbenr <> 0
8.13 c                   eval      lodtlu_numm = fdbenr
8.13 c                   eval      lodtlu_suff = fdbsuf
8.13 c                   eval      lodtlu_line = fdblin
8.13 c                   eval      lodtlu_line = fdblin
8.13 c     lodtlu_key    chain     lodtlu
8.13 c                   if        %found
8.13 c                   eval      ldorsu = fdsuff
8.13 c                   time                    ldedat
8.13 c                   time                    ldetim
8.13 c                   eval      ldeusr = l_user
8.13 c                   update    lodtlur
8.13 c                   endif
8.13 c                   endif
     c                   endif
      *
      * Slette eventuelle tekstlinjer fra "gammel ordre"
      *
     c     fodtl1_key    chain     fodtlur                            94
     c                   if        *in94 = *off
     c                   delete    fodtlur
     c                   endif
      *
     c     xneste        tag
6.31  *
6.31  * Slett fra plukkregister
6.31  *
6.31 c     lwpllu_key    chain     lwpllur
6.31 c                   if        %found(lwpllu)
6.31 c                   delete    lwpllur
6.31 c                   endif
      *
     c                   goto      olres1
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Leser igjenom ordrelinjer for oppdatering av lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     laglag        begsr
      *
     c                   eval      fodtl1_line  = *zero
     c     fodtl1_key    setll     fodtl1r
      *
     c     lanje1        tag
     c                   eval      *in90 = *off
     c                   read      fodtl1r                                90
     c                   if        *in90 = *off
     c                   if        fdfirm = w_firm
     c                   if        fdnumm = fodtl1_numm
     c                   if        fdsuff = fodtl1_suff
      *
      * Behandler fra antall på ordretype FRA
      *
     c                   eval      w_lage = w_lage_sav
     c                   eval      w_otyp = w_otyp_sav
     c                   eval      fdanta = fdanta * -1
     c                   exsr      opplag
8.xx  *
8.xx  * Logg
8.xx  *
8.xx c                   if        b_logg = *on
8.xx c                   eval      w_jtext = 'Oppdaterer lager 5 ' +
8.xx c                             %char(fdnumm) + '/' +
8.xx c                             %char(fdsuff) + '/' +
8.xx c                             ' ordretype ' + footyp + '/'+%char(vaoakk)+
8.xx c                             ' varenr ' + fdvare + '/' + %char(fdanta)
8.xx c                   exsr      logg_debug
8.xx c                   endif
8.xx  *
     c                   eval      fdanta = fdanta * -1
      *
      * Behandler til antall på ordretype TIL
      *
     c                   eval      w_lage = fdlage
     c                   eval      w_otyp = mkotyp
     c                   exsr      opplag
8.xx  *
8.xx  * Logg
8.xx  *
8.xx c                   if        b_logg = *on
8.xx c                   eval      w_jtext = 'Oppdaterer lager 6 ' +
8.xx c                             %char(fdnumm) + '/' +
8.xx c                             %char(fdsuff) + '/' +
8.xx c                             ' ordretype ' + footyp + '/'+%char(vaoakk)+
8.xx c                             ' varenr ' + fdvare + '/' + %char(fdanta)
8.xx c                   exsr      logg_debug
8.xx c                   endif
8.xx  *
     c                   goto      lanje1
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdatering av Lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     opplag        begsr
      *
      * Legger over verdier i overførings-felter
      *
     c                   eval      hlvare = fdvare
5.31 c                   eval      hllage = fdlage
     c                   eval      hlenhe = fdenh1
     c                   eval      hldato = *loval
     c                   eval      hltime = *loval
     c                   eval      hlotyp = w_otyp
     c                   eval      hlltyp = fdltyp
      *
     c                   eval      hlanta = fdanta
     c                   eval      hlkopr = fdkopr
     c                   eval      hlonum = fdnumm
     c                   eval      hlolin = fdline
     c                   eval      hlsyst = 'F'
     c                   eval      hlaltk = *blank
     c                   eval      hlrest = *blank
     c                   move      fdnumm        w_numa
     c                   move      fdline        w_lina
     c                   eval      hlrefr = 'O:' + w_numa +
     c                                      ' L:' + w_lina
3.13 c                   move      fdlety        hllety
      *
      * Kaller opp lageroppdaterings-program
      *
     c                   eval      w_lrec = hlrec
      *
     c                   call      'VL001R'
     c                   parm                    w_stat
     c                   parm                    w_lrec
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for finne std_verdier for utskrift
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     std_verd      begsr
      *
      * Legg inn verdier for henting av utskrifts-opplysninger
      *
     c                   eval      l_chgv = *blank
     c                   eval      l_ruti = w_ruti
      *
      * Oppdaterer LDA
      *
     c                   out       *dtaara
      *
      * Kaller opp program før utskrift
      *
     c                   call      'AP600R'
     c                   parm                    l_ruti
     c                   parm                    l_chgv
     c                   parm                    w_tvbt
     c                   parm                    w_in03
     c                   parm                    w_btch
      *
      * Henter inn igjen LDA
      *
     c                   in        *dtaara
     c                   eval      l_alt1 = 1
      *
5.22  *
5.22  * Tilbakestill kode for manuelt overstyrt OUTQ
5.22  *
5.22 c                   eval      l_outm = ' '
      *
      * Oppdaterer LDA
      *
     c                   out       *dtaara
      *
     c                   endsr
5.51  *
5.51  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.51  * Subrutine for slette plukk-reg. på RadioTerm.
5.51  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.51  *
5.51 c     slett_plukk   begsr
5.51  *
5.51 c     lwpllx_key    setll     lwpllu
5.51 c     lwpllx_key    reade     lwpllu
5.51 c                   dow       not %eof(lwpllu)
5.51  *
5.51 c                   delete    lwpllur
5.51  *
5.51 c     lwpllx_key    reade     lwpllu
5.51 c                   enddo
5.51  *
5.51 c                   endsr
5.51  *
7.01  *
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  * Rekalkuler etter overplukk for å sikre samme linjebeløp
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *
7.01  /FREE
7.01   begsr b2c_calc;
7.01
7.01  //  Overplukk, rekalkuler linje med rabatt
7.01
7.01     w_linnet = fdsums - fdsumr;
7.01     eval(h) w_linbrt = fdsapr * mkantp;
7.01
7.01     if  w_linbrt > w_linnet;
7.01       w_lindif = w_linbrt - w_linnet;
7.01       eval(h) fdrab1 = (w_lindif *100) / w_linbrt;
7.01       fdrab2 = 0;
7.01       fdsumr = w_lindif;
7.01       eval(h) w_nynet = fdsums - ((fdsums * fdrab1) / 100);
7.01       if w_nynet > w_linnet;
7.01         fdrab1 = fdrab1 + 0,01;
7.01       endif;
7.01     endif;
7.01
7.01   endsr;
7.01  /END-FREE
      *
8.18  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.18  * Sjekk om det er lagt på nye linjer med serviceartikler
8.18  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.18  *
8.18 c     sjekk_newline begsr
8.18  *
8.18  * Check if there are added service lines. They have all linenumber 0.
      *
8.18 c                   eval      lwpllu_numm = s_numm
8.18 c                   eval      lwpllu_suff = s_suff
8.19 c*                  eval      lwpllu_line = 0
8.19 c                   eval      lwpllu_line = -9999
8.18 c     lwpllu_key    setll     lwpllur
8.19 c*    lwpllu_key    reade     lwpllur
8.19 c     lwpllu_key2   reade     lwpllur
8.18 c                   dow       not %eof
      * Must be in item table
8.18 c                   if        %trim(mkvare) <> *blanks
8.18 c                   eval      vvarl1_vare = mkvare
8.18 c     vvarl1_key    chain     vvarl1r
8.18 c                   if        %found and
8.18 c                             vvvtyp = 'T'
8.18 c                   exsr      add_serv_line
8.18 c                   endif
8.18 c                   endif
      *
8.18 c**                 unlock    lwpllu
8.18 c                   delete    lwpllur
8.19 c     lwpllu_key2   reade     lwpllur
8.19 c*    lwpllu_key    reade     lwpllur
8.18 c                   enddo
8.18  *
8.18 c                   endsr
8.18  *
8.18  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.18  * Add service line to order
8.18  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.18  *
8.18 c     add_serv_line begsr
8.18  *
8.18  * Get highest linennumber
8.18  *
8.18 c                   eval      w_rab1 = 0
8.18 c                   eval      w_line = 0
8.18 c                   exsr      get_line_nbr
8.18  *
8.18  * Check if this order are prepaid
8.18  *
8.18 c                   eval      sdepl1_numm = fonumm
8.18 c                   eval      sdepl1_kund = fokund
8.18 c                   eval      sdepl1_tell = 0
8.18 c     sdepl1_key    chain     sdepl1r
8.18 c                   if        %found(sdepl1)
8.18 c                   if        sk100p = 1 and
8.18 c                             skdepb <> 0
8.18 c                   eval      w_rab1 = 100
8.18 c                   endif
8.18 c                   endif
8.18  *
8.18 c                   eval      w_kode = *blanks
8.18 c                   call      'FD109R'
8.18 c                   parm                    w_kode
8.18 c                   parm                    w_firm
8.18 c                   parm                    fonumm
8.18 c                   parm                    fosuff
8.18 c                   parm                    w_line
8.18 c                   parm                    faalty
8.18 c                   parm                    folety
8.18 c                   parm                    mkvare
8.18 c                   parm                    mkanta
8.18 c                   parm                    mkenhe
8.18 c                   parm                    foldat
8.18 c                   parm                    w_rab1
8.18  *
8.18 c                   endsr
8.18  *
8.18  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.18  * Get highest linenumber
8.18  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.18  *
8.18 c     get_line_nbr  begsr
8.18  *
8.18 c                   eval      fodtl1_numm = fonumm
8.18 c                   eval      fodtl1_suff = fosuff
8.18 c                   eval      fodtl1_line = 9999
8.18 c     fodtl1_key    setll     fodtl1
8.18 c                   readp     fodtl1
8.18 c                   if        not %eof and
8.18 c                             fdfirm = w_firm and
8.18 c                             fdnumm = fonumm and
8.18 c                             fdsuff = fosuff
8.18 c                   eval      w_line = fdline + 1
8.18 c     new_add       tag
8.18  * Check if new linenumber is available
8.18 c                   eval      fodtl1_line = w_line
8.18 c     fodtl1_key    chain     fodtl1
8.18 c                   if        %found
8.18 c                   eval      w_line = w_line + 1
8.18 c                   goto      new_add
8.18 c                   endif
8.18 c                   endif
8.18  *
8.18 c                   endsr
8.18  *
8.xx  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.xx  * Subrutine for logging
8.xx  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.xx  *
8.xx c     logg_debug    begsr
8.xx  *
8.xx c                   eval      w_jstat = 10
8.xx c                   call      'AB705R'
8.xx c                   parm                    w_jkey
8.xx c                   parm                    w_jstat
8.xx c                   parm                    w_jtext
8.xx  *
8.xx c                   endsr
8.xx  *
8.xx  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.xx  * Subrutine for logging
8.xx  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.xx  *
8.xx c     logg_start    begsr
8.xx  *
8.xx  * Vi starter med logging av jobb, og får tilbake unik jobbid.
8.xx  * Først få jobbnummer
8.xx  *
8.xx c                   eval      w_kode = '0'
8.xx c                   eval      w_firm = l_firm
8.xx c                   eval      w_fell = 'NXTKLI'
8.xx c                   eval      w_type = *blank
8.xx c                   eval      w_fast = *blank
8.xx c                   eval      w_numm = *zero
8.xx  *
8.xx c                   call      'AS100R'
8.xx c                   parm                    w_kode
8.xx c                   parm                    w_firm
8.xx c                   parm                    w_fell
8.xx c                   parm                    w_type
8.xx c                   parm                    w_fast
8.xx c                   parm                    w_numm
8.xx  *
8.xx c                   eval      w_jnav = 'FO619 '+ %char(w_numm)
8.xx c                   eval      w_jtxt = 'Logging av behandling fra -
8.xx c                                      PDA starter'
8.xx c                   call      'AB700R'
8.xx c                   parm                    w_jnav
8.xx c                   parm                    w_jkey
8.xx c                   parm                    w_jtxt
8.xx  *
8.xx c                   endsr
8.xx  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
6.nu c                   parm                    p_numm
     c                   parm                    p_suff
      *
      *
      * Definisjon av local data area for output
      *
     c     *dtaara       define    *lda          lda
      *
      * Key for file : ORDRETYPE-REGISTER
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for file : Arbeidsregister utplukk
      *
     c     lwpllu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lwpllu_numm
     c                   kfld                    lwpllu_suff
     c                   kfld                    lwpllu_line
8.19  *
8.19  * Key for file : Arbeidsregister utplukk
8.19  *
8.19 c     lwpllu_key2   klist
8.19 c                   kfld                    w_firm
8.19 c                   kfld                    lwpllu_numm
8.19 c                   kfld                    lwpllu_suff
5.51  *
5.51  * Key for file : Arbeidsregister utplukk - slett for ordre
5.51  *
5.51 c     lwpllx_key    klist
5.51 c                   kfld                    w_firm
5.51 c                   kfld                    lwpllx_numm
5.51 c                   kfld                    lwpllx_suff
      *
      * Key for file : OFAK-KODE-REGISTER
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for file : ORDRE-HODE-REGISTER
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      * Key for file : ORDRE-LINJE-REGISTER
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
     c                   kfld                    fodtl1_line
5.42  *
5.42  * Key for file : ORDRE-LINJE-REGISTER (les)
5.42  *                            9
5.42 c     fodtlr_key    klist
5.42 c                   kfld                    w_firm
5.42 c                   kfld                    fodtlr_numm
5.42 c                   kfld                    fodtlr_suff
5.42 c                   kfld                    fodtlr_line
      *
      * Key for file : ORDRE-TEKST-REGISTER
      *
     c     fotxl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fotxl1_numm
     c                   kfld                    fotxl1_suff
     c                   kfld                    fotxl1_line
      *
      * Key for file : ORDRE-HODE-REGISTER, for resting
      *
     c     rohel1_key    klist
     c                   kfld                    w_firm
3.11 c                   kfld                    rohel1_numm
     c                   kfld                    rohel1_suff
      *
      * Key for file : ORDRE-LINJE-REGISTER, for resting
      *
     c     rodtlu_key    klist
     c                   kfld                    w_firm
3.11 c                   kfld                    rohel1_numm
     c                   kfld                    rohel1_suff
     c                   kfld                    fodtl1_line
3.33  *
3.33  * Key for oppslag på ordre-info-type
3.33  *
3.33 c     vinfl1_key    klist
3.33 c                   kfld                    w_firm
3.33 c                   kfld                    vinfl1_ityp
      *
      * Key for file : PLUKK-S-LINJE-REGISTER
      *
     c     fpsolu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fpsolu_sonr
     c                   kfld                    fpsolu_ssuf
     c                   kfld                    fpsolu_stll
     c                   kfld                    fpsolu_slin
3.34  *
3.34  * Key for brukerregister
3.34  *
3.34 c     fusrl1_key    klist
3.34 c                   kfld                    w_firm
3.34 c                   kfld                    fusrl1_user
7.02  *
7.02  * Key for propertiesfil
7.02  *
7.02 c     afpslr_key    klist
7.02 c                   kfld                    afpslr_ufil
7.02 c                   kfld                    afpslr_uide
7.03  *
7.03  * Key for kunderegister
7.03  *
7.03 c     rkunl1_key    klist
7.03 c                   kfld                    w_firm
7.03 c                   kfld                    rkunl1_kund
8.01  *
8.01  * Key for posisjonering på fraktregister
8.01  *
8.01 c     ftrpi2_key    klist
8.01 c                   kfld                    w_firm
8.01 c                   kfld                    ftrpi2_rnum
8.01 c                   kfld                    ftrpi2_rsuf
8.01  *
8.01  * Key for posisjonering på fraktregister
8.01  *
8.01 c     ftrpiu_key    klist
8.01 c                   kfld                    w_firm
8.01 c                   kfld                    ftrpiu_roid
8.01  *
8.01  * Forsendelsesmåte - tilleggsregister
8.01  *
8.01 c     raf2l1_key    klist
8.01 c                   kfld                    w_firm
8.01 c                   kfld                    raf2l1_2kod
8.03  *
8.03  * Key for oppslag på flåtestyring pr lager
8.03  *
8.03 c     ftrli1_key    klist
8.03 c                   kfld                    w_firm
8.03 c                   kfld                    ftrli1_llag
8.13  *
8.13  * Key for file : Bestilling-LINJE-REGISTER
8.13  *
8.13 c     lodtlu_key    klist
8.13 c                   kfld                    w_firm
8.13 c                   kfld                    lodtlu_numm
8.13 c                   kfld                    lodtlu_suff
8.13 c                   kfld                    lodtlu_line
8.18  *
8.18  * Key for depositum table
8.18  *
8.18 c     sdepl1_key    klist
8.18 c                   kfld                    w_firm
8.18 c                   kfld                    sdepl1_numm
8.18 c                   kfld                    sdepl1_kund
8.18 c                   kfld                    sdepl1_tell
8.18  *
8.18  * Key for item table
8.18  *
8.18 c     vvarl1_key    klist
8.18 c                   kfld                    w_firm
8.18 c                   kfld                    vvarl1_vare
      *
      * Legger inn ordrenr. i nøkler
      *
     c                   eval      rohel1_numm = p_numm
     c                   eval      fohel1_numm = p_numm
     c                   eval      fohel1_suff = p_suff
     c                   eval      fodtl1_numm = p_numm
     c                   eval      fodtl1_suff = p_suff
     c                   eval      fodtlr_numm = p_numm
     c                   eval      fodtlr_suff = p_suff
     c                   eval      lwpllu_numm = p_numm
     c                   eval      lwpllu_suff = p_suff
5.51 c                   eval      lwpllx_numm = p_numm
5.51 c                   eval      lwpllx_suff = p_suff
8.18 c                   eval      s_numm = p_numm
8.18 c                   eval      s_suff = p_suff
      *
      * Blanker hjelpefelter som benyttes i Local Data Area
      *
     c                   eval      w_firm = p_firm
     c                   eval      l_alt1 = *zero
     c                   eval      l_alt2 = *zero
     c                   eval      l_peri = *zero
     c                   eval      l_dato = *loval
     c                   eval      l_prog = *blank
8.10 c                   eval      l_gsuf = 0
      *
      * Legger inn type for oppdatering av lager  R=Registrering
      *                                             =Utskrift/Utplukk
      *
     c                   eval      hltype = *blank
      *
3.36 c                   eval      w_firm      = p_firm
3.34  *
3.34  * Hente inn brukerregister
3.34  *
3.34 c                   eval      fusrl1_user = l_user
3.34 c     fusrl1_key    chain     fusrl1r                            90
      *
      * Legger inn dagens dato
      *
     c                   time                    w_date
7.01  *
7.01  * Endring 7.01
7.01  *
7.01   CallP CO402R(l_filg:w_firm:0:'VARE_B2C':
7.01                    co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01     u_701 = *on;
7.01   endif;
7.02  *
7.02  * Sjekk om det skal være holdekode
7.02  *
7.02 c                   eval      afpslr_ufil = l_filg
7.02 c                   eval      w_hkod = *blanks
7.02 c                   eval      afpslr_uide = 'POS.PNHOLD'
7.02 c     afpslr_key    chain     afpslrr
7.02 c                   if        %found(afpslr)
7.02 c                   eval      w_hkod = %trim(afudss)
7.02 c                   endif
8.xx  *
8.xx  * Logg
8.xx  *
8.xx c                   if        b_logg = *on
8.xx c                   exsr      logg_start
8.xx c                   eval      w_jtext = 'Starter logging i FO619'
8.xx c**                           tar ??'
8.xx c                   exsr      logg_debug
8.xx c                   endif
8.01  *
8.01  * Endring 7.03 - Koblet til ekstern flåtestyring
8.01  *
8.01   CallP CO402R(l_filg:w_firm:0:'EKSTERN_FLATESTYRING':
8.01                    co402_verdi1:co402_verdi2);
8.01   if co402_verdi1 = '1';
8.01     u_801 = *on;
8.01   endif;
      *
     c                   endsr
