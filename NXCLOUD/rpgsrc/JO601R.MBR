     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : JO601R
      * Beskrivelse . . : Matching, Utskrift matchede varer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       210596 HKO  Nytt program
6.30  * 6.30  290514 BKV  LVAR økt fra 15 til 20
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjmatl6    if   e           k disk    rename(jmatpfr:jmatl6r)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
      *
     fjo601p    o    e             printer
      *
      * Definering av parametre
      *
     d p_parm          s             32
      *
      * Definering av Local data area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
      *
      * Datastruktur for parameterliste -inn
      *
     d                 ds
     d d_list                        32
     d  d_firm                        3  0 overlay(d_list)
     d  d_ldo2                        6  0 overlay(d_list:4)
     d  d_ogrp                        2  0 overlay(d_list:10)
     d  d_hgrp                        2  0 overlay(d_list:12)
     d  d_ugrp                        3  0 overlay(d_list:14)
     d  d_kode                        1  0 overlay(d_list:17)
      *
      * Definering av variable i nøkler
      *
     d jmatl6_vkod     s                   like(jovkod)
     d jmatl6_ldo2     s                   like(joldo2)
     d jmatl6_ogrp     s                   like(joogrp)
     d jmatl6_hgrp     s                   like(johgrp)
     d jmatl6_ugrp     s                   like(jougrp)
     d jvarl1_vare     s                   like(jvvare)
     d jlevl1_ldor     s                   like(jlldor)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
      *
      * Definering av variable
      *
     d b_frst          s              1    inz(*on)
     d utime           s              6  0
      *
     d w_firm          s                   like(jofirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Header
      *
     c                   exsr      hode
      *
      * Detalj
      *
     c                   exsr      detalj
      *
      * Avslutter program
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering og utskrift av hode-opplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode          begsr
      *
      * Legger inn arb.stasjon og bruker i faste felter
      *
     c                   eval      a1firm = d_firm
     c                   eval      a1user = l_user
     c                   eval      a1wsid = l_wsid
     c                   eval      a1date = udate
     c                   time                    utime
     c                   eval      a1time = utime
      *
      * Henter firmaopplysninger
      *
     c     afirl1_key    chain     afirl1                             90
     c                   eval      a1fnav = aafnav
      *
      * Henter parametre med beskrivelse
      *
     c                   eval      a2ldo2 = d_ldo2
     c                   eval      a2ogrp = d_ogrp
     c                   eval      a2hgrp = d_hgrp
     c                   eval      a2ugrp = d_ugrp
     c                   eval      a2kode = d_kode
      *
     c                   if        d_ldo2 <> 0
     c                   eval      jlevl1_ldor = d_ldo2
     c     jlevl1_key    chain     jlevl1                             90
     c                   if        *in90 = *off
     c                   eval      a2lnav = jlnavn
     c                   endif
     c                   endif
      *
     c                   if        d_ogrp <> 0
     c                   eval      vogrl1_oogr = d_ogrp
     c     vogrl1_key    chain     vogrl1                             90
     c                   if        *in90 = *off
     c                   eval      a2utxt = vgotxt
     c                   endif
     c                   endif
      *
     c                   if        d_hgrp <> 0
     c                   eval      vhgrl1_hogr = d_ogrp
     c                   eval      vhgrl1_hhgr = d_hgrp
     c     vhgrl1_key    chain     vhgrl1                             90
     c                   if        *in90 = *off
     c                   eval      a2utxt = vghtxt
     c                   endif
     c                   endif
      *
     c                   if        d_ugrp <> 0
     c                   eval      vugrl1_uogr = d_ogrp
     c                   eval      vugrl1_uhgr = d_hgrp
     c                   eval      vugrl1_uugr = d_ugrp
     c     vugrl1_key    chain     vugrl1                             90
     c                   if        *in90 = *off
     c                   eval      a2utxt = vgutxt
     c                   endif
     c                   endif
      *
      * Skriver heading
      *
     c                   write     a1hdr
     c                   write     a2hdr
     c                   write     a3hdr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utplukk til - og utkskrift av detaljlinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     detalj        begsr
      *
     c                   eval      jmatl6_vkod = 0
     c                   eval      jmatl6_ldo2 = d_ldo2
     c                   eval      jmatl6_ogrp = d_ogrp
     c                   eval      jmatl6_hgrp = d_hgrp
     c                   eval      jmatl6_ugrp = d_ugrp
     c     jmatl6_key    setll     jmatl6
     c                   read      jmatl6                                 90
     c                   dow       *in90 = *off
      *
      * Test på vare-oppdateringskode
      *
     c                   if        jovkod <> 0
     c                   leave
     c                   endif
      *
      * Test på leverandør
      *
     c                   if        d_ldo2 <> 0 and
     c                             joldo2 <> d_ldo2
     c                   leave
     c                   endif
      *
      * Test på overgruppe
      *
     c                   if        d_ogrp <> 0 and
     c                             joogrp <> d_ogrp
     c                   goto      neste_vare
     c                   endif
      *
      * Test på hovedgruppe
      *
     c                   if        d_hgrp <> 0 and
     c                             johgrp <> d_hgrp
     c                   goto      neste_vare
     c                   endif
      *
      * Test på undergruppe
      *
     c                   if        d_ugrp <> 0 and
     c                             jougrp <> d_ugrp
     c                   goto      neste_vare
     c                   endif
      *
      * Test på matche-status (oppdateringsstatus)
      *
     c                   if        jokode = 0
     c                   goto      neste_vare
     c                   endif
      *
     c                   if        d_kode <> 0 and
     c                             jokode <> d_kode
     c                   goto      neste_vare
     c                   endif
      *
      * Behandling ved brudd på leverandør
      *
     c                   if        d_ldo2 = 0 and
     c                             joldo2 <> a4ldo2
     c                   eval      a4ldo2 = joldo2
     c                   eval      jlevl1_ldor = joldo2
     c     jlevl1_key    chain     jlevl1                             91
     c                   if        *in91 = *off
     c                   eval      a4lnav = jlnavn
     c                   else
     c                   eval      a4lnav = *blank
     c                   endif
     c                   if        b_frst = *on
     c                   eval      b_frst = *off
     c                   else
     c                   write     a1hdr
     c                   write     a3hdr
     c                   endif
     c                   write     a4hdr
     c                   endif
      *
      * Flytter verdier til detaljlinje
      *
     c                   eval      d1ogrp = joogrp
     c                   eval      d1hgrp = johgrp
     c                   eval      d1ugrp = jougrp
     c                   eval      d1tek1 = jotek1
     c                   eval      d1tek2 = jotek2
     c                   eval      d1var1 = jovar1
     c                   eval      d1ldor = joldor
6.30 c*                  eval      d1lva1 = jolvar
6.30 c                   movel     jolvar        d1lva1
     c                   eval      d1kode = jokode
      *
      * Henter vareinformasjon
      *
     c                   eval      d1te21 = *blank
     c                   eval      d1te22 = *blank
      *
     c                   if        jovar2 <> *blank
     c                   eval      jvarl1_vare = jovar2
     c     jvarl1_key    chain     jvarl1                             91
     c                   if        *in91 = *off
     c                   eval      d1te21 = jvtek1
     c                   eval      d1te22 = jvtek2
6.30 c*                  eval      d1lva2 = jvlvar
6.30 c                   movel     jvlvar        d1lva2
     c                   endif
     c                   endif
      *
      * Skriver detaljlinje
      *
     c                   exsr      skriv_dtl
      *
     c     neste_vare    tag
      *
     c                   read      jmatl6                                 90
      *
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift av detaljlinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_dtl     begsr
      *
      * Skriv detaljlinje
      *
     c                   write     d1dtl                                30
      *
     c                   exsr      overflow
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for overflow
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     overflow      begsr
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   write     a3hdr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_parm
      *
      * Key for les på matcheregister
      *
     c     jmatl6_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jmatl6_vkod
     c                   kfld                    jmatl6_ldo2
     c                   kfld                    jmatl6_ogrp
     c                   kfld                    jmatl6_hgrp
     c                   kfld                    jmatl6_ugrp
      *
      * Key for oppslag på firma
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på vare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      *
      * Flytter parameter til datasturuktur
      *
     c                   eval      d_list = p_parm
     c                   eval      w_firm = d_firm
      *
     c                   eval      a4ldo2 = 999999
      *
     c                   endsr
