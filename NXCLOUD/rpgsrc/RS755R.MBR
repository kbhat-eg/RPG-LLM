      /TITLE  Henter riktig mva-sats avhengig av dato
     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RS705R                                              *
      * Beskrivelse . . : Henter riktig mva-sats avhengig av dato             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      *           150601  Nytt program                                   KOB  *
      * V5.00     020802  Lagt om til bruk  av RA05L1                    KOB  *
      * V5.00     231002  Endret lengden på w_mvas fra 4.2 til 5.2       KOB  *
      *                                                                       *
      * p_mvap =  Sats mva                       (23.000)                     *
      * p_mvat =  Sats for å få beløp inkl. mva   (1.230)                     *
      * p_mvaf =  Sats for å få beløp ekls. mva   (0.813)                     *
      *                                                                       *
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *                                                                       *
      *  BRUK AV INDIKATORER                                                  *
      *                                                                       *
      *    60-61 = Fileindikatorer chain etc.                                 *
      *                                                                       *
      *  BRUK AV RETURKODE                                                    *
      *                                                                       *
      *  Kode i statusfelt(w_stat = N) : post finnes ikke                     *
      *                                                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     fra05l1    if   e           k disk    rename(ra05pfr:ra05l1r)
      *
     d p_stat          s              1
     d p_ekod          s                   like(raekod)
     d p_etxt          s                   like(raetxt)
     d p_dato          s               d   datfmt(*iso)
     d p_mvap          s              6  2
     d p_mvat          s              5  4
     d p_mvaf          s             10  9
      *
     d w_firm          s                   like(raefir)
     d w_mvas          s              5  2
      *
     d ra05l1_ekod     s                   like(raekod)
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      *
      * Sjekke kode
      *
     c                   eval      ra05l1_ekod = p_ekod                                      .
     c     ra05l1_key    chain     ra05l1r                            60
      *
     c                   if        *in60 = *on                                           .....
     c                   eval      p_stat = 'N'                                              .
     c                   eval      p_etxt = 'Koden er ugyldig  '                             .
     c                   else                                                            .....
     c                   eval      p_stat = *blank                                           .
     c                   eval      p_etxt = raetxt                                           .
     c                   endif                                                           .....
      *
      * Finner hvilken mva-sats som skal benyttes utfra dato
      *
     c                   if        p_dato >= raemda
     c                   eval      w_mvas = raepro
     c                   else
     c                   eval      w_mvas = raegpr
     c                   endif
      *
      * Beregner mva-satser                                             *
      *
     c                   eval      w_mvas = w_mvas + 100
     c                   eval      p_mvat = w_mvas / 100
     c                   eval(h)   p_mvaf = 100 / w_mvas
     c                   eval      p_mvap = w_mvas - 100
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     c     slutt         tag
     c                   eval      *inlr = *on
     c                   return
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     c     *inzsr        begsr
      *
     c                   eval      w_firm = l_firm
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_stat
     c                   parm                    p_ekod
     c                   parm                    p_etxt
     c                   parm                    p_dato
     c                   parm                    p_mvap
     c                   parm                    p_mvat
     c                   parm                    p_mvaf
      *
      * Initierer output-parametre
      *
     c                   eval      p_mvap = 0
     c                   eval      p_mvat = 1
     c                   eval      p_mvaf = 1
      *
      * Key for kode
      *
     c     ra05l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra05l1_ekod
      *
     c                   endsr
