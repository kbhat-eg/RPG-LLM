     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JR763R
      * Beskrivelse . . : Rapp. vareregister, innlesing av record S4
      *
      *                   Oppdaterer varer, priser og forpakninger fra
      *                   S4 - Advisor / MAXBO varemaster
      *
      *                   Det er test i programmet på at det kun leses inn
      *                   varer/priser fra Løvenskiold Handel (9999).
      *
      *                   NB! Oppretter Løvenskiold som prisgiver på Nobb-
      *                       varer. Varer uten Nobbnr opprettes som egne
      *                       varer med Løvenskiold som vareeier
      * 6.30 **
      * Fra 6.30 må det også opprettes vare-pakning-prisgiver og vare-prisgiver
      * Disse opprettes med prisgiver 9999.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       180100 HKO  Nytt program
      *       270300 HKO  Tilpasset at alle records uten data ikke er med
      *                   fra VIDA/S4
      *       130400 HKO  Midlertidig lagt inn konvertering av enheter
      *       260301 HKO  Lagt inn behandling av kampanje
      *       060603 HKO  Fjernet behandling av leverandør 9998 - Christ
      *                   Engebretsen
      *       290305 HKO  Rettet feil rundt innlesing av kampanje
      *       140905 HKO  Byttet program for oppdatering av søktekster
6.11  *       201009 bof  Oppdater vare-pakning-prisgiver med ean-nr
6.12  *       081010 bof  Lagt til JVPENH til jvarlu
6.30  * 6.30  280113 bof  Omskriving i forb. endring pakning VA (fjernet
6.30  *                   jvpppf)
6.31  * 6.30  070716 bof  Nytt nivå, JVLEPF + utvidet lvar til 20
6.32  * 6.30  080716 bof  Lagt inn pakning på leverandør 9999
6.33  * 6.30  111017 bof  Feilretting vedr. vare-pakning og vare-leverandør
      *
8.01  *PRG2   220622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjrvapf    if   e           k disk
      *
     fjvarl8    if   e           k disk    rename(jvarpfr:jvarl8r)
     fjvarl3    if   e           k disk    rename(jvarpfr:jvarl3r)
     ffenkl1    if   e           k disk    rename(fenkpfr:fenkl1r)
     fjvarlu    uf a e           k disk    rename(jvarpfr:jvarlur)
     fjvdtlu    o  a e           k disk    rename(jvdtpfr:jvdtlur)
     fjstslu    uf   e           k disk    rename(jstspfr:jstslur)
     fjvpklu    uf a e           k disk    rename(jvpkpfr:jvpklur)
     fjvprlu    uf a e           k disk    rename(jvprpfr:jvprlur)
     fjkahl1    if   e           k disk    rename(jkahpfr:jkahl1r)
     fjkahlu    uf a e           k disk    rename(jkahpfr:jkahlur)
     fjkavlu    uf a e           k disk    rename(jkavpfr:jkavlur)
6.31 fjvlelu    uf a e           k disk    rename(jvlepfr:jvlelur)
      *
      * Definering av parametre
      *
     d p_list          s             32
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
      *
      * Definering av parameterliste -inn
      *
     d                 ds
     d d_list                        32
     d  d_firm                        3  0 overlay(d_list)
     d  d_rapp                        2  0 overlay(d_list:4)
     d  d_dato                         d   overlay(d_list:6) datfmt(*iso)
      *
      * Definering av recordbeskrivelse - S4
      *
     d                 ds
     d m1_list                       75
     d  m1_rect                       3    overlay(m1_list:1)
     d  m1_vare                      20    overlay(m1_list:4)
     d  m1_vgrp                       9    overlay(m1_list:24)
     d  m1_klag                       1    overlay(m1_list:33)
     d  m1_kgyl                       1    overlay(m1_list:34)
     d  m1_fdat                       8    overlay(m1_list:35)
     d  m1_tdat                       8    overlay(m1_list:43)
     d  m1_prof                      10    overlay(m1_list:51)
     d  m1_enhl                       5    overlay(m1_list:62)
     d  m1_mvat                       9    overlay(m1_list:67)
      *
     d                 ds
     d m2_list                       83
     d  m2_rect                       3    overlay(m2_list:1)
     d  m2_tek1                      40    overlay(m2_list:4)
     d  m2_tek2                      40    overlay(m2_list:44)
      *
     d                 ds
     d m3_list                      102
     d  m3_rect                       3    overlay(m3_list:1)
     d  m3_ldor                      15    overlay(m3_list:4)
     d  m3_lvar                      20    overlay(m3_list:19)
     d  m3_lvgr                      10    overlay(m3_list:39)
     d  m3_klaf                       1    overlay(m3_list:49)
     d  m3_lenh                       5    overlay(m3_list:50)
     d  m3_grpr_kr                    9  0 overlay(m3_list:56)
     d  m3_grpr_Øre                   2  2 overlay(m3_list:66)
     d  m3_prod                      15    overlay(m3_list:68)
     d  m3_pvar                      20    overlay(m3_list:83)
      *
     d m4_list                       92
     d  m4_rect                       3    overlay(m4_list:1)
     d  m4_bran                      15    overlay(m4_list:4)
     d  m4_nobb                      20    overlay(m4_list:19)
     d  m4_vgrp                      10    overlay(m4_list:39)
     d  m4_modn                       9    overlay(m4_list:49)
     d  m4_ldor                      15    overlay(m4_list:58)
     d  m4_lvar                      20    overlay(m4_list:73)
      *
     d m5_list                       38
     d  m5_rect                       3    overlay(m5_list:1)
     d  m5_pbok                       1    overlay(m5_list:4)
     d  m5_pgrp                       9    overlay(m5_list:5)
     d  m5_pugr                       5    overlay(m5_list:14)
     d  m5_sort                      20    overlay(m5_list:19)
      *
     d                 ds
     d m6_list                      167
     d  m6_rect                       3    overlay(m6_list:1)
     d  m6_pdat                       8    overlay(m6_list:16)
     d  m6_s1pr                      12    overlay(m6_list:24)
     d  m6_s2pr                      12    overlay(m6_list:36)
     d  m6_s3pr                      12    overlay(m6_list:48)
     d  m6_s4pr                      12    overlay(m6_list:60)
     d  m6_s1im                      12    overlay(m6_list:72)
     d  m6_s2im                      12    overlay(m6_list:84)
     d  m6_s3im                      12    overlay(m6_list:96)
     d  m6_s4im                      12    overlay(m6_list:108)
     d  m6_lapr                      12    overlay(m6_list:120)
     d  m6_grpr_kr                    9  0 overlay(m6_list:132)
     d  m6_grpr_Øre                   2  2 overlay(m6_list:142)
     d  m6_inpr_kr                    9  0 overlay(m6_list:144)
     d  m6_inpr_Øre                   2  2 overlay(m6_list:154)
     d  m6_kopr                      12    overlay(m6_list:156)
      *
     d                 ds
     d m7_list                      118
     d  m7_rect                       3    overlay(m7_list:1)
     d  m7_enh1                       5    overlay(m7_list:4)
     d  m7_ean1                      13  0 overlay(m7_list:9)
     d  m7_bred                       6  0 overlay(m7_list:22)
     d  m7_leng                       6  0 overlay(m7_list:28)
     d  m7_hoyd                       6  0 overlay(m7_list:34)
     d  m7_vekt                       9  0 overlay(m7_list:40)
     d  m7_volu_hel                   8  0 overlay(m7_list:49)
     d  m7_volu_des                   5  5 overlay(m7_list:58)
     d  m7_ofp2_hel                   8  0 overlay(m7_list:63)
     d  m7_ofp2_des                   5  5 overlay(m7_list:72)
     d  m7_ofp3_hel                   8  0 overlay(m7_list:77)
     d  m7_ofp3_des                   5  5 overlay(m7_list:86)
     d  m7_ofp4_hel                   8  0 overlay(m7_list:91)
     d  m7_ofp4_des                   5  5 overlay(m7_list:100)
     d  m7_ofp5_hel                   8  0 overlay(m7_list:105)
     d  m7_ofp5_des                   5  5 overlay(m7_list:114)
      *
     d                 ds
     d m8_list                      131
     d  m8_rect                       3    overlay(m8_list:1)
     d  m8_enh2                       5    overlay(m8_list:4)
     d  m8_ean2                      13  0 overlay(m8_list:9)
     d  m8_ofv2_hel                   8  0 overlay(m8_list:22)
     d  m8_ofv2_des                   5  5 overlay(m8_list:31)
     d  m8_enh3                       5    overlay(m8_list:36)
     d  m8_ean3                      13  0 overlay(m8_list:41)
     d  m8_ofv3_hel                   8  0 overlay(m8_list:54)
     d  m8_ofv3_des                   5  5 overlay(m8_list:63)
     d  m8_enh4                       5    overlay(m8_list:68)
     d  m8_ean4                      13  0 overlay(m8_list:73)
     d  m8_ofv4_hel                   8  0 overlay(m8_list:86)
     d  m8_ofv4_des                   5  5 overlay(m8_list:95)
     d  m8_enh5                       5    overlay(m8_list:100)
     d  m8_ean5                      13  0 overlay(m8_list:105)
     d  m8_ofv5_hel                   8  0 overlay(m8_list:118)
     d  m8_ofv5_des                   5  5 overlay(m8_list:127)
      *
     d m9_list                       15
     d  m9_rect                       3    overlay(m9_list:1)
     d  m9_ksan                       1    overlay(m9_list:4)
     d  m9_kutg                       1    overlay(m9_list:5)
     d  m9_enhi                       5    overlay(m9_list:6)
     d  m9_enhs                       5    overlay(m9_list:11)
      *
     d                 ds
     d k1_list                       79
     d  k1_rect                       3    overlay(k1_list:1)
     d  k1_kamp                      20    overlay(k1_list:4)
     d  k1_fdat                       8    overlay(k1_list:24)
     d  k1_tdat                       8    overlay(k1_list:32)
     d  k1_besk                      40    overlay(k1_list:40)
      *
     d                 ds
     d k2_list                      218
     d  k2_rect                       3    overlay(k2_list:1)
     d  k2_vare                      20    overlay(k2_list:4)
     d  k2_kamp                      20    overlay(k2_list:24)
     d  k2_ldor                      15    overlay(k2_list:44)
     d  k2_fdat                       8    overlay(k2_list:59)
     d  k2_tdat                       8    overlay(k2_list:67)
     d  k2_s1pr_kr                    9  0 overlay(k2_list:75)
     d  k2_s1pr_Øre                   2  2 overlay(k2_list:85)
     d  k2_s2pr                      12    overlay(k2_list:87)
     d  k2_s3pr                      12    overlay(k2_list:99)
     d  k2_s4pr                      12    overlay(k2_list:111)
     d  k2_s1im                      12    overlay(k2_list:123)
     d  k2_s2im                      12    overlay(k2_list:135)
     d  k2_s3im                      12    overlay(k2_list:147)
     d  k2_s4im                      12    overlay(k2_list:159)
     d  k2_lapr                      12    overlay(k2_list:171)
     d  k2_grpr                      12    overlay(k2_list:183)
     d  k2_inpr_kr                    9  0 overlay(k2_list:195)
     d  k2_inpr_Øre                   2  2 overlay(k2_list:205)
     d  k2_kopr                      12    overlay(k2_list:207)
      *
      * Definering av variable i nøkler
      *
     d jvarl8_ldor     s                   like(jvldor)
     d jvarl8_lvar     s                   like(jvlvar)
     d jvarl3_nobb     s                   like(jvnobb)
     d fenkl1_enhf     s                   like(feenhf)
     d jvarlu_vare     s                   like(jvvare)
     d jvpklu_vare     s                   like(jwvare)
6.32 d jvpklu_ldor     s                   like(jwldor)
     d jvprlu_vare     s                   like(jxvare)
     d jvprlu_ldor     s                   like(jxldor)
     d jkahl1_kamp     s                   like(jmkamp)
8.01 d jkahl1_prgr     s                   like(jmprgr)
     d jkahlu_kamp     s                   like(jmkamp)
8.01 d jkahlu_prgr     s                   like(jmprgr)
     d jkavlu_vkam     s                   like(jmvkam)
8.01 d jkavlu_vprg     s                   like(jmvprg)
     d jkavlu_vvar     s                   like(jmvvar)
6.31 d jvlelu_lvnr     s                   like(jvlvnr)
6.31 d jvlelu_lldo     s                   like(jvlldo)
      *
      * Definering av variable
      *
     d b_fØrste        s              1
     d b_pris          s              1    inz(*off)
     d b_inlr          s              1
      *
     d w_firm          s              3  0
     d w_rect          s              3
     d w_vtxt          s             71
     d w_posi          s              2  0
     d w_dato_6        s              6  0
     d w_ogrp_alf      s              2
     d w_hgrp_alf      s              2
     d w_ugrp_alf      s              3
     d w_ldor_4        s              4
      *
     d w_ogrp          s                   like(jvogrp)
     d w_hgrp          s                   like(jvhgrp)
     d w_ugrp          s                   like(jvugrp)
     d w_adat          s                   like(jvadat)
     d w_udat          s                   like(jvudat)
     d w_gdat          s                   like(jxgdat)
     d w_nobb          s                   like(jvnobb)
     d w_modn          s                   like(jvmodn)
     d w_ldor          s                   like(jvldor)
     d w_lvar          s                   like(jvlvar)
     d w_stat          s                   like(jvstat)
     d w_enhe          s                   like(jwenhe)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      b_fØrste = *on
      *
      * Leser fil
      *
     c                   read      jrvapf                                 90
     c                   dow       *in90 = *off
      *
     c                   eval      w_rect = %subst(jrvrad:1:3)
      *
      * Kaller oppdateringsprogram ved brudd på vare
      *
     c                   if        w_rect = '161' and
     c                             m1_list <> *blank and
     c                             b_fØrste = *off
      *
     c                   exsr      oppdater
      *
     c                   eval      m1_list = *blank
     c                   eval      m2_list = *blank
     c                   eval      m3_list = *blank
     c                   eval      m4_list = *blank
     c                   eval      m5_list = *blank
     c                   eval      m6_list = *blank
     c                   eval      m7_list = *blank
     c                   eval      m8_list = *blank
     c                   eval      m9_list = *blank
      *
     c                   endif
      *
      * Tar vare på data i alle record-typer for vare
      *
     c                   select
     c                   when      w_rect  = '161'
     c                   eval      m1_list = jrvrad
     c                   when      w_rect  = '162'
     c                   eval      m2_list = jrvrad
     c                   when      w_rect  = '163'
     c                   eval      m3_list = jrvrad
     c                   when      w_rect  = '164'
     c                   eval      m4_list = jrvrad
     c                   when      w_rect  = '165'
     c                   eval      m5_list = jrvrad
     c                   when      w_rect  = '166'
     c                   eval      m6_list = jrvrad
     c                   when      w_rect  = '167'
     c                   eval      m7_list = jrvrad
     c                   when      w_rect  = '168'
     c                   eval      m8_list = jrvrad
     c                   when      w_rect  = '169'
     c                   eval      m9_list = jrvrad
     c                   endsl
      *
     c                   if        w_rect = '161'
     c                   eval      b_fØrste = *off
     c                   endif
      *
      * Kampanje-hode
      *
     c                   if        w_rect = '181'
     c                   eval      k1_list = jrvrad
     c                   exsr      kamp_hode
     c                   endif
      *
      * Kampanje-vare
      *
     c                   if        w_rect = '191'
     c                   eval      k2_list = jrvrad
     c                   exsr      kamp_vare
     c                   endif
      *
     c                   read      jrvapf                                 90
     c                   enddo
      *
      * Oppdaterer siste vare
      *
     c                   if        m1_list <> *blank and
     c                             b_fØrste = *off
     c                   exsr      oppdater
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av varerecord
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdater      begsr
      *
      * Test på at kun leverandør 9999 leses inn
      *
     c                   if        m3_ldor <> '9999'
     c                   leavesr
     c                   endif
      *
      * Initierer numeriske verdier i 'tomme' records
      *
     c                   if        m3_list = *blank
     c                   eval      m3_grpr_kr  = 0
     c                   eval      m3_grpr_Øre = 0
     c                   endif
      *
     c                   if        m6_list = *blank
     c                   eval      m6_grpr_kr  = 0
     c                   eval      m6_grpr_Øre = 0
     c                   eval      m6_inpr_kr  = 0
     c                   eval      m6_inpr_Øre = 0
     c                   endif
      *
     c                   if        m7_list = *blank
     c                   eval      m7_ean1     = 0
     c                   eval      m7_bred     = 0
     c                   eval      m7_leng     = 0
     c                   eval      m7_hoyd     = 0
     c                   eval      m7_vekt     = 0
     c                   eval      m7_volu_hel = 0
     c                   eval      m7_volu_des = 0
     c                   eval      m7_ofp2_hel = 0
     c                   eval      m7_ofp2_des = 0
     c                   eval      m7_ofp3_hel = 0
     c                   eval      m7_ofp3_des = 0
     c                   eval      m7_ofp4_hel = 0
     c                   eval      m7_ofp4_des = 0
     c                   eval      m7_ofp5_hel = 0
     c                   eval      m7_ofp5_des = 0
     c                   endif
      *
     c                   if        m8_list = *blank
     c                   eval      m8_ean2     = 0
     c                   eval      m8_ofv2_hel = 0
     c                   eval      m8_ofv2_des = 0
     c                   eval      m8_ean3     = 0
     c                   eval      m8_ofv3_hel = 0
     c                   eval      m8_ofv3_des = 0
     c                   eval      m8_ean4     = 0
     c                   eval      m8_ofv4_hel = 0
     c                   eval      m8_ofv4_des = 0
     c                   eval      m8_ean5     = 0
     c                   eval      m8_ofv5_hel = 0
     c                   eval      m8_ofv5_des = 0
     c                   endif
      *
      * Oversetter "numeriske" alfa-felter til numerisk felt
      *
     c                   eval      w_adat = *loval
     c                   eval      w_udat = *loval
     c                   eval      w_gdat = *loval
      *
     c                   eval      w_ogrp_alf = %subst(m1_vgrp:1:2)
     c                   eval      w_hgrp_alf = %subst(m1_vgrp:3:2)
     c                   eval      w_ugrp_alf = %subst(m1_vgrp:5:3)
     c                   move      w_ogrp_alf    w_ogrp
     c                   move      w_hgrp_alf    w_hgrp
     c                   move      w_ugrp_alf    w_ugrp
      *
     c                   move      m1_fdat       w_dato_6
     c                   if        w_dato_6 <> 0
     c     *ymd          move      w_dato_6      w_adat
     c                   endif
      *
     c                   move      m1_tdat       w_dato_6
     c                   if        w_dato_6 <> 0
     c     *ymd          move      w_dato_6      w_udat
     c                   endif
      *
     c                   move      m6_pdat       w_dato_6
     c                   if        w_dato_6 <> 0
     c     *ymd          move      w_dato_6      w_gdat
     c                   endif
      *
     c                   movel     m4_nobb       w_nobb
     c                   movel     m4_modn       w_modn
      *
     c                   eval      w_ldor = 0
     c                   movel     m3_ldor       w_ldor_4
     c                   move      w_ldor_4      w_ldor
      *
     c                   eval      w_lvar = m3_lvar
      *
      * Starter oppdatering
      *
     c                   clear                   jvarlur
      *
     c                   eval      b_pris = *off
      *
     c                   eval      jvarl8_ldor = w_ldor
     c                   eval      jvarl8_lvar = w_lvar
     c     jvarl8_key    chain     jvarl8
     c                   if        not %found
      *
     c                   if        w_nobb <> 0
     c                   eval      jvarl3_nobb = w_nobb
     c     jvarl3_key    chain     jvarl3
     c                   if        %found and
     c                             jvldor <> 9999
     c                   eval      b_pris = *on
     c                   endif
     c                   endif
      *
     c                   endif
      *
     c                   if        b_pris = *off
     c                   exsr      vareeier
     c                   else
     c                   exsr      prisgiver
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av Løvenskiold som vareeier
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     vareeier      begsr
6.12  *
6.12 c                   eval      w_enhe = m7_enh1
6.12 c                   eval      fenkl1_enhf = m7_enh1
6.12 c     fenkl1_key    chain     fenkl1                             91
6.12 c                   if        *in91 = *off
6.12 c                   eval      w_enhe = feenht
6.12 c                   endif
      *
     c                   if        %found(jvarl8)
     c                   exsr      endr_vare
     c                   else
     c                   exsr      ny_vare
     c                   endif
      *
     c                   eval      jvarlu_vare = jvvare
     c     jvarlu_key    chain     jvarlur                            91
      *
      * Oppretter eller oppdaterer vare
      *
     c                   eval      jvnobb = w_nobb
     c                   eval      jvmodn = w_modn
     c                   eval      jvogrp = w_ogrp
     c                   eval      jvhgrp = w_hgrp
     c                   eval      jvugrp = w_ugrp
     c                   eval      jvldor = w_ldor
     c                   eval      jvlvar = w_lvar
6.12 c                   eval      jvpenh = w_enhe
     c                   eval      jvean1 = m7_ean1
     c                   eval      jvprod = 0
     c                   eval      jvpvar = *blank
     c                   eval      jvadat = w_adat
     c                   eval      jvudat = w_udat
     c                   eval      jveusr = l_user
     c                   time                    jvetim
     c                   time                    jvedat
      *
      * Venstrestiller varetekster og modultekster, og oversetter ansii
      * til ascii
      *
     c                   eval      jvtek1 = %trim(m2_tek1)
     c                   eval      jvtek2 = %trim(m2_tek2)
      *
     c     x'46':'Æ'     xlate     jvtek1        jvtek1
     c     x'77':'Ø'     xlate     jvtek1        jvtek1
     c     x'2A':'Å'     xlate     jvtek1        jvtek1
      *
     c     x'46':'Æ'     xlate     jvtek2        jvtek2
     c     x'77':'Ø'     xlate     jvtek2        jvtek2
     c     x'2A':'Å'     xlate     jvtek2        jvtek2
      *
      * Konkatinerer varetekst-1 og varetekst-2
      *
     c                   exsr      konkatiner
      *
      * Oppdaterer eller oppretter vare
      *
     c                   if        *in91 = *off
     c                   eval      jvstat = w_stat
     c                   update    jvarlur
     c                   else
     c                   write     jvarlur
     c                   endif
      *
      * Oppretter vare i vare-detalj
      *
     c                   if        *in91 = *on
     c                   clear                   jvdtlur
     c                   eval      jvdfir = w_firm
     c                   eval      jvdvar = jvvare
     c                   eval      jvdhko = 1
     c                   write     jvdtlur
     c                   endif
      *
      * Oppretter vare-pakning
      *
     c                   exsr      oppd_pakning
      *
      * Oppretter vare-pris
      *
     c                   exsr      oppd_pris
      *
      * Oppretter/oppdaterer søketekster
      *
     c                   eval      b_inlr = *on
      *
     c                   call      'JV790R'
     c                   parm                    w_firm
     c                   parm                    jvvare
     c                   parm                    b_inlr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av endret vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Dersom varen allerede finnes i vareregisteret, slettes
      * eksisterende vare-pakninger og vare-priser
      *
     c     endr_vare     begsr
      *
      * Dersom varestatus forskjellig fra 0, 1 og 4 beholdes varestatus
      *
     c                   if        jvstat = 0 or
     c                             jvstat = 1 or
     c                             jvstat = 4
     c                   eval      w_stat = 1
     c                   else
     c                   eval      w_stat = jvstat
     c                   endif
      *
      *
      * Sletter forpakninger som ikke er registert manuelt
      *
     c                   eval      jvpklu_vare = jvvare
6.33 c                   eval      jvpklu_ldor = 9999
6.33 c     jvpklu_key2   setll     jvpklu
6.33 c     jvpklu_key2   reade     jvpklur                                91
     c                   dow       *in91 = *off
6.33 c*                  if        jwrsts = 0
     c                   delete    jvpklur
6.33 c*                  else
6.33 c*                  unlock    jvpklu
6.33 c*                  endif
6.33 c     jvpklu_key2   reade     jvpklur                                91
     c                   enddo
      *
      * Sletter priser som ikke er registrert manuelt
      *
     c                   eval      jvprlu_vare = jvvare
     c     jvprlu_key2   setll     jvprlu
     c     jvprlu_key2   reade     jvprlur                                91
     c                   dow       *in91 = *off
     c                   if        jxrsts = 0
     c                   delete    jvprlur
     c                   else
     c                   unlock    jvprlu
     c                   endif
     c     jvprlu_key2   reade     jvprlur                                91
     c                   enddo
6.31  *
6.31 c                   eval      jvlelu_lvnr = jvvare
6.31 c                   eval      jvlelu_lldo = 9999
6.31 c     jvlelu_key    setll     jvlelu
6.31 c     jvlelu_key    reade     jvlelur
6.31 c                   dow       not %eof
6.31 c                   delete    jvlelur
6.31 c     jvlelu_key    reade     jvlelur
6.31 c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av ny vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ny_vare       begsr
      *
     c                   eval      jvstat = 2
      *
      * Henter neste ledige varenummer, og oppdaterer statusregisteret
      *
     c     jstslu_key    chain     jstslu
     c                   eval      jaavsi = jaavsi + 1
     c                   update    jstslur
      *
     c                   movel     jaavsi        jvvare
      *
      * Opprettelsesdato
      *
     c                   time                    jvodat
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for konkatinering av varetekst-1 og varetekst-2
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     konkatiner    begsr
      *
     c                   eval      w_vtxt = *blank
      *
      * Trimmer varetekst-1
      *
     c     ' '           scan      jvtek1:1      w_posi                   92
     c                   dow       w_posi <> 1
      *
     c                   if        *in92 = *off
     c                   eval      w_posi = 36
     c                   endif
      *
     c                   eval      w_vtxt = %trim(w_vtxt) + ' ' +
     c                                      %subst(jvtek1:1:w_posi-1)
      *
     c                   if        w_posi >= 35
     c                   leave
     c                   endif
      *
     c                   eval      jvtek1 = %trim(
     c                                      %subst(jvtek1:w_posi:36-w_posi))
      *
     c     ' '           scan      jvtek1:1      w_posi                   92
     c                   enddo
      *
      * Trimmer varetekst-2, og konkatinerer denne med varetekt-1
      *
     c                   if        jvtek2 <> *blank
      *
     c     ' '           scan      jvtek2:1      w_posi                   92
     c                   dow       w_posi <> 1
      *
     c                   if        *in92 = *off
     c                   eval      w_posi = 36
     c                   endif
      *
     c                   eval      w_vtxt = %trim(w_vtxt) + ' ' +
     c                                      %subst(jvtek2:1:w_posi-1)
      *
     c                   if        w_posi >= 35
     c                   leave
     c                   endif
      *
     c                   eval      jvtek2 = %trim(
     c                                      %subst(jvtek2:w_posi:36-w_posi))
      *
     c     ' '           scan      jvtek2:1      w_posi                   92
     c                   enddo
      *
     c                   endif
      *
      * Splitter konkatinert varetekst i varetekst-1 og varetekst-2
      *
     c                   eval      jvtek1 = %trim(%subst(w_vtxt:1:35))
     c                   eval      jvtek2 = %trim(%subst(w_vtxt:36:35))
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting av vare-pakning
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_pakning  begsr
      *
     c                   eval      w_enhe = m7_enh1
     c                   eval      fenkl1_enhf = m7_enh1
     c     fenkl1_key    chain     fenkl1                             91
     c                   if        *in91 = *off
     c                   eval      w_enhe = feenht
     c                   endif
      *
     c                   clear                   jvpklur
      *
     c                   eval      jwvare = jvvare
6.32 c                   eval      jwldor = 9999
     c                   eval      jwpakn = '000'
     c                   eval      jwenhe = w_enhe
     c                   eval      jwofak = 1
     c                   eval      jwofap = 1
     c                   eval      jwbred = m7_bred
     c                   eval      jwleng = m7_leng
     c                   eval      jwhoyd = m7_hoyd
     c                   eval      jwvekt = m7_vekt
     c                   eval      jwvolu = m7_volu_hel + m7_volu_des
     c                   eval      jwrsts = 0
     c                   eval      jweusr = l_user
      *
     c                   time                    jwodat
     c                   time                    jwedat
     c                   time                    jwetim
      *
     c                   write     jvpklur
      *
      * Enhet-2
      *
     c                   if        m8_enh2 <> *blank
     c                   eval      w_enhe = m8_enh2
     c                   eval      fenkl1_enhf = m8_enh2
     c     fenkl1_key    chain     fenkl1                             91
     c                   if        *in91 = *off
     c                   eval      w_enhe = feenht
     c                   endif
6.33 c                   eval      jwldor = 9999
     c                   eval      jwpakn = '001'
     c                   eval      jwenhe = w_enhe
6.30 c                   eval      jwgtin = m8_ean2
     c                   eval      jwofak = m8_ofv2_hel + m8_ofv2_des
     c                   eval      jwofap = m7_ofp2_hel + m7_ofp2_des
     c                   write     jvpklur
      *
     c                   endif
      *
      * Enhet-3
      *
     c                   if        m8_enh3 <> *blank
     c                   eval      w_enhe = m8_enh3
     c                   eval      fenkl1_enhf = m8_enh3
     c     fenkl1_key    chain     fenkl1                             91
     c                   if        *in91 = *off
     c                   eval      w_enhe = feenht
     c                   endif
6.33 c                   eval      jwldor = 9999
     c                   eval      jwpakn = '002'
     c                   eval      jwenhe = w_enhe
6.30 c                   eval      jwgtin = m8_ean3
     c                   eval      jwofak = m8_ofv3_hel + m8_ofv3_des
     c                   eval      jwofap = m7_ofp3_hel + m7_ofp3_des
     c                   write     jvpklur
      *
     c                   endif
      *
      * Enhet-4
      *
     c                   if        m8_enh4 <> *blank
     c                   eval      w_enhe = m8_enh4
     c                   eval      fenkl1_enhf = m8_enh4
     c     fenkl1_key    chain     fenkl1                             91
     c                   if        *in91 = *off
     c                   eval      w_enhe = feenht
     c                   endif
6.33 c                   eval      jwldor = 9999
     c                   eval      jwpakn = '003'
     c                   eval      jwenhe = w_enhe
6.30 c                   eval      jwgtin = m8_ean4
     c                   eval      jwofak = m8_ofv4_hel + m8_ofv4_des
     c                   eval      jwofap = m7_ofp4_hel + m7_ofp4_des
     c                   write     jvpklur
      *
     c                   endif
      *
      * Enhet-5
      *
     c                   if        m8_enh5 <> *blank
     c                   eval      w_enhe = m8_enh5
     c                   eval      fenkl1_enhf = m8_enh5
     c     fenkl1_key    chain     fenkl1                             91
     c                   if        *in91 = *off
     c                   eval      w_enhe = feenht
     c                   endif
     c                   eval      jwpakn = '004'
6.33 c                   eval      jwldor = 9999
     c                   eval      jwenhe = w_enhe
6.30 c                   eval      jwgtin = m8_ean5
     c                   eval      jwofak = m8_ofv5_hel + m8_ofv5_des
     c                   eval      jwofap = m7_ofp5_hel + m7_ofp5_des
     c                   write     jvpklur
     c                   endif
6.33  *
6.33  * Oppretter vare leverandør
6.33  *
6.33 c                   clear                   jvlelur
6.33  *
6.33 c                   eval      jvlvnr = jvvare
6.33 c                   movel     jvlvar        jvllva
6.33 c                   eval      jvlldo = 9999
6.33 c                   eval      jvleus = l_user
6.33 c                   eval      jvlous = l_user
6.33  *
6.33 c                   time                    jvloda
6.33 c                   time                    jvleda
6.33 c                   time                    jvloti
6.33 c                   time                    jvleti
6.33 c                   write     jvlelur
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting av vare-pris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_pris     begsr
      *
     c                   clear                   jvprlur
      *
     c                   eval      jxvare = jvvare
     c                   eval      jxldor = jvldor
     c                   eval      jxlvar = jvlvar
     c                   eval      jxpris = m6_inpr_kr + m6_inpr_Øre
     c                   eval      jxvalu = *blank
     c                   eval      jxgdat = w_gdat
     c                   eval      jxtdat = *loval
     c                   eval      jxrsts = 0
     c                   eval      jxeusr = l_user
      *
     c                   time                    jxodat
     c                   time                    jxedat
     c                   time                    jxetim
      *
     c                   if        jxpris = 0
     c                   eval      jxpris = m6_grpr_kr + m6_grpr_Øre
     c                   endif
      *
     c                   if        jxpris = 0
     c                   eval      jxpris = m3_grpr_kr + m3_grpr_Øre
     c                   endif
      *
     c                   write     jvprlur
6.32  *
6.32  * Sletter forpakninger som ikke er registert manuelt
6.32  *
6.32 c                   eval      jvpklu_vare = jvvare
6.32 c                   eval      jvpklu_ldor = 9999
6.32 c     jvpklu_key2   setll     jvpklu
6.32 c     jvpklu_key2   reade     jvpklur                                91
6.32 c                   dow       *in91 = *off
6.33 c*                  if        jwrsts = 0
6.32 c                   delete    jvpklur
6.33 c*                  else
6.33 c*                  unlock    jvpklu
6.33 c*                  endif
6.32 c     jvpklu_key2   reade     jvpklur                                91
6.32 c                   enddo
      *
6.32 c                   eval      w_enhe = m7_enh1
6.32 c                   eval      fenkl1_enhf = m7_enh1
6.32 c     fenkl1_key    chain     fenkl1                             91
6.32 c                   if        *in91 = *off
6.32 c                   eval      w_enhe = feenht
6.32 c                   endif
6.32  *
6.32 c                   clear                   jvpklur
6.32  *
6.32 c                   eval      jwvare = jvvare
9.01 c                   eval      jwldor = 9999
6.32 c                   eval      jwpakn = '000'
6.32 c                   eval      jwenhe = w_enhe
6.32 c                   eval      jwofak = 1
6.32 c                   eval      jwofap = 1
6.32 c                   eval      jwbred = m7_bred
6.32 c                   eval      jwleng = m7_leng
6.32 c                   eval      jwhoyd = m7_hoyd
6.32 c                   eval      jwvekt = m7_vekt
6.32 c                   eval      jwvolu = m7_volu_hel + m7_volu_des
6.32 c                   eval      jwrsts = 0
6.32 c                   eval      jweusr = l_user
6.32  *
6.32 c                   time                    jwodat
6.32 c                   time                    jwedat
6.32 c                   time                    jwetim
6.32 c                   write     jvpklur
6.32  *
6.32  * Enhet-2
6.32  *
6.32 c                   if        m8_enh2 <> *blank
6.32 c                   eval      w_enhe = m8_enh2
6.32 c                   eval      fenkl1_enhf = m8_enh2
6.32 c     fenkl1_key    chain     fenkl1                             91
6.32 c                   if        *in91 = *off
6.32 c                   eval      w_enhe = feenht
6.32 c                   endif
9.01 c                   eval      jwldor = 9999
6.32 c                   eval      jwpakn = '001'
6.32 c                   eval      jwenhe = w_enhe
6.32 c                   eval      jwgtin = m8_ean2
6.32 c                   eval      jwofak = m8_ofv2_hel + m8_ofv2_des
6.32 c                   eval      jwofap = m7_ofp2_hel + m7_ofp2_des
6.32 c                   write     jvpklur
6.32  *
6.32 c                   endif
6.32  *
6.32  * Enhet-3
6.32  *
6.32 c                   if        m8_enh3 <> *blank
6.32 c                   eval      w_enhe = m8_enh3
6.32 c                   eval      fenkl1_enhf = m8_enh3
6.32 c     fenkl1_key    chain     fenkl1                             91
6.32 c                   if        *in91 = *off
6.32 c                   eval      w_enhe = feenht
6.32 c                   endif
6.32 c                   eval      jwldor = 9999
6.32 c                   eval      jwpakn = '002'
6.32 c                   eval      jwenhe = w_enhe
6.32 c                   eval      jwgtin = m8_ean3
6.32 c                   eval      jwofak = m8_ofv3_hel + m8_ofv3_des
6.32 c                   eval      jwofap = m7_ofp3_hel + m7_ofp3_des
6.32 c                   write     jvpklur
6.32  *
6.32 c                   endif
6.32  *
6.32  * Enhet-4
6.32  *
6.32 c                   if        m8_enh4 <> *blank
6.32 c                   eval      w_enhe = m8_enh4
6.32 c                   eval      fenkl1_enhf = m8_enh4
6.32 c     fenkl1_key    chain     fenkl1                             91
6.32 c                   if        *in91 = *off
6.32 c                   eval      w_enhe = feenht
6.32 c                   endif
6.32 c                   eval      jwldor = 9999
6.32 c                   eval      jwpakn = '003'
6.32 c                   eval      jwenhe = w_enhe
6.32 c                   eval      jwgtin = m8_ean4
6.32 c                   eval      jwofak = m8_ofv4_hel + m8_ofv4_des
6.32 c                   eval      jwofap = m7_ofp4_hel + m7_ofp4_des
6.32 c                   write     jvpklur
6.32  *
6.32 c                   endif
6.32  *
6.32  * Enhet-5
6.32  *
6.32 c                   if        m8_enh5 <> *blank
6.32 c                   eval      w_enhe = m8_enh5
6.32 c                   eval      fenkl1_enhf = m8_enh5
6.32 c     fenkl1_key    chain     fenkl1                             91
6.32 c                   if        *in91 = *off
6.32 c                   eval      w_enhe = feenht
6.32 c                   endif
6.32 c                   eval      jwpakn = '004'
6.32 c                   eval      jwldor = 9999
6.32 c                   eval      jwenhe = w_enhe
6.32 c                   eval      jwgtin = m8_ean5
6.32 c                   eval      jwofak = m8_ofv5_hel + m8_ofv5_des
6.32 c                   eval      jwofap = m7_ofp5_hel + m7_ofp5_des
6.32 c                   write     jvpklur
6.32  *
6.32 c                   endif
6.31  *
6.31  * Sletter eventuelt vare-leverandør
6.31  *
6.31 c                   eval      jvlelu_lvnr = jvvare
6.31 c                   eval      jvlelu_lldo = 9999
6.31 c     jvlelu_key    setll     jvlelu
6.31 c     jvlelu_key    reade     jvlelur
6.31 c                   dow       not %eof
6.31 c                   delete    jvlelur
6.31 c     jvlelu_key    reade     jvlelur
6.31 c                   enddo
6.31  *
6.31  * Oppretter vare leverandør
6.31  *
6.31 c                   clear                   jvlelur
6.31  *
6.31 c                   eval      jvlvnr = jvvare
6.31 c                   movel     jvlvar        jvllva
6.31 c                   eval      jvlldo = 9999
6.31 c                   eval      jvleus = l_user
6.31 c                   eval      jvlous = l_user
6.31  *
6.31 c                   time                    jvloda
6.31 c                   time                    jvleda
6.31 c                   time                    jvloti
6.31 c                   time                    jvleti
6.31 c                   write     jvlelur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av Løvenskiold som prisgiver
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     prisgiver     begsr
      *
      * Sletter priser som ikke er registrert manuelt
      *
     c                   eval      jvprlu_vare = jvvare
     c                   eval      jvprlu_ldor = 9999
     c     jvprlu_key    setll     jvprlu
     c     jvprlu_key    reade     jvprlur
     c                   dow       not %eof
     c                   if        jxrsts = 0
     c                   delete    jvprlur
     c                   else
     c                   unlock    jvprlu
     c                   endif
     c     jvprlu_key    reade     jvprlur
     c                   enddo
6.33  *
6.33  * Sletter forpakninger som ikke er registert manuelt
6.33  *
6.33 c                   eval      jvpklu_vare = jvvare
6.33 c                   eval      jvpklu_ldor = 9999
6.33 c     jvpklu_key2   setll     jvpklu
6.33 c     jvpklu_key2   reade     jvpklur                                91
6.33 c                   dow       *in91 = *off
6.33 c*                  if        jwrsts = 0
6.33 c                   delete    jvpklur
6.33 c*                  else
6.33 c*                  unlock    jvpklu
6.33 c*                  endif
6.33 c     jvpklu_key2   reade     jvpklur                                91
6.33 c                   enddo
6.33  *
6.33 c                   eval      jvlelu_lvnr = jvvare
6.33 c                   eval      jvlelu_lldo = 9999
6.33 c     jvlelu_key    setll     jvlelu
6.33 c     jvlelu_key    reade     jvlelur
6.33 c                   dow       not %eof
6.33 c                   delete    jvlelur
6.33 c     jvlelu_key    reade     jvlelur
6.33 c                   enddo
      *
      *
      * Oppretter pris
      *
     c                   clear                   jvprlur
      *
     c                   eval      jxvare = jvvare
     c                   eval      jxldor = 9999
     c                   eval      jxlvar = w_lvar
     c                   eval      jxpris = m6_inpr_kr + m6_inpr_Øre
     c                   eval      jxvalu = *blank
     c                   eval      jxgdat = w_gdat
     c                   eval      jxtdat = *loval
     c                   eval      jxrsts = 0
     c                   eval      jxeusr = l_user
      *
     c                   time                    jxodat
     c                   time                    jxedat
     c                   time                    jxetim
      *
     c                   if        jxpris = 0
     c                   eval      jxpris = m6_grpr_kr + m6_grpr_Øre
     c                   endif
      *
     c                   if        jxpris = 0
     c                   eval      jxpris = m3_grpr_kr + m3_grpr_Øre
     c                   endif
      *
     c                   write     jvprlur
6.33  *
6.33  * Oppretter vare-pakning
6.33  *
6.33 c                   exsr      oppd_pakning
      *
      * Oppretter/oppdaterer søketekster
      *
     c                   eval      b_inlr = *on
      *
     c                   call      'JV790R'
     c                   parm                    w_firm
     c                   parm                    jvvare
     c                   parm                    b_inlr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting/oppdatering av kampanje-hode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kamp_hode     begsr
      *
     c                   if        k1_kamp = *blank
     c                   goto      end_hode
     c                   endif
      *
     c                   if        k1_fdat = '00000000' or
     c                             k1_fdat = *blank
     c                   goto      end_hode
     c                   endif
      *
     c                   if        k1_tdat = '00000000' or
     c                             k1_tdat = *blank
     c                   goto      end_hode
     c                   endif
      *
     c                   clear                   jkahlur
      *
     c                   eval      jkahlu_kamp = k1_kamp
8.01 c                   eval      jkahlu_prgr = *blank
     c     jkahlu_key    chain     jkahlur                            91
      *
     c                   eval      jmbesk = k1_besk
     c                   eval      jmfdat = *loval
     c                   eval      jmtdat = *loval
     c                   eval      jmansv = l_user
     c                   eval      jmeusr = l_user
      *
     c                   time                    jmedat
     c                   time                    jmetim
      *
     c                   move      k1_fdat       w_dato_6
     c                   if        w_dato_6 <> 0
     c     *ymd          move      w_dato_6      jmfdat
     c                   endif
      *
     c                   move      k1_tdat       w_dato_6
     c                   if        w_dato_6 <> 0
     c     *ymd          move      w_dato_6      jmtdat
     c                   endif
      *
     c                   if        *in91 = *off
     c                   update    jkahlur
     c                   else
     c                   eval      jmfirm = w_firm
     c                   eval      jmkamp = k1_kamp
8.01 c                   eval      jmprgr = *blank
     c                   eval      jmodat = *loval
     c                   write     jkahlur
     c                   endif
      *
     c     end_hode      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting/oppdatering av kampanje-vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kamp_vare     begsr
      *
     c                   if        k2_kamp = *blank
     c                   leavesr
     c                   endif
      *
     c                   eval      jkahl1_kamp = k2_kamp
8.01 c                   eval      jkahl1_prgr = *blank
     c     jkahl1_key    chain     jkahl1r
     c                   if        not %found
     c                   leavesr
     c                   endif
      *
     c                   select
     c                   when      %len(%trim(k2_vare)) = 6
     c                   eval      jvarl8_ldor = 9999
     c                   eval      jvarl8_lvar = k2_vare
     c     jvarl8_key    chain     jvarl8
     c                   if        not %found
     c                   leavesr
     c                   endif
     c                   when      %len(%trim(k2_vare)) = 8
     c                   movel     k2_vare       jvarl3_nobb
     c     jvarl3_key    chain     jvarl3
     c                   if        not %found
     c                   leavesr
     c                   endif
     c                   other
     c                   leavesr
     c                   endsl
      *
     c                   clear                   jkavlur
      *
     c                   eval      jkavlu_vkam = k2_kamp
8.01 c                   eval      jkavlu_vprg = *blank
     c                   eval      jkavlu_vvar = jvvare
     c     jkavlu_key    chain     jkavlur                            91
      *
     c                   eval      jmvldo = jvldor
     c                   eval      jmvkai = k2_inpr_kr + k2_inpr_Øre
     c                   eval      jmvkpr = k2_inpr_kr + k2_inpr_Øre
     c                   eval      jmvkaa = k2_s1pr_kr + k2_s1pr_Øre
     c                   eval      jmvkau = 0
     c                   eval      jmveus = l_user
      *
     c                   time                    jmveda
     c                   time                    jmveti
      *
     c                   if        *in91 = *off
     c                   update    jkavlur
     c                   else
     c                   eval      jmvfir = w_firm
     c                   eval      jmvkam = k2_kamp
8.01 c                   eval      jmvprg = *blank
     c                   eval      jmvvar = jvvare
     c                   write     jkavlur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_list
      *
      * Key for oppslag på vare m/lev.varenummer
      *
     c     jvarl8_key    klist
     c                   kfld                    jvarl8_ldor
     c                   kfld                    jvarl8_lvar
      *
      * Key for oppslag på vare m/nobb-nummer
      *
     c     jvarl3_key    klist
     c                   kfld                    jvarl3_nobb
      *
      * Key for oppslag på enhet-konvertering
      *
     c     fenkl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fenkl1_enhf
      *
      * Key for oppdatering av vare
      *
     c     jvarlu_key    klist
     c                   kfld                    jvarlu_vare
      *
      * Key for oppdatering av vare-pakning
      *
     c     jvpklu_key    klist
     c                   kfld                    jvpklu_vare
6.32  *
6.32  * Key for oppdatering av vare-pakning
6.32  *
6.32 c     jvpklu_key2   klist
6.32 c                   kfld                    jvpklu_vare
6.32 c                   kfld                    jvpklu_ldor
      *
      *
      * Key for oppdatering av vare-pris
      *
     c     jvprlu_key    klist
     c                   kfld                    jvprlu_vare
     c                   kfld                    jvprlu_ldor
      *
     c     jvprlu_key2   klist
     c                   kfld                    jvprlu_vare
      *
      * Key for oppdatering av kode/status
      *
     c     jstslu_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på kampanje-hode
      *
     c     jkahl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jkahl1_kamp
8.01 c                   kfld                    jkahl1_prgr
      *
      * Key for oppdatering av kampanje-hode
      *
     c     jkahlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jkahlu_kamp
8.01 c                   kfld                    jkahlu_prgr
      *
      * Key for oppdatering av kampanje-vare
      *
     c     jkavlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jkavlu_vkam
8.01 c                   kfld                    jkavlu_vprg
     c                   kfld                    jkavlu_vvar
6.31  *
6.31  * Key for oppdatering av vare-pris
6.31  *
6.31 c     jvlelu_key    klist
6.31 c                   kfld                    jvlelu_lvnr
6.31 c                   kfld                    jvlelu_lldo
      *
      *
      * Fyller datastruktur fra parameter, og henter firma
      *
     c                   eval      d_list = p_list
     c                   eval      w_firm = d_firm
      *
     c                   endsr
