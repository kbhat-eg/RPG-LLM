     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FR190R
      * Beskrivelse . . : Rabattmatrise, kopiering av avtale - parameter
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       021202 HKO  Nytt program
      *       010703 HKO  Lagt inn valg av rabattkategori
7.xx  * 7.00  010416 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fra06l1    if   e           k disk    rename(ra06pfr:ra06l1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
      *
     ffr190d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_fgrp                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variable i nøkler
      *
     d ra06l1_fkod     s                   like(rafkod)
     d rkunl1_kund     s                   like(rkkund)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(rkfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     b1taga        tag
      *
     c                   exfmt     b1bld
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b1taga
     c                   endsl
      *
      * Validering av input
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1taga
     c                   endif
      *
      * Sletter avtale på rabattkategori/kunde det skal kopieres til
      *
     c                   call      'FR195C'
     c                   parm                    w_firm
     c                   parm                    b1fgrp
     c                   parm                    b1rkat
     c                   parm                    b1kunt
      *
      * Kopierer avtale til rabattkategori/kunde
      *
     c                   call      'FR196C'
     c                   parm                    w_firm
     c                   parm                    b1fgrp
     c                   parm                    b1rkaf
     c                   parm                    b1kunf
     c                   parm                    b1rkat
     c                   parm                    b1kunt
      *
      * Viser bildet på nytt
      *
     c                   eval      b1rkat = 0
     c                   eval      b1kunt = 0
     c                   eval      b1txtt = *blank
     c                   eval      b1navt = *blank
      *
     c                   goto      b1taga
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av spørringer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Rabattkategori-fra
      *
     c                   if        w_afld = 'B1RKAF'
     c                   call      'RA506R'
     c                   parm                    w_firm
     c                   parm                    b1rkaf
     c                   parm                    b1txtf
     c                   leavesr
     c                   endif                                                           .....
      *
      * Kunde-fra
      *
     c                   if        w_afld = 'B1KUNF'
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    b1kunf
     c                   parm                    b1navf
     c                   leavesr
     c                   endif                                                           .....
      *
      * Rabattkategori-til
      *
     c                   if        w_afld = 'B1RKAT'
     c                   if        b1fgrp = *blank or
     c                             b1fgrp = l_fgrp
     c                   call      'RA506R'
     c                   parm                    w_firm
     c                   parm                    b1rkat
     c                   parm                    b1txtt
     c                   leavesr
     c                   else
     c                   call      'FR191C'
     c                   parm                    w_firm
     c                   parm                    b1fgrp
     c                   parm                    b1rkat
     c                   parm                    b1txtt
     c                   leavesr
     c                   endif
     c                   endif                                                           .....
      *
      * Kunde-til
      *
     c                   if        w_afld = 'B1KUNT'
     c                   if        b1fgrp = *blank or
     c                             b1fgrp = l_fgrp
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    b1kunt
     c                   parm                    b1navt
     c                   else
     c                   call      'FR193C'
     c                   parm                    w_firm
     c                   parm                    b1fgrp
     c                   parm                    b1kunt
     c                   parm                    b1navt
     c                   endif
     c                   leavesr
     c                   endif                                                           .....
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
     c                   eval      b1txtf = *blank
     c                   eval      b1navf = *blank
     c                   eval      b1txtt = *blank
     c                   eval      b1navt = *blank
      *
      * Rabattkategori-fra
      *
     c                   if        b1rkaf = 0 and
     c                             b1kunf = 0
     c                   eval      *in31 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   if        b1rkaf <> 0 and
     c                             b1kunf <> 0
     c                   eval      *in32 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   if        b1rkaf <> 0
     c                   eval      ra06l1_fkod = b1rkaf
     c     ra06l1_key    chain     ra06l1
     c                   if        not %found
     c                   eval      *in33 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      b1txtf = raftxt
     c                   endif
      *
      * Kunde-fra
      *
     c                   if        b1kunf <> 0
     c                   eval      rkunl1_kund = b1kunf
     c     rkunl1_key    chain     rkunl1
     c                   if        not %found
     c                   eval      *in34 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      b1navf = rknavn
     c                   endif
      *
      * Rabattkategori-til
      *
     c                   if        b1rkat = 0 and
     c                             b1kunt = 0
     c                   eval      *in35 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   if        b1rkat <> 0 and
     c                             b1kunt <> 0
     c                   eval      *in36 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   if        b1rkat <> 0
     c                   if        b1fgrp = *blank or
     c                             b1fgrp = l_fgrp
     c                   eval      ra06l1_fkod = b1rkat
     c     ra06l1_key    chain     ra06l1
     c                   if        not %found
     c                   eval      *in37 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      b1txtt = raftxt
     c                   else
     c                   call      'FR192C'
     c                   parm                    w_firm
     c                   parm                    b1fgrp
     c                   parm                    b1rkat
     c                   parm                    b_feil
     c                   if        b_feil = *on
     c                   eval      *in37 = *on
     c                   leavesr
     c                   endif
     c                   endif
     c                   endif
      *
      * Kunde-til
      *
     c                   if        b1kunt <> 0
     c                   if        b1fgrp = *blank or
     c                             b1fgrp = l_fgrp
     c                   eval      rkunl1_kund = b1kunt
     c     rkunl1_key    chain     rkunl1
     c                   if        not %found
     c                   eval      *in38 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      b1navt = rknavn
     c                   else
     c                   call      'FR194C'
     c                   parm                    w_firm
     c                   parm                    b1fgrp
     c                   parm                    b1kunt
     c                   parm                    b_feil
     c                   if        b_feil = *on
     c                   eval      *in38 = *on
     c                   leavesr
     c                   endif
     c                   endif
     c                   endif
      *
     c                   if        b1fgrp = *blank or
     c                             b1fgrp = l_fgrp
     c                   if        b1rkaf <> 0 and
     c                             b1rkaf = b1rkat or
     c                             b1kunf <> 0 and
     c                             b1kunf = b1kunt
     c                   eval      *in39 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på rabattkategori
      *
     c     ra06l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra06l1_fkod
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
