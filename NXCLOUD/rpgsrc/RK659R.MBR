     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RK659R                                              *
      * Beskrivelse . . : Oppdatering av antall ganger purret                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      * R5.60     060308  Når generalnota, merkes hver enkelt faktura    HFL  *
6.10  * V6.10     210909  Lagt inn sporingsfelter                        BSA  *
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frkwbl1    if   e           k disk    rename(rkwbpfr:rkwbl1r)
T5.60f                                     prefix(x:1)
T5.60frktrl5    if   e           k disk    rename(rktrpfr:rktrl5r)
     frktrlu    uf a e           k disk    rename(rktrpfr:rktrlur)
     fraa5lu    uf   e           k disk    rename(raa5pfr:raa5lur)
      *
     d                uds
     d  pfirm                300    302  0
     d  pnavn                303    332
     d  puser                333    342
     d  pkdat                343    348  0
     d  praar                349    352  0
     d  prper                353    354  0
     d  pavdf                355    358  0
     d  pavdt                359    362  0
     d  pkatf                363    366  0
     d  pkatt                367    370  0
     d  pself                371    374  0
     d  pselt                375    378  0
     d  pknrf                379    384  0
     d  pknrt                385    390  0
     d  kun                  391    450  0 dim(10)
     d  pknr1                391    396  0
     d  pknr2                397    402  0
     d  pknr3                403    408  0
     d  pknr4                409    414  0
     d  pknr5                415    420  0
     d  pknr6                421    426  0
     d  pknr7                427    432  0
     d  pknr8                433    438  0
     d  pknr9                439    444  0
     d  pknr0                445    450  0
     d  pfdat                451    456  0
     d  plope                457    459  0
     d  ppkod                460    460
     d  ppros                461    463  1
     d  pgrns                464    465  0
     d  pgbyr                466    466
     d  pmini                467    468  0
     d  pbskr                469    469
     d  pegrn                470    470
     d  pajor                471    471
     d  ptext                472    472  0
     d  ppunr                473    473  0
     d  ppbeh                474    474
     d  palfa                475    475
     d  pdfgg                476    481  0
     d  pgeby                482    484  0
     d  pparam               300    490
     d l_user                911    920
     d l_firm                944    946  0
     d l_navn                951    980
      *
     d rktrlu_kund     s                   like(rnkund)
     d rktrlu_bdat     s                   like(rnbdat)
     d rktrlu_tist     s                   like(rntist)
T5.60d rktrl5_samf     s                   like(rnsamf)
      *
     d w_firm          s                   like(rnfirm)
     d w_antp          s                   like(rnantp)
      *
      *  Les posteringer
      *
     c     rkwbl1_key    setll     rkwbl1r                                61
     c     rkwbl1_key    reade     rkwbl1r                                61
      *
     c                   dow       *in61 = *off
      *
      *  Beregn antall ganger purret
      *
E5.60c                   if        xnantp = 9
E5.60c                   eval      w_antp = xnantp
     c                   else
E5.60c                   eval      w_antp = xnantp + 1
     c                   endif
      *
T5.60 * Dersom generalnota, finn  og oppdater enkeltfaktura
      *
T5.60c                   if        xnsamf <> *zero
T5.60c                   exsr      gennot
T5.60c                   goto      neste
T5.60c                   endif
      *
E5.60c                   eval      rktrlu_kund = xnkund
E5.60c                   eval      rktrlu_bdat = xnbdat
E5.60c                   eval      rktrlu_tist = xntist
     c     rktrlu_key    chain     rktrlu
      *
B001 c                   if        %found
      *
      *  Oppdater antall ganger purret
      *
     c                   eval      rnantp = w_antp
      *
      *  Oppdater siste purredato
      *
     c     *dmy          move      pkdat         rnpdat
      *
6.10 c                   time                    rnedat
6.10 c                   time                    rnetim
6.10 c                   eval      rneusr = l_user
 001 c                   update    rktrlur
E001 c                   endif
      *
T5.60c     neste         tag
     c     rkwbl1_key    reade     rkwbl1r                                61
     c                   enddo
      *
      *  Oppdater siste purredato - statuskode 5
      *
     c     raa5lu_key    chain     raa5lu
      *
     c                   if        %found
     c     *dmy          move      pkdat         ra5ida
     c                   update    raa5lur
     c                   endif
      *
     c     slutt         tag
      *
     c                   eval      *inlr = *on
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
T5.60 * Subrutine for oppdatering av fakturaene i en generalnota        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
T5.60c     gennot        begsr
      *
     c                   eval      rktrl5_samf = xnsamf
     c     rktrl5_key    setll     rktrl5r                                62
     c     rktrl5_key    reade     rktrl5r                                62
      *
     c                   dow       *in62 = *off
      *
      * Sjekk om generalnota på riktig år
      *
     c                   if        xnraar <> rnraar
     c                   goto      neste2
     c                   endif
      *
     c                   eval      rktrlu_kund = rnkund
     c                   eval      rktrlu_bdat = rnbdat
     c                   eval      rktrlu_tist = rntist
     c     rktrlu_key    chain     rktrlu
      *
     c                   if        %found
      *
      *  Oppdater antall ganger purret
      *
     c                   eval      rnantp = w_antp
      *
      *  Oppdater siste purredato
      *
     c     *dmy          move      pkdat         rnpdat
      *
6.10 c                   time                    rnedat
6.10 c                   time                    rnetim
6.10 c                   eval      rneusr = l_user
     c                   update    rktrlur
     c                   endif
      *
     c     neste2        tag
     c     rktrl5_key    reade     rktrl5r                                62
     c                   enddo
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      *
     c                   eval      w_firm = l_firm
      *
      * Key for kundeposteringer
      *
     c     rkwbl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for kundeposteringer
      *
     c     rktrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rktrlu_kund
     c                   kfld                    rktrlu_bdat
     c                   kfld                    rktrlu_tist
      *
      * Key for statuskode 5
      *
     c     raa5lu_key    klist
     c                   kfld                    w_firm
      *
T5.60 * Key for kundeposteringer
      *
T5.60c     rktrl5_key    klist
T5.60c                   kfld                    w_firm
T5.60c                   kfld                    rktrl5_samf
      *
     c                   endsr
