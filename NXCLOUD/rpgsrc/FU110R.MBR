     H/TITLE  ASOFAK: Sletting av tilbud
     H DATEDIT(*DMY)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FU110R
      * Beskrivelse . . : Sletting av tilbud
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
6.20  * 6.20  201015 bsa  Ordretypen må være av type tilbud
      *
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     FVOTYL1    IF   E           K DISK    RENAME(VOTYPFR:VOTYL1R)
      *
     FFU110D    CF   E             WORKSTN
      *
     D                 DS
      *                                                      Parameter-inn
     D  LDPREC                 1     64
     D  LDOTYP                 1      2
     D  LDDATO                 3     12d   datfmt(*iso)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *               :  Local Data Area                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     D                UDS
     D  DSSFIR               944    946  0
     D  l_fnav               951    980
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandle bilde for registrering av parametre                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     B1TAG         TAG
      *
      *  Nullstiller variable som skal vises i skjermbildet
      *
     C                   MOVE      *BLANK        B1OTYP
     C                   MOVE      *ZERO         B1DATO
      *
     C     B1TAGA        TAG
      *
     C     B1TAGB        TAG
     C                   EXFMT     B1BLD
      *
      *  F3=Gå tilbake  (Avslutt)
      *
     C     *INKC         CABEQ     *ON           XSLUTT
7.xx  *
7.xx  * Dato må være satt
7.xx  *
7.xx c                   if        b1dato = 0
7.xx c                   eval      *in32 = *on
7.xx c                   goto      b1tagb
7.xx c                   endif
7.xx c     *dmy          test(d)                 b1dato                 32
7.xx c                   if        *in32  = *on
7.xx c                   goto      b1tagb
7.xx c                   endif
      *
      *  Sjekk   Finnes ordretype i ORDRETYPE-REGISTER
      *
     C                   MOVE      B1OTYP        OTYTYP
     C     OTYKEY        CHAIN     VOTYL1R                            90
     C     *IN90         IFEQ      *ON
     C                   MOVE      *ON           *IN31
     C                   GOTO      B1TAGB
     C                   END
6.20  * Må være av type tilbud.
6.20 c                   if        %found  and
6.20 c                             vaoakk <> 0
6.20 c                   eval      *in31 = *on
6.20 c                   goto      b1tagb
6.20 c                   endif
      *
      *  Snur dato, og legger inntastede opplysninger i hjelpe-felter
      *  for parameter
      *
     c                   eval      lddato = *loval
      *
     c                   if        b1dato <> 0
     c     *dmy          move      b1dato        lddato
     c                   endif
      *
     C                   MOVE      B1OTYP        LDOTYP
      *
      *  Dersom dato til=0 skal t.o.m. alle datoer velges
      *
7.xx C*    LDDATO        IFEQ      *loval
7.xx C*                  MOVE      *hival        LDDATO
7.xx C*                  END
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Inntasting er ferdig !  Oppstart av utvalg/utskriftsprogrammer *
      *  Kaller opp neste 'CL'-program                                  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      *  F4=Legge jobb på jobbkø
      *
     C     *INKD         IFEQ      *ON
     C                   MOVE      '1'           WWVALG
     C                   END
      *
      *  Kaller opp subprogram
      *
     C                   CALL      'FU112C'
     C                   PARM                    LDPREC
     C                   PARM                    WWVALG
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     XSLUTT        TAG
      *
     C                   SETON                                        LR
     C                   RETURN
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     *INZSR        BEGSR
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     C                   MOVE      *ZERO         WWVALG            1
      *
      *  Redefinering av felter
      *
      *  Redefinering av nøkler
      *
     C     *LIKE         DEFINE    VAOFIR        OTYFIR
     C     *LIKE         DEFINE    VAOTYP        OTYTYP
      *
      *  Key for file : ORDRE-TYPE-REGISTER
      *
     C     OTYKEY        KLIST
     C                   KFLD                    OTYFIR
     C                   KFLD                    OTYTYP
      *
      *  Legge inn firmanummer i nøkklene
      *
     C                   MOVE      DSSFIR        OTYFIR
      *
     CSR                 ENDSR
