     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : LO802R
      * Beskrivelse . . : Varemottak, linjebehandling
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       240402 ASK  Nytt program
      *       150305 BØ   Justert crt_subile slik at den blar riktig
      *       150307 KOB  Lagt inn støtte for nye etikettyper
      * V5.50 030407 KOB  Lagt inn endret styring av etikettutskrift
5.70  *  PRGR 021208 bof  Utvidet med prisgruppe
      *  6.10 180609 BSA  Oppdatert med sporingsinformasjon
6.11  * 6.10  130709 bof  Utvidet eannr til 14 og brukt std. oppslag
6.12  * 6.10  250909 bof  endret ean fra vvenpf vveppf
6.13  * 6.10  010210 bof  fjernet mulighet til å reg. gtin-nr her.
6.14  * 6.10  210710 bsa  Problemer med mottatte linjer fra radioterminal, hvis
6.14  *                   overstyring av restkode fra LO801.
6.15  * 6.10  151110 bof  justerer eann slik at det blir på rette formen i sjekk
6.20  * 6.20  230511 bsa  Håndterer avvikende enheter ved innmelding
6.21  * 6.20  300511 bsa  Lagt inn F tast for vise logg.
6.22  * 6.20  290811 bof  Utvidet med mulighet for å sende sms til kunde eller
6.22  *                   selger
6.23  * 6.20  180412 bof  Feilretting vedr. sms
6.24  * 6.20  230412 bof  Parameterstyrer om SMS ved varemottak FAAK01
6.25  * 6.20  070512 bsa  Ajourført parameterliste ved kall til SMS program
6.26  * 6.20  020813 bof  Justert sms, hente tlf. fra ordre, så k.proj, så ordre
6.27  * 6.20  230513 sst  SMS også til selger ved varemottak
6.28  * 6.20  030913 sst  Div justeringer vedr sms ved varemottak
6.30  * 6.30  050214 bsa  Legger ut varsel om det finnes erstaningsvarer
6.nu  * 6.30  030614 bof  Utvidelser vedr. nummerutvidelse
6.31  * 6.30  171014 bof  sjekk om best. har sperre kode før man får begynne
6.32  * 6.20  120215 bsa  Sjekk om kunde og selger skal informeres via SMS
6.32  *                   (utvidet sjekkk via forsendelsesmåte)
6.33  * 6.30  290515 bsa  Sjekker kun hvis BM modul
6.34  * 6.20  011015 nho  Minus antall ble ikke varemottat
6.35  * 6.30  010615 bsa  Endret logikk rundt håndtering av forsendelsmåte
6.35  *                   (Skal være muølig med manuell overstyring)
6.36  * 6.30  080517 bsa  Mulighet for å skrive ut kollietiketter
6.37  * 6.30  101018 bof  Utvidet med trelast sortiment
      *
7.xx  * 7.00  090316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 7.00  280320 bkv  Newlook vask
7.02  *  9.01 190607 RHO  Foreslår fast antall 1 på scan. varel.
7.04  *       160621 bof  Sette på switch for å få 7.02
      *
8.01  *K000   100123 bsa  Send transportrequest hvis tilhørende ordre oppfyller
8.01  *K000               krav til kjøring.
8.02  *K000   180123 bsa  Flagg om skaffetekst i transportlogg.
8.03  *K000   060223 bsa  Flagg om sjekk på loggkode i transportlogging
8.04  *PRG2   160622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
9.02  * 6.30  010716 Bsa  Aksepterer 0 i sluttdato som høyeste verdi for
9.02  *                   erstatningsvarer.
9.03  * 6.30  120917 Bsa  Sjekker om erstaningsvare gjelder for dette lageret
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flohelr    if   e           k disk    rename(lohepfr:lohelrr)
     flohelu    uf   e           k disk    rename(lohepfr:lohelur)
     flodtl1    if   e           k disk    rename(lodtpfr:lodtl1r)
     flodtlr    if   e           k disk    rename(lodtpfr:lodtlrr)
     flodtlu    if   e           k disk    rename(lodtpfr:lodtlur)
     flodtlv    if   e           k disk    rename(lodtpfr:lodtlvr)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvarl3    if   e           k disk    rename(vvarpfr:vvarl3r)
     fvvarl4    if   e           k disk    rename(vvarpfr:vvarl4r)
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
6.24 ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
6.22 ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
6.22 fra09l1    if   e           k disk    rename(ra09pfr:ra09l1r)
6.22 frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
6.22 fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
6.28 fmjlol1    if   e           k disk    rename(mjlopfr:mjlol1r)
     flplolu    uf a e           k disk    rename(lplopfr:lplolur)
     flplllu    uf a e           k disk    rename(lpllpfr:lplllur)
     flwvalr    if a e           k disk    rename(lwvapfr:lwvalrr)
     flwvalu    uf a e           k disk    rename(lwvapfr:lwvalur)
5.50 fvfaspf    if a e           k disk    rename(vfaspfr:vfasfpr)
5.50 fvetilr    if a e           k disk    rename(vetipfr:vetilrr)
6.26 ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
6.30 fvverl1    if   e           k disk    rename(vverpfr:vverl1r)
6.32 fra17l1    if   e           k disk    rename(ra17pfr:ra17l1r)
7.04 fvve2l1    if   e           k disk    rename(vve2pfr:vve2l1r)
      *
     flo802d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Parameter-inn
      *
     d                 ds
6.nu d d_prec                        35
     d  d_firm                        3  0 overlay(d_prec)
6.nu d  d_numm                        8  0 overlay(d_prec:4)
6.nu d  d_suff                        2  0 overlay(d_prec:12)
6.nu d  d_ldat                         d   overlay(d_prec:13)
6.nu d  d_lage                        2  0 overlay(d_prec:24)
6.nu d  d_innk                        4  0 overlay(d_prec:26)
6.nu d  d_rest                        1  0 overlay(d_prec:30)
6.nu d  d_etik                        1  0 overlay(d_prec:31)
6.nu d  d_leti                        1  0 overlay(d_prec:32)
6.nu d  d_seti                        1  0 overlay(d_prec:33)
6.nu d  d_motk                        1  0 overlay(d_prec:34)
6.nu d  d_dumm                        1    overlay(d_prec:35)
      *
      * Definering av Local data area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
7.04 d l_filg                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d lohelr_numm     s                   like(lonumm)
     d lohelr_suff     s                   like(losuff)
     d lohelu_numm     s                   like(lonumm)
     d lohelu_suff     s                   like(losuff)
     d lodtl1_numm     s                   like(ldnumm)
     d lodtl1_suff     s                   like(ldsuff)
     d lodtl1_line     s                   like(ldline)
     d lodtlu_numm     s                   like(ldnumm)
     d lodtlu_suff     s                   like(ldsuff)
     d lodtlu_line     s                   like(ldline)
     d lodtlr_numm     s                   like(ldnumm)
     d lodtlr_suff     s                   like(ldsuff)
     d lodtlr_line     s                   like(ldline)
     d lodtlv_vare     s                   like(ldvare)
     d lodtlv_numm     s                   like(ldnumm)
     d lodtlv_suff     s                   like(ldsuff)
     d rlevl1_levr     s                   like(rllevr)
     d vvarl1_vare     s                   like(vvvare)
     d vvarl3_nobb     s                   like(vvnobb)
     d vvarl4_lvar     s                   like(vvlvar)
     d votyl1_otyp     s                   like(vaotyp)
6.22 d fohel1_numm     s                   like(fonumm)
6.22 d ra09l1_ikod     s                   like(raikod)
6.22 d rkunl1_kund     s                   like(rkkund)
     d lplolu_ponr     s                   like(lkponr)
     d lplolu_psuf     s                   like(lkpsuf)
     d lplllu_lonr     s                   like(lklonr)
     d lplllu_lsuf     s                   like(lklsuf)
     d lplllu_llin     s                   like(lkllin)
     d lwvalr_numm     s                   like(mvnumm)
     d lwvalr_suff     s                   like(mvsuff)
     d lwvalr_line     s                   like(mvline)
     d lwvalu_numm     s                   like(mvnumm)
     d lwvalu_suff     s                   like(mvsuff)
     d lwvalu_line     s                   like(mvline)
6.14 d lxvalu_numm     s                   like(mvnumm)
6.14 d lxvalu_suff     s                   like(mvsuff)
5.50 d vetilr_vare     s                   like(vvvare)
6.26 d fkprl1_kund     s                   like(flkund)
6.26 d fkprl1_kpro     s                   like(flkpro)
6.28 d mjlol1_firm     s                   like(mjfirm)
6.28 d mjlol1_lage     s                   like(mjlage)
6.28 d mjlol1_avde     s                   like(mjavde)
6.28 d mjlol1_skaf     s                   like(mjskaf)
6.30 d vverl1_envr     s                   like(vvenvr)
6.32 d ra17l1_qkod     s                   like(raqkod)
7.04 d vve2l1_envr     s                   like(v2envr)
7.04 d vve2l1_egvr     s                   like(v2egvr)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_srrn_w        s                   like(w_srrn)
     d w_seqe          s              1
      *
     d w_firm          s                   like(lofirm)
6.nu d w_prec          s             35
     d w_lage          s                   like(lolage)
     d w_innk          s                   like(loinnk)
     d w_line          s                   like(ldline)
     d w_slin          s                   like(ldline)
     d w_dlin          s                   like(ldline)
     d w_nlin          s                   like(ldline)
     d w_dato          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_latx          s             20
     d w_intx          s             20
     d w_ettx          s             20
     d w_stat          s              1
     d w_etik          s              1  0
     d w_dirk          s              1  0
     d w_lety          s              1  0
6.11 d w_eann2         s             14  0
     d w_rkod          s              1
     d w_vare          s                   like(ldvare)
     d w_sapr          s                   like(ldipny)
     d w_bupr          s                   like(ldipny)
     d w_tipr          s                   like(ldipny)
     d w_kopr          s                   like(ldipny)
     d w_vtek          s             30
     d w_on01          s              2
     d w_on02          s              2
     d w_on03          s              2
     d w_on04          s              2
     d w_on05          s              2
     d w_on06          s              2
5.31 d w_mvak          s              1
5.31 d w_mvtx          s             30
5.31 d w_bpro          s              5  2
5.31 d w_mvap          s              6  2
5.31 d w_mvat          s              5  4
5.31 d w_mvaf          s             10  9
8.04 d w_prgr          s              2
6.11 d w_eann          s             14  0
6.11 d w_eava          s                   like(vvvare)
6.11 d w_eaen          s                   like(vvenhe)
6.11 d w_east          s              1
6.15 d w_eanx          s             14
6.15 d w_leng          s              2  0
6.20 d w_enhb          s              3
6.21 d w_numm          s                   like(lonumm)
6.21 d w_suff          s                   like(losuff)
6.28 d w_smsm          s            320
6.22 d w_ftlf          s             11
6.22 d w_fpsw          s             15
6.26 d w_mobi          s                   like(fomobi)
6.28 d w_dta           s             10
6.28 d w_upd           s              1
6.28 d w_txt           s             70
6.28 d w_txtu          s             70
6.28 d sms_avsender    s             11
6.28 d i               s              2  0
6.28 d w               s             35
6.31 d w_mld1          s             50
6.33 d w_disp          s              1
6.33 d w_msts          s              1
6.33 d w_modu          s             10
6.37 d w_lv_vare       s                   like(vvvare)
6.37 d w_nobb          s                   like(vvnobb)
7.01 d w_len           s              3  0
7.01 d w_teller        s              3  0
8.01 d w_reqs          s              5
8.02 d w_skaf          s              1  0
8.03 d w_logg          s              1  0
      *
6.20 d p_etxt          s             35
6.20 d p_enhi          s              3
6.20 d p_antu          s             11  3
      *
     d b_forn          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_nest          s              1    inz(*off)
     d b_rest          s              1    inz(*off)
     d b_pluk          s              1    inz(*off)
     d b_oppd          s              1    inz(*off)
     d b_avik          s              1    inz(*off)
6.22 d b_smsk          s              1    inz(*off)
6.22 d b_smss          s              1    inz(*off)
6.31 d b_sper          s              1    inz(*off)
6.33 d b_bmpp          s              1    inz(*off)
      *
8.01 d u_flat          s              1    inz(*on)
7.04 d u_704           s              1    inz(*off)                            Endring 7.04
7.04  *
7.04  * Definering av eksterne prg
7.04  * BKV
7.04   dcl-s co402_verdi1  char(1);
7.04   dcl-s co402_verdi2  char(2048);
7.04   DCL-PR CO402R EXTPGM('CO402R');
7.04     p_filgrp            char(3)     const;
7.04     p_firm              packed(3:0) const;
7.04     p_lager             packed(2:0) const;
7.04     p_nokkel            char(50)    const;
7.04     p_verdi1            char(1);
7.04     p_verdi2            char(2048);
7.04   END-PR;
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(12)
6.15 d c_null          c                   '0000000000000'
6.33 d c_modu          c                   'BM_MODUL  '
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste recordnummer på siden
      * w_curn - Current recordnummer på siden
      * w_virn - Siden som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1WIN  - C1 Skjermbilde for registrere varemottak - scanning
      * C2WIN  - C2 Skjermbilde for registrere varemottak
      * E8WIN  - E8 Skjermbilde for avslutningsvalg
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedrutine - finn startverdier og bygg sub-file
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     C/Exec SQL
     C+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR
     C/End-Exec
      *
      * Leser valgt bestilling og leverandør
      *
     c     lohelr_key    chain     lohelr
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      rlevl1_levr = loldor
     c     rlevl1_key    chain     rlevl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
6.31  *
6.31  * sjekk om bestilling er sperre
6.31 c                   exsr      sjekk_sperr
6.31 c                   if        b_sper = *on
6.31 c                   goto      avslutt
6.31 c                   endif
      *
     c                   exsr      best_sperr
      *
      * Overfører aktuelle felter til bilde for linjebehandling
      *
     c                   eval      b1numm = d_numm
     c                   eval      b1suff = d_suff
     c                   eval      b1otyp = lootyp
     c                   eval      b1intx = w_intx
     c                   eval      b1lena = lolena
      *
     c                   eval      w_lage = d_lage
     c                   exsr      sjekk_lager
     c                   eval      b1latx = w_latx
      *
     c                   eval      w_innk = d_innk
     c                   exsr      sjekk_selger
     c                   eval      b1intx = w_intx
      *
     c                   eval      w_etik = d_etik
5.50 c                   if        d_etik = 0
5.50 c                   eval      b1ettx = 'Etikett skrives ikke'
5.50 c                   else
5.50 c                   eval      b1ettx = 'Etikett skrives'
5.50 c                   endif
      *
     c                   if        d_rest = 1
     c                   eval      b1retx = '1=JA '
     c                   else
     c                   eval      b1retx = '0=NEI'
     c                   endif
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      lodtl1_line = 0
     c     lodtl1_key    setll     lodtl1
6.14  * Oppdaterer eventuelle varemottatte linjer fra håndterminal fra
6.14  * restkode fra heading.
6.14 c                   exsr      upd_htlines
     c                   exsr      crt_subfile
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1win
     c                   exsr      forny
     c                   goto      b2taga
     c                   when      *inkh
     c                   exsr      xd8win
     c                   exsr      forny
     c                   goto      b2taga
     c                   when      *inki
     c                   if        b_avik = *off
     c                   eval      b_avik = *on
     c                   else
     c                   eval      b_avik = *off
     c                   eval      w_fcrn = 0
     c                   endif
     c                   exsr      forny
     c                   goto      b2taga
6.21 c                   when      *inkj
6.21 c                   exsr      vis_logg
6.21 c                   goto      b2tagb
     c                   when      *inkm
     c                   exsr      xb13win
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   goto      b2taga
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
      *  Sjekk om oppdatering skal gjøres eller bare lagring
      *
     c                   eval      e8valg = 1
      *
     c     e8win_ut      tag
     c                   exfmt     e8win
      *
     c                   if        *inkc = *on
     c                   goto      b2tagb
     c                   endif
      *
     c                   if        e8valg <> 0 and
     c                             e8valg <> 1
     c                   eval      *in40 = *on
     c                   goto      e8win_ut
     c                   endif
      *
     c                   if        e8valg = 1
     c                   exsr      xd1win
     c                   endif
      *
      *  Kaller opp subprogram for å slette utvalg og blanke merking
      *
     c                   call      'LO402R'
     c                   parm                    w_stat
     c                   parm                    w_firm
     c                   parm                    lonumm
     c                   parm                    losuff
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   if        w_slin <> 0
     c                   eval      lodtl1_line = w_slin
     c                   else
     c                   eval      lodtl1_line = b1line
     c                   endif
     c                   else
     c                   eval      lodtl1_line = 0
     c                   endif
      *
     c     lodtl1_key    setll     lodtl1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
     c                   eval      b_forn = *off
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      w_line = b1line
     c                   eval      w_vare = b1vare
     c                   eval      *in41 = *off
     c                   eval      *in42 = *on
     c                   exsr      xc2win
     c                   eval      w_slin = 0
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1win        begsr
      *
     c                   eval      w_slin = 0
      *
     c     c1taga        tag
      *
     c                   eval      c1vari = *blank
     c                   eval      c1eann = 0
     c                   eval      c1svar = 1
      *
     c                   eval      *in41 = *off
     c                   eval      w_line = 0
     c                   eval      w_dlin = 0
     c                   eval      w_nlin = 0
      *
     c     c1tagb        tag
      *
     c                   exfmt     c1win
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   eval      c1eann = 0
     c                   goto      end_c1win
     c                   when      *inkd
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    c1vari
     c                   parm                    w_vtek
     c                   if        *in41 = *on
     c                   goto      c1tagb
     c                   endif
     c                   endsl
      *
     c                   if        *in41 = *on and
     c                             c1svar = 0
     c                   eval      c1eann = 0
     c                   eval      *in41 = *off
     c                   endif
      *
     c                   if        c1vari = *blank
     c                   goto      end_c1win
     c                   endif
      *
     c                   exsr      finn_vare
     c                   exsr      finn_linje
      *
     c                   if        w_line <> 0
     c                   goto      c1tagc
     c                   endif
      *
     c                   eval      *in40 = *on
     c                   eval      w_rkod = '1'
6.12 c                   move      c1vari        w_eann2
     c                   call      'FÅ900R'
6.11 c                   parm                    w_eann2
     c                   parm                    w_rkod
      *
     c                   if        w_rkod = ' '
     c                   eval      *in41 = *on
     c                   movel     c1vari        c1eann
     c                   eval      c1svar = 1
     c                   endif
      *
     c                   goto      c1tagb
      *
     c     c1tagc        tag
      *
     c                   if        *in41 = *on and
     c                             c1svar = 1
     c*                  exsr      reg_ean
     c                   endif
      *
     c                   exsr      xc2win
     c                   goto      c1taga
      *
     c     end_c1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C2WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2win        begsr
      *
     c     c2taga        tag
     c                   eval      lodtlr_line = w_line
     c     lodtlr_key    chain     lodtlr
     c                   if        not %found
     c                   goto      end_c2win
     c                   endif
      *
     c                   eval      vvarl1_vare = w_vare
6.30  * Sjekk om det er en erstatningsvare
6.30 c                   eval      c2info = *blank
6.33  * Sjekk kun om det er BM modul
6.33 c                   if        b_bmpp = *on
6.30 c                   exsr      sjk_erst
6.33 c                   endif
      *
     c                   exsr      les_vare
     c                   exsr      les_pris
      *
      * Leser mottaksregister
      *
     c                   eval      c2vare = *blank
     c                   eval      c2nobb = 0
     c                   eval      c2lvar = *blank
      *
     c                   eval      lwvalr_line = w_line
     c     lwvalr_key    chain     lwvalr
     c                   if        not %found
     c                   eval      mvlevr = 0
     c                   eval      mvrkod = d_rest
5.50 c                   eval      mvekod = b1ekod
     c                   endif
      *
5.50 c                   eval      w_etik = b1ekod
     c                   exsr      etikett
     c                   eval      c2ekod = w_etik
     c                   eval      c2ettx = w_ettx
      *
     c                   eval      *in43 = *off
     c                   eval      c2vare = vvvare
     c                   eval      c2nobb = vvnobb
     c                   eval      c2lvar = vvlvar
      *
     c                   select
     c                   when      c1eann <> 0
     c                   eval      c2eann = c1eann
     c                   when      mveann <> 0
     c                   eval      c2eann = mveann
     c                   other
     c                   eval      c2eann = w_eann
     c                   endsl
      *
     c                   eval      c2tek1 = ldtek1
     c                   eval      c2tek2 = ldtek2
     c                   eval      c2best = ldanta
     c                   eval      c2levt = mvlevr
     c                   eval      c2rkod = mvrkod
     c                   eval      c2enhb = ldenh1
6.20 c**                 eval      w_enhb = ldenh1
     c                   eval      c2enhl = ldenh1
     c                   eval      c2enhr = ldenh1
     c                   eval      c2enhi = ldenh1
     c                   if        c2rkod = 1
     c                   eval      c2rest = c2best - c2levt
     c                   eval      *in43 = *on
     c                   endif
7.04 c                   if        u_704 = *on
7.04 c                   eval      c2levi = 1
7.04 c                   else
7.04 c                   eval      c2levi = c2best - c2levt
7.04 c                   endif
7.04 c*                  eval      c2levi = 1
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2win
     c                   eval      *in44 = *off
      *
     c                   select
     c                   when      *inkc
     c                   goto      end_c2win
     c                   when      *inkd
6.20 c                   exsr      spØrring
6.20 c**                 exsr      velg_etikett
     c                   goto      c2tagb
     c                   when      *inki
     c                   exsr      finn_linje
     c                   goto      c2taga
     c                   endsl
6.20  * Er enhet endret ?
6.20 c                   if        c2enhb <> c2enhi and
6.20 c                             c2levi <> 0
6.20 c                   exsr      omr_enhet
6.20 c                   endif
      *
     c                   if        c2levi > 999999
     c                   eval      *in45 = *on
     c                   goto      c2tagb
     c                   endif
      *
     c                   eval      lwvalu_line = w_line
     c     lwvalu_key    chain     lwvalur
      *
     c                   if        %found
     c                   eval      mvlevr = mvlevr + c2levi
     c                   eval      mvrkod = c2rkod
     c                   eval      mvekod = c2ekod
     c                   update    lwvalur
     c                   else
     c                   eval      mvfirm = w_firm
     c                   eval      mvnumm = d_numm
     c                   eval      mvsuff = d_suff
     c                   eval      mvline = w_line
     c                   eval      mvvare = c2vare
     c                   eval      mvenhe = c2enhi
     c                   eval      mvlevr = c2levi
     c                   eval      mvrkod = c2rkod
     c                   eval      mvekod = c2ekod
     c                   eval      mvstat = *blank
     c                   write     lwvalur
     c                   endif
      *
     c                   eval      b1ekod = c2ekod
     c                   eval      b_oppd = *on
     c                   eval      b_forn = *on
      *
     c     end_c2win     tag
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å vise statuskoder
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb13win       begsr
      *
     c                   exfmt     b13win
      *
     c     end_xb13win   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for avslutning (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      *in45 = *off
     c                   eval      w_dirk = 1
     c                   eval      d1pakk = 1
     c                   eval      d1meld = 0
     c                   eval      votyl1_otyp = lootyp
     c     votyl1_key    chain     votyl1r
     c                   if        %found
     c                   eval      d1otyp = vaonxt
     c                   eval      w_on01 = vaon01
     c                   eval      w_on02 = vaon02
     c                   eval      w_on03 = vaon03
     c                   eval      w_on04 = vaon04
     c                   eval      w_on05 = vaon05
     c                   eval      w_on06 = vaon06
     c                   else
     c                   eval      d1otyp = *blank
     c                   endif
      *
     c                   if        loornr = 0
     c                   eval      *in45 = *on
     c                   endif
      *
6.22  * sjekk om sms kan sendes
6.22  *
6.22 c                   exsr      sjekk_sms
      *
     c     d1tagb        tag
     c                   exfmt     d1win
      *
      *  F3=Avslutt
      *
     c                   if        *inkc = *on
     c                   goto      end_d1win
     c                   end
      *
      *  F4=Spørring ordretype
      *
     c                   if        *inkd = *on
     c                   call      'VA550R'
     c                   parm                    w_firm
     c                   parm                    d1otyp
     c                   goto      d1tagb
     c                   end
      *
      *  Sjekk gyldig ordretype
      *
     c                   eval      votyl1_otyp = d1otyp
     c     votyl1_key    chain     votyl1r
     c                   if        not %found
     c                   eval      *in40 = *on
     c                   goto      d1tagb
     c                   endif
      *
     c                   if        vaosys <> 1
     c                   eval      *in41 = *on
     c                   goto      d1tagb
     c                   endif
      *
     c                   if        laatyk = 1
     c                   if        d1otyp <> w_on01 and
     c                             d1otyp <> w_on02 and
     c                             d1otyp <> w_on03 and
     c                             d1otyp <> w_on04 and
     c                             d1otyp <> w_on05 and
     c                             d1otyp <> w_on06
     c                   eval      *in42 = *on
     c                   goto      d1tagb
     c                   endif
     c                   endif
      *
      *  Sjekk gyldige svaralternativer
      *
     c                   if        d1pakk > 1
     c                   eval      *in43 = *on
     c                   goto      d1tagb
     c                   endif
      *
     c                   if        d1meld > 1
     c                   eval      *in44 = *on
     c                   goto      d1tagb
     c                   endif
      *
     c                   if        d1pakq <> *blank
     c                   eval      w_stat = *blank
     c                   call      'FQPRTC'
     c                   parm                    w_stat
     c                   parm                    d1pakq
     c                   if        w_stat <> *blank
     c                   eval      *in46 = *on
     c                   goto      d1tagb
     c                   endif
     c                   endif
      *
     c                   if        d1melq <> *blank
     c                   eval      w_stat = *blank
     c                   call      'FQPRTC'
     c                   parm                    w_stat
     c                   parm                    d1melq
     c                   if        w_stat <> *blank
     c                   eval      *in47 = *on
     c                   goto      d1tagb
     c                   endif
     c                   endif
      *
6.22 c                   if         d1smsk = 1
6.22 c                   eval       b_smsk = *on
6.23 c                   else
6.23 c                   eval       b_smsk = *off
6.22 c                   endif
6.22 c                   if         d1smss = 1
6.22 c                   eval       b_smss = *on
6.23 c                   else
6.23 c                   eval       b_smss = *off
6.22 c                   endif
      *
     c                   exsr      skriv_plukk
      *
     c                   if        d1meld = 1
     c                   call      'LO650C'
     c                   parm                    w_firm
     c                   parm                    lonumm
     c                   parm                    losuff
     c                   endif
      *
     c                   if        d1pakk = 1
     c                   call      'LO604R'
     c                   parm                    w_stat
     c                   parm                    w_firm
     c                   parm                    lonumm
     c                   parm                    losuff
     c                   parm                    d1otyp
     c                   parm                    w_dirk
     c                   parm                    d1pakq
     c                   endif
6.22  *
6.22  * tester på sms-sending
6.22  *
6.22 c                   if        b_smsk = *on or
6.22 c                             b_smss = *on
6.35 c                   exsr      finn_mobnr
6.22 c                   exsr      send_sms
6.22 c                   endif
6.36  * Mulighet for å skrive ut kollietiketter
6.36 c                   if        d1kole = 1
6.36 c                   eval      w_numm = loornr
6.36 c                   eval      w_suff = loorsu
6.36 c                   call      'FP270R'
6.36 c                   parm                    w_numm
6.36 c                   parm                    w_suff
6.36 c                   endif
8.01  *
8.01  * Hvis flåtestyring, sjekk om vi skal sende transportrequest.
8.01  *
8.01 c                   if        u_flat = *on and
8.01 c                             loornr <> 0
8.01  *
8.01 c                   eval      w_reqs = *blanks
8.02 c                   eval      w_skaf = 1
8.03 c                   eval      w_logg = 1
8.01 c                   call      'AP058R '
8.01 c                   parm                    w_firm
8.01 c                   parm                    loornr
8.01 c                   parm                    loorsu
8.02 c                   parm                    w_skaf
8.03 c                   parm                    w_logg
8.01 c                   parm                    w_reqs
8.01  *
8.01 c                   endif
      *
     c                   eval      b_oppd = *off
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for hurtig innmelding                        (D8WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd8win        begsr
      *
     c                   exfmt     d8win
      *
      *  F3=Avslutt
      *
     c                   if        *inkc = *on
     c                   goto      end_d8win
     c                   end
      *
     c                   eval      lodtl1_line = 0
     c     lodtl1_key    setll     lodtl1
     c                   read      lodtl1
      *
     c                   dow       not %eof  and
     c                             ldnumm = d_numm and
     c                             ldsuff = d_suff
      *
     c                   eval      lwvalu_line = ldline
     c     lwvalu_key    chain     lwvalur
      *
     c                   eval      mvlevr = ldanta
     c                   eval      mvrkod = d_rest
5.50 c                   eval      mvekod = c2ekod
      *
     c                   if        %found
     c                   update    lwvalur
     c                   else
     c                   eval      mvfirm = w_firm
     c                   eval      mvnumm = d_numm
     c                   eval      mvsuff = d_suff
     c                   eval      mvline = ldline
     c                   eval      mvvare = ldvare
     c                   eval      mvenhe = ldenh1
     c                   write     lwvalur
     c                   endif
      *
     c                   read      lodtl1
      *
     c                   enddo
      *
     c     end_d8win     endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      lodtl1
      *
     c                   if        %eof or
     c                             ldnumm <> d_numm or
     c                             ldsuff <> d_suff
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      lodtl1_line = ldline
      *
     c                   eval      b1valg = 0
     c                   eval      b1line = ldline
     c                   movel     ldvare        b1vare
     c                   eval      b1tek1 = ldtek1
     c                   eval      b1anta = ldanta
     c                   eval      b1enhe = ldenh1
     c                   eval      b1rkod = d_rest
5.50 c*                  eval      b1ekod = d_etik
5.50  *
5.50  * Hent etikettkode hvis etiketter skal skrives
5.50  *
5.50 c                   if        vnmotk = 1
5.50  *
5.50 c                   eval      b1ekod = vnetyp
5.50 c                   eval      vetilr_vare = b1vare
5.50 c     vetilr_key    setll     vetilr
5.50 c     vetilr_key    reade     vetilr                                 95
5.50 c                   dow       *in95 = *off
5.50 c                   eval      b1ekod = vmetyp
5.50 c     vetilr_key    reade     vetilr                                 95
5.50 c                   enddo
5.50  *
5.50 c                   endif
5.50  *
     c                   eval      b1stat = ' '
      *
      * Finn om mottak er registrert
      *
     c                   eval      lwvalr_line = ldline
     c     lwvalr_key    chain     lwvalr
     c                   if        %found
     c                   eval      b1antl = mvlevr
     c                   eval      b1rkod = mvrkod
5.50 c*                  eval      b1ekod = mvekod
     c                   eval      b1stat = mvstat
     c                   if        mvstat = *blank
     c                   if        mvlevr < ldanta
     c                   eval      b1stat = '1'
     c                   endif
     c                   if        mvlevr > ldanta
     c                   eval      b1stat = '2'
     c                   endif
     c                   endif
     c                   else
     c                   eval      b1stat = '1'
     c                   eval      b1antl = 0
     c                   endif
6.30  * Sjekk om det er en erstatningsvare
6.30 c                   if        b1stat = ' ' or
6.30 c                             b1stat = '0'
6.30 c                   eval      w_vare = b1vare
6.33  * Sjekk kun hvis BM modul
6.33 c                   if        b_bmpp = *on
6.30 c                   exsr      sjk_erst
6.33 c                   endif
6.30 c                   endif
      *
     c                   if        b_avik = *on and
     c                             b1stat = *blank
     c                   iter
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
7.01  *  Vasker tegn som newlook ikke tåler
7.01 c                   exsr      newlo_vask
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     lodtl1_key    setgt     lodtl1
     c                   readp     lodtl1
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     lodtl1
      *
     c                   if        %eof or
     c                             ldnumm <> d_numm or
     c                             ldsuff <> d_suff
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   eval      lodtl1_numm = ldnumm
      *
     c                   enddo
      *
     c                   if        %eof
     c     lodtl1_key    setll     lodtl1
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kontroll av lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_lager   begsr
      *
     c                   call      'RS710R'
     c                   parm                    w_stat
     c                   parm                    w_lage
     c                   parm                    w_latx
      *
     c                   if        w_stat <> *blank
     c                   eval      b_feil = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kontroll av innkjøper (selger)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_selger  begsr
      *
     c                   call      'RS709R'
     c                   parm                    w_stat
     c                   parm                    w_innk
     c                   parm                    w_intx
      *
     c                   if        w_stat <> *blank
     c                   eval      b_feil = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å redigere etikett-beskrivelse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     etikett       begsr
      *
     c                   select
     c                   when      w_etik = 0
     c                   eval      w_ettx = 'Ikke etikett       '
     c                   when      w_etik = 1
     c                   eval      w_ettx = '1=Pris             '
     c                   when      w_etik = 2
     c                   eval      w_ettx = '2=Hylle 75 x 28    '
     c                   when      w_etik = 3
     c                   eval      w_ettx = '3=Hylle 100 x 28   '
     c                   when      w_etik = 4
     c                   eval      w_ettx = '4=Reol 202 x 60    '
     c                   when      w_etik = 5
     c                   eval      w_ettx = '5=Hylle 76 x 38    '
     c                   when      w_etik = 6
     c                   eval      w_ettx = '6=Hylle 48 x 38    '
     c                   when      w_etik = 7
     c                   eval      w_ettx = '7=Hylle 100 x 38    '
     c                   when      w_etik = 8
     c                   eval      w_ettx = '8=Reol 200 x 60     '
     c                   when      w_etik = 9
     c                   eval      w_ettx = '9=Hylle 76 x 38 Ark '
     c                   endsl
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne vare på EAN,NOBB,LEV.VNR
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_vare     begsr
      *
      * Sjekk, ean-nummer
      *
6.15 c*                  move      c1vari        w_eann
6.15 c                   eval      w_leng = %len(%trim(c1vari))
6.15 c                   eval      w_eanx = %subst(c_null:1:14-w_leng) +
6.15 c                                      c1vari
6.15 c                   movel     w_eanx        w_eann
6.11 c                   call      'VE710R  '
6.11 c                   parm                    w_firm
6.11 c                   parm                    w_eann
6.11 c                   parm                    w_eava
6.11 c                   parm                    w_eaen
6.11 c                   parm                    w_east
6.11 c                   if        w_east = '1'
6.11 c                   eval      w_vare = w_eava
     c                   goto      end_finn_vare
     c                   endif
6.37  * sjekker om logistikk-vare
6.37 c                   eval      w_lv_vare = *blank
6.37 c                   move      c1vari        w_nobb
6.37 c/exec sql
6.37 c+ select vvlvnr
6.37 c+ into   :w_lv_vare
6.37 c+ from   vvlepf
6.37 c+ where  vvlnob = :w_nobb
6.37 c+ fetch first 1 rows only
6.37 c/end-exec
6.37 c                   if        w_lv_vare <> *blank
6.37 c                   eval      w_vare = w_lv_vare
6.37 c                   goto      end_finn_vare
6.37 c                   endif
      *
      * Sjekk, nobb-nummer
      *
     c                   movel     c1vari        vvarl3_nobb
     c     vvarl3_key    chain     vvarl3r
     c                   if        %found
     c                   eval      w_vare = vvvare
     c                   goto      end_finn_vare
     c                   endif
6.37  * sjekker om logistikk-vare
6.37 c                   eval      w_lv_vare = *blank
6.37 c/exec sql
6.37 c+ select vvlvnr
6.37 c+ into   :w_lv_vare
6.37 c+ from   vvlepf
6.37 c+ where  vvllva = :c1vari
6.37 c+ fetch first 1 rows only
6.37 c/end-exec
6.37 c                   if        w_lv_vare <> *blank
6.37 c                   eval      w_vare = w_lv_vare
6.37 c                   goto      end_finn_vare
6.37 c                   endif
      *
      * Sjekk, leverandørens varenummer
      *
     c                   movel     c1vari        vvarl4_lvar
     c     vvarl4_key    chain     vvarl4r
     c                   if        %found
     c                   eval      w_vare = vvvare
     c                   goto      end_finn_vare
     c                   endif
      *
      * Ingen tilslag - prøver eget varenummer
      *
     c                   eval      w_vare = c1vari
      *
     c     end_finn_vare endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne varelinje på varenummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_linje    begsr
      *
     c                   eval      b_nest = *off
     c                   eval      lodtlv_vare = w_vare
     c     lodtlv_key    setll     lodtlv
     c     lodtlv_key    reade     lodtlv
      *
     c                   dow       not %eof
      *
     c                   if        ldline > w_dlin and
     c                             b_nest = *off
     c                   eval      w_line = ldline
     c                   eval      w_dlin = ldline
     c                   eval      b_nest = *on
     c                   endif
      *
     c                   if        ldline > w_dlin
     c                   eval      w_nlin = ldline
     c                   eval      *in44 = *on
     c                   leave
     c                   endif
      *
     c     lodtlv_key    reade     lodtlv
      *
     c                   enddo
      *
     c                   eval      w_slin = w_line
      *
     c     end_linje     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å skrive plukkfiler og etiketter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_plukk   begsr
      *
     c                   eval      b_rest = *off
     c                   eval      b_pluk = *off
     c                   eval      lodtl1_line = 0
     c     lodtl1_key    setll     lodtl1
     c                   read      lodtl1
      *
     c                   dow       not %eof  and
     c                             ldnumm = d_numm and
     c                             ldsuff = d_suff
      *
      * Leser innmeldt og skriver plukklinje
      *
     c                   eval      lwvalr_line = ldline
     c     lwvalr_key    chain     lwvalr
     c                   if        not %found
     c                   eval      mvlevr = 0
     c                   eval      mvrkod = d_rest
     c                   eval      mvekod = 0
     c                   endif
      *
      *  Tekstlinje plukket?
      *
     c                   if        ldvare = *blank
     c                   eval      mvlevr = 1
     c                   endif
      *
     c                   if        mvrkod = 1 or
     c                             mvrkod <> d_rest or
6.34 c***                          mvlevr > 0
6.34 c                             mvlevr <> 0
     c                   eval      b_pluk = *on
     c                   eval      lklfir = w_firm
     c                   eval      lklonr = d_numm
     c                   eval      lklsuf = d_suff
     c                   eval      lkllin = ldline
     c                   eval      lklant = mvlevr
      *
     c                   if        mvrkod = 0
     c                   eval      lklkod = '5'
     c                   else
     c                   eval      lklkod = *blank
     c                   endif
      *
     c                   if        ldvare = *blank
     c                   eval      lklkod = '1'
     c                   eval      lklant = 0
     c                   endif
      *
     c                   write     lplllur
     c                   endif
      *
     c                   if        mvrkod = 1
     c                   eval      b_rest = *on
     c                   endif
     c                   if        mvrkod = 1 and
     c                             mvlevr <> ldanta
     c                   eval      b_rest = *on
     c                   endif
      *
      * Sletter linje fra innmeldingsfil
      *
     c                   eval      lwvalu_line = ldline
     c     lwvalu_key    chain     lwvalu
     c                   if        %found
     c                   delete    lwvalur
     c                   endif
      *
      * Utskrift av etikett
      *
     c                   if        mvekod > 0 and
     c                             mvekod < 8
     c                   call      'FP251C'
     c                   parm                    mvvare
     c                   parm                    mvekod
     c                   endif
      *
     c                   read      lodtl1
     c                   enddo
      *
      * Skriv plukk heading
      *
     c                   if        b_pluk = *on
     c                   eval      lkpfir = w_firm
     c                   eval      lkponr = d_numm
     c                   eval      lkpsuf = d_suff
     c                   eval      lkpoty = d1otyp
     c                   eval      lkplag = d_lage
     c                   eval      lkpwsd = l_wsid
     c                   eval      lkpusr = l_user
     c                   eval      lkpdir = 1
     c                   eval      lkpres = 1
      *
     c                   write     lplolur
     c                   endif
      *
     c     end_plukk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å lese vareinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_vare      begsr
      *
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   eval      vvlvar = *blanks
     c                   eval      w_eann = 0
     c                   goto      end_vare
     c                   endif
6.11  *
6.11 c                   call      'VE711R'
6.11 c                   parm                    w_firm
6.11 c                   parm                    w_vare
6.11 c                   parm                    ldenh1
6.11 c                   parm                    w_eann
6.11 c                   parm                    w_east
6.11 c                   if        w_east = *blank
6.11 c                   eval      w_eann = 0
     c                   endif
      *
     c     end_vare      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å lese prisinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_pris      begsr
      *
     c                   exsr      hent_prisgr
      *
     c                   eval      w_lety = 0
     c                   call      'VP710R'
     c                   parm                    w_firm
     c                   parm                    vvvare
5.70 c                   parm                    w_prgr
     c                   parm                    w_lety
     c                   parm                    w_dato
     c                   parm                    w_time
     c                   parm                    c2enh1
     c                   parm                    c2prie
     c                   parm                    w_bupr
     c                   parm                    w_tipr
     c                   parm                    w_kopr
5.31  *
5.31  * Beregner pris inkl. mva
5.31  *
     c                   if        vvkmva = 0
     c                   eval      w_mvak = '1'
     c                   else
     c                   eval      w_mvak = '0'
     c                   endif
      *
5.31 c                   call      'RS205R'
5.31 c                   parm                    w_stat
5.31 c                   parm                    w_mvak
5.31 c                   parm                    w_mvtx
5.31 c                   parm                    w_dato
5.31 c                   parm                    w_mvap
5.31 c                   parm                    w_mvat
5.31 c                   parm                    w_mvaf
5.31 c                   parm                    w_bpro
5.31  *
     c                   eval      c2prii = c2prie * w_mvat
      *
     c     end_pris      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av søk på etikettyper
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     velg_etikett  begsr
      *
     c                   call      'LO599R'
     c                   parm                    w_firm
     c                   parm                    c2ekod
     c                   parm                    c2ettx
      *
     c     end_velg      endsr
6.31  *
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  * Subrutine for å sjekke om bestilling er sperret
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  *
6.31 c     sjekk_sperr   begsr
6.31  *
6.31 c                   eval      b_sper = *off
6.31  *
6.31 c                   if        lokode <> 0
6.31 c                   eval      b_sper = *on
6.31 c                   eval      w_mld1 = 'Bestilling er i bruk av annen -
6.31 c                             bruker'
6.31 c                   call      'AA005R'
6.31 c                   parm                    w_mld1
6.31 c                   endif
6.31  *
6.31 c                   endsr
6.31  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sperring av bestilling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     best_sperr    begsr
      *
     c     lohelu_key    chain     lohelur
     c                   if        not %found
     c                   goto      end_sperr
     c                   endif
      *
     c                   eval      lokode = 3
     c                   eval      lokous = l_user
     c                   eval      lokows = l_wsid
     c                   time                    lokoda
     c                   time                    lokoti
6.10 c                   time                    loedat
6.10 c                   time                    loetim
6.10 c                   eval      loeusr = l_user
     c                   update    lohelur
      *
     c     end_sperr     endsr
      *
5 70  *
5.70  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5 70  * Subrutine for å hente prisgruppe
5.70  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.70  *
5.70 c     hent_prisgr   begsr
5.70  *
5.70 c                   call      'VL712R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    ldlage
5.70 c                   parm                    ldavde
5.70 c                   parm                    w_prgr
5.70  *
5.70 c                   endsr
      *
6.14  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6 14  * Oppdaterer restlinjer fra heading
6.14  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.14  *
6.14 c     upd_htlines   begsr
6.14  *
6.14  * Finn om mottak er registrert
6.14  *
6.14 c                   eval      lwvalr_line = *zero
6.14 c     lwvalr_key    setll     lwvalu
6.14 c     lxvalu_key    reade     lwvalu                                 95
5.50 c                   dow       *in95 = *off
5.50 c                   eval      mvrkod = d_rest
5.50 c                   update    lwvalur
5.50 c     lxvalu_key    reade     lwvalu                                 95
5.50 c                   enddo
6.14  *
6.14 c                   endsr
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  * Subrutine for behandling av spørring
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  *
6.20 c     spØrring      begsr
6.20  *
6.20  * Hvis enhet, skal vi hente inn denne
6.20  *
6.20 c                   if        w_afld = 'C2ENHI'
6.20 c                   eval      p_enhi = c2enhi
6.20 c                   call      'VE550R'
6.20 c                   parm                    w_firm
6.20 c                   parm                    c2vare
6.20 c                   parm                    p_enhi
6.20 c                   parm                    p_etxt
6.20 c                   eval      c2enhi = p_enhi
6.20  * Regn om til angitt enhet
6.20 c                   if        c2enhi <> c2enhb and
6.20 c                             c2levi = c2best
6.20 c                   eval      p_antu = 0
6.20 c                   call      'VA770R'
6.20 c                   parm                    w_firm
6.20 c                   parm                    c2vare
6.20 c                   parm                    c2enhb
6.20 c                   parm                    c2levi
6.20 c                   parm                    c2enhi
6.20 c                   parm                    p_antu
6.20 c                   eval      c2levi = p_antu
6.20 c                   endif
6.20  *
6.20 c                   goto      end_spØrr
6.20 c                   else
6.20 c                   exsr      velg_etikett
6.20 c                   goto      end_spØrr
6.20 c                   endif
6.20  *
6.20 c     end_spØrr     endsr
6.20  *
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  * Subrutine for behandling av spørring
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  *
6.20 c     omr_enhet     begsr
6.20  *
6.20  * Skal vi regne om ?
6.20  *
6.20 c                   if        c2enhi = *blanks
6.20 c                   eval      c2enhi = c2enhb
6.20 c                   goto      end_enhet
6.20 c                   endif
6.20  * Regn om til bestilt enhet hvis det avvikende enheter
6.20 c                   eval      p_antu = 0
6.20 c                   call      'VA770R'
6.20 c                   parm                    w_firm
6.20 c                   parm                    c2vare
6.20 c                   parm                    c2enhi
6.20 c                   parm                    c2levi
6.20 c                   parm                    c2enhb
6.20 c                   parm                    p_antu
6.20  *
6.20 c                   eval      c2levi = p_antu
6.20  *
6.20 c     end_enhet     endsr
6.20  *
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  * Subrutine for vise logg
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  *
6.21 c     vis_logg      begsr
6.21  *
6.21  *  Call program for loggkoder
6.21  *
6.21 c                   eval      w_numm = d_numm
6.21 c                   eval      w_suff = d_suff
6.21  *
6.21 c                   call      'LM500R'
6.21 c                   parm                    w_firm
6.21 c                   parm                    w_numm
6.21 c                   parm                    w_suff
6.21  *
6.21 c                   endsr
6.21  *
6.22  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.22  * Subrutine for å sjekke om sms kan sendes for kunde og/eller selger
6.22  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.22  *
6.22 c     sjekk_sms     begsr
6.22  *
6.23 c                   eval      d1smsk = 0
6.23 c                   eval      d1smss = 0
6.26 c                   eval      w_mobi = *blank
6.22  *
6.22 c                   if        loornr <> 0
6.26  * finner mob.nr
6.24 c                   if        faak01  = 1
6.32 c                   eval      b_smsk = *off
6.32 c                   eval      b_smss = *off
6.22 c                   eval      fohel1_numm = loornr
6.22 c     fohel1_key    chain     fohel1
6.22 c                   if        %found(fohel1)
6.32 c                   exsr      sjk_fors
6.32  * Sjekk om forsendelsesmåte på ordren trigger SMS
6.32 c                   if        b_smsk = *off
6.32 c                   goto      no_sms
6.32 c                   endif
6.35 c                   endif
6.35 c**                 if        fomobi <> *blank
6.35 c**                 eval      w_mobi = fomobi
6.26 c                   eval      d1smsk = 1
6.35 c**                 goto      mob_funnet
6.35 c**                 endif
6.26  * kundeprosjekt
6.35 c**                 if        fokpro <> 0
6.35 c**                 eval      fkprl1_kund = fokund
6.35 c**                 eval      fkprl1_kpro = fokpro
6.35 c**   fkprl1_key    chain     fkprl1
6.35 c**                 if        %found(fkprl1) and
6.35 c**                           fltlfm <> *blank
6.35 c**                 eval      w_mobi = fltlfm
6.35 c**                 eval      d1smsk = 1
6.35 c**                 goto      mob_funnet
6.35 c**                 endif
6.35 c**                 endif
6.22  * kunde
6.35 c**                 eval      rkunl1_kund = fokund
6.35 c**   rkunl1_key    chain     rkunl1
6.35 c**                 if        %found(rkunl1) and
6.35 c**                           rkmobn <> *blank
6.35 c**                 movel     rkmobn        w_mobi
6.35 c**                 eval      d1smsk = 1
6.35 c**                 endif
6.26  *
6.35 c**   mob_funnet    tag
6.26  *
6.32 c                   if        b_smss = *on
6.35 c**                 eval      ra09l1_ikod = foselg
6.35 c**   ra09l1_key    chain     ra09l1
6.35 c**                 if        %found(ra09l1) and
6.35 c**                           raimob <> *blank
6.23 c                   eval      d1smss = 1
6.35 c**                 endif
6.32 c                   endif
6.32  *
6.35 c**                 endif
6.32  *
6.32 c     no_sms        tag
6.32  *
6.24 c                   endif
6.22 c                   endif
6.22  *
6.22 c                   endsr
6.22  *
6.22  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.22  * Subrutine for å sende sms
6.22  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.22  *
6.22 c     send_sms      begsr
6.22  *
6.28 c                   if        b_smsk = *on or
6.28 c                             b_smss = *on
6.28 c                   eval      w_upd  = ' '
6.28 c                   eval      e1txt1 = *blank
6.28 c                   eval      e1txt2 = *blank
6.28 c                   eval      e1txt6 = *blank
6.28 c                   eval      e1txt7 = *blank
6.28 c                   eval      w_dta  = 'LO802C_' + %trim(%char(lolage))
6.28 c                   eval      *in48  = *on
6.28 c     SendTagA      tag
6.28 c                   call      'LO802C'
6.28 c                   parm                    w_dta
6.28 c                   parm                    e1txt1
6.28 c                   parm                    e1txt2
6.28 c                   parm                    e1txt6
6.28 c                   parm                    e1txt7
6.28 c                   parm                    sms_avsender
6.28 c                   eval      w_txt = %trim(e1txt1) +
6.28 c                                     %trim(e1txt2)
6.28 c     'ordre'       scan      w_txt         i
6.28 c                   if        i = 0
6.28 c     'Ordre'       scan      w_txt         i
6.28 c                   endif
6.28 c                   if        i = 0
6.28 c     'ORDRE'       scan      w_txt         i
6.28 c                   endif
6.28 c                   if        i > 0
6.28 c                   eval      w_txtu =
6.28 c                             %subst(w_txt:1:i+5) +
6.28 c                             %trim(%char(fonumm)) + ' ' +
6.28 c                             %trim(%subst(w_txt:i+6:70-i-5))
6.28 c     next_i        tag
6.28 c     '  '          scan      w_txtu        i
6.28 c                   if        %subst(w_txtu:i:70-i+1) <> *blank
6.28 c                   eval      %subst(w_txtu:i+1:70-i) =
6.28 c                             %subst(w_txtu:i+2:70-i-1)
6.28 c                   eval      %subst(w_txtu:70:1) = *blank
6.28 c                   goto      next_i
6.28 c                   endif
6.28 c                   eval      e1txt1 = %subst(w_txtu:1:35)
6.28 c                   eval      e1txt2 = %subst(w_txtu:36:35)
6.28 c                   endif
6.28 c                   if        e1txt7 = *blank
6.28 c                   eval      mjlol1_firm = lofirm
6.28 c                   eval      mjlol1_lage = lolage
6.28 c                   eval      mjlol1_avde = loavde
6.28 c                   eval      mjlol1_skaf = *blank
6.28 c     mjlol1_key    chain     mjlol1
6.28 c                   if        not %found(mjlol1)
6.28 c                   eval      mjlol1_avde = *zero
6.28 c     mjlol1_key    chain     mjlol1
6.28 c                   if        not %found(mjlol1)
6.28 c                   eval      mjlol1_lage = *zero
6.28 c                   endif
6.28 c                   endif
6.28  *
6.28 c                   if        %found(mjlol1) and
6.28 c                             mjsted <> *blank
6.28 c                   eval      e1txt7 = 'Mvh ' +%trim(mjsted)
6.28 c                   endif
6.28 c                   if        e1txt7 = *blank
6.28 c                   eval      e1txt7 = 'Mvh ' +%trim(aafnav)
6.28 c                   endif
6.28 c                   endif
6.28 c                   eval      kunmob = w_mobi
6.28 c                   eval      slgmob = raimob
6.28  *
6.28 c                   exfmt     e1win
6.28  *
6.28 c                   if        *inkc = *on
6.28 c                   goto      endsms
6.28 c                   endif
6.28 c*                  if        *inkm = *on
6.28 c*                  eval      *in48 = *off
6.28 c*                  goto      SendTagA
6.28 c*                  endif
6.28 c*                  if        *inkn = *on
6.28 c*                  eval      *in48 = *off
6.28 c*                  eval      w_upd = 'J'
6.28 c*                  goto      SendTagA
6.28 c*                  endif
6.28  *
6.28 c                   eval      w_smsm = e1txt1
6.28 c                   if        e1txt2 <> *blank
6.28 c                   eval      w_smsm = %trim(w_smsm) +
6.28 c                                      %trim(e1txt2)
6.28 c                   endif
6.28 c                   if        e1txt3 <> *blank
6.28 c                   eval      w_smsm = %trim(w_smsm) + ' ' +
6.28 c                                      %trim(e1txt3)
6.28 c                   endif
6.28 c                   if        e1txt4 <> *blank
6.28 c                   eval      w_smsm = %trim(w_smsm) + ' ' +
6.28 c                                      %trim(e1txt4)
6.28 c                   endif
6.28 c                   if        e1txt5 <> *blank
6.28 c                   eval      w_smsm = %trim(w_smsm) + ' ' +
6.28 c                                      %trim(e1txt5)
6.28 c                   endif
6.28 c                   if        e1txt6 <> *blank
6.28 c                   eval      w_smsm = %trim(w_smsm) + ' ' +
6.28 c                                      %trim(e1txt6)
6.28 c                   endif
6.28 c                   if        e1txt7 <> *blank
6.28 c                   eval      w_smsm = %trim(w_smsm) + ' ' +
6.28 c                                      %trim(e1txt7)
6.28 c                   else
6.28 c                   eval      w_smsm = %trim(w_smsm) + ' ' +
6.28 c                             'Mvh ' + %trim(aafnav)
6.28 c                   endif
6.28  *
6.28 c                   if        w_upd = 'J'
6.28 c                   call      'LO802C'
6.28 c                   parm                    w_dta
6.28 c                   parm                    e1txt1
6.28 c                   parm                    e1txt2
6.28 c                   parm                    e1txt6
6.28 c                   parm                    e1txt7
6.28 c                   parm                    sms_avsender
6.28 c                   endif
6.28  *
6.28 c*    'Æ':X'40'     xlate     w_smsm        w_smsm
6.28 c*    'æ':X'40'     xlate     w_smsm        w_smsm
6.28 c*    'Ø':X'E0'     xlate     w_smsm        w_smsm
6.28 c*    'ø':X'40'     xlate     w_smsm        w_smsm
6.28 c*    'Å':X'9F'     xlate     w_smsm        w_smsm
6.28 c*    'å':X'40'     xlate     w_smsm        w_smsm
6.28  *
6.28 c                   eval      w_ftlf = sms_avsender
     c                   eval      w_fpsw = *blank
6.28  *
6.22 c                   if        b_smsk = *on and
6.28 c                             kunmob <> *blank
6.25 c                   call      'AF725R '
6.22 c                   parm                    w_ftlf
6.22 c                   parm                    w_fpsw
6.28 c                   parm                    kunmob
6.22 c                   parm                    w_smsm
6.22 c                   endif
6.22  *
6.22 c                   if        b_smss = *on  and
6.28 c                             slgmob <> *blank
6.28 c                   call      'AF725R '
6.22 c                   parm                    w_ftlf
6.22 c                   parm                    w_fpsw
6.28 c                   parm                    slgmob
6.22 c                   parm                    w_smsm
6.22 c                   endif
6.21  *
6.22 c                   endif
6.21  *
6.28 c     endsms        endsr
6.21  *
      *
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30  * Subrutine for å sjekke erstatningsvare
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30  *
6.30 c     sjk_erst      begsr
6.30  *
6.30 c                   eval      vverl1_envr = w_vare
6.30 c     vverl1_key    chain     vverl1
6.30 c                   if        %found
7.03 c                   if        vvsidt <> *loval
6.30 c                   if        vvantb < 3 and
6.30 c                             w_dato >= vvfidt and
6.30 c                             w_dato <= vvsidt
6.30 c                   eval      b1stat = 'E'
6.30 c                   eval      c2info = 'Erstatningsvare'
6.30 c                   endif
7.03 c                   else
7.03 c                   if        vvantb < 3 and
7.03 c                             w_dato >= vvfidt
7.03 c                   eval      b1stat = 'E'
7.03 c                   eval      c2info = 'Erstatningsvare'
7.03 c                   endif
7.03 c                   endif
7.04  * Sjekk om erstatningsvare gjelder for lager
7.04 c                   eval      vve2l1_envr = vvenvr
7.04 c                   eval      vve2l1_egvr = vvegvr
7.04 c     vve2l1_key    chain     vve2l1
7.04 c                   if        %found   and
7.04 c                             v2elag <> w_lage
7.04 c                   eval      c2info = *blanks
7.04 c                   eval      b1stat = *blanks
7.04 c                   endif
7.04  *
6.30 c                   endif
6.30  *
6.30 c                   endsr
6.30  *
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  * Subrutine for sjekke forsendelsesmåte
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  *
6.32 c     sjk_fors      begsr
6.32  *
6.32  *  Sjekk forsendelseskoder
6.32  *
6.32 c                   eval      b_smsk = *off
6.32 c                   eval      b_smss = *off
6.32  *
6.32 c                   eval      ra17l1_qkod = folmat
6.32 c     ra17l1_key    chain     ra17l1
6.32 c                   if        %found
6.32  *
6.29 c                   if        raqsmk = 1
6.29 c                   eval      b_smsk = *on
6.32 c                   endif
6.32 c                   if        raqsms = 1
6.32 c                   eval      b_smss = *on
6.32 c                   endif
6.32  *
6.32 c                   endif
6.32  *
6.32 c                   endsr
6.35  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.35  * Subrutine for å finne mobilnummer
6.35  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.35  *
6.35 c     finn_mobnr    begsr
6.35  *
6.35 c                   eval      w_mobi = *blank
6.35  *
6.35 c                   if        loornr <> 0
6.35  * finner mob.nr
6.35 c                   eval      fohel1_numm = loornr
6.35 c     fohel1_key    chain     fohel1
6.35 c                   if        %found(fohel1)
6.35 c                   if        fomobi <> *blank
6.35 c                   eval      w_mobi = fomobi
6.35 c                   goto      mob_funnet
6.35 c                   endif
6.35  * kundeprosjekt
6.35 c                   if        fokpro <> 0
6.35 c                   eval      fkprl1_kund = fokund
6.35 c                   eval      fkprl1_kpro = fokpro
6.35 c     fkprl1_key    chain     fkprl1
6.35 c                   if        %found(fkprl1) and
6.35 c                             fltlfm <> *blank
6.35 c                   eval      w_mobi = fltlfm
6.35 c                   goto      mob_funnet
6.35 c                   endif
6.35 c                   endif
6.35  * kunde
6.35 c                   eval      rkunl1_kund = fokund
6.35 c     rkunl1_key    chain     rkunl1
6.35 c                   if        %found(rkunl1) and
6.35 c                             rkmobn <> *blank
6.35 c                   movel     rkmobn        w_mobi
6.35 c                   endif
6.35 c                   endif
6.35  *
6.35 c     mob_funnet    tag
6.35  *
6.35 c                   if        b_smss = *on
6.35 c                   eval      ra09l1_ikod = foselg
6.35 c     ra09l1_key    chain     ra09l1
6.35 c                   if        %found(ra09l1) and
6.35 c                             raimob <> *blank
6.35 c                   endif
6.35 c                   endif
6.35  *
6.35 c                   endif
6.35  *
6.35 c                   endsr
7.01  *
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  * Newlook vask av data
7.01  *  Fjerner tegn som gjør at Newlook feiltolker
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01   begsr newlo_vask;
7.01     for w_teller = 1 to 25;
7.01       w_len = %len(%trim(b1tek1));
7.01       if (w_len > 0);
7.01       b1tek1 = %ScanRpl('.':'':b1tek1:w_len); // newlook tåler ikke . på slutten
7.01       endif;
7.01       w_len = %len(%trim(b1tek1));
7.01       if (w_len > 0);
7.01       b1tek1 = %ScanRpl(':':'':b1tek1:w_len); // newlook tåler ikke : på slutten
7.01       endif;
7.01     endfor ;
7.01   endsr;
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    w_prec
     c                   eval      d_prec = w_prec
      *
      *  Key for les av BESTILLING
      *
     c     lohelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelr_numm
     c                   kfld                    lohelr_suff
      *
      *  Key for oppdatering av BESTILLING
      *
     c     lohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelu_numm
     c                   kfld                    lohelu_suff
      *
      *  Key for les av BESTILLINGSLINJER
      *
     c     lodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtl1_numm
     c                   kfld                    lodtl1_suff
     c                   kfld                    lodtl1_line
6.30  *
6.30  *  Key for les av BESTILLINGSLINJER
6.30  *
6.30 c     lodtl1_key2   klist
6.30 c                   kfld                    w_firm
6.30 c                   kfld                    lodtl1_numm
6.30 c                   kfld                    lodtl1_suff
      *
      *  Key for oppdatering av BESTILLINGSLINJER
      *
     c     lodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlu_numm
     c                   kfld                    lodtlu_suff
     c                   kfld                    lodtlu_line
      *
      *  Key for les av BESTILLINGSLINJER
      *
     c     lodtlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlr_numm
     c                   kfld                    lodtlr_suff
     c                   kfld                    lodtlr_line
      *
      * Key for posisjonering på varenummer
      *
     c     lodtlv_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlv_vare
     c                   kfld                    lodtlv_numm
     c                   kfld                    lodtlv_suff
      *
      *  Key for les av LEVERANDØR
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for oppslag på eget varenummer
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på nobb-nummer
      *
     c     vvarl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl3_nobb
      *
      *  Key for oppslag på leverandørens varenr
      *
     c     vvarl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl4_lvar
      *
      * Key for oppslag på ordre-type
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
6.22  *
6.22  * Key for oppslag på ordre
6.22  *
6.22 c     fohel1_key    klist
6.22 c                   kfld                    w_firm
6.22 c                   kfld                    fohel1_numm
6.22  *
6.22  * Key for oppslag på selger
6.22  *
6.22 c     ra09l1_key    klist
6.22 c                   kfld                    w_firm
6.22 c                   kfld                    ra09l1_ikod
6.22  *
6.22  * Key for les av firma opplysning
6.22  *
6.22 c     afirl1_key    klist
6.22 c                   kfld                    w_firm
6.22  *
6.22  * Key for oppslag på kunder
      *
6.22 c     rkunl1_key    klist
6.22 c                   kfld                    w_firm
6.22 c                   kfld                    rkunl1_kund
      *
      *  Key for UTPLUKK-PARAM
      *
     c     lplolu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lplolu_ponr
     c                   kfld                    lplolu_psuf
      *
      *  Key for UTPLUKK-LINJE
      *
     c     lplllu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lplllu_lonr
     c                   kfld                    lplllu_lsuf
     c                   kfld                    lplllu_llin
      *
      * Key for les STATUS
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
6.24  *
6.24  * Key for les STATUS ofak
6.24  *
6.24 c     fstsl1_key    klist
6.24 c                   kfld                    w_firm
      *
      *  Key for les av arbeidsregister
      *
     c     lwvalr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lwvalr_numm
     c                   kfld                    lwvalr_suff
     c                   kfld                    lwvalr_line
      *
      *  Key for oppdatering av arbeidsregister
      *
     c     lwvalu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lwvalu_numm
     c                   kfld                    lwvalu_suff
     c                   kfld                    lwvalu_line
6.14  *
6.14  *  Key for oppdatering av arbeidsregister
6.14  *
6.14 c     lxvalu_key    klist
6.14 c                   kfld                    w_firm
6.14 c                   kfld                    lxvalu_numm
6.14 c                   kfld                    lxvalu_suff
5.50  *
5.50  * Key for faste data etiketter
5.50  *
5.50 c     vfaspf_key    klist
5.50 c                   kfld                    w_firm
5.50  *
5.50  * Key for etiketter
5.50  *
5.50 c     vetilr_key    klist
5.50 c                   kfld                    w_firm
5.50 c                   kfld                    vetilr_vare
6.28  *
6.28  * Key for lagerlokasjon
6.28  *
6.28 c     mjlol1_key    klist
6.28 c                   kfld                    mjlol1_firm
6.28 c                   kfld                    mjlol1_lage
6.28 c                   kfld                    mjlol1_avde
6.28 c                   kfld                    mjlol1_skaf
6.26  *
6.26  * Key for kundeprosjekt
6.26  *
6.26 c     fkprl1_key    klist
6.26 c                   kfld                    w_firm
6.26 c                   kfld                    fkprl1_kund
6.26 c                   kfld                    fkprl1_kpro
6.30  *
6.30  * Key for erstatningsvarer
6.30  *
6.30 c     vverl1_key    klist
6.30 c                   kfld                    w_firm
6.30 c                   kfld                    vverl1_envr
6.32  *
6.32  * Key for forsendelsesmåte
6.32  *
6.32 c     ra17l1_key    klist
6.32 c                   kfld                    w_firm
6.32 c                   kfld                    ra17l1_qkod
7.04  *
7.04  * Key for erstatningsvarer - tilleggsregister
7.04  *
7.04 c     vve2l1_key    klist
7.04 c                   kfld                    w_firm
7.04 c                   kfld                    vve2l1_envr
7.04 c                   kfld                    vve2l1_egvr
      *
      * Henter verdier fra parametre og LDA
      *
     c                   eval      lohelr_numm = d_numm
     c                   eval      lohelr_suff = d_suff
     c                   eval      lohelu_numm = d_numm
     c                   eval      lohelu_suff = d_suff
     c                   eval      lodtl1_numm = d_numm
     c                   eval      lodtl1_suff = d_suff
     c                   eval      lodtlr_numm = d_numm
     c                   eval      lodtlr_suff = d_suff
     c                   eval      lodtlv_numm = d_numm
     c                   eval      lodtlv_suff = d_suff
     c                   eval      lodtlu_numm = d_numm
     c                   eval      lodtlu_suff = d_suff
      *
     c                   eval      lwvalr_numm = d_numm
     c                   eval      lwvalr_suff = d_suff
     c                   eval      lwvalu_numm = d_numm
     c                   eval      lwvalu_suff = d_suff
6.14 c                   eval      lxvalu_numm = d_numm
6.14 c                   eval      lxvalu_suff = d_suff
      *
     c                   eval      w_firm = l_firm
     c                   time                    w_dato
     c                   time                    w_time
     c                   eval      *in22 = *on
6.33  * Sjekk om du har tilgang til BM modulen.
6.33 c                   eval      b_bmpp = *on
6.33 c                   eval      w_disp = '0'
6.33 c                   eval      w_msts = '0'
6.33 c                   eval      w_modu = c_modu
6.33 c                   call      'AX700R'
6.33 c                   parm                    w_modu
6.33 c                   parm                    w_disp
6.33 c                   parm                    w_msts
6.33  *
6.33 c                   if        w_msts = '1'
6.33 c                   eval      b_bmpp = *off
6.33 c                   endif
      *
      * Hent opplysninger fra LAGER-STATUS-KODER
      *
     c     lstsl1_key    chain     lstsl1r                            90
6.24  *
6.24  * Hent opplysninger fra OFAK-STATUS-KODER
6.24  *
6.24 c     fstsl1_key    chain     fstsl1r                            90
6.22  *
6.22  * Hent firmaopplysninger
6.22  *
6.22 c     afirl1_key    chain     afirl1r                            90
      *
5.50  * Henter std. etikettype
5.50  *
5.50 c     vfaspf_key    chain     vfaspf
      *
7.04  * Test om gavekort fra shop er lov
7.04  *
7.04   CallP CO402R(l_filg:w_firm:0:'LO802R_704':
7.04                    co402_verdi1:co402_verdi2);
7.04   if co402_verdi1 = '1';
7.04     u_704  = *on;
7.04   endif;
8.01  *
8.01  * Endring 8.01 - Koblet til ekstern flåtestyring
8.01  *
8.01   CallP CO402R(l_filg:w_firm:0:'EKSTERN_FLATESTYRING':
8.01                    co402_verdi1:co402_verdi2);
8.01   if co402_verdi1 = '1';
8.01     u_flat = *on;
8.01   endif;
     c                   endsr
