      /TITLE  export av xml-fil   - parameter
     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : JX800R
      * Beskrivelse . . : Export av xml-fil       - parameter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.
      * --------- ------  --------------------------------------------------
      *           210108  Nytt program                             BOF
7.xx  * 7.00  030117 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  BRUK AV INDIKATORER
      *
      *       KC = F3  = Exit
      *
      *       LR = Avslutt program
      *       30 = Protect av felter ved vise
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer
      *    70-79 = Arbeidsindikatorer i sub-rutiner
      *    80-89 = Arbeidsindikatorer ordinære
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fvshel1    if   e           k disk    rename(vshepfr:vshel1r)
      *
     fjx800d    cf   e             workstn
      *
      *  Local Data Area
      *
     d                uds
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      *  Parameter-rec
      *
     d                 ds
     d p_prec                       128
     d  p_fogr                        2  0 overlay(p_prec:5)
     d  p_togr                        2  0 overlay(p_prec:7)
     d  p_fhgr                        2  0 overlay(p_prec:9)
     d  p_thgr                        2  0 overlay(p_prec:11)
     d  p_fugr                        3  0 overlay(p_prec:13)
     d  p_tugr                        3  0 overlay(p_prec:16)
     d  p_levn                        6  0 overlay(p_prec:19)
     d  p_vsor                       10    overlay(p_prec:29)
     d  p_mapp                       10    overlay(p_prec:39)
      *
      * Definering av variable i nøkler
      *
     d jlevl1_ldor     s                   like(jlldor)
     d vshel1_vsor     s                   like(vcvsor)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_in03          s              1
     d w_latx          s             20
     d w_letx          s             30
     d w_otxt          s             35
     d w_htxt          s             35
     d w_utxt          s             35
     d w_satx          s             30
     d w_parm          s            128
     d w_vsor          s             10
     d w_navn          s             30
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * A1BLD Behandle bilde for valg av utskriftskriteria
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     a1taga        tag
      *
     c                   exfmt     a1bld
      *
      * CMD3 = Avslutt
      *
     c                   if        *inkc = *on
     c                   eval      w_in03 = '1'
     c                   goto      end_pgm
     c                   endif
      *
      * CMD4 = Spørring
      *
     c                   if        *inkd = *on
     c                   exsr      sp_input
     c                   goto      a1taga
     c                   endif
      *
      *
     c                   if        a1fogr > a1togr
     c                   eval      *in32 = *on
     c                   goto      a1taga
     c                   endif
      *
      *  Sjekk, Finnes leverandør?
      *
     c                   if        a1ldor > 0
     c                   eval      jlevl1_ldor = a1ldor
     c     jlevl1_key    chain     jlevl1r                            90
     c                   if        *in90 = *on
     c                   eval      *in35 = *on
     c                   goto      a1taga
     c                   endif
     c                   endif
      *
      *  Sjekk, Finnes varesortiment ?
      *
     c                   if        a1vsor <> *blank
     c                   eval      vshel1_vsor = a1vsor
     c     vshel1_key    chain     vshel1
     c                   if        not %found(vshel1)
     c                   eval      *in36 = *on
     c                   goto      a1taga
     c                   endif
     c                   endif
      *
     c                   if        a1mapp = *blank
     c                   eval      *in37 = *on
     c                   goto      a1taga
     c                   endif
      *
      * Skriv parameter til Local
      *
     c                   eval      w_in03 = *blank
     c                   eval      p_fogr = a1fogr
     c                   eval      p_togr = a1togr
     c                   eval      p_fhgr = a1fhgr
     c                   eval      p_thgr = a1thgr
     c                   eval      p_fugr = a1fugr
     c                   eval      p_tugr = a1tugr
     c                   eval      p_levn = a1ldor
     c                   eval      p_vsor = a1vsor
     c                   eval      p_mapp = a1mapp
     C                   eval      w_parm = p_prec
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for spørring på input A1BLD
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sp_input      begsr
      *
      *  Spørring i vareregister
      *
      *
      *  Spørring i overgrupperegister
      *
     c                   if        wactfe = 'A1FOGR'
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    a1fogr
     c                   parm                    w_otxt
     c                   goto      sp_end
     c                   endif
      *
     c                   if        wactfe = 'A1TOGR'
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    a1togr
     c                   parm                    w_otxt
     c                   goto      sp_end
     c                   endif
      *
      *  Spørring i hovedgrupperegister
      *
     c                   if        wactfe = 'A1FHGR'
 001 C                   call      'VG511R'
 001 C                   parm                    w_firm
 001 C                   parm                    a1fogr
 001 C                   parm                    a1fhgr
 001 C                   parm                    w_htxt
     c                   goto      sp_end
     c                   endif
      *
     c                   if        wactfe = 'A1THGR'
     c                   if        a1togr = 99
     c                   eval      a1togr = 0
     c                   eval      a1thgr = 0
     c                   endif
 001 c                   call      'VG511R'
 001 c                   parm                    w_firm
 001 c                   parm                    a1togr
 001 c                   parm                    a1thgr
 001 c                   parm                    w_htxt
     c                   goto      sp_end
     c                   endif
      *
      *  Spørring i undergrupperegister
      *
     c                   if        wactfe = 'A1FUGR'
 001 c                   call      'VG512R'
 001 c                   parm                    w_firm
 001 c                   parm                    a1fogr
 001 c                   parm                    a1fhgr
 001 c                   parm                    a1fugr
 001 c                   parm                    w_utxt
     c                   goto      sp_end
     c                   endif
      *
     c                   if        wactfe = 'A1TUGR'
     c                   if        a1togr = 99
     c                   eval      a1togr = 0
     c                   eval      a1thgr = 0
     c                   eval      a1tugr = 0
     c                   endif
     c                   if        a1thgr = 99
     c                   eval      a1thgr = 0
     c                   eval      a1tugr = 0
     c                   endif
 001 c                   call      'VG512R'
 001 c                   parm                    w_firm
 001 c                   parm                    a1togr
 001 c                   parm                    a1thgr
 001 c                   parm                    a1tugr
 001 c                   parm                    w_utxt
     c                   goto      sp_end
     c                   endif
      *
      *  Spørring på leverandør
      *
     c                   if        wactfe = 'A1LDOR'
     c                   call      'JL500R'
     c                   parm                    a1ldor
     c                   parm                    w_letx
     c                   goto      sp_end
     c                   endif
      * varesortiment
     c                   if        wactfe = 'A1VSOR'
     c                   eval      w_firm      = l_firm
     c                   eval      w_vsor      = a1vsor
     c                   eval      w_navn      = *blank
4.86 c                   CALL      'VC500R  '
     c                   PARM                    w_firm
     c                   PARM                    w_vsor
     c                   PARM                    w_navn
     c                   eval      a1vsor      = w_vsor
     c                   goto      sp_end
     c                   endif
      *
     c     sp_end        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      *  Key for les av LEVERANDØR
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for les av varesortiment
      *
     c     vshel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vshel1_vsor
      *
      * Parametejliste
      *
     c     *entry        plist
     c                   parm                    w_in03
     c                   parm                    w_parm
      *
      * Setter standardverdier for skjermbilde
      *
     c                   eval      a1fogr = 0
     c                   eval      a1togr = 99
     c                   eval      a1fhgr = 0
     c                   eval      a1thgr = 99
     c                   eval      a1fugr = 0
     c                   eval      a1tugr = 999
     c                   eval      a1ldor = 0
     c                   eval      a1vsor = *blank
     c                   eval      w_firm = l_firm
      *
     c                   endsr
