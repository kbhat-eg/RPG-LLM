     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASNAVIOKO
      * Program . . . . : IT016R
      * Beskrivelse . . : Leser leveringsbetingelser og skriver til kø
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       250614 ask  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fra18l1    if   e           k disk    rename(ra18pfr:ra18l1r)
      *
      * Local Data Area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variabler i nøkler
      *
     d ra18l1_rkod     s                   like(rarkod)
      *
      *  Definering av datastrukturer for parameter
      *
     d d_parm          ds
     d  d_kode                        2
     d  d_bes1                       25
     d  d_bes2                       25
     d  d_bes3                       25
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_prog          s             40
     d w_comd          s             20
     d w_parm          s           2048
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_rpgp          s             10
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram - leser alle poster og kaller opp std skriveprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser alle poster på ra18PF
      *
     c                   eval      ra18l1_rkod = 0
     c     ra18l1_key    setll     ra18l1
      *
     c                   read      ra18l1
     c                   dow       not %eof and
     c                             rarfir = w_firm
      *
     c                   eval      d_kode = %triml(%editw(rarkod : ' 0'))
     c                   eval      d_bes1 = rartx1
     c                   eval      d_bes2 = rartx2
     c                   eval      d_bes3 = rartx3
     c                   eval      w_parm = d_parm
      *
     c                   call      'IT001R'
     c                   parm                    w_firm
     c                   parm                    w_prog
     c                   parm                    w_comd
     c                   parm                    w_parm
     c                   parm                    w_rpgp
      *
     c                   read      ra18l1
     c                   enddo
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på valgt kode
      *
     c     ra18l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra18l1_rkod
      *
      * Setter inn parameterverdier
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_prog = 'NexstepImportLeveringsbetingelser'
     c                   eval      w_comd = 'Insert'
     c                   eval      w_rpgp = 'IT016R'
      *
     c                   endsr
