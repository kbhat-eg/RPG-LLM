     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VV115R
      * Beskrivelse . . : Vare, sammenslåing av varer - parameter
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       090206 HKO  Nytt program
6.20  * 6.20  051110 bof  Utvidet med lager på sortiment
6.21  * 6.20  020511 bof  Utvidet varegr/lev.nr på vtilpf
6.30  * 6320  241016 bof  Test på flere kriterier for feil.
7.xx  * 7.00  010416 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
8.01  *       080422 bof  Endret på test om vare finnes.
8.01  *PRG2   220622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvsdtl7    if   e           k disk    rename(vsdtpfr:vsdtl7r)
     fvpdtla    if   e           k disk    rename(vpdtpfr:vpdtlar)
     fvbrkl6    if   e           k disk    rename(vbrkpfr:vbrkl6r)
     ffpril5    if   e           k disk    rename(fpripfr:fpril5r)
     ffrabla    if   e           k disk    rename(frabpfr:frablar)
6.21 fvtill9    if   e           k disk    rename(vtilpfr:vtill9r)
     fanotlr    if   e           k disk    rename(anotpfr:anotlrr)
6.30 fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
6.30 fvvenlr    if   e           k disk    rename(vvenpfr:vvenlrr)
6.30 f                                     prefix(xe:2)
6.30 ffodtlv    if   e           k disk    rename(fodtpfr:fodtlvr)
6.30 flodtlv    if   e           k disk    rename(lodtpfr:lodtlvr)
6.30 fltell3    if   e           k disk    rename(ltelpfr:ltell3r)
      *
     fvv115d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
      *
6.30  * Array
6.30 d a_txt           s             40    dim(15)
      * Definering av variable i nøkler
6.30  *
      *
     d vvarl1_vare     s                   like(vvvare)
6.30 d vvenl1_vare     s                   like(vevare)
6.30 d vvenl1_enhe     s                   like(veenhe)
6.30 d vvenlr_vare     s                   like(vevare)
6.30 d vvenlr_enhe     s                   like(veenhe)
     d anotlr_syst     s                   like(ausyst)
     d anotlr_noid     s                   like(aunoid)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
6.30 d b_feil1         s              1    inz(*off)
6.30 d b_feil2         s              1    inz(*off)
6.30 d b_feil3         s              1    inz(*off)
6.30 d b_feil4         s              1    inz(*off)
      *
     d w_firm          s                   like(vvfirm)
     d w_gvar          s                   like(vvvare)
     d w_nvar          s                   like(vvvare)
     d w_mld1          s             50
6.30 d i               s              3  0
      *
     d s_enhs          s                   like(vvenhs)
     d s_enhl          s                   like(vvenhl)
6.30 d s_enhe          s                   like(vvenhe)
6.30 d s_enh1          s                   like(vvenhe)
6.30 d s_enh2          s                   like(vvenhe)
6.30 d s_enh3          s                   like(vvenhe)
6.30 d s_omre          s                   like(veomre)
6.30 d s_omr1          s                   like(veomre)
6.30 d s_omr2          s                   like(veomre)
6.30 d s_omr3          s                   like(veomre)
6.30 d s_omrl          s                   like(veomre)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send bilde
      *
     c     b1taga        tag
      *
     c                   exfmt     b1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc = *on or *inkl = *on
     c                   goto      avslutt
     c                   when      *inkd = *on
     c                   exsr      spØrring
     c                   goto      b1taga
     c                   endsl
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   eval      b_feil = *off
     c                   goto      b1taga
     c                   endif
      * hvis feil 3 eller 4
     c                   if        b_feil1 = *on or
     c                             b_feil2 = *on or
     c                             b_feil3 = *on or
     c                             b_feil4 = *on
     c                   goto      b1taga
     c                   endif
      *
     c                   exsr      xm1win
     c                   if        b_feil = *on
     c                   eval      b_feil = *off
     c                   goto      b1taga
     c                   endif
      *
      * Sjekker filer som slettes
      *
     c                   exsr      xm1win
     c                   if        b_feil = *on
     c                   eval      b_feil = *off
     c                   goto      b1taga
     c                   endif
      *
      * Kaller oppdateringsprogram
      *
     c                   movel     b1gvar        w_gvar
     c                   movel     b1nvar        w_nvar
      *
     c                   call      'VV116R'
     c                   parm                    w_firm
     c                   parm                    w_gvar
     c                   parm                    w_nvar
      *
      * Viser bildet på nytt
      *
     c                   eval      b1gvar = 0
     c                   eval      b1gtek = *blank
     c                   eval      b1nvar = 0
     c                   eval      b1ntek = *blank
      *
     c                   goto      b1taga
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Gammelt varenr
      *
     c                   if        w_afld = 'B1GVAR'
     c                   movel     b1gvar        w_gvar
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_gvar
     c                   parm                    b1gtek
     c                   movel     w_gvar        b1gvar
     c                   leavesr
     c                   endif
      *
      * Nytt varenr
      *
     c                   if        w_afld = 'B1NVAR'
     c                   movel     b1nvar        w_nvar
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_nvar
     c                   parm                    b1ntek
     c                   movel     w_nvar        b1nvar
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
     c                   eval      b1gtek = *blank
     c                   eval      b1ntek = *blank
      *
      * Gammelt varenummer
      *
     c                   if        b1gvar = 0
     c                   eval      *in31  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   movel     b1gvar        vvarl1_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   eval      *in32  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   eval      b1gtek = vvtek1
      *
     c                   eval      s_enhs = vvenhs
     c                   eval      s_enhl = vvenhl
6.30 c                   eval      s_enhe = vvenhe
      *
      * Nytt varenummer
      *
     c                   if        b1nvar = 0
     c                   eval      *in33  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
6.30 c                   movel     b1nvar        vvarl1_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   eval      *in34 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   eval      b1ntek = vvtek1
      *
      * Kan ikke slå sammen varer dersom det er uoverenstemmelse i
      * statistikkenhet eller lagerenhet
      *
     c                   if        vvenhs <> s_enhs or
     c                             vvenhl <> s_enhl
     c                   eval      *in35 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
6.30  *
6.30  *Nye kriterier
6.30  * 1 Basisenhet på den samme på vare1 og vare2
6.30 c                   if        vvenhe <> s_enhe
6.30 c                   eval      w_mld1 = 'Ulik basisenhet på +
6.30 c                                       varene som slås sammen'
6.30 c                   call      'AA005R'
6.30 c                   parm                    w_mld1
6.30 c                   eval      b_feil1 = *on
6.30 c                   leavesr
6.30 c                   endif
6.30  * 2. Lagerenhet har samme omregningsfaktor på vare en og  to på lagerenhet
6.30  *    gammel vare
6.30 c                   movel     b1gvar        vvenl1_vare
6.30 c                   eval      vvenl1_enhe = s_enhl
6.30 c     vvenl1_key    chain     vvenl1
6.30 c                   if        %found
6.30 c                   eval      s_omrl = veomre
6.30 c                   endif
6.30  *    ny vare
6.30 c                   movel     b1nvar        vvenl1_vare
6.30 c                   eval      vvenl1_vare = vvenhl
6.30 c     vvenl1_key    chain     vvenl1
6.30 c                   if        %found and
6.30 c                             veomre <> s_omrl
6.30 c                   eval      w_mld1 = 'Ulik omregningsfaktor +
6.30 c                                      på samme enhet +
6.30 c                                         '
6.30 c                   call      'AA005R'
6.30 c                   parm                    w_mld1
6.30 c                   eval      b_feil2 = *on
6.30 c                   leavesr
6.30 c                   endif
6.30  * 3. Enheter som ikke finnes på ny vare, kan ikke ligger i
6 30  *    bestilling, ordre eller telling.
6.30 c                   exsr      sjekk_enh
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å vise evt. bilde om sletting av varer for gml varenr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xm1win        begsr
      *
      * sjekker filene
     c                   exsr      sjekk_filer
      *
     c                   eval      b_feil = *on
      *
     c                   exfmt     m1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   eval      b_feil = *off
      *
     c                   endsr
6.30  *
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30  * Sjekker filer
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30  *
6.30 c     sjekk_enh     begsr
6.30  *
6.30  *
6.30 c                   movel     b1gvar        w_gvar
6.30 c                   movel     b1nvar        w_nvar
6.30  *
6.30 c                   eval      vvenl1_vare = w_gvar
6.30 c     vvenl1_key2   setll     vvenl1
6.30 c     vvenl1_key2   reade     vvenl1
6.30 c                   dow       not %eof(vvenl1)
6.30 c                   eval      vvenlr_vare = w_nvar
6.30 c                   eval      vvenlr_enhe = veenhe
6.30 c     vvenlr_key    chain     vvenlr
6.30 c                   if        not %found(vvenlr)
6.30 c                   exsr      sjekk_trans
6.30 c                   else
6.30  * funnet, sjekk omregningsfaktor
6.30 c                   if        veomre <> xeomre
6.30 c                   eval      w_mld1 = 'Enhet ' + veenhe +
6.30 c                                      'har ulik omregningsfaktor'
6.30 c                   call      'AA005R'
6.30 c                   parm                    w_mld1
6.30 c                   eval      b_feil4 = *on
6.30 c                   leavesr
6.30 c                   endif
6.30 c                   endif
6.30 c     vvenl1_key2   reade     vvenl1
6.30 c                   enddo
6.30  *
6.30 c                   endsr
6.30  *
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30  * Sjekker filer
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30  *
6.30 c     sjekk_trans   begsr
6.30  *
6.30 c                   eval      i = 0
6.30 c                   eval      a_txt(*) = *blank
6.30  *
6.30  * Sjekker enhet finnes på ordre, bestilling, telling
6.30  *
6.30 c     key_gml       setll     fodtlv
6.30 c     key_gml       reade     fodtlv
6.30 c                   dow       not %eof(fodtlv)   and
6.30 c                             i < 15
6.30 c                   if        fdenh1 = veenhe
6.30 c                   eval      i = i + 1
6.30 c                   eval      a_txt(i)  = 'Ordre  ' +
6.30 c                                      %char(fdnumm) +
6.30 c                                      %char(fdsuff) +
     c                                      'har enhet ' +
     c                                       veenhe
6.30 c                   endif
6.30 c     key_gml       reade     fodtlv
6.30 c                   enddo
6.30  * best
6.30 c                   if        i < 15
6.30 c     key_gml       setll     lodtlv
6.30 c     key_gml       reade     lodtlv
6.30 c                   dow       not %eof(lodtlv)   and
6.30 c                             i < 15
6.30 c                   if        ldenh1 = veenhe
6.30 c                   eval      i = i + 1
6.30 c                   eval      a_txt(i)  = 'Bestilling' +
6.30 c                                      %char(ldnumm) +
6.30 c                                      %char(ldsuff) +
     c                                      'har enhet ' +
     c                                       veenhe
6.30 c                   endif
6.30 c     key_gml       reade     lodtlv
6.30 c                   enddo
6.30 c                   endif
6.30  * tellebatch
6.30 c                   if        i < 15
6.30 c     key_gml       setll     ltell3
6.30 c     key_gml       reade     ltell3
6.30 c                   dow       not %eof(ltell3)  and
6.30 c                             i < 15
6.30 c                   if        leenh1 = veenhe
6.30 c                   eval      i = i + 1
6.30 c                   eval      a_txt(i)  = 'Telling ' +
6.30 c                                      %trim(lebatc) +
6.30 c                                      %char(lenumm) +
6.30 c                                      %char(lesuff) +
     c                                      'har enhet ' +
     c                                       veenhe
6.30 c                   endif
6.30 c     key_gml       reade     ltell3
6.30 c                   enddo
6.30 c                   endif
6.30  * flytter fra matrise til bilde
6.30 c                   if        i > 0
6.30 c                   eval      n1tx01 = a_txt(1)
6.30 c                   eval      n1tx02 = a_txt(2)
6.30 c                   eval      n1tx03 = a_txt(3)
6.30 c                   eval      n1tx04 = a_txt(4)
6.30 c                   eval      n1tx05 = a_txt(5)
6.30 c                   eval      n1tx06 = a_txt(6)
6.30 c                   eval      n1tx07 = a_txt(7)
6.30 c                   eval      n1tx08 = a_txt(8)
6.30 c                   eval      n1tx09 = a_txt(9)
6.30 c                   eval      n1tx10 = a_txt(10)
6.30 c                   eval      n1tx11 = a_txt(11)
6.30 c                   eval      n1tx12 = a_txt(12)
6.30 c                   eval      n1tx13 = a_txt(13)
6.30  *
6.30 c                   exfmt     n1win
6.30 c                   eval      b_feil3 = *on
6.30 c                   endif
6.30 c                   endsr
6.30  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker filer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_filer   begsr
      *
      *
     c                   movel     b1gvar        w_gvar
     c                   movel     b1nvar        w_nvar
      *
      * Sjekker vare fra sortiments-linje dersom den nye varen finnes
      *
     c     key_ny        chain     vsdtl7
     c                   if        %found
     c     key_gml       setll     vsdtl7
     c     key_gml       reade     vsdtl7r
     c                   if        %found(vsdtl7)
     c                   eval      m1tx01 = 'Varesortiment  ' +
     c                                      %trim(vdvsor)
     c                   endif
     c                   endif
      *
      * Sjekker vare fra påslag-linje dersom den nye varen finnes
      *
     c     key_ny        chain     vpdtla
     c                   if        %found
     c     key_gml       setll     vpdtla
     c     key_gml       reade     vpdtlar
     c                   if        %found(vpdtla)
     c                   eval      m1tx02 = 'Påslag  ' +
     c                                      %trim(vjpasl)
     c                   endif
     c                   endif
      *
      * Sjekker vare fra bruksrett-knytning dersom den nye varen finnes
      *
     c     key_ny        chain     vbrkl6
     c                   if        %found
     c     key_gml       setll     vbrkl6
     c     key_gml       reade     vbrkl6r
     c                   if        %found(vbrkl6)
     c                   eval      m1tx03 = 'Bruksrett  ' +
     c                                      %char(vbalme)
     c                   endif
     c                   endif
      *
      * Sjekker vare fra spesialpris dersom den nye varen finnes
      *
     c     key_ny        chain     fpril5
     c                   if        %found
     c     key_gml       setll     fpril5
     c     key_gml       reade     fpril5r
8.01 c                   if        %found(fpril5)  and
8.01 c                             fpvare = w_gvar
     c                   eval      m1tx04 = 'Spesialpris  ' +
     c                                      %char(fpkund)
     c                   endif
     c                   endif
      *
      * Sjekker vare fra rabattmatrise dersom den nye varen finnes
      *
     c     key_ny        chain     frabla
     c                   if        %found
     c     key_gml       setll     frabla
     c     key_gml       reade     frablar
     c                   if        %found(frabla)
     c                   eval      m1tx05 = 'Rabattmatrise  '
     c                   endif
     c                   endif
      *
      * Sjekker vare fra tibudspris dersom den nye varen finnes
      *
6.21 c     key_ny        chain     vtill9
     c                   if        %found
6.21 c     key_gml       setll     vtill9
6.21 c     key_gml       reade     vtill9r
8.01 c                   if        %found(vtill9) and
8.01 c                             vtvare = w_gvar
     c                   eval      m1tx06 = 'Tilbudpris  ' +
     c                                      %trim(vtkamp)
     c                   endif
     c                   endif
      *
      * Fjerner notat på vare dersom den nye varen finnes
      *
     c                   eval      anotlr_syst = 'VVAR'
     c                   eval      anotlr_noid = w_nvar
     c     anotlr_key    chain     anotlr
     c                   if        %found
     c                   eval      anotlr_noid = w_gvar
     c     anotlr_key    setll     anotlr
     c     anotlr_key    reade     anotlrr
     c                   if        %found(anotlr)
     c                   eval      m1tx07 = 'Notat på varen'
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på vareregister
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
     c                   kfld                    vvenl1_enhe
      *
      * Key for oppslag på enhet
      *
     c     vvenl1_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
      *
      * Key for oppslag på enhet
      *
     c     vvenlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenlr_vare
     c                   kfld                    vvenlr_enhe
      *
      * Key for oppslag på "gammel varenr"
      *
     c     key_gml       klist
     c                   kfld                    w_firm
     c                   kfld                    w_gvar
      *
      * Key for oppslag på "nytt varenr"
      *
     c     key_ny        klist
     c                   kfld                    w_firm
     c                   kfld                    w_nvar
      *
      * Key for oppslag på / oppdatering av notat
      *
     c     anotlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    anotlr_syst
     c                   kfld                    anotlr_noid
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
