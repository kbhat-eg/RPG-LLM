      /TITLE    Kvittering av innkomne returer
     h decedit(',') datedit(*dmy.)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASTELE                                              *
      * Program . . . . : GL155R                                              *
      * Beskrivelse . . : Kvittering av innkomne returer                      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      * 07023     070203  Erstattet O-skjema med Printfile.        RH   *
      *                                                                 *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     fgretpf    ip   e             disk
     fgavtl1    if   e           k disk    rename(gavtpfr:gavtl1r)
      *
     f**liste     o    f  132        printer oflind(*inof)
     fgl151p    o    e             printer
      *
     d kto             s             11    dim(999)
     d txt             s             20    dim(4) ctdata perrcd(1)
610  d w_jkey          s             41
610  d w_jsta          s              2  0
610  d w_jmld          s            200
610  d w_stat          s              1  0
      *
     d                 ds
     d gdqret                       160
     d  ocrret                        8    overlay(gdqret)
     d  ocrkto                       11    overlay(gdqret:25)
     d  betf21                        8    overlay(gdqret:41)
     d  betfkt                       11    overlay(gdqret:60)
      *
      *  LDA
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_navn                951    980
      *
     d it              s                   like(ix)
     d iu              s                   like(ix)
     d ix              s              4  0
      *
      *  Les returer
      *
     c     lesrk         tag
      *
      *  KONTO FINNES/NY
      *
INNL.c                   if        betf21 = 'BETFOR21'
UTL. c                             or betf21 = 'BETFOR01'
     c     betfkt        lookup    kto                                    50
     c                   if        *in50 = *off
     c                   eval      ix = ix + 1
     c                   move      betfkt        kto(ix)
     c                   endif
     c                   endif
      *
     c                   if        ocrret = 'NY090020'
     c     ocrkto        lookup    kto                                    50
     c                   if        *in50 = *off
     c                   eval      ix = ix + 1
     c                   move      ocrkto        kto(ix)
     c                   endif
     c                   endif
      *
     c     slutt         tag
      *
     clr                 exsr      subskr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for utskrift av kvittering av returmottak             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     subskr        begsr
      *
07023c*********          except    h101
      *
     c                   eval      it = 1
      *
     c                   dou       it > ix
     c                   eval      iu = 4
     c                   move      kto(it)       gcbgir
     c     avtkey        chain     gavtl1                             41
      *
     c                   if        *in41 = *on
     c                   eval      iu = 3
     c                   eval      gcbgir = *zeros
     c                   eval      d1txt  = txt(3)
     c                   move      kto(it)       d1kto
     c                   else
      *
     c                   eval      d1filg = gcfilg
     c                   eval      d1firm = gcfirm
     c                   move      kto(it)       d1kto
     c                   endif
      *                                                             .
07023c                   write     d1det                                30
      *                                                             .
07023c                   if        *in30 = *on
07023c                   write     a1hdr
07023c                   endif
      *
     c                   eval      gckomm = *blank
     c                   eval      gcfilg = *blank
     c                   eval      gcfirm = *zeros
     c                   eval      gcbgir = *zeros
     c                   eval      gcpgir = *zeros
     c                   eval      d1txt  = *blank
     c                   eval      d1filg = *blank
     c                   eval      d1firm = *zeros
     c                   eval      d1bgir = *zeros
     c                   eval      it = it + 1
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ERROR-rutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *pssr         begsr
      *
6.10 c                   eval      w_stat = 1
6.10 c                   eval      w_jsta = 40
6.10 c                   eval      w_jmld = 'Fatal feil i GL551R'
6.10 c                   call      'AB705R'
6.10 c                   parm                    w_jkey
6.10 c                   parm                    w_jsta
6.10 c                   parm                    w_jmld
610  c                   move      *on           *inlr
610  c                   goto      slutt
      *
     c                   endsr
      *
     c/eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
610  c                   parm                    w_jkey                         user
610  c                   parm                    w_stat                         user
     c                   eval      ix = *zeros
      *
      * Key for AVTALEREGISTER
      *
     c     avtkey        klist
     c                   kfld                    gcbgir
      *
      * Skriver ut heading p√• rapport.
      *
     c                   write     a1hdr
      *
     c                   endsr
      * ===========================================
**   TEKSTER
Motaksretur (1. gang)
Motaksretur (2. gang)
Konto uten avtale
.
