     H/TITLE  ASLAGR: Utskrift av Pakkseddel
     H DECEDIT('0,') DATEDIT(*DMY.)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * Program . . . . : LO930R                                        *
      * Beskrivelse . . : Utskrift av Pakkseddel u/priser og beløp.     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
      *    R2.02  220497  Harmonisert mot Versjon 1.01 av ASOKON        *
      *                                                                 *
      *    R2.04  080499  Laget versjon uten priser og linjesummer ASK  *
6.10  * R6.10 070909 BSA  Utskriften endret pga utvidelse av referansefelter.
6.11  * R6.10 191010 BSA  Problemer med dobbel overflow.
6.nu  * R6.30 120614 bof  Sjekket mtp. nummerutvidelse
      *                                                                 *
      *                                                                 *
      *                                                                 *
      *                                                                 *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     FLPSOL1    IPE  E             DISK    RENAME(LPSOPFR:LPSOL1R)
     FLSTSL1    IF   E           K DISK    RENAME(LSTSPFR:LSTSL1R)
     FVOTYL1    IF   E           K DISK    RENAME(VOTYPFR:VOTYL1R)
     FVLTYL1    IF   E           K DISK    RENAME(VLTYPFR:VLTYL1R)
     FFOHEL1    IF   E           K DISK    RENAME(FOHEPFR:FOHEL1R)
     FLOHEL1    IF   E           K DISK    RENAME(LOHEPFR:LOHEL1R)
     FLODTL1    IF   E           K DISK    RENAME(LODTPFR:LODTL1R)
     FLOTXL1    IF   E           K DISK    RENAME(LOTXPFR:LOTXL1R)
     FVVARL1    IF   E           K DISK    RENAME(VVARPFR:VVARL1R)
     Frlevl1    IF   E           K DISK    RENAME(RLEVPFR:rlevl1R)
     Frkunl1    IF   E           K DISK    RENAME(RKUNPFR:rkunl1R)
     FAFIRL1    IF   E           K DISK    RENAME(AFIRPFR:AFIRL1R)
     FAFORL1    IF   E           K DISK    RENAME(AFORPFR:AFORL1R)
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
5.34 ffvlsl1    if   e           k disk    rename(fvlspfr:fvlsl1r)
      *
     FLO935P    O    E             PRINTER
      *
      * - - - - - - - - - - - - - - - -
      * Datastruktur for foretaksnummer
      * - - - - - - - - - - - - - - - -
      *
     D                 DS
     D  DSFFTX                 1     30
     D  DSFF01                 1      8
     D  DSFF02                 9     11
     D  DSFF03                12     21
     D  DSFF04                22     25
     D  DSFF05                22     25
      *
      * Definering av variable i nøkler
      *
     d vlagl1_lage     s                   like(vllage)
     d vlagl1_vare     s                   like(vlvare)
      *
      * - - - - - - - - - - - - - -
      * Henter inn data fra LDA
      * - - - - - - - - - - - - - -
      *
     D                UDS
     D  DSRUTI               800    809
     D  DSALIN               856    858  0
     D  DSFIRM               944    946  0
5.34  *
5.34  * Definering av variabler i nøkler
5.34  *
5.34 d fvlsl1_numm     s                   like(fvnumm)
5.34 d fvlsl1_suff     s                   like(fvsuff)
5.34 d fvlsl1_line     s                   like(fvline)
      *
     I/EJECT
     ILPSOL1R       01
     I                                          LKSSRT        L4
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av formater                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Benyttede formater
      * - - - - - - - - - -
      *
      * A1HDR  - Heading A1 på rapport
      * A4HDR  - Heading A4 på rapport, Bestillingshode over varelinje
      * D1DET  - Detail  D1 på rapport, Varelinje
      * E1DET  - Detail  E1 på rapport, Tekstlinje
      * O1DET  - Detail  O1 på rapport, Overføring til/fra neste side
      * T1TOT  - Total   T1 på rapport
      *
      *
      * Forklaring på variabler
      * - - - - - - - - - - - -
      *
      * D1ANT0 - Antall med 0 desimaler
      * D1ANT1 - Antall med 1 desimaler
      * D1ANT2 - Antall med 2 desimaler
      * D1ANT3 - Antall med 3 desimaler
      *
      *
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Styre-rutine                                                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Initierings-rutine
      *
     C   01FCYCLE        CASEQ     *ZERO         XSTRSR
     C                   ENDCS
      *
      * Bestillingshode
      *
     C     LKSKOD        CABEQ     'H'           TAG01
      *
      * Varelinje
      *
     C     LKSKOD        CABEQ     'V'           TAG02
      *
      * Kommentar/
      * tekstlinje
      *
     C     LKSKOD        CABEQ     'K'           TAG03
      *
      * Ferdig
      *
     C                   GOTO      XSLUTT
      *
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * A1HDR Behandle heading A1                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     TAG01         TAG
      *
      * Hent bestillings-heading
      *
     C                   MOVE      LKSONR        WWNUMM
     C                   MOVE      LKSSUF        WWSUFF
     C     KEY01         CHAIN     LOHEL1R                            61
      *
     c                   eval      a1bdat = 0
     c                   eval      a1ldat = 0
     c                   eval      a1vdat = 0
      *
     c                   if        lobdat <> *loval
     c     *dmy          move      lobdat        a1bdat
     c                   endif
      *
     c                   if        loldat <> *loval
     c     *dmy          move      loldat        a1ldat
     c                   endif
     c                   if        lomoda <> *loval
     c     *dmy          move      lomoda        a1ldat
     c                   endif
      *
     c                   if        lovida <> *loval
     c     *dmy          move      lovida        a1vdat
     c                   endif
      *
      * Legg inn bestillingsnummer
      *
     C                   MOVE      LONUMM        A1NUMM
     C                   MOVEL     '/'           A1SUFF
     C                   MOVE      LOSUFF        A1SUFF
      *
      * Flere bestillinger på samme
      * blankett ?
      *
     C  NL4              GOTO      TAG01A
      *
      * Hente inn leverandør-register
      *
     C                   MOVE      LOLDOR        WWKUND
     C     KEY04         CHAIN     rlevl1R                            64
     C                   MOVE      LOLDOR        A1LDOR
     C                   MOVE      RLNAVN        A1NAVN
     C                   MOVE      RLGATE        A1GATE
     C                   MOVE      RLADR2        A1ADR2
     C                   MOVE      RLSTED        A1STED
      *
      * Sjekke engangs-leverandøren
      *
     C     LOLDOR        IFEQ      W0LDOR
     C                   MOVE      LOLELE        LOLDOR
     C                   MOVE      LONAVN        A1NAVN
     C                   MOVE      LOADR1        A1GATE
     C                   MOVE      LOADR2        A1ADR2
     C                   MOVE      LOSTED        A1STED
     C                   MOVE      *ZERO         LOLELE
     C                   MOVE      *BLANK        LONAVN
     C                   MOVE      *BLANK        LOADR1
     C                   MOVE      *BLANK        LOADR2
     C                   MOVE      *BLANK        LOSTED
     C                   END
      *
      * Finner hvilken mva-sats
      * som skal benyttes
      *
     c                   call      'FÅ705R'
     c                   parm                    wwfirm
     c                   parm                    lobdat
     c                   parm                    WPMVAS
     c                   parm                    WPMVAT
     c                   parm                    WPMVAF
      *
      * Er det registrert
      * leveringsadresse?
      *
     C                   MOVE      *OFF          *IN31
     C     LONAVN        IFNE      *BLANK
     C                   MOVE      *ON           *IN31
     C                   MOVE      LONAVN        A2NAVN
     C                   MOVE      LOADR1        A2ADR1
     C                   MOVE      LOADR2        A2ADR2
     C                   MOVE      LOSTED        A2STED
     C                   ENDIF
      *
      * Er dette en kundeordre som er plukket til bestilling,
      * i så fall legg kundens navn og adresse til leveringsadresse.
      *
     C                   MOVE      *OFF          *IN36
     C                   MOVE      *BLANK        A2DREF
     C     LOORNR        IFNE      *ZERO
     C                   MOVE      LOORNR        WWORNU
     C                   MOVE      LOORSU        WWORSU
     C     KEY13         CHAIN     FOHEL1R                            73
     C     *IN73         IFEQ      *OFF
     C                   MOVE      FOKUND        WWKKUN
     C     KEY14         CHAIN     rkunl1R                            74
     C     *IN74         IFEQ      *OFF
     C                   MOVE      *ON           *IN31
     C                   MOVE      RKNAVN        A2NAVN
     C                   MOVE      RKGATE        A2ADR1
     C                   MOVE      RKADR2        A2ADR2
     C                   MOVE      RKSTED        A2STED
     C     FODREF        IFNE      *BLANK
     C                   MOVE      *ON           *IN36
     C                   MOVE      FODREF        A2DREF
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
      *
      * Er det registrert
      * leveringsdato?
      *
     C                   MOVE      *OFF          *IN32
     C     LOLDAT        IFNE      *loval
     C                   MOVE      *ON           *IN32
     C                   ENDIF
      *
      * Er det registrert
      * leverandørens referanse?
      *
     C                   MOVE      *OFF          *IN33
     C     LODREF        IFNE      *BLANK
     C                   MOVE      *ON           *IN33
     C                   MOVE      LODREF        A1DREF
     C                   ENDIF
      *
      * Er det registrert
      * leveringsmåte?
      *
     C                   MOVE      *OFF          *IN34
     C     LOLMTX        IFNE      *BLANK
     C                   MOVE      *ON           *IN34
     C                   MOVE      LOLMTX        A1LMTX
     C                   ENDIF
      *
      * Er det registrert
      * rekvisisjon?
      *
     C                   MOVE      *OFF          *IN35
     C     LOREKV        IFNE      *BLANK
     C                   MOVE      *ON           *IN35
     C                   MOVE      LOREKV        A1REKV
     C                   ENDIF
      *
      * Er det registrert
      * ordrenummer?
      *
     C                   MOVE      *OFF          *IN38
     C     LOORNR        IFNE      *ZERO
     C                   MOVE      *ON           *IN38
     C                   MOVE      LOORNR        A1ORNR
     C                   ENDIF
      *
      * Flytte øvrige felter
      *
     C                   MOVE      LOREKV        A1REKV
     C                   MOVE      LOINNK        A1INNK
     C                   MOVE      LOBETB        A1BETB
     C                   MOVE      LOVREF        A1VREF
      *
      * Skrive heading
      *
     C                   Z-ADD     0             WLTELL
     C                   WRITE     A1HDR
      *
      * Skriv heading over
      * varelinjer
      *
     C     VAOSAM        IFEQ      1
     C                   ADD       2             WLTELL
     C                   WRITE     A4HDR                                30
     C                   ENDIF
      *
     C                   GOTO      XSLUTT
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Heading U/L4-brudd                                              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     TAG01A        TAG
      *
      * Skriv heading over
      * varelinjer
      *
     C     VAOSAM        IFEQ      1
     C                   ADD       2             WLTELL
     C                   WRITE     A4HDR                                30
     C                   ENDIF
      *
     C                   GOTO      XSLUTT
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * D1DET Behandle detail D1                                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     TAG02         TAG
      *
      * Nullstilling av variabler
      *
     C                   MOVE      *BLANK        D1VARE
     C                   MOVE      *BLANK        D1TEK1
     C                   MOVE      *BLANK        D1TEK2
     C                   MOVE      *BLANK        D1ENH1
     C*
     C                   MOVE      *ZERO         D1ANT0
     C                   MOVE      *ZERO         D1ANT1
     C                   MOVE      *ZERO         D1ANT2
     C                   MOVE      *ZERO         D1ANT3
     C                   MOVE      *OFF          *IN44
     C                   MOVE      *OFF          *IN46
      *
     C                   MOVE      LKSONR        WWNUMM
     C                   MOVE      LKSSUF        WWSUFF
     C                   MOVE      LKSLIN        WWLINE
     C     KEY02         CHAIN     LODTL1R                            62
      *
      * Hent linje-type
      *
     C                   MOVE      LDLTYP        WWLTYP
     C     KEY08         CHAIN     VLTYL1R                            68
      *
      * Sjekker om linjen ikke skrives på ordretypen
      *
     C     LOOTYP        CABEQ     VALO01        XSLUTT
      *
     C     LOOTYP        CABEQ     VALO02        XSLUTT
      *
     C     LOOTYP        CABEQ     VALO03        XSLUTT
      *
     C     LOOTYP        CABEQ     VALO04        XSLUTT
      *
      * Sjekker om tekstlinjer
      *
     C     VALKTX        CABEQ     1             TAG02A
      *
      * Oppslag mot vareregister
      *
     C                   MOVE      LDVARE        WWVARE
     C     KEY11         CHAIN     VVARL1R                            69
      *
      * Oppslag mot lagerregister
      *
     c                   eval      vlagl1_lage = lolage
     c                   eval      vlagl1_vare = ldvare
     c     vlagl1_key    chain     vlagl1r                            90
      *
      * Test på antall desimaler
      *
     C                   if        ldlvre = 0
     C     VVDESI        COMP      0                                      40
     C     VVDESI        COMP      1                                      41
     C     VVDESI        COMP      2                                      42
     C     VVDESI        COMP      3                                      43
     C   40              Z-ADD     LDANTA        D1ANT0
     C   41              Z-ADD     LDANTA        D1ANT1
     C   42              Z-ADD     LDANTA        D1ANT2
     C   43              Z-ADD     LDANTA        D1ANT3
     c                   else
     c                   z-add     ldanta        d1ant2
     c                   eval      *in42 = *on
     c                   endif
      *
      * Varelinje
      *
     C                   MOVE      LDVARE        D1VARE
     C                   MOVE      LDTEK1        D1TEK1
     C                   MOVE      LDENH1        D1ENH1
     c                   eval      d1lpla = vlplas
      *
      * Skriv varelinjer
      *
     C     TAG02A        TAG
     C                   ADD       2             WLTELL
     C     VALKTX        IFNE      1
      *
     C     LDLVAR        IFNE      *BLANK
     C                   MOVE      LDLVAR        D0VARE
     C                   WRITE     D0DET                                30
     C                   END
      *
     C                   WRITE     D1DET                                30
      *
      * Sjekk om lengde spesifikasjon
      *
     c                   exsr      len_spec
      *
     C                   ELSE
     C                   MOVE      LDTEK1        D1TEK1
     C                   MOVE      LDTEK2        D1TEK2
     C                   MOVE      *ON           *IN44
     C                   WRITE     D2DET                                30
     C                   END
      *
      * Er det overflow ?
      *
     C     *IN30         CASEQ     *ON           XOVRFL
     C                   ENDCS
      *
     C                   GOTO      XSLUTT
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Behandling av tekstlinjer                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     TAG03         TAG
      *
     C                   MOVE      LKSONR        WWNUMM
     C                   MOVE      LKSSUF        WWSUFF
     C                   MOVE      LKSLIN        WWLINE
     C     KEY03         CHAIN     LOTXL1R                            63
      *
      * Gå ut dersom det
      * ikke er oppsamling,
      * og linje-nummer er
      * større enn 5000.
      * Da skal print fore-
      * gå tilslutt på L4.
      *
     C     VAOSAM        IFEQ      0
     C     LKSLIN        IFGE      5000
     C                   GOTO      XSLUTT
     C                   END
     C                   ENDIF
      *
     C                   MOVEL     LTTEXT        E1TEXT
     C                   ADD       1             WLTELL
     C                   WRITE     E1DET                                30
      *
      * Er det overflow ?
      *
     C     *IN30         CASEQ     *ON           XOVRFL
     C                   ENDCS
      *
     C     XSLUTT        TAG
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * T1TOT Behandle total T1                                         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Finne ut om det er
      * plass til å skrive
      * avslutning uten å
      * skifte til ny side
      * først.
      *
      * Trekker ut heading
      *
     CL4   DSALIN        SUB       25            WLTEST
      *
      * Trekker ut antall
      * linjer skrevet
      *
     CL4                 SUB       WLTELL        WLTEST
      *
      * Trekker ut plassbehov
      * for avslutning
      *
     CL4                 SUB       15            WLTEST
      *
      * Skrive ny heading ?
      *
     CL4   WLTEST        IFLT      0
     CL4                 WRITE     A1HDR
     CL4                 Z-ADD     0             WLTELL
     CL4                 ENDIF
      *
      * Skrive totalsum
      *
     CL4                 WRITE     T1TOT                                30
      *
      * Skrive tekstlinjer
      * dersom det ikke er
      * oppsamling og linje
      * nummer er større enn
      * 5000.
      *
     CL4   VAOSAM        IFEQ      0
     CL4                 MOVE      LKSONR        WWNUMM
     CL4                 MOVE      LKSSUF        WWSUFF
     CL4                 Z-ADD     5000          WWLINE
     CL4   KEY03         SETLL     LOTXL1R
     CL4                 READ      LOTXL1R                                63
     CL4   *IN63         CABEQ     *ON           TAG99
      *                                                                 .
     CL4   WWNUMM        DOWEQ     LTNUMM
     CL4   WWSUFF        IFEQ      LTSUFF
      *                                                               . .
     CL4                 MOVEL     LTTEXT        E1TEXT
     CL4                 ADD       1             WLTELL
     CL4                 WRITE     E1DET                                30
      *                                                               . .
      * Er det overflow ?                                             . .
      *                                                               . .
     CL4   *IN30         IFEQ      *ON
     CL4                 WRITE     A1HDR
     CL4                 Z-ADD     0             WLTELL
     CL4                 ENDIF
      *                                                               . .
     CL4                 READ      LOTXL1R                                63
     CL4   *IN63         CABEQ     *ON           TAG99
     CL4                 ENDIF
     CL4                 ENDDO
     CL4                 ENDIF
      *
     CL4   TAG99         TAG
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for overflow                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XOVRFL        BEGSR
      *
     C*
     C                   WRITE     A1HDR
     C*
     C                   Z-ADD     0             WLTELL
6.11 C                   eval      *in30 = *off
      *
     C                   ENDSR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for lengde spesifikasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     len_spec      begsr
      *
5.34 c                   eval      fvlsl1_numm = ldornu
5.34 c                   eval      fvlsl1_suff = ldorsu
5.34 c                   eval      fvlsl1_line = ldorli
5.34 c     fvlsl1_key    setll     fvlsl1
5.34 c     fvlsl1_key    reade     fvlsl1
5.34  *
5.34 c                   dow       not %eof
5.34 c                   eval      e1text = %char(fvleng) + ' mm x ' +
5.34 c                                      %char(fvanta) + ' stk'
5.34  *
5.34 c                   write     e1det                                30
5.34 c     *in30         caseq     *on           xovrfl
5.34 c                   endcs
5.34 c     fvlsl1_key    reade     fvlsl1
5.34 c                   enddo
      *
     c     end_spec      endsr
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for oppstart                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XSTRSR        BEGSR
      *
     C                   Z-ADD     1             FCYCLE            1 0
      *
      * Legger inn firma i nøkkel
      *
     C                   MOVE      LKSFIR        WWFIRM
      *
      * Klargjøring dersom firma -
      * opplysninger skal printes ut
      *
     C     AFPRFI        IFEQ      1
     C     KEY06         CHAIN     AFIRL1R                            66
     C                   MOVE      AAFNAV        A1FNAV
     C                   MOVE      AAFAD1        A1FAD1
     C                   MOVE      AAFAD2        A1FAD2
     C                   MOVE      AAFSTE        A1FSTE
     C                   MOVE      'Foretak:'    DSFF01
     C                   MOVE      ' NO'         DSFF02
     C                   MOVE      AAFFTN        DSFF03
     C                   MOVE      ' MVA'        DSFF04
     C                   MOVE      DSFFTX        A1FFTX
     C                   MOVE      'Tlf/fax:'    A1TFAX
     C                   MOVE      AAFTLF        A1FTLF
     C                   MOVE      AAFFAX        A1FFAX
     C                   ENDIF
      *
      * Hente Engangs-leverandør-nummer
      *
     C     KEY09         CHAIN     LSTSL1R                            69
      *
     C     *IN69         IFEQ      *OFF
     C                   MOVE      LAALEV        W0LDOR
     C                   END
      *
      * Slår opp ordretype
      *
     C                   MOVE      LKSOTY        WWOTYP
     C     KEY07         CHAIN     VOTYL1R                            67
      *
      * Er ordretype negativ ?
      *
     C     VAOKRE        IFEQ      '-'
     C                   MOVE      *ON           *IN36
     C                   ENDIF
      *
     C                   ENDSR
      *
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     *INZSR        BEGSR
      *
     C     *LIKE         DEFINE    LAALEV        W0LDOR
     C     *LIKE         DEFINE    VAOTYP        WWOTYP
     C     *LIKE         DEFINE    LOLDOR        WWKUND
     C     *LIKE         DEFINE    LOFIRM        WWFIRM
     C     *LIKE         DEFINE    LOWSID        WWWSID
     C     *LIKE         DEFINE    LONUMM        WWNUMM
     C     *LIKE         DEFINE    LOSUFF        WWSUFF
     C     *LIKE         DEFINE    LDLINE        WWLINE
     C     *LIKE         DEFINE    LDLTYP        WWLTYP
     C     *LIKE         DEFINE    LDNUMM        SANUMM
     C     *LIKE         DEFINE    VVVARE        WWVARE
      *
     C     *LIKE         DEFINE    LDIPNY        W1RAB1
      *
     C     *LIKE         DEFINE    LDORNU        WWORNU
     C     *LIKE         DEFINE    LDORSU        WWORSU
     C     *LIKE         DEFINE    FOKUND        WWKKUN
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     C                   MOVE      *ZERO         WPMVAS            6 2
     C                   MOVE      *ZERO         WPMVAT            5 4
     C                   MOVE      *ZERO         WPMVAF           10 9
      *
     C                   MOVE      *BLANK        WWRUTI           10
     C                   MOVE      *ZERO         WLTELL            3 0
     C                   MOVE      *ZERO         WLTEST            3 0
      *
      * Key for Heading-file
      *
     C     KEY01         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWNUMM
     C                   KFLD                    WWSUFF
      *
      * Key for Detalj-file
      *
     C     KEY02         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWNUMM
     C                   KFLD                    WWSUFF
     C                   KFLD                    WWLINE
      *
      * Key for Tekst-linjer
      *
     C     KEY03         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWNUMM
     C                   KFLD                    WWSUFF
     C                   KFLD                    WWLINE
      *
      * Key for leverandørregister
      *
     C     KEY04         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWKUND
      *
      * Key for firmaregister
      *
     C     KEY06         KLIST
     C                   KFLD                    WWFIRM
      *
      * Key for ordretyper
      *
     C     KEY07         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWOTYP
      *
      * Key for linjetyper
      *
     C     KEY08         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWLTYP
      *
      * Key for statusregister
      *
     C     KEY09         KLIST
     C                   KFLD                    WWFIRM
      *
      * Key for formular-register
      *
     C     KEY10         KLIST
     C                   KFLD                    WWRUTI
      *
      * Key for vare-register
      *
     C     KEY11         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWVARE
      *
      * Key for Kunde-ordre-heading-file
      *
     C     KEY13         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWORNU
     C                   KFLD                    WWORSU
      *
      * Key for Kunderegister
      *
     C     KEY14         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWKKUN
      *
      *  Key for les av LAGER
      *
     c     vlagl1_key    klist
     c                   kfld                    wwfirm
     c                   kfld                    vlagl1_vare
     c                   kfld                    vlagl1_lage
5.34  *
5.34  * Key for oppslag på lengdespesif.linje
5.34  *
5.34 c     fvlsl1_key    klist
5.34 c                   kfld                    wwfirm
5.34 c                   kfld                    fvlsl1_numm
5.34 c                   kfld                    fvlsl1_suff
5.34 c                   kfld                    fvlsl1_line
      *
      * Hente inn rutinenavn
      * og meldingstekst
      *
     C                   MOVE      DSRUTI        WWRUTI
      *
     C     KEY10         CHAIN     AFORL1R                            69
     C  N69              MOVE      AFTXT1        A1TXT1
     C  N69              MOVE      AFMLD1        T1MLD1
      *
     C                   ENDSR
