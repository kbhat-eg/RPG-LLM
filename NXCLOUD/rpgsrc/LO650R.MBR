     h option(*nodebugio) datedit(*dmy) decedit('0,')
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : LO650R
      * Beskrivelse . . : Utskrift av Varemottaksmelding til kunde.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.
      * --------- ------  --------------------------------------------------
6.10  * R6.10 070909 BSA  Utskriften endret pga utvidelse av referansefelter.
6.nu  * R6.30 120614 bof  Sjekket mtp. nummerutvidelse
      *
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flodtl1    if   e           k disk    rename(lodtpfr:lodtl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     faforl1    if   e           k disk    rename(aforpfr:aforl1r)
      *
     flo650p    o    e             printer
      *
      * Datastruktur for foretaksnummer
      *
     d                 ds
     d d_fftx                        30
     d  d_ff01                        8    overlay(d_fftx)
     d  d_ff02                        3    overlay(d_fftx:9)
     d  d_ff03                       10  0 overlay(d_fftx:12)
     d  d_ff04                        4    overlay(d_fftx:22)
     d   d_ff05                       4    overlay(d_ff04)
      *
      * Local Data Area LDA
      *
     d                uds
     d l_ruti                800    809
     d l_alin                856    858  0
     d l_firm                944    946  0
      *
      * Definering av parametre
      *
     d p_firm          s                   like(lofirm)
     d p_numm          s                   like(lonumm)
     d p_suff          s                   like(losuff)
      *
      * Definering av variable i nøkler
      *
     d aforl1_ruti     s                   like(afruti)
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d lodtl1_line     s                   like(ldline)
     d lodtl1_numm     s                   like(ldnumm)
     d lodtl1_suff     s                   like(ldsuff)
     d lohel1_numm     s                   like(lonumm)
     d lohel1_suff     s                   like(losuff)
     d rkunl1_kund     s                   like(rkkund)
     d vvarl1_vare     s                   like(vvvare)
      *
      * Definering av øvrige variable
      *
     d w_firm          s                   like(lofirm)
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Definering av formater
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * A1HDR  - Heading A1 på rapport
      * A2HDR  - Heading A2 på rapport
      * D1DET  - Detail  D1 på rapport, Varelinje
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Styre-rutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Hent bestillings-heading
      *
     c                   eval      lohel1_numm = p_numm
     c                   eval      lohel1_suff = p_suff
     c     lohel1_key    chain     lohel1r
     c                   if        not %found
     c                   goto      end_pgm
     c                   endif
      *
      * Hent informasjon fra kundeordre
      *
     c                   eval      a1ornr = loornr
     c                   eval      fohel1_numm = loornr
     c                   eval      fohel1_suff = loorsu
     c     fohel1_key    chain     fohel1r
     c                   if        %found
     c                   eval      rkunl1_kund = fokund
     c     rkunl1_key    chain     rkunl1r
     c                   if        %found
     c                   eval      a1navn = rknavn
     c                   eval      a1gate = rkgate
     c                   eval      a1adr2 = rkadr2
     c                   eval      a1sted = rksted
     c                   endif
     c                   endif
      *
      * Er det registrert deres referanse?
      *
     c                   if        fodref <> *blank
     c                   eval      *in31 = *on
     c                   eval      a1dref = fodref
     c                   endif
      *
      * Er det registrert rekvisisjon?
      *
     c                   if        forekv <> *blank
     c                   eval      *in32 = *on
     c                   eval      a1rekv = forekv
     c                   endif
      *
      * Er det registrert vår ref?
      *
     c                   if        lovref <> *blank
     c                   eval      *in33 = *on
     c                   eval      a1vref = lovref
     c                   endif
      *
      * Flytte øvrige felter
      *
     c                   eval      a1ornr = loornr
      *
      * Skrive heading
      *
     c                   write     a1hdr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * D1DET Behandle detail D1
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Nullstilling av variabler
      *
      *
     c                   eval      d1vare = *blank
     c                   eval      d1tek1 = *blank
     c                   eval      d1enh1 = *blank
      *
     c                   eval      d1ant0 = 0
     c                   eval      d1ant1 = 0
     c                   eval      d1ant2 = 0
     c                   eval      d1ant3 = 0
      *
     c                   eval      lodtl1_numm = p_numm
     c                   eval      lodtl1_suff = p_suff
     c     lodtl1_key    setll     lodtl1r
      *
      * Leser bestillingslinjer
      *
     c     lodtl1_key    reade     lodtl1
      *
     c                   dow       not %eof
      *
      * Oppslag mot vareregister
      *
     c                   eval      vvarl1_vare = ldvare
     c     vvarl1_key    chain     vvarl1r                            69
      *
      * Test på antall desimaler
      *
     c                   if        ldlvre = 0
     c                   eval      *in40 = *off
     c                   eval      *in41 = *off
     c                   eval      *in42 = *off
     c                   eval      *in43 = *off
     c                   if        vvdesi = 0
     c                   eval      *in40 = *on
     c                   eval      d1ant0 = ldanta
     c                   endif
     c                   if        vvdesi = 1
     c                   eval      *in41 = *on
     c                   eval      d1ant1 = ldanta
     c                   endif
     c                   if        vvdesi = 2
     c                   eval      *in42 = *on
     c                   eval      d1ant2 = ldanta
     c                   endif
     c                   if        vvdesi = 3
     c                   eval      *in43 = *on
     c                   eval      d1ant3 = ldanta
     c                   endif
     c                   else
     c                   eval      d1ant2 = ldanta
     c                   eval      *in42 = *on
     c                   endif
      *
      * Varelinje
      *
      *
     c                   eval      d1vare = ldvare
     c                   eval      d1tek1 = ldtek1
     c                   eval      d1enh1 = ldenh1
      *
     c                   write     d1det                                30
      *
      * Er det overflow ?
      *
     c                   if        *in30 = *on
     c                   endif
      *
     c     lodtl1_key    reade     lodtl1
      *
     c                   enddo
      *
      * Avslutt program
      *
     c     end_pgm       tag
     c                   eval      *inlr = *on
     c                   return
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_numm
     c                   parm                    p_suff
      *
      * Key for les av BESTILLINGSHODE
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
     c                   kfld                    lohel1_suff
      *
      * Key for les av BESTILLINGSLINJE
      *
     c     lodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtl1_numm
     c                   kfld                    lodtl1_suff
      *
      * Key for les av FIRMA
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for les av FORMULAR
      *
     c     aforl1_key    klist
     c                   kfld                    aforl1_ruti
      *
      * Key for les av VARE
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for les av ORDREHEADING
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      * Key for les av KUNDER
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Hente inn rutine informasjon
      *
     c                   eval      w_firm = p_firm
     c                   eval      aforl1_ruti = l_ruti
      *
     c     aforl1_key    chain     aforl1r
     c                   if        %found
     c                   eval      a1txt1 = aftxt1
     c                   eval      a1mld1 = afmld1
     c                   eval      a1mld2 = afmld2
     c                   eval      a1mld3 = afmld3
     c                   endif
      *
      * Hent firma-opplysninger dersom de skal printes ut
      *
     c                   if        afprfi = 1
     c     afirl1_key    chain     afirl1r
     c                   if        %found
     c                   eval      a1fnav = aafnav
     c                   eval      a1fad1 = aafad1
     c                   eval      a1fad2 = aafad2
     c                   eval      a1fste = aafste
     c                   eval      d_ff01 = 'Foretak:'
     c                   eval      d_ff02 = ' NO'
     c                   eval      d_ff03 = aafftn
     c                   eval      d_ff04 = ' MVA'
     c                   eval      a1fftx = d_fftx
     c                   eval      a1tfax = 'Tlf/fax:'
     c                   eval      a1ftlf = aaftlf
     c                   eval      a1ffax = aaffax
     c                   endif
     c                   endif
      *
      * Redigerer aktuelt tidspunkt ut på melding.
      *
     c                   time                    w_date
     c                   time                    w_time
     c     *dmy          move      w_date        a1dato
     c     *hms          move      w_time        a1time
      *
     c                   endsr
