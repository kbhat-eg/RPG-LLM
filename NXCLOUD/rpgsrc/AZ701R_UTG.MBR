      /TITLE  AZ701R - Zipper ut en fil
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASPGPL
      * Program . . . . : AZ701R
      * Beskrivelse . . : Zipper ut en fil
      *
      *
      *
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       250203 MHA  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Definering av pamametre
      *
     d zipfil          s               o   class(*JAVA:'java.lang.String')
     d directory       s               o   class(*JAVA:'java.lang.String')
     d logfil          s               o   class(*JAVA:'java.lang.String')
     d uz              s               o   class(*JAVA:'asp.zip.UnpackZip')
     d antall          s             10i 0
      *
     d p_zip           s             50a
     d p_log           s             50a
     d p_dir           s             50a
     d p_ant           s                   like(antall)
      *
     d mkstr           pr              o   extproc(*JAVA:
     d                                      'java.lang.String':
     d                                      *CONSTRUCTOR)
     d                                     class(*JAVA:'java.lang.String')
     d   bytes                       50a   CONST VARYING
      *
     d crtzipobj       pr              o   extproc(*JAVA:
     d                                      'asp.zip.UnpackZip':
     d                                      *CONSTRUCTOR)
      *
     d setzip          pr                  extproc(*JAVA:
     d                                      'asp.zip.UnpackZip':
     d                                      'setZipFile')
     d   zipfil                        o   class(*JAVA:
     d                                      'java.lang.String')
      *
     d setlog          pr                  extproc(*JAVA:
     d                                      'asp.zip.UnpackZip':
     d                                      'setLogFile')
     d   log                           o   class(*JAVA:
     d                                      'java.lang.String')
      *
     d setdir          pr                  extproc(*JAVA:
     d                                      'asp.zip.UnpackZip':
     d                                      'setDirectory')
     d   dir                           o   class(*JAVA:
     d                                      'java.lang.String')
      *
     d dezip           pr            10i 0 extproc(*JAVA:
     d                                      'asp.zip.UnpackZip':
     d                                      'unpackFiles')
      *
     c                   eval      uz = crtzipobj
      *
     c                   eval      zipfil = mkstr(%trim(p_zip))
     c                   callp     setzip(uz:zipfil)
      *
     c                   if        p_log <> *blank and
     c                             %trim(p_log) <> '*NONE' and
     c                             %trim(p_log) <> '*BLANK'
     c                   eval      logfil = mkstr(%trim(p_log))
     c                   callp     setlog(uz:logfil)
     c                   endif
      *
     c                   if        p_dir <> *blank and
     c                             %trim(p_dir) <> '*NONE' and
     c                             %trim(p_dir) <> '*BLANK'
     c                   eval      directory = mkstr(%trim(p_dir))
     c                   callp     setdir(uz:directory)
     c                   endif
      *
     c                   eval      antall = dezip(uz)
      *
     c                   eval      p_ant = antall
      *
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_zip
     c                   parm                    p_dir
     c                   parm                    p_log
     c                   parm                    p_ant
      *
     c                   endsr
