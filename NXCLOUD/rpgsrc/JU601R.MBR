     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JU601R
      * Beskrivelse . . : Modultekst, utskrift
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       020200 HKO  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjmtxl1    if   e           k disk    rename(jmtxpfr:jmxtl1r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjmodl1    if   e           k disk    rename(jmodpfr:jmodl1r)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
      *
     fju601p    o    e             printer
      *
      * Definering av parametre
      *
     d p_list          s             32
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
      *
      * Definering av datastruktur, parameterliste -inn
      *
     d                 ds
     d d_list                        32
     d  d_firm                        3  0 overlay(d_list:1)
     d  d_vare                       15    overlay(d_list:4)
      *
      * Definering av variabler i nøkler
      *
     d jmtxl1_tmod     s                   like(jutmod)
     d jvarl1_vare     s                   like(jvvare)
     d jmodl1_modn     s                   like(jumodn)
      *
      * Definering av variable
      *
     d w_timestamp     s             12  0
      *
     d w_firm          s              3  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initiering og utskrift av hode
      *
     c                   exsr      hode
      *
      * Behandler og skriver detaljlinjer
      *
     c                   exsr      behandling
      *
      * Avslutt
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av hode-opplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode          begsr
      *
      * Legger inn arb.stasjon og bruker i faste felter
      *
     c                   time                    w_timestamp
     c                   move      w_timestamp   a1date
      *
      * Henter firmaopplysninger
      *
     c     afirl1_key    chain     afirl1r                            90
     c                   eval      a1fna1 = aafnav
     c                   eval      a1fna2 = aafnav
      *
      * Henter vareinformasjon
      *
     c                   eval      jvarl1_vare = d_vare
     c     jvarl1_key    chain     jvarl1                             90
     c                   eval      a1vtxt = jvtek1
      *
      * Henter modulinformasjon
      *
     c                   eval      jmodl1_modn = jvmodn
     c     jmodl1_key    chain     jmodl1                             90
     c                   eval      a1mtxt = jumte1
      *
      * Initierer hode
      *
     c                   eval      a1vare = d_vare
     c                   eval      a1vtxt = jvtek1
     c                   eval      a1modn = jvmodn
     c                   eval      a1nobb = jvnobb
      *
      * Skriver header
      *
     c                   write     a1hdr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
      * Leser modultekster for vare, og skriver detaljlinje
      *
     c                   eval      jmtxl1_tmod = jvmodn
     c     jmtxl1_key    setll     jmtxl1
     c     jmtxl1_key    reade     jmtxl1                                 90
     c                   dow       *in90 = *off
      *
     c                   exsr      detalj
      *
     c     jmtxl1_key    reade     jmtxl1                                 90
     c                   enddo
      *
     c     end_behandl   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine utskrift av detaljlinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     detalj        begsr
      *
     c                   eval      d1ttxt = juttxt
      *
      * Skriv detaljlinje
      *
     c                   write     d1dtl                                30
     c                   exsr      overflow
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for håndtering av overflow
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     overflow      begsr
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_list
      *
      * Key for les på modultekst
      *
     c     jmtxl1_key    klist
     c                   kfld                    jmtxl1_tmod
      *
      * Key for oppslag på vare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for oppslag på modul
      *
     c     jmodl1_key    klist
     c                   kfld                    jmodl1_modn
      *
      * Key for oppslag på firma
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      *
      * Kopierer parameterliste til datastruktur
      *
     c                   eval      d_list = p_list
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = d_firm
      *
     c                   endsr
