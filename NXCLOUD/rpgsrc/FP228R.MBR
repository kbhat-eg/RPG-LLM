      /TITLE  ASOFAK: Skriver talletiketter
     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FP228R
      * Beskrivelse . . : Skriver talletiketter
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       020799 KOB  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvaw1pf    if   f  512    64aidisk
      *
     fEtikett   o    f  132        Printer oflind(*inof)
     *
     *+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     *               :  LOCAL DATA AREA                                 +
     *+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     *
     D                uds
     *
     d lcskty                110    110  0
     d lcuser                911    920
     d lcfirm                944    946  0
     d l_fnav                951    980
     *
      * Definering av variable
      *
     d w_skty          s                   like(lcskty)
      *
     d w_anta_1        s              1  0
     d w_anta_2        s              2  0
     d w_anta_3        s              3  0
      *
     ivaw1pf    ns  01
      *
     i                                  1    4 0vaanta
     i                                  5    6  vatal1
     i                                  7   11  vatal2
      *
     i                                256  256  vadumy
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   read      vaw1pf                                 61
      *
     c                   dow       *in61 = *off
      *
     c                   if        *in60 = *off
      *
      * Hent skriverinformasjon
      *
     c                   eval      w_skty = lcskty
      *
     c     w_skty        ifeq      0
     c                   except    Æclr0
     c                   except    Æpgm0
     c                   endif
      *
     C     w_skty        ifeq      1
     c                   except    Æclr1
     c                   except    Æpgm1
     c                   endif
      *
     c     w_skty        ifeq      2
     c                   except    Æpgm2
     c                   endif
      *
     c                   eval      *in60 = *on
      *
     c                   endif
      *
      * Skriv avhengig av skrivertype
      *
     c     w_skty        ifeq      0
     c                   except    Ædata0
     c                   endif
      *
     c     w_skty        ifeq      1
     c                   except    Ædata1
     c                   endif
      *
     c     w_skty        ifeq      2
      *
     c                   eval      *in81 = *off
     c                   eval      *in82 = *off
     c                   eval      *in83 = *off
      *
     c                   select
     c                   when      vaanta <= 9
     c                   eval      w_anta_1 = vaanta
     c                   eval      *in81 = *on
     c                   when      vaanta <= 99
     c                   eval      w_anta_2 = vaanta
     c                   eval      *in82 = *on
     c                   when      vaanta <= 500
     c                   eval      w_anta_3 = vaanta
     c                   eval      *in83 = *on
     c                   other
     c                   eval      w_anta_3 = 500
     c                   eval      *in83 = *on
     c                   endsl
      *
     c                   except    Ædata2
     c                   endif
      *
     c                   read      vaw1pf                                 61
     c                   enddo
      *
     c                   goto      avslutt
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c     w_skty        ifeq      2
     c                   except    Æend2
     c                   endif
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c                   endsr
      *
      * Twinax-skriver
      *                                                                ***
      *
      * Halo printer here am I, are you ready?
      *
     oEtikett   e            Æclr0
     O                                              '&02&20&03'
      *
     *Clear PGM-buffer 01
      *
     O          e            Æclr0
     O                                              '&02&5401&03'
      *
     * Skriv program
      *
     O          e            Æpgm0
     O                                              '&02&4401022'
     O                                              '&04'
      *
      * Tall
      *
     O          e            Æpgm0
     O                                              '&02'
     O                                              '&0D004004002036722'
     O                                              '&04'
      *
      * Tall i strekkode
      *
     O          e            Æpgm0
     O                                              '&02'
     O                                              '&0D0200040050200215200'
     O                                              '&04'
      *
      * Avslutt
      *
     O          e            Æpgm0
     O                                              '&02'
     O                                              '&0D'
     O                                              '&0D'
     O                                              '&0D'
     O                                              '&03'
      *
     * Skriv data
      *
     O          e            Ædata0
     O                                              '&02&4300000101&0D'
     O                                              '&04'
      *
      * Tall
      *
     O          e            Ædata0
     O                                              '&02'
     O                       vatal1
     O                                              '&0D'
     O                                              '&04'
      *
      * Tall i strekkode
      *
     O          e            Ædata0
     O                                              '&02'
     O                       vatal2
     O                                              '&0D'
     O                                              '&04'
      *
      * Avslutt
      *
     O          e            Ædata0
     O                                              '&02'
     O                                              '&0D'
     O                                              '&03'
      *
     * Antall
      *
     O          e            Ædata0
     O                                              '&02&66'
     O                       vaanta
     O                                              '&03'
      *
      * Behandle standard-skriver
      *
      *
     * Halo printer here am I, are you ready?
      *
     OEtikett   e            Æclr1
     O                                              '{ & } '
     O                                              '{&S000001} '
      *
     * Clear PGM-buffer 01
      *
     O          e            Æclr1
     O                                              '{&T01} '
      *
     * Skriv program
      *
     O          e            Æpgm1
     O                                              '{&D01022'
     O                                              '! '
      *
      * Tall
      *
     O          e            Æpgm1
     O                                              '{'
     O                                              '\004004002036722'
     O                                              '! '
      *
      * Tall i strekkode
      *
     O          e            Æpgm1
     O                                              '{'
     O                                              '\0200040050200215200'
     O                                              '! '
      *
      * Avslutt
      *
     O          e            Æpgm1
     O                                              '{'
     O                                              '\'
     O                                              '\'
     O                                              '\'
     O                                              '} '
      *
     * Skriv data
      *
     O          e            Ædata1
     O                                              '{&C00000101\'
     O                                              '! '
      *
      * Tall
      *
     O          e            Ædata1
     O                                              '{'
     O                       vatal1
     O                                              '\'
     O                                              '! '
      *
      * Takk i strekkode
      *
     O          e            Ædata1
     O                                              '{'
     O                       vatal2
     O                                              '\'
     O                                              '! '
      *
      * Avslutt
      *
     O          e            Ædata1
     O                                              '{'
     O                                              '\'
     O                                              '} '
      *
     * Antall
      *
     O          e            Ædata1
     O                                              '{&f'
     O                       vaanta
     O                                              '} '
      *
      * Behandle Bandit-skriver
      *                                                                ***
     oEtikett   e            Æpgm2
     O                                              '{'
      *
     O          e            Æpgm2
     O                                              '[Ct "0"]'
      *
     O          e            Æpgm2
     O                                              '[N m "m"]'
      *
     O          e            Æpgm2
     O                                              '[M n "swpaper"]'
      *
     O          e            Æpgm2
     O                                              '[M w "49.0:0:0:1"]'
      *
     O          e            Æpgm2
     O                                              '[M l "68.0:0:0:1:direct '
     O                                              'table 100"]'
      *
     O          e            Æpgm2
     O                                              '[M c "0"]'
      *
     O          e            Æpgm2
     O                                              '[M m "0:00:1"]'
      *
     O          e            Æpgm2
     O                                              '[M a "swpaper"]'
      *
     O          e            Æpgm2
     O                                              '[D W "1" "8" "3" '
     O                                              '"swpaper" "0,0:0,0" "\N"'
      *
      * Avslutt innhold
      *
     O          e            Æpgm2
     O                                              '"\N"]'
      *
      * Etiketter
      *
     O          e            Ædata2
     O                                              '[D W "1" "8" "3" '
     O                                              '"swpaper" "0,0:0,0" "\N"'
      *
      * Tall
      *
     O          e            Ædata2
     O                                              '"C" "07.0:12.0:1" "90" '
     O                                              '"15531807:13" "15:0" '
     O                                              '"" "" "1:1:2:1:1" "\N"'
      *
      * Tall i strekkode
      *
     O          e            Ædata2
     O                                              '"C" "07.0:12.0:1" "90" '
     O                                              '"15531807:13" "15:0" '
     O                                              '"" "" "1:1:2:1:1" "\N"'
      *
      * Avslutt program
      *
     O          e            Ædata2
     O                                              '"\N"]'
      *
      * Start innhold
      *
     O          e            Ædata2
     O                                              '[ D w "LLU" "1" "\N"'
      *
      * Tall
      *
     O          e            Ædata2
     O                                              '"'
     O                       vatal1
     O                                              '"'
      *
      * Tall i strekkode
      *
     O          e            Ædata2
     O                                              '"'
     O                       vatal2
     O                                              '"'
      *
      * Avslutt innhold
      *
     O          e            Ædata2
     O                                              '"\N"]'
      *
      * Antall etiketter
      *
     O          e            Ædata2
     O                                            6 '[P b "'
     O               81      w_anta_1             7
     O               82      w_anta_2             8
     O               83      w_anta_3             9
     O               81                          19 ':1:swpaper"]'
     O               82                          20 ':1:swpaper"]'
     O               83                          21 ':1:swpaper"]'
      *
     O          e            Æend2
     O                                              '}'
