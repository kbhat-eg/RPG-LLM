     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : vt712R
      * Beskrivelse . . : Tilbudspris, innlesing fra fil - SDV
      *
      *
      * Programmet leser valgt fil, og oppretter tilbudspriser
      * er på Nobb prisformat.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       040619 BOF  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fflvwpf    uf   e           k disk    rename(flvwpfr:flvwpfr)
      *
     fvvenl2    if   e           k disk    rename(vvenpfr:vvenl2r)
     fvvarlr    if   e           k disk    rename(vvarpfr:vvarlrr)
     fvklulu    uf a e           k disk    rename(vklupfr:vklulur)

      *
      * Definering av Local data area *LDA
      *
     d                uds
     d l_user                911    920
6.31 d l_filg                931    933
      *
      * Definering av Nobb prisformat
      *
     d                 ds
6.31 d d_list                       100
     d  d_vare                       15    overlay(d_list:1)
     d  d_enhe                        3    overlay(d_list:16)
     d  d_kopr                       10    overlay(d_list:19)
     d   d_kopr_kr                    7  0 overlay(d_kopr:1)
     d   d_kopr_Ore                   2  2 overlay(d_kopr:9)
     d  d_tilb                       10    overlay(d_list:29)
     d   d_tilb_kr                    7  0 overlay(d_tilb:1)
     d   d_tilb_Ore                   2  2 overlay(d_tilb:9)
     d  d_inpr                       10    overlay(d_list:39)
     d   d_inpr_kr                    7  0 overlay(d_inpr:1)
     d   d_inpr_Ore                   2  2 overlay(d_inpr:9)
     d  d_kfda                        6  0 overlay(d_list:49)
     d  d_ktda                        6  0 overlay(d_list:55)
     d  d_kamp                        5    overlay(d_list:61)
6.31 d  d_kprg                        1    overlay(d_list:66)
6.31 d  d_ldor                        6    overlay(d_list:67)
     d    d_ldorn                     6  0 overlay(d_ldor:1)
6.31 d  d_ogrp                        2    overlay(d_list:73)
     d    d_ogrpn                     2  0 overlay(d_ogrp:1)
6.31 d  d_hgrp                        2    overlay(d_list:75)
     d    d_hgrpn                     2  0 overlay(d_hgrp:1)
6.31 d  d_ugrp                        3    overlay(d_list:77)
     d    d_ugrpn                     3  0 overlay(d_ugrp:1)
     d  d_pros                        5    overlay(d_list:80)
     d   d_pros_kr                    2  0 overlay(d_pros:1)
     d   d_pros_Ore                   2  2 overlay(d_pros:4)
      *
     d w_kr            s              7
     d w_kr2           s              2
     d w_ore           s              2
     d w_pris          s              9
     d w_enhe          s                   like(veenhe)
     d w_ken1          s                   like(veenhe)
     d w_ken2          s                   like(veenhe)
     d w_ken3          s                   like(veenhe)
     d w_omr1          s                   like(veomre)
     d w_omr2          s                   like(veomre)
     d w_omr3          s                   like(veomre)
     d w_kti1          s                   like(vtkti1)
     d w_kti2          s                   like(vtkti1)
     d w_kti3          s                   like(vtkti1)
     d w_kko1          s                   like(vtkti1)
     d w_kko2          s                   like(vtkti1)
     d w_kko3          s                   like(vtkti1)
6.21 d w_kin1          s                   like(vtkti1)
6.21 d w_kin2          s                   like(vtkti1)
6.21 d w_kin3          s                   like(vtkti1)

      *
      * Definering av parametre
      *
     d p_firm          s                   like(vtkfir)
      *
      * Definering av variable i nøkler
      *
     d vvarlr_vare     s                   like(vvvare)
6.20 d vklulu_kogr     s                   like(vtkogr)
6.20 d vklulu_khgr     s                   like(vtkhgr)
6.20 d vklulu_kugr     s                   like(vtkugr)
6.20 d vklulu_kldo     s                   like(vtkldo)
     d vklulu_klet     s                   like(vtklet)
     d vklulu_kvar     s                   like(vtkvar)
5.70 d vklulu_kprg     s                   like(vtkprg)
     d vklulu_kfda     s                   like(vtkfda)
     d vklulu_ftim     s                   like(vtkfti)
     d vklulu_ktda     s                   like(vtktda)
     d vklulu_ttim     s                   like(vtktti)
     d vvenl2_kvar     s                   like(vtkvar)
     d vvenl2_saen     s                   like(vesaen)
      *
      * Definering av variable
      *
     d                 ds
     d d_dato                        10
     d  d_dd                          2    overlay(d_dato:1)
     d  d_mm                          2    overlay(d_dato:4)
     d  d_aa                          4    overlay(d_dato:7)
     d                 ds
     d w_dat6                         6
     d  w_dd                          2    overlay(w_dat6:1)
     d  w_mm                          2    overlay(w_dat6:3)
     d  w_aa                          2    overlay(w_dat6:5)
      *
     d w_firm          s                   like(vtkfir)
     d w_posi          s              2  0
     d st              s              2  0
     d len             s              2  0
     d w_2x            s              2
     d w_3x            s              3
     d w_6x            s              6
     d w_anta          s              6  0
     d w_inpr          s              9  2
     d w_kopr          s              9  2
     d w_tilb          s              9  2
     d w_pros          s              4  2

      *
     d b_feil          s              1    inz(*off)
      *
      *
      * Konstanter
      *
     d Lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d Up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
     d c_digi          s             15    inz('0123456789 ')
     d c_null          s             15    inz('000000000000000')
      *
      * Definering av konstanter
6.32 C/Exec SQL
6.32 C+  Set Option DatFmt=*ISO
6.32 C/End-Exec
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Dersom sletting er valgt, slettes alle eksisterende spesialpriser
      * for rabattkategori eller kunde/kundeprosjekt
      *
      *
      * Leser og oppretter/oppdaterer spesialpriser
      *
     c                   read      flvwpfr
     c                   dow       not %eof
     c                   exsr      utled_sdv
     c                   if        b_feil = *off
     c                   exsr      oppdater
     c                   endif
     c                   read      flvwpfr
     c                   enddo
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utleding av datastruktur
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     utled_sdv     begsr
      *
     c                   eval      d_list = *blank
     c                   eval      b_feil = *off
      *
     c     '"':' '       xlate     fwirad        fwirad
      *
      * varenr.
      *
     c                   eval      w_posi = %scan(';':fwirad)
     c                   eval      d_vare = %trim(%subst(fwirad:1:w_posi-1))
     c     c_digi        check     d_vare                                 70
     c                   if        *in70 = *on
     c                   eval      b_feil = *on
     c                   goto      end_utled
     c                   endif
      *
      * ldor
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
     c                   eval      d_ldor = %trim(%subst(fwirad:1:w_posi-1))
     c                   eval      len    = %len(%trim(d_ldor))
     c                   eval      st = 7 - len
     c                   eval      w_6x  = *blank
     c                   eval      %subst(w_6x:st:len) = %subst(d_ldor:1:len)
     c                   eval      d_ldor = w_6x
     c                   if        %check(c_digi : d_ldor) <>0
     c                   leavesr
     c                   endif
     c                   eval      d_ldor = %xlate(' ':'0':d_ldor)
      *
      * ogrp
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
     c                   eval      d_ogrp = %trim(%subst(fwirad:1:w_posi-1))
     c                   eval      len    = %len(%trim(d_ogrp))
     c                   eval      st = 3 - len
     c                   eval      w_2x  = *blank
     c                   eval      %subst(w_2x:st:len) = %subst(d_ogrp:1:len)
     c                   eval      d_ogrp = w_2x
     c                   if        %check(c_digi : d_ogrp) <>0
     c                   leavesr
     c                   endif
     c                   eval      d_ogrp = %xlate(' ':'0':d_ogrp)
      *
      * hgrp
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
     c                   eval      d_hgrp = %trim(%subst(fwirad:1:w_posi-1))
     c                   eval      len    = %len(%trim(d_hgrp))
     c                   eval      st = 3 - len
     c                   eval      w_2x  = *blank
     c                   eval      %subst(w_2x:st:len) = %subst(d_hgrp:1:len)
     c                   eval      d_hgrp = w_2x
     c                   if        %check(c_digi : d_hgrp) <>0
     c                   leavesr
     c                   endif
     c                   eval      d_hgrp = %xlate(' ':'0':d_hgrp)
      *
      * ugrp
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
     c                   eval      d_ugrp = %trim(%subst(fwirad:1:w_posi-1))
     c                   eval      len    = %len(%trim(d_ugrp))
     c                   eval      st = 4 - len
     c                   eval      w_3x  = *blank
     c                   eval      %subst(w_3x:st:len) = %subst(d_ugrp:1:len)
     c                   eval      d_ugrp = w_3x
     c                   if        %check(c_digi : d_hgrp) <>0
     c                   leavesr
     c                   endif
     c                   eval      d_ugrp = %xlate(' ':'0':d_ugrp)
      *
      * enhet
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
     c                   eval      d_enhe = %trim(%subst(fwirad:1:w_posi-1))
6.31 c     X'46':'Æ'     xlate     d_enhe        d_enhe
6.31 c     X'77':'Ø'     xlate     d_enhe        d_enhe
6.31 c     X'2A':'Å'     xlate     d_enhe        d_enhe
6.31 c     X'EF':'Å'     xlate     d_enhe        d_enhe
6.31 c     X'04':X'FC'   xlate     d_enhe        d_enhe
6.31 c     X'90':'Ø'     xlate     d_enhe        d_enhe
6.31  *
6.21  * Innkjøpspris
6.21  *
6.21 c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
6.21 c                   eval      w_posi = %scan(';':fwirad)
6.21 c                   eval      d_inpr = %trim(%subst(fwirad:1:w_posi-1))
      *
      *
      * Kostpris
      *
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
     c                   eval      d_inpr = %trim(%subst(fwirad:1:w_posi-1))
      *
      * Tilbudspris
      *
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
     c                   eval      d_tilb = %trim(%subst(fwirad:1:w_posi-1))
      *
      * Prosent
      *
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
     c                   eval      d_pros = %trim(%subst(fwirad:1:w_posi-1))
      * fra dato
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
     c                   eval      d_dato = %trim(%subst(fwirad:1:w_posi-1))
     c                   exsr      konv_dato
     c                   move      w_dat6        d_kfda
      * til dato
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
     c                   if        w_posi = 0
     c                   eval      d_dato = %trim(%subst(fwirad:1:10))
     c                   else
     c                   eval      d_dato = %trim(%subst(fwirad:1:w_posi-1))
     c                   endif
     c                   exsr      konv_dato
     c                   move      w_dat6        d_ktda
      * kkubbkamp.
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      d_kamp = %trim(%subst(fwirad:1:5))
     c     X'46':'Æ'     xlate     d_kamp        d_kamp
     c     X'A0':'æ'     xlate     d_kamp        d_kamp
     c     X'2A':'Å'     xlate     d_kamp        d_kamp
     c     X'EF':'å'     xlate     d_kamp        d_kamp
     c     X'77':'Ø'     xlate     d_kamp        d_kamp
     c     X'90':'ø'     xlate     d_kamp        d_kamp
     c     lo:up         xlate     d_kamp        d_kamp
      *
      * utleder innkjøpspris
     c                   if        d_inpr <> *blank
      *
     c                   eval      d_inpr = %trim(d_inpr)
     c                   eval      w_posi = %scan(',':d_inpr)
     c                   eval      len = %len(%trim(d_inpr))
      *
     c                   if        w_posi = 0
     c                   eval      w_posi = len + 1
     c                   eval      d_inpr = %trim(d_inpr) + '.00'
     c                   endif
      *
     c     ' ':'0'       xlate     d_inpr        d_inpr
      *
     c                   if        (w_posi-1) < 7
     c                   eval      d_inpr = %subst(c_null:1:7-w_posi+1) +
     c                                      %subst(d_inpr:1:w_posi-1) +
     c                                      '.' +
     c                                      %subst(d_inpr:w_posi+1:2)
     c                   endif
      *
     c                   else
     c                   eval      d_inpr = '0000000.00'
     c                   endif
     c                   eval      w_inpr = d_inpr_kr + d_inpr_ore
      * utleder kostpris
     c                   if        d_kopr <> *blank
      *
     c                   eval      d_kopr = %trim(d_kopr)
     c                   eval      w_posi = %scan(',':d_kopr)
     c                   eval      len = %len(%trim(d_kopr))
      *
     c                   if        w_posi = 0
     c                   eval      w_posi = len + 1
     c                   eval      d_inpr = %trim(d_kopr) + '.00'
     c                   endif
      *
     c     ' ':'0'       xlate     d_kopr        d_kopr
      *
     c                   if        (w_posi-1) < 7
     c                   eval      d_kopr = %subst(c_null:1:7-w_posi+1) +
     c                                      %subst(d_kopr:1:w_posi-1) +
     c                                      '.' +
     c                                      %subst(d_kopr:w_posi+1:2)
     c                   endif
      *
     c                   else
     c                   eval      d_kopr = '0000000.00'
     c                   endif
     c                   eval      w_kopr = d_kopr_kr + d_kopr_ore
      * utleder tilbudspris
     c                   if        d_tilb <> *blank
      *
     c                   eval      d_tilb = %trim(d_tilb)
     c                   eval      w_posi = %scan(',':d_tilb)
     c                   eval      len = %len(%trim(d_tilb))
      *
     c                   if        w_posi = 0
     c                   eval      w_posi = len + 1
     c                   eval      d_tilb = %trim(d_tilb) + '.00'
     c                   endif
      *
     c     ' ':'0'       xlate     d_tilb        d_tilb
      *
     c                   if        (w_posi-1) < 7
     c                   eval      d_tilb = %subst(c_null:1:7-w_posi+1) +
     c                                      %subst(d_tilb:1:w_posi-1) +
     c                                      '.' +
     c                                      %subst(d_tilb:w_posi+1:2)
     c                   endif
      *
     c                   else
     c                   eval      d_tilb = '0000000.00'
     c                   endif
     c                   eval      w_tilb = d_tilb_kr + d_tilb_ore
      * utleder prosent
     c                   if        d_pros <> *blank
      *
     c                   eval      d_pros = %trim(d_pros)
     c                   eval      w_posi = %scan(',':d_pros)
     c                   eval      len = %len(%trim(d_pros))
      *
     c                   if        w_posi = 0
     c                   eval      w_posi = len + 1
     c                   eval      d_pros = %trim(d_pros) + '.00'
     c                   endif
      *
     c     ' ':'0'       xlate     d_pros        d_pros
      *
     c                   if        (w_posi-1) < 2
     c                   eval      d_pros = %subst(c_null:1:7-w_posi+1) +
     c                                      %subst(d_pros:1:w_posi-1) +
     c                                      '.' +
     c                                      %subst(d_pros:w_posi+1:2)
     c                   endif
      *
     c                   else
     c                   eval      d_pros = '0000000.00'
     c                   endif
     c                   eval      w_pros = d_pros_kr + d_pros_ore


      *
      *
     c     end_utled     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for konvertere dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     konv_dato     begsr
      *
     c                   eval      w_dd = d_dd
     c                   eval      w_mm = d_mm
     c                   move      d_aa          w_aa
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting/oppdatering av spesialpriser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdater      begsr
      *
      *
      * Avbryter dersom vare ikke finnes
      *
     c                   if        d_vare <> *Blank
     c                   eval      vvarlr_vare = d_vare
     c     vvarlr_key    chain     vvarlr
     c                   if        not %found
     c                   leavesr
     c                   endif
     c                   endif
      *
      * Avbryter dersom leverandør ikke finnes
      *
     c                   if        d_ldorn <> 0
     c                   eval      w_Anta = 0
6.31 c/exec sql
6.31 c+ select count(*)
6.31 c+ into   :w_anta
6.31 c+ from   rlevpf
6.31 c+ where  rllevr = :d_ldorn
6.31 c/end-exec
     c                   if        w_anta = 0
     c                   leavesr
     c                   endif
     c                   endif
      *
      * Avbryter dersom varegruppe ikke finnes
      *
     c                   if        d_ogrpn <> 0   and
     c                             d_hgrpn <> 0   and
     c                             d_ugrpn <> 0
     c                   eval      w_Anta = 0
6.31 c/exec sql
6.31 c+ select count(*)
6.31 c+ into   :w_anta
6.31 c+ from   vugrpf
6.31 c+ where  vguogr = :d_ogrpn and
     c+        vguhgr = :d_hgrpn and
     c+        vguugr = :d_ugrpn
6.31 c/end-exec
     c                   if        w_anta = 0
     c                   leavesr
     c                   endif
     c                   endif
      *
      * Avbryter dersom varegruppe ikke finnes
      *
     c                   if        d_ogrpn <> 0   and
     c                             d_hgrpn <> 0   and
     c                             d_ugrpn = 0
     c                   eval      w_Anta = 0
6.31 c/exec sql
6.31 c+ select count(*)
6.31 c+ into   :w_anta
6.31 c+ from   vhgrpf
6.31 c+ where  vghogr = :d_ogrpn and
     c+        vghhgr = :d_hgrpn
6.31 c/end-exec
     c                   if        w_anta = 0
     c                   leavesr
     c                   endif
     c                   endif
      *
      * Avbryter dersom varegruppe ikke finnes
      *
     c                   if        d_ogrpn <> 0   and
     c                             d_hgrpn = 0    and
     c                             d_ugrpn = 0
     c                   eval      w_Anta = 0
6.31 c/exec sql
6.31 c+ select count(*)
6.31 c+ into   :w_anta
6.31 c+ from   vogrpf
6.31 c+ where  vgoogr = :d_ogrpn
6.31 c/end-exec
     c                   if        w_anta = 0
     c                   leavesr
     c                   endif
     c                   endif
      *alle begreper på varegruppe må være fylt ut hvis undergruppe
     c                   if        d_ogrpn <> 0 and
     c                             (d_hgrpn = 0 or
     c                              d_ogrpn = 0)
     c                   leavesr
     c                   endif
     c                   if        d_hgrpn <> 0 and
     c                              d_ogrpn = 0
     c                   leavesr
     c                   endif

      *
      * Avbryter dersom datoer ikke er gyldig
      *
     c     *dmy          test(d)                 d_kfda                 32
     c                   if        *in32 = *on
     c                   leavesr
     c                   endif
      *
     c     *dmy          test(d)                 d_ktda                 32
     c                   if        *in32 = *on
     c                   leavesr
     c                   endif
      *
      * Henter kvarnes kvarenhet (pakningskode '000' i LVR)
      *
     c*                  eval      w_enhe = *blank
      *
     c*                  call      'FO712C'
     c*                  parm                    w_firm
     c*                  parm                    vvkvar
     c*                  parm                    w_enhe
      *
     c*                  if        w_enhe = *blank
     c*                  call      'FO713C'
     c*                  parm                    w_firm
     c*                  parm                    vvnobb
     c*                  parm                    w_enhe
     c*                  endif
      *
      * når vare, så må enhet være utfylt
     c                   if        d_enhe = *blank and
     c                             d_vare <> *blank
     c                   leavesr
     c                   endif
      * nullstill
     c                   eval      w_ken1 = *blank
     c                   eval      w_ken2 = *blank
     c                   eval      w_ken3 = *blank
     c                   eval      w_kti1 = 0
     c                   eval      w_kti2 = 0
     c                   eval      w_kti3 = 0
     c                   eval      w_kko1 = 0
     c                   eval      w_kko2 = 0
     c                   eval      w_kko3 = 0
     c                   eval      w_kin1 = 0
     c                   eval      w_kin2 = 0
     c                   eval      w_kin3 = 0
      *
      * Henter tre salgsenheter fra kvarn
      *
     c                   if        d_Enhe <> *Blank
      * kun når enhet er utfylt skal det skje omregning
     c                   eval      w_ken1 = *blank
     c                   eval      w_ken2 = *blank
     c                   eval      w_ken3 = *blank
     c                   eval      w_omr1 = 0
     c                   eval      w_omr2 = 0
     c                   eval      w_omr3 = 0
      *
     c                   eval      vvenl2_kvar = vvvare
     c                   eval      vvenl2_saen = 1
     c     vvenl2_key    setll     vvenl2
     c     vvenl2_key2   reade     vvenl2
     c                   dow       not %eof
      *
     c                   select
     c                   when      w_ken1 = *blank
     c                   eval      w_ken1 = veenhe
     c                   eval      w_omr1 = veomre
     c                   when      w_ken1 <> *blank and
     c                             w_ken2 = *blank
     c                   eval      w_ken2 = veenhe
     c                   eval      w_omr2 = veomre
     c                   when      w_ken1 <> *blank and
     c                             w_ken2 <> *blank and
     c                             w_ken3 = *blank
     c                   eval      w_ken3 = veenhe
     c                   eval      w_omr3 = veomre
     c                   leave
     c                   endsl
      *
     c     vvenl2_key2   reade     vvenl2
     c                   enddo
      *
      * Avbryter dersom enhet ikke finnes på kvarn
      *
     c                   if        w_ken1 <> d_enhe and
     c                             w_ken2 <> d_enhe and
     c                             w_ken3 <> d_enhe
     c                   leavesr
     c                   endif
      *
      * Finner salgspris for alle salgsenheter
      *
     c                   eval      w_kti1 = 0
     c                   eval      w_kti2 = 0
     c                   eval      w_kti3 = 0
      *
     c                   select
     c                   when      d_enhe = w_ken1
     c                   eval      w_kti1 = w_tilb
     c                   eval      w_kti2 = w_kti1 * w_omr2 / w_omr1
     c                   eval      w_kti3 = w_kti1 * w_omr3 / w_omr1
     c                   eval      w_kko1 = w_kopr
     c                   eval      w_kko2 = w_kko1 * w_omr2 / w_omr1
     c                   eval      w_kko3 = w_kko1 * w_omr3 / w_omr1
6.21 c                   eval      w_kin1 = w_inpr
6.21 c                   eval      w_kin2 = w_kin1 * w_omr2 / w_omr1
6.21 c                   eval      w_kin3 = w_kin1 * w_omr3 / w_omr1
     c                   when      d_enhe = w_ken2
     c                   eval      w_kti2 = w_tilb
     c                   eval      w_kti1 = w_kti2 * w_omr1 / w_omr2
     c                   eval      w_kti3 = w_kti2 * w_omr3 / w_omr2
     c                   eval      w_kko2 = w_kopr
     c                   eval      w_kko1 = w_kko2 * w_omr1 / w_omr2
     c                   eval      w_kko3 = w_kko2 * w_omr3 / w_omr2
6.21 c                   eval      w_kin2 = w_inpr
6.21 c                   eval      w_kin1 = w_kin2 * w_omr1 / w_omr2
6.21 c                   eval      w_kin3 = w_kin2 * w_omr3 / w_omr2
     c                   when      d_enhe = w_ken3
     c                   eval      w_kti3 = w_tilb
     c                   eval      w_kti1 = w_kti3 * w_omr1 / w_omr3
     c                   eval      w_kti2 = w_kti3 * w_omr2 / w_omr3
     c                   eval      w_kko3 = w_kopr
     c                   eval      w_kko1 = w_kko3 * w_omr1 / w_omr3
     c                   eval      w_kko2 = w_kko3 * w_omr2 / w_omr3
6.21 c                   eval      w_kin3 = w_inpr
6.21 c                   eval      w_kin1 = w_kin3 * w_omr1 / w_omr3
6.21 c                   eval      w_kin2 = w_kin3 * w_omr2 / w_omr3
     c                   endsl
     c                   endif
      *
     c                   clear                   vklulur
      *
     c                   eval      vklulu_kvar = d_vare
5.70 c                   eval      vklulu_kprg = *blank
6.20 c                   eval      vklulu_kogr = d_ogrpn
6.20 c                   eval      vklulu_khgr = d_hgrpn
6.20 c                   eval      vklulu_kugr = d_ugrpn
6.20 c                   eval      vklulu_kldo = d_ldorn
     c                   eval      vklulu_klet = 0
     c     *dmy          move      d_kfda        vklulu_kfda
     c                   eval      vklulu_ftim = *loval
     c     *dmy          move      d_ktda        vklulu_ktda
     c                   eval      vklulu_ttim = *loval
     c     vklulu_key    chain     vklulur
      *
     c                   eval      vtkkam = d_kamp
     c                   eval      vtken1 = w_ken1
     c                   eval      vtkti1 = w_kti1
     c                   eval      vtkko1 = w_kko1
6.21 c                   eval      vtkin1 = w_kin1
     c                   eval      vtken2 = w_ken2
     c                   eval      vtkti2 = w_kti2
     c                   eval      vtkko2 = w_kko2
6.21 c                   eval      vtkin2 = w_kin2
     c                   eval      vtken3 = w_ken3
     c                   eval      vtkti3 = w_kti3
6.21 c                   eval      vtkin3 = w_kin3
     c                   eval      vtkko3 = w_kko3
     c                   eval      vtkpr1 = w_pros
      *
     c                   eval      vtkeus = l_user
     c                   time                    vtkeda
     c                   time                    vtketi
      *
     c                   if        %found(vklulu)
     c                   update    vklulur
     c                   else
     c                   eval      vtkfir = w_firm
     c                   eval      vtkvar = vklulu_kvar
     c                   eval      vtkprg = vklulu_kprg
     c                   eval      vtkogr = vklulu_kogr
     c                   eval      vtkhgr = vklulu_khgr
     c                   eval      vtkugr = vklulu_kugr
     c                   eval      vtkldo = vklulu_kldo
     c                   eval      vtklet = vklulu_klet
     c                   eval      vtkfda = vklulu_kfda
     c                   eval      vtktda = vklulu_ktda
     c                   eval      vtkfti = vklulu_ftim
     c                   eval      vtktti = vklulu_ttim
      *
     c                   eval      vtkous = l_user
     c                   time                    vtkoda
     c                   time                    vtkoti
      *
     c                   write     vklulur
     c                   endif
      *
      * Sletter rad i arbeidsfil
      *
     c                   delete    flvwpfr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *
     c     *entry        plist
     c                   parm                    p_firm
      *
      *
      * Key for oppslag på kvar
      *
     c     vvarlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlr_vare
      *
     c     vklulu_key    klist
     c                   kfld                    w_firm
6.20 c                   kfld                    vklulu_kogr
6.20 c                   kfld                    vklulu_khgr
6.20 c                   kfld                    vklulu_kugr
6.20 c                   kfld                    vklulu_kldo
     c                   kfld                    vklulu_kvar
5.70 c                   kfld                    vklulu_kprg
     c                   kfld                    vklulu_klet
     c                   kfld                    vklulu_kfda
     c                   kfld                    vklulu_ftim
     c                   kfld                    vklulu_ktda
     c                   kfld                    vklulu_ttim
      *
      * Key for les på kvar-enhet
      *
     c     vvenl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_kvar
     c                   kfld                    vvenl2_saen
      *
     c     vvenl2_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_kvar
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
