     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VD601R
      * Beskrivelse . . : Leser alle varer og sjekker innen type sortiment om
      *                   vare finnes på flere enn et sortiment
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       261110 bof  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvsdtl6    if   e           k disk    rename(vsdtpfr:vsdtl6r)
     fvsdtl1    if   e           k disk    rename(vsdtpfr:vsdtl1r)
     fvshel3    if   e           k disk    rename(vshepfr:vshel3r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
      *
     fvd601p    o    e             printer
      *
      * Definering av Local data area *LDA
      *
     d                uds
     d l_wsid                901    910
     d l_fgrp                931    933
     d l_user                911    920
      *
     d a_vsor          s             10    dim(99) ctdata perrcd(1)
      *
      * Definering av variable i nøkler
      *
     d vshel3_type     s                   like(vctype)
     d vsdtl6_vsor     s                   like(vdvsor)
     d vsdtl6_lage     s                   like(vdlage)
     d vsdtl6_vare     s                   like(vdvare)
     d vsdtl1_vsor     s                   like(vdvsor)
     d vsdtl1_lage     s                   like(vdlage)
     d vsdtl1_ldor     s                   like(vdldor)
     d vsdtl1_ogrp     s                   like(vdogrp)
     d vsdtl1_hgrp     s                   like(vdhgrp)
     d vsdtl1_ugrp     s                   like(vdugrp)
     d vsdtl1_modn     s                   like(vdmodn)
     d vsdtl1_vare     s                   like(vdvare)
     d vvarl1_vare     s                   like(vvvare)
      *
     d b_sess          s              1    inz(*off)
     d b_frst          s              1    inz(*off)
      *
      * Definering av variable
      *
     d w_ant           s              6  0
     d w_firm          s                   like(vdfirm)
     d w_udat          s                   like(vdedat)
     d w_time          s              6  0
     d w_ldor          s                   like(vvldor)
     d w_ogrp          s                   like(vvogrp)
     d w_hgrp          s                   like(vvhgrp)
     d w_ugrp          s                   like(vvugrp)
     d w_modn          s                   like(vvnmod)
     d w_vare          s                   like(vvvare)
      *
     d p_firm          s                   like(vdfirm)
     d p_lage          s                   like(vdlage)
     d p_type          s                   like(vctype)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     vvarl1_key    setll     vvarl1
     c     vvarl1_key    reade     vvarl1
      *
     c                   dow       not %eof(vvarl1)
      *
     c                   eval      w_ant  = 0
     c                   eval      a_vsor(*) = *blank
     c                   eval      d1vare = vvvare
     c                   eval      d1sor1 = *blank
     c                   eval      d1sor2 = *blank
     c                   eval      d1sor3 = *blank
     c                   eval      d1sor4 = *blank
     c                   eval      d1sor5 = *blank
     c                   eval      d1sor6 = *blank
     c                   eval      d1sor7 = *blank
     c                   eval      d1sor8 = *blank
     c                   eval      d1sor9 = *blank
      *
     c                   eval      vshel3_type = p_type
     c     vshel3_key    setll     vshel3
     c     vshel3_key    reade     vshel3
     c                   dow       not %eof(vshel3)
      *
     c                   if        p_lage = 0
     c                   exsr      sjekk_vsor
     c                   else
     c                   if        vclage = p_lage
     c                   exsr      sjekk_vsor
     c                   endif
     c                   endif
      *
     c     vshel3_key    reade     vshel3
     c                   enddo
      *
     c                   if        w_ant > 1
     c                   if        b_frst = *off
     c                   exsr      hode
     c                   eval      b_frst = *on
     c                   endif
     c                   eval      d1sor1 = a_vsor(1)
     c                   eval      d1sor2 = a_vsor(2)
     c                   eval      d1sor3 = a_vsor(3)
     c                   eval      d1sor4 = a_vsor(4)
     c                   eval      d1sor5 = a_vsor(5)
     c                   eval      d1sor6 = a_vsor(6)
     c                   eval      d1sor7 = a_vsor(7)
     c                   eval      d1sor8 = a_vsor(8)
     c                   eval      d1sor9 = a_vsor(9)
      *
     c                   write     d1det1                               30
     c                   if        *in30 = *on
     c                   exsr      overflow
     c                   endif
      *
     c                   endif
      *
     c     vvarl1_key    reade     vvarl1
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sjekke om varen er i sortiment for P - periodestyrte
      * for valgt sortiment og lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_vsor    begsr
      *
      * sjekker først vare
      *
     c                   eval      b_sess = *off
     c                   eval      vsdtl6_vsor = vcvsor
     c                   eval      vsdtl6_lage = vclage
     c                   eval      vsdtl6_vare = vvvare
     c     vsdtl6_key    setll     vsdtl6
     c     vsdtl6_key    reade     vsdtl6
     c                   dow       not %eof(vsdtl6)
      *
     c                   eval      w_ant = w_ant + 1
     c                   eval      a_vsor(w_ant) = vcvsor
      *
     c     vsdtl6_key    reade     vsdtl6
     c                   enddo
     c                   eval      vsdtl1_vsor = vcvsor
     c                   eval      vsdtl1_lage = vclage
     c                   eval      vsdtl1_ldor = vvldor
     c                   eval      vsdtl1_ogrp = vvogrp
     c                   eval      vsdtl1_hgrp = vvhgrp
     c                   eval      vsdtl1_ugrp = vvugrp
     c                   eval      vsdtl1_modn = vvnmod
     c                   eval      vsdtl1_vare = *blank
     c     vsdtl1_key    chain     vsdtl1
     c                   if        not %found
     c                   eval      vsdtl1_modn = 0
     c     vsdtl1_key    chain     vsdtl1
     c                   if        not %found
     c                   eval      vsdtl1_ugrp = 0
     c     vsdtl1_key    chain     vsdtl1
     c                   if        not %found
     c                   eval      vsdtl1_hgrp = 0
     c     vsdtl1_key    chain     vsdtl1
     c                   if        not %found
     c                   eval      vsdtl1_ogrp = 0
     c     vsdtl1_key    chain     vsdtl1
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
     c                   if        not %found
     c                   leavesr
     c                   endif
      *
     c                   eval      w_ant = w_ant + 1
     c                   eval      a_vsor(w_ant) = vcvsor
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * A1HDR Behandle heading A1                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     hode          begsr
      *
      *  Hent tekst for sortiments type
      *
     c                   eval      a1ttxt = *blank
     c                   move      p_type        a1type
      *
     c                   select
     c                   when      p_type = 'C'
     c                   eval      a1ttxt = 'Sentralvarer          '
     c                   when      p_type = 'P'
     c                   eval      a1ttxt = 'Periodevarer          '
     c                   when      p_type = 'V'
     c                   eval      a1ttxt = 'Vareliste             '
     c                   when      p_type = 'H'
     c                   eval      a1ttxt = 'Handleliste nettbutikk'
     c                   endsl
      *
      * Skrive heading
      *
     c                   write     a1hdr                                30
      *
     c                   endsr
     c                                                                                   .....
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  OVFLOW    Behandling av overflow
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     overflow      begsr
      *
      *  Skriver heading
      *
     c                   write     a1hdr                                30
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_lage
     c                   parm                    p_type
      *
      * Key for les av varer fra sortiment
      *
     c     vsdtl6_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vsdtl6_vsor
     c                   kfld                    vsdtl6_lage
     c                   kfld                    vsdtl6_vare
      *
      * Key for les av soritment hode pr. type
      *
     c     vshel3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vshel3_type
      *
      * Key for les/oppslag på varesortiment-linje
      *
     c     vsdtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vsdtl1_vsor
     c                   kfld                    vsdtl1_lage
     c                   kfld                    vsdtl1_ldor
     c                   kfld                    vsdtl1_ogrp
     c                   kfld                    vsdtl1_hgrp
     c                   kfld                    vsdtl1_ugrp
     c                   kfld                    vsdtl1_modn
     c                   kfld                    vsdtl1_vare
      *
      * Key for les av vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   time                    w_udat
     c     *dmy          move      w_udat        a1date
     c                   eval      a1user = l_user
     c                   move      l_wsid        a1wsid
     c                   time                    w_time
     c                   move      w_time        a1time
      *
     c                   endsr
