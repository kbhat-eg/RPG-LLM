     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASSTAT
      * Program . . . . : SX821R
      * Beskrivelse . . : FIX: Oppdaterer SSTAPF med opprinnelig salgspris
      *                   på nye kasser
      *
      *                   Behandler bonger fra den nye kassen hvor oppr.pris
      *                   ikke er utfylt
      *
      *                   Jobben skal kjøres i batch
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       190405 HKO  Nytt program
8.01  *PRG2   220622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner - COMP
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsstal9    uf   e           k disk    rename(sstapfr:sstal9r)
      *
      * Definering av LDA
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d sstal9_paar     s                   like(sfpaar)
      *
      * Definering av variable
      *
     d b_inlr          s              1    inz(*on)
      *
     d w_time          s               t   timfmt(*iso)
     d w_sapr          s              9  2
     d w_bupr          s              9  2
     d w_tipr          s              9  2
     d w_kopr          s              9  2
      *
     d w_firm          s                   like(sffirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      sstal9_paar = 2005
     c     sstal9_key    setll     sstal9r
     c                   read      sstal9r
     c                   dow       not %eof and
     c                             sffirm = w_firm
      *
     c                   exsr      oppdat
      *
     c                   read      sstal9r
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer opprinnelig salgspris i statistikk
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdat        begsr
      *
     c                   if        sfbong = *blank or
     c                             sfvare = *blank or
     c                             sfsumo <> 0
     c                   leavesr
     c                   endif
      *
     c                   if        %scan('-':sfbong) = 0
     c                   leavesr
     c                   endif
      *
      * Henter pris på vare
      *
     c                   call      'VP700R'
     c                   parm                    w_firm
     c                   parm                    sfvare
     c                   parm                    sfenhe
     c                   parm                    sflety
     c                   parm                    sforda
     c                   parm                    w_time
     c                   parm                    b_inlr
     c                   parm                    w_sapr
     c                   parm                    w_bupr
     c                   parm                    w_tipr
     c                   parm                    w_kopr
      *
     c                   eval      sfsumo = w_sapr * sfanta
     c                   update    sstal9r
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for les og oppdatering av statistikk
      *
     c     sstal9_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sstal9_paar
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
