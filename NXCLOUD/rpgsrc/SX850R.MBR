     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASSTAT
      *  Program . . . . : SX850R
      *  Beskrivelse . . : Oppdaterer skyggetabell (statistikk)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign
      * --------- ------  --------------------------------------------------
      *           171014  Nytt program                              BOF
      *           051214  Ved første les, sett dato til gårsdagens  BOF
6.35  *           050215  Feilretting vedr. vaokre (neg.ordre)      BOF
6.3q  *       260515 bof  Datoformat sql
6.36  *           040915  Styrer 1.gang og resten med parameter     BOF
6.36  *                   Kjører 1.gang på kvelden, så nytt fra morgen
6.37  *       030517 BKV  Fix for: too large to fit in the target
6.38  *           221217  a_ord endret til 8                        NHO
6.39  *       030118 bof  Ta med prisgruppe inn i prisbehandling.
6.3a  *       120118 bof  Test på ordretype om det skal legges ut
6.nu  *       180218 bsa  Endret ihht. nummerutvidelse
6.3b  * 6.30  170918 bkv  Utvidet med trelast sortiment
6.40  * 6.30  030419 nho  Utvidet med trelast sortiment
6.3c  * 6.30  230819 bof  sfkpri tas alltid fra fdkpri (ordrelinja)
      *
8.01  *PRG2   160622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     fsstxlt    if   e           k disk    rename(sstxpfr:sstxltr)
     fsstxlo    uf   e           k disk    rename(sstxpfr:sstxlor)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     fvltyl1    if   e           k disk    rename(vltypfr:vltyl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
5.24 fvvgkl1    if   e           k disk    rename(vvgkpfr:vvgkl1r)
6.3b fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
     fsstxpf    o  a e             disk
      *
      * Definering av array
      *
6.38 d** 6.38 a_ordr          s              6  0 dim(6000)
6.38 d a_ordr          s              8  0 dim(6000)
6.38 d a_suff          s              2  0 dim(6000)
      *
      *
      *  Local Data Area
      *
     d                uds
     d l_wsid                901    910
     d l_user                        10
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d sstxlx_tims     s                   like(sftims)
     d sstxlo_paar     s                   like(sfpaar)
     d sstxlo_ornr     s                   like(sfornr)
     d sstxlo_orsu     s                   like(sforsu)
     d votyl1_otyp     s                   like(vaotyp)
     d vltyl1_ltyp     s                   like(valtyp)
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d fodtl1_numm     s                   like(fdnumm)
     d fodtl1_suff     s                   like(fdsuff)
     d rkunl1_kund     s                   like(rkkund)
     d vvarl1_vare     s                   like(vvvare)
     d vvenl1_vare     s                   like(vevare)
5.24 d vvgkl1_ogrp     s                   like(vkogrp)
5.24 d vvgkl1_hgrp     s                   like(vkhgrp)
5.24 d vvgkl1_ugrp     s                   like(vkugrp)
5.24 d vvgkl1_ldor     s                   like(vkldor)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
      *
      * Definering av variable
      *
5.32 d w_saar          s              4  0
5.32 d w_suke          s              2  0
     d w_firm          s                   like(sffirm)
     d w_edat          s                   like(foedat)
     d w_etim          s                   like(foetim)
     d w_tims          s                   like(sftims)
     d w_timsx         s             26
     d w_timex         s              8
3.10 d w_vlin          s              1
6.34 d i               s              4  0
      *
     d w_omrs          s                   like(veomre)
     d w_omrl          s                   like(veomre)
5.63 d w_omrm          s                   like(veomre)
5.20 d w_lety          s                   like(fdlety)
5.38 d w_onum          s                   like(vaonum)
5.39 d w_pran          s              4
5.62 d w_trek          s             11  2
5.63 d w_akm3          s             11  3
5.63 d w_nom3          s             11  3
      *
6.34 d w_raba          s                   like(fdsums)
6.34 d w_sumr          s                   like(fdsums)
6.34 d w_pkon          s              3
6.34 d w_sumn          s             11  2
6.34 d w_suno          s             11  2
6.34 d w_ntpr          s             11  2
6.34 d w_sumi          s             11  2
6.34 d w_sumo          s             11  2
6.34 d w_suoi          s             11  2
6.34 d w_sumk          s             11  2
6.34 d w_suok          s             11  2
6.34 d w_diff          s             11  2
6.34 d w_pkns          s              3
6.34 d w_pkko          s              3
6.34 d w_pkok          s              3
6.34 d w_pkin          s              3
6.34 d w_pkoi          s              3
6.34 d w_kofa          s              1
6.34 d w_card          s              2
6.34 d w_fcar          s              2
6.34 d w_kild          s              3
6.34 d w_soda          s                   like(sfsoda)
6.34 d w_soti          s                   like(sfsoti)
6.28 d w_udat          s               d   datfmt(*iso)
6.28 d w_ddat          s               d   datfmt(*iso)
6.28 d w_ldor          s                   like(vvldor)
6.3b d w_lv_nobb       s                   like(vvlnob)
6.3b d w_lv_modn       s                   like(vvlmod)
6.3b d w_lv_lldo       s                   like(vvlnle)
6.3b d w_lv_llva       s                   like(vvllva)
     d w_belb          s             11  2
     d w_beln          s             12  3
     d w_belo          s             11  2
     d w_belw          s             11  2
5.30 d w_belx          s             11  2
5.30 d w_bely          s             11  2
     d w_bels          s             11  2
6.34 d w_belk          s             11  2
     d w_kost          s             11  2
     d w_rkro          s             11  2
     d w_rkro_stk      s             11  2
     d w_rkr1          s             11  2
     d w_rkr2          s             11  2
5.31 d w_brk1          s             11  2
5.31 d w_brk2          s             11  2
6.13 d w_mgrl          s             11  2
6.13 d w_moms          s             11  2
     d w_fkda          s              6  0
     d w_peri          s              4  0
     d w_fast          s             10
     d w_fell          s              6
     d w_kode          s              1
     d w_numm          s              6  0
     d w_type          s              2
5.40 d w_mvag          s             11  2
     d w_dbkr          s              1
     d w_valg          s              1  0
     d w_gebm          s             13  4
     d w_geby          s             11  2
     d w_geb0          s             11  2
     d w_geb1          s             11  2
     d w_geb2          s             11  2
     d w_geb3          s             11  2
     d w_anta          s                   like(fdanta)
      *
     d w_eaar          s              2  0
     d w_emnd          s              2  0
5.20 d w_hele          s            128
     d w_hgrp          s              2  0
     d w_ogrp          s              2  0
     d w_otyp          s              2
     d w_qmem          s              1
     d w_sald          s             11  2
     d w_retk          s              1
     d w_ugrp          s              3  0
     d w_vare          s             15
     d w_atxt          s             20
5.04 d w_kidr          s              6  0
5.04 d w_ksif          s              1  0
5.04 d w_kkus          s             24
5.04 d w_kkid          s             25
5.05 d w_line          s              4  0
5.49 d w_kki2          s             25
5.32 d w_latx          s             20
5.32 d w_adrs          s             30
5.32 d w_ponr          s              4  0
5.32 d w_pste          s             30
5.32 d w_avde          s              4  0
      *
5.31 d w_stat          s              1
5.31 d w_mvak          s              1
5.31 d w_mvtx          s             30
5.31 d w_bpro          s              5  2
5.31 d w_dato          s               d   datfmt(*iso)
5.31 d w_mvap          s              6  2
5.31 d w_mvat          s              5  4
5.31 d w_mvaf          s             10  9
5.70 d p_vtyp          s                   like(vvvtyp)
6.28 d b_inlr          s              1    inz(*off)
6.28 d b_frst          s              1    inz(*off)
5.32 d b_feil          s              1    inz(*off)
6.36  *
6.36 d p_first         s              1
6.34  *
6.34  * Datastruktur parameterliste -inn
6.34  *
6.34 d                 ds
6.34 d hirec                        120
6.34 d  hifirm                        3  0 overlay(hirec)
6.34 d  hirkat                        3  0 overlay(hirec:4)
6.34 d  hikund                        6  0 overlay(hirec:7)
6.34 d  hikpro                        6  0 overlay(hirec:13)
6.34 d  hivare                       15    overlay(hirec:19)
6.34 d  hilety                        1  0 overlay(hirec:34)
6.34 d  hienhe                        3    overlay(hirec:35)
6.34 d  hidato                         d   overlay(hirec:38) datfmt(*iso)
6.34 d  hitime                         t   overlay(hirec:48) timfmt(*iso)
6.nu d  hinumm                        8  0 overlay(hirec:56)
6.nu d  hikode                        1    overlay(hirec:64)
6.nu d  hidekn                        5  2 overlay(hirec:65)
6.nu d  hiavgf                        1  0 overlay(hirec:70)
6.nu d  hipriv                        1  0 overlay(hirec:71)
6.nu d  hitilb                        1    overlay(hirec:72)
6.nu d  hiskaf                        1    overlay(hirec:73)
6.nu d  hildor                        6  0 overlay(hirec:74)
6.n4 d  hisapr                        9  2 overlay(hirec:80)
6.n4 d  hikopr                        9  2 overlay(hirec:89)
8.01 d  hiprgr                        2    overlay(hirec:98)
8.01 d  hiinpr                        9  2 overlay(hirec:100)
6.34 d  hiinlr                        1    overlay(hirec:120)
6.34  *
6.34  * Datastruktur parameterliste -ut
6.34  *
6.34 d                 ds
6.34 d horec                        100
6.34 d  holety                        1  0 overlay(horec)
6.34 d  hokpri                        1    overlay(horec:2)
6.34 d  hooppr                        9  2 overlay(horec:3)
6.34 d  hosapr                        9  2 overlay(horec:12)
6.34 d  hokopr                        9  2 overlay(horec:21)
6.34 d  horab1                        5  2 overlay(horec:30)
6.34 d  horab2                        5  2 overlay(horec:35)
6.34 d  hodekn                        5  2 overlay(horec:40)
6.34 d  hopros                        5  2 overlay(horec:45)
6.34 d  hokamp                        5    overlay(horec:50)
6.34 d  hoalme                        4  0 overlay(horec:55)
6.34 d  hobrty                        1  0 overlay(horec:59)
6.34 d  hobrr1                        5  2 overlay(horec:60)
6.34 d  hobrr2                        5  2 overlay(horec:65)
6.34 d  hoomva                        1    overlay(horec:70)
8.01 d  hoprgr                        2    overlay(horec:71)
8.01 d  hoinpr                        9  2 overlay(horec:73)
8.01 d  hopkon                        3    overlay(horec:82)
8.01 d  hopkns                        3    overlay(horec:85)
8.01 d  hopkko                        3    overlay(horec:88)
8.01 d  hopkok                        3    overlay(horec:91)
8.01 d  hopkoi                        3    overlay(horec:94)
6.34  *
      *
      * Datastruktur for kontoinformasjon
      *
     d                 ds
5.20 d d_hele                       128
     d  d_kav1                        4  0 overlay(d_hele)
     d  d_kko1                        6  0 overlay(d_hele:5)
4.02 d  d_ksp1                        4  0 overlay(d_hele:11)
4.02 d  d_kav2                        4  0 overlay(d_hele:15)
4.02 d  d_kko2                        6  0 overlay(d_hele:19)
4.02 d  d_ksp2                        4  0 overlay(d_hele:25)
4.02 d  d_kav3                        4  0 overlay(d_hele:29)
4.02 d  d_kko3                        6  0 overlay(d_hele:33)
4.02 d  d_ksp3                        4  0 overlay(d_hele:39)
4.02 d  d_kav4                        4  0 overlay(d_hele:43)
4.02 d  d_kko4                        6  0 overlay(d_hele:47)
4.02 d  d_ksp4                        4  0 overlay(d_hele:53)
4.02 d  d_kav5                        4  0 overlay(d_hele:57)
4.02 d  d_kko5                        6  0 overlay(d_hele:61)
4.02 d  d_ksp5                        4  0 overlay(d_hele:67)
4.02 d  d_kav6                        4  0 overlay(d_hele:71)
4.02 d  d_kko6                        6  0 overlay(d_hele:75)
4.02 d  d_ksp6                        4  0 overlay(d_hele:81)
4.02 d  d_kav7                        4  0 overlay(d_hele:85)
4.02 d  d_kko7                        6  0 overlay(d_hele:89)
4.02 d  d_ksp7                        4  0 overlay(d_hele:95)
4.02 d  d_khnv                        4    overlay(d_hele:99)
5.20 d  d_nivo                        1    overlay(d_hele:103)
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Behandle utplukk, Setter inn riktige parameterverdier
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.3q C/Exec SQL
6.3q C+  Set Option DatFmt=*ISO
6.3q C/End-Exec
     c                   time                    w_ddat
     c                   eval      b_frst = *off
5.24  *
5.24  * Finner alternativ varegruppe
     c                   eval      w_tims = *loval
      * finn siste timestamp
     c                   eval      sstxlx_tims = *hival
     c                   read      sstxlt
     c                   dow       not %eof(sstxlt)
     c                   eval      w_tims = sftims
     c                   leave
     c                   enddo
      *
6.36 c                   if        p_first = '1'
     c                   eval      b_frst = *on
     c                   exsr      dann_total
     c                   goto      end_pgm
     c                   endif
      *
     c                   exsr      finn_ordre
     c                   exsr      dann_nye
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
     c                   eval      hiinlr = *off
6.34 c                   eval      hifirm = w_firm
6.34 c                   eval      hirkat = 0
6.34 c                   eval      hikund = 0
6.34 c                   eval      hikpro = 0
6.34 c                   eval      hivare = *blank
6.34 c                   eval      hienhe = *blank
6.34 c                   eval      hilety = 0
6.34 c                   eval      hiavgf = 0
6.34 c                   eval      hipriv = 0
6.34 c                   eval      hiskaf = *off
6.34 c                   eval      hidato = *loval
6.34 c                   eval      hitime = *loval
6.34  *
6.34 c                   call      'VP905R'
6.34 c                   parm                    hirec
6.34 c                   parm                    horec
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å danne total skyggetabell
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dann_total    begsr
      *
     c     fohel1_key    setll     fohel1
     c     fohel1_key    reade     fohel1
     c                   dow       not %eof(fohel1)
     c                   if        fokode = 0 and
     c                             fofsys <> 'INT'
     c                   eval      votyl1_otyp = footyp
     c     votyl1_key    chain     votyl1r
      *
6.3a c                   if        vaoakk = 3 and
6.3a c                             vaosta = 0
6.3a c                   goto      neste
6.3a c                   endif
6.34  *
     c                   eval      rkunl1_kund = fokund
     c     rkunl1_key    chain     rkunl1r
      *
     c                   eval      sstxlo_paar = %subdt(w_ddat:*years)
     c                   eval      sstxlo_ornr = fonumm
     c                   eval      sstxlo_orsu = fosuff
     c                   exsr      rydd_ordre
      *
     c                   exsr      dann_stat
     c                   endif
6.3a c     neste         tag
     c     fohel1_key    reade     fohel1
     c                   enddo
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å danne finner ordre underveis på dagen (2.gang)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_ordre    begsr
      *
      *  Rydder i ordretabell
     c                   eval      a_ordr(*) = 0
     c                   eval      a_suff(*) = 0
     c                   eval      i = 0
      *
     c                   eval      w_edat = %date(w_tims)
      *
      * Hvis forrige trans er dagen før så sett tidspunkt til dagens dato
      * og lavest mulig klokkeslett
      *
     c                   eval      w_edat = %date(w_tims)
     c                   if        w_edat <> w_ddat
     c                   time                    w_edat
     c                   eval      w_etim = *loval
     c                   else
     c                   move      w_tims        w_timsx
     c                   eval      w_timex = %subst(w_timsx:12:8)
     c                   move      w_timex       w_etim
     c                   endif
     c                   eval      *in15 = *off
      *
     c/exec sql
     c+                  declare sok_o cursor for
     c+                  select fonumm,
     c+                         fosuff
     c+                  from   fohepf
     c+                  where  fofirm = :w_firm and
     c+                         foedat >= :w_edat and
     c+                         foetim >= :w_etim and
     c+                         fokode  = 0  and
     c+                         fofsys <> 'INT'
     c+                  order by fonumm, fosuff
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_o
     c/end-exec
     c/exec sql
     c+                  open sok_o
     c/end-exec
      *
      *
      *
     c                   dou       *in15 = *on or
6.38 c                             i = 5999
      *
      *  Leser alle ordre etter opprettet et bestemt tidspunkt
     c     fetch_ordre   tag
     c/exec sql
     c+                  fetch next
     c+                  from  sok_o
     c+                  into  :fonumm,
     c+                        :fosuff
     c/end-exec
      *
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   iter
     c                   endif
      *
      * legger ordre i tabell
     c                   eval      i = i + 1
     c                   eval      a_ordr(i)  =  fonumm
     c                   eval      a_suff(i)  =  fosuff
      *
      *  Velg neste Vare-register post
      *
     c     neste_ordre   tag
     c                   goto      fetch_ordre
      *
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å danne total skyggetabell
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dann_nye      begsr
      *
     c                   eval      i = 1
6.38 c                   dow       i <  6000
     c                   eval      fohel1_numm = a_ordr(i)
     c                   eval      fohel1_suff = a_suff(i)
     c     fohel1_key2   chain     fohel1
     c                   if        %found(fohel1)
     c                   eval      votyl1_otyp = footyp
     c     votyl1_key    chain     votyl1r
      *
6.3a c                   if        vaoakk = 3 and
6.3a c                             vaosta = 0
6.3a c                   goto      neste2
6.3a c                   endif
      *
     c                   eval      rkunl1_kund = fokund
     c     rkunl1_key    chain     rkunl1r
      *
     c                   eval      sstxlo_paar = %subdt(w_ddat:*years)
     c                   eval      sstxlo_ornr = a_ordr(i)
     c                   eval      sstxlo_orsu = a_suff(i)
     c                   exsr      rydd_ordre
     c                   exsr      dann_stat
      *
6.3a c     neste2        tag
     c                   endif
     c                   eval      i = i + 1
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å danne en og en ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     rydd_ordre    begsr
      *
     c     sstxlo_key    setll     sstxlo
     c     sstxlo_key    reade     sstxlo
     c                   dow       not %eof(sstxlo)
     c                   delete    sstxlor
      *
     c     sstxlo_key    reade     sstxlo
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å danne en og en ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dann_stat     begsr
      *
     c                   if        fonumm = 995352
     c                   eval      fodtl1_numm = fonumm
     c                   endif
      *
     c                   eval      fodtl1_numm = fonumm
     c                   eval      fodtl1_suff = fosuff
     c     fodtl1_key    setll     fodtl1
     c     fodtl1_key    reade     fodtl1
     c                   dow       not %eof(fodtl1)
5.70  *
5.70  * Kaller program for henting av prisgruppe
5.70  *
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    fdvare
5.70 c                   parm                    folage
5.70 c                   parm                    p_vtyp
6.28 c                   parm                    w_udat
6.28 c                   parm                    w_ldor
6.28 c                   parm                    b_inlr
6.3b c                   parm                    w_lv_nobb
6.3b c                   parm                    w_lv_modn
6.3b c                   parm                    w_lv_lldo
6.3b c                   parm                    w_lv_llva
      *
     c                   eval      vltyl1_ltyp = fdltyp
     c     vltyl1_key    chain     vltyl1r                            90
      *
     c                   if        valktx = 0
6.35  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.35  * NB!  Utgangspunktet er at alle beløp i ordreregisteret er
6.35  *      lagret med positive fortegn.  Derfor blir disse snudd
6.35  *      her i henhold til koder på ordretype og linjetype. Det
6.35  *      viser seg imidlertid at negative varelinjer på en ordre
6.35  *      ligger med positivt antall, men negative beløp på
6.35  *      sum salg og sum kost.  For ikke å skape problemer
6.35  *      for resten av systemet, blir disse snudd her med det
6.35  *      samme de hentes inn, for så å følge samme prinsipper
6.35  *      videre i programmet.
6.35  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.35  *
6.35 c                   if        valkre = '-'
6.35 c                   eval      fdsums = fdsums * -1
6.35 c                   eval      fdsumk = fdsumk * -1
6.35 c                   endif
      *
     c                   eval      vvarl1_vare = fdvare
     c     vvarl1_key    chain     vvarl1r
     c                   if        %found(vvarl1)
6.34  *
     c                   eval      vugrl1_uogr = vvogrp
     c                   eval      vugrl1_uhgr = vvhgrp
     c                   eval      vugrl1_uugr = vvugrp
     c     vugrl1_key    chain     vugrl1
      *
     c                   else
     c                   eval      vugrl1_uogr = fdogrp
     c                   eval      vugrl1_uhgr = fdhgrp
     c                   eval      vugrl1_uugr = fdugrp
     c     vugrl1_key    chain     vugrl1
     c                   endif
      *
      * Bruttobeløp og kostpris
      *
6.34 c                   exsr      stat_null
6.34 c                   exsr      x_bereg
     c                   exsr      x_konto
6.34 c                   exsr      utvid_stat
      *
     c                   clear                   sstxpfr
     c                   eval      sffirm = w_firm
     c                   eval      sfpaar = %subdt(w_ddat:*years)
     c                   eval      sfpmnd = %subdt(w_ddat:*months)
      *
     c                   eval      sfvkun = folkun
     c                   eval      sffkun = fokund
     c                   if        folkun = 0
     c                   eval      sfvkun = fokund
     c                   endif
      *
     c                   eval      sfkpro = fokpro
     c                   eval      sfkkat = rkkatg
     c                   eval      sfrkat = forkat
     c                   eval      sfselg = foselg
     c                   eval      sfland = 0
     c                   eval      sfdist = fodist
     c                   eval      sffrik = rkfrie
3.11 c                   eval      sflety = fdlety
6.2d c                   eval      sflvre = fdlvre
     c                   eval      sfvare = fdvare
     c                   movel     fdtek1        sftek1
     c                   eval      sfogrp = vvogrp
     c                   eval      sfhgrp = vvhgrp
     c                   eval      sfugrp = vvugrp
     c                   eval      sfrgrp = 0
6.3c c                   eval      sfkpri = fdkpri
     c                   eval      sfoakk = vaoakk
     c                   if        foityp <> *blank
     c                   eval      sfityp = foityp
     c                   endif
     c                   eval      sfityp = foityp
5.70  *
5.70 c                   eval      sfvtyp = p_vtyp
     c                   eval      sfldor = vvldor
6.3b c                   if        w_lv_nobb > 0
6.3b c                   eval      sflvar = w_lv_llva
6.3b c                   eval      sfnobb = w_lv_nobb
6.3b c                   eval      sfmodn = w_lv_modn
6.3b c                   else
     c                   eval      sflvar = vvlvar
     c                   eval      sfnobb = vvnobb
     c                   eval      sfmodn = vvnmod
6.3b c                   endif
     c                   eval      sfahgr = vvahgr
     c                   eval      sfaugr = vvaugr
     c                   eval      sfkamp = fdkamp
5.32 c                   eval      sflage = fdlage
     c                   eval      sfbinr = 0
5.05 c                   eval      sfline = 0
     c                   eval      sfbida = fofkda
     c                   eval      sfotyp = footyp
     c                   eval      sfornr = fonumm
     c                   eval      sforsu = fosuff
     c                   eval      sforli = fdline
     c                   eval      sforda = fobdat
6.40  * Hvis tilbud så skal oppg.dato og utgår dato legges ut
6.40 c                   eval      sftdat = *loval
6.40 c                   eval      sfddat = *loval
6.40 c                   if        vaoakk = 0
6.40 c                   eval      sftdat = fotdat
6.40 c                   eval      sfddat = foddat
6.40 c                   endif
6.40  *
5.39 c                   eval      sfbong = fobong
     c                   eval      sfsavd = w_avde
     c                   eval      sfskon = fdkont
     c                   eval      sfssps = fdspes
     c                   eval      sfanto = fdanta
     c                   eval      sfanta = w_anta
5.63 c                   eval      sfakm3 = w_akm3
5.63 c                   eval      sfnom3 = w_nom3
     c                   eval      sffakr = *blank
     c                   eval      sfsumo = w_belo
     c                   eval      sfsumb = w_belb
     c                   eval      sfsumk = w_kost
     c                   eval      sfrab1 = w_rkr1
     c                   eval      sfrab2 = w_rkr2
     c                   eval      sfrabo = w_rkro
     c                   eval      sfkmva = fomkod
     c                   eval      sfkres = 0
     c                   eval      sfenhe = vvenhs
     c                   eval      sfenho = fdenh1
5.01 c                   eval      sfnref = fonref
5.21 c                   eval      sffsys = fofsys
5.02 c                   eval      sfbenr = fdbenr
5.02 c                   eval      sfbsuf = fdbsuf
6.2f c                   eval      sfblin = fdblin
5.03 c                   eval      sflmat = folmat
5.31 c                   eval      sfalme = fdalme
5.31 c                   eval      sfbrty = fdbrty
5.31 c                   eval      sfpriv = fdpriv
5.31 c                   eval      sfomva = fdomva
5.31 c                   eval      sfbrk1 = w_brk1
5.31 c                   eval      sfbrk2 = w_brk2
5.31 c                   eval      sfuser = fouser
     c                   eval      sfwsid = fowsid
5.64 c                   eval      sfkhev = d_khnv
5.65 c                   eval      sfeann = fdeann
5.70 c                   eval      sfprgr = fdprgr
5.70 c                   eval      sfoprg = fdoprg
6.31 c                   eval      sfproj = foproj
6.31 c                   eval      sfakti = foakti
6.34  *
6.34  * utvidet statistikk
6.34  *
6.34 c                   eval      sfpkon = w_pkon
6.34 c                   eval      sfpkns = w_pkns
6.34 c                   eval      sfpkko = w_pkko
6.34 c                   eval      sfpkok = w_pkok
6.34 c                   eval      sfpkoi = w_pkoi
6.34 c                   eval      sfpkin = w_pkin
6.34 c                   eval      sfkofa = w_kofa
6.34 c                   eval      sfcard = w_card
6.34 c                   eval      sffcar = w_fcar
6.34 c                   eval      sfkild = w_kild
      *
6.34 c                   eval      sfsuno = w_suno
6.34 c                   eval      sfsuok = w_suok
6.34 c                   eval      sfsumn = w_sumn
6.34 c                   eval      sfsumi = w_sumi
6.34 c                   eval      sfsuoi = w_suoi
      *                  endif
6.34 c                   eval      sfsoda = w_soda
6.34 c                   eval      sfsoti = w_soti
3.10  *
3.10  * Overstyring av felter, dersom varelinjen
3.10  * er en vare hentet fra LVR
3.10  *
3.10 c                   if        fdlvre =  1
3.10 c                   eval      sfogrp = fdogrp
3.10 c                   eval      sfhgrp = fdhgrp
3.10 c                   eval      sfugrp = fdugrp
6.3b c                   if        w_lv_nobb > 0
6.3b c                   eval      sflvar = w_lv_llva
6.3b c                   eval      sfnobb = w_lv_nobb
6.3b c                   else
3.10 c                   eval      sflvar = fdlvar
5.37 c                   eval      sfnobb = fdnobb
6.3b c                   endif
3.10 c                   eval      sfkpri = fdkpri
3.10 c                   eval      sfvtyp = 'S'
3.10 c                   eval      sfldor = fdldor
3.10 c***                eval      sfmodn = 0
3.10 c                   eval      sfenhe = fdenh1
3.10 c                   eval      sfenho = fdenh1
3.10 c                   endif
5.24  *
5.24 c                   exsr      x_altvgr
5.39  *
5.39  * Henter inn produktansvarlig
5.39  *
5.39 c                   call      'VG710R'
5.39 c                   parm                    w_firm
5.39 c                   parm                    sfogrp
5.39 c                   parm                    sfhgrp
5.39 c                   parm                    sfugrp
5.39 c                   parm                    w_pran
5.39  *
5.39 c                   eval      sfpran = w_pran
5.43  *
6.29  * Oppdaterer leveringsdato, pakkseddelsdato  og uke
5.43  *
6.29 c                   if        fdldat <> *loval
6.29 c                   eval      sfldat =fdldat
6.29 c                   else
5.43 c                   eval      sfldat =foldat
6.29 c                   endif
6.29 c                   eval      sfpada =fopada
5.43  *
5.43 c                   if        sfldat <> *loval
5.43 c                   eval      w_dato = sfldat
5.43 c                   else
5.43 c                   eval      w_dato = sfbida
5.43 c                   endif
5.43  *
5.43 c                   call      'AX711R'
5.43 c                   parm                    w_dato
5.43 c                   parm                    w_saar
5.43 c                   parm                    w_suke
5.43 c                   parm                    w_stat
5.43  *
5.43 c                   if        w_suke > 52
5.43 c                   eval      w_suke = 52
5.43 c                   endif
5.47 c                   eval      sfpuke = w_suke
      *
     c                   time                    sfdate
     c                   time                    sftime
     c                   if        b_frst = *on
     c     sfdate        subdur    1:*d          sfdate
     c                   eval      sftime = *loval
     c                   endif
      *
     c                   time                    sftims
6.10 c                   eval      sfuser = l_user
      *
     c                   write     sstxpfr
      *
     c                   endif
5.24  *
     c     fodtl1_key    reade     fodtl1
     c                   enddo
      *
     c                   endsr
6.34  *
      *------------------------------------------------------------------------------
      * Subrutine for å nullstille før utvidet stat.beregning
      *------------------------------------------------------------------------------
      * nullstiller
     c     stat_null     begsr
     c                   eval      w_bels = 0
     c                   eval      w_belk = 0
     c                   eval      w_beln = 0
     c                   eval      w_belo = 0
     c                   eval      w_belb = 0
6.34 c                   eval      w_pkon = *blank
6.34 c                   eval      w_pkns = *blank
6.34 c                   eval      w_pkko = *blank
6.34 c                   eval      w_pkok = *blank
6.34 c                   eval      w_pkin = *blank
6.34 c                   eval      w_pkoi = *blank
6.34 c                   eval      w_kofa = *blank
6.34 c                   eval      w_card = *blank
6.34 c                   eval      w_fcar = *blank
6.34 c                   eval      w_kild = *blank
6.34 c                   eval      w_sumn = 0
6.34 c                   eval      w_sumi = 0
6.34 c                   eval      w_suoi = 0
6.34 c                   eval      w_sumo = 0
6.34 c                   eval      w_suno = 0
6.34 c                   eval      w_suok = 0
6.34 c                   eval      w_suoi = 0
6.34 c                   eval      w_ntpr = 0
6.34 c                   eval      w_rkro_stk = 0
6.34 c                   eval      w_rkro = 0
6.34 c                   endsr
      *
6.34  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.34  * Henter utvidet statstikk-data
6.34  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.34  *
6.34 csr   utvid_stat    begsr
6.34  *
6.34  *
6.34 c                   eval      hifirm = w_firm
6.34 c                   eval      hirkat = 0
6.34  *
6.34 c                   eval      hikund = fokund
6.34 c                   if        folkun <> 0
6.34 c                   eval      hikund = folkun
6.34 c                   endif
6.34  *
6.34 c                   eval      hikpro = fokpro
6.34 c                   eval      hivare = fdvare
     c                   eval      hienhe = fdenh1
     c                   eval      hilety = fdlety
     c                   eval      hidekn = fdpasl
6.39 c                   eval      hiprgr = fdprgr
     c                   eval      hiavgf = fomkod
     c                   eval      hipriv = 0
6.34 c                   eval      hidato = fobdat
6.34 c                   if        fobdat = fodate
6.34 c                   eval      hitime = fotime
6.34 c                   else
6.34 c                   eval      hitime = *loval
6.34 c                   endif
6.34 c                   eval      w_soda = fodate
6.34 c                   eval      w_soti = fotime
6.34 c                   eval      hinumm = fopord
6.34 c                   eval      hikode = fopask
6.34 c                   eval      hiskaf = *off
6.34 c                   eval      hildor = fdldor
6.34 c                   eval      hisapr = 0
6.34 c                   eval      hikopr = 0
6.34 c                   eval      hiinpr = fdinpr
     c                   eval      hidekn = fdpasl
     c                   if        fdlvre = 1
     c                   eval      hiskaf = *on
     c                   else
     c                   eval      hiskaf = *off
     c                   endif
     c                   eval      hiinlr = *on
6.34  *
6.34 c                   call      'VP905R'
6.34 c                   parm                    hirec
6.34 c                   parm                    horec
6.34  *
6.34  * Koder fra VP905
6.34  *
6.34 c                   eval      w_pkon = hopkon
6.34 c                   eval      w_pkns = hopkns
6.34 c                   eval      w_pkko = hopkko
6.34 c                   eval      w_pkok = hopkok
6.34 c                   eval      w_pkoi = hopkoi
      *
     c                   if        fdlvre  = 1
      * her må det testes mer for å sette PK-koder
     c                   if        w_bels <> 0
     c                   if        w_pkon = *blank
     c                   eval      w_pkon = 'VEI'
     c                   endif
     c                   if        w_pkns = *blank
     c                   eval      w_pkns = 'VEI'
     c                   endif
     c                   endif
      *
     c                   if        w_belk <> 0
     c                   if        w_pkko = *blank
     c                   eval      w_pkko = 'VEI'
     c                   endif
     c                   if        w_pkok = *blank
     c                   eval      w_pkok = 'VEI'
     c                   endif
     c                   endif
      *
     c                   endif
6.34  *
6.34  * Sum opprinnelig netto salgspris (SFSUMO) ---------------------------
6.34  *
     c                   if        fdlvre = 1
     c                   eval      w_ntpr = w_bels
     c                   eval      hokopr = w_belk
     c                   else
6.34 c                   eval      w_ntpr = hosapr
     c                   endif
6.34  *
6.34 c                   if        horab1 > 0
6.34 c                   eval(h)   w_ntpr = w_ntpr - (w_ntpr * horab1 / 100)
6.34 c                   endif
6.34 c                   if        horab2 > 0
6.34 c                   eval(h)   w_ntpr = w_ntpr - (w_ntpr * horab2 / 100)
6.34 c                   endif
      *
      * Varelinjens andel av ordrerabatt (pr. stk)
      *
     c                   if        vvkord = 0   and
     c                             rkorab <> 0
     c                   eval(h)   w_rkro_stk = (w_ntpr * rkorab) / 100
     c                   endif
      * trekkker fra ordrerabatt
     c                   eval      w_ntpr = w_ntpr - w_rkro_stk
      *
     c                   if        (w_ntpr * fdanta) > 999999999,00
     c                   eval      w_suno = 999999999,00
     c                   else
6.34 c                   eval(h)   w_suno = w_ntpr * fdanta
     c                   endif
6.34  *
6.34  * Sum netto salg salgspris (SFSUMN) --------------------------------
6.34  *
6.34 c                   exsr      beregn_rabatt
6.34  *
6.34 c                   eval(h)   w_sumn = fdsums - w_sumr
6.34  *
6.34  * Priskode netto salgspris  (SFPKNS)
6.34  *
     c                   if        p_vtyp <> 'D'
6.34  * ikke Diverse varer
      *
     c                   if        w_suno <> 0 and
     c                             w_sumn <> 0
6.34 c                   eval      w_diff  = %abs(w_suno - w_sumn)
6.34 c                   if        w_diff > 1
6.34 c                   eval      w_pkns  = 'MAN'
6.34 c                   endif
6.34 c                   endif
6.34  *
6.34  * Sum  kostpris (SFSUMK)
6.34  *
6.34 c                   eval      w_sumk = fdkopr * fdanta
6.34  *
6.34  * Sum opprinnelig kostpris (SFSUOK)
6.34  *
6.37 c                   if        ((hokopr * fdanta) > 999999999)
6.37 c                   eval      w_suok = 999999999
6.37 c                   else
6.37 c                   eval      w_suok = hokopr * fdanta
6.37 c                   endif
6.34  *
6.34  * Priskode netto kostpris  (SFPKKO)
6.34  *
     c                   if        w_sumk <> 0  and
     c                             w_suok <> 0
6.34 c                   eval      w_diff  = %abs(w_sumk - w_suok)
6.34 c                   if        w_diff > 1
6.34 c                   eval      w_pkko  = 'MAN'
6.34 c                   endif
6.34 c                   endif
6.34  *
6.34  * Sum  innkjøps (SFSUMI)
6.34  *
6.34 c                   eval      w_sumi = fdinpr * fdanta
6.34  *
6.34  * Sum  opprinnelig innkjøps (SFSUOI)
6.34  *
6.34 c                   eval      w_suoi = hoinpr * fdanta
6.34  *
6.34  * Priskode netto kostpris  (SFPKIN)
6.34  *
6.34 c                   if        fdinpr <> 0 and
     c                             hoinpr <> 0
6.34 c                   eval      w_diff  = %abs(w_sumi - w_suoi)
6.34 c                   if        w_diff > 1
6.34 c                   eval      w_pkin  = 'XXX'
6.34 c                   endif
6.34 c                   endif
6.34  * ikke Diverse varer
6.34 c                   endif
      *
6.34  *
6.34  * Kilde  (SFKILD)
6.34  *
     c                   if        fofsys <> *blank
     c                   eval      w_kild = fofsys
     c                   else
     c                   eval      w_kild = 'ORD'
     c                   endif
6.34  *
6.34  * CARD   (SFCARD)
6.34  *
6.34  *
6.34 c                   if        fopsuf =  5
6.34 c                   eval      w_card = '05'
6.34 c                   endif
6.34 c                   if        fopsuf =  6
6.34 c                   eval      w_card = '06'
6.34 c                   endif
6.34  *
6.34  *
6.34  * Kontakt/fakturert (SFKOFA)
6.34  *
6.34 c                   if        footyp = 'CD' or
6.34 c                             footyp = 'CK'
6.34 c                   eval      w_kofa = 'K'
6.34 c                   else
6.34 c                   eval      w_kofa = 'F'
6.34 c                   endif
6.34  *
6.34  * Behandle diverse vare
6.34  *
     c                   if        %found(vvarl1) and
     c                             vvvtyp = 'D'
     c                   eval      w_suno = 0
     c                   eval      w_pkon = *blank
     c                   eval      w_pkns = 'DIV'
     c                   eval      w_suok = 0
     c                   eval      w_pkok = *blank
     c                   eval      w_pkko = 'DIV'
     c                   endif
6.34  *
6.34 csr                 endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Beregning av beløp + oppdatering av  statistikk
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     csr   x_bereg       begsr
6.34  *
      *
      * Opprinnelig beløp fra vareregister
      *
     c                   if        fdlvre = 1
      *
      * tar utg.punkt i nobb-pris, mult med innkjøpsfaktor
      * eller det er nettopris fra betingelse
     c                   if        fdbifa = 1 and
     c                             fdbntp <> 0
     c                   eval      w_bels = fdbntp
     c                   else
     c                   eval      w_bels = fdnopr * fdbifa
     c                   endif
      * mult med kostprisfaktor eller kostprisbeløp
     c                   if        fdpkfa = 1
     c                   eval(h)   w_bels = (w_bels * fdpkfa) + fdpkbe
     c                   else
     c                   eval      w_bels = w_bels * fdpkfa
     c                   endif
      * kostpris
     c                   eval      w_belk = w_bels
      * mult med salgprisfaktor eller salgsprisbeløp
     c                   eval      w_bels = w_bels * fdpsfa
      *
     c                   if        w_bels <> 0
     c                   if        (w_bels * fdanta) > 999999999,00
     c                   eval      w_belo = 999999999,00
     c                   else
     c                   eval      w_belo = w_bels * fdanta
     c                   endif
     c                   eval      w_pkon = 'VEI'
     c                   eval      w_pkns = 'VEI'
     c                   endif
     c                   if        w_belk <> 0
     c                   eval      w_pkko = 'VEI'
     c                   eval      w_pkok = 'VEI'
     c                   endif
      * slutt lvr
     c                   else
      *
     c                   if        (fdoppr * fdanta) > 999999999,00
     c                   eval      w_belo = 999999999,00
     c                   else
6.1a c                   eval(h)   w_belo = fdoppr * fdanta
     c                   endif
     c                   endif
      *
      * Bruttobeløp og kostpris
      *
6.1a c                   eval(h)   w_belb = fdsapr * fdanta
     c                   eval      w_kost = fdkopr * fdanta
      *
      * Linjerabatter
      *
     c                   eval(h)   w_rkr1 = (w_belb * fdrab1) / 100
     c                   eval      w_belw =  w_belb - w_rkr1
     c                   eval(h)   w_rkr2 = (w_belw * fdrab2) / 100
      *
      * Trekker ut linje rabatter
      *
     c                   eval      w_beln = fdsums - w_rkr1 - w_rkr2
      *
      * Varelinjens andel av ordrerabatt (pr. stk)
      *
     c                   if        vvkord = 0
     c                   eval(h)   w_rkro = (w_beln * forabp) / 100
     c                   endif
5.31  *
5.31  * Almenningsrabatter
5.31  *
5.31 c                   eval      w_belx =  w_belw - w_rkr2 - w_rkro
5.31 c                   eval(h)   w_brk1 = (w_belx * fdbrr1) / 100
5.31  *
5.31 c                   eval      w_bely =  w_belx - w_brk1
5.31 c                   eval(h)   w_brk2 = (w_bely * fdbrr2) / 100
5.31  *
5.31  * Henter inn prosentsats på mva kode
5.31  *
5.31 c                   eval      w_mvap = 0
5.31 c                   eval      w_mvat = 0
5.31 c                   eval      w_mvaf = 0
5.31  *
5.31 c                   if        fdomva <> *blank
5.31 c                   eval      w_stat =  *blank
5.31 c                   eval      w_mvak =  fdomva
6.30 c                   eval      w_dato =  w_ddat
5.31  *
5.31 c                   call      'RS205R'
5.31 c                   parm                    w_stat
5.31 c                   parm                    w_mvak
5.31 c                   parm                    w_mvtx
5.31 c                   parm                    w_dato
5.31 c                   parm                    w_mvap
5.31 c                   parm                    w_mvat
5.31 c                   parm                    w_mvaf
5.31 c                   parm                    w_bpro
5.31 c                   endif
      *
      * Sjekker om dette er en negativ ordre
      *
6.35 c                   if        vaokre = '-'
     c                   eval      fdanta = fdanta * -1
     c                   eval      fdsums = fdsums * -1
     c                   eval      fdsumk = fdsumk * -1
     c                   eval      w_beln = w_beln * -1
     c                   eval      w_belb = w_belb * -1
     c                   eval      w_belo = w_belo * -1
     c                   eval      w_kost = w_kost * -1
     c                   eval      w_rkro = w_rkro * -1
     c                   eval      w_rkr1 = w_rkr1 * -1
     c                   eval      w_rkr2 = w_rkr2 * -1
5.31 c                   eval      w_brk1 = w_brk1 * -1
5.31 c                   eval      w_brk2 = w_brk2 * -1
5.31 c                   eval      w_mvag = w_mvag * -1
     c                   endif
      *
      * Sjekker om dette er en negativ linje
      *
     c                   if        valkre = '-'
     c                   eval      fdanta = fdanta * -1
     c                   eval      fdsums = fdsums * -1
     c                   eval      fdsumk = fdsumk * -1
     c                   eval      w_beln = w_beln * -1
     c                   eval      w_belb = w_belb * -1
     c                   eval      w_belo = w_belo * -1
     c                   eval      w_kost = w_kost * -1
     c                   eval      w_rkro = w_rkro * -1
     c                   eval      w_rkr1 = w_rkr1 * -1
     c                   eval      w_rkr2 = w_rkr2 * -1
5.31 c                   eval      w_brk1 = w_brk1 * -1
5.31 c                   eval      w_brk2 = w_brk2 * -1
5.31 c                   eval      w_mvag = w_mvag * -1
     c                   endif
      *
      * Kundeprosjekt oppdateres i feltet
      * for prisordre, dersom kundeprosjekt
      * er oppgitt
      *
     c                   if        fokpro <> 0
     c                   eval      fopord = fokpro
     c                   endif
5.63  *
5.63  * Omregning av enhet til statistikk.........
5.63  *
5.63  * Dersom stat.enhet ikke finnes på vare, hentes stat.enhet fra
5.63  * undergruppe. Dersom stat.enhet ikke finnes på undergruppe, settes
5.63  * stat.enhet lik solgt enhet
5.63  *
5.63 c                   if        vvenhs = *blank
5.63 c                   eval      vvenhs = vguens
5.63 c                   endif
5.63  *
5.63 c                   if        vvenhs = *blank
5.63 c                   eval      vvenhs = fdenh1
5.63 c                   endif
5.63  *
5.63  * Kaller opp subrutine for omregning av antall til statistikkenhet
5.63  *
5.63 c                   eval      w_anta = fdanta
5.63  *
5.63 c                   exsr      x_omrant
5.32  *
5.32  * Sjekker om lager er overstyrt på linjenivå - henter avdeling
5.32  *
5.32 c                   eval      w_avde = foavde
5.32  *
5.32 c                   if        fdlage <> folage
5.32  *
5.32 c                   eval      b_feil = *off
5.32 c                   call      'FÅ720R'
5.32 c                   parm                    w_firm
5.32 c                   parm                    fdlage
5.32 c                   parm                    w_latx
5.32 c                   parm                    w_adrs
5.32 c                   parm                    w_ponr
5.32 c                   parm                    w_pste
5.32 c                   parm                    w_avde
5.32 c                   parm                    b_feil
5.32  *
     c                   endif
5.32  *
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for omregning av antall i forhold til statestikkenhet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     csr   x_omrant      begsr
      *
      * Dersom solgt enhet er lik stat.enhet beholdes antall
      *
     c                   if        fdenh1 = vvenhs
     c                   goto      end_omr
     c                   endif
      *
      * Initierer arbeidsvariable
      *
     c                   eval      w_omrs = 0
     c                   eval      w_omrl = 0
5.63 c                   eval      w_omrm = 0
5.63 c                   eval      w_akm3 = 0
5.63 c                   eval      w_nom3 = 0
      *
      * Finner omregningsfaktor volum for stat.enhet og for solgt enhet
      * Antall regnes om etter forholdet mellom disse
      *
     c                   eval      vvenl1_vare = fdvare
     c     vvenl1_key    setll     vvenl1
     c     vvenl1_key    reade     vvenl1                                 90
     c                   dow       *in90 = *off
      *
     c                   if        fdenh1 = veenhe
     c                   eval      w_omrs = veomre
     c                   endif
      *
     c                   if        vvenhs = veenhe
     c                   eval      w_omrl = veomre
     c                   endif
5.63  *
5.63 c                   if        veenhe = 'M3 '
5.63 c                   eval      w_omrm = veomre
5.63 c                   endif
5.63  *
     c     vvenl1_key    reade     vvenl1                                 90
     c                   enddo
      *
     c                   if        w_omrs = 0
     c                   eval      w_omrs = 1
     c                   endif
      *
     c                   if        w_omrl = 0
     c                   eval      w_omrl = 1
     c                   endif
5.63  *
5.63 c                   if        w_omrm = 0
5.63 c                   eval      w_omrm = 1
5.63 c                   endif
      *
     c                   eval(h)   w_anta = w_anta * w_omrs / w_omrl
5.63  *
5.63  * Omregn antall til aktuell m3
5.63  *
5.63 c                   eval(h)   w_akm3 = w_anta * w_omrl / w_omrm
5.63  *
5.63  * Omregner antall til nominell m3
5.63  *
5.63 c                   eval(h)   w_nom3 = w_akm3 * vvomrn
      *
     csr   end_omr       endsr
      *
5.24  *
5.24  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.24  * Subrutine som finner alternativ varegruppe
5.24  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.24  *
5.24 csr   x_altvgr      begsr
5.24  *
5.24  * Leser på varegruppeknytning
5.24  * Leter først etter knytninger mot leverandør
5.24  *
5.24 c                   eval      vvgkl1_ogrp = vvogrp
5.24 c                   eval      vvgkl1_hgrp = vvhgrp
5.24 c                   eval      vvgkl1_ugrp = vvugrp
5.24 c                   eval      vvgkl1_ldor = vvldor
5.24  *
5.24 c     vvgkl1_key    chain     vvgkl1
5.24 c                   if        not %found
5.24 c                   eval      vvgkl1_ldor = 0
5.24 c     vvgkl1_key    chain     vvgkl1
5.24 c                   if        not %found
5.24 c                   eval      vvgkl1_ldor = sfldor
5.24 c                   eval      vvgkl1_ugrp = 0
5.24 c     vvgkl1_key    chain     vvgkl1
5.24 c                   if        not %found
5.24 c                   eval      vvgkl1_ldor = 0
5.24 c     vvgkl1_key    chain     vvgkl1
5.24 c                   if        not %found
5.24 c                   eval      vvgkl1_ldor = sfldor
5.24 c                   eval      vvgkl1_hgrp = 0
5.24 c     vvgkl1_key    chain     vvgkl1
5.24 c                   if        not %found
5.24 c                   eval      vvgkl1_ldor = 0
5.24 c     vvgkl1_key    chain     vvgkl1
5.24 c                   endif
5.24 c                   endif
5.24 c                   endif
5.24 c                   endif
5.24 c                   endif
5.24  *
5.24 c                   if        %found
5.24 c                   eval      sfahgr = vkahgr
5.24 c                   eval      sfaugr = vkaugr
5.24 c                   endif
5.24  *
5.24 csr                 endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter avdeling/konto/spesifikasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     csr   x_konto       begsr
      *
      * Legger ut parametere for konto-henvisnings-program
      *
     c                   eval      w_retk = *blank
     c                   eval      w_ogrp = vvogrp
     c                   eval      w_hgrp = vvhgrp
     c                   eval      w_ugrp = vvugrp
     c                   eval      w_vare = vvvare
     c                   move      '0'           w_hele
3.10  *
3.10  * Dersom grunnlag for konto er varelinje,
3.10  * skal varegruppe hentes fra linjenivå
3.10  *
3.10 c                   if        fdlvre = 1
3.10 c                   eval      w_ogrp = fdogrp
3.10 c                   eval      w_hgrp = fdhgrp
3.10 c                   eval      w_ugrp = fdugrp
3.10 c                   eval      w_vare = fdvare
     c                   move      '0'           w_hele
3.10 c                   endif
5.20  *
5.20  * Legger inn leveringstype fra varelinje,
5.20  *
5.20 c                   eval      w_lety = fdlety
      *
      * Kaller program for å finne konto
      *
     c                   call      'VA720R'
     c                   parm                    w_retk
5.20 c                   parm                    w_lety
     c                   parm                    w_otyp
     c                   parm                    w_ogrp
     c                   parm                    w_hgrp
     c                   parm                    w_ugrp
     c                   parm                    w_vare
     c                   parm                    w_hele
     c                   eval      d_hele = w_hele
      *
      * Overstyrer avdeling dersom denne
      * finnes på ordrehode
      *
     c                   if        foavde > 0
     c                   eval      d_kav1 = foavde
     c                   eval      d_kav2 = foavde
     c                   eval      d_kav3 = foavde
     c                   eval      d_kav4 = foavde
     c                   eval      d_kav5 = foavde
     c                   eval      d_kav6 = foavde
     c                   eval      d_kav7 = foavde
     c                   endif
      *
     csr                 endsr
6.2A  *
6.34  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.34  * Subrutine for å beregne rabatt
6.34  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.34  *
6.34 c     beregn_rabatt begsr
6.34  *
6.34 c                   eval      w_sumr = 0
6.34  *
6.34  * Rabatt
6.34  *
6.34 c                   if        vvkord = 0
6.34 c                   eval(h)   w_raba = (fdsums * forabp) / 100
6.34 c                   eval      w_sumr = w_raba
6.34 c                   endif
6.34  *
6.34 c                   if        fdrab1 <> 0
6.34 c                   eval(h)   w_raba = (fdsums - w_sumr) * fdrab1 / 100
6.34 c                   eval      w_sumr = w_sumr + w_raba
6.34 c                   endif
6.34  *
6.34 c                   if        fdrab2 <> 0
6.34 c                   eval(h)   w_raba = (fdsums - w_sumr) * fdrab2 / 100
6.34 c                   eval      w_sumr = w_sumr + w_raba
6.34 c                   endif
6.34  *
6.34 c                   endsr
6.34  *
6.34  *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
6.36  *
6.36 c     *entry        plist
6.36 c                   parm                    p_first
      *
      *  Key for les av VARE
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for file : UNDER-GRUPPE-REGISTER
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      *  Key for les av VARE enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
5.24  *
5.24  * Key for les på varegruppeknytning
5.24  *
5.24 c     vvgkl1_key    klist
5.24 c                   kfld                    w_firm
5.24 c                   kfld                    vvgkl1_ogrp
5.24 c                   kfld                    vvgkl1_hgrp
5.24 c                   kfld                    vvgkl1_ugrp
5.24 c                   kfld                    vvgkl1_ldor
      *
      * Key for file : KUNDE-REGISTER
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      *
      * Key for file : ORDRE-TYPE-REGISTER
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for file : LINJE-TYPE-REGISTER
      *
     c     vltyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltyl1_ltyp
      *
      * Key for file : ORDRE-LINJE-heading
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
      *
      * Key for file : ORDRE-LINJE-heading
      *
     c     fohel1_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      * Key for file : ORDRE-LINJE-REGISTER
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
      *
      * Key for file : skyggetabell
      *
     c     sstxlo_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sstxlo_paar
     c                   kfld                    sstxlo_ornr
     c                   kfld                    sstxlo_orsu
      *
      *
      *  Legge inn firmanummer i nøklene
      *
     c                   eval      w_firm = l_firm
      *
      *
     c                   endsr
