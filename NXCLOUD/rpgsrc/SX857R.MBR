     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASSTAT
      *  Program . . . . : SX857R
      *  Beskrivelse . . : Fix : retter div. felter etter feil med
      *                          snuing av fortegn i 6.30
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign
      * --------- ------  --------------------------------------------------
      *           070316  Nytt program                              BOF
8.01  *PRG2   220622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner - COMP
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsstal1    if   e           k disk    rename(sstapfr:sstal1r)
     fsstalu    uf   e           k disk    rename(sstapfr:sstalur)
      *
      *
      *  Local Data Area
      *
     d                uds
     d l_wsid                901    910
     d l_user                        10
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d sstal1_paar     s                   like(sfpaar)
     d sstalu_paar     s                   like(sfpaar)
     d sstalu_binr     s                   like(sfbinr)
     d sstalu_ornr     s                   like(sfornr)
     d sstalu_orsu     s                   like(sforsu)
     d sstalu_orli     s                   like(sforli)
     d sstalu_bong     s                   like(sfbong)
     d sstalu_vare     s                   like(sfvare)
     d sstalu_enhe     s                   like(sfenhe)
     d sstalu_anta     s                   like(sfanta)
     d sstalu_date     s                   like(sfdate)
     d sstalu_time     s                   like(sftime)

      *
      * Definering av variable
      *
5.32 d p_paar          s              4
     d w_paar          s                   like(sfpaar)
     d w_firm          s                   like(sffirm)
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Behandle utplukk, Setter inn riktige parameterverdier
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c                   move      p_paar        w_paar
     c                   exsr      les_stat
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å å lese all stat for ett år
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_stat      begsr
      *
     c                   eval      sstal1_paar = w_paar
     c     sstal1_key    setll     sstal1
     c     sstal1_key    reade     sstal1
     c                   dow       not %eof(sstal1)
     c                   if        sfotyp = 'CK'
     c                   if        (sfanta > 0 and sfsumb < 0) or
     c                             (sfanta < 0 and sfsumb > 0)
      *
     c                   exsr      rett_stat
      *
     c                   endif
     c                   endif
      *
     c     sstal1_key    reade     sstal1
     c                   enddo
      *
     c                   endsr
      *
6.34  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.34  * Henter utvidet statstikk-data
6.34  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.34  *
6.34 csr   rett_stat     begsr
6.34  *
      ***
      * oppdaterer statistikk
     c                   eval      sstalu_paar  = sfpaar
     c                   eval      sstalu_binr  = sfbinr
     c                   eval      sstalu_ornr  = sfornr
     c                   eval      sstalu_orsu  = sforsu
     c                   eval      sstalu_orli  = sforli
     c                   eval      sstalu_bong  = sfbong
     c                   eval      sstalu_vare  = sfvare
     c                   eval      sstalu_enhe  = sfenhe
     c                   eval      sstalu_anta  = sfanta
     c                   eval      sstalu_date  = sfdate
     c                   eval      sstalu_time  = sftime
     c     sstalu_key    chain     sstalu
6.34 c                   if        %found(sstalu)
6.34 c                   eval      sfsumo = sfsumo * -1
6.34 c                   eval      sfsumb = sfsumb * -1
6.34 c                   eval      sfsumk = sfsumk * -1
     c                   eval      sfrab1 = sfrab1 * -1
     c                   eval      sfrab2 = sfrab2 * -1
     c                   eval      sfrabo = sfrabo * -1
     c                   eval      sfbrk1 = sfbrk1 * -1
     c                   eval      sfbrk2 = sfbrk2 * -1
      * egen test sfsuok
     c                   if        (sfsuok > 0 and sfsumb < 0) or
     c                             (sfsuok < 0 and sfsumb > 0 )
     c                   eval      sfsuok = sfsuok * -1
     c                   endif
     c                   eval      sfkous = %subst(sfkous:1:8) +
     c                                      'EG'
      *
     c                   update    sstalur
     c                   endif
6.34  *
6.34 csr                 endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_paar
      *
      * Key for file : statliner
      *
     c     sstal1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sstal1_paar
      *
      * Key for file : oppdatering av statistikk
      *
     c     sstalu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sstalu_paar
     c                   kfld                    sstalu_binr
     c                   kfld                    sstalu_ornr
     c                   kfld                    sstalu_orsu
     c                   kfld                    sstalu_orli
     c                   kfld                    sstalu_bong
     c                   kfld                    sstalu_vare
     c                   kfld                    sstalu_enhe
     c                   kfld                    sstalu_anta
     c                   kfld                    sstalu_date
     c                   kfld                    sstalu_time
      *
      *
      *  Legge inn firmanummer i nøklene
      *
     c                   eval      w_firm = l_firm
      *
      *
     c                   endsr
