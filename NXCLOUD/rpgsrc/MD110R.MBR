     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : MD110R
      * Beskrivelse . . : Plukkområde, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       181016 bsa  Nytt program
8.01  * 8.xx  301112 bsa  Utvider med RDS lager
8.02  * 8.xx  021212 bsa  Feil i vedlikehold
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flpomlr    if   e           k disk    rename(lpompfr:lpomlrr)
     flpoml1    if   e           k disk    rename(lpompfr:lpoml1r)
     flpomlu    uf a e           k disk    rename(lpompfr:lpomlur)
     flpoml2    if   e           k disk    rename(lpompfr:lpoml2r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fjmodl1    if   e           k disk    rename(jmodpfr:jmodl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
8.01 fra10l1    if   e           k disk    rename(ra10pfr:ra10l1r)
      *
     fmd110d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d lpoml1_olag     s                   like(lpolag)
     d lpoml1_oogr     s                   like(lpoogr)
     d lpoml1_ohgr     s                   like(lpohgr)
     d lpoml1_ougr     s                   like(lpougr)
     d lpoml1_omod     s                   like(lpomod)
     d lpoml1_ovar     s                   like(lpovar)
     d lpomlr_olag     s                   like(lpolag)
     d lpomlr_oogr     s                   like(lpoogr)
     d lpomlr_ohgr     s                   like(lpohgr)
     d lpomlr_ougr     s                   like(lpougr)
     d lpomlr_omod     s                   like(lpomod)
     d lpomlr_ovar     s                   like(lpovar)
     d lpomlu_olag     s                   like(lpolag)
     d lpomlu_oogr     s                   like(lpoogr)
     d lpomlu_ohgr     s                   like(lpohgr)
     d lpomlu_ougr     s                   like(lpougr)
     d lpomlu_omod     s                   like(lpomod)
     d lpomlu_ovar     s                   like(lpovar)
     d lpoml2_opom     s                   like(lpopom)
     d lpoml2_olag     s                   like(lpolag)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d jmodl1_modn     s                   like(jumodn)
     d vvarl1_vare     s                   like(vvvare)
8.01 d ra10l1_jkod     s                   like(rajkod)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_stat          s              1
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(lpofir)
     d w_lage          s                   like(lpolag)
     d w_latx          s             20
      *
     d s_olag          s                   like(lpolag)
     d s_ogrp          s                   like(lpoogr)
     d s_hgrp          s                   like(lpohgr)
     d s_ugrp          s                   like(lpougr)
     d s_modn          s                   like(lpomod)
     d s_vare          s                   like(lpovar)
     d s_opom          s                   like(lpopom)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2taga
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2olag <> 0      or
     c                             b2oogr <> 0      or
     c                             b2ohgr <> 0      or
     c                             b2ougr <> 0      or
     c                             b2omod <> 0      or
     c                             b2ovar <> *blank or
     c                             b2opom <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'L'
     c                   eval      lpoml1_olag = b1olag
     c                   eval      lpoml1_oogr = b1oogr
     c                   eval      lpoml1_ohgr = b1ohgr
     c                   eval      lpoml1_ougr = b1ougr
     c                   eval      lpoml1_omod = b1omod
     c                   eval      lpoml1_ovar = b1ovar
     c     lpoml1_key    setll     lpoml1
     c                   when      w_seqe = 'O'
     c                   eval      lpoml2_opom = b1opom
     c                   eval      lpoml2_olag = b1olag
     c     lpoml2_key    setll     lpoml2
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på lager
      *
     c                   if        b2olag <> 0  or
     c                             b2oogr <> 0  or
     c                             b2ohgr <> 0  or
     c                             b2ougr <> 0  or
     c                             b2omod <> 0  or
     c                             b2ovar <> *blanks
     c                   eval      w_seqe = 'L'
     c                   eval      lpoml1_olag = b2olag
     c                   eval      lpoml1_oogr = b2oogr
     c                   eval      lpoml1_ohgr = b2ohgr
     c                   eval      lpoml1_ougr = b2ougr
     c                   eval      lpoml1_omod = b2omod
     c                   eval      lpoml1_ovar = b2ovar
     c     lpoml1_key    setll     lpoml1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på lokasjon
      *
     c                   if        b2opom <> *blank
     c                   eval      w_seqe = 'O'
     c                   eval      lpoml2_opom = b2opom
     c                   eval      lpoml2_olag = b2olag
     c     lpoml2_key    setll     lpoml2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2olag = 0
     c                   eval      b2oogr = 0
     c                   eval      b2ohgr = 0
     c                   eval      b2ougr = 0
     c                   eval      b2omod = 0
     c                   eval      b2ovar = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1olag = b1olag
     c                   eval      c1oogr = b1oogr
     c                   eval      c1ohgr = b1ohgr
     c                   eval      c1ougr = b1ougr
     c                   eval      c1omod = b1omod
     c                   eval      c1ovar = b1ovar
8.01  * Hent tekster
8.01 c                   if        b1olag <> 0
8.01 c                   eval      ra10l1_jkod = b1olag
8.01 c     ra10l1_key    chain     ra10l1
8.01 c                   if        %found
8.01 c                   eval      c1ltxt = rajtxt
8.01 c                   endif
8.01 c                   endif
8.01  * Overgruppe
8.01 c                   if        b1oogr <> 0
8.01 c                   eval      vogrl1_oogr = b1oogr
8.01 c     vogrl1_key    chain     vogrl1
8.01 c                   if        %found
8.01 c                   eval      c1otxt = vgotxt
8.01 c                   endif
8.01 c                   endif
8.01  * Hovedgruppe
8.01 c                   if        b1oogr <> 0 and
8.01 c                             b1ohgr <> 0
8.01 c                   eval      vhgrl1_hogr = b1oogr
8.01 c                   eval      vhgrl1_hhgr = b1ohgr
8.01 c     vhgrl1_key    chain     vhgrl1
8.01 c                   if        %found
8.01 c                   eval      c1htxt = vghtxt
8.01 c                   endif
8.01 c                   endif
8.01  * Undergruppe
8.01 c                   if        b1oogr <> 0 and
8.01 c                             b1ohgr <> 0 and
8.01 c                             b1ougr <> 0
8.01 c                   eval      vugrl1_uogr = b1oogr
8.01 c                   eval      vugrl1_uhgr = b1ohgr
8.01 c                   eval      vugrl1_uugr = b1ougr
8.01 c     vugrl1_key    chain     vugrl1
8.01 c                   if        %found
8.01 c                   eval      c1utxt = vgutxt
8.01 c                   endif
8.01 c                   endif
8.01  * Modulnummer
8.01 c                   if        b1omod <> 0
8.01 c                   eval      jmodl1_modn = b1omod
8.01 c     jmodl1_key    chain     jmodl1
8.01 c                   if        %found
8.01 c                   eval      c1mtxt = jumte1
8.01 c                   endif
8.01 c                   endif
8.01  * Varenummer
8.01 c                   if        %trim(b1ovar) <> *blanks
8.01 c                   eval      vvarl1_vare = b1ovar
8.01 c     vvarl1_key    chain     vvarl1
8.01 c                   if        %found
8.01 c                   eval      c1vtxt = vvtek1
8.01 c                   endif
8.01 c                   endif
8.01  *
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1olag = b1olag
     c                   eval      k1oogr = b1oogr
     c                   eval      k1ohgr = b1ohgr
     c                   eval      k1ougr = b1ougr
     c                   eval      k1omod = b1omod
     c                   eval      k1ovar = b1ovar
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1olag = b1olag
     c                   eval      d1oogr = b1oogr
     c                   eval      d1ohgr = b1ohgr
     c                   eval      d1ougr = b1ougr
     c                   eval      d1omod = b1omod
     c                   eval      d1ovar = b1ovar
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1olag = b1olag
     c                   eval      c1oogr = b1oogr
     c                   eval      c1ohgr = b1ohgr
     c                   eval      c1ougr = b1ougr
     c                   eval      c1omod = b1omod
     c                   eval      c1ovar = b1ovar
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1olag = 0
     c                   eval      c1oogr = 0
     c                   eval      c1ohgr = 0
     c                   eval      c1ougr = 0
     c                   eval      c1omod = 0
     c                   eval      c1ovar = *blanks
     c                   eval      c1ltxt = *blanks
     c                   eval      c1otxt = *blanks
     c                   eval      c1htxt = *blanks
     c                   eval      c1utxt = *blanks
     c                   eval      c1mtxt = *blanks
     c                   eval      c1vtxt = *blanks
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      c1taga
     c                   endsl
      * Slå av alle feilindikatorer
     c                   eval      *in31 = *off
     c                   eval      *in35 = *off
      *
      * Sjekker gyldig input
      *
     c                   if        c1olag = 0 and
     c                             c1oogr = 0 and
     c                             c1ohgr = 0 and
     c                             c1ougr = 0 and
     c                             c1omod = 0 and
     c                             c1ovar = *blank
     c                   eval      *in31 = *on
     c                   goto      c1taga
     c                   endif
      * Sjekk ugyldige kombinasjoner av varegrupperinger
      * - uten utfylt lager
     c                   if        c1olag =  0 and
     c                             c1oogr <> 0 and
     c                             c1ohgr <> 0 and
     c                             c1ougr <> 0
     c                   eval      *in35 = *on
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1olag =  0 and
     c                             c1oogr =  0 and
     c                             c1ohgr <> 0 and
     c                             c1ougr <> 0
     c                   eval      *in35 = *on
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1olag =  0 and
     c                             c1oogr =  0 and
     c                             c1ohgr =  0 and
     c                             c1ougr <> 0
     c                   eval      *in35 = *on
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1olag =  0 and
     c                             c1oogr =  0 and
     c                             c1ohgr =  0 and
     c                             c1ougr =  0 and
     c                             c1omod <> 0
     c                   eval      *in35 = *on
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1olag =  0 and
     c                             c1oogr =  0 and
     c                             c1ohgr =  0 and
     c                             c1ougr =  0 and
     c                             c1ovar <> *blanks
     c                   eval      *in35 = *on
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1olag =  0 and
     c                             c1oogr =  0 and
     c                             c1ohgr =  0 and
     c                             c1ougr =  0 and
     c                             c1omod <> 0 and
     c                             c1ovar <> *blanks
     c                   eval      *in35 = *on
     c                   goto      c1taga
     c                   endif
      * - med utfylt lager
     c                   if        c1olag <> 0 and
     c                             c1oogr =  0 and
     c                             c1ohgr <> 0 and
     c                             c1ougr <> 0
     c                   eval      *in35 = *on
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1olag <> 0 and
     c                             c1oogr =  0 and
     c                             c1ohgr =  0 and
     c                             c1ougr <> 0
     c                   eval      *in35 = *on
     c                   goto      c1taga
     c                   endif
      * med utfylt lager
     c                   if        c1olag <> 0 and
     c                             c1oogr =  0 and
     c                             c1ohgr <> 0 and
     c                             c1ougr <> 0
     c                   eval      *in35 = *on
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1olag <> 0 and
     c                             c1oogr =  0 and
     c                             c1ohgr =  0 and
     c                             c1ougr =  0 and
     c                             c1omod <> 0 and
     c                             c1ovar <> *blanks
     c                   eval      *in35 = *on
     c                   goto      c1taga
     c                   endif
      *
      * Sjekker gyldig lager
      *
     c                   eval      w_lage = c1olag
     c                   exsr      sjekk_lager
     c                   if        b_feil = *on
     c                   eval      *in31 = *on
     c                   goto      c1taga
     c                   endif
      *
      * Sjekk overgruppe
      *
     c                   if        c1oogr <> 0 and
     c                             c1ohgr =  0 and
     c                             c1ougr =  0
     c                   eval      vogrl1_oogr = c1oogr
     c     vogrl1_key    chain     vogrl1
     c                   if        not %found
     c                   eval      *in32 = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Sjekk hovedgruppe
      *
     c                   if        c1oogr <> 0  and
     c                             c1ohgr <> 0  and
     c                             c1ougr =  0
     c                   eval      vhgrl1_hogr = c1oogr
     c                   eval      vhgrl1_hhgr = c1ohgr
     c     vhgrl1_key    chain     vhgrl1
     c                   if        not %found
     c                   eval      *in33 = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Sjekk undergruppee
      *
     c                   if        c1oogr <> 0  and
     c                             c1ohgr <> 0  and
     c                             c1ougr <> 0
     c                   eval      vugrl1_uogr = c1oogr
     c                   eval      vugrl1_uhgr = c1ohgr
     c                   eval      vugrl1_uugr = c1ougr
     c     vugrl1_key    chain     vugrl1
     c                   if        not %found
     c                   eval      *in34 = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Sjekk modulnummer
      *
     c                   if        c1omod <> 0
     c                   eval      jmodl1_modn = c1omod
     c     jmodl1_key    chain     jmodl1
     c                   if        not %found
     c                   eval      *in36 = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Sjekk modulnummer
      *
     c                   if        c1omod <> 0
     c                   eval      jmodl1_modn = c1omod
     c     jmodl1_key    chain     jmodl1
     c                   if        not %found
     c                   eval      *in36 = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Sjekk varenummer
      *
     c                   if        c1ovar <> *blanks
     c                   eval      vvarl1_vare = c1ovar
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   eval      *in37 = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      lpomlr_olag = c1olag
     c                   eval      lpomlr_oogr = c1oogr
     c                   eval      lpomlr_ohgr = c1ohgr
     c                   eval      lpomlr_ougr = c1ougr
     c                   eval      lpomlr_omod = c1omod
     c                   eval      lpomlr_ovar = c1ovar
     c     lpomlr_key    chain     lpomlr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1olag = 0
     c                   eval      c1oogr = 0
     c                   eval      c1ohgr = 0
     c                   eval      c1ougr = 0
     c                   eval      c1omod = 0
     c                   eval      c1ovar = *blanks
     c                   eval      c1ltxt = *blanks
     c                   eval      c1otxt = *blanks
     c                   eval      c1htxt = *blanks
     c                   eval      c1utxt = *blanks
     c                   eval      c1mtxt = *blanks
     c                   eval      c1vtxt = *blanks
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      lpomlr_olag = c1olag
     c                   eval      lpomlr_oogr = c1oogr
     c                   eval      lpomlr_ohgr = c1ohgr
     c                   eval      lpomlr_ougr = c1ougr
     c                   eval      lpomlr_omod = c1omod
     c                   eval      lpomlr_ovar = c1ovar
     c     lpomlr_key    chain     lpomlr
      *
     c                   if        %found
     c                   eval      c2opom = lpopom
     c                   eval      c2otxt = lpotxt
     c                   eval      c2oeus = lpoeus
8.02 c                   eval      c2orla = lporla
8.02 c                   eval      c2rltx = *blanks
8.02 c                   if        c2orla <> 0
8.02 c                   eval      ra10l1_jkod = c2orla
8.02 c     ra10l1_key    chain     ra10l1
8.02 c                   if        %found
8.02 c                   eval      c2rltx = rajtxt
8.02 c                   endif
8.02 c                   endif
     c     *dmy          move      lpoeda        c2oeda
      *
     c                   else
     c                   eval      c2opom = *blank                                            .
     c                   eval      c2otxt = *blank                                            .
     c                   eval      c2oeus = *blank
     c                   eval      c2oeda = 0
8.02 c                   eval      c2orla = 0
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1olag = k1olag
     c                   eval      c1oogr = k1oogr
     c                   eval      c1ohgr = k1ohgr
     c                   eval      c1ougr = k1ougr
     c                   eval      c1omod = k1omod
     c                   eval      c1ovar = k1ovar
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
8.01 c                   select
8.01 c                   when      *inkc or *inkl
8.01 c**                 if        *inkc or *inkl or *in30
8.01 c                   goto      end_c2bld
8.01 c**                 endif
8.01 c                   when      *inkd
8.01 c                   exsr      spØrring
8.01 c                   goto      c2taga
8.01 c                   endsl
      *
      * Validering av input
      *
     c                   eval      *in41 = *off
     c                   if        c2opom = *blanks or
     c                             c2otxt = *blanks
     c                   eval      *in41 = *on
     c                   goto      c2taga
     c                   endif
8.01  *
8.01  * Sjekker gyldig lager
8.01  *
8.01 c                   if        c2orla <> 0
8.01 c                   eval      w_lage = c2orla
8.01 c                   eval      c2rltx = *blanks
8.01 c                   exsr      sjekk_lager
8.01 c                   if        b_feil = *on
8.01 c                   eval      *in42 = *on
8.01 c                   goto      c2taga
8.01 c                   else
8.01 c                   eval      c2rltx = w_latx
8.01 c                   endif
8.01 c                   endif
      *
      * Oppdatering
      *
     c                   clear                   lpomlur
      *
     c                   eval      lpomlu_olag = c1olag
     c                   eval      lpomlu_oogr = c1oogr
     c                   eval      lpomlu_ohgr = c1ohgr
     c                   eval      lpomlu_ougr = c1ougr
     c                   eval      lpomlu_omod = c1omod
     c                   eval      lpomlu_ovar = c1ovar
     c     lpomlu_key    chain     lpomlur
      *
     c                   eval      lpopom = c2opom                                            .
     c                   eval      lpotxt = c2otxt                                            .
8.01 c                   eval      lporla = c2orla                                            .
      *
     c                   time                    lpoeda
     c                   time                    lpoeti
     c                   eval      lpoeus = l_user
      *
     c                   if        %found
     c                   time                    lpoeda
     c                   time                    lpoeti
     c                   eval      lpoeus = l_user
     c                   update    lpomlur
     c                   else
     c                   eval      lpofir = w_firm
     c                   eval      lpolag = c1olag
     c                   eval      lpoogr = c1oogr
     c                   eval      lpohgr = c1ohgr
     c                   eval      lpougr = c1ougr
     c                   eval      lpomod = c1omod
     c                   eval      lpovar = c1ovar
     c                   time                    lpooda
     c                   time                    lpooti
     c                   eval      lpoous = l_user
     c                   time                    lpoeda
     c                   time                    lpoeti
     c                   eval      lpoeus = l_user
     c                   write     lpomlur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b_forn = *on
     c*                  eval      b1lbes = mdlbes
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
      * Fyller slette-bildet
      *
     c                   eval      lpomlr_olag = d1olag
     c                   eval      lpomlr_oogr = d1oogr
     c                   eval      lpomlr_ohgr = d1ohgr
     c                   eval      lpomlr_ougr = d1ougr
     c                   eval      lpomlr_omod = d1omod
     c                   eval      lpomlr_ovar = d1ovar
     c     lpomlr_key    chain     lpomlr
      *
     c                   eval      d1olag = lpolag
     c                   eval      d1oogr = lpoogr
     c                   eval      d1ohgr = lpohgr
     c                   eval      d1ougr = lpougr
     c                   eval      d1omod = lpomod
     c                   eval      d1ovar = lpovar
     c                   eval      d1opom = lpopom
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      lpomlu_olag = d1olag
     c                   eval      lpomlu_oogr = d1oogr
     c                   eval      lpomlu_ohgr = d1ohgr
     c                   eval      lpomlu_ougr = d1ougr
     c                   eval      lpomlu_omod = d1omod
     c                   eval      lpomlu_ovar = d1ovar
     c     lpomlu_key    chain     lpomlur
      *
     c                   if        %found
     c                   delete    lpomlur
     c                   endif
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_olag = k1olag
     c                   eval      s_ogrp = k1oogr
     c                   eval      s_hgrp = k1ohgr
     c                   eval      s_ugrp = k1ougr
     c                   eval      s_modn = k1omod
     c                   eval      s_vare = k1ovar
      *
      * Fyller kopierings-bildet
      *
     c                   eval      lpomlr_olag = k1olag
     c                   eval      lpomlr_oogr = k1oogr
     c                   eval      lpomlr_ohgr = k1ohgr
     c                   eval      lpomlr_ougr = k1ougr
     c                   eval      lpomlr_omod = k1omod
     c                   eval      lpomlr_ovar = k1ovar
     c     lpomlr_key    chain     lpomlr
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_k1win
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      k1taga
     c                   endsl
      *
      * Sjekker gyldig lager
      *
     c                   eval      w_lage = k1olag
     c                   exsr      sjekk_lager
     c                   if        b_feil = *on
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker gyldig input
      *
     c                   if        k1olag = 0 and
     c                             k1oogr = 0 and
     c                             k1ohgr = 0 and
     c                             k1ougr = 0 and
     c                             k1omod = 0 and
     c                             k1ovar = *blank
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      * Sjekk ugyldige kombinasjoner av varegrupperinger
      * - uten utfylt lager
     c                   if        k1olag =  0 and
     c                             k1oogr <> 0 and
     c                             k1ohgr <> 0 and
     c                             k1ougr <> 0
     c                   eval      *in35 = *on
     c                   goto      k1taga
     c                   endif
      *
     c                   if        k1olag =  0 and
     c                             k1oogr =  0 and
     c                             k1ohgr <> 0 and
     c                             k1ougr <> 0
     c                   eval      *in35 = *on
     c                   goto      k1taga
     c                   endif
      *
     c                   if        k1olag =  0 and
     c                             k1oogr =  0 and
     c                             k1ohgr =  0 and
     c                             k1ougr <> 0
     c                   eval      *in35 = *on
     c                   goto      k1taga
     c                   endif
      *
     c                   if        k1olag =  0 and
     c                             k1oogr =  0 and
     c                             k1ohgr =  0 and
     c                             k1ougr =  0 and
     c                             k1omod <> 0
     c                   eval      *in35 = *on
     c                   goto      k1taga
     c                   endif
      *
     c                   if        k1olag =  0 and
     c                             k1oogr =  0 and
     c                             k1ohgr =  0 and
     c                             k1ougr =  0 and
     c                             k1ovar <> *blanks
     c                   eval      *in35 = *on
     c                   goto      k1taga
     c                   endif
      *
     c                   if        k1olag =  0 and
     c                             k1oogr =  0 and
     c                             k1ohgr =  0 and
     c                             k1ougr =  0 and
     c                             k1omod <> 0 and
     c                             k1ovar <> *blanks
     c                   eval      *in35 = *on
     c                   goto      k1taga
     c                   endif
      * - med utfylt lager
     c                   if        k1olag <> 0 and
     c                             k1oogr =  0 and
     c                             k1ohgr <> 0 and
     c                             k1ougr <> 0
     c                   eval      *in35 = *on
     c                   goto      k1taga
     c                   endif
      *
     c                   if        k1olag <> 0 and
     c                             k1oogr =  0 and
     c                             k1ohgr =  0 and
     c                             k1ougr <> 0
     c                   eval      *in35 = *on
     c                   goto      k1taga
     c                   endif
      * med utfylt lager
     c                   if        k1olag <> 0 and
     c                             k1oogr =  0 and
     c                             k1ohgr <> 0 and
     c                             k1ougr <> 0
     c                   eval      *in35 = *on
     c                   goto      k1taga
     c                   endif
      *
     c                   if        k1olag <> 0 and
     c                             k1oogr =  0 and
     c                             k1ohgr =  0 and
     c                             k1ougr =  0 and
     c                             k1omod <> 0 and
     c                             k1ovar <> *blanks
     c                   eval      *in35 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekk overgruppe
      *
     c                   if        k1oogr <> 0 and
     c                             k1ohgr =  0 and
     c                             k1ougr =  0
     c                   eval      vogrl1_oogr = k1oogr
     c     vogrl1_key    chain     vogrl1
     c                   if        not %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
     c                   endif
      *
      * Sjekk hovedgruppe
      *
     c                   if        k1oogr <> 0  and
     c                             k1ohgr <> 0  and
     c                             k1ougr =  0
     c                   eval      vhgrl1_hogr = k1oogr
     c                   eval      vhgrl1_hhgr = k1ohgr
     c     vhgrl1_key    chain     vhgrl1
     c                   if        not %found
     c                   eval      *in33 = *on
     c                   goto      k1taga
     c                   endif
     c                   endif
      *
      * Sjekk undergruppee
      *
     c                   if        k1oogr <> 0  and
     c                             k1ohgr <> 0  and
     c                             k1ougr <> 0
     c                   eval      vugrl1_uogr = k1oogr
     c                   eval      vugrl1_uhgr = k1ohgr
     c                   eval      vugrl1_uugr = k1ougr
     c     vugrl1_key    chain     vugrl1
     c                   if        not %found
     c                   eval      *in34 = *on
     c                   goto      k1taga
     c                   endif
     c                   endif
      *
      * Sjekk modulnummer
      *
     c                   if        k1omod <> 0
     c                   eval      jmodl1_modn = k1omod
     c     jmodl1_key    chain     jmodl1
     c                   if        not %found
     c                   eval      *in36 = *on
     c                   goto      k1taga
     c                   endif
     c                   endif
      *
      * Sjekk modulnummer
      *
     c                   if        k1omod <> 0
     c                   eval      jmodl1_modn = k1omod
     c     jmodl1_key    chain     jmodl1
     c                   if        not %found
     c                   eval      *in36 = *on
     c                   goto      k1taga
     c                   endif
     c                   endif
      *
      * Sjekk varenummer
      *
     c                   if        k1ovar <> *blanks
     c                   eval      vvarl1_vare = k1ovar
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   eval      *in37 = *on
     c                   goto      k1taga
     c                   endif
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      lpomlr_olag = k1olag
     c                   eval      lpomlr_oogr = k1oogr
     c                   eval      lpomlr_ohgr = k1ohgr
     c                   eval      lpomlr_ougr = k1ougr
     c                   eval      lpomlr_omod = k1omod
     c                   eval      lpomlr_ovar = k1ovar
     c     lpomlr_key    chain     lpomlr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1olag = s_olag
     c                   eval      c1oogr = s_ogrp
     c                   eval      c1ohgr = s_hgrp
     c                   eval      c1ougr = s_ugrp
     c                   eval      c1omod = s_modn
     c                   eval      c1ovar = s_vare
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'L'
     c                   read      lpoml1
     c                   when      w_seqe = 'O'
     c                   read      lpoml2
     c                   endsl
      *
     c                   if        %eof or
     c                             lpofir <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1olag = lpolag
     c                   eval      b1oogr = lpoogr
     c                   eval      b1ohgr = lpohgr
     c                   eval      b1ougr = lpougr
     c                   eval      b1omod = lpomod
     c                   eval      b1ovar = lpovar
     c                   eval      b1opom = lpopom
     c                   eval      b1otxt = lpotxt
8.01 c                   eval      b1orla = lporla
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'L'
     c     lpoml1_key    setgt     lpoml1
     c                   readp     lpoml1
     c                   when      w_seqe = 'O'
     c     lpoml2_key    setgt     lpoml2
     c                   readp     lpoml2
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'L'
     c                   readp     lpoml1
     c                   when      w_seqe = 'O'
     c                   readp     lpoml2
     c                   endsl
      *
     c                   if        %eof or
     c                             lpofir <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'L'
     c     lpoml1_key    setll     lpoml1
     c                   when      w_seqe = 'O'
     c     lpoml2_key    setll     lpoml2
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Lager
      *
     c                   if        w_afld = 'B2OLAG'
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    b2olag
     c                   parm                    c1ltxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'C1OLAG'
     c                   eval      c1ltxt = *blank
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    c1olag
     c                   parm                    c1ltxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'K1OLAG'
     c                   eval      c1ltxt = *blank
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    k1olag
     c                   parm                    c1ltxt
     c                   goto      end_spØrr
     c                   endif
8.01  *
8.01 c                   if        w_afld = 'C2ORLA'
8.01 c                   eval      c2rltx = *blank
8.01 c                   call      'RA510R'
8.01 c                   parm                    w_firm
8.01 c                   parm                    c2orla
8.01 c                   parm                    c2rltx
8.01 c                   goto      end_spØrr
8.01 c                   endif
      *
      * Overgruppe
      *
     c                   if        w_afld = 'B2OOGR'
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b2oogr
     c                   parm                    c1otxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'C1OOGR'
     c                   eval      c1otxt = *blank
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    c1oogr
     c                   parm                    c1otxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'K1OOGR'
     c                   eval      c1otxt = *blank
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    k1oogr
     c                   parm                    c1otxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        w_afld = 'B2OHGR'
     c                   eval      c1htxt = *blanks
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    b2oogr
     c                   parm                    b2ohgr
     c                   parm                    c1htxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'C1OHGR'
     c                   eval      c1htxt = *blank
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    c1oogr
     c                   parm                    c1ohgr
     c                   parm                    c1htxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'K1OHGR'
     c                   eval      c1htxt = *blank
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    k1oogr
     c                   parm                    k1ohgr
     c                   parm                    c1htxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Undergruppe
      *
     c                   if        w_afld = 'B2OUGR'
     c                   eval      c1utxt = *blanks
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    b2oogr
     c                   parm                    b2ohgr
     c                   parm                    b2ougr
     c                   parm                    c1utxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'C1OUGR'
     c                   eval      c1utxt = *blank
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    c1oogr
     c                   parm                    c1ohgr
     c                   parm                    c1ougr
     c                   parm                    c1utxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'K1OUGR'
     c                   eval      c1utxt = *blank
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    k1oogr
     c                   parm                    k1ohgr
     c                   parm                    k1ougr
     c                   parm                    c1utxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Varenummer
      *
     c                   if        w_afld = 'B2OVAR'
     c                   eval      c1vtxt = *blank
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    b2ovar
     c                   parm                    c1vtxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'C1OVAR'
     c                   eval      c1vtxt = *blank
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    c1ovar
     c                   parm                    c1vtxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'K1OVAR'
     c                   eval      c1vtxt = *blank
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    k1ovar
     c                   parm                    c1vtxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Modulnummer
      *
     c                   if        w_afld = 'B2OMOD'
     c                   eval      c1mtxt = *blank
     c                   call      'JU500R'
     c                   parm                    w_firm
     c                   parm                    b2omod
     c                   parm                    c1mtxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'C1OMOD'
     c                   eval      c1mtxt = *blank
     c                   call      'JU500R'
     c                   parm                    w_firm
     c                   parm                    c1omod
     c                   parm                    c1mtxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'K1OMOD'
     c                   eval      c1mtxt = *blank
     c                   call      'JU500R'
     c                   parm                    w_firm
     c                   parm                    k1omod
     c                   parm                    c1mtxt
     c                   goto      end_spØrr
     c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekk/hente lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_lager   begsr
      *
     c                   eval      b_feil = *off
     c                   call      'RS710R'
     c                   parm                    w_stat
     c                   parm                    w_lage
     c                   parm                    w_latx
      *
     c                   if        w_stat <> *blank
     c                   eval      b_feil = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering lager, plukkområdeknytning
      *
     c     lpoml1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lpoml1_olag
     c                   kfld                    lpoml1_oogr
     c                   kfld                    lpoml1_ohgr
     c                   kfld                    lpoml1_ougr
     c                   kfld                    lpoml1_omod
     c                   kfld                    lpoml1_ovar
      *
      * Key for posisjonering lager, plukkområdeknytning
      *
     c     lpomlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lpomlr_olag
     c                   kfld                    lpomlr_oogr
     c                   kfld                    lpomlr_ohgr
     c                   kfld                    lpomlr_ougr
     c                   kfld                    lpomlr_omod
     c                   kfld                    lpomlr_ovar
      *
      * Key for posisjonering lager, plukkområdeknytning
      *
     c     lpomlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lpomlu_olag
     c                   kfld                    lpomlu_oogr
     c                   kfld                    lpomlu_ohgr
     c                   kfld                    lpomlu_ougr
     c                   kfld                    lpomlu_omod
     c                   kfld                    lpomlu_ovar
      *
      * Key for posisjonering lager, plukkområdeknytning
      *
     c     lpoml2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lpoml2_opom
     c                   kfld                    lpoml2_olag
      *
      * Key for posisjonering hovedgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for posisjonering overgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for posisjonering undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for posisjonering modulnummer
      *
     c     jmodl1_key    klist
     c                   kfld                    jmodl1_modn
      *
      * Key for posisjonering vareregister
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
8.01  *
8.01  * Key for posisjonering lager
8.01  *
8.01 c     ra10l1_key    klist
8.01 c                   kfld                    w_firm
8.01 c                   kfld                    ra10l1_jkod
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
      * Initierer skjermbildet
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      w_seqe = 'L'
     c                   eval      lpoml1_olag = 0
     c                   eval      lpoml1_oogr = 0
     c                   eval      lpoml1_ohgr = 0
     c                   eval      lpoml1_ougr = 0
     c                   eval      lpoml1_omod = 0
     c                   eval      lpoml1_ovar = *blanks
     c     lpoml1_key    setll     lpoml1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
