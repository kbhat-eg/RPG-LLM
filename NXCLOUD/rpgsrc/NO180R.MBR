     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : NO180R
      * Beskrivelse . . : Winner, vedlikehold av kobling katalog/leverandør
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       070619 ASK  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fnowcir    if   e           k disk    rename(nowcst:nowcirr)
     fnowci1    if   e           k disk    rename(nowcst:nowci1r)
     fnowci2    if   e           k disk    rename(nowcst:nowci2r)
     fnowciu    uf a e           k disk    rename(nowcst:nowciur)
     frlevlr    if   e           k disk    rename(rlevpfr:rlevplr)
     fvvarlr    if   e           k disk    rename(vvarpfr:vvarplr)
      *
     fno180d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d nowcir_supp     s                   like(ncsupp)
     d nowci1_supp     s                   like(ncsupp)
     d nowci2_ldor     s                   like(ncldor)
     d nowci2_supp     s                   like(ncsupp)
     d nowciu_supp     s                   like(ncsupp)
     d rlevlr_ldor     s                   like(rllevr)
     d vvarlr_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_stat          s              1  0
     d w_retu          s              1
     d w_ltxt          s             20
     d w_ldat          s               d   datfmt(*iso)
     d w_udat          s               d   datfmt(*iso)
      *
     d w_firm          s                   like(ncfirm)
     d w_ldor          s                   like(rllevr)
     d w_lena          s                   like(rlnavn)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      sok_levr
     c                   eval      b2ldor = w_ldor
     c                   goto      b2tagb
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   eval      b_oppr = *on
     c                   exsr      xc2bld
     c                   exsr      forny
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2supp <> *blank or
     c                             b2ldor <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'L'
     c                   eval      nowci2_ldor = b1ldor
     c                   eval      nowci2_supp = b1suppk
     c     nowci2_key    setll     nowci2
     c                   other
     c                   eval      nowci1_supp = b1suppk
     c     nowci1_key    setll     nowci1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Katalog leverandør
      *
     c                   if        b2supp <> *blank
     c                   eval      w_seqe = *blank
     c                   eval      nowci1_supp = b2supp
     c     nowci1_key    setll     nowci1
     c                   goto      end_pos
     c                   endif
      *
      * Leverandør
      *
     c                   if        b2ldor <> 0
     c                   eval      w_seqe = 'L'
     c                   eval      nowci2_ldor = b2ldor
     c                   eval      nowci2_supp = *blank
     c     nowci2_key    setll     nowci2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2supp = *blank
     c                   eval      b2ldor = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      *in31 = *on
     c                   eval      *in32 = *off
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      *in31 = *on
     c                   eval      *in32 = *on
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
      *
      * Initierer bildet
      *
     c                   if        b_oppr = *on
     c                   eval      c2supp = *blank
     c                   eval      c2ctlg = *blank
     c                   eval      c2modl = *blank
     c                   eval      c2expd = 0
     c                   eval      c2ldor = 0
     c                   eval      c2lnav = *blank
     c                   eval      *in31 = *off
     c                   eval      *in32 = *off
     c                   else
     c                   eval      nowcir_supp = b1suppk
     c     nowcir_key    chain     nowcir
     c                   if        not %found
     c                   goto      end_c2bld
     c                   endif
     c                   eval      c2supp = ncsupp
     c                   eval      c2ctlg = ncctlg
     c                   eval      c2modl = ncmodl
     c                   if        nccdat <> *loval
     c     *dmy          move      nccdat        c2expd
     c                   else
     c                   eval      c2expd = 0
     c                   endif
     c                   eval      c2ldor = ncldor
     c                   eval      w_ldor = ncldor
     c                   exsr      finn_levr
     c                   eval      c2lnav = w_lena
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * F4=Spørring
      *
     c                   if        *inkd
     c                   exsr      sok_levr
     c                   eval      c2ldor = w_ldor
     c                   eval      w_ldor = w_ldor
     c                   exsr      finn_levr
     c                   eval      c2lnav = w_lena
     c                   goto      c2taga
     c                   endif
      *
      * Duplikatkontroll
      *
     c                   if        b_oppr = *on
     c                   exsr      finn_dup
     c                   if        b_feil = *on
     c                   exfmt     c3msg
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
      * Kontroll av av input
      *
      * Leverandør
      *
     c                   eval      w_ldor = c2ldor
     c                   exsr      finn_levr
     c                   if        b_feil = *on
     c                   exfmt     c1msg
     c                   goto      c2taga
     c                   endif
      *
      * Diversevarenummer kontroll
      *
     c                   exsr      finn_divvar
     c                   if        b_feil = *on
     c                   exfmt     c2msg
     c                   goto      c2taga
     c                   endif
      *
      * Oppdatering av katalog/leverandørkobling
      *
     c                   clear                   nowciur
      *
     c                   if        b_oppr = *on
     c                   eval      ncfirm = w_firm
     c                   eval      ncsupp = c2supp
     c                   eval      ncctlg = c2ctlg
     c                   eval      ncmodl = c2modl
     c                   if        c2expd > 0
     c     *dmy          move      c2expd        nccdat
     c                   else
     c                   eval      nccdat = *loval
     c                   endif
     c                   eval      ncldor = c2ldor
     c                   write     nowciur
     c                   else
     c                   eval      nowciu_supp = b1suppk
     c     nowciu_key    chain     nowciur
      *
      *
     c                   if        %found(nowciu)
     c                   eval      ncctlg = c2ctlg
     c                   eval      ncmodl = c2modl
     c                   if        c2expd > 0
     c     *dmy          move      c2expd        nccdat
     c                   else
     c                   eval      nccdat = *loval
     c                   endif
     c                   eval      ncldor = c2ldor
     c                   update    nowciur
     c                   endif
     c                   endif
      *
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1ldor = ncldor
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 2
     c                   eval      b_forn = *on
     c                   eval      b_oppr = *off
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30 = *off
     c                   eval      b1ldor = ncldor
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      nowcir_supp = b1suppk
     c     nowcir_key    chain     nowcir
      *
     c                   eval      d1supp = ncsupp
     c                   eval      d1ctlg = ncctlg
     c                   eval      d1modl = ncmodl
     c                   eval      d1ldor = ncldor
     c                   eval      w_ldor = ncldor
     c                   exsr      finn_levr
     c                   eval      d1lnav = w_lena
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter katalog-leverandør kobling
      *
     c                   eval      nowciu_supp = b1suppk
     c     nowciu_key    chain     nowciur
     c                   delete    nowciur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'L'
     c                   read      nowci2
     c                   other
     c                   read      nowci1
     c                   endsl
      *
     c                   if        %eof
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'L'
     c                   eval      nowci2_ldor = ncldor
     c                   other
     c                   eval      nowci1_supp = ncsupp
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1ctlg = ncctlg
     c                   eval      b1supp = ncsupp
     c                   eval      b1ldor = ncldor
     c                   eval      b1ctlgk = ncctlg
     c                   eval      b1suppk = ncsupp
      *
     c                   eval      w_ldor = b1ldor
     c                   exsr      finn_levr
     c                   eval      b1ltek = w_lena

      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'L'
     c     nowci2_key    setgt     nowci2
     c                   other
     c     nowci1_key    setgt     nowci1
     c                   readp     nowci1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'L'
     c                   readp     nowci2
     c                   other
     c                   readp     nowci1
     c                   endsl
      *
     c                   if        %eof or
     c                             ncfirm <> w_firm
     c                   iter
     c                   endif
      *
     c                   if        ncsupp <> *blanks
     c                   iter
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'L'
     c                   eval      nowci2_ldor = ncldor
     c                   other
     c                   eval      nowci1_supp = ncsupp
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'L'
     c     nowci2_key    setll     nowci2
     c                   other
     c     nowci1_key    setll     nowci1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for søk på leverandør
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sok_levr      begsr
      *
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    w_ldor
     c                   parm                    w_lena
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne leverandør
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_levr     begsr
      *
     c                   eval      b_feil = *off
     c                   eval      rlevlr_ldor = w_ldor
     c     rlevlr_key    chain     rlevlr
     c                   if        %found(rlevlr)
     c                   eval      w_lena = rlnavn
     c                   else
     c                   eval      w_lena = *blank
     c                   eval      b_feil = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for duplikatkontroll
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_dup      begsr
      *
     c                   eval      b_feil = *off
     c                   eval      nowcir_supp = c2supp
     c     nowcir_key    chain     nowcir
     c                   if        %found
     c                   eval      b_feil = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kontroll av diversevare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_divvar   begsr
      *
     c                   eval      b_feil = *off
     c                   eval      vvarlr_vare = '00' + %char(w_ldor)
     c     vvarlr_key    chain     vvarlr
     c                   if        not %found(vvarlr)
     c                   eval      b_feil = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på katalog leverandør
      *
     c     nowci1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    nowci1_supp
      *
      * Key for posisjonering på Nexstep leverandør
      *
     c     nowci2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    nowci2_ldor
     c                   kfld                    nowci2_supp
      *
      * Key for les av katalog leverandør
      *
     c     nowcir_key    klist
     c                   kfld                    w_firm
     c                   kfld                    nowcir_supp
      *
      * Key for oppdatering av katalog kobling
      *
     c     nowciu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    nowciu_supp
      *
      * Key for oppslag på leverandør
      *
     c     rlevlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevlr_ldor
      *
      * Key for oppslag på vare
      *
     c     vvarlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlr_vare
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
      * Initierer skjermbildet
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      nowci1_supp = *blank
     c     nowci1_key    setll     nowci1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
