     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : MGB
      * Program . . . . : JP785R
      * Beskrivelse . . : Kopier betingelse i JBEUPF /JRAUPF/JPFUPF/JLEUPF
      *                   til JBETPF / JRAEPF / JPFAPF/JLEVPF
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       151117 bof  Nytt program
6.31  *       060219 bof  Utvidet med jleupf (va leverandør fra MGPRIS)
6.32  *       260221 bof  Utvidet med mgk1st, mgk2st oppd. jkahpf og jkavpf
6.33  *       240321 bof  Utvidet med jvk1pf, jvk2st oppd. jvkhpf og jvklpf
6.34  *       270521 bof  Utvidet med frakt jfbepf
7.01  *       290921 bof  Switch styre om man skal ha frakt
7.02  *       290921 bof  Lagt inn sql code sjekk på delete
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     fjbeupf    if   e           k disk    rename(jbeupfr:jbeupfr)
     fjraupf    if   e           k disk    rename(jraupfr:jraupfr)
     fjpfupf    if   e           k disk    rename(jpfupfr:jpfupfr)
     fjleupf    if   e           k disk    rename(jleupfr:jleupfr)
     fjvkhpf    if   e           k disk    rename(jvkhpfr:jvkhpfr)
6.32 fjkahpf    if   e           k disk    rename(jkahpfr:jkahpfr)
6.32 fjkavpf    if   e           k disk    rename(jkavpfr:jkavpfr)
6.34 fjfbepf    if   e           k disk    rename(jfbepfr:jfbepfr)
      **
6.32 d* ds_mgk1st     E ds                  extName(mgk1st)
6.32 d* ds_mgk2st     E ds                  extName(mgk2st)
6.32 d* mgkv_uid      e                     extfld(uid)
     d
      *
     d b_eof           s              1    inz(*off)
     d b_work          s              1    inz(*off)
      *
      *
     d p_stat          s              1
     d p_forh          s             10
     d p_jkey          s             41
     d w_jbes          s             35
     d w_jnav          s             15
     d w_jtxt          s            200
     d w_jsta          s              2  0
     d w_stat          s              1
6.32 d w_firm          s                   like(jpfirm)
6.32 d w_lova          s                   like(jpedat)
     d w_kamp          s                   like(jmkamp)
     d w_prgr          s                   like(jmprgr)
     d w_besk          s                   like(jmbesk)
     d w_fdat          s                   like(jmfdat)
     d w_tdat          s                   like(jmtdat)
     d w_ansv          s                   like(jmansv)
     d w_foda          s                   like(jmfoda)
     d w_odat          s                   like(jmodat)
     d w_edat          s                   like(jmedat)
     d w_etim          s                   like(jmetim)
     d w_eusr          s                   like(jmeusr)
     d w_vkam          s                   like(jmvkam)
     d w_vprg          s                   like(jmvprg)
     d w_vvar          s                   like(jmvvar)
     d w_vldo          s                   like(jmvldo)
     d w_vkai          s                   like(jmvkai)
     d w_vkpr          s                   like(jmvkpr)
     d w_vkaa          s                   like(jmvkaa)
     d w_vkau          s                   like(jmvkau)
     d w_vans          s                   like(jmvans)
     d w_vidf          s                   like(jmvidf)
     d w_vidt          s                   like(jmvidt)
     d w_vlas          s                   like(jmvlas)
     d w_voda          s                   like(jmvoda)
     d w_veda          s                   like(jmveda)
     d w_veti          s                   like(jmveti)
     d w_veus          s                   like(jmveus)
      *
      * Definering av datastruktur
      *
      *
     d                uds
     d l_user                911    920
7.01 d l_filg                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
7.01 d u_frak          s              1    inz(*off)
      *
7.01   dcl-s co402_verdi1  char(1);
7.01   dcl-s co402_verdi2  char(2048);
7.01   dcl-pr co402r extpgm('CO402R');
7.01     p_filgrp            char(3)     const;
7.01     p_firm              packed(3:0) const;
7.01     p_lager             packed(2:0) const;
7.01     p_nokkel            char(50)    const;
7.01     p_verdi1            char(1);
7.01     p_verdi2            char(2048);
7.01   end-pr;
      *
6.3q C/Exec SQL
6.3q C+  Set Option DatFmt=*ISO , commit=*none
6.3q C/End-Exec
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      w_jnav = 'BETING_IMP' + %trim(l_fnav)
     c                   call      'AB700R  '
     c                   parm                    w_jnav
     c                   parm                    p_jkey
     c                   parm                    w_jtxt
      *
      *
     c                   eval      w_jtxt = 'Starter bet.import til Filgr.'
     c                   eval      w_jsta = 10
     c                   call      'AB705R  '
     c                   parm                    p_jkey
     c                   parm                    w_jsta
     c                   parm                    w_jtxt
      *
      *
6.31 c                   eval      w_jtxt = 'Starter rydder bet/rae/jpfa/jlev'
     c                   eval      w_jsta = 10
     c                   call      'AB705R  '
     c                   parm                    p_jkey
     c                   parm                    w_jsta
     c                   parm                    w_jtxt
      *
     c                   exsr      rydd_beurau
      *
6.31 c                   eval      w_jtxt = 'Starter kopi bet/rae/jpfa/jlev'
     c                   eval      w_jsta = 10
     c                   call      'AB705R  '
     c                   parm                    p_jkey
     c                   parm                    w_jsta
     c                   parm                    w_jtxt
      *
     c                   exsr      skriv_beurau
6.31 c                   eval      w_jtxt = 'Starter rydder jvkhpf, jvklpf'
6.33 c                   eval      w_jsta = 10
6.33 c                   call      'AB705R  '
6.33 c                   parm                    p_jkey
6.33 c                   parm                    w_jsta
6.33 c                   parm                    w_jtxt
6.33  *  logistikk
6.33 c                   exsr      rydd_logi
6.33  *
6.33 c                   eval      w_jtxt = 'Starter kopi jvkhpf,jvklpf'
6.33 c                   eval      w_jsta = 10
6.33 c                   call      'AB705R  '
6.33 c                   parm                    p_jkey
6.33 c                   parm                    w_jsta
6.33 c                   parm                    w_jtxt
6.33  *
6.33 c                   exsr      skriv_logi
      *
      *  kampanje
6.32 c                   exsr      rydd_kahkav
      *
6.32 c                   eval      w_jtxt = 'Starter kopi kah/kav'
6.32 c                   eval      w_jsta = 10
6.32 c                   call      'AB705R  '
6.32 c                   parm                    p_jkey
6.32 c                   parm                    w_jsta
6.32 c                   parm                    w_jtxt
6.32  *
6.32 c                   exsr      skriv_kahkav
      *
     c
      * sier at jobben er ferdig
      *
     c                   call      'AB710R  '
     c                   parm                    p_jkey
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å rydde evt . tidligere innlest.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     rydd_beurau   begsr
      * betingelse
     c/exec sql
     c+ delete
     c+ from   jbetpf
     c/end-exec
      * rabattelement
     c/exec sql
     c+ delete
     c+ from   jraepf
     c/end-exec
      * påslag
     c/exec sql
     c+ delete
     c+ from   jpfapf
     c/end-exec
7.02 c                   if        sqlcod <> 0
7.02 c                   eval      w_jtxt = *blank
7.02 c                   eval      w_jtxt = 'Del JPFAPF ' +
7.02 c                             %char(sqlcod)
7.02 c                   exsr      feil
7.02 c                   endif
6.31  * leverandør
6.31 c/exec sql
6.31 c+ delete
6.31 c+ from   jlevpf
6.31 c/end-exec
6.31  *
7.01 c                   if        u_frak = *on
6.34  * frakt
6.34 c/exec sql
6.34 c+ delete
6.34 c+ from   jfbepf
6.34 c/end-exec
7.01 c                   endif
6.31  *
     c                   endsr
6.33  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.33  * Subrutine for å rydde logistikk-tabeller  .
6.33  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.33  *
6.33 c     rydd_logi     begsr
6.33  * logi. hode
6.33 c/exec sql
6.33 c+ delete
6.33 c+ from   jvkhpf
6.33 c/end-exec
6.33  * logi. hode
6.33 c/exec sql
6.33 c+ delete
6.33 c+ from   jvklpf
6.33 c/end-exec
6.33 c                   endsr
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  * Subrutine for å rydde tidligere innlest.
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  *
6.32 c     rydd_kahkav   begsr
6.32  * Kamp.hode
6.32 c/exec sql
6.32 c+ delete
6.32 c+ from   jkahpf
6.32 c+ where jmansv = 'SOLVE'
6.32 c/end-exec
6.32  * Kamp.linje
6.32 c/exec sql
6.32 c+ delete
6.32 c+ from   jkavpf
6.32 c+ where jmvans = 'SOLVE'
6.32 c/end-exec
6.32  *
6.32 c                   endsr
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å skrive til jbeupf/jraupf/jpfupf fra jbetpf/jraepf/jpfapf
6.34  *                 til jfbepf fra jfbupf
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_beurau  begsr
      * betingelse
     c/exec sql
     c+ insert into jbetpf
     c+ select jpfirm,
     c+        jpldor,
     c+        jpprgr,
     c+        jpogrp,
     c+        jphgrp,
     c+        jpugrp,
     c+        jpmodn,
     c+        jpvare,
     c+        jpvtyp,
     c+        jplety,
     c+        jprsts,
     c+        jpline,
     c+        jpbeti,
     c+        jpfdat,
     c+        jptdat,
     c+        jpdato,
     c+        jpakti,
     c+        jpodat,
     c+        jpedat,
     c+        jpetim,
     c+        jpeusr
     c+   from jbeupf
     c/end-exec
     c                   if        sqlcod <> 0
     c                   eval      w_jtxt = *blank
     c                   eval      w_jtxt = 'JBETPF ' +
     c                             %char(sqlcod)
     c                   exsr      feil
     c                   leavesr
     c                   endif
     c/exec sql
     c+ insert into jraepf
     c+ select jpefir,
     c+        jpeldo,
     c+        jpeprg,
     c+        jpeogr,
     c+        jpehgr,
     c+        jpeugr,
     c+        jpemod,
     c+        jpevar,
     c+        jpevty,
     c+        jpelet,
     c+        jpebrs,
     c+        jpebli,
     c+        jperst,
     c+        jpelin,
     c+        jperae,
     c+        jpereg,
     c+        jpever,
     c+        jpefda,
     c+        jpetda,
     c+        jpeakt,
     c+        jpeoda,
     c+        jpeeda,
     c+        jpeeti,
     c+        jpeeus
     c+        from jraupf
     c/end-exec
     c                   if        sqlcod <> 0
     c                   eval      w_jtxt = *blank
     c                   eval      w_jtxt = 'JRAEPF ' +
     c                             %char(sqlcod)
     c                   exsr      feil
     c                   leavesr
     c                   endif
      * påslag
     c/exec sql
     c+ insert into jpfapf
     c+ select jcfirm,
     c+        jcprgr,
     c+        jcogrp,
     c+        jchgrp,
     c+        jcugrp,
     c+        jcldor,
     c+        jcmodn,
     c+        jcvare,
     c+        jckpri,
     c+        jcvtyp,
     c+        jclety,
     c+        jckofa,
     c+        jckobe,
     c+        jcsafa,
     c+        jcsphl,
6.31 c+        jcnobb,
6.31 c+        jcsano,
     c+        jcodat,
     c+        jcedat,
     c+        jcetim,
     c+        jceusr
     c+        from jpfupf
     c/end-exec
     c                   if        sqlcod <> 0
     c                   eval      w_jtxt = *blank
     c                   eval      w_jtxt = 'JPFAPF ' +
     c                             %char(sqlcod)
     c                   exsr      feil
     c                   leavesr
     c                   endif
6.31  * leverandør
6.31 c/exec sql
6.31 c+ insert into jlevpf
6.31 c+ select *  from jleupf
6.31 c/end-exec
6.31 c                   if        sqlcod <> 0
6.31 c                   eval      w_jtxt = *blank
6.31 c                   eval      w_jtxt = 'JLEVPF ' +
6.31 c                             %char(sqlcod)
6.31 c                   exsr      feil
6.31 c                   leavesr
6.31 c                   endif
7.01 c                   if        u_frak = *on
6.34  * frakt
6.34 c/exec sql
6.34 c+ insert into jfbepf
6.34 c+ select *  from jfbupf
6.34 c/end-exec
6.34 c                   if        sqlcod <> 0
6.34 c                   eval      w_jtxt = *blank
6.34 c                   eval      w_jtxt = 'JFBEPF ' +
6.34 c                             %char(sqlcod)
6.34 c                   exsr      feil
6.34 c                   leavesr
6.34 c                   endif
7.01 c                   endif
6.31  ** setter kalkylekode på jlevpf
6.31 c*/exec sql
6.31 c*+ update jlevpf
6.31 c*+ set jlkkal = 1
6.31 c*+ where jlldor in (select jcldor from jpfupf
6.31 c*+                  where jckkal = 1)
6.31 c*/end-exec
6.31 c*                   if        sqlcod <> 0 and
6.31 c*                             sqlcod <> 100
6.31 c*                   eval      w_jtxt = *blank
6.31 c*                   eval      w_jtxt = 'JLEVPF 2'
6.31 c*                   exsr      feil
6.31 c*                   leavesr
6.31 c*                   endif
6.31  *
6.31  **
6.31 c*/exec sql
6.31 c*+ commit
6.31 c*/end-exec
      *
     c                   endsr
6.33  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.33  * Subrutine for å skrive til jkahpf/jkavpf
6.33  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.33  *
6.33 c     skriv_logi    begsr
6.33  * logistikk hode
6.33 c/exec sql
6.33 c+ insert into jvkhpf
6.33 c+ select *  from jvk1pf
6.33 c/end-exec
6.33 c                   if        sqlcod <> 0
6.33 c                   eval      w_jtxt = *blank
6.33 c                   eval      w_jtxt = 'JVKHPF ' +
6.33 c                             %char(sqlcod)
6.33 c                   exsr      feil
6.33 c                   leavesr
6.33 c                   endif
6.33  * logistikk linjer
6.33 c/exec sql
6.33 c+ insert into jvklpf
6.33 c+ select *  from jvk2pf
6.33 c/end-exec
6.33 c                   if        sqlcod <> 0
6.33 c                   eval      w_jtxt = *blank
6.33 c                   eval      w_jtxt = 'JVKLPF ' +
6.33 c                             %char(sqlcod)
6.33 c                   exsr      feil
6.33 c                   leavesr
6.33 c                   endif
6.33 c                   endsr
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  * Subrutine for å skrive til jkahpf/jkavpf
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  *
6.32 c     skriv_kahkav  begsr
     c
6.39  * hode
6.39 c/exec sql
6.39 c+ declare hode cursor for
6.39 c+ select jmkamp,
6.39 c+        jmprgr,
6.39 c+        jmbesk,
6.39 c+        jmfdat,
6.39 c+        jmtdat,
6.39 c+        jmansv,
6.39 c+        jmfoda,
6.39 c+        jmodat,
6.39 c+        jmedat,
6.39 c+        jmetim,
6.39 c+        jmeusr
6.39 c+ from    mgk1st
6.39 c+ for read only
6.39 c/end-exec
6.39 c/exec sql
6.39 c+ open hode
6.39 c/end-exec
6.39 c                   eval      b_eof = *off
6.39 c                   dow       b_EOF = *off
6.39  *
6.39 c/exec sql
6.39 c+ fetch hode
6.39 c+ into  :w_kamp,
6.39 c+       :w_prgr,
6.39 c+       :w_besk,
6.39 c+       :w_fdat,
6.39 c+       :w_tdat,
6.39 c+       :w_ansv,
6.39 c+       :w_foda,
6.39 c+       :w_odat,
6.39 c+       :w_edat,
6.39 c+       :w_etim,
6.39 c+       :w_eusr
6.39 c/end-exec
6.39 c                   if        sqlcod = 100 or sqlstt = '02000'
6.39 c/exec sql
6.39 c+  close hode
6.39 c/end-exec
6.39 c                   leave
6.39 c                   endif
6.39 c                   if        sqlcod < 0
6.39 c                   if        sqlcod <> 0
6.39 c                   eval      w_jtxt = *blank
6.39 c                   eval      w_jtxt = 'MGK1ST '
6.39 c                   exsr      feil
6.39 c                   leavesr
6.39 c                   endif
6.39 c                   endif
6.39  * insert
6.39 c/exec sql
6.39 c+ insert into jkahpf
6.39 c+      (jmkamp,
6.39 c+       jmprgr,
6.39 c+       jmbesk,
6.39 c+       jmfdat,
6.39 c+       jmtdat,
6.39 c+       jmansv,
6.39 c+       jmfoda,
6.39 c+       jmodat,
6.39 c+       jmedat,
6.39 c+       jmetim,
6.39 c+       jmeusr)
6.39 c+       values
6.39 c+      (:w_kamp,
6.39 c+       :w_prgr,
6.39 c+       :w_besk,
6.39 c+       :w_fdat,
6.39 c+       :w_tdat,
6.39 c+       :w_ansv,
6.39 c+       :w_foda,
6.39 c+       :w_odat,
6.39 c+       :w_edat,
6.39 c+       :w_etim,
6.39 c+       :w_eusr)
6.39 c/end-exec
6.39 c                   if        sqlcod < 0
6.39 c                   if        sqlcod <> 0
6.39 c                   eval      w_jtxt = *blank
6.39 c                   eval      w_jtxt = 'Insert JKAHPF '
6.39 c                   exsr      feil
6.39 c                   leavesr
6.39 c                   endif
6.39 c                   endif
6.39 c                   enddo
      *
6.39  * kampanje linjer
6.39 c/exec sql
6.39 c+ declare linjer cursor for
6.39 c+ select jmvkam,
6.39 c+        jmvprg,
6.39 c+        jmvvar,
6.39 c+        jmvldo,
6.39 c+        jmvkai,
6.39 c+        jmvkpr,
6.39 c+        jmvkaa,
6.39 c+        jmvkau,
6.39 c+        jmvans,
6.39 c+        jmvidf,
6.39 c+        jmvidt,
6.39 c+        jmvlas,
6.39 c+        jmvoda,
6.39 c+        jmveda,
6.39 c+        jmveti,
6.39 c+        jmveus
6.39 c+ from    mgk2st
6.39 c+ for read only
6.39 c/end-exec
      *
6.39 c/exec sql
6.39 c+ open linjer
6.39 c/end-exec
6.39 c                   eval      b_eof = *off
6.39 c                   dow       b_EOF = *off
6.39  *
6.39 c/exec sql
6.39 c+ fetch linjer
6.39 c+ into  :w_vkam,
6.39 c+       :w_vprg,
6.39 c+       :w_vvar,
6.39 c+       :w_vldo,
6.39 c+       :w_vkai,
6.39 c+       :w_vkpr,
6.39 c+       :w_vkaa,
6.39 c+       :w_vkau,
6.39 c+       :w_vans,
6.39 c+       :w_vidf,
6.39 c+       :w_vidt,
6.39 c+       :w_vlas,
6.39 c+       :w_voda,
6.39 c+       :w_veda,
6.39 c+       :w_veti,
6.39 c+       :w_veus
6.39 c/end-exec
6.39 c                   if        sqlcod = 100 or sqlstt = '02000'
6.39 c/exec sql
6.39 c+  close linjer
6.39 c/end-exec
6.39 c                   leave
6.39 c                   endif
6.39 c                   if        sqlcod <> 0
6.39 c                   eval      w_jtxt = *blank
6.39 c                   eval      w_jtxt = 'MGK2ST '
6.39 c                   exsr      feil
6.39 c                   leavesr
6.39 c                   endif
6.39  * insert
6.39 c/exec sql
6.39 c+ insert into jkavpf
6.39 c+      (jmvkam,
6.39 c+       jmvprg,
6.39 c+       jmvvar,
6.39 c+       jmvldo,
6.39 c+       jmvkai,
6.39 c+       jmvkpr,
6.39 c+       jmvkaa,
6.39 c+       jmvkau,
6.39 c+       jmvans,
6.39 c+       jmvidf,
6.39 c+       jmvidt,
6.39 c+       jmvlas,
6.39 c+       jmvoda,
6.39 c+       jmveda,
6.39 c+       jmveti,
6.39 c+       jmveus)
6.39 c+       values
6.39 c+      (:w_vkam,
6.39 c+       :w_vprg,
6.39 c+       :w_vvar,
6.39 c+       :w_vldo,
6.39 c+       :w_vkai,
6.39 c+       :w_vkpr,
6.39 c+       :w_vkaa,
6.39 c+       :w_vkau,
6.39 c+       :w_vans,
6.39 c+       :w_vidf,
6.39 c+       :w_vidt,
6.39 c+       :w_vlas,
6.39 c+       :w_voda,
6.39 c+       :w_veda,
6.39 c+       :w_veti,
6.39 c+       :w_veus)
6.39 c/end-exec
6.39 c                   if        sqlcod < 0
6.39 c                   if        sqlcod <> 0
6.39 c                   eval      w_jtxt = *blank
6.39 c                   eval      w_jtxt = 'Insert JKAVPF '
6.39 c                   exsr      feil
6.39 c                   leavesr
6.39 c                   endif
6.39 c                   endif
6.39 c                   enddo
6.32 c                   endsr
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     feil          begsr
      *
     c                   eval      w_jtxt = w_jtxt +
     c                                      'Feil med SQL ' + %char(sqlcod)
     c                   eval      w_jsta = 10
     c                   call      'AB705R  '
     c                   parm                    p_jkey
     c                   parm                    w_jsta
     c                   parm                    w_jtxt
     c                   eval      w_stat = '1'
     c                   endsr

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
6.32 c*                   eval      w_firm = l_firm
6.32 c*                   eval      w_lova = *loval
      *
7.01  * Skal det settes automatisk fakturatype
7.01  *
7.01   CallP CO402R(l_filg:w_firm:0:'FRAKT_FRA_MGPRIS':
7.01                    co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01     u_frak = *on;
7.01   endif;
     c                   endsr
