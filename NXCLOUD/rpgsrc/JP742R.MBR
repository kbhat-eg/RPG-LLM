     HDFTACTGRP(*NO)
      /free
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JP742R
      * Beskrivelse . . : Leser NOBB-kontrakter fra XML format (http://NOBB.Schemas.Avtale).
      *                   Skriver betingelser til jbeiPF og betingelseselementer til jraiPF.
      *
      *                   Dette programmet er skrevet i free format RPG og benytter dynamisk SQL.
      *                   (SQL-insert krever at jbeiPF og jraiPF bruker journalisering).
      *
      * XML ParserError : Link til feilkoder: https://www.ibm.com/support/knowledgecenter/ssw
      * https://www.ibm.com/support/knowledgecenter/en/ssw_ibm_i_61/rzasc/sc092507227.htm
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       120617 ARENG  Nytt program
9.01  *       110121 bjmar  Sett JLSINT for hver betingelse lev. som leses inn.
9.21  *       260121 bjmar  Utvidet avtale array pga. Luna
9.22  *       220421 bjmar  Feilretting vedr. tildato som hang igjen.
9.23  *       200821 bjmar  Gjort om til innlesing i Nexstep
9.24  *       111224 bjmar  Feil med behandling av innleste levendører
9.25  *       250325 bjmar  Feilretting av 9.24
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Prototype for dette programmet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
       dcl-pr JP742R  extpgm('JP742R');
         filNavn       char(255)   options(*varsize);
         leverandorer  char(12000);
         antallLev     packed(4);
         status        char(1);
       end-pr;

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Definering av Local data area *LDA
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
       dcl-ds *n dtaara(*auto) ;
         ldaUserId char(10) pos(911);
         ldaFirmaNr zoned (3:0) pos(944);
       end-ds;

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Prototype grensesnitt-definisjoner
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
       dcl-pr P_AB705R extpgm('AB705R');
         p_jbid        char(41);
         p_jsts        packed(2:0);
         p_jmld        char(200);
       end-pr;

       dcl-pr P_AB710R extpgm('AB710R');
         p_jnav        char(15);
         p_jbid        char(41);
       end-pr;

       dcl-pr P_AB700R extpgm('AB700R');
         p_jnav        char(15);
         p_jbid        char(41);
         p_jtxt        char(35);
       end-pr;

       dcl-pr P_AS100R extpgm('AS100R');
         p_kode        char(1);
         p_firm        like(jpfirm);
         p_fell        char(6);
         p_type        char(2);
         p_fast        char(10);
         p_numm        packed(8);
       end-pr;

      * Definering av tabell med meldinger (se ** nederst i denne fila)
       dcl-s a_meld    char(100) dim(4) ctdata perrcd(1);

       dcl-ds pgm_stat PSDS qualified;
          status *STATUS;
          routine *ROUTINE;
          exceptionType char(3) pos(40);
          exceptionNo char(4) pos(43);
          library    char(10) POS(81);
          xmlRc      int(10:0) pos(368);
       end-ds;

9.21   dcl-ds vareArray likeds(discount_t) dim(40000);
9.21   dcl-s vareIndex  packed(6:0) inz(0);

9.21   dcl-ds modulArray likeds(discount_t) dim(40000);
9.21   dcl-s modulIndex  packed(6:0) inz(0);

9.21   dcl-ds vgrpArray likeds(discount_t) dim(40000);
9.21   dcl-s vgrpIndex  packed(6:0) inz(0);

9.21   dcl-ds hgrpArray  likeds(discount_t) dim(40000);
9.21   dcl-s hgrpIndex  packed(6:0) inz(0);

9.21   dcl-ds ogrpArray  likeds(discount_t) dim(40000);
9.21   dcl-s ogrpIndex  packed(6:0) inz(0);

9.21   dcl-ds levArray  likeds(discount_t) dim(40000);
9.21   dcl-s levIndex  packed(6:0) inz(0);

9.21   dcl-ds avtArray  likeds(discount_t) dim(40000);
9.21   dcl-s avtIndex  packed(6:0) inz(0);

       dcl-ds levBetArray likeds(discount_t) dim(2000);
9.21   dcl-s levBetIndex  packed(6:0) inz(0);

       dcl-ds ogrBetArray likeds(discount_t) dim(2000);
9.21   dcl-s ogrBetIndex  packed(6:0) inz(0);

       dcl-ds hgrBetArray likeds(discount_t) dim(2000);
9.21   dcl-s hgrBetIndex  packed(6:0) inz(0);

       dcl-ds vgrBetArray likeds(discount_t) dim(2000);
9.21   dcl-s vgrBetIndex  packed(6:0) inz(0);

       dcl-ds modBetArray likeds(discount_t) dim(2000);
9.21   dcl-s modBetIndex  packed(6:0) inz(0);

       dcl-ds varBetArray likeds(discount_t) dim(2000);
9.21   dcl-s varBetIndex  packed(6:0) inz(0);

       dcl-ds rabElemArray likeds(discount_t) dim(1000);
9.21   dcl-s rabElemIndex  packed(6:0) inz(0);

9.21   dcl-s forIndex  packed(6:0) inz(0);

       dcl-ds discount_t;
         avtNr         char(6);
         mottakerNr    char(6);
         eierNr        char(6);
         avtaledato    char(25);
         sendedato     char(25);
         opprettetdato char(25);
         godkjentdato  char(25);
         avtaletekst   char(50);
         deltagerNr    char(6);
         overgruppe    char(2);
         hovedgruppe   char(4);
         varegruppe    char(7);
         modul         char(8);
         nobbnummer    char(8);
         rabbetnavn    char(35);
         rabbetakt     char(5);
         rabeltype     char(12);
         rabelakt      char(5);
         rabelnavn     char(35);
         rabelregel    char(3);
         rabelverdi    char(8);
         rabelfdat     char(10);
         rabeltdat     char(10);
         lineno        packed(10);
       end-ds;

       dcl-ds conditions_t;
         isAvtKomplet  ind;
         isAvtale      ind;
         isAvtaleDato  ind;
         isAvtaleDatoDato ind;
         isAvtaleDatoType ind;
         isLeverandor  ind;
         isRabattBet   ind;
         isOvergrp     ind;
         isHovedgrp    ind;
         isVaregrp     ind;
         isModul       ind;
         isVare        ind;
         isRabattElem  ind;
         isAvtaleNr    ind;
         isMottaker    ind;
         isEier        ind;
         isAvtaleTekst ind;
         isDeltagerNr  ind;
         isBetNavn     ind;
         isBetAktiv    ind;
         isOvergrpId   ind;
         isHovedgrpId  ind;
         isVaregrpId   ind;
         isModulNr     ind;
         isNobbNummer  ind;
         isRabElType   ind;
         isRabElAktiv  ind;
         isRabElNvn    ind;
         isRabElRegel  ind;
         isRabElVerdi  ind;
         isRabElFraD   ind;
         isRabElTilD   ind;
       end-ds;

      * datastruktur som brukes for å kommunisere med XML-parser
       dcl-ds handlerInfo_t qualified;
         accumulators  likeds (accumulators_t);
         conditions    likeds (conditions_t);
         discounts     likeds (discount_t);
       end-ds;

      * Deklarasjon av info som overføres til XML-handler
       dcl-ds xmlHandlerInfo likeds (handlerInfo_t);
      *
       dcl-ds accumulators_t qualified;
9.21     antAvtaler      int(10:0) inz(0);
9.21     antLeverand     int(10:0) inz(0);
9.21     antRabBet       int(10:0) inz(0);
9.21     antRabElem      int(10:0) inz(0);
         sekvensNr       packed(8) inz(0);
       end-ds;

      * Job log teller
       dcl-ds ids;
         d_job_log      char(50);
         w_jmstt        char(5) pos(2);
         w_jmsdt        char(5) pos(7);
         w_jmtxt        char(5) pos(12);
         w_jmodt        char(5) pos(17);
       end-ds;

      * Brukes som parametre til jobblogging
       dcl-s p_jnav       char(15);
       dcl-s p_jbid       char(41);
       dcl-s p_jsts       packed(2:0);
       dcl-s p_jmld       char(200);
       dcl-s p_jtxt       char(35);
       dcl-s p_stat       char(1);

      * Brukes for å hente sekvensnummer
       dcl-s  p_kode      char(1);
       dcl-s  p_firm      like(jpfirm);
       dcl-s  p_fell      char(6);
       dcl-s  p_type      char(2);
       dcl-s  p_fast      char(10);
       dcl-s  p_numm      packed(8);

      * Arbeidsvariabler
       dcl-s filNavnTrim     varchar(255);
       dcl-s res             char(1);
       dcl-s callRes         char(800);
9.21   dcl-s temp            packed(6:0) inz (0);
       dcl-s previousLineNo  packed(10) inz(0);
       dcl-s messagetext     varchar(32740);
       dcl-s messageId       char(10);
       dcl-s messageId1      varchar(7);
       dcl-s messageId2      like(messageId1);
       dcl-s currentSqlState char(35);
       dcl-s rabattElementNo packed(10) inz(0);
9.01   dcl-s b_first         char(1) inz(*off);


       dcl-ds leverandorRec;
          leverandorRes   char(12000) inz('');
          leverandorArray char(6) dim(2000) overlay(leverandorRes);
       end-ds;


      * SQL-Host variabler for jbeiPF
       dcl-ds jbeipf;
         jpfirm          packed(3) inz(0);
         jpldor          packed(6) inz(0);
         jpprgr          char(2)   inz('');
         jpogrp          packed(2) inz(0);
         jphgrp          packed(2) inz(0);
         jpugrp          packed(3) inz(0);
         jpmodn          packed(8) inz(0);
         jpvare          char(15)  inz('');
         jpvtyp          char(1)   inz('');
         jplety          packed(1) inz(0);
         jprsts          packed(1) inz(0);
         jpline          packed(10)inz(0);
         jpbeti          char(35)  inz('');
         jpfdat          char(10)  inz('0001-01-01');
         jptdat          char(10)  inz('0001-01-01');
         jpdato          char(10)  inz('0001-01-01');
         jpakti          packed(1) inz(0);
         jpavnr          packed(6) inz(0);
         jpodat          char(10)  inz('0001-01-01');
         jpedat          char(10)  inz('0001-01-01');
         jpetim          char(8)   inz('00:00:00');
         jpeusr          char(10)  inz('');
       end-ds;

      * SQL-Host variabler for jraiPF
       dcl-ds jraipf;
         jpefir	packed	(3) inz(0);
         jpeldo	packed	(6) inz(0);
         jpeprg	char	  (2) inz('');
         jpeogr	packed	(2) inz(0);
         jpehgr	packed	(2) inz(0);
         jpeugr	packed	(3) inz(0);
         jpemod	packed	(8) inz(0);
         jpevar	char	  (15)inz('');
         jpevty	char	  (1) inz('');
         jpelet	packed	(1) inz(0);
         jpebrs	packed	(1) inz(0);
         jpebli	packed	(10) inz(0);
         jperst	packed	(1) inz(0);
         jpelin	packed	(10) inz(0);
         jperae	char	  (35) inz('');
         jpereg	char	  (1) inz('');
         jpever	packed	(9:2) inz(0);
         jpefda	char	  (10) inz('');
         jpetda	char	  (10) inz('');
         jpeakt	packed  (1) inz(0);
         jpeoda  char    (10) inz('0001-01-01');
         jpeeda  char    (10) inz('0001-01-01');
         jpeeti  char    (8) inz ('00:00:00');
         jpeeus  char    (10) inz('');
       end-ds;

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
       dcl-pi JP742R;
         filNavn          char(255)    options(*varsize);
         leverandorer     char(12000);
         antallLev        packed(4);
         status           char(1);
       end-pi;

       filnavnTrim = SubString(filnavn:100:'.XML');
       filnavnTrim = SubString(filnavnTrim:100:'.xml');
       filnavnTrim = %trim(filnavnTrim) + '.XML';
       leverandorer = '';
       leverandorRes = '';
       antallLev = 0;
       status = '';

      * Initiell jobblogging
       StartLogging();

       xmlHandlerInfo.conditions.isAvtale = *off;
       xmlHandlerInfo.conditions.isAvtaleDato = *off;
       xmlHandlerInfo.conditions.isAvtaleDatoDato = *off;
       xmlHandlerInfo.conditions.isAvtaleDatoType = *off;
       xmlHandlerInfo.conditions.isLeverandor = *off;
       xmlHandlerInfo.conditions.isRabattBet = *off;
       xmlHandlerInfo.conditions.isOvergrp = *off;
       xmlHandlerInfo.conditions.isHovedgrp = *off;
       xmlHandlerInfo.conditions.isVaregrp  = *off;
       xmlHandlerInfo.conditions.isModul = *off;
       xmlHandlerInfo.conditions.isVare = *off;
       xmlHandlerInfo.conditions.isRabattElem = *off;
       xmlHandlerInfo.conditions.isAvtaleNr = *off;
       xmlHandlerInfo.conditions.isMottaker = *off;
       xmlHandlerInfo.conditions.isEier = *off;
       xmlHandlerInfo.conditions.isAvtaleTekst = *off;
       xmlHandlerInfo.conditions.isDeltagerNr = *off;
       xmlHandlerInfo.conditions.isBetNavn = *off;
       xmlHandlerInfo.conditions.isBetAktiv = *off;
       xmlHandlerInfo.conditions.isOvergrpId = *off;
       xmlHandlerInfo.conditions.isHovedgrpId = *off;
       xmlHandlerInfo.conditions.isVaregrpId = *off;
       xmlHandlerInfo.conditions.isModulNr = *off;
       xmlHandlerInfo.conditions.isNobbNummer = *off;
       xmlHandlerInfo.conditions.isRabElType = *off;
       xmlHandlerInfo.conditions.isRabElAktiv = *off;
       xmlHandlerInfo.conditions.isRabElNvn = *off;
       xmlHandlerInfo.conditions.isRabElRegel = *off;
       xmlHandlerInfo.conditions.isRabElVerdi = *off;
       xmlHandlerInfo.conditions.isRabElFraD = *off;
       xmlHandlerInfo.conditions.isRabElTilD = *off;

       monitor;
         // Start XML parsing first pass
         XML-SAX %HANDLER(xmlSaxHandler : xmlHandlerInfo)
                  %XML(filNavnTrim : 'doc=file');
       // The XML parse completed normally
       // Results are passed back in the communication
       // area specified by the %HANDLER built-in function
       on-error 00351;
         // The XML parse failed with a parser error.
         // The return code from the parser is in the PSDS.
         p_stat = '1';
         p_jmld = ('XML parser error on file:' + filNavnTrim + ',rc='
           + %CHAR(pgm_stat.xmlRc) +
           ' ' + pgm_stat.exceptionType + pgm_stat.exceptionNo);
         ErrorLogging(p_jmld);
         EndLogging();
         *inlr = *on;
         status = 'C';
         return;
       on-error 00354;
         // The XML parse failed with a file not found
         // The return code from the parser is in the PSDS.
         p_stat = '1';
         p_jmld = ('XML parser error on file:' + filNavnTrim + ',rc='
           + %CHAR(pgm_stat.xmlRc) +
           ' ' + pgm_stat.exceptionType + pgm_stat.exceptionNo);
         ErrorLogging(p_jmld);
         EndLogging();
         *inlr = *on;
         status = 'N';
         return;
       on-error;
         p_stat = '1';
         p_jmld = ('Failed to parse:' + filNavnTrim + ', rc='
           + %CHAR(pgm_stat.xmlRc) + ' ' + %char(pgm_stat.status)
           + ' ' + pgm_stat.exceptionType + pgm_stat.exceptionNo);
         ErrorLogging(p_jmld);
         EndLogging();
         *inlr = *on;
         status = 'F';
         return;
       endmon;

       // hvis ikke feilstatus er satt så skrives avtalene
       if not (p_stat = '1');
         // sletter først alle leverandører som er med i inputfilen
         previousLineNo = 0;
         for forIndex = 1 to avtindex;
           // returnerer en liste med leverandører.
           if not (avtarray(forindex).lineNo = previousLineNo);
             callRes = SlettBetingelserForLev(avtarray(forindex));
             if not (callres = 'success');
               ErrorLogging(callres);
               p_jmld = *blanks;
               p_stat = '1';
               leave;
             endif;
           endif;
           previousLineNo = avtarray(forindex).lineNo;
         endfor;

         previousLineNo = 0;
         for forIndex = 1 to avtindex;
           if not (avtarray(forindex).lineNo = previousLineNo);
             rabattElementNo = 0;
             callRes = SkrivRabattBetingelse(avtarray(forindex));
             if not (callres = 'success');
               ErrorLogging(callres);
               p_jmld = *blanks;
               p_stat = '1';
               leave;
             endif;
           endif;
           rabattElementNo += 1;
           callRes = SkrivRabattElement(avtarray(forindex):rabattElementNo);
           if not (callres = 'success');
             ErrorLogging(callres);
             p_jmld = *blanks;
             p_stat = '1';
             leave;
           endif;
           previousLineNo = avtarray(forindex).lineNo;
         endfor;
       endif;

       if p_stat = '1';
         exec sql rollback;
       else;
         exec sql commit;
       endif;

       if antallLev > 0;
         leverandorer = leverandorRes;
       endif;

      * Avsluttende jobblogging
       EndLogging();
       //dsply ('Ferdig') '' res;

       *inlr = *on;
       return;

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Procedyre som parser XML-fila
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
       dcl-proc xmlSaxHandler;
          dcl-pi *N int(10:0);
             info        likeds(handlerInfo_t);
             event       int(10:0) value;
             stringPtr   pointer value;
             stringLen   int(20:0) value;
             exceptionId int(10:0) value;
          end-pi;

          dcl-s chars     char(65535)    BASED(stringPtr);


          select;
          // start parsing
          when event = *XML_START_DOCUMENT;
             clear info;
          when event = *XML_START_ELEMENT;
             if info.conditions.isAvtale = *On;
                if %subst(chars : 1 : stringLen) = 'AvtaleNr';
                   info.conditions.isAvtaleNr = *On;
                endif;
                if %subst(chars : 1 : stringLen) = 'Mottaker';
                   info.conditions.isMottaker = *On;
                endif;
                if %subst(chars : 1 : stringLen) = 'Eier';
                   info.conditions.isEier = *On;
                endif;
                if %subst(chars : 1 : stringLen) = 'AvtaleTekst';
                   info.conditions.isAvtaleTekst = *On;
                endif;
                if info.conditions.isAvtaleDato  = *On;
                  if %subst(chars : 1 : stringLen) = 'Dato';
                     info.conditions.isAvtaleDatoDato = *On;
                  endif;
                  if %subst(chars : 1 : stringLen) = 'DatoType';
                     info.conditions.isAvtaleDatoType = *On;
                  endif;
                endif;
             endif;

             if info.conditions.isLeverandor = *On;
                if %subst(chars : 1 : stringLen) = 'Deltagernummer';
                   info.conditions.isDeltagerNr = *On;
                endif;
             endif;

             if info.conditions.isOvergrp = *On;
                if %subst(chars : 1 : stringLen) = 'Nummer';
                   info.conditions.isOvergrpId = *on;
                endif;
             endif;

             if info.conditions.isHovedgrp = *On;
                if %subst(chars : 1 : stringLen) = 'Nummer';
                   info.conditions.isHovedgrpId = *on;
                endif;
             endif;

             if info.conditions.isVaregrp = *On;
                if %subst(chars : 1 : stringLen) = 'Nummer';
                   info.conditions.isVaregrpId = *on;
                endif;
             endif;

             if info.conditions.isModul = *On;
                if %subst(chars : 1 : stringLen) = 'Nummer';
                   info.conditions.isModulNr = *on;
                endif;
             endif;

             if info.conditions.isVare = *On;
                if %subst(chars : 1 : stringLen) = 'NOBBNummer';
                   info.conditions.isNobbNummer = *on;
                endif;
             endif;

             if info.conditions.isRabattBet = *On;
                 if %subst(chars : 1 : stringLen) = 'Navn';
                   info.conditions.isBetNavn = *On;
                 endif;

                 if %subst(chars : 1 : stringLen) = 'Aktiv';
                    info.conditions.isBetAktiv = *On;
                 endif;
             endif;

             if info.conditions.isRabattElem = *On;
               if %subst(chars : 1 : stringLen) = 'Type';
                 info.conditions.isRabElType = *on;
               endif;
               if %subst(chars : 1 : stringLen) = 'Aktiv';
                 info.conditions.isRabElAktiv = *on;
               endif;
               if %subst(chars : 1 : stringLen) = 'Navn';
                 info.conditions.isRabElNvn = *on;
               endif;
               if %subst(chars : 1 : stringLen) = 'Regel';
                 info.conditions.isRabElRegel = *on;
               endif;
               if %subst(chars : 1 : stringLen) = 'Verdi';
                 info.conditions.isRabElVerdi = *on;
               endif;
               if %subst(chars : 1 : stringLen) = 'FraDato';
                 info.conditions.isRabElFraD = *on;
               endif;
               if %subst(chars : 1 : stringLen) = 'TilDato';
                 info.conditions.isRabElTilD = *on;
               endif;
             endif;

             if %subst(chars : 1 : stringLen) = 'Avtale';
                info.conditions.isAvtale = *On;
                info.accumulators.antAvtaler += 1;
                levIndex = 0;
                CLEAR levArray;
             endif;

             if %subst(chars : 1 : stringLen) = 'Leverandor';
                info.conditions.isLeverandor = *On;
                info.accumulators.antLeverand +=1;
                ogrpIndex = 0;
                CLEAR ogrpArray;
                levBetIndex = 0;
                CLEAR levBetArray;
             endif;

             if %subst(chars : 1 : stringLen) = 'Overgruppe';
                info.conditions.isOvergrp = *On;
                hgrpIndex = 0;
                CLEAR hgrpArray;
                ogrBetIndex = 0;
                CLEAR ogrBetArray;
             endif;

             if %subst(chars : 1 : stringLen) = 'Hovedgruppe';
                info.conditions.isHovedgrp = *On;
                vgrpIndex = 0;
                CLEAR vgrpArray;
                hgrBetIndex = 0;
                CLEAR hgrBetArray;
             endif;

             if %subst(chars : 1 : stringLen) = 'Varegruppe';
                info.conditions.isVaregrp = *On;
                modulIndex = 0;
                CLEAR modulArray;
                vgrBetIndex = 0;
                CLEAR vgrBetArray;
             endif;

             if %subst(chars : 1 : stringLen) = 'Modul';
                info.conditions.isModul = *On;
                vareIndex = 0;
                CLEAR vareArray;
                modBetIndex = 0;
                CLEAR modBetArray;
             endif;

             if %subst(chars : 1 : stringLen) = 'Vare';
                info.conditions.isVare = *On;
                varBetIndex = 0;
                CLEAR varBetArray;
             endif;

             if %subst(chars : 1 : stringLen) = 'RabattBetingelse';
                info.conditions.isRabattBet = *On;
                info.accumulators.antRabBet +=1;
                info.accumulators.sekvensNr = HentNesteSekvensNr();
                rabElemIndex = 0;
                CLEAR rabElemArray;
             endif;

             if %subst(chars : 1 : stringLen) = 'RabattElement';
                info.conditions.isRabattElem = *On;
                info.accumulators.antRabElem +=1;
             endif;

          when event = *XML_END_ELEMENT;
             if %subst(chars : 1 : stringLen) = 'Avtale';
                info.conditions.isAvtale = *Off;
                if levIndex > 0;
                  for forIndex = 1 to levIndex;
                    avtIndex +=1;
                    avtArray(avtIndex) = levArray(forIndex);
                    avtArray(avtIndex).avtNr =
                      info.discounts.avtNr;
                    avtArray(avtIndex).mottakernr = info.discounts.mottakernr;
                    avtArray(avtIndex).eiernr = info.discounts.eiernr;
                    avtArray(avtIndex).avtaletekst = info.discounts.avtaletekst;
                    avtArray(avtIndex).opprettetdato =
                      info.discounts.opprettetdato;
                    avtArray(avtIndex).sendedato =
                      info.discounts.sendedato;
                    avtArray(avtIndex).godkjentdato =
                      info.discounts.godkjentdato;
                  endfor;
                else;
                  avtIndex +=1;
                  avtArray(avtIndex).avtNr = info.discounts.avtNr;
                  avtArray(avtIndex).mottakernr = info.discounts.mottakernr;
                  avtArray(avtIndex).eiernr = info.discounts.eiernr;
                  avtArray(avtIndex).avtaletekst = info.discounts.avtaletekst;
                    avtArray(avtIndex).opprettetdato =
                      info.discounts.opprettetdato;
                    avtArray(avtIndex).sendedato =
                      info.discounts.sendedato;
                    avtArray(avtIndex).godkjentdato =
                      info.discounts.godkjentdato;
                  avtArray(avtIndex).lineno = info.discounts.lineno;
                endif;
             endif;

             if %subst(chars : 1 : stringLen) = 'Leverandor';
                info.conditions.isLeverandor = *Off;
                temp = levIndex;

                if levBetIndex > 0;
                  for forIndex = 1 to levBetIndex;
                    levIndex +=1;
                    levArray(levIndex) = levBetArray(forIndex);
                    levArray(levIndex).deltagernr = info.discounts.deltagernr;
                  endfor;
                endif;

                if ogrpIndex > 0;
                  for forIndex = 1 to ogrpIndex;
                    levIndex +=1;
                    levArray(levIndex) = ogrpArray(forIndex);
                    levArray(levIndex).deltagernr =
                      info.discounts.deltagernr;
                  endfor;
                endif;

                if temp = levIndex;
                  levIndex +=1;
                  levArray(levIndex).deltagernr =
                  info.discounts.deltagernr;
                endif;
             endif;

             if %subst(chars : 1 : stringLen) = 'Overgruppe';
                info.conditions.isOvergrp = *Off;
                temp = ogrpIndex;

                if ogrBetIndex > 0;
                  for forIndex = 1 to ogrBetIndex;
                    ogrpIndex +=1;
                    ogrpArray(ogrpIndex) = ogrBetArray(forIndex);
                    ogrpArray(ogrpIndex).overgruppe =
                      info.discounts.overgruppe;
                  endfor;
                endif;

                if hgrpIndex > 0;
                  for forIndex = 1 to hgrpIndex;
                    ogrpIndex +=1;
                    ogrpArray(ogrpIndex) = hgrpArray(forIndex);
                    ogrpArray(ogrpIndex).overgruppe =
                      info.discounts.overgruppe;
                  endfor;
                endif;

                if temp = ogrpIndex;
                  ogrpIndex +=1;
                  ogrpArray(ogrpIndex).overgruppe =
                      info.discounts.overgruppe;
                endif;

             endif;

             if %subst(chars : 1 : stringLen) = 'Hovedgruppe';
                info.conditions.isHovedgrp = *Off;
                temp = hgrpIndex;

                if hgrBetIndex > 0;
                  for forIndex = 1 to hgrBetIndex;
                    hgrpIndex +=1;
                    hgrpArray(hgrpIndex) = hgrBetArray(forIndex);
                    hgrpArray(hgrpIndex).hovedgruppe =
                      info.discounts.hovedgruppe;
                  endfor;
                endif;

                if vgrpIndex > 0;
                  for forIndex = 1 to vgrpIndex;
                    hgrpIndex +=1;
                    hgrpArray(hgrpIndex) = vgrpArray(forIndex);
                    hgrpArray(hgrpIndex).hovedgruppe =
                      info.discounts.hovedgruppe;
                  endfor;
                endif;

                if temp = hgrpIndex;
                  hgrpIndex +=1;
                  hgrpArray(hgrpIndex).hovedgruppe =
                      info.discounts.hovedgruppe;
                endif;
             endif;

             if %subst(chars : 1 : stringLen) = 'Varegruppe';
                info.conditions.isVaregrp = *Off;

                temp = vgrpIndex;

                if vgrBetIndex > 0;
                  for forIndex = 1 to vgrBetIndex;
                    vgrpIndex +=1;
                    vgrpArray(vgrpIndex) = vgrBetArray(forIndex);
                    vgrpArray(vgrpIndex).varegruppe =
                      info.discounts.varegruppe;
                  endfor;
                endif;

                if modulIndex > 0;
                  for forIndex = 1 to modulIndex;
                    vgrpIndex +=1;
                    vgrpArray(vgrpIndex) = modulArray(forIndex);
                    vgrpArray(vgrpIndex).varegruppe = info.discounts.varegruppe;
                  endfor;
                endif;

                if temp = vgrpIndex;
                  vgrpIndex +=1;
                  vgrpArray(vgrpIndex).varegruppe = info.discounts.varegruppe;
                endif;
             endif;

             if %subst(chars : 1 : stringLen) = 'Modul';
                info.conditions.isModul = *Off;

                temp = modulIndex;

                if modBetIndex > 0;
                  for forIndex = 1 to modBetIndex;
                    modulIndex +=1;
                    modulArray(modulIndex) = modBetArray(forIndex);
                    modulArray(modulIndex).modul =
                      info.discounts.modul;
                  endfor;
                endif;

                if vareIndex > 0;
                  for forIndex = 1 to vareIndex;
                    modulIndex +=1;
                    modulArray(modulIndex) = vareArray(forindex);
                    modulArray(modulIndex).modul = info.discounts.modul;
                  endfor;
                endif;

                if temp = modulIndex;
                  modulIndex +=1;
                  modulArray(modulIndex).modul = info.discounts.modul;
                endif;
             endif;

             if %subst(chars : 1 : stringLen) = 'Vare';
                info.conditions.isVare = *Off;

                temp = vareIndex;

                if varBetIndex > 0;
                  for forIndex = 1 to varBetIndex;
                    vareIndex +=1;
                    vareArray(vareIndex) = varBetArray(forIndex);
                    vareArray(vareIndex).nobbnummer =
                      info.discounts.nobbnummer;
                  endfor;
                endif;

                if temp = vareIndex;
                  vareIndex +=1;
                  vareArray(vareIndex).nobbnummer = info.discounts.nobbnummer;
                endif;
             endif;


             if %subst(chars : 1 : stringLen) = 'RabattBetingelse';
                info.conditions.isRabattBet = *Off;
                if info.conditions.isVare = *On;
                  if rabElemIndex > 0;
                    for forIndex = 1 to rabElemIndex;
                     varBetIndex +=1;
                     varBetArray(varBetIndex).rabbetakt =
                        info.discounts.rabbetakt;
                     varBetArray(varBetIndex).rabbetnavn =
                        info.discounts.rabbetnavn;
                     varBetArray(varBetIndex).rabeltype =
                        rabElemArray(forIndex).rabeltype;
                     varBetArray(varBetIndex).rabelakt =
                        rabElemArray(forIndex).rabelakt;
                     varBetArray(varBetIndex).rabelnavn =
                        rabElemArray(forIndex).rabelnavn;
                     varBetArray(varBetIndex).rabelregel =
                        rabElemArray(forIndex).rabelregel;
                     varBetArray(varBetIndex).rabelverdi =
                        rabElemArray(forIndex).rabelverdi;
                     varBetArray(varBetIndex).rabelfdat =
                        rabElemArray(forIndex).rabelfdat;
                     varBetArray(varBetIndex).rabeltdat =
                        rabElemArray(forIndex).rabeltdat;
                     varBetArray(varBetIndex).lineno =
                        info.accumulators.sekvensNr;
                    endfor;
                  endif;
                elseif info.conditions.isModul = *On;
                  if rabElemIndex > 0;
                    for forIndex = 1 to rabElemIndex;
                     modBetIndex +=1;
                     modBetArray(modBetIndex).rabbetakt =
                        info.discounts.rabbetakt;
                     modBetArray(modBetIndex).rabbetnavn =
                        info.discounts.rabbetnavn;
                     modBetArray(modBetIndex).rabeltype =
                        rabElemArray(forIndex).rabeltype;
                     modBetArray(modBetIndex).rabelakt =
                        rabElemArray(forIndex).rabelakt;
                     modBetArray(modBetIndex).rabelnavn =
                        rabElemArray(forIndex).rabelnavn;
                     modBetArray(modBetIndex).rabelregel =
                        rabElemArray(forIndex).rabelregel;
                     modBetArray(modBetIndex).rabelverdi =
                        rabElemArray(forIndex).rabelverdi;
                     modBetArray(modBetIndex).rabelfdat =
                        rabElemArray(forIndex).rabelfdat;
                     modBetArray(modBetIndex).rabeltdat =
                        rabElemArray(forIndex).rabeltdat;
                     modBetArray(modBetIndex).lineno =
                        info.accumulators.sekvensNr;
                    endfor;
                  endif;
                elseif info.conditions.isVaregrp = *On;
                  if rabElemIndex > 0;
                    for forIndex = 1 to rabElemIndex;
                     vgrBetIndex +=1;
                     vgrBetArray(vgrBetIndex).rabbetakt =
                        info.discounts.rabbetakt;
                     vgrBetArray(vgrBetIndex).rabbetnavn =
                        info.discounts.rabbetnavn;
                     vgrBetArray(vgrBetIndex).rabeltype =
                        rabElemArray(forIndex).rabeltype;
                     vgrBetArray(vgrBetIndex).rabelakt =
                        rabElemArray(forIndex).rabelakt;
                     vgrBetArray(vgrBetIndex).rabelnavn =
                        rabElemArray(forIndex).rabelnavn;
                     vgrBetArray(vgrBetIndex).rabelregel =
                        rabElemArray(forIndex).rabelregel;
                     vgrBetArray(vgrBetIndex).rabelverdi =
                        rabElemArray(forIndex).rabelverdi;
                     vgrBetArray(vgrBetIndex).rabelfdat =
                        rabElemArray(forIndex).rabelfdat;
                     vgrBetArray(vgrBetIndex).rabeltdat =
                        rabElemArray(forIndex).rabeltdat;
                     vgrBetArray(vgrBetIndex).lineno =
                        info.accumulators.sekvensNr;
                    endfor;
                  endif;
                elseif info.conditions.isHovedgrp = *On;
                  if rabElemIndex > 0;
                    for forIndex = 1 to rabElemIndex;
                     hgrBetIndex +=1;
                     hgrBetArray(hgrBetIndex).rabbetakt =
                        info.discounts.rabbetakt;
                     hgrBetArray(hgrBetIndex).rabbetnavn =
                        info.discounts.rabbetnavn;
                     hgrBetArray(hgrBetIndex).rabeltype =
                        rabElemArray(forIndex).rabeltype;
                     hgrBetArray(hgrBetIndex).rabelakt =
                        rabElemArray(forIndex).rabelakt;
                     hgrBetArray(hgrBetIndex).rabelnavn =
                        rabElemArray(forIndex).rabelnavn;
                     hgrBetArray(hgrBetIndex).rabelregel =
                        rabElemArray(forIndex).rabelregel;
                     hgrBetArray(hgrBetIndex).rabelverdi =
                        rabElemArray(forIndex).rabelverdi;
                     hgrBetArray(hgrBetIndex).rabelfdat =
                        rabElemArray(forIndex).rabelfdat;
                     hgrBetArray(hgrBetIndex).rabeltdat =
                        rabElemArray(forIndex).rabeltdat;
                     hgrBetArray(hgrBetIndex).lineno =
                        info.accumulators.sekvensNr;
                    endfor;
                  endif;
                elseif info.conditions.isOvergrp = *On;
                  if rabElemIndex > 0;
                    for forIndex = 1 to rabElemIndex;
                     ogrBetIndex +=1;
                     ogrBetArray(ogrBetIndex).rabbetakt =
                        info.discounts.rabbetakt;
                     ogrBetArray(ogrBetIndex).rabbetnavn =
                        info.discounts.rabbetnavn;
                     ogrBetArray(ogrBetIndex).rabeltype =
                        rabElemArray(forIndex).rabeltype;
                     ogrBetArray(ogrBetIndex).rabelakt =
                        rabElemArray(forIndex).rabelakt;
                     ogrBetArray(ogrBetIndex).rabelnavn =
                        rabElemArray(forIndex).rabelnavn;
                     ogrBetArray(ogrBetIndex).rabelregel =
                        rabElemArray(forIndex).rabelregel;
                     ogrBetArray(ogrBetIndex).rabelverdi =
                        rabElemArray(forIndex).rabelverdi;
                     ogrBetArray(ogrBetIndex).rabelfdat =
                        rabElemArray(forIndex).rabelfdat;
                     ogrBetArray(ogrBetIndex).rabeltdat =
                        rabElemArray(forIndex).rabeltdat;
                     ogrBetArray(ogrBetIndex).lineno =
                        info.accumulators.sekvensNr;
                    endfor;
                  endif;
                elseif info.conditions.isLeverandor = *On;
                  if rabElemIndex > 0;
                    for forIndex = 1 to rabElemIndex;
                     levBetIndex +=1;
                     levBetArray(levBetIndex).rabbetakt =
                        info.discounts.rabbetakt;
                     levBetArray(levBetIndex).rabbetnavn =
                        info.discounts.rabbetnavn;
                     levBetArray(levBetIndex).rabeltype =
                        rabElemArray(forIndex).rabeltype;
                     levBetArray(levBetIndex).rabelakt =
                        rabElemArray(forIndex).rabelakt;
                     levBetArray(levBetIndex).rabelnavn =
                        rabElemArray(forIndex).rabelnavn;
                     levBetArray(levBetIndex).rabelregel =
                        rabElemArray(forIndex).rabelregel;
                     levBetArray(levBetIndex).rabelverdi =
                        rabElemArray(forIndex).rabelverdi;
                     levBetArray(levBetIndex).rabelfdat =
                        rabElemArray(forIndex).rabelfdat;
                     levBetArray(levBetIndex).rabeltdat =
                        rabElemArray(forIndex).rabeltdat;
                     levBetArray(levBetIndex).lineno =
                        info.accumulators.sekvensNr;
                    endfor;
                  endif;
                endif;
             endif;

             if %subst(chars : 1 : stringLen) = 'RabattElement';
                info.conditions.isRabattElem = *Off;
                rabElemIndex +=1;
                rabElemArray(rabElemIndex).rabeltype =
                  info.discounts.rabeltype;
                rabElemArray(rabElemIndex).rabelakt =
                  info.discounts.rabelakt;
                rabElemArray(rabElemIndex).rabelnavn =
                  info.discounts.rabelnavn;
                rabElemArray(rabElemIndex).rabelregel=
                  info.discounts.rabelregel;
                rabElemArray(rabElemIndex).rabelverdi=
                  info.discounts.rabelverdi;
                rabElemArray(rabElemIndex).rabelfdat=
                  info.discounts.rabelfdat;
                rabElemArray(rabElemIndex).rabeltdat=
                  info.discounts.rabeltdat;
                rabElemArray(rabElemIndex).lineno =
                  info.accumulators.sekvensNr;
             endif;

             if info.conditions.isAvtale = *On;
                if %subst(chars : 1 : stringLen) = 'AvtaleNr';
                   info.conditions.isAvtaleNr = *Off;
                endif;
                if %subst(chars : 1 : stringLen) = 'Mottaker';
                   info.conditions.isMottaker = *Off;
                endif;
                if %subst(chars : 1 : stringLen) = 'Eier';
                   info.conditions.isEier = *Off;
                endif;
                if %subst(chars : 1 : stringLen) = 'AvtaleTekst';
                   info.conditions.isAvtaleTekst = *Off;
                endif;
                if info.conditions.isAvtaleDato  = *On;
                  if %subst(chars : 1 : stringLen) = 'Dato';
                     info.conditions.isAvtaleDatoDato = *On;
                  endif;
                  if %subst(chars : 1 : stringLen) = 'DatoType';
                     info.conditions.isAvtaleDatoType = *On;
                  endif;
                endif;
             endif;

             if info.conditions.isLeverandor = *On;
                if %subst(chars : 1 : stringLen) = 'Deltagernummer';
                   info.conditions.isDeltagerNr = *Off;
                endif;
             endif;

             if info.conditions.isRabattBet = *On;
                if %subst(chars : 1 : stringLen) = 'Navn';
                   info.conditions.isBetNavn = *Off;
                endif;
                if %subst(chars : 1 : stringLen) = 'Aktiv';
                   info.conditions.isBetAktiv = *Off;
                endif;
             endif;

             if info.conditions.isRabattElem = *On;
                if %subst(chars : 1 : stringLen) = 'Type';
                   info.conditions.isRabElType = *off;
                endif;
                if %subst(chars : 1 : stringLen) = 'Aktiv';
                   info.conditions.isRabElAktiv = *off;
                endif;
                if %subst(chars : 1 : stringLen) = 'Navn';
                   info.conditions.isRabElNvn = *off;
                endif;
                if %subst(chars : 1 : stringLen) = 'Regel';
                   info.conditions.isRabElRegel = *off;
                endif;
                if %subst(chars : 1 : stringLen) = 'Verdi';
                   info.conditions.isRabElVerdi = *off;
                endif;
                if %subst(chars : 1 : stringLen) = 'FraDato';
                   info.conditions.isRabElFraD = *off;
                endif;
                if %subst(chars : 1 : stringLen) = 'TilDato';
                   info.conditions.isRabElTilD = *off;
                endif;
             endif;

             if info.conditions.isOvergrp = *On;
                if %subst(chars : 1 : stringLen) = 'Nummer';
                   info.conditions.isOvergrpId = *off;
                endif;
             endif;

             if info.conditions.isHovedgrp = *On;
                if %subst(chars : 1 : stringLen) = 'Nummer';
                   info.conditions.isHovedgrpId = *off;
                endif;
             endif;

             if info.conditions.isVaregrp = *On;
                if %subst(chars : 1 : stringLen) = 'Nummer';
                   info.conditions.isVaregrpId = *off;
                endif;
             endif;

             if info.conditions.isModul = *On;
                if %subst(chars : 1 : stringLen) = 'Nummer';
                   info.conditions.isModulNr = *off;
                endif;
             endif;

             if info.conditions.isVare = *On;
                if %subst(chars : 1 : stringLen) = 'NOBBNummer';
                   info.conditions.isNobbNummer = *off;
                endif;
             endif;
          when event = *XML_CHARS;
             if info.conditions.isAvtale = *On;
                if info.conditions.isAvtaleNr = *On;
                   info.discounts.avtNr = SubString(chars:6:'<');
                endif;
                if info.conditions.isMottaker = *On;
                   info.discounts.mottakerNr = SubString(chars:6:'<');
                endif;
                if info.conditions.isEier = *On;
                   info.discounts.eierNr = SubString(chars:6:'<');
                endif;
                if info.conditions.isAvtaleTekst = *On;
                   info.discounts.avtaletekst = SubString(chars:50:'<');
                endif;
                if info.conditions.isAvtaleDato  = *On;
                  if info.conditions.isAvtaleDatoDato = *On;
                     info.discounts.avtaledato = SubString(chars:25:'<');
                  endif;
                  if info.conditions.isAvtaleDatoType = *On;
                     if SubString(chars:13:'<') = 'OpprettetDato';
                       info.discounts.opprettetdato = info.discounts.avtaledato;
                     elseif SubString(chars:13:'<') = 'SendeDato';
                       info.discounts.sendedato = info.discounts.sendedato;
                     elseif SubString(chars:13:'<') = 'GodkjentDato';
                       info.discounts.godkjentdato =
                         info.discounts.godkjentdato;
                     endif;
                  endif;
                endif;
             endif;

             if info.conditions.isLeverandor = *On;
                if info.conditions.isDeltagerNr = *On;
                   info.discounts.deltagerNr = SubString(chars:6:'<');
                endif;
             endif;

             if info.conditions.isOvergrp = *On;
                if info.conditions.isOvergrpId = *On;
                   info.discounts.overgruppe = SubString(chars:2:'<');
                endif;
             endif;

             if info.conditions.isHovedgrp = *On;
                if info.conditions.isHovedgrpId = *On;
                   info.discounts.hovedgruppe = SubString(chars:4:'<');
                endif;
             endif;

             if info.conditions.isVaregrp = *On;
                if info.conditions.isVaregrpId = *On;
                  info.discounts.varegruppe = SubString(chars:7:'<');
                endif;
             endif;

             if info.conditions.isModul = *On;
                if info.conditions.isModulNr = *On;
                  info.discounts.modul = SubString(chars:8:'<');
                endif;
             endif;

             if info.conditions.isVare = *On;
                if info.conditions.isNobbNummer = *On;
                   info.discounts.nobbnummer = SubString(chars:8:'<');
                endif;
             endif;

             if info.conditions.isRabattBet = *On;
                if info.conditions.isRabattElem = *Off;
                  if info.conditions.isBetNavn = *On;
                     info.discounts.rabbetnavn = SubString(chars:35:'<');
                     info.conditions.isBetNavn = *Off;
                     //dsply ('Rabattbet ' + info.discounts.rabbetnavn);
                  endif;

                  if info.conditions.isBetAktiv = *On;
                     info.discounts.rabbetakt = SubString(chars:5:'<');
                     //dsply ('Rabattbet akt ' + info.discounts.rabbetakt);
                  endif;
                endif;
             endif;

             if info.conditions.isRabattElem = *On;
               if info.conditions.isRabElType = *On;   // Pristype
                 info.discounts.rabeltype = SubString(chars:12:'<');
               endif;
               if info.conditions.isRabElAktiv = *On;   // Aktiv
                 info.discounts.rabelakt = SubString(chars:5:'<');
               endif;
               if info.conditions.isRabElNvn = *On; // Navn
                 info.discounts.rabelnavn = SubString(chars:35:'<');
                 info.conditions.isRabElNvn = *Off;
               endif;
               if info.conditions.isRabElRegel = *On;  //  Regel
                 info.discounts.rabelregel = SubString(chars:3:'<');
               endif;
               if info.conditions.isRabElVerdi = *On;  // Verdi
                 info.discounts.rabelverdi = SubString(chars:8:'<');
               endif;
               if info.conditions.isRabElFraD = *On;    // FraDato
                 info.discounts.rabelfdat = SubString(chars:10:'<');
9.22              info.discounts.rabeltdat = '          ';
               endif;
               if info.conditions.isRabElTilD = *On;   // TilDato
                  info.discounts.rabeltdat = SubString(chars:10:'<');
               endif;
             endif;
          // handle an exception
          when event = *XML_EXCEPTION;
             //dsply ('Exception ' + (%char(exceptionId))  + ' occurred.');
             //dsply ('Value ' + (%subst(chars:1:45)));
          return exceptionId;
          other;
          endsl;


          return 0;
       end-proc;

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Prosedyre som sletter leverandør fra jbeiPF og jraiPF
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
       dcl-proc SlettBetingelserForLev;
         dcl-pi *N char(800);
            discount likeds(discount_t);
         end-pi;

         dcl-s leverandorNr packed(6:0);
9.24     dcl-s w_ldorx char(6);
         dcl-s lookupRes packed(6:0);

         leverandorNr  = 0;
         if not (discount.deltagerNr = *blanks);
           leverandorNr = %dec(%trim(discount.deltagerNr):6:0);
         endif;
         if leverandorNr > 0;
9.25       evalr w_ldorx = %char(leverandorNr);
9.24       lookupRes = %lookup(w_ldorx:leverandorArray);
9.24   //    lookupRes = %lookup(%char(leverandorNr):leverandorArray);
           // if not found in array add it
           if lookupRes = 0;
             // sletter gamle leverandør forekomster på jbeiPF og jraiPF
             exec sql delete from jraipf where jpefir = 0
               and jpeldo = :leverandorNr;
             if sqlcod <> 0;
               if sqlcod <> 100;
                 currentSqlState = 'SqlCode ' + %char(sqlcode)
                   + ' SqlState ' + sqlstate;
                 exec sql Get Diagnostics Condition 1
                    :messageText = MESSAGE_TEXT,
                    :messageId = DB2_MESSAGE_ID,
                    :messageId1 = DB2_MESSAGE_ID1,
                    :messageId2 = DB2_MESSAGE_ID2;
                 return ('SlettBetingelserForLev(1) ' + %trim(currentSqlState) +
                   ' Msg: ' +
                   messageText  + ' MsgId: ' + messageId + ' MsgId2: ' +
                   messageId2);
               endif;
             endif;

             exec sql delete from jbeipf where jpfirm = 0
               and jpldor = :leverandorNr;

             if sqlcod <> 0;
               if sqlcod <> 100;
                 currentSqlState = 'SqlCode ' + %char(sqlcode)
                   + ' SqlState ' + sqlstate;
                 exec sql Get Diagnostics Condition 1
                    :messageText = MESSAGE_TEXT,
                    :messageId = DB2_MESSAGE_ID,
                    :messageId1 = DB2_MESSAGE_ID1,
                    :messageId2 = DB2_MESSAGE_ID2;
                 return ('SlettBetingelserForLev(2) ' + %trim(currentSqlState) +
                   ' Msg: ' +
                   messageText  + ' MsgId: ' + messageId + ' MsgId2: ' +
                   messageId2);
               endif;
             endif;

             antallLev +=1;
             // høyrejusterer leverandørnummeret
9.24   //      evalr leverandorArray(antallLev) = %char(leverandorNr);
9.25         evalr leverandorArray(antallLev) =  %char(leverandorNr);
           endif;
         endif;

         return 'success';
       end-proc;

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Prosedyre som skriver betingelser til jbeiPF
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
       dcl-proc SkrivRabattBetingelse;
         dcl-pi *N char(800);
           discount likeds(discount_t);
         end-pi;

         dcl-s replaceString char(35);
9.01     dcl-s w_tims timestamp;

         reset jbeipf;

         if discount.avtNr = *blanks;
           return 'Avtalenr er ikke utfylt';
         else;
           jpavnr = %dec(%trim(discount.avtNr):6:0);
         endif;

         jpfirm = 0;

         if discount.deltagerNr = *blanks;
           jpldor = 0;
         else;
           jpldor = %dec(%trim(discount.deltagerNr):6:0);
         endif;

         if discount.overgruppe = *blanks;
           jpogrp = 0;
         else;
           jpogrp = %dec(%trim(discount.overgruppe):2:0);
         endif;

         if discount.hovedgruppe = *blanks;
           jphgrp = 0;
         else;
           jphgrp = %dec(%subst(discount.hovedgruppe:3:2):2:0);
         endif;

         if discount.varegruppe = *blanks;
           jpugrp = 0;
         else;
           jpugrp = %dec(%subst(discount.varegruppe:5:3):3:0);
         endif;

         if discount.modul = *blanks;
           jpmodn = 0;
         else;
           jpmodn = %dec(%trim(discount.modul):8:0);
         endif;

         if discount.avtaledato = *blanks;
           jpdato = '0001-01-01';
         else;
           jpdato = SubString(discount.avtaledato:10:'T');
         endif;

         jpvare = %trim(discount.nobbnummer);

         jpline = discount.lineno;

         replaceString = Replace(%trim(discount.rabbetnavn):'&amp;':'&');
         replaceString = Replace(%trim(replaceString):'&lt;':'<');
         replaceString = Replace(%trim(replaceString):'&gt;':'>');
         jpbeti = replaceString;

         if discount.rabbetakt = 'true' ;
             jpakti = 1;
         else;
             jpakti = 0;
         endif;

         if ldaUserId = *blanks;
           jpeusr = '';
         else;
           jpeusr = ldaUserId;
         endif;

         jpodat = %char(%date);
         jpedat = %char(%date);
         jpetim = %char(%time);

         exec sql insert into jbeipf
           (jpfirm,
            jpldor,
            jpprgr,
            jpogrp,
            jphgrp,
            jpugrp,
            jpmodn,
            jpvare,
            jpvtyp,
            jplety,
            jprsts,
            jpline,
            jpbeti,
            jpfdat,
            jptdat,
            jpdato,
            jpakti,
            jpavnr,
            jpodat,
            jpedat,
            jpetim,
            jpeusr) values (
            :jpfirm,
            :jpldor,
            :jpprgr,
            :jpogrp,
            :jphgrp,
            :jpugrp,
            :jpmodn,
            :jpvare,
            :jpvtyp,
            :jplety,
            :jprsts,
            :jpline,
            :jpbeti,
            :jpfdat,
            :jptdat,
            :jpdato,
            :jpakti,
            :jpavnr,
            :jpodat,
            :jpedat,
            :jpetim,
            :jpeusr);

         if sqlcode <> 0;
           currentSqlState = 'SqlCode ' + %char(sqlcode)
             + ' SqlState ' + sqlstate;
           exec sql Get Diagnostics Condition 1
              :messageText = MESSAGE_TEXT,
              :messageId = DB2_MESSAGE_ID,
              :messageId1 = DB2_MESSAGE_ID1,
              :messageId2 = DB2_MESSAGE_ID2;
           return ('SkrivRabattBetingelse: ' + %trim(currentSqlState)
             + ' Msg: ' + messageText
             + ' MsgId: ' + messageId + ' MsgId2: ' + messageId2);
         endif;
9.01   //  if   b_First = *off;
9.01   //       w_tims = %timestamp();
9.01   //       exec sql update jlstpf
9.01   //         set jlsint = :w_tims
9.01   //         where jlsldo = :jpldor;
9.01   //       b_first = *on;
9.01   //  endif;

         return 'success';
       end-proc;

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Prosedyre som skriver betingelser til jraiPF
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
       dcl-proc SkrivRabattElement;
         dcl-pi *N char(800);
           discount likeds(discount_t);
           elementLine packed(10);
         end-pi;

         dcl-s replaceString char(35);

         if discount.avtNr = *blanks;
           return 'Avtalenr er ikke utfylt';
         endif;

         reset jraipf;

         jpefir = 0;

         if discount.deltagerNr = *blanks;
           jpeldo = 0;
         else;
           jpeldo = %dec(%trim(discount.deltagerNr):6:0);
         endif;

         if discount.overgruppe = *blanks;
           jpeogr = 0;
         else;
           jpeogr = %dec(%trim(discount.overgruppe):2:0);
         endif;

         if discount.hovedgruppe = *blanks;
           jpehgr = 0;
         else;
           jpehgr = %dec(%subst(discount.hovedgruppe:3:2):2:0);
         endif;

         if discount.varegruppe = *blanks;
           jpeugr = 0;
         else;
           jpeugr = %dec(%subst(discount.varegruppe:5:3):3:0);
         endif;

         if discount.modul = *blanks;
           jpemod = 0;
         else;
           jpemod = %dec(%trim(discount.modul):8:0);
         endif;

         jpevar = %trim(discount.nobbnummer);

         jpebli = discount.lineno;


         replaceString  = Replace(%trim(discount.rabelnavn):'&amp;':'&');
         replaceString  = Replace(%trim(replaceString):'&lt;':'<');
         replaceString  = Replace(%trim(replaceString):'&gt;':'>');
         jperae = replaceString;

         jpeLin = elementLine;

         if discount.rabelakt = 'true' ;
             jpeakt = 1;
         else;
             jpeakt = 0;
         endif;

         jpereg = '';
         if %trim(discount.rabelregel) = '%';
           jpereg = '1';
         endif;
         if %trim(discount.rabelregel) = '(%)';
           jpereg = '2';
         endif;
         if %trim(discount.rabelregel) = 'NT';
           jpereg = 'N';
         endif;
         if %trim(discount.rabelregel) = 'KR';
           jpereg = 'K';
         endif;
         if jpereg = *blanks;
           callRes =  'Rabattregel ' + discount.rabelregel
             + ' er ikke supportert (settes til blank i jraiPF)';
           ErrorLogging(callRes);
         endif;

         if discount.rabelverdi = *blanks;
           jpever = 0;
         else;
           jpever = %dec(%trim(discount.rabelverdi):9:2);
           // snu fortegn slik at alle rabattverdiene blir positive
           // skal ikke snu fortegn dersom nettopris (jpereg = N) (er positivt)
           if not (jpereg = 'N') and not (jpereg = 'K');
             jpever = jpever * -1;
           endif;
         endif;

         if discount.rabelfdat = *blanks;
           jpefda = '0001-01-01';
         else;
           jpefda = %trim(discount.rabelfdat);
         endif;

         if discount.rabeltdat = *blanks;
           jpetda = '0001-01-01';
         else;
           jpetda = %trim(discount.rabeltdat);
         endif;

         if ldaUserId = *blanks;
           jpeeus = '';
         else;
           jpeeus = ldaUserId;
         endif;

         jpeoda = %char(%date);
         jpeeda = %char(%date);
         jpeeti = %char(%time);

         exec sql insert into jraipf
           (jpefir,
            jpeldo,
            jpeprg,
            jpeogr,
            jpehgr,
            jpeugr,
            jpemod,
            jpevar,
            jpevty,
            jpelet,
            jpebrs,
            jpebli,
            jperst,
            jpelin,
            jperae,
            jpereg,
            jpever,
            jpefda,
            jpetda,
            jpeakt,
            jpeoda,
            jpeeda,
            jpeeti,
            jpeeus) values (
            :jpefir,
            :jpeldo,
            :jpeprg,
            :jpeogr,
            :jpehgr,
            :jpeugr,
            :jpemod,
            :jpevar,
            :jpevty,
            :jpelet,
            :jpebrs,
            :jpebli,
            :jperst,
            :jpelin,
            :jperae,
            :jpereg,
            :jpever,
            :jpefda,
            :jpetda,
            :jpeakt,
            :jpeoda,
            :jpeeda,
            :jpeeti,
            :jpeeus);

         if sqlcode <> 0;
           currentSqlState = 'SqlCode ' + %char(sqlcode)
             + ' SqlState ' + sqlstate;
           exec sql Get Diagnostics Condition 1
              :messageText = MESSAGE_TEXT,
              :messageId = DB2_MESSAGE_ID,
              :messageId1 = DB2_MESSAGE_ID1,
              :messageId2 = DB2_MESSAGE_ID2;
           return ('SkrivRabattElement: ' + %trim(currentSqlState)
             + ' Msg: ' + messageText
             + ' MsgId: ' + messageId + ' MsgId2: ' + messageId2);
         endif;
      *prøver å blanke feltet alltid
         discount.rabeltdat = '';

         return 'success';
       end-proc;

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * SubString: returner string til venstre for oppgitt delingsparameter
      * Maks lengde på returstring er 200 tegn.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
       dcl-proc SubString;
         dcl-pi *N char(200);
           chars  char(65535) const;
           maxlength packed(3) const;
           delimeter char(5) const;
         end-pi;
         dcl-s Result varchar(200);
         dcl-s position packed(3);
         result = %subst(chars : 1 : maxlength);
         position = %scan(%trim(delimeter) : result);
         if (position > 0);
           result = %subst(result : 1 : (position-1));
         endif;

         return %trim(result);
       end-proc;

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Replace: erstatter en gitt verdi i en string med en annen verdi
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
       dcl-proc Replace;
         dcl-pi *N char(35);
           string_data char(35) value;
           scan_data char(10) value;
           replace_data char(10) value;
         end-pi;
         dcl-s pos packed(10);

         pos = 1;
         dou pos <= 0;
           pos = %scan(%trim(scan_data):string_data:pos);
           if (pos = 0);
             leave;
           endif;
           string_data = %replace( %trim(replace_data)
                           : string_data
                           : pos
                           : %len(%trim(scan_data)));
           if (pos + %len(%trim(replace_data)) > %len(string_data));
             leave;
           endif;
         enddo;

         return string_data;
       end-proc;

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * HentNesteSekvensNr: Henter neste sekvensnr ved å kalle AS100R
      * Retunerer sekvensnummeret
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
        dcl-proc HentNesteSekvensNr;
          dcl-pi *N packed(8);

          end-pi;
          p_fell = 'BETING';
          p_kode = '0';
          p_type = *blank;
          p_fast = *blank;
          p_numm = 0;
          CallP P_AS100R(p_kode : p_firm : p_fell : p_type : p_fast : p_numm);

          return p_numm;
        end-proc;

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Start JobLogging
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
       dcl-proc StartLogging;
         p_jnav = 'JP742R';
         p_jtxt = 'Parsing av  NOBB XML kontrakter';
         CallP P_AB700R (p_jnav : p_jbid : p_jtxt);
       end-proc;

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Logg feil
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
       dcl-proc ErrorLogging;
         dcl-pi *N;
           message char(800) value;
         end-pi;
         dcl-s msgpart char(200) inz('');

         p_jsts = 50;
         if message = *blank;
           message = 'Alvorlig feil oppstod i JV740R';
         endif;
         if %len(%trim(message)) > 600;
           msgpart = %trim(%subst(message:1:200));
           CallP P_AB705R (p_jbid : p_jsts : msgpart);
           msgpart = %trim(%subst(message:201:200));
           CallP P_AB705R (p_jbid : p_jsts : msgpart);
           msgpart = %trim(%subst(message:401:200));
           CallP P_AB705R (p_jbid : p_jsts : msgpart);
           msgpart = %trim(%subst(message:601));
           CallP P_AB705R (p_jbid : p_jsts : msgpart);
         elseif %len(%trim(message)) > 400;
           msgpart = %trim(%subst(message:1:200));
           CallP P_AB705R (p_jbid : p_jsts : msgpart);
           msgpart = %trim(%subst(message:201:200));
           CallP P_AB705R (p_jbid : p_jsts : msgpart);
           msgpart = %trim(%subst(message:401));
           CallP P_AB705R (p_jbid : p_jsts : msgpart);
         elseif %len(%trim(message)) > 200;
           msgpart = %trim(%subst(message:1:200));
           CallP P_AB705R (p_jbid : p_jsts : msgpart);
           msgpart = %trim(%subst(message:201));
           CallP P_AB705R (p_jbid : p_jsts : msgpart);
         else;
           CallP P_AB705R (p_jbid : p_jsts : message);
         endif;
         //dsply (%trim(%subst(message:1:50)));

       end-proc;
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avsluttende JobLogging
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
       dcl-proc EndLogging;
       if (p_stat = '1');
         p_jsts = 50;
         if p_jmld = *blank;
           p_jmld = 'Alvorlig feil oppstod i JV740R';
         endif;
         //dsply (%trim(%subst(p_jmld:1:50)));
         CallP P_AB705R (p_jbid : p_jsts : p_jmld);
       else;
         p_jsts = 10;
         p_jmld = *blanks;
         p_jmld = %trim(a_meld(1)) + ' ' +
            %Char(xmlHandlerInfo.accumulators.antAvtaler);

         //dsply (%trim(%subst(p_jmld:1:50)));
         CallP P_AB705R (p_jbid : p_jsts : p_jmld);

         p_jsts = 10;
         p_jmld = *blanks;
         p_jmld = %trim(a_meld(2)) + ' ' +
            %Char(xmlHandlerInfo.accumulators.antLeverand);
         //dsply (%trim(%subst(p_jmld:1:50)));
         CallP P_AB705R (p_jbid : p_jsts : p_jmld);

         p_jsts = 10;
         p_jmld = *blanks;
         p_jmld = %trim(a_meld(3)) + ' ' +
            %Char(xmlHandlerInfo.accumulators.antRabBet);
         //dsply (%trim(%subst(p_jmld:1:50)));
         CallP P_AB705R (p_jbid : p_jsts : p_jmld);

         p_jsts = 10;
         p_jmld = *blanks;
         p_jmld = %trim(a_meld(4)) + ' ' +
            %Char(xmlHandlerInfo.accumulators.antRabElem);
         //dsply (%trim(%subst(p_jmld:1:50)));
         CallP P_AB705R (p_jbid : p_jsts : p_jmld);
       endif;

       CallP P_AB710R (p_jnav : p_jbid );
       end-proc;

      /end-free
      //
**
Antall avtaler:
Antall leverandører:
Antall rabattbetingelser:
Antall rabattelementer:
