     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO786R
      * Beskrivelse . . : Ordre-hode, henter antall ordre til oppfølging
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       150206 HKO  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffohel9    if   e           k disk    rename(fohepfr:fohel9r)
     ffusrl1    if   e           k disk    rename(fusrpfr:fusrl1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     fvinfl1    if   e           k disk    rename(vinfpfr:vinfl1r)
      *
      * Definering av parametre
      *
     d p_anta          s              6  0
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av variabler i nøkler
      *
     d fohel9_selg     s                   like(foselg)
     d fusrl1_user     s                   like(fbuser)
     d votyl1_otyp     s                   like(vaotyp)
     d vinfl1_ityp     s                   like(vaityp)
      *
      * Definering av variable
      *
     d w_firm          s                   like(fofirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   if        l_user = *blank
     c                   goto      avslutt
     c                   endif
      *
      * Initierer output-parametre
      *
     c                   eval      p_anta = 0
      *
      * Finner selgernr for bruker
      *
     c                   eval      fusrl1_user = l_user
     c     fusrl1_key    chain     fusrl1
     c                   if        not %found or
     c                             fbselg = 0
     c                   goto      avslutt
     c                   endif
      *
      * Leser ordre-hode for selger, og finner antall ordre hvor:
      * - leveringsdato er passert eller
      * - ordre informasjonskoden er valgt å følges opp
      *
     c                   eval      fohel9_selg = fbselg
     c     fohel9_key    setll     fohel9
     c     fohel9_key    reade     fohel9
     c                   dow       not %eof
      *
     c                   eval      votyl1_otyp = footyp
     c     votyl1_key    chain     votyl1r
     c                   if        %found and
     c                             vaoakk = 1
     c                   if        foldat <> *loval and
     c                             foldat <= %date
     c                   eval      p_anta = p_anta + 1
     c                   else
     c                   if        foityp <> *blank
     c                   eval      vinfl1_ityp = foityp
     c     vinfl1_key    chain     vinfl1
     c                   if        %found and
     c                             vaisel = '1'
     c                   eval      p_anta = p_anta + 1
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
     c     fohel9_key    reade     fohel9
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_anta
      *
      * Key for les på ordre-hode
      *
     c     fohel9_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel9_selg
      *
      * Key for oppslag på ofak bruker
      *
     c     fusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fusrl1_user
      *
      * Key for oppslag på ordre-type
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for oppslag på ordre informasjonskode
      *
     c     vinfl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vinfl1_ityp
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
