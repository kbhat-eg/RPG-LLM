     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FW112R
      * Beskrivelse . . : Lev.vare, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       060599 HKO  Nytt program
      *       250300 HKO  Endret definering av høyde, bredde, lengde, vekt
      *                   og volum
      *       301101 HKO  Lagt inn varegruppe
      *       040805 HKO  Endret lengde, bredde og høydeangivelse fra m til
      *                   mm
      * 6.10  230609 BSA  Oppdatert med sporingsinformasjon
8.01  *PRG2   100622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fflval1    if   e           k disk    rename(flvapfr:flval1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fvvarl5    if   e           k disk    rename(vvarpfr:vvarl5r)
     fflvalu    uf   e           k disk    rename(flvapfr:flvalur)
      *
     ffw112d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
6.10 d l_user                911    920
     d l_fnav                951    980
      *
      * Definering av parametre
      *
     d p_firm          s                   like(fwfirm)
     d p_ldor          s                   like(fwldor)
     d p_line          s                   like(fwline)
     d p_kvar          s              1  0
     d p_stat          s              1  0
     d p_valg          s              1  0
      *
      * Definering av variable i nøkler
      *
     d vvarl5_lvar     s                   like(vvlvar)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
     d w_mvap          s              6  2
     d w_mvat          s              5  4
     d w_mvaf          s             10  9
     d w_udat          s               d   datfmt(*iso)
     d w_lety          s              1  0
     d w_inlr          s              1
     d w_indi          s              2  0
     d w_etxt          s             35
      *
     d w_firm          s                   like(fwfirm)
     d w_ldor          s                   like(fwldor)
     d w_line          s                   like(fwline)
8.01 d w_prgr          s              2
      *
     d w_bupr          s                   like(fwsapr)
     d w_kopr          s                   like(fwinpr)
     d w_enhe_gml      s                   like(fwenhe)
     d w_inpr_gml      s                   like(fwinpr)
     d w_sapr_gml      s                   like(fwsapr)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter vare
      *
     c     flval1_key    chain     flval1                             90
     c                   if        *in90 = *on
     c                   goto      avslutt
     c                   endif
      *
      * Initierer skjermbildet
      *
     c                   exsr      init_bilde
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      c2tagb
     c                   endsl
      *
      * F13=Vise statuskoder
      *
     c                   if        *inkm = *on
     c                   call      'FW110R'
     c                   goto      c2tagb
     c                   endif
      *
      * Vise
      *
     c                   if        p_valg = 5                                            .....
     c                   goto      avslutt                                                   .
     c                   endif                                                           .....
      *
      * Validering av input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      c2tagb
     c                   endif
      *
      * Sender bildet opp på nytt dersom variable som påvirker disse er
      * endret
      *
     c                   if        c2enhe <> w_enhe_gml or
     c                             c2inpr <> w_inpr_gml or
     c                             c2sapr <> w_sapr_gml
      *
     c                   exsr      hent_kontr
      *
     c                   eval      w_enhe_gml = c2enhe
     c                   eval      w_inpr_gml = c2inpr
     c                   eval      w_sapr_gml = c2sapr
      *
     c                   goto      c2tagb
      *
     c                   endif
      *
      * Oppdater register
      *
     c                   exsr      oppdater
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Initier skjermbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     init_bilde    begsr
      *
     c                   eval      c2ldor = w_ldor                                           .
     c                   eval      c2stat = p_stat                                           .
     c                   eval      c2ekod = fwekod                                           .
     c                   eval      c2lvar = fwlvar                                           .
     c                   eval      c2tek1 = fwtek1                                           .
     c                   eval      c2tek2 = fwtek2                                           .
     c                   eval      c2vgrp = fwvgrp                                           .
     c                   eval      c2eann = fweann                                           .
     c                   eval      c2nobb = fwnobb                                           .
     c                   eval      c2enhe = fwenhe                                           .
     c                   eval      c2inpr = fwinpr                                           .
     c                   eval      c2sapr = fwsapr                                           .
     c                   eval      c2hoyd = fwhoyd                                           .
     c                   eval      c2bred = fwbred                                           .
     c                   eval      c2leng = fwleng                                           .
     c                   eval      c2volu = fwvolu                                           .
     c                   eval      c2vekt = fwvekt                                           .
     c                   eval      c2anta = fwanta                                           .
     c                   eval      c2bmen = fwbmen                                           .
     c                   eval      c2lvae = fwlvae                                           .
      *
     c                   if        fwdato <> *loval
     c     *dmy          move      fwdato        c2dato
     c                   endif
      *
     c                   if        fwudat <> *loval
     c     *dmy          move      fwudat        c2udat
     c                   endif
      *
      * Henter leverandørnavn
      *
     c     rlevl1_key    chain     rlevl1                             91
     c                   if        *in91 = *off
     c                   eval      c2lnav = rlnavn
     c                   endif
      *
      * Henter beskrivelse, og beregner salgspris enhet-3 og DG
      *
     c                   exsr      hent_kontr
      *
      * Tar vare på sammenlignigsverdier
      *
     c                   eval      w_enhe_gml = c2enhe
     c                   eval      w_inpr_gml = c2inpr
     c                   eval      w_sapr_gml = c2sapr
      *
     c                   if        p_valg = 5                                            .....
     c                   eval      *in30 = *on                                               .
     c                   endif                                                           .....
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av kontroll-informasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_kontr    begsr
      *
     c                   eval      c2dekn = 0
     c                   eval      c2ingm = 0
     c                   eval      c2sagm = 0
     c                   eval      c2inen = 0
     c                   eval      c2saen = 0
      *
      * Beregner dekningsgrad (DG)
      *
     c                   if        c2sapr <> 0
     c                   select
     c                   when      (100 - (c2inpr * 100 / c2sapr)) < -999,99
     c                   eval      c2dekn = -999,99
     c                   when      (100 - (c2inpr * 100 / c2sapr)) > 999,99
     c                   eval      c2dekn = 999,99
     c                   other
     c                   eval(h)   c2dekn = 100 - (c2inpr * 100 / c2sapr)
     c                   endsl
     c                   endif
      *
      * Beregner salgspriser inkl. mva
      *
     c                   eval      c2saim = c2sapr * w_mvat
      *
      * Henter eksisterende priser på vare
      *
     c                   if        c2lvar <> *blank
     c                   eval      vvarl5_lvar = c2lvar
     c     vvarl5_key    chain     vvarl5                             91
     c                   if        *in91 = *off
      *
     c                   eval      w_lety = 0
     c                   eval      w_inlr = *blank
      *
     c                   call      'VP701R'
     c                   parm                    w_firm
     c                   parm                    vvvare
5.70 c                   parm                    w_prgr
     c                   parm                    c2enhe
     c                   parm                    w_lety
     c                   parm                    w_udat
     c                   parm                    w_inlr
     c                   parm                    c2sagm
     c                   parm                    w_bupr
     c                   parm                    w_kopr
     c                   parm                    c2ingm
      *
     c                   endif
     c                   endif
      *
      * Finner prosentvis endring av pris
      *
     c                   if        c2ingm <> 0
     c                   eval(h)   c2inen = (100 * c2inpr / c2ingm) - 100
     c                   endif
      *
     c                   if        c2sagm <> 0
     c                   eval(h)   c2saen = (100 * c2sapr / c2sagm) - 100
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørringer (F4)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
     c                   if        w_afld = 'C2ENHE'
     c                   call      'VA510R'
     c                   parm                    w_firm
     c                   parm                    c2enhe
     c                   parm                    w_etxt
     c                   goto      end_spØrr
     c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
      * Kaller program for validering av vare
      *
     c                   call      'FW113R'
     c                   parm                    w_firm
     c                   parm                    w_ldor
     c                   parm                    w_line
     c                   parm                    c2lvar
     c                   parm                    c2tek1
     c                   parm                    c2eann
     c                   parm                    c2nobb
     c                   parm                    c2enhe
     c                   parm                    c2inpr
     c                   parm                    c2sapr
     c                   parm                    fwtvar
     c                   parm                    p_kvar
     c                   parm                    c2stat
     c                   parm                    w_indi
      *
     c                   if        c2stat <> 0
      *
     c                   select
     c                   when      w_indi = 31
     c                   eval      *in31 = *on
     c                   when      w_indi = 32
     c                   eval      *in32 = *on
     c                   when      w_indi = 33
     c                   eval      *in33 = *on
     c                   when      w_indi = 34
     c                   eval      *in34 = *on
     c                   when      w_indi = 35
     c                   eval      *in35 = *on
     c                   when      w_indi = 36
     c                   eval      *in36 = *on
     c                   when      w_indi = 37
     c                   eval      *in37 = *on
     c                   when      w_indi = 38
     c                   eval      *in38 = *on
     c                   when      w_indi = 39
     c                   eval      *in39 = *on
     c                   when      w_indi = 40
     c                   eval      *in40 = *on
     c                   when      w_indi = 41
     c                   eval      *in41 = *on
     c                   endsl
      *
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
      *
     c                   endif
      *
      * Sjekk dato
      *
     c                   if        c2dato <> 0
     c     *dmy          test(d)                 c2dato                 50
     c                   if        *in50 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk                                                 .
     c                   endif
     c                   endif
      *
      * Sjekk utgått-dato
      *
     c                   if        c2udat <> 0
     c     *dmy          test(d)                 c2udat                 51
     c                   if        *in51 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk                                                 .
     c                   endif
     c                   endif
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdater      begsr
      *
     c                   clear                   flvalur
     c     flvalu_key    chain     flvalur                            90
      *
     c                   eval      fwlvar = c2lvar
     c                   eval      fwtek1 = c2tek1
     c                   eval      fwtek2 = c2tek2
     c                   eval      fwvgrp = c2vgrp
     c                   eval      fweann = c2eann
     c                   eval      fwnobb = c2nobb
     c                   eval      fwdato = *loval
     c                   eval      fwenhe = c2enhe
     c                   eval      fwinpr = c2inpr
     c                   eval      fwsapr = c2sapr
     c                   eval      fwhoyd = c2hoyd
     c                   eval      fwbred = c2bred
     c                   eval      fwleng = c2leng
     c                   eval      fwvolu = c2volu
     c                   eval      fwvekt = c2vekt
     c                   eval      fwanta = c2anta
     c                   eval      fwbmen = c2bmen
     c                   eval      fwlvae = c2lvae
      *
     c                   if        c2dato <> 0
     c     *dmy          move      c2dato        fwdato
     c                   endif
      *
     c                   if        c2udat <> 0
     c     *dmy          move      c2udat        fwudat
     c                   endif
      *
      * Oppdatering av register
      *
     c                   if        *in90 = *off                                             ..
6.10 c                   time                    fwedat
6.10 c                   time                    fwetim
6.10 c                   eval      fweusr = l_user
     c                   update    flvalur                                                   .
     c                   endif                                                           .....
      *
     c     end_oppd      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ldor
     c                   parm                    p_line
     c                   parm                    p_kvar
     c                   parm                    p_stat
     c                   parm                    p_valg
      *
      * Key for oppslag på leverandør vareregister
      *
     c     flval1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    w_line
      *
      * Key for oppslag på leverandør-register
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
      *
      * Key for oppslag på vareregister
      *
     c     vvarl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    vvarl5_lvar
      *
      * Key for oppdatering av leverandør vareregister
      *
     c     flvalu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    w_line
      *
      *
      * Henter firma, leverandør og linjenummer fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_ldor = p_ldor
     c                   eval      w_line = p_line
      *
      * Henter dagens dato
      *
     c                   time                    w_udat
      *
      * Henter mva-faktor
      *
     c                   call      'FÅ705R'
     c                   parm                    w_firm
     c                   parm                    w_udat
     c                   parm                    w_mvap
     c                   parm                    w_mvat
     c                   parm                    w_mvaf
      *
      * Finner prisgruppe
      *
     c                   call      'VL711R'
     c                   parm                    w_prgr
      *
     c                   endsr
