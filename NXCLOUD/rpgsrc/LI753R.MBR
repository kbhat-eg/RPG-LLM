     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : NXCLOUD
      *  Program . . . . : LI753R
      *  Beskrivelse . . : Danner betalingstranser til PowerOfficeGO
      *                    basert på inngåande fakturatranser fra gjennomstrøm
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      *  --------- ------  -------------------------------------------------
      *  800       0070323 Nytt program                            ask
8.01  *  800       271023  NXS-18197, NXS-18120
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flwefpf    if   e           k disk
     fipogst    o    e           k disk    rename(ipogst:ipogstr)
      *
      * Definering av LDA
      *
     d                uds
     d l_user                911    920
     d l_filg                931    933
     d l_firm                944    946  0
      *
      * Definering av parametre
      *
     d p_fakl          s             13  0
      *
      * Definering av variable i nøkler
      *
      *
      * Definering av øvrige variable
      *
     d w_rect          s              4
     d w_logg          s               z
     d w_tell          s              2  0
     d w_strt          s              2  0
     d w_firm          s              3  0
     d w_kode          s              1
     d w_fell          s              6
     d w_type          s              2
     d w_fast          s             10
     d w_biln          s              8  0
     d w_bilk          s              2  0
     d w_bilt          s             20
     d w_dekr          s              1
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_lfaknr        s             15
     d w_kidn          s             25
     d w_betb          s              2  0
     d w_betf          s              2  0
     d w_dagr          s              3  0
     d w_dagk          s              3  0
     d w_fada          s               d   datfmt(*iso)
     d w_ffda          s               d   datfmt(*iso)
     d w_ldor          s              6  0
     d w_flev          s              6  0
     d w_vlev          s              6  0
     d w_sumt          s             10  2
     d w_hela          s              8
     d w_heln          s              8  0
     d w_desa          s              2
     d w_desn          s              2  0
     d w_desm          s              2  2
      *
     d b_fakl          s              1    inz(*off)
     d b_sjek          s              1    inz(*off)
     d b_logg          s              1    inz(*off)
     d u_remf          s              1    inz(*off)
      *
     d w_lintel        s              8  0
     d w_sumbel        s             11  2
     d w_dumtim        s               z
     d w_dumsum        s             11  2
     d w_dumusr        s             10
     d w_dumerr        s              2
     d w_de010         s              1  0
     d w_de020         s              2  0
     d w_de040         s              4  0
     d w_de050         s              5  0
     d w_de060         s              6  0
     d w_de080         s              8  0
     d w_de092         s              9  2
     d w_de102         s             10  2
     d w_de112         s             11  2
     d w_ch001         s              1
     d w_ch003         s              3
     d w_ch006         s              6
     d w_ch025         s             25
     d w_dumant        s              5  0
      *
     d c_digi          c                   '0123456789'
     d c_char          c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅ ab+
     d                                      cdefghijklmnopqrstuvwxyzæøå.,/:-'
     d c_maxp          c                   'ED09'
     d c_diga          c                   ' ,.0123456789'
      *
      *  Definering av datastrukturer for melding inn
      *
      * FAKTURA
      *
     d f_ed00          ds
     d  f_id00                        4
     d  f_decs00                      1
     d  f_send00                     13
     d  f_mott00                     13
     d  f_gdat00                      6
     d  f_gtim00                      4
     d  f_test00                      1
      *
     d f_ed01          ds
     d  f_id01                        4
     d  f_mref01                     14
     d  f_mtyp01                      6
     d  f_mver01                      3
     d  f_mrel01                      3
     d  f_mkon01                      2
     d  f_mfor01                      6
     d  f_mkod01                      3
     d  f_fanr01                     35
     d  f_mfun01                      3
      *
     d f_ed02          ds
     d  f_id02                        4
     d  f_kva102                      3
     d  f_mdat02                      8
     d  f_for102                      3
     d  f_kva202                      3
     d  f_levd02                      8
     d  f_for202                      3
      *
     d f_ed03          ds
     d  f_id03                        4
     d  f_ftxq03                      3
     d  f_ftxt03                     70
      *
     d f_ed04          ds
     d  f_id04                        4
     d  f_rkv104                      3
     d  f_repa04                     35
      *
     d f_ed05          ds
     d  f_id05                        4
     d  f_pkva05                      3
     d  f_peana05                    13
     d    f_pean05                   13  0 overlay(f_peana05:1)
     d  f_porg05                      3
     d  f_pnav05                     30
      *
     d f_ed06          ds
     d  f_id06                        4
     d  f_pad106                     30
     d  f_pad206                     30
      *
     d f_ed07          ds
     d  f_id07                        4
     d  f_ppon07                      4
     d  f_ppst07                     30
     d  f_lkod07                      3
      *
     d f_ed08          ds            80
     d  f_id08                        4
     d  f_rkva08                      3
     d  f_rref08                     35
      *
     d f_ed09          ds
     d  f_id09                        4
     d  f_kfun09                      3
     d  f_kkon09                     35
      *
     d f_ed10          ds
     d  f_id10                        4
     d  f_vadk10                      3
     d  f_vako10                      3
     d  f_vakv10                      3
     d  f_fdko10                      3
     d  f_fdkv10                      3
     d  f_fdat10                      8  0
     d  f_fdfo10                      3
     d  f_ffko10                      3
     d  f_ffkv10                      3
     d  f_ffat10                      8  0
     d  f_fffo10                      3
      *
     d f_ed11          ds
     d  f_id11                        4
     d  f_trkv11                      3
     d  f_trmo11                      3
     d  f_trna11                     35
     d  f_leko11                      3
     d  f_lebe11                      3
     d  f_lekv11                      3
      *
     d f_ed12          ds
     d  f_id12                        4
     d  f_rgkv12                      3
     d  f_rgko12                      3
     d  f_rgbe12                     35
     d  f_beko12                      3
     d  f_belp12                     18
     d    f_belh12                   15  0 overlay(f_belp12:1)
     d    f_beld12                    2  2 overlay(f_belp12:17)
     d  f_takv12                      3
     d  f_tako12                      3
     d  f_tapr12                      6
     d    f_taph12                    3  0 overlay(f_tapr12:1)
     d    f_tapd12                    2  2 overlay(f_tapr12:5)
     d  f_tafr12                      2
      *
     d f_ed13          ds
     d  f_id13                        4
     d  f_vlina13                     6
     d    f_vlin13                    6  0 overlay(f_vlina13:1)
     d  f_veana13                    13
     d    f_vean13                   13  0 overlay(f_veana13:1)
     d  f_eank13                      3
     d  f_piq113                      3
     d  f_nobba13                    15
     d    f_nobb13                    8  0 overlay(f_nobba13:1)
     d    f_nobx13                    7    overlay(f_nobba13:9)
     d  f_noko13                      3
     d  f_piq213                      3
     d  f_lvar13                     15
     d  f_lvko13                      3
      *
6.13 d x_ed13          ds
6.13 d  x_id13                        4
6.13 d  x_vlina13                     6
6.13 d    x_vlin13                    6  0 overlay(x_vlina13:1)
6.13 d  x_veana13                    14
6.13 d    x_vean13                   14  0 overlay(x_veana13:1)
6.13 d  x_eank13                      3
6.13 d  x_piq113                      3
6.13 d  x_nobba13                    15
6.13 d    x_nobb13                    8  0 overlay(x_nobba13:1)
6.13 d    x_nobx13                    7    overlay(x_nobba13:9)
6.13 d  x_noko13                      3
6.13 d  x_piq213                      3
6.13 d  x_lvar13                     15
6.13 d  x_lvko13                      3
      *
     d f_ed14          ds
     d  f_id14                        4
     d  f_txko14                      3
     d  f_vtx114                     35
     d  f_vtx214                     35
      *
     d f_ed15          ds
     d  f_id15                        4
     d  f_kvko15                      3
     d  f_kvan15                     15
     d    f_kvah15                   11  0 overlay(f_kvan15:1)
     d    f_kvad15                    3  3 overlay(f_kvan15:13)
     d  f_enhk15                      3
      *
     d f_ed16          ds
     d  f_id16                        4
     d  f_fxlq16                      3
     d  f_ftxl16                     70
      *
     d f_ed17          ds
     d  f_id17                        4
     d  f_bonu17                      3
     d  f_bekf17                      3
     d  f_befo17                     18
     d    f_befh17                   15  0 overlay(f_befo17:1)
     d    f_befd17                    2  2 overlay(f_befo17:17)
     d  f_beke17                      3
     d  f_beet17                     18
     d    f_beeh17                   15  0 overlay(f_beet17:1)
     d    f_beed17                    2  2 overlay(f_beet17:17)
      *
     d f_ed18          ds
     d  f_id18                        4
     d  f_prkv18                      3
     d  f_pris18                     15
     d    f_prih18                   11  0 overlay(f_pris18:1)
     d    f_prid18                    3  3 overlay(f_pris18:13)
     d  f_enha18                      9
     d    f_enhh18                    9  0 overlay(f_enha18:1)
     d  f_enhp18                      3
      *
     d f_ed19          ds
     d  f_id19                        4
     d  f_rekv19                      3
     d  f_refn19                     35
     d  f_relia19                     6
     d    f_reli19                    6  0 overlay(f_relia19:1)
      *
     d f_ed20          ds
     d  f_id20                        4
     d  f_mvak20                      3
     d  f_mvty20                      3
     d  f_mvgr20                     15
     d  f_mvap20                      6
     d    f_mvah20                    3  0 overlay(f_mvap20:1)
     d    f_mvad20                    2  2 overlay(f_mvap20:5)
     d  f_mvko20                      3
      *
     d f_ed21          ds
     d  f_id21                        4
     d  f_sakv21                      3
     d  f_saty21                      3
     d  f_mvbe21                     35
     d  f_sagr21                     15
     d    f_sagh21                   12  0 overlay(f_sagr21:1)
     d    f_sagd21                    2  2 overlay(f_sagr21:14)
     d  f_sasa21                     15
     d    f_sash21                   12  0 overlay(f_sasa21:1)
     d    f_sasd21                    2  2 overlay(f_sasa21:14)
     d  f_same21                      3
      *
     d f_ed22          ds
     d  f_id22                        4
     d  f_sako22                      3
     d  f_sabe22                     18
     d    f_sabh22                   15  0 overlay(f_sabe22:1)
     d    f_sabd22                    2  2 overlay(f_sabe22:17)
      *
     d f_ed23          ds
     d  f_id23                        4
     d  f_ftkv23                      3
     d  f_ftrf23                      3
     d  f_ftty23                      3
     d  f_fttx23                     35
     d  f_ftmk23                     15
     d    f_ftmh23                   11  0 overlay(f_ftmk23:1)
     d    f_ftmd23                    3  3 overlay(f_ftmk23:13)
     d  f_ftme23                      3
      *
     d f_ed24          ds
     d  f_id24                        4
     d  f_ftpr24                      8
     d    f_ftph24                    5  0 overlay(f_ftpr24:1)
     d    f_ftpd24                    2  2 overlay(f_ftpr24:7)
     d  f_ftbe24                     18
     d    f_ftbh24                   15  0 overlay(f_ftbe24:1)
     d    f_ftbd24                    2  2 overlay(f_ftbe24:17)
     d  f_ftko24                      3
     d  f_ftsa24                     15
     d    f_ftsh24                   12  0 overlay(f_ftsa24:1)
     d    f_ftsd24                    2  2 overlay(f_ftsa24:14)
      *
     d f_ed25          ds
     d  f_id25                        4
     d  f_toty25                      3
     d  f_tobe25                     18
     d    f_tobh25                   15  0 overlay(f_tobe25:1)
     d    f_tobd25                    2  2 overlay(f_tobe25:17)
      *
     d f_ed26          ds
     d  f_id26                        4
     d  f_avkv26                      3
     d  f_avty26                      3
     d  f_avgr26                     18
     d    f_avgh26                   15  0 overlay(f_avgr26:1)
     d    f_avgd26                    2  2 overlay(f_avgr26:17)
     d  f_avsa26                      6
     d    f_avsh26                    3  0 overlay(f_avsa26:1)
     d    f_avsd26                    2  2 overlay(f_avsa26:5)
     d  f_avbk26                      3
     d  f_avbe26                     18
     d    f_avbh26                   15  0 overlay(f_avbe26:1)
     d    f_avbd26                    2  2 overlay(f_avbe26:17)
      *
     d f_ed27          ds
     d  f_id27                        4
     d  f_tokv27                      3
     d  f_toty27                      3
     d  f_totx27                     35
     d  f_tobe27                     18
     d    f_tobh27                   15  0 overlay(f_tobe27:1)
     d    f_tobd27                    2  2 overlay(f_tobe27:17)
      *
       dcl-s co402_verdi1  char(1);
       dcl-s co402_verdi2  char(2048);
       DCL-PR CO402R EXTPGM('CO402R');
         p_filgrp            char(3)     const;
         p_firm              packed(3:0) const;
         p_lager             packed(2:0) const;
         p_nokkel            char(50)    const;
         p_verdi1            char(1);
         p_verdi2            char(2048);
       END-PR;
      *
     c/Exec SQL
     c+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR,
     c+             commit=*none
     c/End-Exec
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hoved program styrerutine - les av innposter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   read      lwefpf
      *
     c                   dow       not %eof
     c                   eval      w_rect = %subst(faklin:1:4)
      *
     c                   if        b_sjek = *on
     c                   if        b_fakl = *off
     c                   goto      les_neste
     c                   endif
     c                   endif
      *
     c                   select
     c                   when      w_rect = 'ED01'
     c                   exsr      ed01
     c                   when      w_rect = 'ED02'
     c                   exsr      ed02
     c                   when      w_rect = 'ED03'
     c                   exsr      ed03
     c                   when      w_rect = 'ED04'
     c                   exsr      ed04
     c                   when      w_rect = 'ED05'
     c                   exsr      ed05
     c                   when      w_rect = 'ED06'
     c                   exsr      ed06
     c                   when      w_rect = 'ED07'
     c                   exsr      ed07
     c                   when      w_rect = 'ED08'
     c                   exsr      ed08
     c                   when      w_rect = 'ED09'
     c                   exsr      ed09
     c                   when      w_rect = 'ED10'
     c                   exsr      ed10
     c                   when      w_rect = 'ED11'
     c                   exsr      ed11
     c                   when      w_rect = 'ED12'
     c                   exsr      ed12
     c                   when      w_rect = 'ED13'
     c                   exsr      ed13
     c                   when      w_rect = 'ED14'
     c                   exsr      ed14
     c                   when      w_rect = 'ED15'
     c                   exsr      ed15
     c                   when      w_rect = 'ED16'
     c                   exsr      ed16
     c                   when      w_rect = 'ED17'
     c                   exsr      ed17
     c                   when      w_rect = 'ED18'
     c                   exsr      ed18
     c                   when      w_rect = 'ED19'
     c                   exsr      ed19
     c                   when      w_rect = 'ED20'
     c                   exsr      ed20
     c                   when      w_rect = 'ED21'
     c                   exsr      ed21
     c                   when      w_rect = 'ED22'
     c                   exsr      ed22
     c                   when      w_rect = 'ED23'
     c                   exsr      ed23
     c                   when      w_rect = 'ED24'
     c                   exsr      ed24
     c                   when      w_rect = 'ED25'
     c                   exsr      ed25
     c                   when      w_rect = 'ED26'
     c                   exsr      ed26
     c                   when      w_rect = 'ED27'
     c                   exsr      ed27
     c                   endsl
      *
     c     les_neste     tag
      *
     c                   read      lwefpf
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
      * Sjekker om det skal skrives poster fra siste faktura
      *
     c                   if        b_sjek = *on and
     c                             b_fakl = *on
     c                   exsr      hent_bilnum
     c                   exsr      postering
     c                   endif
      *
     c                   if        b_logg = *on
     c                   exsr      logg_bilag
     c                   exsr      logg_hode
     c                   endif
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  ED01 - Meldingshode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed01          begsr
      *
      * Sjekker om det skal skrives poster fra forrige faktura
      *
     c                   if        b_sjek = *on and
     c                             b_fakl = *on
     c                   exsr      hent_bilnum
     c                   exsr      postering
     c                   eval      b_sjek = *off
     c                   endif
      *
     c                   eval      f_ed01 = faklin
     c                   if        f_mkod01 = '380'
     c                   eval      w_dekr = 'D'
     c                   else
     c                   eval      w_dekr = 'K'
     c                   endif
     c                   eval      w_lfaknr = f_fanr01
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  ED02 - Datoinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed02          begsr
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  ED03 - Fritekstinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed03          begsr
      *
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  ED04 - Referanserinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed04          begsr
      *
     c                   eval      f_ed04 = faklin
     c                   if        f_rkv104 = 'PQ '
     c                   eval      w_kidn = f_repa04
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  ED05 - Partsinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed05          begsr
      *
     c                   eval      f_ed05 = faklin
      *
     c                   if        f_pkva05 = 'II '
     c                   eval      b_sjek = *on
     c                   if        f_pean05 = p_fakl
     c                   exsr      hent_lev
     c                   if        w_ldor > 0
     c                   eval      b_fakl = *on
     c                   eval      w_flev = w_ldor
     c                   exsr      hent_bet
     c                   else
     c                   eval      b_fakl = *off
     c                   endif
     c                   endif
     c                   endif
      *
     c                   if        f_pkva05 = 'SU '
     c                   if        f_pean05 = p_fakl
     c                   eval      b_fakl = *off
     c                   else
     c                   exsr      hent_lev
     c                   eval      w_vlev = w_ldor
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  ED06 - Partsinformasjon adresse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed06          begsr
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  ED07 - Partsinformasjon adresse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed07          begsr
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  ED08 - Partsinformasjon referanse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed08          begsr
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  ED09 - Partsinformasjon kontaktperson
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed09          begsr
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  ED10 - Valuta/forfalls - informasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed10          begsr
      *
     c                   eval      f_ed10 = faklin
      *
     c                   eval      w_fada = %date(f_fdat10:*iso)
     c                   if        w_dagr > 0
     c                   eval      w_ffda = w_fada +%days(w_dagr)
     c                   else
     c                   eval      w_ffda = w_fada +%days(w_dagk)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  ED11 - Transport/leverings - informasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed11          begsr
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED12 - Fradrags og tilleggsposter på hodenivå
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed12          begsr
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED13 - Varelinjer/vareidentifikasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed13          begsr
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED14 - Varelinjer/varetekst
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed14          begsr
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED15 - Varelinjer/kvantumsinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed15          begsr
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED16 - Varelinjer/fritekst
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed16          begsr
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED17 - Varelinjer/rabatt & gebyrer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed17          begsr
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED18 - Varelinjer/priser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed18          begsr
      *
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED19 - Varelinjer/kjøpers referanse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed19          begsr
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED20 - Varelinjer/mva prosent
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed20          begsr
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED21 - Varelinjer/avgiftsinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed21          begsr
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED22 - Varelinjer/avgiftsbeløp
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed22          begsr
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED23 - Varelinjer/fradrag & tilleggskoder
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed23          begsr
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED24 - Varelinjer/fradrag & tilleggsbeløp / prosent
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed24          begsr
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED25 - Fakturatotaler
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed25          begsr
      *
     c                   eval      f_ed25 = faklin
      *
     c                   if        f_toty25 = '86 '
     c                   eval      w_tell = %scan('.':f_tobe25)
     c                   eval      w_strt = w_tell - 8
     c                   eval      w_hela = %subst(f_tobe25:w_strt:8)
     c                   eval      w_desa = %subst(f_tobe25:w_tell+1:2)
     c                   eval      w_heln = %dec(w_hela:8:0)
     c                   eval      w_desn = %dec(w_desa:2:0)
     c                   eval      w_desm = w_desn / 100
     c                   eval      w_sumt = w_heln + w_desm
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED26 - Fakturatotalavgift
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed26          begsr
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED27 - Fakturatot. fradrag/tillegg
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed27          begsr
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hent leverandørinfo
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_lev      begsr

       exec sql
             select rllevr, rlbetb
               into :w_ldor, :w_betb
              from rlevpf
                where rlfirm = :w_firm and
                      rleanl = :f_pean05;

       if sqlcod < 0 or
          sqlcod = 100;
        w_ldor = 0;
        w_betb = 0;
       endif;

     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hent betalingsbetingelse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_bet      begsr

       w_betf = w_betb;
       exec sql
             select racda1, racdag
               into :w_dagr, :w_dagk
              from ra03pf
                where racfir = :w_firm and
                      rackod = :w_betb;

       if sqlcod < 0 or
          sqlcod = 100;
        w_dagr = 0;
        w_dagk = 0;
       endif;

     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hent betalingsbetingelse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     postering     begsr
      *
     c                   eval      w_bilk = 61
     c                   if        w_dekr = 'K'
     c                   eval      w_bilt = 'Kre. ' + w_lfaknr
     c                   eval      iitype = 'KRED'
     c                   else
     c                   eval      w_bilt = 'Fak. ' + w_lfaknr
     c                   eval      iitype = 'FAKT'
     c                   endif
      *
     c                   eval      iifirm = w_firm
     c                   eval      iibiln = w_biln
     c                   eval      iibdat = w_fada
     c                   eval      iifdat = w_ffda
     c                   eval      iibilk = w_bilk
     c                   eval      iitext = w_bilt
     c                   eval      iidavd = 0
     c                   eval      iidsps = 0
     c                   eval      iidmom = ' '
     c                   eval      iicavd = 0
     c                   eval      iicsps = 0
     c                   eval      iicmom = ' '
     c                   eval      iikund = w_flev
     c                   eval      iikref = 0
     c                   eval      iikidi = w_kidn
     c                   eval      iivalk = ' '
     c                   eval      iivalb = 0
     c                   eval      iiproj = ' '
     c                   eval      iiakti = ' '
     c                   eval      iisfak = w_lfaknr
     c                   eval      iiboda = w_fada
     c                   eval      iiodat = w_date
     c                   eval      iiotim = w_time
     c                   eval      iiousr = l_user
     c                   eval      iibetb = w_betf
      *
8.01 c*                  if        u_remf = *on
8.01 c*                  eval      iiremi = 'NEI'
8.01 c*                  else
     c                   eval      iiremi = 'JA '
8.01 c*                  endif
      *
     c                   exsr      skriv_deb
     c                   exsr      logg_linjer
     c                   exsr      skriv_kre
     c                   exsr      logg_linjer
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Skriver betalingstransaksjoner  debet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_deb     begsr
      *
     c                   if        w_dekr = 'K'
     c                   eval      iidkto = w_flev
     c                   eval      iickto = 0
     c                   else
     c                   eval      iidkto = w_vlev
     c                   eval      iickto = 0
     c                   eval      iitype = 'FAKT'
     c                   endif
      *
8.01 c                   eval      iibelp = w_sumt
     c                   write     ipogstr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Skriver betalingstransaksjoner  kredit
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_kre     begsr
      *
     c                   if        w_dekr = 'K'
     c                   eval      iidkto = 0
     c                   eval      iickto = w_vlev
     c                   else
     c                   eval      iidkto = 0
     c                   eval      iickto = w_flev
     c                   endif
      *
8.01 c                   eval      iibelp = w_sumt * -1
     c                   write     ipogstr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Legger ut posteringstrans til loggfil linjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      /Free
       begsr logg_linjer;

         w_lintel = w_lintel + 1;
         b_logg = *on;
         w_de010 = 0;
         w_de020 = 0;
         w_de040 = 0;
         w_de050 = 0;
         w_de060 = 0;
         w_de080 = 0;
         w_de092 = 0;
         w_de102 = 0;
         w_de112 = 0;
         w_ch001 = ' ';
         w_ch003 = ' ';
         w_ch006 = ' ';
         w_ch025 = ' ';

            exec sql
            insert into rlodst
              (rldfir,rldtyp,rldts1,rldlin,rldper,
               rldbun,rldbin,rldbli,rldbda,rldfda,
               rldbko,rldbtx,rlddav,rlddko,rlddsp,
               rlddmv,rldbel,rldmvg,rldkav,rldkko,
               rldksp,rldkmv,rldpro,rldpra,rldpru,
               rldpao,rldkrf,rldres,rldlki,rldkki,
               rldkkk,rldaki,rldvko,rldvbe,rldmko,
               rldmen,rldsal,rldsfa,rldtty,rldbet,rldrem)
            values(:w_firm,'BETR',:w_logg,:w_lintel,
                   :w_de040,:w_de050,:w_biln,:w_de040,
                   :w_fada,:w_ffda,:w_bilk,:w_bilt,
                   :w_de040,:iidkto,:w_de060,:w_ch001,
                   :w_sumt,:w_de102,:w_de040,:iickto,
                   :w_de060,:w_ch001,:w_ch006,:w_ch006,
                   :w_ch006,:w_ch006,:w_de080,:w_flev,
                   :w_kidn,:w_de080,:w_de010,:w_ch025,
                   :w_ch003,:w_de112,:w_de020,:w_de092,
                   :w_ch001,:w_lfaknr,:iitype,:w_betf,:iiremi);
           if sqlcod <> 0;
              sqlcod = sqlcod;
           endif;

       endsr;
      /END-FREE
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Legger ut post til loggfil hode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      /FREE
       begsr logg_bilag;

         w_sumbel = 0;
         w_dumtim = *loval;
         w_dumsum = 0;
         w_dumant = 0;
         w_dumusr = '  ';
         w_dumerr = '  ';

            exec sql
             insert into rlobst
              (RLBFIR, RLBBIL, RLBTS1,RLBAN1,RLBSD1,RLBSK1,RLBBR1,
               RLBTS2,RLBAN2,RLBSD2,RLBSK2,RLBBR2,RLBFF2,RLBFT2,
               RLBTS3,RLBAN3,RLBSD3,RLBSK3,RLBBR3,RLBFF3,RLBFT3,
               RLBTS4,RLBAN4,RLBSD4,RLBSK4,RLBBR4,RLBFF4,RLBFT4,
               RLBTS5, RLBBR5, RLBTS6, RLBBR6)
               select
                rldfir, rldbin, :w_logg, count(*) ,
                sum(rldbel)/2 ,sum(rldbel)/2, :l_user,
                :w_dumtim, :w_dumant, :w_dumsum, :w_dumsum,
                :w_dumusr, ' ', ' ',
                :w_dumtim, :w_dumant, :w_dumsum, :w_dumsum,
                :w_dumusr, ' ', ' ',
                :w_dumtim, :w_dumant, :w_dumsum, :w_dumsum,
                :w_dumusr, ' ', ' ',
                :w_dumtim, :w_dumusr, :w_dumtim, :w_dumusr
              from rlodst
              where rldfir = :w_firm and rldts1 = :w_logg
              group by rldfir,rldbin
              order by rldbin ;
           if sqlcod <> 0;
              sqlcod = sqlcod;
           endif;
       endsr;
      /END-FREE
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Legger ut post til loggfil hode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      /FREE
       begsr logg_hode;

         w_sumbel = 0;
         w_dumtim = *loval;
         w_dumsum = 0;
         w_dumusr = '  ';
         w_dumerr = '  ';

            exec sql
            select sum(rldbel) into :w_sumbel
              from rlodst
              where rldfir = :w_firm and rldts1 = :w_logg;

            if w_sumbel > 0;
            exec sql
            insert into rlohst
              (rlhfir,rlhtyp,rlhts1,rlhbs1,rlhbr1,
               rlhts2,rlhbs2,rlhbr2,rlhfl2,
               rlhts3,rlhbs3,rlhbr3,rlhfl3,
               rlhts4,rlhbs4,rlhbr4,rlhfl4,
               rlhts5,rlhbr5,
               rlhts6,rlhbr6)
            values(:w_firm,'BETR',:w_logg,:w_sumbel,:l_user,
                   :w_dumtim,:w_dumsum,:w_dumusr,:w_dumerr,
                   :w_dumtim,:w_dumsum,:w_dumusr,:w_dumerr,
                   :w_dumtim,:w_dumsum,:w_dumusr,:w_dumerr,
                   :w_dumtim,:w_dumusr,
                   :w_dumtim,:w_dumusr);

              if sqlcod <> 0;
                 sqlcod = sqlcod;
              endif;
            endif;
       endsr;
      /END-FREE
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Skriver betalingstransaksjoner  kredit
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_bilnum   begsr
      *
     c                   eval      w_kode = '0'
     c                   eval      w_fell = 'FBILAG'
     c                   eval      w_type = 'PO'
     c                   eval      w_fast = *blank
     c                   eval      w_biln = *zero
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
     c                   parm                    w_biln
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_fakl
      *
     c                   time                    w_time
     c                   time                    w_date
     c                   eval      w_firm = l_firm
     c                   time                    w_logg
      *
      * Skal remitteringsflagg slås av
      *
       CallP CO402R(l_filg:w_firm:0:'POWOFF_REM_AV':
                       co402_verdi1:co402_verdi2);
         if co402_verdi1 = '1';
             u_remf = *on;
         endif;
      *
     c                   endsr
