     H/TITLE  ASOFAK: Vedlikehold av avrundingsregler
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : EK130R
      * Beskrivelse . . : Parametre for kreditering av bonus
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * 6.20  171013 BSA  Nytt program
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     fvbotl1    if   e           k disk    rename(vbotpfr:vbotl1r)
     febkrlu    uf a e           k disk    rename(ebkrpfr:ebkrlur)
     febkrl1    if   e           k disk    rename(ebkrpfr:ebkrl1r)
      *
     fek130d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av nøkkelverdier
      *
     d vbotl1_tbot     s                   like(vatbot)
     d votyl1_otyp     s                   like(vaotyp)
     d vvarl1_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d w_firm          s                   like(ebkfir)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * A1BLD Behandle bildet for valg
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     a1tag1        TAG
      *
      * Hent opplysninger fra siste kjøring
      *
     c                   eval      a1kbtx = *blanks
     c                   eval      a1kotx = *blanks
     c                   eval      a1kvtx = *blanks
      *
     c     ebkrl1_key    chain     ebkrl1
     c                   if        %found
     c                   eval      a1kbot = ebkbot
     c                   eval      a1kpfr = ebkpfr
     c                   eval      a1kpti = ebkpti
     c                   eval      a1koty = ebkoty
     c                   eval      a1kvar = ebkvar
     c                   eval      a1ktx1 = ebktx1
     c                   eval      a1ktx2 = ebktx2
     c                   eval      a1ktx3 = ebktx3
     c                   eval      a1ktx4 = ebktx4
     c                   else
     c                   eval      a1kbot = *blanks
     c                   eval      a1kpfr = 0
     c                   eval      a1kpti = 0
     c                   eval      a1koty = *blanks
     c                   eval      a1kvar = *blanks
     c                   eval      a1ktx1 = *blanks
     c                   eval      a1ktx2 = *blanks
     c                   eval      a1ktx3 = *blanks
     c                   eval      a1ktx4 = *blanks
     c                   endif
      *
     c     a1tag2        TAG
      *
      * Vis bilde
      *
     c                   exfmt     a1bld
      *
      * F3=Avslutt
      *
     c                   if        *inkc = *on or
     c                             *inkl = *on
     c                   goto      xslutt
     c                   endif
      *
      * F4=Spørring
      *
     c                   if        *inkd  = *on
     c                   exsr      spØrring
     c                   goto      a1tag2
     c                   endif
      *
     c                   eval      *in31 = *off
     c                   eval      *in32 = *off
     c                   eval      *in33 = *off
     c                   eval      *in34 = *off
     c                   eval      *in35 = *off
      *
      * Kontroller bonustype er gyldig
      *
     c                   if        a1kbot = *blanks
     c                   eval      *in31 = *on
     c                   goto      a1tag1
     c                   endif
      *
     c                   eval      vbotl1_tbot = a1kbot
     c     vbotl1_key    chain     vbotl1
     c                   if        not %found
     c                   eval      *in31 = *on
     c                   goto      a1tag1
     c                   endif
     c                   eval      a1kbtx = vatbes
      *
      * Kontroller ordretype er gyldig
      *
     c                   if        a1koty = *blanks
     c                   eval      *in32 = *on
     c                   goto      a1tag1
     c                   endif
      *
     c                   eval      votyl1_otyp = a1koty
     c     votyl1_key    chain     votyl1
     c                   if        not %found
     c                   eval      *in32 = *on
     c                   goto      a1tag1
     c                   endif
     c                   eval      a1kotx = vaotxt
      *
      * Kontroller varenummer er gyldig
      *
     c                   if        a1kvar = *blanks
     c                   eval      *in34 = *on
     c                   goto      a1tag1
     c                   endif
      *
     c                   eval      vvarl1_vare = a1kvar
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   eval      *in34 = *on
     c                   goto      a1tag1
     c                   endif
     c                   eval      a1kvtx = vvtek1
      *
      * Sjekk at det er gyldig periode
      *
     c     a1kpfr        ifeq      *zeros
     c                   eval      *in33 = *on
     c                   goto      a1tag1
     c                   endif
      *
      * Sjekk at det er gyldig periodeintervall
      *
     c     a1kpti        ifeq      *zeros
     c                   eval      *in33 = *on
     c                   goto      a1tag1
     c                   endif
      *
     c     a1kpfr        ifgt      a1kpti
     c                   eval      *in35 = *on
     c                   goto      a1tag1
     c                   endif
      *
      * Oppdaterer parameter register
      *
     c     ebkrlu_key    chain     ebkrlur
      *
     c                   eval      ebkbot = a1kbot
     c                   eval      ebkpfr = a1kpfr
     c                   eval      ebkpti = a1kpti
     c                   eval      ebkoty = a1koty
     c                   eval      ebkvar = a1kvar
     c                   eval      ebktx1 = a1ktx1
     c                   eval      ebktx2 = a1ktx2
     c                   eval      ebktx3 = a1ktx3
     c                   eval      ebktx4 = a1ktx4
     c                   time                    ebkeda
     c                   time                    ebketi
     c                   eval      ebkeus = l_user
      *
     c                   if        %found
     c                   update    ebkrlur
     c                   else
     c                   eval      ebkfir = w_firm
     c                   write     ebkrlur
     c                   endif
      *
     c                   goto      xslutt
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Spørring på ordre-info-type
      *
     c                   if        w_afld = 'A1KBOT'
     c                   call      'VA565R'
     c                   parm                    w_firm
     c                   parm                    a1kbot
     c                   parm                    a1kbtx
     c                   leavesr
     c                   endif
      *
      * Spørring på ordretyper
      *
     c                   if        w_afld = 'A1KOTY'
     c                   call      'VA550R'
     c                   parm                    w_firm
     c                   parm                    a1koty
     c                   leavesr
     c                   endif
      *
      * Spørring på varenummer
      *
     c                   if        w_afld = 'A1KVAR'
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    a1kvar
     c                   parm                    a1kvtx
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        Begsr
      *
      * Key for oppslag på status-registeret
      *
     c     ebkrl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppdatering  av status-registeret
      *
     c     ebkrlu_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på bonustype register
      *
     c     vbotl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vbotl1_tbot
      *
      * Key for oppslag på ordretype register
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for oppslag på ordretype register
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Legger inn firmanummer i nøkklene
      *
     c                   eval      w_firm = l_firm
      *
     c                   Endsr
