     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LU102R
      * Beskrivelse . . : Program for registrering av nye tellebunter
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *  6.10  170609 BSA  Oppdatert med sporingsinformasjon
6.nu  *  6.30  170614 bof  Endring ihht. nummerutvidelse
7.xx  *  7.00  190516 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
     flbhelu    uf a e           k disk    rename(lbhepfr:lbhelur)
      *
     flu102d    cf   e             workstn
      *
      * Data Area for ut parametre
      *
     d                 ds
     d d_prec                        20
     d  d_firm                        3  0 overlay(d_prec)
     d  d_batc                       10    overlay(d_prec:4)
     d  d_numm                        6  0 overlay(d_prec:14)
     d  d_kode                        1    overlay(d_prec:20)
      *
      * Parameter for registreringsprogrammer
      *
     d                 ds
     d d_rrec                        30
     d d_retu                         1    overlay(d_rrec)
     d d_rfir                         3  0 overlay(d_rrec:2)
     d d_rbat                        10    overlay(d_rrec:5)
     d d_rnum                         6  0 overlay(d_rrec:15)
     d d_rtyp                        10    overlay(d_rrec:21)
      *
      * Local Data Area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
     d l_lage               1001   1002  0
      *
      * Definering av variable i parametre
      *
     d p_retu          s              1
     d p_firm          s                   like(lufirm)
     d p_batc          s                   like(lubatc)
     d p_numm          s                   like(lunumm)
      *
      * Definering av variable i nøkler
      *
     d lbhelu_batc     s                   like(lubatc)
     d lbhelu_numm     s                   like(lunumm)
      *
      * Definering av variable
      *
     d w_fdat          s               d   datfmt(*iso)
     d w_tdat          s               d   datfmt(*iso)
     d w_firm          s                   like(laafir)
     d w_feil          s              1
     d w_valg          s              1
     d w_retu          s              1
     d w_fast          s             10
     d w_fell          s              6
     d w_kode          s              1
6.nu d w_numm          s              8  0
     d w_ntyp          s              2
     d w_type          s                   like(lutype)
     d w_latx          s             20
     d w_adrs          s             30
     d w_ponr          s              4  0
     d w_pste          s             30
     d w_avde          s              4  0
     d w_prec          s             20
     d w_rrec          s             30
     d w_besk          s             30
      *
     d b_feil          s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  E2 Behandler innlegging av opplysninger LAGERTELLINGSBUNT-HODE
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Hent inn opplysninger fra LAGERTELLEBUNT-HODE
      *
     c     lbhelu_key    chain     lbhelur
      *
     c                   eval      w_type = *blank
      *
     c                   if        %found
     c                   eval      e2numm = lunumm
     c                   eval      e2text = lutext
     c                   eval      e2oper = luoper
     c                   eval      e2lage = lulage
     c                   exsr      finn_lager
     c                   eval      e2latx = w_latx
     c                   eval      e2type = lutype
     c                   eval      w_type = lutype
     c                   eval      *in30 = *on
     c                   else
     c                   eval      e2numm = 0
     c                   eval      e2text = *blank
     c                   eval      e2oper = *blank
     c                   eval      e2lage = l_lage
     c                   exsr      finn_lager
     c                   eval      e2type = *blank
     c                   endif
     c                   eval      *in21 = *on
     c                   eval      w_awro = 0
     c                   eval      w_awco = 0
      *
     c     e2taga        tag
     c                   exfmt     e2bld
     c                   eval      *in21 = *off
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_pgm
     c                   when      *inkd
     c                   exsr      sp_input
     c                   goto      e2taga
     c                   endsl
      *
      *  Sjekk,  Er tekstfeltet fylt ut
      *
     c                   if        e2text = *blank
     c                   eval      *in32 = *on
     c                   goto      e2taga
     c                   endif
      *
      *  Sjekk,  Finnes lager i KODE-REGISTER
      *
     c                   exsr      finn_lager
      *
     c                   if        b_feil = *on
     c                   eval      *in34 = *on
     c                   goto      e2taga
     c                   endif
      *
      *  Sjekk,  Er type fylt ut
      *
     c                   if        e2type = *blank
     c                   eval      *in35 = *on
     c                   goto      e2taga
     c                   endif
      *
      *  Sjekk,  Er type endret for DIVERSE
      *
     c                   if        w_type <> *blank
      *
     c                   if        w_type = 'DIVERSE'
     c                   if        e2type <> 'DIVERSE'
     c                   eval      *in36 = *on
     c                   goto      e2taga
     c                   endif
     c                   endif
      *
     c                   if        w_type <> 'DIVERSE'
     c                   if        e2type = 'DIVERSE'
     c                   eval      *in37 = *on
     c                   goto      e2taga
     c                   endif
     c                   endif
      *
     c                   endif
      *
      *  Henter neste ledige nummer dersom ikke utfylt
      *
     c                   if        e2numm = 0
     c                   eval      w_kode = '0'
     c                   eval      w_numm = 0
     c                   exsr      hntnum
     c                   eval      e2numm = w_numm
     c                   endif
      *
     c                   eval      lbhelu_numm = e2numm
     c     lbhelu_key    chain     lbhelur
      *
     c                   eval      lutext = e2text
     c                   eval      luoper = e2oper
     c                   eval      lulage = e2lage
     c                   eval      lutype = e2type
      *
     c                   if        %found
6.10 c                   time                    luedat
6.10 c                   time                    luetim
6.10 c                   eval      lueusr = l_user
     c                   update    lbhelur
     c                   eval      d_retu = *blank
     c                   else
      *
     c                   eval      luwsid = l_wsid
     c                   eval      luuser = l_user
      *
     c                   eval      lutnum = 0
      *
     c                   time                    ludato
     c                   time                    lutime
      *
     c                   eval      lusdat = *loval
     c                   eval      luodat = *loval
     c                   eval      luskod = 0
     c                   eval      lukode = 0
      *
     c                   eval      lufirm = w_firm
     c                   eval      lubatc = lbhelu_batc
     c                   eval      lunumm = lbhelu_numm
6.10 c                   time                    ludato
6.10 c                   time                    lutime
6.10 c                   eval      luuser = l_user
6.10 c                   time                    luedat
6.10 c                   time                    luetim
6.10 c                   eval      lueusr = l_user
     c                   write     lbhelur
     c                   eval      d_retu = 'N'
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Kaller opp programmer for registrering av telle-transer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Tester om automatisk innhenting
      *
     c                   if        *in30 = *off
     c                   if        e2type = 'HTPC'
     c                   exsr      les_ht
     c                   eval      d_retu = *blank
     c                   endif
      *
     c                   if        e2type = 'LISTE'
     c                   exsr      les_liste
     c                   eval      d_retu = *blank
     c                   endif
     c                   endif
      *
      *  Setter verdier i parameter-record
      *
     c                   eval      d_rfir = w_firm
     c                   eval      d_rbat = p_batc
     c                   eval      d_rnum = lbhelu_numm
     c                   eval      d_rtyp = lutype
     c                   eval      w_rrec = d_rrec
      *
     c                   call      'LV100R'
     c                   parm                    w_rrec
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for innhenting av varer fra håndterminal (batch)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_ht        begsr
      *
      *  Kaller program for kopiering av ekstern fil
      *
     c                   call      'HT700C'
     c                   parm                    w_feil
     c                   parm                    w_valg
      *
     c                   if        w_feil = '1'
     c                   goto      end_ht
     c                   endif
      *
      *  Kaller program avhengig av valg
      *
     c                   eval      d_firm = p_firm
     c                   eval      d_batc = lbhelu_batc
     c                   eval      d_numm = lbhelu_numm
      *
     c                   if        w_valg = '1'
     c                   call      'HT701R'
     c                   parm                    d_prec
     c                   goto      end_ht
     c                   endif
      *
     c                   if        w_valg = '2'
     c                   call      'HT702R'
     c                   parm                    d_prec
     c                   goto      end_ht
     c                   endif
      *
     c                   if        w_valg = '3'
     c                   call      'HT703R'
     c                   parm                    d_prec
     c                   goto      end_ht
     c                   endif
      *
     c     end_ht        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for innhenting av varer fra subliste
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_liste     begsr
      *
      *
     c                   eval      d_firm = p_firm
     c                   eval      d_batc = lbhelu_batc
     c                   eval      d_numm = lbhelu_numm
      *
     c                   call      'LU103R'
     c                   parm                    d_prec
      *
     c     end_liste     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Henter/Oppdatering av NUMMER-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hntnum        begsr
      *
     c                   eval      w_fell = laatbu
     c                   eval      w_ntyp = *blank
     c                   eval      w_fast = l_wsid
      *
      *  Kaller opp subprogram for henting av nummer fra NUMMER-REGISTER
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_ntyp
     c                   parm                    w_fast
     c                   parm                    w_numm
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for spørring på input
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sp_input      begsr
      *
      * Spørring lager
      *
     c                   if        w_afld = 'E2LAGE'
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    e2lage
     c                   parm                    e2latx
     c                   goto      sp_end
     c                   endif
      *
      * Spørring på type
      *
     c                   if        w_afld = 'E2TYPE'
     c                   call      'LU500R'
     c                   parm                    w_firm
     c                   parm                    e2type
     c                   parm                    w_besk
     c                   goto      sp_end
     c                   endif
      *
     c     sp_end        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å sjekke lager/hente lagertekst
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_lager    begsr
      *
     c                   call      'FÅ720R'
     c                   parm                    w_firm
     c                   parm                    e2lage
     c                   parm                    w_latx
     c                   parm                    w_adrs
     c                   parm                    w_ponr
     c                   parm                    w_pste
     c                   parm                    w_avde
     c                   parm                    b_feil
      *
     c     lager_end     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    p_retu
     c                   parm                    p_firm
     c                   parm                    p_batc
     c                   parm                    p_numm
      *
      *  Key for les av LAGER-KODE
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      *  Key for oppdatering av LAGERTELLEBUNT-HODE
      *
     c     lbhelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbhelu_batc
     c                   kfld                    lbhelu_numm
      *
      *  Legger inn startverdier i nøklene
      *
     c                   eval      w_firm = p_firm
     c                   eval      lbhelu_batc = p_batc
     c                   eval      lbhelu_numm = p_numm
      *
      *  Nullstiller returvalg
      *
     c                   eval      p_retu = *blank
      *
      *  Hent opplysninger fra LAGER-KODE
      *
     c     lstsl1_key    chain     lstsl1r
      *
     c                   endsr
