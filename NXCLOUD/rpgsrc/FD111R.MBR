     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FD111R
      * Beskrivelse . . : Ordre-linje, hurtigoppretting av tekstlinje
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       211002 HKO  Nytt program
      * 6.10  240609 BSA  Oppdatert med sporingsinformasjon
6.nu  * R6.30 030614 bsa  Endringer grunnet nummerutvidelser
8.01  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     ffodtlu    uf a e           k disk    rename(fodtpfr:fodtlur)
      *
6.10  * Definering av Local data area
6.10  *
6.10 d                uds
6.10 d l_user                911    920
      *
      * Definering av parametre
      *
     d p_firm          s                   like(fofirm)
6.nu d p_numm          s                   like(fonumm)
     d p_suff          s                   like(fosuff)
     d p_line          s                   like(fdline)
     d p_ltyp          s                   like(fdltyp)
     d p_text          s             70
      *
      * Definering av variable
      *
     d w_firm          s                   like(fdfirm)
6.nu d w_numm          s                   like(fdnumm)
     d w_suff          s                   like(fdsuff)
      *
      * Definering av konstanter
      *
     d lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer ordre-linje
      *
     c                   clear                   fodtlur
      *
     c                   eval      fdfirm = w_firm
     c                   eval      fdnumm = w_numm
     c                   eval      fdsuff = w_suff
     c                   eval      fdline = p_line
      *
     c                   eval      fdotyp = footyp
     c                   eval      fdlage = folage
      *
     c                   eval      fdkund = fokund
     c                   if        folkun <> 0
     c                   eval      fdkund = folkun
     c                   endif
      *
     c                   eval      fdtek1 = %subst(p_text:1:35)
     c                   eval      fdtek2 = %subst(p_text:36:35)
      *
     c                   if        faahoy = 1
     c     lo:up         xlate     fdtek1        fdtek1
     c     lo:up         xlate     fdtek2        fdtek2
     c                   endif
      *
     c                   eval      fdltyp = p_ltyp
      *
      * Skriver til ordre-linje
      *
6.10 c                   time                    fdodat
6.10 c                   time                    fdotim
6.10 c                   eval      fdoous = l_user
6.10 c                   time                    fdedat
6.10 c                   time                    fdetim
6.10 c                   eval      fdeusr = l_user
     c                   write     fodtlur
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
6.nu c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_line
     c                   parm                    p_ltyp
     c                   parm                    p_text
      *
      * Key for oppslag på ordre-hode
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
      *
      * Key for oppslag på kode-register
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      *
      * Henter parametre
      *
     c                   eval      w_firm = p_firm
6.nu c                   eval      w_numm = p_numm
     c                   eval      w_suff = p_suff
      *
      * Hent opplysninger fra ordre-hode-register
      *
     c     fohel1_key    chain     fohel1r
      *
      * Hent opplysninger fra kode-register
      *
     c     fstsl1_key    chain     fstsl1r
      *
     c                   endsr
