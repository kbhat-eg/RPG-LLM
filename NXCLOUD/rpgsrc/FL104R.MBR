     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FL104R
      * Beskrivelse . . : Kundeprosjekt, opprett fra ordre
      *
      *                   Oppretting fra ordre når kunde ikke er kjent
      *                   Ved kjent kunde kalles FL105R direkte fra ordre
      *
      *                   Returnerer opprettet kunde og kundeprosjekt
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       131005 HKO  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffkprpf    if   e           k disk
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
      *
     ffl104d    cf   e             workstn
      *
      * Definering av parametre
      *
     d p_firm          s                   like(flfirm)
     d p_kund          s                   like(flkund)
     d p_kpro          s                   like(flkpro)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d rkunl1_kund     s                   like(rkkund)
      *
      * Definering av variable
      *
     d w_firm          s                   like(flfirm)
     d w_kpro          s                   like(flkpro)
     d w_navn          s                   like(flnavn)
     d w_adr1          s                   like(fladr1)
     d w_adr2          s                   like(fladr2)
5.42  *
     d w_kund_kopi     s                   like(flkund)
     d w_kpro_kopi     s                   like(flkpro)
5.42  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      c1kund = 0
     c                   eval      c1knav = *blank
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   goto      avslutt
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   if        w_afld = 'C1KUND'
     c                   eval      c1kpro = 0
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    c1kund
     c                   parm                    c1knav
     c                   endif
     c                   goto      c1taga
     c                   endif
      *
      * Avbryter dersom kunde ikke er valgt
      *
     c                   if        c1kund = 0
     c                   goto      avslutt
     c                   endif
      *
      * Validering
      *
     c                   eval      c1knav = *blank
      *
     c                   eval      rkunl1_kund = c1kund
     c     rkunl1_key    chain     rkunl1
     c                   if        not %found
     c                   eval      *in32 = *on
     c                   goto      c1taga
     c                   endif
      *
     c                   eval      c1knav = rknavn
      *
      * Kaller vedlikeholdsprogram/opprettingsprogram
      *
     c                   call      'FL105R'
     c                   parm                    w_firm
     c                   parm                    c1kund
     c                   parm                    w_kpro
     c                   parm                    w_navn
     c                   parm                    w_adr1
     c                   parm                    w_adr2
     c                   parm                    w_kund_kopi
     c                   parm                    w_kpro_kopi
      *
      * Dersom kundeprosjekt er opprettet returneres kunde og kundeprosjekt
      *
     c                   if        w_kpro <> 0
     c                   eval      p_kund = c1kund
     c                   eval      p_kpro = w_kpro
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_kund
     c                   parm                    p_kpro
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
