     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JV101R
      * Beskrivelse . . : Vedlikehold av vareregister ny/endring
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       290296 HKO  Nytt program
      *       020197 HKO  Fjernet Vise-modus
      *       240397 HKO  Rettet feil m.h.p. oppretting og
      *                   kopiering av vare
      *       250397 HKO  Fjerner og oppretter nye søketekster
      *                   m.h.p. varetekst-1 og produsent-navn
      *       230497 HKO  Kaller fellesporgram for oppretting av
      *                   søketekster
      *                   Nye varer legges default i 'hold'
      *       170904 HKO  Viser info om slettet i Nobb
      *       310505 HKO  Fjernet priskode og sortimentskode, og lagt inn
      *                   dokumentasjonsindikator og flagg for best.vare
      *                   Blanker lev.varenr og EAN-nr ved oppretting av
      *                   vare-pris ved kopiering av vare
      *       130605 HKO  Endret parameter ved kall av JP500R
      *       120905 HKO  Lagt inn vedlikehold av EPIC-nr, UPC-nr og andre
      *                   strekkoder
      *                   Viser opprettet dato, endret dato og endret av
6.11  *       160709 bof  Utvidet med nye felter fra NOBB
6.12  *       200809 bof  Utvidet med kall til nye vedl.programmer
6.13  *       120510 bof  Lagt på eann på lev.pris inntil videre-skal utgå
6.14  *       041110 bof  Lagt på vedlikehold av pris-enhet på bilde 2
6.20  *       300511 bof  Lagt på skrivebeskyttelse på utvalgte felter ihht.
6.20  *                   AVALPF
6.30  *       110214 bof  Div. endringer på bilder ihht. XML 8.0 og BMpakke
6.31  *       270214 bof  endret kall til vedlikehold av pakning
6.32  *       140314 bof  Justert dato test for utlegg i skjerm
6.33  * 6.30  270314 bof  feilretting + test om varepris VA er utgått
6.34  * 6.30  020414 bof  Kall til JW100R endret til JW505R
6.35  * 6.30  280414 bsa  Prisenhet uteglemt for oppdatering.
6.36  * 6.30  160714 bof  Fjernet lev.varenr fra vare-pris
6.37  * 6.30  220814 bof  Mulighet til å se kladdebok fra EVR
6.38  * 6.30  161014 bkv  Kun duplikat LVAR hvis vare ikke utgått
6.39  * 6.30  211014 bsa  Lagt til F tast for NOBB info.
6.3x  * 6.30  040615 bof  utvidet parm i forb. med prgr. på betingelse
6.3a  * 6.30  020816 bof  Feilretting vedr. test på til prisdato.
6.3b  * 6.30  180317 bof  Lev.varenr må legges i JVLEPF (9000v-varer)
7.xx  * 7.00  030616 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 7.01  170220 hko  Rettet feil med at felter sperres ved oppretting
7.01  *                   av ny vare
8.01  *PRG2   140622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
8.02  *       290824 BOF  Dropper test på JVPROD, ikke oppdat fra nobb lenger
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjvdtl1    if   e           k disk    rename(jvdtpfr:jvdtl1r)
     fjvarl3    if   e           k disk    rename(jvarpfr:jvarl3r)
     fjvarl8    if   e           k disk    rename(jvarpfr:jvarl8r)
     fjvarld    if   e           k disk    rename(jvarpfr:jvarldr)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
6.14 fvenhl1    if   e           k disk    rename(venhpfr:venhl1r)
     fjprol1    if   e           k disk    rename(jpropfr:jprol1r)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
6.20 favall1    if   e           k disk    rename(avalpfr:avall1r)
6.30 fjvtxl1    if   e           k disk    rename(jvtxpfr:jvtxl1r)
6.30 fjkatl1    if   e           k disk    rename(jkatpfr:jkatl1r)
     fjstslu    uf   e           k disk    rename(jstspfr:jstslur)
     fjvarlu    uf a e           k disk    rename(jvarpfr:jvarlur)
     fjvdtlu    uf a e           k disk    rename(jvdtpfr:jvdtlur)
     fjvpkl1    if   e           k disk    rename(jvpkpfr:jvpkl1r)
     fjvpklu    uf a e           k disk    rename(jvpkpfr:jvpklur)
     fjvprl1    if   e           k disk    rename(jvprpfr:jvprl1r)
     fjvprlu    uf a e           k disk    rename(jvprpfr:jvprlur)
6.36 fjvlel1    if   e           k disk    rename(jvlepfr:jvlel1r)
6.36 fjvlelu    uf a e           k disk    rename(jvlepfr:jvlelur)
6.37 fanotl1    if   e           k disk    rename(anotpfr:anotl1r)
      *
     fjv101d    cf   e             workstn
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_vare          s                   like(jvvare)
     d p_valg          s              1  0
6.39 d p_nobb          s              8
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_fnav                951    980
      *
      * Definering av variable i nøkler
      *
     d jvarl1_vare     s                   like(jvvare)
     d jvdtl1_dvar     s                   like(jvdvar)
     d jvarl3_nobb     s                   like(jvnobb)
     d jvarl8_ldor     s                   like(jvldor)
     d jvarl8_lvar     s                   like(jvlvar)
     d jvarld_ean1     s                   like(jvean1)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
6.14 d venhl1_eenh     s                   like(vaeenh)
     d jprol1_prod     s                   like(jqprod)
     d jlevl1_ldor     s                   like(jlldor)
6.20 d avall1_apgm     s                   like(avapgm)
     d jvarlu_vare     s                   like(jvvare)
     d jvdtlu_dvar     s                   like(jvdvar)
     d jvpkl1_vare     s                   like(jvvare)
     d jvprl1_vare     s                   like(jvvare)
6.36 d jvlel1_lvnr     s                   like(jvlvnr)
6.30 d jvtxl1_tvar     s                   like(jvtvar)
6.30 d jvtxl1_tlin     s                   like(jvtlin)
6.30 d jkatl1_kate     s                   like(jkkate)
6.37 d anotl1_syst     s                   like(ausyst)
6.37 d anotl1_noid     s                   like(aunoid)
6.3b d jvlelu_lvnr     s                   like(jvlvnr)
6.3b d jvlelu_lldo     s                   like(jvlldo)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
     d b_inlr          s              1    inz(*off)
     d b_retur         s              1    inz(*off)
      *
     d w_firm          s              3  0
     d w_lety          s              1  0
8.01 d w_prgr          s              2
      *
     d w_pldo          s                   like(jvldor)
     d w_pnav          s                   like(jqnavn)
     d w_lnav          s                   like(jlnavn)
6.33 d w_dato          s                   like(jwodat)
6.37 d w_syst          s              4
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   exsr      xc2bld
     c                   exsr      xc3bld
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for bilde 1
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
6.20  *
6.20  * henter evt. oversikt over skrivebeskyttelse for C2BLD
6.20 c                   exsr      skrb_C2
      *
     c                   eval      jvarl1_vare = p_vare
     c     jvarl1_key    chain     jvarl1                             90
     c                   if        *in90 = *off
      *
     c                   eval      *in80 = jvstat = 4
      *
     c                   eval      c2vare = p_vare
     c                   eval      c2nobb = jvnobb
6.30 c                   eval      c2enob = jvenob
     c                   eval      c2modn = jvmodn
     c                   eval      c2ogrp = jvogrp
     c                   eval      c2hgrp = jvhgrp
     c                   eval      c2ugrp = jvugrp
     c                   eval      c2eogr = jveogr
     c                   eval      c2ehgr = jvehgr
     c                   eval      c2eugr = jveugr
     c                   eval      c2tek1 = jvtek1
     c                   eval      c2tek2 = jvtek2
     c                   eval      c2ete1 = jvete1
     c                   eval      c2ete2 = jvete2
     c                   eval      c2prod = jvprod
     c                   eval      c2pvar = jvpvar
     c                   eval      c2ldor = jvldor
     c                   eval      c2lvar = jvlvar
6.30 c*                  eval      c2leve = jvleve
     c                   eval      c2sort = jvsort
6.30 c                   eval      c2sldo = jvsldo
6.30 c                   eval      c2kate = jvkate
6.30 c*                  eval      c2utko = jvutko
6.30 c                   eval      c2penh = jvpenh
6.30 c                   eval      c2senh = jvsenh
6.30 c                   eval      c2lfor = jvlfor
6.30 c                   eval      c2hdat = jvhdat
6.30 c                   eval      c2tfro = jvtfro
6.30 c                   eval      c2hpse = jvhpse
6.30 c                   eval      c2uarb = jvuarb
6.30 c                   eval      c2gsts = jvgsts
     c                   eval      c2eusr = jveusr
     c                   eval      c2dpko = *blank
     c                   eval      c2dsko = 0
6.30  *
6.30  * henter varetype tekst
6.30  *
6.30 c                   select
6.30 c                   when      jvtype = 'ST'
6.30 c                   eval      c2vttx = 'Standard'
6.30 c                   when      jvtype = 'SS'
6.30 c                   eval      c2vttx = 'Sammensatt'
6.30 c                   when      jvtype = 'SP'
6.30 c                   eval      c2vttx = 'Spesialvare'
6.30 c                   when      jvtype = 'DS'
6.30 c                   eval      c2vttx = 'Display'
6.30 c                   other
6.30 c                   eval      c2vttx = *blank
6.30 c                   endsl
6.30  *
6.30  * henter kategori-ansv-navn
6.30  *
6.30 c                   eval      jkatl1_kate = jvkate
6.30 c     jkatl1_key    chain     jkatl1
6.30 c                   if        %found(jkatl1)
6.30 c                   eval      c2knav = jknavn
6.30 c                   endif
      *
6.32 c                   if        jvodat <> *loval
     c     *dmy          move      jvodat        c2odat
6.32 c                   endif
6.32 c                   if        jvedat <> *loval
     c     *dmy          move      jvedat        c2edat
6.32 c                   endif
      *
     c                   if        jvadat <> *loval
     c     *dmy          move      jvadat        c2adat
     c                   endif
      *
     c                   if        jvudat <> *loval
     c     *dmy          move      jvudat        c2udat
     c                   endif
      *
     c                   eval      jvdtl1_dvar = p_vare
     c     jvdtl1_key    chain     jvdtl1                             91
     c                   if        *in91 = *off
     c                   eval      c2dpko = jvdpko
     c                   eval      c2dsko = jvdsko
     c                   endif
      *
     c                   exsr      hent_kontr
     c                   exsr      sjekk_kladde
      *
     c                   else
      *
     c                   eval      c2nobb = 0
     c                   eval      c2modn = 0
     c                   eval      c2ogrp = 0
     c                   eval      c2hgrp = 0
     c                   eval      c2ugrp = 0
     c                   eval      c2utxt = *blank
     c                   eval      c2eogr = 0
     c                   eval      c2ehgr = 0
     c                   eval      c2eugr = 0
     c                   eval      c2eutx = *blank
     c                   eval      c2tek1 = *blank
     c                   eval      c2tek2 = *blank
     c                   eval      c2ete1 = *blank
     c                   eval      c2ete2 = *blank
     c                   eval      c2adat = 0
     c                   eval      c2udat = 0
     c                   eval      c2prod = 0
     c                   eval      c2pnav = *blank
     c                   eval      c2pvar = *blank
     c                   eval      c2ldor = 0
     c                   eval      c2lnav = *blank
     c                   eval      c2lvar = *blank
6.30 c*                  eval      c2mmk1 = *blank
6.30 c*                  eval      c2mlk1 = *blank
6.30 c*                  eval      c2mmk2 = *blank
6.30 c*                  eval      c2mlk2 = *blank
6.30 c*                  eval      c2leve = 0
6.30 c                   eval      c2penh = *blank
6.30 c                   eval      c2senh = *blank
6.30 c                   eval      c2lfor = 0
6.30 c                   eval      c2hdat = 0
6.30 c                   eval      c2tfro = 0
6.30 c                   eval      c2hpse = 0
6.30 c                   eval      c2uarb = 0
6.30 c                   eval      c2gsts = 0
6.30 c                   eval      c2sldo = 0
6.30 c                   eval      c2kate = 0
6.30 c                   eval      c2knav = *blank
     c                   eval      c2eusr = *blank
     c                   eval      c2odat = 0
     c                   eval      c2edat = 0
     c                   eval      c2dpko = *blank
     c                   eval      c2dsko = 0
      *
     c                   endif
      *
     c                   if        p_valg = 3
     c                   eval      c2vare = *blank
     c                   endif
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc = *on or *inkl = *on
     c                   goto      avslutt
     c                   when      *inkd = *on
     c                   exsr      spØrring
     c                   goto      c2tagb
     c                   endsl
      *
      * Validering av input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      c2tagb
     c                   endif
      *
      * Sender bildet opp på nytt dersom kontrolltekstene ikke er utfyllt
      *
     c                   if        c2utxt =  *blank or
     c                             c2ehgr <> 0      and
     c                             c2eutx =  *blank or
8.02 c*                             c2prod <> 0      and
8.02 c*                             c2pnav =  *blank or
     c                             c2lnav =  *blank
     c                   exsr      hent_kontr
     c                   goto      c2tagb
     c                   endif
      *
      * Oppdater register
      *
     c                   exsr      oppdater
      *
      * Vedlikehold av modul
      *
     c**                 if        *inkm = *on
     c**                 call      'JV102R'
     c**                 parm                    w_firm
     c**                 parm                    c2vare
     c**                 parm                    p_valg
     c**                 goto      c2tagb
     c**                 endif
      *
      * Vedlikehold av forpakningskode
      *
     c                   if        *inkn = *on
6.31 c                   call      'JW505R'
     c                   parm                    w_firm
     c                   parm                    c2vare
     c                   goto      c2tagb
     c                   endif
      *
      * Vedlikehold av pris
      *
     c                   if        *inkp = *on
     c                   call      'JX100R'
     c                   parm                    c2vare
     c                   goto      c2tagb
     c                   endif
      *
      * Vedlikehold av søketekst
      *
     c                   if        *inkq = *on
     c                   call      'JS100R'
     c                   parm                    w_firm
     c                   parm                    c2vare
     c                   goto      c2tagb
     c                   endif
      *
      * Vedlikehold av betingelser
      *
     c                   if        *inkr = *on
     c                   eval      w_pldo = 0
     c                   eval      w_lety = 0
6.3x c                   eval      w_prgr = *blank
     c                   call      'JP500R'
     c                   parm                    w_firm
     c                   parm                    c2vare
     c                   parm                    w_pldo
6.3x c                   parm                    w_prgr
     c                   parm                    w_lety
     c                   goto      c2tagb
     c                   endif
      *
      * Vise kampanjepriser
      *
     c                   if        *inks = *on
     c                   call      'JM510R'
     c                   parm                    w_firm
     c                   parm                    c2vare
     c                   parm                    c2tek1
     c                   goto      c2tagb
     c                   endif
      *
      * Vise modultekst/produktinformasjon
      *
     c                   if        *inkt = *on
     c                   call      'JU510R'
     c                   parm                    w_firm
     c                   parm                    c2vare
     c                   goto      c2tagb
     c                   endif
6.12  *
6.12  * Vedlikehold av alternative varenr
6.12  *
6.12 c                   if        *inku = *on
6.12 c                   call      'JV113R'
6.12 c                   parm                    c2nobb
6.12 c                   parm                    p_valg
6.12 c                   goto      c2tagb
6.12 c                   endif
6.12  *
6.12  * Vedlikehold av Avgifter
6.12  *
6.12 c                   if        *inkv = *on
6.12 c                   call      'JV114R'
6.12 c                   parm                    c2nobb
6.12 c                   parm                    p_valg
6.12 c                   goto      c2tagb
6.12 c                   endif
6.12  *
6.12  * Vedlikehold av Avgifter
6.12  *
6.12 c                   if        *inkw = *on
6.12 c                   call      'JV115R'
6.12 c                   parm                    c2nobb
6.12 c                   parm                    p_valg
6.12 c                   goto      c2tagb
6.12 c                   endif
6.39  *
6.39  * F23=NOBB informasjon
6.39  *
6.39 c                   if        *inkx = *on
6.39 c                   exsr      vis_nobb
6.39 c                   goto      c2tagb
6.39 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for bilde 2
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc3bld        begsr
     c                   eval      jvarl1_vare = p_vare
     c     jvarl1_key    chain     jvarl1                             90
     c                   if        *in90 = *off
      *
     c                   eval      *in80 = jvstat = 4
      *
     c                   eval      c3vare = p_vare
     c                   eval      c3nobb = jvnobb
     c                   eval      c3modn = jvmodn
     c                   eval      c3ogrp = jvogrp
     c                   eval      c3hgrp = jvhgrp
     c                   eval      c3ugrp = jvugrp
     c                   eval      c3tek1 = jvtek1
6.30 c                   eval      c3mmk1 = jvmmk1
6.30 c                   eval      c3mlk1 = jvmlk1
6.30 c                   eval      c3mmk2 = jvmmk2
6.30 c                   eval      c3mlk2 = jvmlk2
6.30 c                   eval      c3tono = jvtono
6.30 c                   eval      c3toeu = jvtoeu
6.30 c                   eval      c3unnr = jvunnr
6.30 c                   eval      c3fanr = jvfanr
6.30 c                   eval      c3fabs = jvfabs
6.30 c                   eval      c3embm = jvembm
6.30 c                   eval      c3ltx1 = %subst(jvltxt:1:50)
6.30 c                   eval      c3ltx2 = %subst(jvltxt:51:50)
      *
6.30 c*                  eval      c3ean1 = jvean1
6.30 c*                  eval      c3stre = jvstre
6.30 c*                  eval      c3doki = jvdoki
     c                   eval      c3eusr = jveusr
      *
     c     *dmy          move      jvodat        c3odat
     c     *dmy          move      jvedat        c3edat
      *
     c                   if        jvadat <> *loval
     c     *dmy          move      jvadat        c3adat
     c                   endif
      *
     c                   if        jvudat <> *loval
     c     *dmy          move      jvudat        c3udat
     c                   endif
      *
6.30  * leser 3 linjer av fritekst
6.30  *
6.30 c                   eval      c3fte1 = *blank
6.30 c                   eval      c3fte2 = *blank
6.30 c                   eval      c3fte3 = *blank
6.30 c                   eval      jvtxl1_tvar = jvvare
6.30 c                   eval      jvtxl1_tlin = 1
6.30 c     jvtxl1_key    chain     jvtxl1
6.30 c                   if        %found(jvtxl1)
6.30 c                   eval      c3fte1 = jvttxt
6.30 c                   eval      jvtxl1_tlin = 2
6.30 c     jvtxl1_key    chain     jvtxl1
6.30 c                   if        %found(jvtxl1)
6.30 c                   eval      c3fte2 = jvttxt
6.30 c                   eval      jvtxl1_tlin = 4
6.30 c     jvtxl1_key    chain     jvtxl1
6.30 c                   if        %found(jvtxl1)
6.30 c                   eval      c3fte2 = jvttxt
6.30 c                   endif
6.30 c                   endif
6.30 c                   endif
      *
     c                   exsr      hent_kontr
      *
     c                   else
     c                   goto      end_c3bld
      *
     c                   endif
      *
      *
     c     c3tagb        tag
      *
     c                   exfmt     c3bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc = *on or *inkl = *on
     c                   goto      avslutt
     c                   when      *inkd = *on
     c                   exsr      spØrring
     c                   goto      c3tagb
     c                   endsl
      *
      * Validering av input-felter
      *
6.30  * skal ikke oppdateres noe fra bilde 2 - heller ikke validering
6.30  * sjekker om prisenhet er gyldig.
6.30 c*                  eval      venhl1_eenh = c3penh
6.30 c*    venhl1_key    chain     venhl1
6.30 c*                  if        not %found(venhl1)
6.30 c*                  eval      *in31 = *on
6.30 c*                  goto      c3tagb
6.30 c*                  endif
6.30  *
6.30 c*                  if        c3ean1 <> 0
6.30  *
6.30 c*                  call      'JÅ700R'
6.30 c*                  parm                    c3ean1
6.30 c*                  parm                    b_retur
6.30 c*                  if        b_retur = *on
6.30 c*                  eval      *in44 = *on
6.30 c*                  goto      c3tagb
6.30 c*                  endif
6.30  *
6.30 c*                  eval      jvarld_ean1 = c3ean1
6.30 c*    jvarld_key    setll     jvarld
6.30 c*    jvarld_key    reade     jvarld                                 90
6.30 c*                  dow       *in90 = *off
6.30 c*                  if        jvvare <> c3vare
6.30 c*                  eval      *in45 = *on
6.30 c*                  goto      c3tagb
6.30 c*                  endif
6.30 c*    jvarld_key    reade     jvarld                                 90
6.30 c*                  enddo
6.30  *
6.30 c*                  endif
6.30  *
6.30  * Oppdater register
6.30  * skal ikke oppdateres noe fra bilde 2
6.30 c*                  clear                   jvarlur
6.30  *
6.30 c*                  eval      jvarlu_vare = c3vare
6.30 c*    jvarlu_key    chain     jvarlur                            90
6.30 c*                  if        %found(jvarlu)
6.30 c*                  eval      jvenob  = c3enob
6.30 c*                  eval      jvftxt  = c3fte1 +
6.30 c*                                      c3fte2 +
6.30 c*                                      c3fte3 +
6.30 c*                                      c3fte4
6.30 c*                  eval      jvean1 =  c3ean1
6.30 c*                  eval      jvstre =  c3stre
6.30 c*                  eval      jvdoki =  c3doki
6.30 c*                  eval      jvpenh =  c3penh
6.30  *
6.30 c*                  time                    jvetim
6.30 c*                  time                    jvedat
6.30 c*                  update    jvarlur
6.30 c*                  endif
      *
      * Vedlikehold av forpakningskode
      *
     c                   if        *inkn = *on
6.34 c                   call      'JW505R'
     c                   parm                    w_firm
     c                   parm                    c3vare
     c                   goto      c3tagb
     c                   endif
      *
      * Vedlikehold av pris
      *
     c                   if        *inkp = *on
     c                   call      'JX100R'
     c                   parm                    c3vare
     c                   goto      c3tagb
     c                   endif
      *
      * Vedlikehold av søketekst
      *
     c                   if        *inkq = *on
     c                   call      'JS100R'
     c                   parm                    w_firm
     c                   parm                    c3vare
     c                   goto      c3tagb
     c                   endif
      *
      * Vedlikehold av betingelser
      *
     c                   if        *inkr = *on
     c                   eval      w_pldo = 0
     c                   eval      w_lety = 0
6.3x c                   eval      w_prgr = *blank
     c                   call      'JP500R'
     c                   parm                    w_firm
     c                   parm                    c3vare
     c                   parm                    w_pldo
6.3x c                   parm                    w_prgr
     c                   parm                    w_lety
     c                   goto      c3tagb
     c                   endif
      *
      * Vise kampanjepriser
      *
     c                   if        *inks = *on
     c                   call      'JM510R'
     c                   parm                    w_firm
     c                   parm                    c3vare
     c                   parm                    c3tek1
     c                   goto      c3tagb
     c                   endif
      *
      * Vise modultekst/produktinformasjon
      *
     c                   if        *inkt = *on
     c                   call      'JU510R'
     c                   parm                    w_firm
     c                   parm                    c3vare
     c                   goto      c3tagb
     c                   endif
6.12  *
6.12  * Vedlikehold av alternative varenr
6.12  *
6.12 c                   if        *inku = *on
6.12 c                   call      'JV113R'
6.12 c                   parm                    c2nobb
6.12 c                   parm                    p_valg
6.12 c                   goto      c3tagb
6.12 c                   endif
6.12  *
6.12  * Vedlikehold av Avgifter
6.12  *
6.12 c                   if        *inkv = *on
6.12 c                   call      'JV114R'
6.12 c                   parm                    c2nobb
6.12 c                   parm                    p_valg
6.12 c                   goto      c3tagb
6.12 c                   endif
6.12  *
6.12  * Vedlikehold av Avgifter
6.12  *
6.12 c                   if        *inkw = *on
6.12 c                   call      'JV115R'
6.12 c                   parm                    c2nobb
6.12 c                   parm                    p_valg
6.12 c                   goto      c3tagb
6.12 c                   endif
      *
     c     end_c3bld     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av kontroll-informasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_kontr    begsr
      *
     c                   eval      c2utxt = *blank
     c                   eval      c3utxt = *blank
     c                   eval      c2eutx = *blank
     c                   eval      c2pnav = *blank
     c                   eval      c2lnav = *blank
      *
     c                   eval      vugrl1_uogr = c2ogrp
     c                   eval      vugrl1_uhgr = c2hgrp
     c                   eval      vugrl1_uugr = c2ugrp
     c     vugrl1_key    chain     vugrl1                             91
     c                   if        *in91 = *off
     c                   eval      c2utxt = vgutxt
     c                   eval      c3utxt = vgutxt
     c                   endif
      *
     c                   eval      vugrl1_uogr = c2eogr
     c                   eval      vugrl1_uhgr = c2ehgr
     c                   eval      vugrl1_uugr = c2eugr
     c     vugrl1_key    chain     vugrl1                             91
     c                   if        *in91 = *off
     c                   eval      c2eutx = vgutxt
     c                   endif
      *
     c                   eval      jprol1_prod = c2prod
     c     jprol1_key    chain     jprol1                             91
     c                   if        *in91 = *off
     c                   eval      c2pnav = jqnavn
     c                   endif
      *
     c                   eval      jlevl1_ldor = c2ldor
     c     jlevl1_key    chain     jlevl1                             91
     c                   if        *in91 = *off
     c                   eval      c2lnav = jlnavn
     c                   endif
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sjekke om kladdebok finnes
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_kladde  begsr
      *
6.37 c                   eval      c2klad = *blank
6.37 c                   eval      *in75  = *off
6.37 c                   eval      anotl1_syst = 'VVAR'
6.37 c                   eval      anotl1_noid = jvvare
6.37 c     anotl1_key    chain     anotl1
6.37 c                   if        %found
6.37 c                   eval      c2klad = 'J'
6.37 c                   eval      *in75 = *on
6.37 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørringer (F4)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
     c                   if        w_afld = 'C2OGRP'
     c                   eval      c2hgrp = 0
     c                   eval      c2ugrp = 0
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    c2ogrp
     c                   parm                    c2utxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C2HGRP'
     c                   eval      c2ugrp = 0
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    c2ogrp
     c                   parm                    c2hgrp
     c                   parm                    c2utxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C2UGRP'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    c2ogrp
     c                   parm                    c2hgrp
     c                   parm                    c2ugrp
     c                   parm                    c2utxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C2EOGR'
     c                   eval      c2ehgr = 0
     c                   eval      c2eugr = 0
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    c2eogr
     c                   parm                    c2eutx
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C2EHGR'
     c                   eval      c2eugr = 0
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    c2eogr
     c                   parm                    c2ehgr
     c                   parm                    c2eutx
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C2EUGR'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    c2eogr
     c                   parm                    c2ehgr
     c                   parm                    c2eugr
     c                   parm                    c2eutx
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C2PROD'
     c                   call      'JQ500R'
     c                   parm                    c2prod
     c                   parm                    w_pnav
     c                   eval      c2pnav = w_pnav
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C2LDOR'
     c                   call      'JL500R'
     c                   parm                    c2ldor
     c                   parm                    w_lnav
     c                   eval      c2lnav = w_lnav
     c                   leavesr
     c                   endif
6.37  *
6.37 c                   if        w_afld = 'C2KLAD' and
     c                             c2klad = 'J'
6.37 c                   eval      w_syst = 'VVAR'
6.37 c                   call      'AU100R'
6.37 c                   parm                    w_firm
6.37 c                   parm                    w_syst
6.37 c                   parm                    jvvare
6.37 c                   leavesr
6.37 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
      * Sjekk på NOBB-nummer
      *
     c                   if        c2nobb <> 0
      *
     c                   if        c2nobb < 10000000
     c                   eval      *in31 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   eval      jvarl3_nobb = c2nobb
     c     jvarl3_key    setll     jvarl3
     c     jvarl3_key    reade     jvarl3                                 90
     c                   dow       *in90 = *off
6.38 c                   if        jvvare <> c2vare and jvudat = *loval
     c                   eval      *in32 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c     jvarl3_key    reade     jvarl3                                 90
     c                   enddo
      *
     c                   endif
      *
      * Sjekk på varegruppe
      *
     c                   eval      vugrl1_uogr = c2ogrp
     c                   eval      vugrl1_uhgr = c2hgrp
     c                   eval      vugrl1_uugr = c2ugrp
     c     vugrl1_key    chain     vugrl1                             34
     c                   if        *in34 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      c2utxt = vgutxt
      *
      * Sjekk på egen varegruppe
      *
     c                   if        c2eogr <> 0 or
     c                             c2ehgr <> 0 or
     c                             c2eugr <> 0
     c                   eval      vugrl1_uogr = c2eogr
     c                   eval      vugrl1_uhgr = c2ehgr
     c                   eval      vugrl1_uugr = c2eugr
     c     vugrl1_key    chain     vugrl1                             36
     c                   if        *in36 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      c2eutx = vgutxt
     c                   endif
      *
      * Sjekk på varetekst 1
      *
     c                   if        c2tek1 = *blank
     c                   eval      *in37 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      * Sjekk på egen varetekst
      *
     c                   if        c2ete2 <> *blank and
     c                             c2ete1 = *blank
     c                   eval      *in39 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      * Sjekk på produsent
      *
     c                   if        c2prod <> 0
     c                   eval      jprol1_prod = c2prod
8.02 c     jprol1_key    chain     jprol1
8.02 c                   if        not %found(jprol1)
8.02 c*                   eval      b_feil = *on
8.02 c*                   leavesr
8.02 c                   eval      c2pnav = *Blank
     c                   endif
     c                   eval      c2pnav = jqnavn
     c                   endif
      *
      * Sjekk på leverandør
      *
     c                   if        c2ldor = 0
     c                   eval      *in41 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   if        c2ldor <> 0
     c                   eval      jlevl1_ldor = c2ldor
     c     jlevl1_key    chain     jlevl1                             42
     c                   if        *in42 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      c2lnav = jlnavn
     c                   endif
      *
     c                   if        c2ldor <> 0 and
     c                             c2lvar <> *blank
     c                   eval      jvarl8_ldor = c2ldor
     c                   eval      jvarl8_lvar = c2lvar
     c     jvarl8_key    setll     jvarl8
     c     jvarl8_key    reade     jvarl8                                 90
     c                   dow       *in90 = *off
6.38 c                   if        jvvare <> c2vare and jvudat = *loval
     c                   eval      *in43 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c     jvarl8_key    reade     jvarl8                                 90
     c                   enddo
     c                   endif
      *
      * Sjekk på EAN-nummer (nytt)
      *
      * Sjekk på egen priskode
      *
     c                   if        c2dpko <> *blank and
     c                             c2dpko <> 'N' and
     c                             c2dpko <> 'B'
     c                   eval      *in48 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      * Sjekk på skaffevarekode
      *
     c                   if        c2dsko <> 0 and
     c                             c2dsko <> 1
     c                   eval      *in49 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      * Sjekk på gyldige datoer
      *
     c                   if        c2adat <> 0
     c     *dmy          test (d)                c2adat                 54
     c                   if        *in54 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        c2udat <> 0
     c     *dmy          test (d)                c2udat                 55
     c                   if        *in55 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdater      begsr
      *
      * Oppdatering av vareregister
      *
     c                   clear                   jvarlur
      *
     c                   eval      jvarlu_vare = c2vare
     c     jvarlu_key    chain     jvarlur                            90
      *
     c                   eval      jvnobb = c2nobb
     c                   eval      jvmodn = c2modn
     c                   eval      jvogrp = c2ogrp
     c                   eval      jvhgrp = c2hgrp
     c                   eval      jvugrp = c2ugrp
     c                   eval      jveogr = c2eogr
     c                   eval      jvehgr = c2ehgr
     c                   eval      jveugr = c2eugr
     c                   eval      jvtek1 = c2tek1
     c                   eval      jvtek2 = c2tek2
     c                   eval      jvete1 = c2ete1
     c                   eval      jvete2 = c2ete2
     c                   eval      jvprod = c2prod
     c                   eval      jvpvar = c2pvar
     c                   eval      jvldor = c2ldor
     c                   eval      jvlvar = c2lvar
     c                   eval      jvsort = c2sort
6.30 c*                  eval      jvutko = c2utko
6.30 c*                  eval      jvmmk1 = c2mmk1
6.30 c*                  eval      jvmlk1 = c2mlk1
6.30 c*                  eval      jvmmk2 = c2mmk2
6.30 c*                  eval      jvmlk2 = c2mlk2
6.30 c*                  eval      jvstat = 0
6.35 c                   eval      jvpenh = c2penh
     c                   eval      jvadat = *loval
     c                   eval      jvudat = *loval
     c                   eval      jveusr = l_user
      *
     c                   if        c2adat <> 0
     c     *dmy          move      c2adat        jvadat
     c                   endif
      *
     c                   if        c2udat <> 0
     c     *dmy          move      c2udat        jvudat
     c                   endif
      *
     c                   time                    jvetim
     c                   time                    jvedat
      *
     c                   if        *in90 = *off
     c                   update    jvarlur
     c                   else
     c                   exsr      oppr_vare
     c                   endif
      *
      * Oppdatering av vare-detalj
      *
     c                   clear                   jvdtlur
      *
     c                   eval      jvdtlu_dvar = jvvare
     c     jvdtlu_key    chain     jvdtlur                            91
     c                   eval      jvdpko = c2dpko
     c                   eval      jvdsko = c2dsko
     c                   if        *in91 = *off
     c                   update    jvdtlur
     c                   else
     c                   eval      jvdfir = w_firm
     c                   eval      jvdvar = jvvare
     c                   eval      jvdhko = 1
     c                   write     jvdtlur
     c                   endif
6.3b  *
6.3b  * nivå vare-leverandør må opprettes på 9000-varer
6.3b c                   if        jvvare >= '90000000'
6.3b c                   clear                   jvlelur
6.3b c                   eval      jvlelu_lvnr = jvvare
6.3b c                   eval      jvlelu_lldo = jvldor
6.3b c     jvlelu_key    chain     jvlelu
6.3b c                   eval      jvllva = jvlvar
6.3b c                   eval      jvlvei = 1
6.3b c                   time                    jvleda
6.3b c                   time                    jvleti
6.3b c                   eval      jvleus = l_user
6.3b c                   if        %found(jvlelu)
6.3b c                   update    jvlelur
6.3b c                   else
6.3b c                   eval      jvlvnr = jvvare
6.3b c                   eval      jvlldo = jvldor
6.3b c                   time                    jvloda
6.3b c                   time                    jvloti
6.3b c                   eval      jvlous = l_user
6.3b c                   write     jvlelur
6.3b c                   endif
6.3b c                   endif
      *
      *
      * Oppdatering av søketekster
      *
     c                   call      'JV790R'
     c                   parm                    w_firm
     c                   parm                    jvvare
     c                   parm                    b_inlr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting av vare.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppr_vare     begsr
      *
     c                   time                    jvodat
      *
      * Henter og oppdaterer neste varenummer fra status-register.
      *
     c     jstslu_key    chain     jstslur                            91
     c                   eval      jaavsi = jaavsi + 1
     c                   movel     jaavsi        jvvare
     c                   movel     jaavsi        c2vare
     c                   update    jstslur
      *
      * Oppretter vare
      *
     c                   write     jvarlur
      *
     c                   if        p_valg <> 3
     c                   eval      p_vare = c2vare
     c                   leavesr
     c                   endif
      *
      * Dersom kopiering, kopieres vare-pakning og vare-pris fra
      * kopierende vare.
      *
     c                   eval      jvpkl1_vare = p_vare
     c     jvpkl1_key    setll     jvpkl1
     c     jvpkl1_key    reade     jvpkl1                                 91
     c                   dow       *in91 = *off
     c                   eval      jwvare = c2vare
     c                   eval      jwrsts = 1
     c                   write     jvpklur
     c     jvpkl1_key    reade     jvpkl1                                 91
     c                   enddo
6.36  * vare-leverandør
6.36 c                   eval      jvlel1_lvnr = p_vare
6.36 c     jvlel1_key    setll     jvlel1
6.36 c     jvlel1_key    reade     jvlel1
6.36 c                   dow       not %eof(jvlel1)
6.36 c                   eval      jvlvnr = c2vare
6.36 c                   eval      jvllva = *blank
6.36 c                   write     jvlelur
6.36 c     jvlel1_key    reade     jvlel1
6.36 c                   enddo
      *
     c                   eval      jvprl1_vare = p_vare
     c     jvprl1_key    setll     jvprl1
     c     jvprl1_key    reade     jvprl1                                 91
     c                   dow       *in91 = *off
6.33 c                   if        (jxtdat =  *loval) or
6.33 c                             (jxtdat <> *loval and
6.3a c                             jxtdat >= w_dato)
     c                   eval      jxvare = c2vare
6.36 c*                  eval      jxlvar = *blank
6.13 c                   eval      jxeann = 0
     c                   eval      jxrsts = 1
     c                   write     jvprlur
6.33 c                   endif
     c     jvprl1_key    reade     jvprl1                                 91
     c                   enddo
      *
      * Flytter nytt varenummer til paramter
      *
     c                   eval      p_vare = c2vare
      *
     c                   endsr
6.20  *
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  * Subrutine for lage oversikt over felter som skal skrivebeskyttes
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  *
6.20 c     skrb_c2       begsr
6.20  *
6.20 c                   eval      *in60  = *off
6.20 c                   eval      *in61  = *off
6.20 c                   eval      *in62  = *off
6.20 c                   eval      *in63  = *off
6.20 c                   eval      *in64  = *off
6.20 c                   eval      *in65  = *off
6.20 c                   eval      *in66  = *off
6.20 c                   eval      *in67  = *off
6.20 c                   eval      *in68  = *off
6.20 c                   eval      *in69  = *off
6.20 c                   eval      *in70  = *off
6.20 c                   eval      *in71  = *off
6.20 c                   eval      *in72  = *off
6.20 c                   eval      *in73  = *off
6.20 c                   eval      *in74  = *off
6.20  *
7.01 c                   if        p_vare  = '00000000' or
6.20 c                             p_vare >= '90000000'
6.20 c                   leavesr
6.20 c                   endif
6.20  *
6.20  * Firmastyrt validering av utvalgte felter
6.20  *
6.20 c                   eval      avall1_apgm = 'JV101R'
6.20 c     avall1_key    setll     avall1
6.20 c     avall1_key    reade     avall1
6.20 c                   dow       not %eof(avall1)
6.20 c                   if        avaflt = 'JVNOBB'  and
6.20 c                             avabes = 1
6.20 c                   eval      *in60  = *on
6.20 c                   endif
6.20 c                   if        avaflt = 'JVMODN'  and
6.20 c                             avabes = 1
6.20 c                   eval      *in61  = *on
6.20 c                   endif
6.20 c                   if        avaflt = 'JVOGRP'  and
6.20 c                             avabes = 1
6.20 c                   eval      *in62  = *on
6.20 c                   endif
6.20 c                   if        avaflt = 'JVHGRP'  and
6.20 c                             avabes = 1
6.20 c                   eval      *in63  = *on
6.20 c                   endif
6.20 c                   if        avaflt = 'JVUGRP'  and
6.20 c                             avabes = 1
6.20 c                   eval      *in64  = *on
6.20 c                   endif
6.20 c                   if        avaflt = 'JVTEK1'  and
6.20 c                             avabes = 1
6.20 c                   eval      *in65  = *on
6.20 c                   endif
6.20 c                   if        avaflt = 'JVTEK2'  and
6.20 c                             avabes = 1
6.20 c                   eval      *in66  = *on
6.20 c                   endif
6.20 c                   if        avaflt = 'JVPROD'  and
6.20 c                             avabes = 1
6.20 c                   eval      *in67  = *on
6.20 c                   endif
6.20 c                   if        avaflt = 'JVLDOR'  and
6.20 c                             avabes = 1
6.20 c                   eval      *in68  = *on
6.20 c                   endif
6.20 c                   if        avaflt = 'JVPVAR'  and
6.20 c                             avabes = 1
6.20 c                   eval      *in69  = *on
6.20 c                   endif
6.20 c                   if        avaflt = 'JVLVAR'  and
6.20 c                             avabes = 1
6.20 c                   eval      *in70  = *on
6.20 c                   endif
6.20 c                   if        avaflt = 'JVMMK1'  and
6.20 c                             avabes = 1
6.20 c                   eval      *in71  = *on
6.20 c                   endif
6.20 c                   if        avaflt = 'JVMLK1'  and
6.20 c                             avabes = 1
6.20 c                   eval      *in72  = *on
6.20 c                   endif
6.20 c                   if        avaflt = 'JVMMK2'  and
6.20 c                             avabes = 1
6.20 c                   eval      *in73  = *on
6.20 c                   endif
6.20 c                   if        avaflt = 'JVMLK2'  and
6.20 c                             avabes = 1
6.20 c                   eval      *in74  = *on
6.20 c                   endif
6.20 c     avall1_key    reade     avall1
6.20 c                   enddo
6.20  *
6.20 c                   endsr
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  * Subrutine for lage oversikt over felter som skal skrivebeskyttes
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  *
6.20 c     skrb_c3       begsr
6.20  *
6.20 c                   eval      *in60  = *off
6.20 c                   eval      *in61  = *off
6.20 c                   eval      *in62  = *off
6.20 c                   eval      *in63  = *off
6.20 c                   eval      *in64  = *off
6.20 c                   eval      *in65  = *off
6.20  *
7.01 c                   if        p_vare  = '00000000' or
6.20 c                             p_vare >= '90000000'
6.20 c                   leavesr
6.20 c                   endif
6.20  *
6.20  * Firmastyrt validering av utvalgte felter
6.20  *
6.20 c                   eval      avall1_apgm = 'JV101R'
6.20 c     avall1_key    setll     avall1
6.20 c     avall1_key    reade     avall1
6.20 c                   dow       not %eof(avall1)
6.20 c                   if        avaflt = 'JVPENH'  and
6.20 c                             avabes = 1
6.20 c                   eval      *in60  = *on
6.20 c                   endif
6.20 c                   if        avaflt = 'JVENOB'  and
6.20 c                             avabes = 1
6.20 c                   eval      *in61  = *on
6.20 c                   endif
6.20 c                   if        avaflt = 'JVFTE1'  and
6.20 c                             avabes = 1
6.20 c                   eval      *in62  = *on
6.20 c                   endif
6.20 c                   if        avaflt = 'JVFTE2'  and
6.20 c                             avabes = 1
6.20 c                   eval      *in63  = *on
6.20 c                   endif
6.20 c                   if        avaflt = 'JVFTE3'  and
6.20 c                             avabes = 1
6.20 c                   eval      *in64  = *on
6.20 c                   endif
6.20 c                   if        avaflt = 'JVFTE4'  and
6.20 c                             avabes = 1
6.20 c                   eval      *in65  = *on
6.20 c                   endif
6.20 c     avall1_key    reade     avall1
6.20 c                   enddo
6.20  *
6.20 c                   endsr
      *
6.39  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.39  * Vis NOBB informasjon for varen. Samme prinsipp som preview av utskrifter
6.39  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.39  *
6.39 c     vis_nobb      begsr
6.39 c
6.39 c                   eval      p_nobb = %char(c2nobb)
6.39 c                   call      'AP633R'
6.39 c                   parm                    p_nobb
6.39  *
6.39 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_valg
      *
      * Key for oppslag på vare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for oppslag på vare med NOBB-nummer
      *
     c     jvarl3_key    klist
     c                   kfld                    jvarl3_nobb
      *
      * Key for oppslag på vare med lev./lev.varenummer
      *
     c     jvarl8_key    klist
     c                   kfld                    jvarl8_ldor
     c                   kfld                    jvarl8_lvar
      *
      * Key for oppslag på vare med EAN-nummer
      *
     c     jvarld_key    klist
     c                   kfld                    jvarld_ean1
      *
      * Key for oppslag på vare-detalj
      *
     c     jvdtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jvdtl1_dvar
      *
      * Key for oppslag mot undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag mot produsent
      *
     c     jprol1_key    klist
     c                   kfld                    jprol1_prod
      *
      * Key for oppslag mot leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
6.20  *
6.20  * Key for file : Firmastyrt validering
6.20  *
6.20 c     avall1_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    avall1_apgm
      *
      * Key for oppdatering av kode/status
      *
     c     jstslu_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppdatering av vare
      *
     c     jvarlu_key    klist
     c                   kfld                    jvarlu_vare
      *
      * Key for oppdatering av vare-detalj
      *
     c     jvdtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jvdtlu_dvar
      *
      * Key for oppslag på vare-pakning
      *
     c     jvpkl1_key    klist
     c                   kfld                    jvpkl1_vare
      *
      * Key for oppslag på vare-pris
      *
     c     jvprl1_key    klist
     c                   kfld                    jvprl1_vare
6.36  *
6.36  * Key for oppslag på vare-leverandør
6.36  *
6.36 c     jvlel1_key    klist
6.36 c                   kfld                    jvlel1_lvnr
6.3b  *
6.3b  * Key for skriv på vare-leverandør
6.3b  *
6.3b c     jvlelu_key    klist
6.3b c                   kfld                    jvlelu_lvnr
6.3b c                   kfld                    jvlelu_lldo
6.14  *
6.14  * Key for les på enhet
6.14  *
6.14 c     venhl1_key    klist
6.14 c                   kfld                    w_firm
6.14 c                   kfld                    venhl1_eenh
6.30  *
6.30  * Key for les på varefritekst
6.30  *
6.30 c     jvtxl1_key    klist
6.30 c                   kfld                    jvtxl1_tvar
6.30 c                   kfld                    jvtxl1_tlin
6.30  *
6.30  * Key for les på kategoriansvarlig
6.30  *
6.30 c     jkatl1_key    klist
6.30 c                   kfld                    jkatl1_kate
6.37  *
6.37  * Key for oppslag på notat
6.37  *
6.37 c     anotl1_key    klist
6.37 c                   kfld                    w_firm
6.37 c                   kfld                    anotl1_syst
6.37 c                   kfld                    anotl1_noid
      *
      *
      *
     c                   eval      w_firm = p_firm
6.33 c                   time                    w_dato
      *
     c                   endsr
