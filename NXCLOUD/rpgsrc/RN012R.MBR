     h option(*nodebugio) datedit(*dmy)
     h decedit('0.')
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASNAVIOKO
      * Program . . . . : RN002R
      * Beskrivelse . . : Leser linjer fra BOVF til NAV transaksjonsfil
      *
      * Spesialversjon. : OBS!!! Det finnes et "søsterprgram" RN002R
      *                   Endringer må også gjøres der
      * Tilpasset for . :
      *
      * ----- ------ ---- -------------------------------------------------
      *       240921 ask  Nytt program
8.01  *       290921 ask  Dropper overføring av poster med bilagssum = 0
8.11  *       160623 mst  Fjerner salgsordrereferanse til NAV/BC
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.11 f*bomkl1    if   e           k disk    rename(bomkpfr:bomkl1r)
8.11 fbomkl1    if   e           k disk    rename(bovfpfr:bomkl1r)
     fibutst    o    e           k disk    rename(ibutst:ibutstr)
     fsohel1    if   e           k disk    rename(sohepfr:sohel1r)
     fikonir    if   e           k disk    rename(ikonst:ikonstr)
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
     d l_user                911    920
      *
      * Definering av variabler i nøkler
      *
     d bomkl1_peri     s                   like(bfperi)
     d sohel1_aarr     s                   like(soaarr)
     d sohel1_binr     s                   like(sobinr)
     d ikonir_bfra     s                   like(ikbfra)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_bilk          s              2  0
     d w_kidr          s              9  0
     d w_aarr          s              4  0
     d w_kidd          s                   like(sokidd)
     d w_kida          s              9
     d w_obil          s                   like(laabil)
     d w_pos           s              2  0
      *
     c/Exec SQL
     c+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR,
     c+             commit=*none
     c/End-Exec
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram - leser poster fra bomkL1 og skriver til IBUTST
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      bomkl1_peri = 0
     c     bomkl1_key    setll     bomkl1
     c                   read      bomkl1
     c                   dow       not %eof
8.01  *
8.01  * Sjekker om "0" - bilag
8.01  *
8.01 c                   if        bfbelp = 0
8.01 c                   goto      les_neste
8.01 c                   endif
      *
      * Konverterer bilagskoder for øreavrunding
      *
     c                   if        bfbilk <> w_obil
     c                   eval      w_bilk = bfbilk
     c                   else
     c                   eval      ikonir_bfra = w_bilk
     c     ikonir_key    chain     ikonir
     c                   if        %found
     c                   eval      bfbilk = ikbtil
     c                   endif
     c                   endif
      *
     c                   exsr      finn_kid
     c                   eval      w_kida = %EditC(w_kidr :'X')
      *
     c                   eval      ibfirm = bffirm
     c                   eval      ibbiln = bfbiln
     c                   eval      ibbdat = bfbdat
     c                   eval      ibfdat = bffdat
     c                   eval      ibbilk = bfbilk
     c                   eval      ibtext = bftext
     c                   eval      ibdavd = bfdavd
     c                   eval      ibdkto = bfdkto
     c                   eval      ibdsps = bfdsps
     c                   eval      ibdmom = bfdmom
     c                   eval      ibcavd = bfcavd
     c                   eval      ibckto = bfckto
     c                   eval      ibcsps = bfcsps
     c                   eval      ibcmom = bfcmom
     c                   if        bfckto > 0
     c                   eval      ibbelp = bfbelp * -1
     c                   else
     c                   eval      ibbelp = bfbelp
     c                   endif
     c                   eval      ibmvag = bfmvag
     c                   eval      ibkund = bfkund
     c                   eval      ibkref = bfkref
8.11 c                   eval      ibkref = 0
     c                   eval      ibvalk = bfvalk
     c                   eval      ibvalb = bfvalb
     c                   eval      ibproj = bfproj
     c                   eval      ibakti = bfakti
     c                   eval      ibkidi = w_kidd
     c                   eval      ibkidr = w_kida
     c                   eval      ibtype = 'FAKT'
     c                   eval      ibbetb = 0
     c                   eval      ibodat = w_date
     c                   eval      ibotim = w_time
     c                   eval      ibousr = l_user
      *
     c                   eval      w_pos = %scan('/' : bftext)
     c                   if        w_pos > 0
     c                   eval      ibbong = %subst(ibtext:1:w_pos-1)
     c                   else
     c                   eval      ibbong = *blank
     c                   endif
      *
     c                   write     ibutstr
      *
8.01 c     les_neste     tag
     c                   read      bomkl1
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å hente kid-informasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_kid      begsr
      *
     c                   eval      w_kidd = *blank
     c                   eval      w_kidr = 0
      *
      * Fra BREFPF
      *
     c/Exec SQL
     c+  select bxkidr
     c+    into :w_kidd
     c+  from brefpf
     c+    where bxfirm = :w_firm and bxbiln = :bfbiln
     c+
     c/End-Exec
     c                   if        w_kidd <> *blank
     c                   goto      end_kid
     c                   endif
      *
      * Fra SOHEPF med bilagsnummer
      *
     c                   eval      w_aarr = %subdt(bfbdat:*years)
     c                   eval      sohel1_aarr = w_aarr
     c                   eval      sohel1_binr = bfbiln
      *
     c     sohel1_key    setll     sohel1
     c     sohel1_key    reade     sohel1
     c                   dow       not %eof
     c                   if        sokidd <> *blanks or
     c                             sokidr <> 0
     c                   eval      w_kidd = sokidd
     c                   eval      w_kidr = sokidr
     c                   goto      end_kid
     c                   endif
     c     sohel1_key    reade     sohel1
     c                   enddo
      *
     c                   if        w_kidd <> *blank
     c                   goto      end_kid
     c                   endif
      *
      * Fra SOHEPF med kreditreferanse
      *
     c                   eval      w_aarr = %subdt(bfbdat:*years)
     c                   eval      sohel1_aarr = w_aarr
     c                   eval      sohel1_binr = bfkref
      *
     c     sohel1_key    setll     sohel1
     c     sohel1_key    reade     sohel1
     c                   dow       not %eof
     c                   if        sokidd <> *blanks or
     c                             sokidr <> 0
     c                   eval      w_kidd = sokidd
     c                   eval      w_kidr = sokidr
     c                   goto      end_kid
     c                   endif
     c     sohel1_key    reade     sohel1
     c                   enddo
      *
     c     end_kid       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på posteringsfil
      *
     c     bomkl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bomkl1_peri
      *
      * Key for oppslag på fakturanummer
      *
     c     sohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sohel1_aarr
     c                   kfld                    sohel1_binr
      *
      * Key for les på konverteringstabell
      *
     c     ikonir_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ikonir_bfra
      *
      * Key for les på status-register
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Klargjør for øreavrundings konvertering
      *
     c                   eval      w_firm = l_firm
     c                   time                    w_date
     c                   time                    w_time
     c     lstsl1_key    chain     lstsl1
     c                   eval      w_obil = laabil
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
