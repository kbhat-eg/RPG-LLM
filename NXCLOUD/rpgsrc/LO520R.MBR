     H/TITLE  ASLAGR: Spørring mot bestillings-register, Varenummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  System. . . . . : ASLAGR                                       *
      *  Program . . . . : LO520R                                       *
      *  Beskrivelse . . : Spørring mot bestilling/ Varenummer          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  Spesialversjon. :                                              *
      *  Tilpasset for . :                                              *
      *                                                                 *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.*
      *  --------- ------  ---------------------------------------------*
6.nu  *  6.30   300514 bof Behandlet mpt. nummerutvidelser              *
7.01  *  7.01  1202121 ask Endret skjermbildet med tanke på GU visning  *
      *                                                                 *
      *                                                                 *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     Frlevl1    IF   E           K DISK    RENAME(RLEVPFR:rlevl1R)
      *
     FLOHEL1    IF   E           K DISK    RENAME(LOHEPFR:LOHEL1R)
     FLODTLV    IF   E           K DISK    RENAME(LODTPFR:LODTLVR)
      *
     FLO520D    CF   E             WORKSTN SFILE(D1SFL:SRRN02)
      *
     D                UDS
     D  l_firm               944    946  0
     D  l_fnav               951    980
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Kaller opp subrutine for å vise ordre-linjer                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C                   MOVE      WPVARE        D2VARE
     C                   MOVE      WPTEK1        D2TEK1
      *
     C                   EXSR      XD1BLD
      *
     C                   GOTO      XSLUTT
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     XSLUTT        TAG
      *
     C                   SETON                                        LR
     C                   RETURN
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  D1 Behandle bilde for VISE VARELINJER                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     CSR   XD1BLD        BEGSR
      *
      *  Hent opplysninger fra BESTILLING-LINJE-REGISTER
      *
     C                   MOVE      *BLANK        PRIENH
     C     PRIKEY        SETLL     LODTLV
     C                   EXSR      XCLR02
     C                   EXSR      XCRT02
      *
      *  Send Alt
      *
     C     D2TAGA        TAG
     C                   WRITE     D2CMD
      *
      *  Bare data
      *
     C     D2TAGB        TAG
     C                   EXSR      XDSP02
      *
      *  F1=Vise bla-alternativer
      *
     C     *INKA         IFEQ      *ON
     C                   MOVE      '1'           WRETUR
     C                   CALL      'VA556R'
     C                   PARM                    WRETUR
     C                   PARM                    HEDFIR
     C                   PARM                    WPOTYP
     C                   ENDIF
      *
      *  F3=Avslutt
      *
     C     *INKC         CABEQ     *ON           XD1SLT
      *
      *  F5=Forny
      *
     C     *INKE         IFEQ      *ON
     C                   EXSR      XLSP02
     C                   GOTO      D2TAGB
     C                   ENDIF
      *
      *  Roll
      *
     C     *IN10         IFEQ      *ON
     C                   EXSR      XCRT02
     C                   GOTO      D2TAGB
     C                   ENDIF
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandling av subfile, Foretar valgene                         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *  Sjekk om valg
      *  er foretatt
      *
     C                   DO        SSRN02
     C                   READC     D1SFL                                  16
     C     *IN16         IFEQ      *ON
     C                   LEAVE
     C                   ENDIF
      *
      *  Sjekk alle linjer
      *
     C****>                MOVE D1LINE    PRILIN
     C****>                EXSR XD7WIN
      *
     C                   ENDDO
      *
     C                   EXSR      XLSP02
     C                   GOTO      D2TAGA
      *
      *  Avslutter behandlingen av sub-file
      *
     CSR   XD1SLT        ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Fornyer subfilen (dvs. loader subfilen pånytt)                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XLSP02        BEGSR
      *
     C     SFRN02        IFNE      *ZERO
     C     SFRN02        CHAIN     D1SFL                              60
     C                   END
      *
     C*>                   MOVE D1LINE    PRILIN
      *
     C     PRIKEY        SETLL     LODTLV
      *
     C                   EXSR      XCLR02
     C                   EXSR      XCRT02
      *
     C                   ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Display subfile                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XDSP02        BEGSR
      *
     C     SRRN02        IFNE      0
     C                   MOVE      *ON           *IN12
     C                   ENDIF
      *
     C                   MOVE      *ON           *IN13
     C                   EXFMT     D2CTL
     C                   MOVE      *OFF          *IN12
     C                   MOVE      *OFF          *IN13
      *
     C                   ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tøm subfile                                                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XCLR02        BEGSR
     C                   MOVE      *ON           *IN14
     C                   WRITE     D2CTL
     C                   MOVE      *OFF          *IN14
      *
     C                   MOVE      *OFF          *IN15
     C                   Z-ADD     0             SRRN02
     C                   Z-ADD     0             SFRN02
     C                   Z-ADD     0             SSRN02
     C                   Z-ADD     0             SPGE02
      *
     C                   ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Fyll opp subfile                                               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XCRT02        BEGSR
     C                   ADD       14            SPGE02
     C                   Z-ADD     SSRN02        SRRN02
      *
     C     SRRN02        DOUEQ     SPGE02
      *
     C                   READ      LODTLV                                 15
      *
     C     *IN15         IFEQ      *OFF
     C     LDVARE        IFNE      PRIVAR
     C                   MOVE      *ON           *IN15
     C                   END
     C                   END
      *
     C     *IN15         CABEQ     '1'           XEND02
      *
     C                   MOVE      LDNUMM        HEDNUM
     C                   MOVE      LDSUFF        HEDSUF
     C     HEDKEY        CHAIN     LOHEL1R                            90
     C                   MOVE      LOLDOR        D1LDOR
     C                   MOVEL     LONAVN        D1NAVN
     C                   MOVE      LOOTYP        D1OTYP
      *
     C     WPOTYP        IFNE      *BLANK
     C     LOOTYP        IFNE      WPOTYP
     C                   ITER
     C                   END
     C                   END
      *
     C                   ADD       1             SRRN02
     C                   MOVE      LDNUMM        D1NUMM
     C                   MOVE      LDSUFF        D1SUFF
     C                   MOVE      LDLINE        D1LINE
     C                   MOVE      LDANTA        D1ANTA
     C                   MOVE      LDENH1        D1ENH1
     c                   eval      d1dato = 0
      *
     c                   if        loldat <> *loval
     c     *dmy          move      loldat        d1dato
     c                   endif
      *
     C     D1LDOR        IFEQ      *ZERO
     C                   MOVE      LOLDOR        D1LDOR
     C                   END
      *
     C     D1NAVN        IFEQ      *BLANK
     C                   MOVE      D1LDOR        LEVNUM
     C     LEVKEY        CHAIN     rlevl1R                            66
     C  N66              MOVEL     RLNAVN        D1NAVN
     C                   END
      *
     C                   WRITE     D1SFL
     C                   ENDDO
      *
     C     XEND02        TAG
      *
     C     SRRN02        IFGT      SSRN02
     C     SSRN02        ADD       1             SFRN02
     C                   ENDIF
      *
     C                   Z-ADD     SRRN02        SSRN02
     C                   Z-ADD     SFRN02        SVRN02
      *
     C                   ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     *INZSR        BEGSR
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     C                   Z-ADD     0             SPGE01            3 0
     C                   Z-ADD     0             SRRN01            4 0
     C                   MOVE      *BLANK        WSEQEN            1
     C                   MOVE      *BLANK        WRETUR            1
     C                   MOVE      *BLANK        WPENOR            1
     C                   MOVE      *BLANK        WPIPOR            1
     C                   MOVE      *BLANK        WPPLOR            1
      *
     C                   Z-ADD     0             SPGE02            3 0
     C                   Z-ADD     0             SRRN02            4 0
      *
      *  Redefinering av felter
      *
     C     *LIKE         DEFINE    SRRN01        SFRN01
     C     *LIKE         DEFINE    SRRN01        SSRN01
      *
     C     *LIKE         DEFINE    SRRN02        SFRN02
     C     *LIKE         DEFINE    SRRN02        SSRN02
      *
     C     *LIKE         DEFINE    LDLINE        WWLINE
      *
      *  Redefinering av parameterfeltene
      *
     C     *LIKE         DEFINE    LOOTYP        WPOTYP
     C     *LIKE         DEFINE    LDFIRM        WPFIRM
     C     *LIKE         DEFINE    LDVARE        WPVARE
      *
      *  Redefinering av nøkler
      *
     C     *LIKE         DEFINE    RLFIRM        LEVFIR
     C     *LIKE         DEFINE    RLLEVR        LEVNUM
      *
     C     *LIKE         DEFINE    LOFIRM        HEDFIR
     C     *LIKE         DEFINE    LONUMM        HEDNUM
     C     *LIKE         DEFINE    LOSUFF        HEDSUF
      *
     C     *LIKE         DEFINE    LDFIRM        PRIFIR
     C     *LIKE         DEFINE    LDVARE        PRIVAR
     C     *LIKE         DEFINE    LDNUMM        PRINUM
     C     *LIKE         DEFINE    LDSUFF        PRISUF
     C     *LIKE         DEFINE    LDENH1        PRIENH
      *
      *  Key for file : LEVERANDØR-REGISTER
      *
     C     LEVKEY        KLIST
     C                   KFLD                    LEVFIR
     C                   KFLD                    LEVNUM
      *
      *  Key for file : BESTILLING-HODE-REGISTER
      *
     C     HEDKEY        KLIST
     C                   KFLD                    HEDFIR
     C                   KFLD                    HEDNUM
     C                   KFLD                    HEDSUF
      *
      *  Key for file : VARE-BESTILLING-ENHET-REGISTER
      *
     C     PRIKEY        KLIST
     C                   KFLD                    PRIFIR
     C                   KFLD                    PRIVAR
     C                   KFLD                    PRINUM
     C                   KFLD                    PRISUF
     C                   KFLD                    PRIENH
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     *ENTRY        PLIST
     C                   PARM                    WPPLOR
     C                   PARM                    WPOTYP
     C                   PARM                    WPFIRM
     C                   PARM                    WPVARE
     C                   PARM                    WPTEK1           35
      *
      *  Legger inn firma i nøkklene
      *
     C                   MOVE      WPFIRM        LEVFIR
     C                   MOVE      WPFIRM        HEDFIR
     C                   MOVE      WPFIRM        PRIFIR
      *
      *  Legger inn kundenummer i nøkklene
      *
     C                   MOVE      WPVARE        PRIVAR
      *
      *  Nullstiller felter
      *
     C                   MOVE      *ZERO         PRINUM
     C                   MOVE      *ZERO         PRISUF
     C                   MOVE      *BLANK        PRIENH
      *
     C                   ENDSR
