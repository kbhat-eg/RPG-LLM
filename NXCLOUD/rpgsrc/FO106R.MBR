     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO106R
      * Beskrivelse . . : Henter beløpsgrense pr. almenning
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * R5.50 030807 KOB  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frmknl2    if   e           k disk    rename(rmknpfr:rmknl2r)
     fralml1    if   e           k disk    rename(ralmpfr:ralml1r)
      *
      * Definering av parametere
      *
     d p_kund          s                   like(rmxknr)
     d p_belp          s                   like(rmbgrn)
      *
      * Definering av local data area
      *
     d                uds
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variable i nøkler
      *
     d rmknl2_xknr     s                   like(rmxknr)
     d ralml1_alme     s                   like(rmalme)
      *
      * Definering av variable
      *
     d w_firm          s                   like(rmxfir)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Finn almenning
      *
     c                   eval      rmknl2_xknr =  p_kund
     c     rmknl2_key    chain     rmknl2
      *
      *  Finn beløpsgrense for almenning
      *
     c                   if        %found(rmknl2)
     c                   eval      ralml1_alme = rmxalm
     c     ralml1_key    chain     ralml1
      *
     c                   if        %found(ralml1)
     c                   eval      p_belp = rmbgrn
     c                   else
     c                   eval      p_belp = 0
     c                   endif
      *
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_kund
     c                   parm                    p_belp
      *
     c                   eval      w_firm = l_firm
      *
      * Key les på eiendomsknytingsregisteret
      *
     c     rmknl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rmknl2_xknr
      *
      * Key les på almenningsregisteret
      *
     c     ralml1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ralml1_alme
      *
     c                   endsr
