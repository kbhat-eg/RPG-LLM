     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASPGPL
      * Program . . . . : AA010R
      * Beskrivelse . . : Vedlikehold av utkøer/enheter
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       290605 AMD  Skrevet om til ny standard
      * V5.41 301005 KOB  Lagt inn støtte for Zebra QL320
      * V5.50 051106 KOB  Fjernet støtte for Eltron skriver
      * V5.60 210109 KOB  Lagt inn støtte for Datamax Ex-2
7.xx  * 7.xx      140618  Endringer reltart til ny GUI. NB Kan kun gjelde BKV
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     faenhl1    if   e           k disk    rename(aenhpfr:aenhl1r)
     faenhl2    if   e           k disk    rename(aenhpfr:aenhl2r)
     faenhlr    if   e           k disk    rename(aenhpfr:aenhlrr)
     faenhlu    uf a e           k disk    rename(aenhpfr:aenhlur)
      *
     faa010d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av array
      *
     d a_meld          s             50    dim(2) ctdata perrcd(1)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_sbib                934    943
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d aenhl1_prid     s                   like(aeprid)
     d aenhl2_besk     s                   like(aebesk)
     d aenhl2_prid     s                   like(aeprid)
     d aenhlr_prid     s                   like(aeprid)
     d aenhlu_prid     s                   like(aeprid)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
     d w_prid          s                   like(aeprid)
     d w_tilk          s                   like(aetilk)
     d w_qmsg          s             10
     d w_form          s             10
     d w_mld1          s             50
     d w_mld2          s             50
     d w_svar          s              1
     d w_in12          s              1
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d s_prid          s                   like(aeprid)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * C3WIN  - C3 Vindu for tilleggsinformasjon
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inki
     c                   exsr      start_alle
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2prid <> *blank or
     c                             b2besk <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      aenhl2_besk = b1besk
     c                   eval      aenhl2_prid = *blank
     c     aenhl2_key    setll     aenhl2
     c                   other
     c                   eval      aenhl1_prid = b1prid
     c     aenhl1_key    setll     aenhl1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Utkø / enhet
      *
     c                   if        b2prid <> *blank
     c                   eval      w_seqe = *blank
     c                   eval      aenhl1_prid = b2prid
     c     aenhl1_key    setll     aenhl1
     c                   goto      end_pos
     c                   endif
      *
      * Beskrivelse
      *
     c                   if        b2besk <> *blank
     c                   eval      w_seqe = 'B'
     c                   eval      aenhl2_besk = b2besk
     c                   eval      aenhl2_prid = *blank
     c     aenhl2_key    setll     aenhl2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2prid = *blank
     c                   eval      b2besk = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1prid = b1prid
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1prid = b1prid
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1prid = b1prid
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1prid = b1prid
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Starte utkøer/skrivere
      *
     c                   if        b1valg = 9
     c                   exsr      call_pgm
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1prid = b2prid
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1prid = *blank
     c                   goto      end_c1bld
     c                   endif
      *
      * Validering
      *
     c***                if        c1prid = *blank
     c***                eval      *in31  = *on
     c***                goto      c1taga
     c***                endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      aenhlr_prid = c1prid
     c     aenhlr_key    chain     aenhlr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1prid = *blank
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2prid = *blank
     c                   eval      b2besk = *blank
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      aenhlr_prid = c1prid
     c     aenhlr_key    chain     aenhlr
      *
     c                   if        %found
     c                   eval      c2besk = aebesk
     c                   eval      c2plas = aeplas
     c                   eval      c2tilk = aetilk
     c                   eval      c2strt = aestrt
     c                   eval      c2qmsg = aeqmsg
     c                   eval      c2form = aeform
     c                   eval      c2ocrk = aeocrk
     c                   eval      c2hex1 = aehex1
     c                   eval      c2hex2 = aehex2
     c                   eval      c3type = aetype
     c                   eval      c2eusr = aeeusr
     c***  *dmy          move      awodat        c2odat
     c     *dmy          move      aeedat        c2edat
     c                   else
     c                   eval      c2besk = *blank
     c                   eval      c2plas = *blank
     c                   eval      c2tilk = 0
     c                   eval      c2strt = 0
     c                   eval      c2qmsg = 0
     c                   eval      c2form = *blank
     c                   eval      c2ocrk = 0
     c                   eval      c2hex1 = *blank
     c                   eval      c2hex2 = *blank
     c                   eval      c3type = 0
     c                   eval      c2eusr = *blank
     c***                eval      c2odat = 0
     c                   eval      c2edat = 0
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1prid = k1prid
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * F9 = Henter opp bilde for tilleggsinformasjon
      *
     c                   if        *inki
     c                   exsr      xc3win
     c                   goto      c2taga
     c                   endif
      *
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c2taga
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2besk = *blank
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   aenhlur
      *
     c                   eval      aenhlu_prid = c1prid
     c     aenhlu_key    chain     aenhlur
      *
     c                   eval      aebesk = c2besk
     c                   eval      aeplas = c2plas
     c                   eval      aetilk = c2tilk
     c                   eval      aestrt = c2strt
     c                   eval      aeqmsg = c2qmsg
     c                   eval      aeform = c2form
     c                   eval      aeocrk = c2ocrk
     c                   eval      aetype = c3type
     c                   eval      aehex1 = c2hex1
     c                   eval      aehex2 = c2hex2
      *
     c                   time                    aeedat
     c                   time                    aeetim
     c                   eval      aeeusr = l_user
      *
     c                   if        %found
     c                   update    aenhlur
     c                   else
     c                   eval      aeprid = c1prid
     c***                time                    awodat
     c                   write     aenhlur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1prid = aeprid
     c                   eval      b1besk = aebesk
     c                   eval      b1tilk = aetilk
     c                   eval      b1strt = aestrt
     c                   eval      b1qmsg = aeqmsg
     c                   eval      b1ocrk = aeocrk
     c                   eval      b1form = aeform
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      aenhlr_prid = d1prid
     c     aenhlr_key    chain     aenhlr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      aenhlu_prid = d1prid
     c     aenhlu_key    chain     aenhlur
      *
     c                   delete    aenhlur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_prid = k1prid
      *
      * Fyller kopierings-bildet
      *
     c                   eval      aenhlr_prid = k1prid
     c     aenhlr_key    chain     aenhlr
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1prid = *blank
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      aenhlr_prid = k1prid
     c     aenhlr_key    chain     aenhlr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1prid = s_prid
     c                   exsr      xc2bld
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av tilleggs-informasjon (c3WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc3win        begsr
      *
     c                   exfmt     c3win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_c3win
     c                   endif
      *
     c     end_c3win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      aenhl2
     c                   other
     c                   read      aenhl1
     c                   endsl
      *
     c                   if        %eof
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      aenhl2_besk = aebesk
     c                   other
     c                   eval      aenhl1_prid = aeprid
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1prid = aeprid
     c                   eval      b1besk = aebesk
     c                   eval      b1tilk = aetilk
     c                   eval      b1strt = aestrt
     c                   eval      b1qmsg = aeqmsg
     c                   eval      b1ocrk = aeocrk
     c                   eval      b1form = aeform
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'B'
     c     aenhl2_key    setgt     aenhl2
     c                   readp     aenhl2
     c                   other
     c     aenhl1_key    setgt     aenhl1
     c                   readp     aenhl1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   readp     aenhl2
     c                   other
     c                   readp     aenhl1
     c                   endsl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      aenhl2_besk = aebesk
     c                   other
     c                   eval      aenhl1_prid = aeprid
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'B'
     c     aenhl2_key    setll     aenhl2
     c                   other
     c     aenhl1_key    setll     aenhl1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Kaller program for start/stop funksjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     call_pgm      begsr
      *
     c                   eval      w_prid = b1prid
     c                   eval      w_tilk = b1tilk
      *
     c                   if        b1qmsg = 1
     c                   eval      w_qmsg = '*INQMSG   '
     c                   else
     c                   eval      w_qmsg = '*NOMSG    '
     c                   endif
      *
     c                   if        b1form = *blank
     c                   eval      w_form = '*ALL      '
     c                   else
     c                   eval      w_form = b1form
     c                   endif
      *
      * Kaller program
      *
     c                   call      'AE702C'
     c                   parm                    w_prid
     c                   parm                    w_qmsg
     c                   parm                    w_form
     c                   parm                    w_tilk
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Kaller program for oppstart av alle utkøer/skrivere
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     start_alle    begsr
      *
     c                   eval      w_mld1 = a_meld(1)
     c                   eval      w_mld2 = a_meld(2)
     c                   eval      w_svar = 'N'
      *
     c                   call      'AA007R'
     c                   parm                    w_mld1
     c                   parm                    w_mld2
     c                   parm                    w_svar
     c                   parm                    w_in12
      *
     c                   if        w_in12 = '1'
     c                   goto      end_start
     c                   endif
      *
     c                   if        w_svar = 'J'
     c                   call      'AE700R'
     c                   endif
      *
     c     end_start     endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på utkø/enhet
      *
     c     aenhl1_key    klist
     c                   kfld                    aenhl1_prid
      *
      * Key for posisjonering på beksrivelse
      *
     c     aenhl2_key    klist
     c                   kfld                    aenhl2_besk
     c                   kfld                    aenhl2_prid
      *
      * Key for les på skjerm / utkø/enhet
      *
     c     aenhlr_key    klist
     c                   kfld                    aenhlr_prid
      *
      * Key for oppdatering av utkø/enhet
      *
     c     aenhlu_key    klist
     c                   kfld                    aenhlu_prid
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      aenhl1_prid = *blank
     c     aenhl1_key    setll     aenhl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
**
Det er bedt om oppstart av alle utkøer/skrivere
Bekreft dersom dette skal utføres ...
