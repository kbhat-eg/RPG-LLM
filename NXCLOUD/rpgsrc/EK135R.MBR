     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : EK135R
      * Beskrivelse . . : Kunde bonus, vedlikehold - parameter (liste)
      *
      *                   Generering av kreditnota                     tnota
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       221013 bsa  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     febkrl1    if   e           k disk    rename(ebkrpfr:ebkrl1r)
     febkrlu    uf a e           k disk    rename(ebkrpfr:ebkrlur)
     fvbotl1    if   e           k disk    rename(vbotpfr:vbotl1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fek135d    cf   e             workstn
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d lda            uds
     d  l_user               911    920
     d  l_firm               944    946  0
     d  l_fnav               951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d vbotl1_tbot     s                   like(vatbot)
     d votyl1_otyp     s                   like(vaotyp)
     d vvarl1_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
      *
     d w_udat          s               d   datfmt(*iso)
     d w_udat_8        s              8
     d w_firm_alf      s              3
     d w_fper          s              4  0
     d w_tper          s              4  0
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
      *
     d w_firm          s                   like(vatfir)
     d w_tbes          s             35
     d w_text          s             35
     d w_pfra          s              4  0
     d w_ptil          s              4  0
     d b_kuti          s              1    inz(*off)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * - - - - - - - - - - - - - - - - -
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * P1WIN  - P1 Vindu for starte rutine
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
      * Fyller bildet
      *
     c                   exfmt     p1win
      *
     c                   if        *inkc or *inkl
     c                   goto      avslutt
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd = *on
     c                   exsr      sr_spor
     c                   goto      b2taga
     c                   endif
      *
      * Validering av valg - angitt bonustype
      *
     c                   if        p1kbot = *blanks
     c                   eval      *in31 = *on
     c                   goto      b2taga
     c                   endif
      *
     c                   eval      vbotl1_tbot = p1kbot
     c     vbotl1_key    chain     vbotl1
     c                   if        not %found
     c                   eval      *in31 = *on
     c                   goto      b2taga
     c                   endif
     c                   movel     vatbes        p1kbtx
      *
      * Kontroller ordretype er gyldig
      *
     c                   if        p1koty = *blanks
     c                   eval      *in32 = *on
     c                   goto      b2taga
     c                   endif
      *
     c                   eval      votyl1_otyp = p1koty
     c     votyl1_key    chain     votyl1
     c                   if        not %found
     c                   eval      *in32 = *on
     c                   goto      b2taga
     c                   endif
     c                   movel     vaotxt        p1kotx
      *
      * Kontroller varenummer er gyldig
      *
     c                   if        p1kvar = *blanks
     c                   eval      *in34 = *on
     c                   goto      b2taga
     c                   endif
      *
     c                   eval      vvarl1_vare = p1kvar
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   eval      *in34 = *on
     c                   goto      b2taga
     c                   endif
     c                   movel     vvtek1        p1kvtx
      *
      * Sjekk at det er gyldig periode
      *
     c                   eval      w_pfra = %int(p1kfra)
     c     w_pfra        ifeq      *zeros
     c                   eval      *in33 = *on
     c                   goto      b2taga
     c                   endif
      *
      * Sjekk at det er gyldig periodeintervall
      *
     c                   eval      w_ptil = %int(p1ktil)
     c     w_ptil        ifeq      *zeros
     c                   eval      *in33 = *on
     c                   goto      b2taga
     c                   endif
      *
     c     w_pfra        ifgt      w_ptil
     c                   eval      *in35 = *on
     c                   goto      b2taga
     c                   endif
      *
      * Oppdater file med "parametre"
      *
     c     ebkrlu_key    chain     ebkrlu
     c                   if        not %found
     c                   clear                   ebkrlur
     c                   endif
      *
     c                   eval      ebkbot = p1kbot
     c                   eval      ebkpfr = %int(p1kfra)
     c                   eval      ebkpti = %int(p1ktil)
     c                   eval      ebkoty = p1koty
     c                   eval      ebkvar = p1kvar
     c                   eval      ebktx1 = p1ktx1
     c                   eval      ebktx2 = p1ktx2
     c                   eval      ebktx3 = p1ktx3
     c                   eval      ebktx4 = p1ktx4
     c                   eval      ebktx4 = p1ktx4
      *
     c                   time                    ebkskd
     c                   time                    ebkskt
     c                   time                    ebkeda
     c                   time                    ebketi
     c                   eval      ebkeus = l_user
      *
     c                   if        %found
     c                   update    ebkrlur
     c                   else
     c                   eval      ebkfir = l_firm
     c                   write     ebkrlur
     c                   endif
      *
     c                   call      'EK140C'
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for soørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sr_spor       begsr
      *
     c                   if        w_afld = 'P1KBOT'
     c                   eval      p1kbtx = *blank
     c                   call      'VA565R'
     c                   parm                    w_firm
     c                   parm                    p1kbot
     c                   parm                    w_tbes
      *
     c                   if        p1kbot <> *blank
     c                   movel     w_tbes        p1kbtx
     c                   endif
     c                   endif
      *
     c                   if        w_afld = 'P1KOTY'
     c                   eval      p1koty = *blank
     c                   eval      p1kotx = *blank
     c                   call      'VA550R'
     c                   parm                    w_firm
     c                   parm                    p1koty
     c                   endif
      *
     c                   if        w_afld = 'P1KVAR'
     c                   eval      p1kvar = *blank
     c                   eval      w_text = *blank
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    p1kvar
     c                   parm                    w_text
      *
     c                   if        p1kvar <> *blank
     c                   movel     w_text        p1kvtx
     c                   endif
     c                   endif
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *dtaara       define    *lda          lda
      *
      * Key for oppslag på kundebonus
      *
     c     ebkrl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på kundebonus
      *
     c     ebkrlu_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på bonustype register
      *
     c     vbotl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vbotl1_tbot
      *
      * Key for oppslag på ordretype register
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for oppslag på ordretype register
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
     c                   time                    w_date
     c                   time                    w_time
      *
      * Initierer skjermbildet
      *
     c     ebkrl1_key    chain     ebkrl1
     c                   if        %found
     c                   eval      p1kbot = ebkbot
     c                   move      ebkpfr        p1kfra
     c                   move      ebkpti        p1ktil
     c                   eval      p1koty = ebkoty
     c                   eval      p1kvar = ebkvar
     c                   eval      p1ktx1 = ebktx1
     c                   eval      p1ktx2 = ebktx2
     c                   eval      p1ktx3 = ebktx3
     c                   eval      p1ktx4 = ebktx4
     c                   else
     c                   eval      p1kbot = *blanks
     c                   eval      p1kfra = *blanks
     c                   eval      p1ktil = *blanks
     c                   eval      p1koty = *blanks
     c                   eval      p1kvar = *blanks
     c                   eval      p1ktx1 = *blanks
     c                   eval      p1ktx2 = *blanks
     c                   eval      p1ktx3 = *blanks
     c                   eval      p1ktx4 = *blanks
     c                   endif
      * Hent info
     c                   if        p1kbot <> *blanks
     c                   eval      vbotl1_tbot = p1kbot
     c     vbotl1_key    chain     vbotl1
     c                   if        %found
     c                   movel     vatbes        p1kbtx
     c                   endif
     c                   endif
      *
     c                   if        p1koty <> *blanks
     c                   eval      votyl1_otyp = p1koty
     c     votyl1_key    chain     votyl1
     c                   if        %found
     c                   movel     vaotxt        p1kotx
     c                   endif
     c                   endif
      *
     c                   if        p1kvar <> *blanks
     c                   eval      vvarl1_vare = p1kvar
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   movel     vvtek1        p1kvtx
     c                   endif
     c                   endif
      *
     c                   endsr
