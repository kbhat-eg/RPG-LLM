      /TITLE  Danner posteringer fra bankinnbetalinger
     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RF122R                                              *
      * Beskrivelse . . : Danner posteringer fra OCR-innbetalinger            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign.*
      * --------- ------  ---------------------------------------------- ---- *
      *           250603  Lagt om til ekstern definert printfil          KOB  *
      * V5.20     231203  Markerer ugyldig kundenr.                      HFL  *
      * 060104    060104  Lagt inn årstall i RSRAAR.                     KOB  *
T5.00 * R5.40     081005  Lagt inn behandling av negative poster         KOB
T5.40 * R5.40     020506  Skriver fullt navnefelt (bare comp.)           HFL
T5.00 * R5.40     270806  Lagt inn behandling av leverandørposter        KOB
T5.00 * R5.40     081006  Endret slik at referansefeltet 0-stilles       KOB
T5.00 * R5.40     081006  dersom bilaget ikke finnes                     KOB
T5.00 * R5.50     201206  Nullstiller posteringsbeløp hvis ikke match    KOB
T5.50 * R5.50     050608  OCR for 2 perioder: Linjer ble skrevet bare
T5.50 *                   for første periode.                            HFL
      *                                                                       *
 6.11 * R6.10     171011  Ikke summer post dersom konto eller bilags-
      *                   kode ikke er utfyllt
 6.20 * R6.20     120312  Flytter blanke til prosjekt                    NHO
 6.21 * R6.20     110412  Sett på *in88 dersom member ulik               NHO
 6.22 * R6.20     290812  Dersom poster lå igjen i arb. filer ble        NHO
      *                   poster lagt feil ut i rtra og rbun
 6.23 * R6.20     191012  Fjernet poster merket med 6.22
      *                   Gammel feil vil da kanskje forekomme dersom
      *                   'brudd'
6.30  *  6.30     150814  Innført mulighet for arkivering og PDF   Bsa  *
6.31  * R6.30 180816 bsa  Rokkert på rekkefølge av close printerfile
6.32  * R6.30 161019 sst  Bruke bare et bilagsnr p} alle posteringer    *
      *
7.fb  * K000  260117 bof  Hvis depositumsinnbetaling, endre bk og tekst
      *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frjoupf    ip   e           k disk
     fraa4lu    uf a e           k disk    rename(raa4pfr:raa4lur)
     fra03l1    if   e           k disk    rename(ra03pfr:ra03l1r)
     fra04l1    if   e           k disk    rename(ra04pfr:ra04l1r)
     fra05l1    if   e           k disk    rename(ra05pfr:ra05l1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     frktrl8    if   e           k disk    rename(rktrpfr:rktrl8r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     frtrapf    o    e             disk
     frbunl1    if   e           k disk    rename(rbunpfr:rbunl1r)
     frbunpf    o    e             disk
     frf122p    o    e             printer oflind(*in30)
6.30 f                                     usropn
      *
610  d rbunl1_nivo     s                   like(rsnivo)
610  d rbunl1_memb     s                   like(rsmemb)
610  d w_memb          s                   like(rjmemb)
610  d w_mem1          s              7
     d                 ds
     d w_bref                        10  0
     d  w_seid                        2  0 overlay(w_bref)
     d  w_dagk                        2  0 overlay(w_bref:3)
     d  w_dela                        1  0 overlay(w_bref:5)
     d  w_lØpn                        5  0 overlay(w_bref:6)
      *
     d                 ds
     d whtext                        20
     d  whfl01                        7    overlay(whtext)
     d  whfl02                        6    overlay(whtext:8)
     d  whfl03                        1    overlay(whtext:14)
     d  whfl04                        6    overlay(whtext:15)
      *
     d                 ds
     d wktext                        20
     d  wkfl01                        8    overlay(wktext)
     d  wkfl02                        8    overlay(wktext:9)
     d  wkfl03                        4    overlay(wktext:17)
      *
     d                uds
6.30 d l_poff                584    584
6.30 d l_utqm                585    594
6.30 d l_bach                595    635
6.30 d l_arch                636    636  0
6.30 d l_skje                637    637  0
6.30 d l_land                638    638  0
6.30 d l_exlp                639    639  0
6.30 d l_mail                640    640  0
6.30 d l_ruti                800    809
6.30 D l_outq                810    819
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d ra03l1_ckod     s                   like(rackod)
     d ra05l1_ekod     s                   like(raekod)
     d rkunl1_kund     s                   like(rkkund)
     d rktrl8_kund     s                   like(rnkund)
     d rktrl8_biln     s                   like(rnbiln)
     d rlevl1_levr     s                   like(rllevr)
      *
     d antdag          s              5  0
     d w_antd          s              5  0
     d avgift          s             11  2
     d belØua          s             11  2
     d prosh           s              4  2
     d prosl           s              4  2
     d rabsum          s             11  2
     d restua          s             11  2
     d sjpro           s              5  3
6.30 d p_btyp          s            100
6.30 d w_year          s              4  0
6.30 d w_dato          s               d   datfmt(*iso)
7.fb d w_bilk          s                   like(rjbilk)
      *
     d w_biln          s                   like(rnbiln)
     d w_bsum          s                   like(rsbsum)
6.11 d w_dsum          s                   like(rsbsum)
6.11 d w_ksum          s                   like(rsbsum)
     d w_bunt          s                   like(rsbunt)
     d w_fØrs          s                   like(rsfØrs)
     d w_firm          s                   like(rkfirm)
     d w_krab          s                   like(rtkrab)
     d w_sist          s                   like(rssist)
     d w_4sgb          s                   like(ra4sgb)
     d w_4sig          s                   like(ra4sig)
     d w_edat          s               d   datfmt(*iso)
      *
     d w_belo          s             10  2
     d w_linj          s              5  0
     d w_egpr          s              5  4
     d w_epro          s              5  4
      *
6.30 d splfname2       s             10a
6.30 d jobname2        s             10a
6.30 d username2       s             10a
6.30 d jobnbr2         s              6a
6.30 d splfnbr2        s             10i 0
6.30 d splfname3       s             10a
6.30 d jobname3        s             10a
6.30 d username3       s             10a
6.30 d jobnbr3         s              6a
6.30 d splfnbr3        s             10i 0
6.30 d p_tagg0         s             20
6.30 d p_tagg0val      s             50
6.30 d p_tagg1         s             20
6.30 d p_tagg1val      s             50
6.30 d p_tagg2         s             20
6.30 d p_tagg2val      s             50
6.30 d p_tagg3         s             20
6.30 d p_tagg3val      s             50
6.30 d p_tagg4         s             20
6.30 d p_tagg4val      s             50
6.30 d p_tagg5         s             20
6.30 d p_tagg5val      s             50
6.30 d p_tagg6         s             20
6.30 d p_tagg6val      s             50
6.30 d p_tagg7         s             20
6.30 d p_tagg7val      s             50
6.30 d p_tagg8         s             20
6.30 d p_tagg8val      s             50
6.30 d p_tagg9         s             20
6.30 d p_tagg9val      s             50
6.30 d p_refn          s             50
6.30 d p_dspl          s            100
      *
6.30 d qsprilsp        pr                  extpgm('QSPRILSP')
6.30 d rcvvar                     32767a   options(*varsize)
6.30 d rcvvarlen                     10i 0 const
6.30 d format                         8a   const
6.30 d errorcode                  32767a   options(*varsize)
      *
6.30 d sprl0100        ds
6.30 d  bytesrtn                     10i 0
6.30 d  bytesavl                     10i 0
6.30 d  splfname                     10a
6.30 d  jobname                      10a
6.30 d  username                     10a
6.30 d  jobnbr                        6a
6.30 d  splfnbr                      10i 0
6.30 d  systemname                    8a
6.30 d  createdate                    7a
6.30 d                                1a
6.30 d  createtime                    6a
      *
6.30 d errorcode       ds
6.30 d  bytesprov                    10i 0 inz(0)
6.30 d  byteavail                    10i 0 inz(0)
      *
     irjoupfr
     i                                          rjfirm        l3
     i                                          rjraar        l3
     i                                          rjrper        l3
     i                                          rjbref        l2
     i                                          rjresn        l1
      *
     c     dummy         tag
610   *
610   *
610  c                   if        rjmemb <> w_memb
6.21 c                   move      *on           *in88
610  c                   move      *off          *inl1
610  c                   move      *off          *inl2
610  c                   move      *off          *inl3
610  c                   goto      slutt
610  c                   endif
      *
      * Brudd på Firma/År/Periode
      *
     c   l3              do
      *
      * Finn ledig bunt-nr. for OCR-poster
      *
610  c                   eval      rbunl1_nivo = *blank
610  c                   eval      rbunl1_memb = w_mem1
     c                   move      rjfirm        w_firm
     c                   move      99999         w_bunt
     c     rbunl1_key    setgt     rbunl1
     c                   readp     rbunl1                                 62
      *
     c                   if        *in62 = *on
     c                   eval      w_bunt = 1
     c                   else
     c                   eval      w_bunt = rsbunt + 1
     c                   endif
      *
     c                   enddo
      *
6.32  * Hent sist brukte bilagsnummer
6.32  *
6.32 c   l2              do
6.32 c                   move      rjraar        ra4aar
6.32 c     raa4lu_key    chain     raa4lu                             91
6.32  *
6.32 c                   if        *in91 = *on
6.32 c                   eval      w_4sig = 0
6.32 c                   eval      w_4sgb = 0
6.32 c                   write     raa4lur
6.32 c                   else
6.32 c                   eval      ra4sgb = ra4sgb + 1
6.32 c                   eval      w_4sgb = ra4sgb
6.32 c                   update    raa4lur
6.32 c                   endif
6.32  *
6.32 c                   enddo
6.32  *
      * Brudd referanse
      *
     c   l2              do
      *
     c                   eval      w_belo = 0
      *
      * Hent sist brukte bilagsnummer
      *
6.32 c*                  move      rjraar        ra4aar
6.32 c*    raa4lu_key    chain     raa4lu                             91
6.32  *
6.32 c*                  if        *in91 = *on
6.32 c*                  eval      w_4sig = 0
6.32 c*                  else
6.32 c*                  eval      w_4sig = ra4sig
6.32 c*                  eval      w_4sgb = ra4sgb + 1
6.32 c*                  endif
6.32  *
6.32 c*                  eval      ra4sig = w_4sig
6.32 c*                  eval      ra4sgb = w_4sgb
6.32  *
6.32 c*                  if        *in91 = *off
6.32 c*                  update    raa4lur
6.32 c*                  else
6.32 c*                  write     raa4lur
6.32 c*                  endif
      *
     c                   enddo
      *
      * Brudd på kundenr./levnr.
      *
     c                   if        *inl1 = *on
      *
     c                   eval      rkunl1_kund = rjresn
     c     rkunl1_key    chain     rkunl1
T5.2 c                   if        %found(rkunl1)
T5.20c                   movel     rknavn        w_navn
T5.20c                   else
T5.20c                   eval      w_navn = '*** Ugyldig kundenr. ***'
T5.20c                   endif
      *
T5.2 c                   if        not%found(rkunl1)
     c                   eval      rlevl1_levr = rjresn
     c     rlevl1_key    chain     rlevl1
T5.2 c                   if        %found(rlevl1)
T5.20c                   movel     rlnavn        w_navn
T5.20c                   endif
T5.20c                   endif
      *
T5.20c                   endif
      *
      * Nullstill arbeidsfelt
      *
     c                   eval      racrab = 0
     c                   eval      restua = 0
     c                   eval      avgift = 0
     c                   eval      belØua = 0
     c                   eval      rabsum = 0
     c                   eval      sjpro = 0
     c                   eval      prosh = 0
     c                   eval      w_krab = 0                                                  V2 03
      *
      * Behandler kun poster med beløp utfylt
      *
     c                   if        rjneto = *zero
     c                   goto      slutt
     c                   endif
      *
      * Div. tester for å sjekke rabattberettighet
      *
      * Autogiro
      *
     c                   if        rjtype = 01
     c                   move      'S'           rtkrys
     c                   goto      norab
     c                   endif
      *
      * Les inn kundetransaksjon
      *
     c                   eval      rktrl8_kund = rjresn
     c                   eval      rktrl8_biln = rjbiln
     c     rktrl8_key    chain     rktrl8
      *
      * Legg ut bilagsdato og beløp på rapport
      *
     c                   if        %found(rktrl8)
     c     *dmy          move      rnbdat        w_fdat
T5.40c                   eval      w_belØ = rnbelØ
     c                   else
     c                   move      *zeros        w_fdat
T5.40c                   eval      w_belØ = 0
     c                   endif
      *
     c     *dmy          move      rjbdat        w_bdat
      *
      * Hvis feil referansenr.
      *
     c                   if        not%found(rktrl8)
     c                   move      'X'           rtkrys
T5.40c***                eval      rjrefn = 0
     c                   goto      norab
     c                   endif
      *
      * Hvis saldert tidligere
      *
     c                   if        rnrest <> rnbelØ
     c                   move      'X'           rtkrys
     c                   goto      norab
     c                   endif
      *
      * Hvis innbetalt beløp
      * større enn fakturabeløp
      *
     c                   if        rjneto > rnbelØ
     c                   move      'X'           rtkrys
     c                   goto      norab
     c                   endif
      *
      *  MVA-kode i KID utfylt
      *  beregn beløp inkl. avgift
      *
     c                   eval      rtcmva = *zero
      *
     c                   if        rjmvak <> *zero
     c                   move      rjmvak        rtcmva
      *
     c                   if        rjbdat < w_edat
     c                   eval      avgift = avgift * w_egpr                     Gml mva %
     c                   else
     c                   eval      avgift = avgift * w_epro                     Ny mva %
     c                   endif
      *
     c                   eval      restua = rnbelØ - avgift
      *
     c                   if        restua <= *zero
     c                   goto      norab
     c                   endif
      *
     c                   eval      belØua = rjneto - avgift
      *
     c                   if        belØua <= *zero
     c                   goto      norab
     c                   endif
      *
     c                   eval      rabsum = restua - belØua
     c                   if        rabsum <= *zero
     c                   goto      norab
     c                   endif
      *
     c                   endif
      *
      * Hvis betalingsbetingelse
      * i KID ikke er utfylt
      *
     c                   if        rjbetb = 0                                    BET  I KID
     c                   goto      norab
     c                   endif
      *
      * Beregn trukket rabatt
      *
     c                   eval      w_krab = rnbelØ - rjneto
      *
      * Kontroller beregnet rabatt
      *
     c                   exsr      rabatt
      *
     c     norab         tag
      *
6.32 c*                  eval      w_4sig = w_4sig + 1
6.32 c*    raa4lu_key    chain     raa4lu
6.32  *
6.32 c*                  eval      ra4sig = w_4sig
6.32  *
6.32 c*                  if        %found
6.32 c*                  update    raa4lur
6.32 c*                  endif
      *
     c                   eval      w_linj = w_linj + 1
      *
     c                   if        rjdbkr = 'K'
     c                   eval      w_belo = w_belo - rjneto
     c                   else
     c                   eval      w_belo = w_belo + rjneto
     c                   endif
      *
     c                   eval      w_bsum = w_bsum + rjneto
6.11  *
6.11  *Ny utregning for debet/kredit
6.11  * Ikke summer dersom bilagskode eller konto = 0
6.11 c                   if        rjkont = 0 or
6.11 c                             rjbilk = 0
6.11 c                   goto      iksum
6.11 c                   endif
6.11  *
6.11 c                   if        rjdbkr = 'K'
6.11 c                   eval      w_ksum = w_ksum - rjneto
6.11 c                   else
6.11 c                   eval      w_dsum = w_dsum + rjneto
6.11 c                   endif
6.11  *
6.11 c     iksum         tag
6.11  *
      *
      * Legg ut reskontropostering
      *
     c                   eval      rtreid = 'T'
     c                   eval      rtfirm = w_firm
     c                   eval      rtbunt = w_bunt
     c                   eval      rtlinj = w_linj
     c                   eval      rtstat = *blank
     c                   eval      rtbdat = rjbdat
     c                   eval      rtbelØ = rjneto
     c                   eval      rtrper = *zero
6.32 c**                 eval      rtbiln = w_4sig
6.32 c                   eval      rtbiln = w_4sgb
      *
     c                   eval      rtdavd = *zero
     c                   eval      rtdkto = *zero
     c                   eval      rtdsps = *zero
     c                   eval      rtdmva = *blank
     c                   eval      rtcavd = *zero
     c                   eval      rtckto = *zero
     c                   eval      rtcsps = *zero
     c                   eval      rtcmva = *blank
      *
     c                   if        rjdbkr = 'K'
     c                   eval      rtdkto = rjresn
     c                   else
     c                   eval      rtckto = rjresn
     c                   endif
      *
6.20 c***                eval      rtpros = *zero                               Prosjekt
6.20 c                   eval      rtpros = *blank                              Prosjekt
     c                   eval      rtrefn = rjrefn                              Referanse
     c                   eval      rtfdat = rjbdat                              Forfall
     c                   eval      rtkrab = w_krab                              Rabatt
     c                   eval      rtvalk = rjvalk                              Valuta-kode
     c                   eval      rtvalb = rjvalb                              Valuta-beløp
     c                   eval      rtakti = rjakti                              Aktivitet
     c                   eval      rtmngd = rjmngd                              Mengde
     c                   eval      rtmngk = rjmngk                              Mengde-kode
     c                   eval      rtresn = rjresn                              Reskontronr
     c                   eval      rtvirk = rjvirk                              Virksomhet
     c                   eval      rtatte = rjatte                              Attestert
     c                   eval      rtkrys = rjkrys                              Avkryssing
     c                   eval      rtkidi = rjkidi                              KID-referanse
     c                   eval      rtbilk = rjbilk                              Bilagskode
     c                   eval      rttext = rjtext                              Bilagstekst
     c                   eval      rtbetb = rjbetb                              Betalingbet
     c                   eval      rtbref = rjbref                              Bank-referanse
     c                   eval      rtdeck = rjdeck
      *
610  c                   eval      rtnivo = *blank
610  c                   eval      rtmemb = w_mem1
610  c                   time                    rtedat
610  c                   time                    rtetim
610  c                   eval      rteusr = l_user
     c                   write     rtrapfr
      *
      * Heading først gang + linje
      *
S5.50c*                  if        *in62 = *on
      *
     c                   if        *in63 = *off
     c                   write     head1
     c                   eval      *in63 = *on
     c                   endif
      *
      * Skriv heading ved OF
      *
     c                   if        *in30 = *on
     c                   write     head1
     c                   endif
      *
     c                   write     linje                                30
7.fb  *
7.fb  * Sjekk om innbetaling er depositum
7.fb  *
7.fb  *
7.fb c                   eval      w_bilk = rjbilk
7.fb c                   if        %subst(rtkidi:8:1) = '4'
7.fb c                   exsr      depositum
7.fb c                   endif
      *
S5.50c*                  endif
      *
     c     slutt         tag
      *
     c                   if        *inl3 = *on
     c                   eval      w_fØrs = w_linj
     c                   endif
      *
      * Oppdater hovedbok
      *
     cl2                 eval      w_linj = w_linj + 1
     cl2                 eval      rtreid = 'T'
     cl2                 eval      rtfirm = w_firm
     cl2                 eval      rtbunt = w_bunt
     cl2                 eval      rtlinj = w_linj
     cl2                 eval      rtstat = *blank
     cl2                 eval      rtbdat = rjbdat
     cl2                 eval      rtbiln = w_4sgb
     cl2                 eval      rtdavd = *zero
     cl2                 eval      rtdkto = *zero
     cl2                 eval      rtdsps = *zero
     cl2                 eval      rtcavd = *zero
     cl2                 eval      rtckto = *zero
     cl2                 eval      rtcsps = *zero
      *
     cl2                 if        w_belo > 0
     cl2                 eval      rtdavd = rada15
     cl2                 eval      rtdkto = radk15
     cl2                 eval      rtdsps = rads15
     cl2                 eval      rtbelØ = w_belo
     cl2                 else
     cl2                 eval      rtcavd = rada15
     cl2                 eval      rtckto = radk15
     cl2                 eval      rtcsps = rads15
     cl2                 eval      rtbelØ = w_belo * -1
     cl2                 endif
      *
6.20 c**l2                 eval      rtpros = *zero                             Prosjekt
6.20 cl2                 eval      rtpros = *blank                              Prosjekt
     cl2                 eval      rtrefn = *zero                               Referanse
     cl2                 eval      rtfdat = *loval                              Forfall
     cl2                 eval      rtkrab = *zero                               Rabatt
     cl2                 eval      rtvalk = rjvalk                              Valuta-kode
     cl2                 eval      rtvalb = rjvalb                              Valuta-beløp
     cl2                 eval      rtakti = rjakti                              Aktivitet
     cl2                 eval      rtmngd = rjmngd                              Mengde
     cl2                 eval      rtmngk = rjmngk                              Mengde-kode
     cl2                 eval      rtresn = rjresn                              Reskontronr
     cl2                 eval      rtvirk = *blank                              Virksomhet
     cl2                 eval      rtatte = rjatte                              Attestert
     cl2                 eval      rtkrys = rjkrys                              Avkryssing
     cl2                 eval      rtkidi = *blank                              KID-referanse
7.fb c*l2                 eval      rtbilk = rjbilk                              Bilagskode
7.fb cl2                 eval      rtbilk = w_bilk                              Bilagskode
     cl2                 eval      rttext = rjtext                              Bilagstekst
     cl2                 eval      rtbetb = rjbetb                              Betalingbet
     cl2                 eval      rtbref = rjbref                              Bank-referanse
     cl2                 eval      rtdeck = rjdeck
      *
610  cl2                 eval      rtnivo = *blank
610  cl2                 eval      rtmemb = w_mem1
610  cl2                 time                    rtedat
610  cl2                 time                    rtetim
610  cl2                 eval      rteusr = l_user
6.21 c*l2                 write     rtrapfr
6.22  **Skriver kun ut 'sum' post ved brudd, da den egentlige posten
6.22  **blir lagt ut på detaljnivå
6.22 c*l2                 if         rtbelØ <> rjneto
6.21 cl2N88              write     rtrapfr
6.22 c*l2                endif
      *
      * Oppdater buntrecord
      *
     cl3                 eval      w_sist = w_linj
      *
     cl3                 eval      rsfirm = w_firm
     cl3                 eval      rsbunt = w_bunt
     cl3                 eval      rsstat = *blank
     cl3                 move      rjraar        rsrper
     cl3                 movel     rjrper        rsrper
06014cl3                 movel     20            rsraar
06014cl3                 move      rjraar        rsraar
     cl3                 eval      rsfØrs = w_fØrs
     cl3                 eval      rssist = w_sist
     cl3                 time                    rspdat
     cl3                 eval      rsbsum = w_bsum
     cl3                 eval      rspsum = w_bsum
6.11 c*3                 eval      rspdeb = w_bsum
6.11 cl3                 eval      rspkre = w_dsum
6.11 cl3                 eval      rspdeb = w_bsum
      *
610  cl3                 eval      rsnivo = *blank
610  cl3                 eval      rsmemb = w_mem1
610  cl3                 time                    rsedat
610  cl3                 time                    rsetim
610  cl3                 eval      rseusr = l_user
6.21 c*l3                 write     rbunpfr                                      SKRIV BUNT
6.21 cl3N88              write     rbunpfr                                       SKRIV BUNT
      * Generer xml for videre utskrift
6.30 clr                 if        l_poff = '1'
6.30 clr                 exsr      spoolnbr
6.31 clr                 close     rf122p
6.31 clr                 exsr      xml_create
6.30 clr                 exsr      pdf_preview
6.30  * Avslutt jobbiden
6.30 clr                 call      'AB710R'
6.30 clr                 parm                    l_bach
6.30 clr                 endif
      *
      /eject
      *
     c     rabatt        begsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for kontroll av trukket rabatt                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Leser koderegister og finner rabatter og rabatt-
      * granser for aktuelle betalingsbetingelser
      *
      * Hent betalingsbetingelse
      *
     c                   move      rjbetb        ra03l1_ckod
     c     ra03l1_key    chain     ra03l1                                        betalingsbetingelse
      *
     c                   if        not%found                                    Kode
     c                   goto      notrec                                        finnes
     c                   endif                                                    ikke
      *
      *  Beregn trukket rabatt i %
      *
     c                   if        restua <> *zero
     c                   eval      sjpro  = rabsum * 100 /restua
     c                   else
     c                   eval      sjpro  = 0
     c                   endif
      *
     c                   move      *off          *in61
      *
      * Sjekk om kontantrabatt-1
      *
     c                   if        racrab > *zero
      *
     c                   eval      prosh = 0
     c                   eval      prosl = 0
     c                   eval      prosh = racrab + 0.1                          RABATTGRENSE
     c                   eval      prosl = racrab - 0.1                           +- 10%
      *
     c                   if        sjpro <= prosh
     c                   eval      antdag = racda1
     c                   move      *on           *in61
     c                   goto      datosj
     c                   endif
      *
     c                   endif
      *
      * Sjekk om kontantrabatt-2
      *
     c                   eval      prosh = 0
     c                   eval      prosl = 0
      *
     c                   if        racra2 > *zero
      *
     c                   eval      prosh = racra2 + 0.1
     c                   eval      prosl = racra2 - 0.1
      *
     c                   if        sjpro <= prosh
     c                   eval      antdag = racda2
     c                   move      *on           *in61
     c                   goto      datosj
     c                   endif
      *
     c                   endif
      *
      * Sjekk om kontantrabatt-3
      *
     c                   eval      prosh = 0
     c                   eval      prosl = 0
      *
     c                   if        racra3 > *zero
      *
     c                   eval      prosh = racra3 + 0.1
     c                   eval      prosl = racra3 - 0.1
      *
     c                   if        sjpro <= prosh
     c                   eval      antdag = racda3
     c                   move      *on           *in61
     c                   goto      datosj
     c                   endif
      *
     c                   endif
      *
     c     datosj        tag
      *
      * Beregn antall dager mellom bilagsdato på
      * innbetalingen og forfallsdato på postering
      *
     c                   if        *in61 = *on
      *
     C     rjbdat        subdur    rnfdat        w_antd : *d
      *
     c                   if        w_antd > antdag
     c                   move      *on           *in62
     c                   endif
      *
     c                   endif
      *
     c     notrec        endsr
      *
     c/eject
      *
7.fb  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
7.fb  * Sender kid og sjekker om depositum, evt. oppdaterer
7.fb  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
7.fb c     depositum     begsr
7.fb c                   call      'RF781C '
7.fb c                   parm                    rtfirm
7.fb c                   parm                    rtkidi
7.fb c                   parm                    w_bilk
7.fb  *
7.fb c                   endsr
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.30  * Finner spool som er generert av jobben
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.30 c     spoolnbr      begsr
6.30  /Free
6.30
6.30           QSPRILSP( SPRL0100
6.30                   : %size(SPRL0100)
6.30                   : 'SPRL0100'
6.30                   : errorcode );
6.30  /End-free
6.30  *
6.30 c                   endsr
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.30  * Kaller eget program for generering av xml fil. Eget pgm         *
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.30 c     xml_create    begsr
6.30  *
6.30 c                   eval      splfname2 = *blanks
6.30 c                   eval      splfname3 = *blanks
6.30 c                   eval      jobname2 = *blanks
6.30 c                   eval      jobname3 = *blanks
6.30 c                   eval      username2 = *blanks
6.30 c                   eval      username3 = *blanks
6.30 c                   eval      jobnbr2 = *blanks
6.30 c                   eval      jobnbr3 = *blanks
6.30 c                   eval      splfnbr2 = *zeros
6.30 c                   eval      splfnbr3 = *zeros
6.30 c                   eval      p_btyp = 'OCRINNBETALINGER'
6.30 c                   eval      p_dspl = 'OCR innbetalinger '+
6.30 c                                      %char(w_dato)
6.30 c                   eval      p_refn = %char(w_year)+%char(w_dato)
6.30 c                   eval      p_tagg0 = 'KJOREDATO'
6.30 c                   eval      p_tagg0val = %char(w_dato)
6.30 c                   eval      p_tagg1 = *blanks
6.30 c                   eval      p_tagg1val = *blanks
6.30 c                   eval      p_tagg2 = *blanks
6.30 c                   eval      p_tagg2val = *blanks
6.30 c                   eval      p_tagg3 = *blanks
6.30 c                   eval      p_tagg3val = *blanks
6.30 c                   eval      p_tagg4 = *blanks
6.30 c                   eval      p_tagg4val = *blanks
6.30 c                   eval      p_tagg5 = *blanks
6.30 c                   eval      p_tagg5val = *blanks
6.30 c                   eval      p_tagg6 = *blanks
6.30 c                   eval      p_tagg6val = *blanks
6.30 c                   eval      p_tagg7 = *blanks
6.30 c                   eval      p_tagg7val = *blanks
6.30 c                   eval      p_tagg8 = *blanks
6.30 c                   eval      p_tagg8val = *blanks
6.30 c                   eval      p_tagg9 = *blanks
6.30 c                   eval      p_tagg9val = *blanks
      *
6.30 c                   call      'AP627R'
6.30 c                   PARM                    splfname
6.30 c                   PARM                    jobname
6.30 c                   PARM                    username
6.30 c                   PARM                    jobnbr
6.30 c                   PARM                    splfnbr
6.30 c                   PARM                    splfname2
6.30 c                   PARM                    jobname2
6.30 c                   PARM                    username2
6.30 c                   PARM                    jobnbr2
6.30 c                   PARM                    splfnbr2
6.30 c                   PARM                    splfname3
6.30 c                   PARM                    jobname3
6.30 c                   PARM                    username3
6.30 c                   PARM                    jobnbr3
6.30 c                   PARM                    splfnbr3
6.30 c                   parm                    p_tagg0
6.30 c                   parm                    p_tagg0val
6.30 c                   parm                    p_tagg1
6.30 c                   parm                    p_tagg1val
6.30 c                   parm                    p_tagg2
6.30 c                   parm                    p_tagg2val
6.30 c                   parm                    p_tagg3
6.30 c                   parm                    p_tagg3val
6.30 c                   parm                    p_tagg4
6.30 c                   parm                    p_tagg4val
6.30 c                   parm                    p_tagg5
6.30 c                   parm                    p_tagg5val
6.30 c                   parm                    p_tagg6
6.30 c                   parm                    p_tagg6val
6.30 c                   parm                    p_tagg7
6.30 c                   parm                    p_tagg7val
6.30 c                   parm                    p_tagg8
6.30 c                   parm                    p_tagg8val
6.30 c                   parm                    p_tagg9
6.30 c                   parm                    p_tagg9val
6.30 c                   parm                    l_bach
6.30 c                   parm                    p_btyp
6.30 c                   parm                    p_refn
6.30 c                   parm                    p_dspl
6.30  *
6.30 c                   endsr
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.30  * Subrutiner for XML/jasper integrasjon - Skjermvisning           *
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.30 c     pdf_preview   begsr
6.30 c                   if        l_skje = 1
6.30  * Vi kaller program for launch av PDF i browser
6.30 c                   call      'AP621R'
6.30 c                   parm                    l_bach
6.30  *
6.30 c                   endif
6.30  *
6.30 c                   endsr
6.30  *
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     c     *inzsr        begsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
610  c     *entry        plist
610  c                   parm                    w_memb                         user
610  c                   parm                    w_mem1                         user
      *
     c                   eval      w_linj = 1
     c                   eval      w_fØrs = 2
      *
6.30 c                   open      rf122p
6.30 c                   eval      w_year = *year
6.30 c                   time                    w_dato
      *
      * Legge inn firma
      *
     c                   move      l_firm        w_firm
      *
      *  Hent mva-koder
      *
     c                   move      '3'           ra05l1_ekod
      *
     c     ra05l1_key    chain     ra05l1                                        MVA-
      *
     c                   if        %found                                       Kode
     c                   move      raedat        w_edat
     c                   eval      w_egpr = (raegpr + 100) / 100
     c                   eval      w_epro = (raepro + 100) / 100
     c                   else
     c                   eval      w_egpr = 1
     c                   eval      w_epro = 1
     c                   endif
      *
      *  Hent faste kontonr.
      *
     c     ra04l1_key    chain     ra04l1                             61         faste
      *
     c                   eval      w_bunt = 0
      *
      * Key for kunderegister
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for kundeposteringer
      *
     c     rktrl8_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rktrl8_kund
     c                   kfld                    rktrl8_biln
      *
      * Key for leverandørregister
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for statuskode 4
      *
     c     raa4lu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra4aar
      *
      * Key for file RBUNL1 nummerisk
      *
     c     rbunl1_key    klist
610  c                   kfld                    rbunl1_nivo
610  c                   kfld                    rbunl1_memb
     c                   kfld                    w_firm
     c                   kfld                    w_bunt
      *
      * Key for betalingsbetingelse
      *
     c     ra03l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra03l1_ckod
      *
      * Key for faste konti
      *
     c     ra04l1_key    klist
     c                   kfld                    w_firm
      *
      * Key for mva - koder
      *
     c     ra05l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra05l1_ekod
6.31  *
     c                   endsr
