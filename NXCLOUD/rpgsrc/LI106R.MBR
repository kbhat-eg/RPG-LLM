     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : LI106R
      * Beskrivelse . . : Detaljer på faktura
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * 5.60  270208 TSV  Endringer på vise partsinfo.
6.10  * R6.10 250809 BSA  Utvidet ref. felter fra 20 til 30 posisjoner
6.11  * R6.10 281209 BSA  Lagt ut meldingsnummer
6.12  * R6.10 270110 BSA  Kobler mot bestilling og ordre
6.20  * R6.20 060511 bsa  Skal ikke oppdatere subfile når en har vært inne på en linje
6.21  * R6.20 120312 bsa  Mulighet for utskrift via F6.
6.22  * R6.20 260312 BSA  Utvidet parameterliste til FO701R
6.23  * R6.20 110512 BSA  Mangler i parameterliste til FO701R
6.24  * R6.20 060612 BSA  Prisen for oppdatering skal ta hensyn til tillegg/fradrag
6.25  * R6.20 011014 BSA  Feil felt brukes for presentasjon av forfallsdato
6.26  * R6.20 031014 BSA  Trekker fra rabatt ved utlegg av totaler
6.27  * R6.20 150615 BKV  Lagt inn ekstra test, hvis EDI ikke er fulstendig
6.nu  * R6.30 280514 bof  Utvidelser ihht. nummerutvidelse
6.30  * R6.30 090914 bsa  Legger ut kostverdi
6.31  * R6.30 231215 bsa  Problemer med visning av tall mindre enn 1
6.32  * R6.30 190116 bsa  Fjernet valg for 6,7 og 8, siden dette ikke gir
6.32  *                   mening når oppdatering erstatter det som ligger
6.32  *                   på bestillingen uansett. NB Kun endret i skjermbilde.
6.32  *                   Koden ligger igjen hvis det blir aktuelt å aktivere
6.32  *                   valgene igjen.
      *
7.xx  * R7.00 060319 bkv  GUI tilpassinger
7.01  * 7.00  070319 bkv  Vasker vekk probl Newlook tegn
7.02  * 7.00  150819 bsa  Takler bredere skjermbilder
7.03  * 7.xx  230317 bsa  Fast 14 siffer i GTIN nummer
7.04  * 7.xx  150621 bsa  Ny status pga enheter PCE osv
7.05  *K0000  220621 BSA  Utvidet meldingsnummer fra 6-8 siffer
      *
8.01  *K0000  271221 bsa  Utvider skjermbilde
8.02  *800    290923 ask  Utvidet felt for lev.faknr (bare kompilering)
8.03  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = avslutt
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *
      *
      *  Bruk av linjenummersekvenser (2 første siffer i linjenr LIMLIN)
      *
      *    10 : Hodeinformasjon
      *    20 : Partsinformasjon
      *    30 : Referanser
      *    40 : Transportinformasjon
      *    50 : Linjer
      *    55 : Linjer fra best. (ikke match i EDI-melding)
      *    60 : Tillegg/fradrag hodenivå
      *    65 : Fritkstlinjer på hodenivå
      *    90 : Totaler
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fraa8l1    if   e           k disk    rename(raa8pfr:raa8l1r)
     fmjlol2    if   e           k disk    rename(mjlopfr:mjlol2r)
     fledilr    if   e           k disk    rename(ledipfr:ledilrr)
     fledil1    if   e           k disk    rename(ledipfr:ledil1r)
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flohelr    if   e           k disk    rename(lohepfr:lohelrr)
     flodtl1    if   e           k disk    rename(lodtpfr:lodtl1r)
     flodtlr    if   e           k disk    rename(lodtpfr:lodtlrr)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     frlevl5    if   e           k disk    rename(rlevpfr:rlevl5r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvenlr    if   e           k disk    rename(vvenpfr:vvenlrr)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
6.12 ffodtlr    if   e           k disk    rename(fodtpfr:fodtlrr)
      *
     fli106d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Parameter-inn
      *
     d d_prec          ds
     d  d_firm                        3  0
     d  d_type                        4
7.05 d  d_mnum                        8  0
     d  d_mlin                        6  0
     d  d_kode                        1
      *
      * FAKTURA Hode
      *
     d d_fakh1         ds
     d  d_fh_dtyp                     3
     d  d_fh_fanr                    35
     d  d_fh_fako                     3
     d  d_fh_ldat                      d   datfmt(*iso)
     d  d_fh_best                    20
     d  d_fh_lord                    20
     d  d_fh_lfak                    20
     d  d_fh_sean                    13  0
     d  d_fh_snav                    30
     d  d_fh_fean                    13  0
     d  d_fh_fnav                    30
     d  d_fh_fad1                    30
     d  d_fh_fad2                    30
     d  d_fh_fste                    30
     d  d_fh_forg                    20
     d  d_fh_kref                    20
     d  d_fh_lref                    20
     d  d_fh_valk                     3
     d  d_fh_ffda                      d   datfmt(*iso)
     d  d_fh_levb                     3
     d  d_fh_forf                      d   datfmt(*iso)
      *
      * Tillegg/fradragslinjer
      *
     d d_fakh2         ds
     d  d_fh_tsig                     3
     d  d_fh_tkod                     3
     d  d_fh_ttek                    30
     d  d_fh_tpro                     4  2
     d  d_fh_tbel                     8  2
     d  d_fh_apro                     4  2
      *
      * Fritekst/hodelinje
      *
     d d_fakh3         ds
     d  d_fh_ftxq                     3
     d  d_fh_ftxt                    70
      *
      * Fakturareferanser/hode
      *
     d d_fakh4         ds
     d  d_fh_bstn                    35
     d  d_fh_ordr                    35
     d  d_fh_fakk                    35
     d  d_fh_paks                    35
     d  d_fh_kunr                    35
     d  d_fh_proj                    35
     d  d_fh_lvfa                    35
     d  d_fh_ofav                    35
     d  d_fh_plnr                    35
     d  d_fh_konr                    35
     d  d_fh_kidn                    35
      *
      * Faktura partsinfo./hode
      *
     d d_fakh5         ds
     d  d_fh_part                     3
     d  d_fh_pean                    13  0
     d  d_fh_pnav                    30
     d  d_fh_pad1                    30
     d  d_fh_pad2                    30
     d  d_fh_pste                    30
     d  d_fh_porg                    20
      *
      * Faktura transport/lev. - hode
      *
     d d_fakh6         ds
     d  d_fh_trmo                     3
     d  d_fh_trna                    35
     d  d_fh_lebe                     3
      *
      * FAKTURA Linje
      *
     d d_fakl          ds
     d  d_fl_llin                     6  0
7.03 d* d_fl_vean                    13  0
7.03 d  d_fl_vean                    14  0
     d  d_fl_vnob                     8  0
     d  d_fl_vlev                    15
     d  d_fl_tek1                    35
     d  d_fl_tek2                    35
     d  d_fl_bkva                    15  3
     d  d_fl_benh                     3
     d  d_fl_lkva                    15  3
     d  d_fl_lenh                     3
     d  d_fl_fkva                    15  3
     d  d_fl_fenh                     3
     d  d_fl_bnet                    15  2
     d  d_fl_bbto                    15  2
     d  d_fl_prkv                     3
     d  d_fl_pris                    15  3
     d  d_fl_penh                     3
     d  d_fl_prpr                     9  0
     d  d_fl_bnum                    20
     d  d_fl_blin                     6  0
     d  d_fl_mvap                     4  2
     d  d_fl_sig1                     3
     d  d_fl_kod1                     3
     d  d_fl_bel1                     9  2
     d  d_fl_pro1                     5  2
     d  d_fl_sig2                     3
     d  d_fl_kod2                     3
     d  d_fl_bel2                     9  2
     d  d_fl_pro2                     5  2
     d  d_fl_sig3                     3
     d  d_fl_kod3                     3
     d  d_fl_bel3                     9  2
     d  d_fl_pro3                     5  2
     d  d_fl_sig4                     3
     d  d_fl_kod4                     3
     d  d_fl_bel4                     9  2
     d  d_fl_pro4                     5  2
     d  d_fl_sig5                     3
     d  d_fl_kod5                     3
     d  d_fl_bel5                     9  2
     d  d_fl_pro5                     5  2
     d  d_fl_sig6                     3
     d  d_fl_kod6                     3
     d  d_fl_bel6                     9  2
     d  d_fl_pro6                     5  2
      *
      * Fakturatotallinjer
      *
     d d_fakt          ds
     d  d_ft_ftot                    15  2
     d  d_ft_teks                    15  2
     d  d_ft_tmva                    15  2
     d  d_ft_mvgr                    15  2
     d  d_ft_veks                    15  2
     d  d_ft_vink                    15  2
     d  d_ft_stil                    15  2
     d  d_ft_sfra                    15  2
     d  d_ft_oavx                    15
     d  d_ft_oavr                    15  2 overlay(d_ft_oavx:1)
      *
      * Definering av variabler i nøkler
      *
     d ledilr_type     s                   like(litype)
     d ledilr_mnum     s                   like(limnum)
     d ledilr_mlin     s                   like(limlin)
     d ledil1_type     s                   like(litype)
     d ledil1_mnum     s                   like(limnum)
7.05 d ledil1_mlin     s                   like(limlin)
     d lohel1_numm     s                   like(lonumm)
     d lohel1_suff     s                   like(losuff)
     d lohelr_numm     s                   like(lonumm)
     d lodtl1_numm     s                   like(ldnumm)
     d lodtl1_suff     s                   like(ldsuff)
     d lodtl1_line     s                   like(ldline)
     d lodtlr_numm     s                   like(ldnumm)
     d lodtlr_suff     s                   like(ldsuff)
     d lodtlr_line     s                   like(ldline)
     d rlevl1_levr     s                   like(rllevr)
     d rlevl5_eanl     s                   like(rleanl)
     d vvenlr_vare     s                   like(vevare)
     d vvarl1_vare     s                   like(vvvare)
     d vvenlr_enhe     s                   like(veenhe)
     d mjlol2_eann     s                   like(mjeann)
     d votyl1_otyp     s                   like(vaotyp)
6.12 d fodtlr_numm     s                   like(fdnumm)
6.12 d fodtlr_suff     s                   like(fdsuff)
6.12 d fodtlr_line     s                   like(fdline)
      *
6.12 d w_anta          s                   like(ldanta)
6.12 d w_inkp          s                   like(ldipny)
6.22 d w_kopr          s                   like(ldipny)
6.12 d w_numm          s                   like(fdnumm)
6.12 d w_suff          s                   like(fdsuff)
6.12 d w_csta          s              1
6.21 d w_prec          s             45
7.01 d w_len           s              3  0
7.01 d w_teller        s              3  0
      *
      * Definering av variable i parametre
      *
7.05 d p_parm          s             22
      *
      * Definering av variable
      *
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
6.12 d b_best          s              1    inz(*off)
6.12 d b_ordr          s              1    inz(*off)
      *
     d w_firm          s                   like(lifirm)
     d w_line          s                   like(ldline)
     d w_stat          s             12
     d w_mvap          s              6  2
     d w_mvat          s              4  4
     d w_mvaf          s             10  9
     d w_tell          s              1  0
     d w_sign          s              2
     d w_tilk          s              3
     d w_tilt          s             35
     d w_frad          s              9  2
     d w_ttlg          s              9  2
     d w_tfra          s              9  2
     d w_pris          s             15  3
6.24 d w_npri          s             15  3
     d w_omr1          s                   like(veomre)
     d w_omr2          s                   like(veomre)
     d w_lnav          s                   like(rlnavn)
     d w_val2          s              2  0
     d w_ipct          s              2  0
     d w_lpro          s              1
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.60  * For visning NAD - partsinfo
 .    * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
 .    * w_n_ini: Hvilke parter(3035) skal vises og i hvilken rekkefølge.
 .    *
 .    * w_n_ine: Ditto ean-adr.info: 0=Nei
 .    *          (hvis navn blankt)  1=Leverandører (rlevl5 / lean)
 .    *          (i meldinga prøv )  2=Hovednummer  (raa8l1 / hean)
 .    *          (å hente navn her)  3=Logistikk    (mjlol2 / loge)
 .    *                              4=Både leverandør og hovednummer
 .    *
 .    * w_n_ina: Ditto vise adresse: 0=Bare navn
 .    *          (vise fra melding ) 1=Ja
 .    *          (navn + adr1,adr2,) 2=Ja hvis Ean mangler
 .    *
 .   d w_n_ini         s             18    inz('BYOBDPIVIIUDSUSFCA')
 .   d w_n_ine         s             18    inz('2 0 4 2 1 4 1 1 1 ')
 .   d w_n_ina         s             18    inz('1 0 2 0 0 2 0 0 0 ')
 .    *
 .    * Arbeidsvariable partsinfo
 .   d w_ine           s              1    inz('0')
 .   d w_ina           s              1    inz('0')
 .   d w_hdg           s                   like(B7HDG01)
 .   d w_num           s                   like(B7NUM01)
 .   d w_txt           s                   like(B7TXT01)
 .   d w_ptr           s              3  0 inz(0)
 .   d w_pt2           s              3  0
 .   d dummy           s              2  0
 .   d w_adri          s              1  0 inz(0)
 .   d w_init          s              1    inz(*off)
 .   d w_n_wrk         s              2
5.60 d wll             s              6  0 dim(9)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Definering av konstanter
      *
8.01 d c_sfil          s              2  0 inz(13)
      *
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_srrn - Relativt recordnummer i subfile
      * w_spge - Sidestørrelse i subfile (page)
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * w_virn - Vis den side som inneholder denne record.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * B5WIN  - B5 Vindu for viseing av detaljer
      * B7WIN  - B7 Vindu for vising av partsinformasjon
      * B8WIN  - B8 Vindu for vising av tillegg/fradrag
      * B9WIN  - B9 Vindu for vising av ordretotaler
      * B11Win - B11 Vindu for vising av referanseinformasjon
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_pgm
6.21 c                   when      *inkf
6.21 c                   exsr      skr_dok
6.21 c                   goto      b2tagb
     c                   when      *inkg
     c                   exsr      vis_parter
     c                   goto      b2tagb
     c                   when      *inkh
     c                   exsr      vis_tillegg
     c                   goto      b2tagb
     c                   when      *inki
     c                   exsr      vis_total
     c                   goto      b2tagb
     c                   when      *inkj
     c                   exsr      vis_best
     c                   goto      b2tagb
     c                   when      *inkk
     c                   exsr      vis_refn
     c                   goto      b2tagb
     c                   when      *inkn
     c                   exsr      oppd_ordre
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
      * Beholder eksisterende posisjonering i subfilen
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      w_virn = w_srrn
      *
      * Se på detaljer
      *
     c                   if        b1valg = 5
     c                   exsr      xk5win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Søke på bestillingsliner
      *
     c                   if        b1valg = 9
     c                   exsr      xk9win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
6.12  * Opddater antall på bestilling og ordre
6.12  *
6.12 c                   if        b1valg = 6
6.12  * Slå opp og finn linje som er valgt
6.12 c                   exsr      meld_linje
6.12 c                   eval      lodtlr_line = liline
6.12 c                   eval      b_best = *off
6.12 c                   eval      b_ordr = *off
6.12 c                   exsr      best_linje
6.12  * Vi oppdaterer linja hvis det er avviken kvantum og bestilling ble funnet
6.12 c                   if        b_best = *on and
6.12 c                             ldanta <> d_fl_fkva
6.12 c                   eval      w_numm = ldnumm
6.12 c                   eval      w_suff = ldsuff
6.12 c                   eval      w_line = ldline
6.12 c                   eval      w_anta = d_fl_fkva
6.12 c                   eval      w_inkp = 0
6.12 c                   eval      w_csta = *blank
6.12 c                   call      'LO701R'
6.12 c                   parm                    w_firm
6.12 c                   parm                    w_numm
6.12 c                   parm                    w_suff
6.12 c                   parm                    w_line
6.12 c                   parm                    w_anta
6.12 c                   parm                    w_inkp
6.12 c                   parm                    w_csta
6.12  *
6.12  * Kaller program for oppdatering av summer på bestilling
6.12  *
6.12 c                   call      'LO700R'
6.12 c                   parm                    w_firm
6.12 c                   parm                    w_numm
6.12 c                   parm                    w_suff
6.12 c                   endif
6.12  *
6.12  * Hvis tilhørende ordrenummer oppdateres denne også
6.12  *
6.12 c                   if        b_ordr = *on and
6.12 c                             fdanta <> d_fl_fkva
6.12 c                   eval      w_numm = ldornu
6.12 c                   eval      w_suff = ldorsu
6.12 c                   eval      w_line = ldorli
6.12 c                   eval      w_anta = d_fl_fkva
6.12 c                   eval      w_inkp = 0
6.22 c                   eval      w_kopr = 0
6.12 c                   eval      w_csta = *blank
6.12 c                   call      'FO701R'
6.12 c                   parm                    w_firm
6.12 c                   parm                    w_numm
6.12 c                   parm                    w_suff
6.12 c                   parm                    w_line
6.12 c                   parm                    w_anta
6.12 c                   parm                    w_inkp
6.22 c                   parm                    w_kopr
6.12 c                   parm                    w_csta
6.12  *
6.12  * Kaller program for oppdatering av summer på ordreheading
6.12  *
6.12 c                   call      'FO700R'
6.12 c                   parm                    w_firm
6.12 c                   parm                    w_numm
6.12 c                   parm                    w_suff
6.12  *
6.12 c                   endif
6.12  *
6.12 c                   eval      b1valg = 0
6.12 c                   update    b1sfl
6.12 c                   iter
6.12 c                   endif
6.12  *
6.12  * Opddater pris på bestilling og ordre(kalkulerer kostverdi)
6.12  *
6.12 c                   if        b1valg = 7
6.12  * Slå opp og finn linje som er valgt
6.12 c                   exsr      meld_linje
6.12 c                   eval      lodtlr_line = liline
6.12 c                   exsr      best_linje
6.24  * Kalkuler pris det skal sammenlignes med
6.24 c                   exsr      kal_ntopr
6.12  * Vi oppdaterer hvis avvikende pris
6.12 c                   if        b_best = *on and
6.24 c**                           ldipny <> d_fl_pris
6.24 c                             ldipny <> w_npri
6.12 c                   eval      w_numm = ldnumm
6.12 c                   eval      w_suff = ldsuff
6.12 c                   eval      w_line = ldline
6.12 c                   eval      w_anta = 0
6.24 c**                 eval      w_inkp = d_fl_pris
6.24 c                   eval      w_inkp = w_npri
6.12 c                   eval      w_csta = *blank
6.12 c                   call      'LO701R'
6.12 c                   parm                    w_firm
6.12 c                   parm                    w_numm
6.12 c                   parm                    w_suff
6.12 c                   parm                    w_line
6.12 c                   parm                    w_anta
6.12 c                   parm                    w_inkp
6.12 c                   parm                    w_csta
6.12  *
6.12  * Kaller program for oppdatering av summer på bestilling
6.12  *
6.12 c                   call      'LO700R'
6.12 c                   parm                    w_firm
6.12 c                   parm                    w_numm
6.12 c                   parm                    w_suff
6.12 c                   endif
6.12  *
6.12  * Hvis tilhørende ordrenummer oppdateres denne også
6.12  *
6.12 c                   if        b_ordr = *on and
6.24 c**                           fdinpr <> d_fl_pris
6.24 c                             fdinpr <> w_npri
6.12 c                   eval      w_numm = ldornu
6.12 c                   eval      w_suff = ldorsu
6.12 c                   eval      w_line = ldorli
6.12 c                   eval      w_anta = 0
6.23 c                   eval      w_kopr = 0
6.24 c**                 eval      w_inkp = d_fl_pris
6.24 c                   eval      w_inkp = w_npri
6.12 c                   eval      w_csta = *blank
6.12 c                   call      'FO701R'
6.12 c                   parm                    w_firm
6.12 c                   parm                    w_numm
6.12 c                   parm                    w_suff
6.12 c                   parm                    w_line
6.12 c                   parm                    w_anta
6.12 c                   parm                    w_inkp
6.23 c                   parm                    w_kopr
6.12 c                   parm                    w_csta
6.12  *
6.12  * Kaller program for oppdatering av summer på ordreheading
6.12  *
6.12 c                   call      'FO700R'
6.12 c                   parm                    w_firm
6.12 c                   parm                    w_numm
6.12 c                   parm                    w_suff
6.12  *
6.12 c                   endif
6.12  *
6.12 c                   eval      b1valg = 0
6.12 c                   update    b1sfl
6.12 c                   iter
6.12 c                   endif
6.12  *
6.12  * Opddater både pris og antall på bestilling og ordre
6.12  *
6.12 c                   if        b1valg = 8
6.12  * Slå opp og finn linje som er valgt
6.12 c                   exsr      meld_linje
6.12 c                   eval      lodtlr_line = liline
6.12 c                   exsr      best_linje
6.12  * Vi oppdaterer både pris og antall
6.12 c                   if        b_best = *on
6.24  * Kalkuler pris det skal sammenlignes med
6.24 c                   exsr      kal_ntopr
6.12  *
6.12 c                   eval      w_numm = ldnumm
6.12 c                   eval      w_suff = ldsuff
6.12 c                   eval      w_line = ldline
6.12 c                   eval      w_anta = d_fl_fkva
6.24 c**                 eval      w_inkp = d_fl_pris
6.24 c                   eval      w_inkp = w_npri
6.12 c                   eval      w_csta = *blank
6.12 c                   call      'LO701R'
6.12 c                   parm                    w_firm
6.12 c                   parm                    w_numm
6.12 c                   parm                    w_suff
6.12 c                   parm                    w_line
6.12 c                   parm                    w_anta
6.12 c                   parm                    w_inkp
6.12 c                   parm                    w_csta
6.12  *
6.12  * Kaller program for oppdatering av summer på bestilling
6.12  *
6.12 c                   call      'LO700R'
6.12 c                   parm                    w_firm
6.12 c                   parm                    w_numm
6.12 c                   parm                    w_suff
6.12 c                   endif
6.12  *
6.12  * Hvis tilhørende ordrenummer oppdateres denne også
6.12  *
6.12 c                   if        b_ordr = *on
6.12  *
6.12 c                   eval      w_numm = ldornu
6.12 c                   eval      w_suff = ldorsu
6.12 c                   eval      w_line = ldorli
6.12 c                   eval      w_anta = d_fl_fkva
6.24 c**                 eval      w_inkp = d_fl_pris
6.24 c                   eval      w_inkp = w_npri
6.23 c                   eval      w_kopr = 0
6.12 c                   eval      w_csta = *blank
6.12 c                   call      'FO701R'
6.12 c                   parm                    w_firm
6.12 c                   parm                    w_numm
6.12 c                   parm                    w_suff
6.12 c                   parm                    w_line
6.12 c                   parm                    w_anta
6.12 c                   parm                    w_inkp
6.23 c                   parm                    w_kopr
6.12 c                   parm                    w_csta
6.12  *
6.12  * Kaller program for oppdatering av summer på ordreheading
6.12  *
6.12 c                   call      'FO700R'
6.12 c                   parm                    w_firm
6.12 c                   parm                    w_numm
6.12 c                   parm                    w_suff
6.12  *
6.12 c                   endif
6.12  *
6.12 c                   eval      b1valg = 0
6.12 c                   update    b1sfl
6.12 c                   iter
6.12 c                   endif
      *
     c                   enddo
      *
6.12 c*                  if        b1valg = 6 or
6.12 c*                            b1valg = 7 or
6.12 c*                            b1valg = 8
6.20 c**                 eval      ledil1_mlin = 500000
6.20 c**   ledil1_key    setll     ledil1
6.20 c**                 exsr      clr_subfile
6.20 c**                 exsr      crt_subfile
6.12 c**                 endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.60  * Subrutine for å vise partsinformasjon
 .    * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
 .    *
 .   c     vis_parter    begsr
 .    *
 .    * initiering
 .   c                   eval      w_hdg = *blanks
 .   c                   eval      w_num = *zero
 .   c                   eval      w_txt = *blanks
 .   c                   eval      w_ptr = 0
 .   c                   eval      w_init = *on
 .   c     1             do        11            dummy
 .   c                   exsr      sr_ins_b7lin
 .   c                   enddo
 .   c                   eval      w_init = *off
 .   c                   eval      w_hdg = *blanks
 .   c     1             DO        9             w_pt2
 .   c                   eval      wll(w_pt2) = 0
 .   c                   enddo
 .    *
 .    * Bygger sortert tabell for "kfld  limlin"
 .   c                   eval      ledilr_mlin = 200000
 .   c     ledilr_key    setll     ledilr
 .   c                   read      ledilr
 .   c                   dow       limlin < 300000 and
 .   c                             not %eof
 .   c                   eval      d_fakh5 = limeld
 .   c     1             DO        9             w_pt2
 .   c                   eval      w_n_wrk=%SUBST(w_n_ini:(w_pt2*2)-1:2)
 .   c                   if        w_n_wrk = d_fh_part
 .   c                   eval      wll(w_pt2) = limlin
 .   c                   endif
 .   c                   enddo
 .   c                   read      ledilr
 .   c                   enddo
 .    *
 .    * Henter partsinfo i valgt rekkefølge og legger ut til bilde
 .   c                   eval      w_ptr = 0
 .   c     1             DO        9             w_pt2
 .   c                   if        wll(w_pt2) = 0
 .   c                   iter
 .   c                   endif
 .   c                   eval      ledilr_mlin = wll(w_pt2)
 .   c     ledilr_key    chain     ledilr
 .   c                   if        %found
 .   c                   eval      d_fakh5 = limeld
 .    *
 .   c                   select
 .    * BY - kjøper
 .   c                   when      d_fh_part = 'BY '
 .   c                   eval      w_hdg = 'Kjøper  . . . . .'
 .    * OB - Bestiller
 .   c                   when      d_fh_part = 'OB '
 .   c                   eval      w_hdg = 'Bestiller . . . .'
 .    * DP - Mottager
 .   c                   when      d_fh_part = 'DP '
 .   c                   eval      w_hdg = 'Varemottager  . .'
 .    * IV - Fakturamottager
 .   c                   when      d_fh_part = 'IV '
 .   c                   eval      w_hdg = 'Fakturamottager .'
 .    * II - Fakturautsteder
 .   c                   when      d_fh_part = 'II '
 .   c                   eval      w_hdg = 'Fakturautsteder .'
 .    * UD - Sluttmottager
 .   c                   when      d_fh_part = 'UD '
 .   c                   eval      w_hdg = 'Sluttmottager . .'
 .    * SU - leverandør
 .   c                   when      d_fh_part = 'SU '
 .   c                   eval      w_hdg = 'Leverandør/selger'
 .    * SF - hentested
 .   c                   when      d_fh_part = 'SF '
 .   c                   eval      w_hdg = 'Hentested . . . .'
 .    * CA - Transportør
 .   c                   when      d_fh_part = 'CA '
 .   c                   eval      w_hdg = 'Transportør . . .'
 .   c                   endsl
 .    *
 .    * Evt Ean-oppslag hvis gitt og navn er blankt
 .   c                   eval      w_num = d_fh_pean
 .   c                   eval      w_txt = d_fh_pnav
 .   c                   if        w_txt = *blank
 .   c                   eval      w_ine  =%SUBST(w_n_ine:(w_pt2*2)-1:1)
 .   c                   select
 .   c     w_ine         wheneq    '1'
 .   c                   exsr      sr_les_lean
 .   c                   if        w_txt = '*blank'
 .   c                   eval      w_txt = 'Ikke i lokasjon-leverandører'
 .   c                   endif
 .   c     w_ine         wheneq    '2'
 .   c                   exsr      sr_les_hean
 .   c                   if        w_txt = '*blank'
 .   c                   eval      w_txt = 'Ikke i lokasjon-hovednummer'
 .   c                   endif
 .   c     w_ine         wheneq    '3'
 .   c                   exsr      sr_les_loge
 .   c                   if        w_txt = '*blank'
 .   c                   eval      w_txt = 'Ikke i lokasjon-logistikk'
 .   c                   endif
 .   c     w_ine         wheneq    '4'
 .   c                   exsr      sr_les_lean
 .   c                   if        w_txt = '*blank'
 .   c                   exsr      sr_les_hean
 .   c                   endif
 .   c                   endsl
 .   c                   if        w_txt = '*blank'
 .   c                   eval      w_txt = 'Ikke i lokasjons-tabeller"'
 .   c                   endif
 .   c                   endif
 .    *
 .    * Legg ut heading, ean og navn til partsinfo - bildet.
 .   c                   exsr      sr_ins_b7lin
 .    *
 .    * Legg evt ut utvidet adresse info til partsinfo - bildet.
 .   c                   eval      w_ina  =%SUBST(w_n_ina:(w_pt2*2)-1:1)
 .   c                   if        w_ina = '1' or
 .   c                             (w_ina = '2' and w_num = 0)
 .   c                   eval      w_txt = d_fh_pad1
 .   c                   exsr      sr_ins_b7lin
 .   c                   eval      w_txt = d_fh_pad2
 .   c                   exsr      sr_ins_b7lin
 .   c                   eval      w_txt = d_fh_pste
 .   c                   exsr      sr_ins_b7lin
 .   c                   eval      w_txt = d_fh_porg
 .   c                   exsr      sr_ins_b7lin
 .   c                   endif
 .    *
 .   c                   endif
 .   c                   enddo
 .    *
 .    * vis bildet
 .   c                   exfmt     b7win
5.60 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.60  * Subrutine les EAN - Leverandør
 .    * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
 .   c     sr_les_lean   begsr
 .   c                   eval      rlevl5_eanl = d_fh_pean
 .   c                   eval      w_txt = '*blank'
 .   c     rlevl5_key    chain     rlevl5
 .   c                   if        %found
 .   c                   eval      w_txt = rlnavn
 .   c                   endif
5.60 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.60  * Subrutine les EAN - Hovednummer
 .    * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
 .   c     sr_les_hean   begsr
 .   c     raa8l1_key    chain     raa8l1
 .   c                   if        not %found or ra8ean <> d_fh_pean
 .   c                   eval      w_txt = '*blank'
 .   c                   else
 .   c                   eval      w_txt = l_fnav
 .   c                   endif
5.60 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.60  * Subrutine les EAN - Logistikk
 .    * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
 .   c     sr_les_loge   begsr
 .   c                   eval      mjlol2_eann = d_fh_pean
 .   c                   eval      w_txt = '*blank'
 .   c     mjlol2_key    chain     mjlol2
 .   c                   if        %found
 .   c                   eval      w_txt = mjsted
 .   c                   endif
5.60 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.60  * Subrutine for å legge inn linje på bilde partsinformasjon
 .    * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
 .   c     sr_ins_b7lin  begsr
 .   c                   if        w_hdg = *blanks and
 .   c                             w_num = *zero   and
 .   c                             w_txt = *blanks and
 .   c                             w_init = *off
 .   c                   goto      sr_ins_b7end
 .   c                   endif
 .    *
 .   c                   eval      w_ptr = w_ptr + 1
 .   c                   if        w_ptr > 11
 .   c                   goto      sr_ins_b7end
 .   c                   endif
 .    *
 .   c                   select
 .   c     w_ptr         wheneq    1
 .   c                   eval      B7HDG01 = w_hdg
 .   c                   eval      B7NUM01 = w_num
 .   c                   eval      B7TXT01 = w_txt
 .   c     w_ptr         wheneq    2
 .   c                   eval      B7HDG02 = w_hdg
 .   c                   eval      B7NUM02 = w_num
 .   c                   eval      B7TXT02 = w_txt
 .   c     w_ptr         wheneq    3
 .   c                   eval      B7HDG03 = w_hdg
 .   c                   eval      B7NUM03 = w_num
 .   c                   eval      B7TXT03 = w_txt
 .   c     w_ptr         wheneq    4
 .   c                   eval      B7HDG04 = w_hdg
 .   c                   eval      B7NUM04 = w_num
 .   c                   eval      B7TXT04 = w_txt
 .   c     w_ptr         wheneq    5
 .   c                   eval      B7HDG05 = w_hdg
 .   c                   eval      B7NUM05 = w_num
 .   c                   eval      B7TXT05 = w_txt
 .   c     w_ptr         wheneq    6
 .   c                   eval      B7HDG06 = w_hdg
 .   c                   eval      B7NUM06 = w_num
 .   c                   eval      B7TXT06 = w_txt
 .   c     w_ptr         wheneq    7
 .   c                   eval      B7HDG07 = w_hdg
 .   c                   eval      B7NUM07 = w_num
 .   c                   eval      B7TXT07 = w_txt
 .   c     w_ptr         wheneq    8
 .   c                   eval      B7HDG08 = w_hdg
 .   c                   eval      B7NUM08 = w_num
 .   c                   eval      B7TXT08 = w_txt
 .   c     w_ptr         wheneq    9
 .   c                   eval      B7HDG09 = w_hdg
 .   c                   eval      B7NUM09 = w_num
 .   c                   eval      B7TXT09 = w_txt
 .   c     w_ptr         wheneq    10
 .   c                   eval      B7HDG10 = w_hdg
 .   c                   eval      B7NUM10 = w_num
 .   c                   eval      B7TXT10 = w_txt
 .   c     w_ptr         wheneq    11
 .   c                   eval      B7HDG11 = w_hdg
 .   c                   eval      B7NUM11 = w_num
 .   c                   eval      B7TXT11 = w_txt
 .   c                   endsl
 .    *
 .   c                   clear                   w_hdg
 .   c                   clear                   w_num
 .   c                   clear                   w_txt
5.60 c     sr_ins_b7end  endsr
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å vise tillegg- /fradragsinfo
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     vis_tillegg   begsr
      *
     c                   eval      b8tek0 = *blank
     c                   eval      b8tek1 = *blank
     c                   eval      b8tek2 = *blank
     c                   eval      b8tek3 = *blank
     c                   eval      b8tek4 = *blank
     c                   eval      b8tek5 = *blank
     c                   eval      b8tek6 = *blank
     c                   eval      b8tek7 = *blank
     c                   eval      b8bel0 = 0
     c                   eval      b8bel1 = 0
     c                   eval      b8bel2 = 0
     c                   eval      b8bel3 = 0
     c                   eval      b8bel4 = 0
     c                   eval      b8bel5 = 0
     c                   eval      b8bel6 = 0
     c                   eval      b8bel7 = 0
      *
     c                   eval      ledilr_mlin = 600000
     c     ledilr_key    setll     ledilr
     c                   eval      w_tell = 0
     c                   read      ledilr
      *
     c                   dow       limlin < 650000 and
     c                             not %eof
      *
     c                   eval      d_fakh2 = limeld
     c                   eval      w_tell = w_tell + 1
     c                   if        d_fh_tsig = 'A  '
     c                   eval      d_fh_tbel = d_fh_tbel * -1
     c                   endif
      *
     c                   select
      *
     c                   when      w_tell = 1
     c                   eval      b8tek0 = d_fh_ttek
     c                   eval      b8bel0 = d_fh_tbel
      *
     c                   when      w_tell = 2
     c                   eval      b8tek1 = d_fh_ttek
     c                   eval      b8bel1 = d_fh_tbel
      *
     c                   when      w_tell = 3
     c                   eval      b8tek2 = d_fh_ttek
     c                   eval      b8bel2 = d_fh_tbel
      *
     c                   when      w_tell = 4
     c                   eval      b8tek3 = d_fh_ttek
     c                   eval      b8bel3 = d_fh_tbel
      *
     c                   when      w_tell = 5
     c                   eval      b8tek4 = d_fh_ttek
     c                   eval      b8bel4 = d_fh_tbel
      *
     c                   when      w_tell = 6
     c                   eval      b8tek5 = d_fh_ttek
     c                   eval      b8bel5 = d_fh_tbel
      *
     c                   when      w_tell = 7
     c                   eval      b8tek6 = d_fh_ttek
     c                   eval      b8bel6 = d_fh_tbel
      *
     c                   when      w_tell = 8
     c                   eval      b8tek7 = d_fh_ttek
     c                   eval      b8bel7 = d_fh_tbel
     c                   endsl
      *
     c                   read      ledilr
     c                   enddo
      *
     c                   exfmt     b8win
      *
     c     end_tillegg   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å vise beløpsinfo
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     vis_total     begsr
      *
     c                   eval      b9tek0 = 'Sum varelinjer (eks.til/fra)  '
     c                   eval      b9tek1 = 'Sum varelinjer (ink.til/fra)  '
     c                   eval      b9tek2 = 'Sum alle tillegg faktura      '
     c                   eval      b9tek3 = 'Sum alle fradrag faktura      '
     c                   eval      b9tek4 = 'Netto total (eks.mva.)        '
     c                   eval      b9tek5 = 'Mva grunnlag                  '
     c                   eval      b9tek6 = 'Total mva.                    '
     c                   eval      b9tek7 = 'Øreavrunding                  '
     c                   eval      b9tek8 = 'Fakturatotal                  '
      *
     c                   eval      ledilr_mlin = 900001
     c     ledilr_key    chain     ledilr
     c                   if        not %found
     c                   eval      b9bel0 = 0
     c                   eval      b9bel1 = 0
     c                   eval      b9bel2 = 0
     c                   eval      b9bel3 = 0
     c                   eval      b9bel4 = 0
     c                   eval      b9bel5 = 0
     c                   eval      b9bel6 = 0
     c                   eval      b9bel7 = 0
     c                   else
     c                   eval      d_fakt = limeld
     c                   eval      b9bel0 = d_ft_veks
     c                   eval      b9bel1 = d_ft_vink
     c                   eval      b9bel2 = d_ft_stil
     c                   eval      b9bel3 = d_ft_sfra * -1
     c                   eval      b9bel4 = d_ft_teks
     c                   eval      b9bel5 = d_ft_mvgr
     c                   eval      b9bel6 = d_ft_tmva
      *
     c                   if        d_ft_oavx <> *blank
     c                   eval      b9bel7 = d_ft_oavr
     c                   else
     c                   eval      b9bel7 = *zero
     c                   endif
      *
     c                   eval      b9bel8 = d_ft_ftot
     c                   endif
      *
     c                   exfmt     b9win
      *
     c     end_total     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for visning av bestilling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     vis_best      begsr
      *
     c                   eval      w_lpro = *blank
      *
     c                   if        lohel1_numm <> 0
     c                   call      'LO500R'
     c                   parm                    lohel1_numm
     c                   parm                    w_lpro
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å vise referanseinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     vis_refn      begsr
      *
     c                   eval      b11vrf = *blank
     c                   eval      b11drf = *blank
     c                   eval      b11bst = *blank
     c                   eval      b11ord = *blank
     c                   eval      b11fak = *blank
     c                   eval      b11pak = *blank
     c                   eval      b11kun = *blank
     c                   eval      b11pro = *blank
     c                   eval      b11lvf = *blank
     c                   eval      b11ofa = *blank
     c                   eval      b11pln = *blank
     c                   eval      b11kon = *blank
     c                   eval      b11kid = *blank
      *
     c                   eval      b11vrf = b2vref
     c                   eval      b11drf = b2dref
     c                   eval      b11fak = b2fakn
     c                   eval      b11bst = b2besn
      *
     c                   eval      ledilr_mlin = 300001
     c     ledilr_key    chain     ledilr
      *
     c                   if        %found
     c                   eval      d_fakh4 = limeld
      *
     c                   if        b11fak = *blank
     c                   eval      b11fak = d_fh_fakk
     c                   endif
     c                   if        b11bst = *blank
     c                   eval      b11bst = d_fh_bstn
     c                   endif
      *
     c                   eval      b11ord = d_fh_ordr
     c                   eval      b11pak = d_fh_paks
     c                   eval      b11kun = d_fh_kunr
     c                   eval      b11pro = d_fh_proj
     c                   eval      b11lvf = d_fh_lvfa
     c                   eval      b11ofa = d_fh_ofav
     c                   eval      b11pln = d_fh_plnr
     c                   eval      b11kon = d_fh_konr
     c                   eval      b11kid = d_fh_kidn
      *
     c                   endif
      *
     c                   exfmt     b11win
      *
     c     end_refn      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppkall av ordreoppdatering
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_ordre    begsr
      *
     c*                  eval      w_val2 = 2
     c*                  call      'FO100R'
     c*                  parm                    w_val2
     c*                  parm                    loornr
     c*                  parm                    loorsu
      *
     c                   eval      w_lpro = *blank
      *
     c                   call      'FO500R'
     c                   parm                    loornr
     c                   parm                    w_lpro
      *
     c     end_ordre     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å vise detaljer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk5win        begsr
      *
     c                   exsr      meld_linje
     c                   eval      lodtlr_suff = lisuff
     c                   eval      lodtlr_line = liline
      *
     c                   exsr      status_linje
     c                   eval      b5stat = w_stat
      *
     c                   exsr      best_linje
      *
     c                   eval      b5linn = ldline
     c                   eval      b5llin = d_fl_llin
     c                   eval      b5eann = d_fl_vean
     c                   eval      b5nobb = d_fl_vnob
     c                   if        ldvare <> *blank
     c                   movel     ldvare        b5evnr
     c                   else
     c                   eval      b5evnr = *zero
     c                   endif
      *
     c                   eval      b5lvnr = d_fl_vlev
     c                   eval      b5tek1 = d_fl_tek1
     c                   eval      b5tek2 = d_fl_tek2
     c                   eval      b5bkva = ldanta
     c                   eval      b5benh = ldenh1
     c                   eval      b5bpen = ldenh1
     c                   eval      b5oenh = ldenh1
     c                   eval      b5bken = ldenh1
     c                   eval      b5lkva = d_fl_fkva
     c                   eval      b5lenh = d_fl_fenh
     c                   eval      b5bpri = ldipny
     c                   eval      b5bkos = ldkpny
     c*                  eval(h)   b5bsum = ldkpny * ldanta
6.12 c                   eval      b5fkva = fdanta
6.12 c                   eval      b5fkos = fdkopr
6.12 c                   eval      b5fenh = fdenh1
6.12 c                   move      ldnumm        b5bnum
6.12 c                   move      fdnumm        b5onum
      *
      * Prisenhet?
      *
     c                   if        d_fl_penh <> *blank
     c                   eval      b5nenh = d_fl_penh
     c                   eval      b5penh = d_fl_penh
     c                   else
     c                   eval      b5nenh = d_fl_lenh
     c                   eval      b5penh = d_fl_lenh
     c                   endif
      *
      * Tillegg og fradrag ?
      *
     c                   exsr      tilfra_linje
      *
      * Nobb pris / netto pris ?
      *
     c                   eval      b5npri = *zero
     c                   eval      b5lpri = *zero
      *
     c                   if        d_fl_prkv = 'AAB'
     c                   eval      b5npri = d_fl_pris
     c                   eval      b5lpri = d_fl_pris + b5till - b5frad
     c                   else
     c                   if        d_fl_prkv = 'AAA'
     c                   eval      b5npri = *zero
     c                   eval      b5nenh = *blank
     c                   eval      b5lpri = d_fl_pris
     c                   endif
     c                   endif
      *
      * Pris-segm. mangler - bruk varelinjebeløp etter rab. hvis utfyllt
      * Korr. - viser nettopris uten kalk. av tlg./fradrag uansett...
      *
     c*                  if        b5lpri = *zero and
     c*                            b5npri = *zero and
     c                   if        d_fl_bnet <> *zero and
     c                             d_fl_fkva <> *zero
     c                   eval(h)   b5lpri = d_fl_bnet / d_fl_fkva
     c                   endif
      *
     c*                  eval(h)   b5lsum = b5lkva * b5lpri
      *
     c                   if        d_fl_prpr <> 0
     c                   endif
      *
      * Avvikende prisenhet?
      *
     c                   if        d_fl_penh <> *blank and
     c                             ldenh1 <> *blank and
     c                             ldenh1 <> d_fl_penh
     c                   exsr      omregn_pris
     c                   else
     c                   eval      b5opri = 0
     c                   eval      b5oenh = *blank
     c                   endif
      *
     c     b5_ut         tag
     c                   exfmt     b5win
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_k5win
     c                   when      *inkh
     c                   exfmt     b6win
     c                   goto      b5_ut
     c                   endsl
      *
     c     end_k5win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å søke på bestillingslinjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk9win        begsr
      *
     c                   eval      w_line = 0
      *
     c                   call      'LD500R'
     c                   parm                    w_firm
     c                   parm                    lodtl1_numm
     c                   parm                    lodtl1_suff
     c                   parm                    w_line
      *
     c     end_k9win     endsr
      *
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  * Skriv ut dokument
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  *
6.21 c     skr_dok       begsr
6.21  *
6.21 c                   eval      bsdkop = *blank
6.21 c                   eval      bsavvk = *blank
6.21 c***                eval      w_xrow = 6 + w_srrn
6.21  *
6.21 c     xbsut         tag
6.21 c                   exfmt     bswin
6.21  *
6.21 c                   if        *inkc or *inkl
6.21 c                   goto      end_bsbld
6.21 c                   endif
6.21  *
6.21 c                   eval      w_prec = d_prec
6.21  *
6.21 c                   if        bsdkop = '1'
6.21 c                   if        d_type = 'OBKR'
6.21 c                   call      'LI601C'
6.21 c                   parm                    w_prec
6.21 c                   endif
6.21  *
6.21 c                   if        d_type = 'FAKT'
6.21 c                   call      'LI605C'
6.21 c                   parm                    w_prec
6.21 c                   endif
6.21 c                   endif
6.21  *
6.21 c                   if        bsavvk = '1'
6.21 c                   call      'LI607C'
6.21 c                   parm                    w_prec
6.21 c                   endif
6.21  *
6.21 c     end_bsbld     endsr
6.21  *
6.12  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.12  * Subrutine for tømming av subfile
6.12  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.12  *
6.12 c     clr_subfile   begsr
6.12  *
6.12 c                   eval      *in14 = *on
6.12 c                   write     b2ctl
6.12 c                   eval      *in14 = *off
6.12  *
6.12 c                   eval      *in15 = *off
6.12 c                   eval      w_srrn = 0
6.12 c                   eval      w_sfrn = 0
6.12 c                   eval      w_ssrn = 0
6.12 c                   eval      w_spge = 0
6.12  *
6.12 c                   endsr
6.12  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   if        *in15 = *on
     c                   leavesr
     c                   endif
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      ledil1
      *
     c                   if        %eof
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   if        limlin > 599999
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
6.27 c                   if        ledil1_mnum <> limnum
6.27 c                   eval      *in15 = *on
6.27 c                   leave
6.27 c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      d_fakl = limeld
     c                   exsr      status_linje
     c                   eval      lodtlr_suff = lisuff
     c                   eval      lodtlr_line = liline
     c                   exsr      best_linje
      *
     c                   eval      b1valg = 0
     c                   eval      b1line = limlin
     c                   eval      b1tek1 = d_fl_tek1
     c                   eval      b1bkva = ldanta
     c                   eval      b1benh = ldenh1
     c                   eval      b1lkva = d_fl_fkva
     c                   eval      b1lenh = d_fl_fenh
     c                   eval      b1stat = w_stat
6.12 c                   eval      b1okva = fdanta
      *
7.01 c                   exsr      newlo_vask
     c                   write     b1sfl
      *
     c     les_forbi     tag
      *
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutiner for kontroll av meldingsstatus
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     status_hode   begsr
      *
     c                   select
     c                   when      listat = ' '
     c                   eval      b2stat = 'Ok!            '
     c                   when      listat = '1'
     c                   eval      b2stat = 'Feil i best.nr.'
     c                   when      listat = '2'
     c                   eval      b2stat = 'Sjekk lever.   '
     c                   when      listat = '3'
     c                   eval      b2stat = 'Avvik fra best.'
     c                   when      listat = '4'
     c                   eval      b2stat = 'Feil ordretype.'
     c                   when      listat = '5'
     c                   eval      b2stat = 'Avvik totalsum.'
     c                   when      listat = '7'
     c                   eval      b2stat = 'Slettemarkert  '
     c                   when      listat = '8'
     c                   eval      b2stat = 'Manuelt ok     '
     c                   when      listat = '9'
     c                   eval      b2stat = 'Oppdatert      '
7.04 c                   when      listat = 'A'
7.04 c                   eval      b2stat = 'Enhetsproblemer'
     c                   endsl
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     status_linje  begsr
      *
     c                   select
     c                   when      listat = ' '
     c                   eval      w_stat = 'Ok!         '
     c                   when      listat = '1'
     c                   eval      w_stat = 'Endret lev. '
     c                   when      listat = '2'
     c                   eval      w_stat = 'Avvist lev  '
     c                   when      listat = '3'
     c                   eval      w_stat = 'Ikke på fakt'
     c                   when      listat = '4'
     c                   eval      w_stat = 'Ikke på best'
     c                   when      listat = '5'
     c                   eval      w_stat = 'Avvik antall'
     c                   when      listat = '6'
     c                   eval      w_stat = 'Avvik enhet '
     c                   when      listat = '7'
     c                   eval      w_stat = 'Avvik pris  '
7.04 c                   when      listat = 'A'
7.04 c                   eval      w_stat = 'Enhetsprob. '
     c                   endsl
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for lesing av leverandør
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_ldor      begsr
      *
     c                   eval      rlevl1_levr = lildor
     c     rlevl1_key    chain     rlevl1
     c                   if        %found
     c                   eval      b2lnav = rlnavn
     c                   else
     c                   eval      b2lnav = d_fh_snav
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for lesing av bestillingshode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     best_hode     begsr
      *
     c                   eval      w_ipct = *zero
      *
     c                   eval      lohelr_numm = linumm
     c     lohelr_key    setll     lohelr
     c     lohelr_key    reade     lohelr
     c                   dow       not %eof
     c                   eval      votyl1_otyp = lootyp
     c     votyl1_key    chain     votyl1
     c                   if        %found
     c                   if        vaosys = 1
     c                   eval      w_ipct = w_ipct + 1
     c                   endif
     c                   endif
      *
     c     lohelr_key    reade     lohelr
     c                   enddo
      *
     c                   eval      *in33 = *off
     c                   eval      b2vtxt = *blank
     c                   if        w_ipct > 1
     c                   eval      b2vtxt = 'Flere suffix finnes!'
     c                   eval      *in33 = *on
     c                   endif
      *
     c     lohel1_key    chain     lohel1
     c                   if        not %found
     c                   eval      loldat = *loval
     c                   eval      lovref = *blank
     c                   endif
      *
      *  Finner hvilken mva-sats som skal benyttes
      *
     c                   call      'FÅ705R'
     c                   parm                    w_firm
     c                   parm                    lobdat
     c                   parm                    w_mvap
     c                   parm                    w_mvat
     c                   parm                    w_mvaf
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for lesing av bestillingslinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     best_linje    begsr
      *
6.12 c                   eval      lodtlr_suff = lisuff
     c     lodtlr_key    chain     lodtlr
     c                   if        not %found
     c                   eval      ldline = 0
     c                   eval      ldanta = 0
     c                   eval      ldenh1 = *blank
     c                   eval      ldvare = *blank
     c                   eval      ldipny = 0
     c                   eval      ldkpny = 0
6.12 c                   eval      fdanta = 0
6.12 c                   eval      fdenh1 = *blank
6.12 c                   else
6.12 c                   eval      b_best = *on
6.12 c                   eval      fodtlr_numm = ldornu
6.12 c                   eval      fodtlr_suff = ldorsu
6.12 c                   eval      fodtlr_line = ldorli
6.12 c     fodtlr_key    chain     fodtlr
6.12 c                   if        not %found
6.12 c                   eval      fdanta = 0
6.12 c                   eval      fdkopr = 0
6.12 c                   eval      fdenh1 = *blank
6.12 c                   else
6.12 c                   eval      b_ordr = *on
6.12 c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for lesing av meldingslinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     meld_linje    begsr
      *
     c                   eval      ledilr_mlin = b1line
     c     ledilr_key    chain     ledilr
     c                   if        %found
     c                   eval      d_fakl = limeld
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for omregning av pris hvis avvikende prisenhet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     omregn_pris   begsr
      *
     c                   eval      vvenlr_vare = ldvare
     c                   eval      vvenlr_enhe = b5oenh
     c     vvenlr_key    chain     vvenlr
     c                   if        not %found
     c                   eval      w_omr1 = 1
     c                   else
     c                   eval      w_omr1 = veomre
     c                   endif
      *
     c                   eval      vvenlr_enhe = b5penh
     c     vvenlr_key    chain     vvenlr
     c                   if        not %found
     c                   eval      w_omr2 = 1
     c                   else
     c                   eval      w_omr2 = veomre
     c                   endif
      *
     c                   eval      b5opri = (w_omr1 / w_omr2) * b5lpri
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av tillegg/fradrag på linjenivå
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     tilfra_linje  begsr
      *
      * Nullstiller
      *
     c                   eval      b5frad = 0
     c                   eval      b5till = 0
     c                   eval      b6sig1 = *blank
     c                   eval      b6kod1 = *blank
     c                   eval      b6tek1 = *blank
     c                   eval      b6bel1 = 0
     c                   eval      b6sig2 = *blank
     c                   eval      b6kod2 = *blank
     c                   eval      b6tek2 = *blank
     c                   eval      b6bel2 = 0
     c                   eval      b6sig3 = *blank
     c                   eval      b6kod3 = *blank
     c                   eval      b6tek3 = *blank
     c                   eval      b6bel3 = 0
     c                   eval      b6sig4 = *blank
     c                   eval      b6kod4 = *blank
     c                   eval      b6tek4 = *blank
     c                   eval      b6bel4 = 0
     c                   eval      b6sig5 = *blank
     c                   eval      b6kod5 = *blank
     c                   eval      b6tek5 = *blank
     c                   eval      b6bel5 = 0
     c                   eval      b6sig6 = *blank
     c                   eval      b6kod6 = *blank
     c                   eval      b6tek6 = *blank
     c                   eval      b6bel6 = 0
      *
      * Tillegg 1
      *
     c                   eval      w_ttlg = 0
     c                   eval      w_tfra = 0
     c                   eval      w_pris = 0
      *
     c                   eval      w_frad = 0
     c                   if        d_fl_bel1  <> 0 and
     c                             d_fl_fkva  <> 0
     c                   eval(h)   w_frad = d_fl_bel1 / d_fl_fkva
     c                   else
     c                   if        d_fl_pro1 <> 0
     c                   eval(h)   w_frad = (d_fl_pris * d_fl_pro1 ) / 100
     c                   endif
     c                   endif
      *
     c                   if        d_fl_sig1 = 'A  '
     c                   eval      b5frad = b5frad + w_frad
     c                   eval      w_tfra = w_tfra + w_frad
     c                   else
     c                   eval      b5till = b5till + w_frad
     c                   eval      w_ttlg = w_ttlg + w_frad
     c                   endif
      *
     c                   eval      w_tilk = d_fl_kod1
     c                   exsr      tilfra_tekst
     c                   eval      b6sig1 = d_fl_sig1
     c                   eval      b6kod1 = d_fl_kod1
     c                   eval      b6tek1 = w_tilt
     c                   eval      b6bel1 = w_frad
      *
      * Tillegg 2
      *
     c                   eval      w_pris = d_fl_pris + w_ttlg - w_tfra
     c                   eval      w_frad = 0
     c                   if        d_fl_bel2  <> 0 and
     c                             d_fl_fkva  <> 0
     c                   eval(h)   w_frad = d_fl_bel2 / d_fl_fkva
     c                   else
     c                   if        d_fl_pro2 <> 0
     c                   eval(h)   w_frad = (w_pris * d_fl_pro2 ) / 100
     c                   endif
     c                   endif
      *
     c                   if        d_fl_sig2 = 'A  '
     c                   eval      b5frad = b5frad + w_frad
     c                   eval      w_tfra = w_tfra + w_frad
     c                   else
     c                   eval      b5till = b5till + w_frad
     c                   eval      w_ttlg = w_ttlg + w_frad
     c                   endif
      *
     c                   eval      w_tilk = d_fl_kod2
     c                   exsr      tilfra_tekst
     c                   eval      b6sig2 = d_fl_sig2
     c                   eval      b6kod2 = d_fl_kod2
     c                   eval      b6tek2 = w_tilt
     c                   eval      b6bel2 = w_frad
      *
      * Tillegg 3
      *
     c                   eval      w_pris = d_fl_pris + w_ttlg - w_tfra
     c                   eval      w_frad = 0
     c                   if        d_fl_bel3  <> 0 and
     c                             d_fl_fkva  <> 0
     c                   eval(h)   w_frad = d_fl_bel3 / d_fl_fkva
     c                   else
     c                   if        d_fl_pro3 <> 0
     c                   eval(h)   w_frad = (w_pris * d_fl_pro3 ) / 100
     c                   endif
     c                   endif
      *
     c                   if        d_fl_sig3 = 'A  '
     c                   eval      b5frad = b5frad + w_frad
     c                   eval      w_tfra = w_tfra + w_frad
     c                   else
     c                   eval      b5till = b5till + w_frad
     c                   eval      w_ttlg = w_ttlg + w_frad
     c                   endif
      *
     c                   eval      w_tilk = d_fl_kod3
     c                   exsr      tilfra_tekst
     c                   eval      b6sig3 = d_fl_sig3
     c                   eval      b6kod3 = d_fl_kod3
     c                   eval      b6tek3 = w_tilt
     c                   eval      b6bel3 = w_frad
      *
      * Tillegg 4
      *
     c                   eval      w_pris = d_fl_pris + w_ttlg - w_tfra
     c                   eval      w_frad = 0
     c                   if        d_fl_bel4  <> 0 and
     c                             d_fl_fkva  <> 0
     c                   eval(h)   w_frad = d_fl_bel4 / d_fl_fkva
     c                   else
     c                   if        d_fl_pro4 <> 0
     c                   eval(h)   w_frad = (w_pris * d_fl_pro4 ) / 100
     c                   endif
     c                   endif
      *
     c                   if        d_fl_sig4 = 'A  '
     c                   eval      b5frad = b5frad + w_frad
     c                   eval      w_tfra = w_tfra + w_frad
     c                   else
     c                   eval      b5till = b5till + w_frad
     c                   eval      w_ttlg = w_ttlg + w_frad
     c                   endif
      *
     c                   eval      w_tilk = d_fl_kod4
     c                   exsr      tilfra_tekst
     c                   eval      b6sig4 = d_fl_sig4
     c                   eval      b6kod4 = d_fl_kod4
     c                   eval      b6tek4 = w_tilt
     c                   eval      b6bel4 = w_frad
      *
      * Tillegg 5
      *
     c                   eval      w_pris = d_fl_pris + w_ttlg - w_tfra
     c                   eval      w_frad = 0
     c                   if        d_fl_bel5  <> 0 and
     c                             d_fl_fkva  <> 0
     c                   eval(h)   w_frad = d_fl_bel5 / d_fl_fkva
     c                   else
     c                   if        d_fl_pro5 <> 0
     c                   eval(h)   w_frad = (w_pris * d_fl_pro5 ) / 100
     c                   endif
     c                   endif
      *
     c                   if        d_fl_sig5 = 'A  '
     c                   eval      b5frad = b5frad + w_frad
     c                   eval      w_tfra = w_tfra + w_frad
     c                   else
     c                   eval      b5till = b5till + w_frad
     c                   eval      w_ttlg = w_ttlg + w_frad
     c                   endif
      *
     c                   eval      w_tilk = d_fl_kod5
     c                   exsr      tilfra_tekst
     c                   eval      b6sig5 = d_fl_sig5
     c                   eval      b6kod5 = d_fl_kod5
     c                   eval      b6tek5 = w_tilt
     c                   eval      b6bel5 = w_frad
      *
      * Tillegg 6
      *
     c                   eval      w_pris = d_fl_pris + w_ttlg - w_tfra
     c                   eval      w_frad = 0
     c                   if        d_fl_bel6  <> 0 and
     c                             d_fl_fkva  <> 0
     c                   eval(h)   w_frad = d_fl_bel6 / d_fl_fkva
     c                   else
     c                   if        d_fl_pro6 <> 0
     c                   eval(h)   w_frad = (w_pris * d_fl_pro6 ) / 100
     c                   endif
     c                   endif
      *
     c                   if        d_fl_sig6 = 'A  '
     c                   eval      b5frad = b5frad + w_frad
     c                   eval      w_tfra = w_tfra + w_frad
     c                   else
     c                   eval      b5till = b5till + w_frad
     c                   eval      w_ttlg = w_ttlg + w_frad
     c                   endif
      *
     c                   eval      w_tilk = d_fl_kod6
     c                   exsr      tilfra_tekst
     c                   eval      b6sig6 = d_fl_sig6
     c                   eval      b6kod6 = d_fl_kod6
     c                   eval      b6tek6 = w_tilt
     c                   eval      b6bel6 = w_frad
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne tillegg/fradrags tekst fra kode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     tilfra_tekst  begsr
      *
     c                   eval        w_tilt = *blank
     c                   select
     c                   when        w_tilk = 'PAD'
     c                   eval        w_tilt = 'Tilbudsrabatt'
     c                   when        w_tilk = 'PAR'
     c                   eval        w_tilt = 'Partner/kjede/aktivtetsrabatt'
     c                   when        w_tilk = 'DAE'
     c                   eval        w_tilt = 'Grossistrabatt leverandør'
     c                   when        w_tilk = 'VAB'
     c                   eval        w_tilt = 'Verdi/beløps/funksjonsrabatt'
     c                   when        w_tilk = 'PDE'
     c                   eval        w_tilt = 'Pallerabatt'
     c                   when        w_tilk = 'PI '
     c                   eval        w_tilt = 'Hentegodtgjørelse'
     c                   when        w_tilk = 'OTE'
     c                   eval        w_tilt = 'Generelt fadrag'
     c                   when        w_tilk = 'QD '
     c                   eval        w_tilt = 'Kvantumsrabatt'
     c                   when        w_tilk = 'LCA'
     c                   eval        w_tilt = 'Pallelagsrabatt'
     c                   when        w_tilk = 'CAG'
     c                   eval        w_tilt = 'Kjøpers fradrag'
     c                   when        w_tilk = 'DI '
     c                   eval        w_tilt = 'Engangsrabatt'
     c                   when        w_tilk = 'WHE'
     c                   eval        w_tilt = 'Grossistrabatt butikk/kjede'
     c                   when        w_tilk = 'FC '
     c                   eval        w_tilt = 'Frakttillegg '
     c                   when        w_tilk = 'IN '
     c                   eval        w_tilt = 'Assuransetillegg'
     c                   when        w_tilk = 'RCH'
     c                   eval        w_tilt = 'Returtillegg '
     c                   when        w_tilk = 'PC '
     c                   eval        w_tilt = 'Pakketillegg '
     c                   when        w_tilk = 'BPE'
     c                   eval        w_tilt = 'Anbrekkstillegg'
     c                   when        w_tilk = 'PBE'
     c                   eval        w_tilt = 'Anbrekkstillegg'
     c                   when        w_tilk = 'IS '
     c                   eval        w_tilt = 'Fakturagebyr '
     c                   when        w_tilk = 'GAC'
     c                   eval        w_tilt = 'Garantitillegg'
     c                   when        w_tilk = 'HD '
     c                   eval        w_tilt = 'Håndteringsgebyr'
     c                   endsl
      *
     c                   endsr
      *
6.24  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.24  * Subrutine for å finne tillegg/fradrags tekst fra kode
6.24  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.24  *
6.24 c     kal_ntopr     begsr
6.24  *
6.24 c                   eval      w_npri = d_fl_pris
6.24  * Finn tillegg og fradrag for linja
6.24 c                   exsr      tilfra_linje
6.24  *
6.24 c                   if        d_fl_prkv = 'AAB'
6.24 c                   eval      w_npri = d_fl_pris + b5till - b5frad
6.24 c                   else
6.24 c                   if        d_fl_prkv = 'AAA'
6.24 c                   eval      w_npri = d_fl_pris
6.24 c                   endif
6.24 c                   endif
6.24  *
6.24 c                   endsr
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  * Newlook vask av data
7.01  *  Fjerner tegn som gjør at Newlook feiltolker
7.01  *  Looper 5 ganger i tilfelle ..... på slutten av tekst
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01   begsr newlo_vask;
7.01     for w_teller = 1 to 5 ;
7.01     w_len = %len(%trim(b1tek1));
7.01     if (w_len > 0);
7.01       b1tek1 = %ScanRpl('.':'':b1tek1:w_len); // newlook tåler ikke . på slutten
7.01     endif;
7.01     w_len = %len(%trim(b1tek1));
7.01     if (w_len > 0);
7.01       b1tek1 = %ScanRpl(':':'':b1tek1:w_len); // newlook tåler ikke : på slutten
7.01     endif;
7.01     endfor ;
7.01   endsr;
6.24  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Tar inn parameter fra forrige program
      *
     c     *entry        plist
     c                   parm                    p_parm
     c                   eval      d_prec = p_parm
      *
      *  Key for les av MELDINGSLINJER
      *
     c     ledil1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ledil1_type
     c                   kfld                    ledil1_mnum
     c                   kfld                    ledil1_mlin
      *
      *  Key for oppslag på MELDINGSLINJER
      *
     c     ledilr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ledilr_type
     c                   kfld                    ledilr_mnum
     c                   kfld                    ledilr_mlin
      *
      *  Key for les av BESTILLING-HODE
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
     c                   kfld                    lohel1_suff
      *
      *  Key for les av BESTILLING-HODE (suffix)
      *
     c     lohelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelr_numm
      *
      *  Key for les av BESTILLING-LINJE
      *
     c     lodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtl1_numm
     c                   kfld                    lodtl1_suff
      *
      *  Key for oppslag på BESTILLING-LINJE
      *
     c     lodtlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlr_numm
     c                   kfld                    lodtlr_suff
     c                   kfld                    lodtlr_line
      *
      * Key for les av leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for les av leverandør på EAN - lokasjonsnummer
      *
     c     rlevl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl5_eanl
      *
      * Key for les av vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for les av vare-enhet
      *
     c     vvenlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenlr_vare
     c                   kfld                    vvenlr_enhe
      *
      *  Key for file les hoved EAN-lokasjonsnummer
      *
     c     raa8l1_key    klist
     c                   kfld                    w_firm
      *
      * Key for les av lager
      *
     c     mjlol2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mjlol2_eann
      *
      * Key for les av ordretype-register
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
6.12  *
6.12  *  Key for oppslag på ordrelinje
6.12  *
6.12 c     fodtlr_key    klist
6.12 c                   kfld                    w_firm
6.12 c                   kfld                    fodtlr_numm
6.12 c                   kfld                    fodtlr_suff
6.12 c                   kfld                    fodtlr_line
      *
      * Legg inn nøkkelbegreper fra parameter
      *
     c                   eval      w_firm = d_firm
     c                   eval      ledilr_type = d_type
     c                   eval      ledilr_mnum = d_mnum
     c                   eval      ledilr_mlin = d_mlin
     c                   eval      ledil1_type = d_type
     c                   eval      ledil1_mnum = d_mnum
      *
      * Finn informasjon på meldingshode
      *
     c                   eval      ledilr_mlin = 100001
     c     ledilr_key    chain     ledilr
     c                   if        %found
     c     *dmy          move      limdat        b2mdat
     c                   eval      lohel1_numm = linumm
     c                   eval      lohel1_suff = lisuff
     c                   eval      lodtl1_numm = linumm
     c                   eval      lodtl1_suff = lisuff
     c                   eval      lodtlr_numm = linumm
     c                   eval      lodtlr_suff = lisuff
6.11 c                   eval      b2mlnr = limnum
      *
     c                   exsr      status_hode
     c                   exsr      best_hode
      *
     c                   eval      b2numm = linumm
      *
     c                   if        lisuff <> *zero
     c                   eval      b2suff = lisuff
     c                   eval      *in30 = *on
     c                   else
     c                   eval      *in30 = *off
     c                   endif
      *
     c                   eval      d_fakh1 = limeld
     c                   movel     d_fh_fanr     b2fakn
     c                   movel     d_fh_best     b2besn
      *
6.25 c*                  if        d_fh_ffda <> *loval
6.25 c*    *dmy          move      d_fh_ffda     b2fdat
6.25 c*                  else
6.25 c*                  eval      b2fdat = 0
6.25 c*                  endif
      *
6.25 c                   if        d_fh_forf <> *loval
6.25 c     *dmy          move      d_fh_forf     b2fdat
6.25 c                   else
6.25 c                   eval      b2fdat = 0
6.25 c                   endif
      *
     c                   exsr      les_ldor
     c                   eval      b2ldor = lildor
     c                   eval      b2lean = d_fh_sean
     c                   eval      b2fnav = d_fh_fnav
     c                   eval      b2fean = d_fh_fean
      *
     c                   if        d_fh_dtyp = '381'
     c                   eval      b2type = 'KREDITNOTA '
     c                   eval      *in31 = *on
     c                   else
     c                   eval      b2type = 'FAKTURA    '
     c                   eval      *in31 = *off
     c                   endif
      *
     c                   eval      b2vref = d_fh_lref
     c                   eval      b2dref = d_fh_kref
      *
     c                   if        loornr <> 0
     c                   eval      b2ornr = loornr
     c                   eval      *in32 = *on
     c                   else
     c                   eval      b2ornr = 0
     c                   eval      *in32 = *off
     c                   endif
      *
     c                   eval      ledilr_mlin = 900001
     c     ledilr_key    chain     ledilr
     c                   if        %found
     c                   eval      d_fakt = limeld
     c                   eval      b2fbel = d_ft_teks
     c                   else
     c                   eval      b2fbel = 0
     c                   endif
6.26 c**                 eval      b2bbel = lototi
6.26 c                   eval      b2bbel = lototi - lototr
6.30 c                   eval      b2kbel = lototk
      *
     c                   endif
      *
      * Fyller opp subfile med meldingslinjer
      *
     c                   eval      ledil1_mlin = 500000
      *
     c     ledil1_key    setll     ledil1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
