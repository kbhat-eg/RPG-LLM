     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : HV702R
      * Beskrivelse . . : Vareinfo til håndterminal, produsere fil PSI
      *
      *                   Produsere register for ekstern scanning av
      *                   vareregister.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       310500 ASK  Nytt program
      *       240101 HKO  Leser varesortiment via utplukksfil
      *                   Legger ut filen sortert etter varenummer
      * V5.30 030105 KOB  Lagt inn nye parameter i call til VP716R
5.70  *  PRGR 021208 bof  Utvidet med prisgruppe
6.11  *  6.11 130709 bof  Utvidet ean-nr med 13 posisjon + std. kall
6.12  *  6.10 240909 bof  Endret til vveppf ist. vvenpf
6.20  *       121110 BOF  Utvidet med lager på sortiment
6.21  * 6.20  190511 bø   Utvidet vl710r med div. info
6.22  * 6.20  080312 bsa  Utvidet vp716r med lager
6.30  * 6.30  170918 bkv  Utvidet med trelast sortiment
8.01  *PRG2   130622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fveanl2    if   e           k disk    rename(veanpfr:veanl2r)
6.12 fvvepl1    if   e           k disk    rename(vveppfr:vvepl1r)
6.12 fvvepl2    if   e           k disk    rename(vveppfr:vvepl2r)
     fvvawpf    if   e           k disk
     fhbvapf    o  a e           k disk    rename(hbvapfr:hbvapfr)
     fhbvepf    o  a e           k disk    rename(hbvepfr:hbvepfr)
     fhbvlpf    o  a e           k disk    rename(hbvlpfr:hbvlpfr)
6.30 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
      * Definering av datastruktur
      *
      *  Splitter beløp i kroner og øre
      *
     d                 ds
     d d_spim                        11
     d  d_spim_kr                     7    overlay(d_spim:2)
     d  d_spim_komma                  1    overlay(d_spim:9) inz(',')
     d  d_spim_Øre                    2    overlay(d_spim:10)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vvfirm)
     d p_vsor          s             10
6.20 d p_lage          s              2  0
     d p_dato          s               d   datfmt(*iso)
      *
      * Definering av variabler i nøkler
      *
     d veanl2_vare     s                   like(vxvare)
6.12 d vvepl1_pvar     s                   like(vepvar)
6.12 d vvepl2_pgti     s                   like(vepgti)
      *
      * Definering av variable
      *
     d b_svgr          s              1
     d b_sldo          s              1
     d b_stek          s              1
      *
     d w_mvap          s              6  2
     d w_mvat          s              5  4
     d w_mvaf          s             10  9
     d w_kode          s              1  0
      *
     d w_s1im          s              9  2
     d w_s2im          s              9  2
     d w_s3im          s              9  2
     d w_kr            s              7  0
     d w_eann          s             14  0
     d w_eana          s             14
      *
     d w_lety          s              1  0
     d w_utim          s               t   timfmt(*iso)
     d w_sap1          s              9  2
     d w_bup1          s              9  2
     d w_kop1          s              9  2
     d w_inp1          s              9  2
     d w_pda1          s               d   datfmt(*iso)
     d w_sap2          s              9  2
     d w_bup2          s              9  2
     d w_omr2          s              9  2
     d w_pda2          s               d   datfmt(*iso)
     d w_sap3          s              9  2
     d w_bup3          s              9  2
     d w_omr3          s              9  2
     d w_pda3          s               d   datfmt(*iso)
     d w_fdat          s               d   datfmt(*iso)
     d w_ftid          s               t   timfmt(*iso)
     d w_tdat          s               d   datfmt(*iso)
     d w_ttid          s               t   timfmt(*iso)
     d w_kamp          s              5
     d w_tip1          s              9  2
     d w_tkp1          s              9  2
     d w_tip2          s              9  2
     d w_tip3          s              9  2
     d w_enh1          s              3
     d w_enh2          s              3
     d w_enh3          s              3
     d w_pri1          s             11
     d w_pri2          s             11
     d w_pri3          s             11
      *
     d w_enin          s              3
     d w_enps          s              3
     d w_pspr          s              9  2
      *
     d w_vare_gml      s                   like(vvvare)
      *
     d w_firm          s                   like(vvfirm)
     d w_vinf          s             88
     d w_einf          s             29
     d w_linf          s             31
8.01 d w_prgr          s              2
5.70 d w_lage          s              2  0
5.70 d w_vtyp          s                   like(vvvtyp)
6.21 d w_utda          s                   like(vvudat)
6.21 d w_ldor          s                   like(vvldor)
6.21 d b_inlr          s              1    inz(*off)
6.30 d w_lv_nobb       s                   like(vvlnob)
6.30 d w_lv_modn       s                   like(vvlmod)
6.30 d w_lv_lldo       s                   like(vvlnle)
6.30 d w_lv_llva       s                   like(vvllva)
      *
5.70 d                uds
5.70 d l_lage               1001   1002  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Kaller utplukksprogram for valg varesortiment
      *
     c                   eval      b_svgr = *off
     c                   eval      b_sldo = *off
     c                   eval      b_stek = *off
      *
     c                   call      'VD700R'
     c                   parm                    w_firm
     c                   parm                    p_vsor
     c                   parm                    b_svgr
     c                   parm                    b_sldo
     c                   parm                    b_stek
6.20 c                   parm                    p_lage
      *
      * Leser utplukksfil som inneholder alle varer i sortimentet
      *
     c                   eval      w_vare_gml = *blank
      *
     c                   read      vvawpf                                 90
     c                   dow       *in90 = *off
      *
     c                   if        vvvare <> w_vare_gml
     c                   exsr      behandling
     c                   endif
      *
     c                   eval      w_vare_gml = vvvare
      *
     c                   read      vvawpf                                 90
     c                   enddo
      *
      * Avslutt
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
      * Avbryter dersom varen er skaffevare, diverse-vare eller tjeneste
      *
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    vvvare
5.70 c                   parm                    w_lage
5.70 c                   parm                    w_vtyp
6.21 c                   parm                    w_utda
6.21 c                   parm                    w_ldor
6.21 c                   parm                    b_inlr
6.30 c                   parm                    w_lv_nobb
6.30 c                   parm                    w_lv_modn
6.30 c                   parm                    w_lv_lldo
6.30 c                   parm                    w_lv_llva
      *
5.70 c                   if        w_vtyp = 'S' or
5.70 c                             w_vtyp = 'D' or
5.70 c                             w_vtyp = 'T'
     c                   goto      end_behandl
     c                   endif
      *
      * Oppretter rad i eksternt scanne-register
      *
     c                   eval      w_s1im = 0
     c                   eval      w_s2im = 0
     c                   eval      w_s3im = 0
      *
      * Henter priser på tre salgsenheter
      *
     c                   eval      w_lety = 0
      *
     c                   call      'VP716R'
     c                   parm                    w_firm
     c                   parm                    vvvare
5.70 c                   parm                    w_prgr
6.22 c                   parm                    p_lage
     c                   parm                    w_lety
     c                   parm                    p_dato
     c                   parm                    w_utim
     c                   parm                    w_enh1
     c                   parm                    w_sap1
     c                   parm                    w_bup1
     c                   parm                    w_kop1
     c                   parm                    w_inp1
     c                   parm                    w_pda1
     c                   parm                    w_enh2
     c                   parm                    w_sap2
     c                   parm                    w_bup2
     c                   parm                    w_omr2
     c                   parm                    w_pda2
     c                   parm                    w_enh3
     c                   parm                    w_sap3
     c                   parm                    w_bup3
     c                   parm                    w_omr3
     c                   parm                    w_pda3
     c                   parm                    w_fdat
     c                   parm                    w_ftid
     c                   parm                    w_tdat
     c                   parm                    w_ttid
     c                   parm                    w_kamp
     c                   parm                    w_tip1
     c                   parm                    w_tkp1
     c                   parm                    w_tip2
     c                   parm                    w_tip3
     c                   parm                    w_enin
     c                   parm                    w_enps
     c                   parm                    w_pspr
      *
      * Dersom tilbudet ikke er et aktivt tilbud, nullstilles tilbudspriser
      *
     c                   if        w_tip1 <> 0 or
     c                             w_tip2 <> 0 or
     c                             w_tip3 <> 0
     c                   if        p_dato < w_fdat or
     c                             p_dato > w_tdat
     c                   eval      w_tip1 = 0
     c                   eval      w_tip2 = 0
     c                   eval      w_tip3 = 0
     c                   endif
     c                   endif
      *
      *  Enhet 1
      *
     c                   select
     c                   when      w_tip1 <> 0
     c                   eval      w_s1im = w_tip1
     c                   when      w_bup1 <> 0
     c                   eval      w_s1im = w_bup1
     c                   when      w_sap1 <> 0
     c                   eval      w_s1im = w_sap1
     c                   endsl
      *
     c                   eval      w_s1im = w_s1im * w_mvat
      *
     c                   if        w_s1im <> 0
     c                   eval      w_kode = 4
     c                   call      'FA909R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   parm                    w_kode
     c                   parm                    w_s1im
     c                   endif
      *
     c                   eval      w_kr      = w_s1im
     c                   eval      d_spim_kr = %editc(w_kr : 'Z')
     c                   move      w_s1im        d_spim_Øre
      *
     c                   eval      w_pri1 = d_spim
      *
      *  Enhet 2
      *
     c                   if        w_enh2 <> *blank
      *
     c                   select
     c                   when      w_tip2 <> 0
     c                   eval      w_s2im = w_tip2
     c                   when      w_bup2 <> 0
     c                   eval      w_s2im = w_bup2
     c                   when      w_sap2 <> 0
     c                   eval      w_s2im = w_sap2
     c                   endsl
      *
     c                   eval      w_s2im = w_s2im * w_mvat
      *
     c                   if        w_s2im <> 0
     c                   eval      w_kode = 4
     c                   call      'FA909R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   parm                    w_kode
     c                   parm                    w_s2im
     c                   endif
      *
     c                   endif
      *
     c                   eval      w_kr      = w_s2im
     c                   eval      d_spim_kr = %editc(w_kr : 'Z')
     c                   move      w_s2im        d_spim_Øre
      *
     c                   eval      w_pri2 = d_spim
      *
      *  Enhet 3
      *
     c                   if        w_enh3 <> *blank
      *
     c                   select
     c                   when      w_tip3 <> 0
     c                   eval      w_s3im = w_tip3
     c                   when      w_bup3 <> 0
     c                   eval      w_s3im = w_bup3
     c                   when      w_sap3 <> 0
     c                   eval      w_s3im = w_sap3
     c                   endsl
      *
     c                   eval      w_s3im = w_s3im * w_mvat
      *
     c                   if        w_s3im <> 0
     c                   eval      w_kode = 4
     c                   call      'FA909R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   parm                    w_kode
     c                   parm                    w_s3im
     c                   endif
      *
     c                   endif
      *
     c                   eval      w_kr      = w_s3im
     c                   eval      d_spim_kr = %editc(w_kr : 'Z')
     c                   move      w_s3im        d_spim_Øre
      *
     c                   eval      w_pri3 = d_spim
6.111 *
6.11  * Oppretter med EAN-nummer fra Enhetsregister
6.11  *
6.12 c                   eval      vvepl1_pvar = vvvare
6.12 c     vvepl1_key    setll     vvepl1
6.12 c     vvepl1_key    reade     vvepl1                                 92
6.12 c                   dow       *in92 = *off
6.12 c                   if        vepgti <> 0
6.12 c                   eval      w_eann = vepgti
6.12 c                   move      w_eann        w_eana
6.12 c                   exsr      skriv_ean
6.12 c                   endif
6.12 c     vvepl1_key    reade     vvepl1                                 92
6.12 c                   enddo
      *
      * Oppretter med EAN-nummer fra EAN-register
6.11  * - de som ikke ligger på vare-enhet
      *
     c                   eval      veanl2_vare = vvvare
     c     veanl2_key    setll     veanl2
     c     veanl2_key    reade     veanl2                                 92
     c                   dow       *in92 = *off
     c                   if        vxeann <> 0
6.12 c                   eval      vvepl2_pgti = vxeann
6.12 c     vvepl2_key    chain     vvepl2
6.12 c                   if        not %found(vvepl2)
     c                   eval      w_eann = vxeann
     c                   move      w_eann        w_eana
     c                   exsr      skriv_ean
     c                   endif
6.11 c                   endif
     c     veanl2_key    reade     veanl2                                 92
     c                   enddo
      *
      * Oppretter med lev. varenummer dersom det er fylt ut
      *
6.30 c                   if        vvlvar <> *blank or
6.30 c                             w_lv_llva <> *blank
     c                   exsr      skriv_lvnr
     c                   endif
      *
     c                   exsr      skriv_vare
      *
     c     end_behandl   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegg av rad i varefil
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_vare    begsr
     c                   clear                   hbvapfr
      *
     c                   eval      w_vinf = 'VR;***;'
     c                   eval      w_vinf = %trim(w_vinf) + %subst(
     c                                      vvvare:1:8) + ';'
     c                   eval      w_vinf = %trim(w_vinf) + %trimr(
     c                                      %subst(vvtek1:1:30)) + ';'
     c                   eval      w_vinf = %trim(w_vinf) + w_enh1 + ';'
     c                   eval      w_vinf = %trim(w_vinf) + %triml(
     c                                      %subst(w_pri1:3:9)) + ';'
     c                   eval      w_vinf = %trim(w_vinf) + w_enh2 + ';'
     c                   eval      w_vinf = %trim(w_vinf) + %triml(
     c                                      %subst(w_pri2:3:9)) + ';'
     c                   eval      w_vinf = %trim(w_vinf) + w_enh3 + ';'
     c                   eval      w_vinf = %trim(w_vinf) + %triml(
     c                                      %subst(w_pri3:3:9)) + ';'
     c                   eval      hvinfo = w_vinf
     c                   write     hbvapfr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegg av rad i ean/varefil
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_ean     begsr
      *
     c                   clear                   hbvepfr
      *
     c                   eval      w_einf = 'VE;***;'
     c                   eval      w_einf = %trim(w_einf) +
     c                                      %subst(w_eana:2:13) + ';'
     c                   eval      w_einf = %trim(w_einf) + %subst(
     c                                      vvvare:1:8)
     c                   eval      heinfo = w_einf
     c                   write     hbvepfr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegg av rad i lev.vnr/varefil
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_lvnr    begsr
      *
     c                   clear                   hbvlpfr
      *
     c                   eval      w_linf = 'VL;***;'
6.30 c                   if        w_lv_nobb > 0
6.30 c                   eval      w_linf = %trim(w_linf) + w_lv_llva + ';'
6.30 c                   else
     c                   eval      w_linf = %trim(w_linf) + vvlvar + ';'
6.30 c                   endif
     c                   eval      w_linf = %trim(w_linf) + %subst(
     c                                      vvvare:1:8)
     c                   eval      hlinfo = w_linf
     c                   write     hbvlpfr
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vsor
     c                   parm                    p_dato
6.20 c                   parm                    p_lage
      *
      * Key for oppslag på EAN-register med varenummer
      *
     c     veanl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    veanl2_vare
6.11  *
6.11  * Key for oppslag på vare-enhet   med varenummer
6.11  *
6.12 c     vvepl1_key    klist
6.12 c                   kfld                    w_firm
6.12 c                   kfld                    vvepl1_pvar
6.12  *
6.12  * Key for oppslag på vare-enhet   med ean-nr
6.12  *
6.12 c     vvepl2_key    klist
6.12 c                   kfld                    w_firm
6.12 c                   kfld                    vvepl2_pgti
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
      * Henter current tidspunkt
      *
     c                   time                    w_utim
      *
      * Henter mva-faktor
      *
     c                   call      'FÅ705R'
     c                   parm                    w_firm
     c                   parm                    p_dato
     c                   parm                    w_mvap
     c                   parm                    w_mvat
     c                   parm                    w_mvaf
5.70  *
5.70  * Henter prisgruppe
5.70  *
5.70 c                   call      'VL711R'
5.70 c                   parm                    w_prgr
      *
5.70 c                   eval      w_lage = l_lage
      *
     c                   endsr
