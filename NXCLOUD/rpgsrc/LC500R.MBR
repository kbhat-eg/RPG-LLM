     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LC500R
      *  Beskrivelse . . : Inntasting av batchnummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      *  --------- ------  -------------------------------------------------
7.xx  * 7.00 190516  bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      *
     flusrl1    if   e           k disk    rename(lusrpfr:lusrl1r)
     flbchl1    if   e           k disk    rename(lbchpfr:lbchl1r)
     flc500d    cf   e             workstn
      *
      *   Local Data Area
      *
     d                uds
     d l_user                911    920
     d l_sfir                944    946  0
     d l_fnav                951    980
      *
      * Definering av variable i parametre
      *
     d p_batc          s             10
     d p_retu          s              1
      *
      * Definering av variable i nøkler
      *
     d lbchl1_batc     s                   like(lcbatc)
     d lusrl1_user     s                   like(lbuser)
      *
      * Definering av variable
      *
     d w_firm          s                   like(lbfirm)
     d w_kode          s              1
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  A1 Behandler inntasting av batchnummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Send Alt
      *
     c     a1taga        tag
     c                   exfmt     a1win
      *
      *  F3/F12=Avslutt program
      *
     c                   if        *inkc = *on or
     c                             *inkl = *on
     c                   eval      p_retu = '9'
     c                   goto      end_pgm
     c                   endif
      *
      *  F4=Spørring
      *
     c                   if        *inkd = *on
     c                   call      'LC501R'
     c                   parm                    w_firm
     c                   parm                    a1batc
     c                   goto      a1taga
     c                   endif
      *
      *  Sjekk, Finnes batchnummer i BATCH-REGISTERET
      *
     c                   eval      lbchl1_batc = a1batc
     c     lbchl1_key    chain     lbchl1                             90
     c                   if        *in90 = *on
     c                   eval      *in31 = *on
     c                   goto      a1taga
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sjekker koder m.m.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  w_kode    T = Utskrift av telle-liste
      *            R = Registrering/endring av telleliste
      *            L = Utskrift av telle resultatlister
      *            O = Oppdatere lagertelling
      *            D = Kjøring av diverseoppgaver
      *
      *
      *  Sjekk, Er Lager oppdatert, kan likevel Lagerlister skrives ut
      *
     c                   if        lcsper <> 0  and
     c                             w_kode <> 'L'
     c                   eval      *in33 = *on
     c                   goto      a1taga
     c                   endif
      *
      *  Sjekk, Kan denne brukeren skrive ut Telle-lister
      *
     c                   if        w_kode = 'T'
     c                   if        lbtans < 1
     c                   eval      *in36 = *on
     c                   goto      a1taga
     c                   endif
     c                   endif
      *
      *  Sjekk, Kan denne brukeren vedlikeholde tellefil
      *
     c                   if        w_kode = 'R'
     c                   if        lbtans < 1
     c                   eval      *in36 = *on
     c                   goto      a1taga
     c                   endif
     c                   endif
      *
      *  Sjekk, Kan denne brukeren skrive telle resultatlister
      *
     c                   if        w_kode = 'L'
     c                   if        lbtans < 1
     c                   eval      *in36 = *on
     c                   goto      a1taga
     c                   endif
     c                   endif
      *
      *  Sjekk, Kan denne brukeren oppdatere lagerteling
      *
     c                   if        w_kode = 'O'
     c                   if        lbtans < 2
     c                   eval      *in36 = *on
     c                   goto      a1taga
     c                   endif
     c                   endif
      *
      *  Sjekk, Kan denne brukeren kjøre diverse oppdateringsoppgaver
      *
     c                   if        w_kode = 'D'
     c                   if        lbtans < 2
     c                   eval      *in36 = *on
     c                   goto      a1taga
     c                   endif
     c                   endif
      *
     c                   eval      p_batc = a1batc
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for les av GPL-BRUKER
      *
     c     lusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lusrl1_user
      *
      *  Key for les av BATCH
      *
     c     lbchl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbchl1_batc
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    p_retu
     c                   parm                    p_batc
      *
     c                   eval      w_kode = p_retu
     c                   eval      p_retu = *blank
      *
      *  Setter startverdier
      *
     c                   eval      w_firm = l_sfir
     c                   eval      a1batc = *blank
      *
      *  Henter opplysninger fra BRUKER-REGISTER OFAK
      *
     c                   eval      lusrl1_user = l_user
     c     lusrl1_key    chain     lusrl1                             92
      *
     c                   endsr
