     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO781R
      * Beskrivelse . . : Kalkulering av vekt og volum for ordrelinje
      *
      *                   Returnerer vekt i kg og volum i m3
      *                   Håndterer kun lager og skaffevarer
      *                   Dersom vekt og volum-informasjon ikke finnes i
      *                   EVR letes det etter informasjon i VA
      *                   Dersom en vare ikke har vekt og volum-informasjon
      *                   returneres avviksflagg
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       151122 bsa  Nytt program
      *
8.01  *K0000  160623 bsa  Utvidet volum for å ta med flere desimaler
8.02  *PGRP   090124 bsa  Utvidet prisgruppe. Kompilert
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvasl1    if   e           k disk    rename(vvaspfr:vvasl1r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     fjvpkl2    if   e           k disk    rename(jvpkpfr:jvpkl2r)
     fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(fdfirm)
     d p_numm          s                   like(fdnumm)
     d p_suff          s                   like(fdsuff)
     d p_line          s                   like(fdline)
     d p_vekt          s              9  3
     d p_volu          s              9  3
     d p_avvi          s              1
     d p_vtyp          s                   like(vvvtyp)
      *
      * Definering av variabler i nøkler
      *
     d fodtl1_numm     s                   like(fdnumm)
     d fodtl1_suff     s                   like(fdsuff)
     d fodtl1_line     s                   like(fdline)
     d vvarl1_vare     s                   like(vvvare)
     d vvenl1_vare     s                   like(vevare)
     d vvenl1_enhe     s                   like(veenhe)
     d jvpkl2_vare     s                   like(jwvare)
     d jvpkl2_ldor     s                   like(jwldor)
     d jvpkl2_enhe     s                   like(jwenhe)
      *
      * Definering av variable
      *
     d b_vekt          s              1    inz(*off)
     d b_volu          s              1    inz(*off)
     d b_inlr          s              1    inz(*off)
      *
     d w_vekt          s              9  3
8.01 d*w_volu          s              9  3
8.01 d w_volu          s             11  5
     d w_ldor          s                   like(vvldor)
     d w_utda          s                   like(vvudat)
     d w_lv_nobb       s                   like(vvlnob)
     d w_lv_modn       s                   like(vvlmod)
     d w_lv_lldo       s                   like(vvlnle)
     d w_lv_llva       s                   like(vvllva)
      *
     d w_firm          s                   like(fdfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer output-parametre
      *
     c                   eval      p_vekt = 0
     c                   eval      p_volu = 0
      *
     c                   eval      p_avvi = *off
      *
      * Finner vekt og volum
      *
     c                   eval      fodtl1_numm = p_numm
     c                   eval      fodtl1_suff = p_suff
     c                   eval      fodtl1_line = p_line
     c     fodtl1_key    chain     fodtl1
     c                   if        %found
      *
     c                   if        fdvare <> *blank
     c                   exsr      kalkuler
     c                   endif
      *
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
      * Setter avviks-flagg dersom vekt- eller volum-informasjon ikke ble
      * funnet på alle varer
      *
     c                   if        b_vekt = *on or
     c                             b_vekt = *on
     c                   eval      p_avvi = *on
     c                   endif
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Kalkulerer vekt og volum for varer i en ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kalkuler      begsr
      *
5.70 c                   eval      p_vtyp = *blank
      *
     c                   eval      vevekt = 0
     c                   eval      vevolu = 0
     c                   eval      jwvekt = 0
     c                   eval      jwvolu = 0
      *
      * Behandler ikke tjenester og diverse-varer
      *
     c                   eval      vvarl1_vare = fdvare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c     vvarl1_key    chain     vvasl1
     c                   endif
      *
      * Kaller program for henting av prisgruppe
      *
     c                   call      'VL710R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   parm                    fdlage
     c                   parm                    p_vtyp
     c                   parm                    w_utda
     c                   parm                    w_ldor
     c                   parm                    b_inlr
     c                   parm                    w_lv_nobb
     c                   parm                    w_lv_modn
     c                   parm                    w_lv_lldo
     c                   parm                    w_lv_llva
      *
     c                   if        p_vtyp = 'T'
     c                   leavesr
     c                   endif
      *
     c                   if        p_vtyp = 'D'
     c                   eval      b_vekt = *on
     c                   eval      b_volu = *on
     c                   leavesr
     c                   endif
      *
      * Finner vekt og volum
      *
     c                   eval      vvenl1_vare = fdvare
     c                   eval      vvenl1_enhe = fdenh1
     c     vvenl1_key    chain     vvenl1
     c                   if        not %found or
     c                             vevekt = 0 or
     c                             vevolu = 0
     c                   eval      jvpkl2_vare = fdvare
6-31 c                   eval      jvpkl2_ldor = vvnlev
     c                   eval      jvpkl2_enhe = fdenh1
     c     jvpkl2_key    chain     jvpkl2
     c                   endif
      *
     c                   if        vevekt <> 0
     c                   eval      w_vekt = vevekt
     c                   else
6.33 c*                   eval      w_vekt = jwvekt * 1000
6.33 c                   eval      w_vekt = jwvekt / 1000
     c                   endif
      *
     c                   if        vevolu <> 0
     c                   eval      w_volu = vevolu
     c                   else
     c                   eval      w_volu = jwvolu
     c                   endif
      *
     c                   if        w_vekt = 0
     c                   eval      b_vekt = *on
     c                   endif
      *
     c                   if        w_volu = 0
     c                   eval      b_volu = *on
     c                   endif
      *
      * Summerer vekt og volum
      *
     c                   eval      p_vekt = p_vekt + (w_vekt * fdanta)
     c                   eval      p_volu = p_volu + (w_volu * fdanta)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Feilhåndtering
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *pssr         begsr
      *
     c                   eval      p_vekt = 0
     c                   eval      p_volu = 0
      *
     c                   goto      avslutt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_line
     c                   parm                    p_vekt
     c                   parm                    p_volu
     c                   parm                    p_avvi
      *
      * Key for les på ordre-linje
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
     c                   kfld                    fodtl1_line
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
     c                   kfld                    vvenl1_enhe
      *
      * Key for oppslag på vare-pakning (VA)
      *
     c     jvpkl2_key    klist
     c                   kfld                    jvpkl2_vare
     c                   kfld                    jvpkl2_ldor
     c                   kfld                    jvpkl2_enhe
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
