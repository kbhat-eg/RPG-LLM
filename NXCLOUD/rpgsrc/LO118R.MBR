      /TITLE  ASLAGR: Rydder opp i bestilling etter utskrift
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LO118R
      *  Beskrivelse . . : Rydder opp bestilling etter utskrift
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      *  --------- ------  -------------------------------------------------
      *   6.10     180609  Oppdatert med sporingsinformasjon       BSA
6.nu  *   6.30     110614  Sjekket mtp. nummerutvidelser           bof
      *
8.01  *K0000  bsa  280824  Endret program for sletting av bestillinger.
8.01  *K0000               Logger også ved fakturering.
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flpsol1    if   e           k disk    rename(lpsopfr:lpsol1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     flohelu    uf   e           k disk    rename(lohepfr:lohelur)
      *
      * Definering av LDA
      *
     d                uds
6.10 d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d votyl1_otyp     s                   like(vaotyp)
     d lohelu_numm     s                   like(lonumm)
     d lohelu_suff     s                   like(losuff)
      *
      * Definering av øvrige variable
      *
     d w_firm          s                   like(lofirm)
8.01 d w_type          s              4
8.01 d w_kom1          s             30
8.01 d w_kom2          s             30
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Behandler bestillingene som er skrevet ut
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Leser parameter for henting av verdier
      *
     c     lpsol1_key    reade     lpsol1                                 90
      *
     c                   dow       *in90 = *off
     c                   if        lkskod = 'H'
     c                   exsr      rydd_opp
     c                   endif
     c     lpsol1_key    reade     lpsol1                                 90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
      *
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for opprydding
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     rydd_opp      begsr
      *
      *  Legger inn verdier i nøklene
      *
     c                   eval      lohelu_numm = lksonr
     c                   eval      lohelu_suff = lkssuf
      *
      *  Hent opplysninger fra ORDRETYPE
      *
     c                   eval      votyl1_otyp = lksoty
     c     votyl1_key    chain     votyl1r                            90
      *
      *  Sletter ordren etter at faktura/Kreditnota er skrevet
      *
     c                   if        vaoakk = 3
8.01 c                   eval      w_type = 'FAKT'
8.01 c                   eval      w_kom1 = *blanks
8.01 c                   eval      w_kom2 = *blanks
8.01 c                   call      'LO401R'
     c                   parm                    w_firm
     c                   parm                    lksonr
     c                   parm                    lkssuf
8.01 c                   parm                    w_type
8.01 c                   parm                    w_kom1
8.01 c                   parm                    w_kom2
     c                   goto      rydd_ut
     c                   endif
      *
      *  Opphever merkingen av bestillingen slik at den kan behandles igjen
      *
     c     lohelu_key    chain     lohelur                            90
     c                   if        *in90 = *off
     c                   eval      lokode = *zero
     c                   eval      lokoti = *loval
     c                   eval      lokoda = *loval
     c                   eval      lokous = *blank
     c                   eval      lokows = *blank
6.10 c                   time                    loedat
6.10 c                   time                    loetim
6.10 c                   eval      loeusr = l_user
     c                   update    lohelur
     c                   endif
      *
     c     rydd_ut       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     *inzsr        begsr
      *
      *  Key for les av PARAMETRE
      *
     c     lpsol1_key    klist
     c                   kfld                    w_firm
      *
      *  Key for les av ORDRE-TYPE
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      *  Key for oppdatering av BESTILLING-HODE
      *
     c     lohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelu_numm
     c                   kfld                    lohelu_suff
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
