      /TITLE  Utskrift av åpne poster  - kunder
     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RK636R                                              *
      * Beskrivelse . . : Utskrift av åpne poster  - kunder                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * V5.50 071105 KOB  Nytt program
T5.50 * V5.50 120907 HFL  Skriver restbeløp i stedet for opprinnelig beløp.
7.00  *       110325 mst  Innført PDF utskrift,
      *                   for å kunne sende utskrift på mail
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     frktrl9    if   e           k disk    rename(rktrpfr:rktrl9r)
     faforl1    if   e           k disk    rename(aforpfr:aforl1r)
     frk636p    o    e             printer oflind(*in30)
7.00 f                                     usropn
      *
      *
      /eject
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      * Data struktur
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      *
     d w_firm          s                   like(rkfirm)
      *
     d w_prfi          s              1  0
     d w_ffir          s              3  0
     d w_avde          s              4  0
     d w_fnav          s             30
     d w_fad1          s             30
     d w_fad2          s             30
     d w_fste          s             30
     d w_fftx          s             30
     d w_tfax          s              8
     d w_ftlf          s             11
     d w_ffax          s             11
     d w_tbnk          s             10
     d w_fbgi          s             13
      *
     d w_pper          s              6  0
     d w_kper          s              6  0
      *
     d p_kund          s                   like(rkkund)
      *
     d rkunl1_kund     s                   like(rkkund)
     d rktrl9_kund     s                   like(rnkund)
     d aforl1_ruti     s                   like(afruti)
      *
7.00 d p_btyp          s            100
      *
7.00 d splfname2       s             10a
7.00 d jobname2        s             10a
7.00 d username2       s             10a
7.00 d jobnbr2         s              6a
7.00 d splfnbr2        s             10i 0
7.00 d splfname3       s             10a
7.00 d jobname3        s             10a
7.00 d username3       s             10a
7.00 d jobnbr3         s              6a
7.00 d splfnbr3        s             10i 0
7.00 d p_tagg0         s             20
7.00 d p_tagg0val      s             50
7.00 d p_tagg1         s             20
7.00 d p_tagg1val      s             50
7.00 d p_tagg2         s             20
7.00 d p_tagg2val      s             50
7.00 d p_tagg3         s             20
7.00 d p_tagg3val      s             50
7.00 d p_tagg4         s             20
7.00 d p_tagg4val      s             50
7.00 d p_tagg5         s             20
7.00 d p_tagg5val      s             50
7.00 d p_tagg6         s             20
7.00 d p_tagg6val      s             50
7.00 d p_tagg7         s             20
7.00 d p_tagg7val      s             50
7.00 d p_tagg8         s             20
7.00 d p_tagg8val      s             50
7.00 d p_tagg9         s             20
7.00 d p_tagg9val      s             50
7.00 d p_refn          s             50
7.00 d p_dspl          s            100
7.00  *
7.00 d qsprilsp        pr                  extpgm('QSPRILSP')
7.00 d rcvvar                     32767a   options(*varsize)
7.00 d rcvvarlen                     10i 0 const
7.00 d format                         8a   const
7.00 d errorcode                  32767a   options(*varsize)
7.00  *
7.00 d sprl0100        ds
7.00 d  bytesrtn                     10i 0
7.00 d  bytesavl                     10i 0
7.00 d  splfname                     10a
7.00 d  jobname                      10a
7.00 d  username                     10a
7.00 d  jobnbr                        6a
7.00 d  splfnbr                      10i 0
7.00 d  systemname                    8a
7.00 d  createdate                    7a
7.00 d                                1a
7.00 d  createtime                    6a
7.00  *
7.00 d errorcode       ds
7.00 d  bytesprov                    10i 0 inz(0)
7.00 d  byteavail                    10i 0 inz(0)
7.00  *
      *
      * Local data
      *
     d                uds
     d l_dato                300    305  0
     d l_aarf                         4  0
     d l_aart                         4  0
     d l_perf                         2  0
     d l_pert                         2  0
7.00 d l_poff                584    584
7.00 d l_bach                595    635
7.00 d l_skje                637    637  0
     d l_ruti                800    809
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      /eject
      *
      * Leser kunde
      *
     c                   eval      rkunl1_kund = p_kund
     c     rkunl1_key    chain     rkunl1                             61
      *
     c                   eval      t1kjØp = rkkjØp
     c                   eval      t1kjØf = rkkjØf
      *
      * Skriv ut faste data
      *
     c                   write     head
      *
      * Leser kundeposteringer
      *
     c                   exsr      les_kuntr
      *
      * Skriv ut totaler
      *
     c                   write     total
      *
     c     slutt         tag
      *
     c                   eval      *inlr = *on                                  ..............
7.00  * Generer xml for videre utskrift
7.00 clr                 if        l_poff = '1'
7.00 clr                 exsr      spoolnbr
7.00 clr                 close     rk636p
7.00 clr                 exsr      xml_create
7.00 clr                 exsr      pdf_preview
7.00  * Avslutt jobbiden
7.00 clr                 call      'AB710R'
7.00 c                   parm                    l_bach
7.00 clr                 endif
      *
      /eject
      *
      * Leser kundetransaksjoner
      *
     c     les_kuntr     begsr
      *
     c                   eval      rktrl9_kund = p_kund
     c     rktrl9_key    setll     rktrl9
     c     rktrl9_key    reade     rktrl9                                 62
      *
     c                   dow       *in62 = *off
      *
      * Sjekk mot periode
      *
     c                   movel     rnraar        w_kper
     c                   move      rnrper        w_kper
      *
     c                   if        w_kper <= w_pper
      *
      * Ta vare på siste transaksjonsdato
      *
     c     *dmy          move      rnbdat        t1sdat
      *
      * Beregn saldo
      *
E5.50c                   eval      t1sald = t1sald + rnrest                     Saldo
      *
      * Skriver detalj-linje
      *
     c     *dmy          move      rnbdat        d1bdat
     c     *dmy          move      rnfdat        d1fdat
      *
     c                   write     linje
      *
      * Overflow ?
      *
     c                   if        *in30 = *on
     c                   write     head
     c                   eval      *in30 = *off
     c                   endif
      *
     c                   endif
      *
     c     rktrl9_key    reade     rktrl9                                 62
     c                   enddo                                                     fordeling
      *
     c                   endsr
      *
      /eject
7.00  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
7.00  * Finner spool som er generert av jobben
7.00  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
7.00 c     spoolnbr      begsr
7.00  /Free
7.00
7.00           QSPRILSP( SPRL0100
7.00                   : %size(SPRL0100)
7.00                   : 'SPRL0100'
7.00                   : errorcode );
7.00  /End-free
7.00 c*
7.00  *
7.00 c                   endsr
7.00  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
7.00  * Kaller eget program for generering av xml fil. Eget pgm         *
7.00  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
7.00 c     xml_create    begsr
7.00  *
7.00 c                   eval      splfname2 = *blanks
7.00 c                   eval      splfname3 = *blanks
7.00 c                   eval      jobname2 = *blanks
7.00 c                   eval      jobname3 = *blanks
7.00 c                   eval      username2 = *blanks
7.00 c                   eval      username3 = *blanks
7.00 c                   eval      jobnbr2 = *blanks
7.00 c                   eval      jobnbr3 = *blanks
7.00 c                   eval      splfnbr2 = *zeros
7.00 c                   eval      splfnbr3 = *zeros
7.00 c                   eval      p_btyp = 'AAPEN POST  '
7.00 c                   eval      p_dspl = 'AApen post liste   , '+
7.00 c                                      ' kjørt dato: '+
7.00 c                                      %char(udate)
7.00 c                   eval      p_refn = 'KKU-' + %char(udate)
7.00 c                   eval      p_tagg0 = *blanks
7.00 c                   eval      p_tagg0val = *blanks
7.00 c                   eval      p_tagg1 = *blanks
7.00 c                   eval      p_tagg1val = *blanks
7.00 c                   eval      p_tagg2 = *blanks
7.00 c                   eval      p_tagg2val = *blanks
7.00 c                   eval      p_tagg3 = *blanks
7.00 c                   eval      p_tagg3val = *blanks
7.00 c                   eval      p_tagg4 = *blanks
7.00 c                   eval      p_tagg4val = *blanks
7.00 c                   eval      p_tagg5 = *blanks
7.00 c                   eval      p_tagg5val = *blanks
7.00 c                   eval      p_tagg6 = *blanks
7.00 c                   eval      p_tagg6val = *blanks
7.00 c                   eval      p_tagg7 = *blanks
7.00 c                   eval      p_tagg7val = *blanks
7.00 c                   eval      p_tagg8 = *blanks
7.00 c                   eval      p_tagg8val = *blanks
7.00 c                   eval      p_tagg9 = *blanks
7.00 c                   eval      p_tagg9val = *blanks
7.00  *
7.00 c**                 call      'AP620R'
7.00 c                   call      'AP627R'
7.00 c                   parm                    splfname
7.00 c                   parm                    jobname
7.00 c                   parm                    username
7.00 c                   parm                    jobnbr
7.00 c                   parm                    splfnbr
7.00 c                   parm                    splfname2
7.00 c                   parm                    jobname2
7.00 c                   parm                    username2
7.00 c                   parm                    jobnbr2
7.00 c                   parm                    splfnbr2
7.00 c                   parm                    splfname3
7.00 c                   parm                    jobname3
7.00 c                   parm                    username3
7.00 c                   parm                    jobnbr3
7.00 c                   parm                    splfnbr3
7.00 c                   parm                    p_tagg0
7.00 c                   parm                    p_tagg0val
7.00 c                   parm                    p_tagg1
7.00 c                   parm                    p_tagg1val
7.00 c                   parm                    p_tagg2
7.00 c                   parm                    p_tagg2val
7.00 c                   parm                    p_tagg3
7.00 c                   parm                    p_tagg3val
7.00 c                   parm                    p_tagg4
7.00 c                   parm                    p_tagg4val
7.00 c                   parm                    p_tagg5
7.00 c                   parm                    p_tagg5val
7.00 c                   parm                    p_tagg6
7.00 c                   parm                    p_tagg6val
7.00 c                   parm                    p_tagg7
7.00 c                   parm                    p_tagg7val
7.00 c                   parm                    p_tagg8
7.00 c                   parm                    p_tagg8val
7.00 c                   parm                    p_tagg9
7.00 c                   parm                    p_tagg9val
7.00 c                   parm                    l_bach
7.00 c                   PARM                    p_btyp
7.00 c                   PARM                    p_refn
7.00 c                   PARM                    p_dspl
7.00  *
7.00 c                   endsr
7.00  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
7.00  * Subrutiner for XML/jasper integrasjon - Skjermvisning           *
7.00  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
7.00 c     pdf_preview   begsr
7.00 c                   if        l_skje = 1
7.00  * Vi kaller program for launch av PDF i browser
7.00 c                   call      'AP621R'
7.00 c                   parm                    l_bach
7.00  *
7.00 c                   endif
7.00  *
7.00 c                   endsr
7.00  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_kund
      *
7.00 c                   open      rk636p
     c                   eval      w_firm = l_firm
      *
     c                   movel     l_aart        w_pper
     c                   move      l_pert        w_pper
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
     c     rktrl9_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rktrl9_kund
      *
      * Key for rutine-register
      *
     c     aforl1_key    klist
     c                   kfld                    aforl1_ruti
      *
      * Hente inn formular-register
      *
     c                   eval      aforl1_ruti = l_ruti
     c     aforl1_key    chain     aforl1r
      *
     c                   if        not%found
     c                   eval      afprfi = 1
     c                   endif
      *
      *  Hent inn firma opplysninger dersom
      *  kode for det er satt i rutineregister
      *
     c                   if        afprfi = 1
     c                   eval      w_prfi = afprfi
     c                   eval      w_ffir = w_firm
     c                   eval      w_avde = 0
      *
     c                   call      'RÅ707R'
     c                   parm                    w_prfi
     c                   parm                    w_ffir
     c                   parm                    w_avde
     c                   parm                    w_fnav
     c                   parm                    w_fad1
     c                   parm                    w_fad2
     c                   parm                    w_fste
     c                   parm                    w_fftx
     c                   parm                    w_tfax
     c                   parm                    w_ftlf
     c                   parm                    w_ffax
     c                   parm                    w_tbnk
     c                   parm                    w_fbgi
      *
     c                   eval      a1fnav = w_fnav
     c                   eval      a1fad1 = w_fad1
     c                   eval      a1fad2 = w_fad2
     c                   eval      a1fste = w_fste
     c                   eval      a1fftx = w_fftx
     c                   eval      a1tfax = w_tfax
     c                   eval      a1ftlf = w_ftlf
     c                   eval      a1ffax = w_ffax
     c                   eval      a1tbnk = w_tbnk
     c                   eval      a1fbgi = w_fbgi
      *
      * Skal firmanavn droppes ?
      *
     c                   if        w_prfi = 0
     c                   eval      a1fnav = *blank
     c                   eval      a1fad1 = *blank
     c                   eval      a1fad2 = *blank
     c                   eval      a1fste = *blank
     c                   eval      a1fftx = *blank
     c                   eval      a1tfax = *blank
     c                   eval      a1ftlf = *blank
     c                   eval      a1ffax = *blank
     c                   eval      a1tbnk = *blank
     c                   eval      a1fbgi = *blank
     c                   endif
      *
     c                   endif
      *
     c                   endsr
      *
