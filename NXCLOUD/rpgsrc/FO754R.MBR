     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : fo754R
      * Beskrivelse . . : Oppdatere depositum med innbetalt beløp
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       260117 bof  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *    31-79 = Varsle m/feilmeldinger/skvdrmindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffst2l1    if   e           k disk    rename(fst2pfr:fst2l1r)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     fsdepl1    if   e           k disk    rename(sdeppfr:sdepl1r)
     fsdeplu    uf   e           k disk    rename(sdeppfr:sdeplur)
      *
     ffo753d    cf   e             workstn
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variable i nøkler
     d sdepl1_numm     s                   like(sknumm)
     d sdepl1_kund     s                   like(skkund)
     d sdepl1_tell     s                   like(sktell)
     d sdeplu_numm     s                   like(sknumm)
     d sdeplu_kund     s                   like(skkund)
     d sdeplu_tell     s                   like(sktell)

      *
      *
      * Definering av variable
      *
     d b_funn          s              1    inz(*off)
      *
     d w_firm          s              3  0
     d w_kidi          s             25
     d w_belØ          s             11  2
      *
     d p_firm          s              3  0
     d p_kidi          s             25
     d p_belØ          s             11  2
     d p_bkod          s              2  0
      *
6.40  *
6.40  *       Lang kid forskuddsbetaling
6.40  *
6.40 d                 ds
6.40 d d_kkid                        21
6.40 d  d_kkus                       20    overlay(d_kkid)
6.40 d  d_kfir                        3  0 overlay(d_kkus)
6.40 d  d_kkod                        1    overlay(d_kkus:4)
6.40 d  d_k001                        1    overlay(d_kkus:5)
6.40 d  d_kmva                        1    overlay(d_kkus:6)
6.40 d  d_kkun                        6  0 overlay(d_kkus:7)
6.40 d  d_kfak                        8  0 overlay(d_kkus:13)
6.40 d d_kmod                         1  0 overlay(d_kkid:21)

      *
      * sjekker om depositum
      *
     c                   eval      sdepl1_kund = d_kkun
     c                   eval      sdepl1_numm = d_kfak
     c                   eval      sdepl1_tell = 0
     c     sdepl1_key    chain     sdepl1
     c                   if        not %found(sdepl1)
     c                   goto      avslutt
     c                   endif
     c                   if        skiupd = 1
     c                   goto      avslutt
     c                   endif
      * oppdaterer
     c                   eval      sdeplu_kund = d_kkun
     c                   eval      sdeplu_numm = d_kfak
     c                   eval      sdeplu_tell = 0
     c     sdeplu_key    chain     sdeplu
     c                   if        %found(sdeplu)
     c                   eval      skityp = 'B'
     c                   eval      skdepb = skdepb + w_belØ
     c                   time                    skedat
     c                   time                    sketim
     c                   eval      skeusr = l_user
     c                   update    sdeplur
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      * sjekk om avvikende bilagskode
     c                   if        faafbk <> 0
     c                   eval      p_bkod = faafbk
     c                   endif
      *
     c                   eval      *inlr = *on
     c                   return
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_kidi
     c                   parm                    p_belØ
     c                   parm                    p_bkod
      *
      * Key for oppslag på depositum-fil
      *
     c     sdepl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sdepl1_numm
     c                   kfld                    sdepl1_kund
     c                   kfld                    sdepl1_tell
      *
      * Key for oppslag på depositum-fil
      *
     c     sdeplu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sdeplu_numm
     c                   kfld                    sdeplu_kund
     c                   kfld                    sdeplu_tell
      *
      * Key for file : STATUS-KODER OFAK
      *
     c     fst2l1_key    klist
     c                   kfld                    w_firm
      *
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_kidi = p_kidi
     c                   eval      w_belØ = p_belØ * -1
     c                   move      w_kidi        d_kkid
      *
6.40 c     fst2l1_key    chain     fst2l1r                            90
     c
     c                   endsr
