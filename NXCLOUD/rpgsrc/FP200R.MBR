     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * System. . . . . : ASOFAK                                              *
      * Program . . . . : FP200R                                        *
      * Beskrivelse . . : Etiketter - utskrift/parameter                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ---------------------------------------- -----*
      * V5.30     140304  Lagt ut lager som parameter              KOB  *
      * V5.30     180404  Lagt inn kontroll mot at varen finnes    KOB  *
      *                   i lagerregisteret                             *
      * V6.10     160909  Lagt inn valg på leverandør og over-     BSA  *
      *                   styring av allerede utskrevne etiketter       *
7.xx  * 7.00  150316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KA = F1  = Valg av firma                                  *
      *       KC = F3  = Exit                                           *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     ffp200d    cf   e             workstn
      *
     d                uds
      *
     d l_vtyp                124    124
     d l_kamp                125    125
6.10 d l_ldor                126    131  0
6.10 d l_omkj                132    132
      *
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
     d l_lage               1001   1002  0
      *
     d p_in03          s              1
     d p_stat          s              1                                           kode
     d p_jkod          s                   like(a2lage)                           kode
     d p_jtxt          s             20                                           kode
     d p_lage          s                   like(a2lage)                           kode
     d p_alle          s                   like(a2alle)
      *
     d w_firm          s                   like(l_firm)
     d w_user          s                   like(l_user)
     d w_navn          s                   like(l_fnav)
      *
      /eject
     c     dummy         tag
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av felter                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      *
      * A2BLD  - A2 Bilde for valg av parametre
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * A2BLD Behandle bilde for valg av utskriftskriteria              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     a2taga        tag
      *
      * Bare data
      *
     c     a2tagb        tag
      *
     c                   exfmt     a2bld
      *
      * Gå ut
      *
     c                   if        *inkc = *on                                  .....
     c                   move      '1'           p_in03                             .
     c                   goto      xslutt                                           .
     c                   endif                                                  .....
6.10  *
6.10  *  Spørring på leverandør
6.10  *
6.10 c                   if        *inkd = *on
6.10 c                   if        w_afld = 'A2LDOR'
6.10 c                   call      'RL500R'
6.10 c                   parm                    w_firm
6.10 c                   parm                    a2ldor
6.10 c                   parm                    a2ltek
6.10 c                   goto      a2tagb
6.10 c                   endif
6.10 c                   endif
      *
      * Sjekk lager
      *
     c                   if        a2lage <> 0                                  .....
     c                   move      a2lage        p_jkod                                      .
     c                   call      'RS710R'                                      lager-
     c                   parm                    p_stat                           kode
     c                   parm                    p_jkod                           kode
     c                   parm                    p_jtxt                           kode
      *
     c                   if        p_stat = 'N'                                 Kode
     c                   eval      *in30 = *on                                      .
     c                   goto      a2tagb                                           .
     c                   endif                                                  .....
     c                   endif                                                  .....
      *
      * Sjekk varetype
      *
     c                   if        a2vtyp <> 'L' and                            .....
     c                             a2vtyp <> 'S' and                            .....
     c                             a2vtyp <> ' '                                .....
     c                   eval      *in31 = *on                                      .
     c                   goto      a2tagb                                           .
     c                   endif                                                  .....
      *
      * Sjekk kampanje med
      *
     c                   if        a2kamp <> 'J' and                            .....
     c                             a2kamp <> ' '                                .....
     c                   eval      *in32 = *on                                      .
     c                   goto      a2tagb                                           .
     c                   endif                                                  .....
6.10  *
6.10  * Sjekk valg for overstyring av pris
6.10  *
6.10 c                   if        a2omkj <> ' ' and                            .....
6.10 c                             a2omkj <> 'J'
6.10 c                   eval      *in33 = *on                                      .
6.10 c                   goto      a2tagb                                           .
6.10 c                   endif                                                  .....
      *
      * Skriv parameter
      *
     c                   move      a2vtyp        l_vtyp
     c                   move      a2kamp        l_kamp
6.11 c                   move      a2ldor        l_ldor
6.11 c                   move      a2omkj        l_omkj
      *
     c                   move      a2lage        p_lage
     c                   move      a2alle        p_alle
      *
      /space 3
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c                   eval      p_in03 = ' '
     c                   eval      a2lage = l_lage
     c                   eval      a2alle = ' '
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_in03
     c                   parm                    p_lage
     c                   parm                    p_alle
      *
     c                   endsr
