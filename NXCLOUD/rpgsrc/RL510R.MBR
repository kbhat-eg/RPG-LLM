     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RL510R
      * Beskrivelse . . : Leverandørposteringer, spørring bilag/beløp
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       060802 KOB  Nytt program
      * V5.30 121004 HFL  Beløp utvidet til 11.2.
      * V5.30 141004 KOB  Nullstilt nøkkel etter eof
      * V5.30 181004 KOB  Lagt inn dato som nytt posisjoneringsfelt
      * V5.40 220305 KOB  Lagt inn raar i logisk file rltrl4
      * V5.40 220305 KOB  Lagt inn ny logisk file rltrlb (raar/biln)
      * V5.40 220305 KOB  Lagt ut lev.fakturanummer i skjermbildet
      * V5.40 220305 KOB  Lagt inn søk på lev.fakturanummer
      * V5.40 160505 KOB  Lagt ut attestert av og dato i bildet
      * V5.41 280805 KOB  Lagt inn ny funksjon for å vise arkivpost
      * V5.60 030308 HFL  Tatt vekk raar i logisk file rltrl4
      * V5.60 120308 HFL  Logisk file rltrl3 for bilagnr. uansett år.
pdf   * R6.10     041010  Henter fra ASP arkiv. Mulighet for valg mellom BSA  *
pdf   *                   ASP og Multiarkiv gjøres i AP622C. Det finnes  BSA  *
pdf   *                   to varianter av dette programmet.              BSA  *
6.20  * R6.20     070611  Starter arkiv, hvis ikke faktura               BSA  *
6.21  * R6.20     210711  Legger ut firmanummer i skjultfelt for         bsa  *
6.21  *                   spørring mot ASP-arkiv fra Jacada              bsa  *
6.22  * R6.20     061212  Endret metode for oppslag mot arkiv            bsa  *
6.23  * R6.20     040113  Endret oppbygging av refno                     bsa  *
6.24  * R6.20     270213  Egen kode for forskjellige bilag               bsa  *
6.25  * R6.20     260313  Henter dokumenttype basert på bilagskode       bsa  *
7.xx  * 7.xx      030516  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frltrlc    if   e           k disk    rename(rltrpfr:rltrlcr)
T5.60frltrl3    if   e           k disk    rename(rltrpfr:rltrl3r)
     frltrle    if   e           k disk    rename(rltrpfr:rltrler)
     frltrl4    if   e           k disk    rename(rltrpfr:rltrl4r)
     frltrlb    if   e           k disk    rename(rltrpfr:rltrlbr)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     frltrl2    if   e           k disk    rename(rltrpfr:rltrl2r)
      *
     frl510d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d rltrlc_raar                         like(roraar)
     d rltrlc_biln                         like(robiln)
T5.60d rltrl3_biln                         like(robiln)
     d rltrle_raar                         like(roraar)
     d rltrle_lfak                         like(rolfak)
S5.60d*rltrl4_raar     s                   like(roraar)
     d rltrl4_belØ     s                   like(robelØ)
     d rltrlb_bdat     s                   like(robdat)
     d rlevl1_levr     s                   like(rolevr)
     d rltrl2_levr     s                   like(rolevr)
     d rltrl2_bdat     s                   like(robdat)
     d rltrl2_tist     s                   like(rotist)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_valg_ok       s              1    inz(*off)
      *
     d w_firm          s                   like(rofirm)
     d w_biln          s                   like(robiln)
     d w_lfak          s                   like(rolfak)
     d w_belØ          s                   like(robelØ)
     d w_bdat          s                   like(robdat)
     d w_raar          s                   like(roraar)
pdf  d*w_modu          s             10    inz('MSA     ')
6.22 d w_type          s             50
pdf  d w_modu          s             10    inz('PROA    ')
     d w_disp          s              1    inz('1')
     d w_tilg          s              1
6.22 d w_refn          s             50
6.25 d w_dtyp          s             50
6.25 d w_year          s              1
6.25 d w_ok            s              1
      *
      * Definering av parametre MultiArcive
      *
     d w_pgrp          s              2
     d w_aara          s              4
     d w_bila          s              6
     d w_afir          s              3
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(13)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * d_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2biln <> 0 or
     c                             b2lfak <> ' ' or
     c                             b2belØ <> 0 or
     c                             b2bdat <> 0 or
     c                             b2raar <> w_raar
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        d_fcrn <> 0
     c     d_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
S5.60c*                  eval      rltrl4_raar = b2raar
     c                   eval      rltrl4_belØ = b1belØ
     c     rltrl4_key    setll     rltrl4
     c                   when      w_seqe = 'N'
     c                   eval      rltrlc_raar = b2raar
     c                   eval      rltrlc_biln = b2biln
     c     rltrlc_key    setll     rltrlc
T5.60c                   when      w_seqe = 'M'
T5.60c                   eval      rltrl3_biln = b2biln
T5.60c     rltrl3_key    setll     rltrl3
     c                   when      w_seqe = 'L'
     c                   eval      rltrle_raar = b2raar
     c                   eval      rltrle_lfak = b2lfak
     c     rltrle_key    setll     rltrle
     c                   other
     c                   eval      rltrlb_bdat = b1bdat
     c     rltrlb_key    setll     rltrlb
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi, regnskapsår endret
      *
     c                   if        b2raar <> w_raar
     c                   eval      w_raar = b2raar
      *
     c                   if        b2biln = 0 and
     c                             b2belØ = 0 and
     c                             b2lfak = ' ' and
     c                             b2bdat = 0
     c                   eval      b2biln = 1
     c                   endif
      *                                                                 .
     c                   endif
      *
      * Bilagsnummer
      *
     c                   if        b2biln <> 0
     c                   eval      w_biln = b2biln
     c                   eval      w_belØ = 0
     c                   eval      w_bdat = *loval
T5.60c                   if        b2raar <> 0
     c                   eval      w_seqe = 'N'
     c                   eval      rltrlc_raar = b2raar
     c                   eval      rltrlc_biln = b2biln
     c     rltrlc_key    setll     rltrlc
T5.60c                   else
T5.60c                   eval      w_seqe = 'M'
T5.60c                   eval      rltrl3_biln = b2biln
T5.60c     rltrl3_key    setll     rltrl3
T5.60c                   endif
     c                   goto      end_pos
     c                   endif
      *
      * Leverandørens fakturanummer
      *
     c                   if        b2lfak <> ' '
     c                   eval      w_seqe = 'L'
     c                   eval      w_lfak = b2lfak
     c                   eval      w_belØ = 0
     c                   eval      w_biln = 0
     c                   eval      w_bdat = *loval
     c                   eval      rltrle_raar = b2raar
     c                   eval      rltrle_lfak = b2lfak
     c     rltrle_key    setll     rltrle
     c                   goto      end_pos
     c                   endif
      *
      * Beløp
      *
     c                   if        b2belØ <> 0
     c                   eval      w_seqe = 'B'
     c                   eval      w_belØ = b2belØ
     c                   eval      w_biln = 0
     c                   eval      w_lfak = ' '
     c                   eval      w_bdat = *loval
S5.60c*                  eval      rltrl4_raar = b2raar
     c                   eval      rltrl4_belØ = b2belØ
     c     rltrl4_key    setll     rltrl4
     c                   goto      end_pos
     c                   endif
      *
      * Bilagsdato
      *
     c                   if        b2bdat <> 0
     c                   eval      w_seqe = 'D'
     c     *dmy          move      b2bdat        w_bdat
     c                   eval      w_biln = 0
     c                   eval      w_lfak = ' '
     c                   eval      w_belØ = 0
     c     *dmy          move      b2bdat        rltrlb_bdat
     c     rltrlb_key    setll     rltrlb
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2biln = 0
     c                   eval      b2lfak = ' '
     c                   eval      b2belØ = 0
     c                   eval      b2bdat = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = d_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   exsr      xc2win
      *
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis arkiv
      *
     c                   if        b1valg = 9
      *
      * Kontrollerer gyldig tilgang
      *
     c                   call      'AX700R'
     c                   parm                    w_modu
     c                   parm                    w_disp
     c                   parm                    w_tilg
      *
     c                   if        w_tilg = '0'
6.25  *
6.25  * Hent dokumenttype basert på bilagskoden
6.25  *
6.25 c                   eval      w_dtyp = *blanks
6.25 c                   eval      w_year = *blanks
6.25 c                   eval      w_ok = '0'
6.25  *
6.25 c                   call      'AP632R'
6.25 c                   parm                    w_firm
6.25 c                   parm                    b1bilk
6.25 c                   parm                    w_dtyp
6.25 c                   parm                    w_year
6.25 c                   parm                    w_ok
6.25  *
6.25 c                   if        w_ok = '1'
6.25 c                   eval      w_type = w_dtyp
6.25 c                   if        w_year = 'J'
6.25 c                   eval      w_refn = %char(b1raar)+%trim(%char(b1biln))
6.25 c                   else
6.25 c                   eval      w_refn = %trim(%char(b1biln))
6.25 c                   endif
6.25 c                   else
6.25  * Gammel logikk hvis den ikke finnes
      *
6.24 c                   if        b1bilk = 1 or
6.24 c                             b1bilk = 3
pdf  c                   eval      w_type = 'FAKTURA'
6.23 c                   eval      w_refn = %char(b1raar)+%trim(%char(b1biln))
6.24 c                   elseif    b1bilk = 2 or
6.24 c                             b1bilk = 4
6.22 c                   eval      w_type = 'INNGÅENDE%20FAKTURA'
6.23 c                   eval      w_refn = %trim(%char(b1biln))
6.24 c                   else
6.24 c                   eval      w_type = 'DIVERSEBILAG'
6.24 c                   eval      w_refn = %char(b1raar)+%trim(%char(b1biln))
pdf  c                   endif
6.25 c                   endif
      *
     c                   move      w_firm        w_afir
     c                   eval      w_pgrp = '30'
     c                   eval      w_aara = %char(b1raar)
     c                   eval      w_bila = %char(b1biln)
      *
pdf  c                   call      'AP622C'
pdf  c                   parm                    w_type
6.22 c                   parm                    w_refn
6.22 c*                  parm                    w_aara
6.22 c*                  parm                    w_bila
6.22 c*                  parm                    w_pgrp
pdf  c**                 call      'AH720C'
pdf  c**                 parm                    w_pgrp
pdf  c**                 parm                    w_afir
pdf  c**                 parm                    w_aara
pdf  c**                 parm                    w_bila
      *
     c                   endif
      *
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      rltrl4
     c                   when      w_seqe = 'N'
     c                   read      rltrlc
T5.60c                   when      w_seqe = 'M'
T5.60c                   read      rltrl3
     c                   when      w_seqe = 'L'
     c                   read      rltrle
     c                   other
     c                   read      rltrlb
     c                   endsl
      *
     c                   if        %eof or
     c                             rofirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *                                                                 .
     c                   if        b2raar <> *zero and
     c                             b2raar <> roraar
     c                   iter
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
S5.60c*                  eval      rltrl4_raar = roraar
     c                   eval      rltrl4_belØ = robelØ
     c                   when      w_seqe = 'N'
     c                   eval      rltrlc_raar = roraar
     c                   eval      rltrlc_biln = robiln
T5.60c                   when      w_seqe = 'M'
T5.60c                   eval      rltrl3_biln = robiln
     c                   when      w_seqe = 'L'
     c                   eval      rltrle_raar = roraar
     c                   eval      rltrle_lfak = rolfak
     c                   other
     c                   eval      rltrlb_bdat = robdat
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1bdat = robdat
     c                   eval      b1raar = roraar
     c                   eval      b1tist = rotist
     c                   eval      b1biln = robiln
     c     *dmy          move      robdat        b1bida
     c                   eval      b1bilk = robilk
      *
     c                   if        rolfak = *blank
     c                   eval      b1text = rotext
     c                   else
     c                   eval      b1text = %trim(rolfak) + '/' +
     c                                      %trim(rotext)
     c                   endif
      *
     c                   eval      b1belØ = robelØ
     c                   eval      b1levr = rolevr
      *
      * Hent leverandør
      *
     c                   eval      rlevl1_levr = rolevr
     c     rlevl1_key    chain     rlevl1                             61
     c                   if        *in61 = *off
     c                   eval      b1navn = rlnavn
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'B'
     c     rltrl4_key    setgt     rltrl4
     c                   readp     rltrl4
     c                   when      w_seqe = 'N'
T5.30c                   eval      rltrlc_raar = b2raar
T5.30c                   eval      rltrlc_biln = 0
     c     rltrlc_key    setgt     rltrlc
     c                   readp     rltrlc
T5.60c                   when      w_seqe = 'M'
T5.60c                   eval      rltrl3_biln = 0
T5.60c     rltrl3_key    setgt     rltrl3
T5.60c                   readp     rltrl3
     c                   when      w_seqe = 'L'
T5.30c                   eval      rltrle_raar = b2raar
T5.30c                   eval      rltrle_lfak = ' '
     c     rltrle_key    setgt     rltrle
     c                   readp     rltrle
     c                   other
T5.30c                   eval      rltrlb_bdat = *loval
     c     rltrlb_key    setgt     rltrlb
     c                   readp     rltrlb
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   readp     rltrl4
     c                   when      w_seqe = 'N'
     c                   readp     rltrlc
T5.60c                   when      w_seqe = 'M'
T5.60c                   readp     rltrl3
     c                   when      w_seqe = 'L'
     c                   readp     rltrle
     c                   other
     c                   readp     rltrlb
     c                   endsl
      *
     c                   if        %eof or
     c                             rofirm <> w_firm
     c                   leave
     c                   endif
      *                                                                 .
     c                   if        b2raar <> *zero and
     c                             b2raar <> roraar
     c                   iter
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      rltrl4_belØ = robelØ
     c                   when      w_seqe = 'N'
     c                   eval      rltrlc_biln = robiln
T5.60c                   when      w_seqe = 'M'
T5.60c                   eval      rltrl3_biln = robiln
     c                   when      w_seqe = 'L'
     c                   eval      rltrle_lfak = rolfak
     c                   other
     c                   eval      rltrlb_bdat = robdat
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'B'
     c     rltrl4_key    setll     rltrl4
     c                   when      w_seqe = 'N'
     c     rltrlc_key    setll     rltrlc
T5.60c                   when      w_seqe = 'M'
T5.60c     rltrl3_key    setll     rltrl3
     c                   when      w_seqe = 'L'
     c     rltrle_key    setll     rltrle
     c                   other
     c     rltrlb_key    setll     rltrlb
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * C2WIN Behandle bilde for vis                                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xc2win        begsr
      *
     c                   eval      rltrl2_levr = b1levr
     c                   eval      rltrl2_bdat = b1bdat
     c                   eval      rltrl2_tist = b1tist
     c     rltrl2_key    chain     rltrl2                             60
      *
     c                   move      rosamb        c2samb
     c                   move      robiln        c2biln
     c                   move      robilk        c2bilk
     c                   move      rotext        c2text
     c     *dmy          move      robdat        c2bdat
     c     *dmy          move      rofdat        c2fdat
     c                   move      robetb        c2betb
     c                   move      rorper        c2rper
     c                   move      roraar        c2raar
     c                   move      rorekl        c2rekl
     c                   move      rojour        c2jour
     c                   move      rorefn        c2refn
     c                   move      robelØ        c2belØ
     c                   move      rorest        c2rest
     c                   move      rovalk        c2valk
     c                   move      roavde        c2avde
     c                   move      rovalb        c2valb
     c                   move      rovalr        c2valr
     c                   move      roatts        c2atts
     c                   if        roadat = *loval
     c                   eval      c2adat = 0
     c                   else
T5.40c     *dmy          move      roadat        c2adat
     c                   endif
T5.40c                   move      roattu        c2attu
     c                   move      robetk        c2betk
     c                   move      rorrf1        c2rrf1
     c                   move      rorrf2        c2rrf2
     c                   move      rodeck        c2deck
     c                   move      rokidi        c2kidi
T5.20c                   move      rolfak        c2lfak
      *
     c     c2taga        tag
      *
     c                   exfmt     c2win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c2taga
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   endsr
      *
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
E5.60 * Key for posisjonering på bilagsnummer pr.år
      *
     c     rltrlc_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rltrlc_raar
     c                   kfld                    rltrlc_biln
      *
T5.60 * Key for posisjonering på bilagsnummer
      *
T5.60c     rltrl3_key    klist
T5.60c                   kfld                    w_firm
T5.60c                   kfld                    rltrl3_biln
      *
      * Key for posisjonering på leverandørens fakturanummer
      *
     c     rltrle_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rltrle_raar
     c                   kfld                    rltrle_lfak
      *
      * Key for posisjonering på beløp
      *
     c     rltrl4_key    klist
     c                   kfld                    w_firm
S5.60c*                  kfld                    rltrl4_raar
     c                   kfld                    rltrl4_belØ
      *
      * Key for posisjonering på bilagsdato
      *
     c     rltrlb_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rltrlb_bdat
      *
      * Key for leverandørregister via leverandørnummer
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for lesing av postering
      *
     c     rltrl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rltrl2_levr
     c                   kfld                    rltrl2_bdat
     c                   kfld                    rltrl2_tist
      *
      * Henter parametre, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
6.21 c                   eval      b2firm = w_firm
     c                   eval      w_raar = *year
     c                   eval      b2raar = *year
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      w_seqe = 'N'
     c                   eval      rltrlc_raar = *year
     c                   eval      rltrlc_biln = 0
     c     rltrlc_key    setll     rltrlc
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
