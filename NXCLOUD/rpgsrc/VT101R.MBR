     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * NHO      030511   Nytt program
      * System. . . . . : ASOFAK                                              *
      * Program . . . . : VT101R                                              *
      * Beskrivelse . . : Slett av tilbudspriser                              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ----------------------------------------------------*
      *                                                                       *
6.22  * 6.20  090112 bof  Justert på sletting
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KA = F1  = Valg av firma                                  *
      *       KC = F3  = Exit                                           *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     fausrl1    if   e           k disk
     fvtill5    uf   e           k disk    rename(vtilpfr:vtill5r)
6.21 fvtill3    uf   e           k disk    rename(vtilpfr:vtill3r)
     fvt101d    cf   e             workstn
      *
      *
     d w_fdat          s              6  0
     d w_tdat          s              6  0
     d w_ftim          s              6  0
     d w_ttim          s              6  0
      *
      *
     d w_fdat_w        s                   like(vtfdat)
     d w_ftim_w        s                   like(vtftim)
     d w_tdat_w        s                   like(vttdat)
     d w_ttim_w        s                   like(vtttim)
      *
     d vtill5_kamp     s                   like(vtkamp)
6.21 d vtill3_fdat     s                   like(vtfdat)
      *
      * Definering av Local data area
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d s               s              1  0
     d w_firm          s              3  0
     d w_in03          s              1
      *
     c     dummy         tag
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av felter                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      *
      * A2BLD  - A2 Bilde for valg av parametre
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * A2BLD Behandle bilde for valg av utskriftskriteria              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     a2taga        tag
      *
      * Bare data
      *
     c                   exfmt     a2bld
      *
     c                   eval      *in37 = *off
     c                   eval      *in38 = *off
     c                   eval      *in39 = *off
     c                   eval      *in40 = *off
     c                   eval      *in41 = *off
     c                   eval      *in42 = *off
     c                   eval      *in43 = *off
      *
      * Gå ut
      *
     c                   if        *inkc = *on                                  .....
     c                   move      '1'           w_in03                             .
     c                   goto      xslutt                                           .
     c                   endif                                                  .....
      *
      *
     c                   eval      w_fdat = a2fdat
     c                   eval      w_ftim = a2ftim * 100
     c                   eval      w_tdat = a2tdat
     c                   eval      w_ttim = a2ttim * 100
      *
      * Test på kampanje
6.21 c                   if        a2kamp = *blank and
6.21 c                                      a2fdat = 0 and
6.21 c                                      a2tdat = 0
     c                   eval      *in43  = *on
     c                   goto      end_sjekk
     c                   endif
6.21  *
      * Dato/tidspunkt
      *
6.21 c                   if         a2fdat <> 0 or
6.21 c                              a2tdat <> 0 or
6.21 c                              a2ftim <> 0 or
6.21 c                              a2ttim <> 0
      *
     c     *dmy          test(d)                 w_fdat                 37
     c                   if        *in37 = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c     *hms          test(t)                 w_ftim                 38
     c                   if        *in38 = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c     *dmy          test(d)                 w_tdat                 39
     c                   if        *in39 = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c     *hms          test(t)                 w_ttim                 40
     c                   if        *in40 = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c     *dmy          move      w_fdat        w_fdat_w
     c     *dmy          move      w_tdat        w_tdat_w
      *
     c     *hms          move      w_ftim        w_ftim_w
     c     *hms          move      w_ttim        w_ttim_w
      *
     c                   if        w_fdat_w > w_tdat_w
     c                   eval      *in41  = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c                   if        w_fdat_w = w_tdat_w
     c                   if        w_ftim_w > w_ttim_w
     c                   eval      *in42  = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
6.21 c                   endif
      *
      * Vis angrebildet før sletting
     c                   exfmt     a2msg
      *
      * Gå ut
      *
     c                   if        *inkc = *on                                  .....
     c                   move      '1'           w_in03                             .
     c                   goto      a2taga                                           .
     c                   endif                                                  .....
      *
      *
      * Call sletterutine
      *
E5.50c                   exsr      sletilb
     c                   goto      xslutt
      *
     c     end_sjekk     tag
     c                   goto      a2taga
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
     c                   return
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for sletting av poster                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     sletilb       begsr
      *
      *
6.21 c                   if        a2kamp <> *blank
6.21  * her slettes det med kampanjenavn
     c                   eval      vtill5_kamp = a2kamp
     c     vtill5_key    setll     vtill5
6.21 c     vtill5_key    reade     vtill5
      *
6.21 c                   dow       not %eof(vtill5)
      * evt. med tidsintervall
6.21 c                   if         a2fdat <> 0 or
6.21 c                              a2tdat <> 0 or
6.21 c                              a2ftim <> 0 or
6.21 c                              a2ttim <> 0
      *
      *
      * Sjekk dato fra til
     c                   if        vtfdat >= w_fdat_w and
     c                             vttdat <= w_tdat_w
     c                   else
     c                   goto      nyles
     c                   endif
     *
6.21  * Sjekk klokke - hvis utfylt
6.21 c                   if        a2ftim <> 0 or
6.21 c                             a2ttim <> 0
     c                   if        vtftim >= w_ftim_w and
     c                             vtttim <= w_ttim_w
     c                   else
     c                   goto      nyles
     c                   endif
6.21 c                   endif
     *
6.21 c                   endif
     *
      * Slett post
6.21 c                   if        vtkamp  = a2kamp
     c                   delete    vtill5r
6.21 c                   endif
      *
      *
     c     nyles         tag
      *
6.21 c     vtill5_key    reade     vtill5
     c                   enddo
      *
6.21 c                   else
6.21  *
6.21  * her slettes det bare med tidspunkt
6.21 c                   eval      vtill3_fdat = w_fdat_w
6.21 c     vtill3_key    setll     vtill3
6.21 c                   read      vtill3                                 90
6.21  *
6.21 c                   dow       not %eof(vtill3)
6.21 c                   if        vtfirm <> w_firm or
6.21 c                             vtfdat >  w_fdat_w
6.21 c                   leavesr
6.21 c                   endif
6.21  *
6.21  * Sjekk dato fra til
6.21 c                   if        vtfdat >= w_fdat_w and
6.21 c                             vttdat <= w_tdat_w
6.21  *
6.21 c                   if        a2ftim <> 0 or
6.21 c                             a2ttim <> 0
6.21 c                   if        vtftim >= w_ftim_w and
6.21 c                             vtttim <= w_ttim_w
6.21  * Slett post med tidspunkt
6.21 c                   delete    vtill3r
6.21 c                   endif
6.21 c                   else
6.21  * Slett post uten tidspunkt
6.21 c                   delete    vtill3r
6.21 c                   endif
6.21  *
6.21 c                   endif
6.21  *
6.21 c     vtill3_key    reade     vtill3                                 90
6.21 c                   enddo
6.21  *
6.21 c                   endif
     *
     c                   endsr
      /eject
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      *
      * Key for les av tilbuds-register
      *
     c     vtill5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vtill5_kamp
6.21  *
6.21  * Key for les av tilbuds-register
6.21  *
6.21 c     vtill3_key    klist
6.21 c                   kfld                    w_firm
6.21 c                   kfld                    vtill3_fdat
      *
      * Legge inn firmanummer
      *
     c                   move      l_firm        w_firm
E5.50c                   eval      w_in03 = ' '
      *
     c                   endsr
