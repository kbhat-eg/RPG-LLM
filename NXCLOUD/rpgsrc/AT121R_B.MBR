     h option(*nodebugio : *srcstmt) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASPGPLU
      * Program . . . . : AT121R
      * Beskrivelse . . : Kundetilpass konfig
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       090819 BKV  Nytt program
7.01  *K0000  160920 bsa  Må tillate både store og små bokstaver i nøkkel.
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
       //dcl-f AKTIST keyed RENAME(AKTIST:AKTISTR);
      *
     fat121d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering tabell DS
      *
       //dcl-ds Data extname('AKTIST') qualified ;
       //end-ds ;
      *
      * Definering av variabler i nøkler
      *
      *
      * Definering av variable
      *
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d b_forn          s              1    inz(*off)
     d b_oppr          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
      *
     d* w_firm          s                   like(data.aktfir)
     d w_aktuid        s             18  0
     d w_aktfgp        s              3
     d w_aktfir        s              3  0
     d w_aktlag        s              2  0
     d w_aktnok        s             50
     d w_aktvd1        s              1
     d w_aktvd2        s           2048
     d w_aktkjd        s             50
     d w_aktsys        s             50
     d w_aktmdl        s             50
     d w_aktety        s             50
     d w_aktprs        s             50
     d w_aktbsk        s           2000
     d w_akteus        s             10
     d w_aktoda        s               d   datfmt(*iso)
     d w_akteda        s               d   datfmt(*iso)
      *
     d sqlquery        s           4096
     d w_frisok        s             30
     d w_teller        s              3  0
     d b_open_sok      s              1    inz(*off)
      *
     d w_firm          s                    like(w_aktfir)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(18)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      *
     C/Exec SQL
     C+  Set Option DatFmt=*ISO, commit=*none, DlyPrp=*YES
     C+  , SRTSEQ=*LANGIDSHR, LANGID=NOR
     C/End-Exec
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2taga
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   eval      *in30 = *on
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c*                   when      *in11
     c*                   exsr      bck_subfile
     c*                   goto      b2tagb
     c*                   when      *in21
     c*                   eval      *in22 = *on
     c*                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2tfgr <> *blank or
     c                             b2tfir <> 0 or
     c                             b2tlag <> 0 or
     c                             b2tnok <> *blank or
     c                             b2fris <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      b_open_sok = *on
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
     c                   eval      b_open_sok = *on
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2tfgr = *blank
     c                   eval      b2tfir = 0
     c                   eval      b2tlag = 0
     c                   eval      b2tnok = *blank
     c                   eval      b2fris = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      *in30 = *on
     c                   exsr      xc2bld
     c                   eval      *in30 = *off
     c                   eval      b1valg = 0
     c                   eval      b_forn = *on
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopi
      *
     c                   if        b1valg = 3
     c                   eval      *in30 = *on
     c                   exsr      xc1bld
     c                   eval      *in30 = *off
     c                   eval      b_forn = *on
     c                   eval      b1valg = 0
     c*                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1tuid = b1tuid
     c                   exsr      xd1win
     c                   eval      b_forn = *on
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c*                   eval      c1gave = b1gave
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
     c     c1taga        tag
      *
     c                   if        b1valg = 3
     c                   eval      c1tfgp = b1tfgp
     c                   eval      c1tfir = b1tfir
     c                   eval      c1tlag = b1tlag
     c                   eval      c1tnok = b1tnok
     c                   else
     c                   eval      c1tfgp = *blank
     c                   eval      c1tfir = 0
     c                   eval      c1tlag = 0
     c                   eval      c1tnok = *blank
     c                   endif
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c/exec sql
     c+ select count(*)
     c+ into  :w_teller
     c+ from aktist
     c+ where aktfgp = :c1tfgp and
     c+  aktfir = :c1tfir and
     c+  aktlag = :c1tlag and
     c+  aktnok = :c1tnok
     c/end-exec
     c                   if        w_teller > 0
     c                   exsr      xc1msg
     c*                   if        b_anul = *on
     c                   goto      c1taga
     c*                   endif
     c                   endif
     c                   eval      b_forn = *on
      *
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      c1tfgp = *blank
     c                   eval      c1tfir = 0
     c                   eval      c1tlag = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c/exec sql
     c+ select filgruppe, firma, lager, nokkel
     c+ into  :w_aktfgp,:w_aktfir,:w_aktlag,:w_aktnok
     c+ from aktist
     c+ where aktuid = :d1tuid
     c/end-exec
     c                   eval      d1tfgp = w_aktfgp
     c                   eval      d1tfir = w_aktfir
     c                   eval      d1tlag = w_aktlag
     c                   eval      d1tnok = w_aktnok
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter nokklel
      *
     c/exec sql
     c+ delete
     c+ from aktist
     c+ where aktuid = :d1tuid
     c/end-exec
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   if        b_oppr = *on  and b1valg <> 3
     c                   eval      c2tfgp = c1tfgp
     c                   eval      c2tfir = c1tfir
     c                   eval      c2tlag = c1tlag
     c                   eval      c2tnok = c1tnok
     c                   eval      c2tvd1 = *blank
     c                   eval      c2tvd2 = *blank
     c                   eval      c2tkjd = *blank
     c                   eval      c2tsys = *blank
     c                   eval      c2tmdl = *blank
     c                   eval      c2tety = *blank
     c                   eval      c2tprs = *blank
     c                   eval      c2tbsk = *blank
     c                   eval      c2eusr = l_user
     c                   else
     c/exec sql
     c+ select filgruppe, firma, lager, nokkel, verdi1, verdi2
     c+ , kjede, system, modul, endringtype, pris, beskrivelse
     c+ , endretav, opprettetdato, endretdato
     c+ into  :w_aktfgp,:w_aktfir,:w_aktlag,:w_aktnok,:w_aktvd1,:w_aktvd2
     c+ ,:w_aktkjd,:w_aktsys,:w_aktmdl,:w_aktety,:w_aktprs,:w_aktbsk
     c+ ,:w_akteus, :w_aktoda, :w_akteda
     c+ from aktist
     c+ where aktuid = :b1tuid
     c/end-exec
      *
     c                   if        b1valg = 3
     c                   eval      c2tfgp = c1tfgp
     c                   eval      c2tfir = c1tfir
     c                   eval      c2tlag = c1tlag
     c                   eval      c2tnok = c1tnok
     c                   else
     c                   eval      c2tfgp = w_aktfgp
     c                   eval      c2tfir = w_aktfir
     c                   eval      c2tlag = w_aktlag
     c                   eval      c2tnok = w_aktnok
     c                   endif
     c                   eval      c2tvd1 = w_aktvd1
     c                   eval      c2tvd2 = w_aktvd2
     c                   eval      c2tkjd = w_aktkjd
     c                   eval      c2tsys = w_aktsys
     c                   eval      c2tmdl = w_aktmdl
     c                   eval      c2tety = w_aktety
     c                   eval      c2tprs = w_aktprs
     c                   eval      c2tbsk = w_aktbsk
     c                   eval      c2eusr = w_akteus
     c     *dmy          move      w_aktoda      c2odat
     c     *dmy          move      w_akteda      c2edat
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   eval      b_forn = *off
     c                   goto      end_c2bld
     c                   endsl
      *
      * Oppdatering er valgt
      *
     c                   if        *in30 = *on
     c                   time                    w_time
     c                   if        b1valg = 2
     c/exec sql
     c+ update aktist set filgruppe=:c2tfgp, firma = :c2tfir, lager = :c2tlag
     c+ ,nokkel = :c2tnok , verdi1 = :c2tvd1, verdi2 = :c2tvd2
     c+ ,kjede = :c2tkjd, system = :c2tsys, modul = :c2tmdl
     c+ ,endringtype = :c2tety, pris = :c2tprs, beskrivelse = :c2tbsk
     c+ , endretdato = :w_date, endretkl = :w_time, endretav = :l_user
     c+ where aktuid = :b1tuid
     c/end-exec
     c                   elseif    b1valg = 3 or b_oppr = *on
     c/exec sql
     C+ INSERT INTO AKTIST
     c+ (filgruppe,firma,lager,nokkel,verdi1,verdi2,kjede
     c+ ,system,modul,endringtype,pris,beskrivelse,opprettetav
     c+ ,opprettetkl,opprettetdato,endretdato,endretkl,endretav)
     c+ values (
     c+ :c2tfgp,:c2tfir,:c2tlag,:c2tnok,:c2tvd1,
     c+ :c2tvd2,:c2tkjd,:c2tsys,:c2tmdl,:c2tety,
     c+ :c2tprs,:c2tbsk,:l_user,:w_time,:w_date,:w_date,:w_time,:l_user
     c+ )
     c/end-exec
     c                   endif
     c                   if        sqlcod < 0
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
      *
     c     end_c2bld     tag
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
     c
     c
      *
      * Initierer SQL for søk
      *
     c                   if        b_open_sok = *on
     c/exec sql
     c+ close STMT
     c/end-exec
     c                   eval      sqlquery  = 'select +
     c                                  uid         +
     c                                  ,filgruppe  +
     c                                  ,firma      +
     c                                  ,lager      +
     c                                  ,nokkel     +
     c                                  ,verdi1     +
     c                                  ,beskrivelse +
     c                                   from aktist +
     c                               '
     c                   if        b2tfgr <> *blank or
     c                             b2tfir <> 0 or
     c                             b2tlag <> 0 or
     c                             b2tnok <> *blank or
     c                             b2fris <> *blank
     c                   eval      w_frisok = '%' + %trim(b2fris) + '%'
     c                   eval      sqlquery  = %trim(sqlquery) +
     c                                 '  where      +
     c                                   ((filgruppe = ?) or ('''' = ?)) and +
     c                                   ((firma = ?) or (0 = ?)) and +
     c                                   ((lager = ?) or (0 = ?)) and +
     c                                   ((nokkel = ?) or ('''' = ?)) and +
     c                                   (((nokkel like ?) +
     c                                       or (UPPER(beskrivelse) like ?)) +
     c                                       or ('''' = ?)) +
     c                                   '
     c                   endif
     c
     c                   eval      sqlquery  = %trim(sqlquery) +
     c                                 '  order by      +
     c                                   filgruppe     +
     c                                   ,firma        +
     c                                   ,lager        +
     c                                   ,nokkel       +
     c                                   for read only +
     c                                   '
     c
     c/exec sql
     c+ PREPARE SQL_STMT FROM :sqlquery
     c/end-exec
     c
     c/exec sql
     c+ DECLARE STMT CURSOR FOR SQL_STMT
     c/end-exec
     c
     c                   if        b2tfgr <> *blank or
     c                             b2tfir <> 0 or
     c                             b2tlag <> 0 or
     c                             b2tnok <> *blank or
     c                             b2fris <> *blank
     c/exec sql
     c+ OPEN STMT using  :b2tfgr, :b2tfgr, :b2tfir, :b2tfir
     c+                 ,:b2tlag, :b2tlag,:b2tnok, :b2tnok
     c+                 ,:w_frisok, :w_frisok, :b2fris
     c/end-exec
     c                   else
     c/exec sql
     c+ OPEN STMT
     c/end-exec
     c                   endif
     c
     c                   endif
     c
     c                   if        sqlcod = 100 or sqlcod < 0
     c                   eval      *in15 = *on
     c                   leavesr
     c                   endif
      *
     c                   dou       w_srrn = w_spge
      *
     c/exec sql
     c+ fetch STMT
     c+ into  :w_aktuid,:w_aktfgp,:w_aktfir,:w_aktlag,:w_aktnok,:w_aktvd1
     c+       ,:w_aktbsk
     c/end-exec
     c                   if        sqlcod = 100 or sqlcod < 0
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1tfgp = w_aktfgp
     c                   eval      b1tfir = w_aktfir
     c                   eval      b1tlag = w_aktlag
     c                   eval      b1tnok = w_aktnok
     c                   eval      b1tvd1 = w_aktvd1
     c                   eval      b1tbsk = w_aktbsk
     c                   eval      b1tuid = w_aktuid
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   eval      b_open_sok = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Kunde
      *
     c*                   if        w_afld = 'B2KUND'
     c*                   call      'RK500R'
     c*                   parm                    w_firm
     c*                   parm                    b2kund
     c*                   parm                    w_knav
     c*                   leavesr
     c*                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
      * Initierer skjermbildet
      *
     c                   eval      *in22 = *on
     c                   time                    w_date
     c                   time                    w_time
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      b_open_sok = *on
     c                   exsr      crt_subfile
      *
     c                   endsr
