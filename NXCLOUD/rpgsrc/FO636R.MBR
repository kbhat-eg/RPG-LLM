     H/TITLE  ASOFAK: Utskrift av kontroll-liste
     H DECEDIT('0,') DATEDIT(*DMY.) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : FO636R
      * Beskrivelse . . : Utskrift av kontroll-liste
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
3.13  * R3.13 110900 AMD  Dersom vare er hentet fra LVR settes det inn
3.13  *                   2 desimaler som standard (finnes jo ikke i EVR)
3.14  * R3.14 190900 AMD  Betalingsbetingelse er tatt med på utskriften
3.14  *                   I tillegg kan det styres fra rutineregister om
3.14  *                   prisene skal skrives inkl/eks. mva.
3.31  * R3.31 030401 AMD  Programmet kunne gå i loop på under helt spesielle
      *                   forhold (se beskrivelse i AAINF330).
3.32  * R3.32 060601 AMD  Differensiert moms, 24 og 12 %
3.33  * R3.33 170901 AMD  Tatt med utskrift av ordre-info-type
      * 6.10  240609 BSA  Oppdatert med sporingsinformasjon
6.10  * R6.10 070909 BSA  Utskriften endret pga utvidelse av referansefelter.
pdf   * R6.10 291110 BSA  Innført PDF utskrift. Innebærer primært å finne     *
pdf   *                   generert spool, og kall av AP620R med dette som     *
pdf   *                   parametre.                                          *
6.11  * R6.10 211210 BSA  Legger ut leveringstypen på linje
6.20  * 6.20  140211 nho  Oppslag mot nytt prosjekt
6.21  * 6.20  301112 bsa  Tillegg for å berike metataggger
6.nu  * R6.30 160614 bof  Endringer ihht. nummerutvidelse
6.30  * R6.30 120416 nho  WWPARM ikke endret lengde
6.31  * R6.30 180816 bsa  Rokkert på rekkefølge av close printerfile
8.01  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     FFPSOL1    IPE  E           K DISK    RENAME(FPSOPFR:FPSOL1R)
     FVOTYL1    IF   E           K DISK    RENAME(VOTYPFR:VOTYL1R)
     FVLTYL1    IF   E           K DISK    RENAME(VLTYPFR:VLTYL1R)
     FVPKOL1    IF   E           K DISK    RENAME(VPKOPFR:VPKOL1R)
     FFSTSL1    IF   E           K DISK    RENAME(FSTSPFR:FSTSL1R)
     FFOHELU    UF   E           K DISK    RENAME(FOHEPFR:FOHELUR)
     FFOHEL1    IF   E           K DISK    RENAME(FOHEPFR:FOHEL1R)
     FFODTL1    IF   E           K DISK    RENAME(FODTPFR:FODTL1R)
     FFOTXL1    IF   E           K DISK    RENAME(FOTXPFR:FOTXL1R)
     FVVARL1    IF   E           K DISK    RENAME(VVARPFR:VVARL1R)
     Frkunl1    IF   E           K DISK    RENAME(RKUNPFR:rkunl1r)
     FAFIRL1    IF   E           K DISK    RENAME(AFIRPFR:AFIRL1R)
     FAFORL1    IF   E           K DISK    RENAME(AFORPFR:AFORL1R)
      *
     FFO636P    O    E             PRINTER
pdf  f                                     usropn
      *
      *
     D                 DS
      *                                                      Parameter-inn
6.nu D  LDPREC                 1     84
     D  LDOTYP                 1      2
     D  LDSEKV                 3      4  0
6.nu D  LDFONR                 5     12  0
6.nu D  LDTONR                13     20  0
6.nu D  LDFKUN                21     26  0
6.nu D  LDTKUN                27     32  0
6.nu D  LDFDAT                33     42d   datfmt(*iso)
6.nu D  LDTDAT                43     52d   datfmt(*iso)
6.nu D  LDVAL1                53     53  0
6.nu D  LDVAL2                54     54  0
6.nu D  LDVAL3                55     55  0
6.nu D  LDVAL4                56     56  0
6.nu D  LDVAL5                57     57  0
6.nu D  LDVAL6                58     58  0
6.nu D  LDVAL7                59     59  0
6.nu D  LDPROS                60     61  0
      *
      * - - - - - - - - - - - - - -
      * Henter inn data fra LDA
      * - - - - - - - - - - - - - -
      *
     D                UDS
pdf  d l_poff                584    584
pdf  d l_bach                595    635
pdf  d l_skje                637    637  0
     D  DSRUTI               800    809
     D  DSALIN               856    858  0
      *
     D  DSWSID               901    910
     D  DSUSER               911    920
     D  DSFIRM               944    946  0
      *
      * Definering av variable
      *
3.14 d w_ffda          s               d   datfmt(*iso)
3.14 d w_csts          s              1
3.14 d w_ctx2          s             20
     d w_retk          s              1
     d w_gtxt          s             20
     d w_itxt          s             20
     d w_jtxt          s             20
     d w_pdat          s               d   datfmt(*iso)
6.20 d w_uprs          s              1
6.20 d w_upro          s                   like(a1proj)
      *
pdf  d p_btyp          s            100
      *
pdf  d splfname2       s             10a
pdf  d jobname2        s             10a
pdf  d username2       s             10a
pdf  d jobnbr2         s              6a
pdf  d splfnbr2        s             10i 0
pdf  d splfname3       s             10a
pdf  d jobname3        s             10a
pdf  d username3       s             10a
pdf  d jobnbr3         s              6a
pdf  d splfnbr3        s             10i 0
6.21 d p_tagg0         s             20
6.21 d p_tagg0val      s             50
6.21 d p_tagg1         s             20
6.21 d p_tagg1val      s             50
6.21 d p_tagg2         s             20
6.21 d p_tagg2val      s             50
6.21 d p_tagg3         s             20
6.21 d p_tagg3val      s             50
6.21 d p_tagg4         s             20
6.21 d p_tagg4val      s             50
6.21 d p_tagg5         s             20
6.21 d p_tagg5val      s             50
6.21 d p_tagg6         s             20
6.21 d p_tagg6val      s             50
6.21 d p_tagg7         s             20
6.21 d p_tagg7val      s             50
6.21 d p_tagg8         s             20
6.21 d p_tagg8val      s             50
6.21 d p_tagg9         s             20
6.21 d p_tagg9val      s             50
6.21 d p_refn          s             50
6.21 d p_dspl          s            100
pdf   *
pdf  d qsprilsp        pr                  extpgm('QSPRILSP')
pdf  d rcvvar                     32767a   options(*varsize)
pdf  d rcvvarlen                     10i 0 const
pdf  d format                         8a   const
pdf  d errorcode                  32767a   options(*varsize)
pdf   *
pdf  d sprl0100        ds
pdf  d  bytesrtn                     10i 0
pdf  d  bytesavl                     10i 0
pdf  d  splfname                     10a
pdf  d  jobname                      10a
pdf  d  username                     10a
pdf  d  jobnbr                        6a
pdf  d  splfnbr                      10i 0
pdf  d  systemname                    8a
pdf  d  createdate                    7a
pdf  d                                1a
pdf  d  createtime                    6a
pdf   *
pdf  d errorcode       ds
pdf  d  bytesprov                    10i 0 inz(0)
pdf  d  byteavail                    10i 0 inz(0)
pdf   *
     IFPSOL1R       01
     I                                          FKSSRT        L4
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av formater                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Benyttede formater
      * - - - - - - - - - -
      *
      * A1HDR  - Heading A1 på rapport
      * D1DET  - Detail  D1 på rapport, Varelinje
      * E1DET  - Detail  E1 på rapport, Tekstlinje
      * O1DET  - Detail  O1 på rapport, Overføring til/fra neste side
      * T1TOT  - Total   T1 på rapport
      *
      *
      * Forklaring på variabler
      * - - - - - - - - - - - -
      *
      * D1ANT0 - Antall med 0 desimaler
      * D1ANT1 - Antall med 1 desimaler
      * D1ANT2 - Antall med 2 desimaler
      * D1ANT3 - Antall med 3 desimaler
      * D1NPRI - Netto  pris
      * D1BPRI - Brutto pris
      * D1NBEL - Netto  beløp
      * D1KBEL - Kost   beløp
      * D1RABP - Rabatt i prosent (Sum prosent 1 og 2)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Styre-rutine                                                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Initierings-rutine
      *
     C   01FCYCLE        CASEQ     *ZERO         XSTRSR
     C                   ENDCS
      *
      * Ordrehode
      *
     C     FKSKOD        CABEQ     'H'           TAG01
      *
      * Varelinje
      *
     C     FKSKOD        CABEQ     'V'           TAG02
      *
      * Kommentar/
      * tekstlinje
      *
     C     FKSKOD        CABEQ     'K'           TAG03
      *
      * Ferdig
      *
     C                   GOTO      XSLUTT
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * A1HDR Behandle heading A1                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     TAG01         TAG
      *
      * Null-stilling av
      * variabler
      *
     C                   MOVE      *OFF          *IN37
3.32 C                   MOVE      *OFF          *IN38
     C                   Z-ADD     0             O1TRSP
     C                   MOVE      *BLANK        A1ATXT
     C                   MOVE      *BLANK        A1VTXT
     C                   MOVE      *BLANK        A1LTXT
     C                   MOVE      *BLANK        A1STXT
3.32  *
3.32 c                   eval      t1grln = 0
3.32 c                   eval      t2grln = 0
      *
      * Skal kontroll-kode
      * slettes ?
      *
     C     LDVAL6        IFEQ      1                                                     .....
     C                   MOVE      FKSONR        WWNUMM                                      .
     C                   MOVE      FKSSUF        WWSUFF                                      .
     C     KEY01         CHAIN     FOHELUR                            61                     .
     C     *IN61         IFEQ      *OFF                                                      .
     C                   MOVE      *BLANK        FOBIND                                      .
6.10 C                   time                    foedat
6.10 C                   time                    foetim
6.10 C                   eval      foeusr = dsuser
     C                   UPDATE    FOHELUR                                                   .
     C                   ENDIF                                                               .
     C                   ENDIF                                                           .....
      *
      * Hent ordre-heading
      *
     C                   MOVE      FKSONR        WWNUMM
     C                   MOVE      FKSSUF        WWSUFF
     C     KEY01         CHAIN     FOHEL1R                            61
      *
      * Snu dato
      *
     c                   if        fobdat <> *loval
     c     *dmy          move      fobdat        a1bdat
     c                   endif
      *
     c                   if        foldat <> *loval
     c     *dmy          move      foldat        a1ldat
     c                   else
     c                   move      *zeros        a1ldat
     c                   endif
      *
      * Legg inn tilbudsnummer
      *
     C                   MOVE      FONUMM        A1NUMM
     C                   MOVEL     '/'           A1SUFF
     C                   MOVE      FOSUFF        A1SUFF
      *
      * Hente inn kunderegister
      *
     C                   MOVE      FOKUND        WWKUND
     C     KEY04         CHAIN     rkunl1r                            64
     C                   MOVE      FOKUND        A1KUND
     C                   MOVE      RKKATG        A1KATG
     C                   MOVE      RKNAVN        A1NAVN
     C                   MOVE      RKGATE        A1GATE
     C                   MOVE      RKADR2        A1ADR2
     C                   MOVE      RKSTED        A1STED
      *
      * Finner hvilken mva-sats
      * som skal benyttes for full moms
      *
     c                   call      'FÅ705R'
     C                   PARM                    WWFIRM
     C                   PARM                    FOBDAT
     C                   PARM                    WPMVAS
     C                   PARM                    WPMVAT
     C                   PARM                    WPMVAF
3.32  *
3.32  * Finner hvilken mva-sats
3.32  * som skal benyttes for redusert moms
3.32  *
3.32 c                   call      'FÅ706R'
3.32 c                   parm                    wwfirm
3.32 c                   parm                    fobdat
3.32 c                   parm                    wpmv2s
3.32 c                   parm                    wpmv2t
3.32 c                   parm                    wpmv2f
      *
      *  Hent tekst for avdeling
      *
     C                   CALL      'RS707R'
     C                   parm                    w_retk
     C                   parm                    foavde
     C                   parm                    w_gtxt
     C                   MOVE      w_gtxt        A1ATXT
      *
      *  Hent tekst for selger
      *
     C                   CALL      'RS709R'
     C                   parm                    w_retk
     C                   parm                    foselg
     C                   parm                    w_itxt
     C                   move      w_itxt        a1stxt
      *
      *  Hent tekst for lager
      *
     C                   CALL      'RS710R'
     C                   parm                    w_retk
     C                   parm                    folage
     C                   parm                    w_jtxt
     C                   move      w_jtxt        a1ltxt
      *
      *  Sjekk/Hent tekst for valuta
      *
     C                   CALL      'RS216R'
     c                   parm                    w_retk
     c                   parm                    fovalk
     c                   parm                    a1vtxt
     c                   parm                    w_pdat
     c                   parm                    wppomr
      *
      *  Sjekk/Hent tekst for prosjekt
      *
     C                   MOVE      *ZERO         A1PROJ
     C                   MOVE      *BLANK        A1AKTI
     C                   MOVE      *BLANK        A1PRTX
     C     FOPROJ        IFNE      *ZERO
     C                   EXSR      SBPROJ
     C                   ENDIF
3.14  *
3.14  * Hente inn betalings-
3.14  * betingelser
3.14  *
3.14 c                   call      'FÅ703R'
3.14 c                   parm                    wwfirm
3.14 c                   parm                    fobetb
3.14 c                   parm                    fobdat
3.14 c                   parm                    w_csts
3.14 c                   parm                    w_ffda
3.14 c                   parm                    a1ctxt
3.14 c                   parm                    w_ctx2
      *
      * Flytte øvrige felter
      *
     C                   MOVE      FONAVN        A2NAVN
     C                   MOVE      FOADR1        A2ADR1
     C                   MOVE      FOADR2        A2ADR2
     C                   MOVE      FOSTED        A2STED
     C                   MOVE      FOMKOD        A1MKOD
     C                   MOVE      FORABP        A1RABP
     C                   MOVE      FOAVDE        A1AVDE
     C                   MOVE      FOVALK        A1VALK
     C                   MOVE      FOLAGE        A1LAGE
3.33 C                   MOVE      FOITYP        A1ITYP
     C                   MOVE      FOSELG        A1SELG
3.14 C                   MOVE      FOBETB        A1BETB
     C                   MOVE      FOVREF        A1VREF
     C                   MOVE      FODREF        A1DREF
     C                   MOVE      FOREKV        A1REKV
      *
      * Skrive heading
      *
     C     FKSALT        IFNE      5
     C                   Z-ADD     0             WLTELL
     C                   WRITE     A1HDR
     C                   ENDIF
      *
     C                   WRITE     A1HDR1                               30
      *
     C     FKSALT        IFNE      5
     C                   WRITE     A1HDR2
     C                   END
      *
      * Er det overflow ?
      *
     C     FKSALT        IFEQ      5
     C     *IN30         CASEQ     *ON           XOVRFL
     C                   ENDCS
     C                   END
      *
     C                   GOTO      XSLUTT
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * D1DET Behandle detail D1                                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     TAG02         TAG
      *
      * Nullstilling av variabler
      *
3.32 C                   move      *blank        d1info
6.11 C                   move      *blank        d1lety
     C                   MOVE      *ZERO         D1LINE
     C                   MOVE      *BLANK        D1VARE
     C                   MOVE      *BLANK        D1TEK1
     C                   MOVE      *BLANK        D1ENH1
     C                   MOVE      *BLANK        D1TINE
     C                   MOVE      *ZERO         D1ANT0
     C                   MOVE      *ZERO         D1ANT1
     C                   MOVE      *ZERO         D1ANT2
     C                   MOVE      *ZERO         D1ANT3
     C                   MOVE      *OFF          *IN44
     C                   MOVE      *OFF          *IN46
      *
     C                   MOVE      FKSONR        WWNUMM
     C                   MOVE      FKSSUF        WWSUFF
     C                   MOVE      FKSLIN        WWLINE
     C     KEY02         CHAIN     FODTL1R                            62
      *
      * Hent linje-type
      *
     C                   MOVE      FDLTYP        WWLTYP
     C     KEY08         CHAIN     VLTYL1R                            68
      *
      * Sjekker om tekstlinjer
      *
     C     VALKTX        CABEQ     1             TAG02A
      *
      * Oppslag mot vareregister
      *
     C                   MOVE      FDVARE        WWVARE
     C     KEY11         CHAIN     VVARL1R                            69
      *
      * Sjekk om tilbud/netto-pris
      *
     C     FDSUMR        IFEQ      *ZERO
     C                   MOVE      *ON           *IN46
     C                   MOVE      *BLANK        D1TINE
     C                   MOVE      FDkpri        WWPKOD
     C     KEY19         CHAIN     VPKOL1R                            69
     C     *IN69         IFEQ      *OFF
     C                   MOVE      vaptxt        D1TINE
     C                   END
     C                   END
3.13  *
3.13  * Dersom vare er hentet fra LVR,
3.13  * sett inn  2 desimaler som std.
3.13  *
3.13 c                   if        fdlvre = 1
3.13 c                   eval      vvdesi = 2
3.13 c                   endif
      *
      * Test på antall desimaler
      *
     C     VVDESI        COMP      0                                      40
     C     VVDESI        COMP      1                                      41
     C     VVDESI        COMP      2                                      42
     C     VVDESI        COMP      3                                      43
     C   40              Z-ADD     FDANTA        D1ANT0
     C   41              Z-ADD     FDANTA        D1ANT1
     C   42              Z-ADD     FDANTA        D1ANT2
     C   43              Z-ADD     FDANTA        D1ANT3
      *
      * Varelinje
      *
     C                   MOVE      FDLINE        D1LINE
     C                   MOVE      FDVARE        D1VARE
     C                   MOVEL     FDTEK1        D1TEK1
     C                   MOVE      FDENH1        D1ENH1
      *
      * Brutto pris
      *
     C                   Z-ADD     FDsapr        D1BPRI
      *
      * Netto pris
      *
     C                   Z-ADD     FDANTA        W1ANTA
     C     W1ANTA        IFEQ      0
     C                   Z-ADD     1             W1ANTA
     C                   ENDIF
     C     FDSUMS        SUB       FDSUMR        W1NPRI
     C     W1NPRI        DIV       W1ANTA        D1NPRI
      *
      * Beregne snittrabatt
      *
     C     100           SUB       FDRAB1        W1RAB1
     C                   MULT      FDRAB2        W1RAB1
     C                   DIV       100           W1RAB1
     C                   ADD       FDRAB1        W1RAB1
     C                   Z-ADD     W1RAB1        D1RABP
6.11  * Leveringstype
6.11 c                   if        fdlety <> 0
6.11 c                   move      fdlety        d1lety
6.11 c                   endif
      *
      * Sum Beløp
      *
     C                   Z-ADD     FDSUMK        D1KBEL
     C     FDSUMS        SUB       FDSUMR        D1NBEL
     C     D1NBEL        SUB       D1KBEL        D1DEKN
      *
     C                   Z-ADD     0             WWDEKN
     C     D1NBEL        IFNE      0
     C     D1DEKN        DIV(H)    D1NBEL        WWDEKN
     C     WWDEKN        MULT(H)   100           D1GRAD
     C                   END
3.32  *
3.32  * Akkumuler opp for å kunne splitte på
3.32  * to moms-satser
3.32  *
3.32 c                   if        vvkmva = 2
3.32 c                   eval      d1info = 'H'
3.32 c                   eval      t2grln = t2grln + d1nbel
3.32 c                   else
3.32 c                   eval      t1grln = t1grln + d1nbel
3.32 c                   endif
      *
      * Skal mva. beregnes ?
      * Instruksjoner er foreløpig
      * sperret da mva. legges til
      * bare på bunnlinjen.
3.14  * Åpnet igjen for priser inkl/eks. mva.
      *
3.14 C     AFMVAK        IFEQ      1
     C*                  MOVE      *ON           *IN37
3.32 c     vvkmva        ifeq      2
3.32 c     d1npri        mult      wpmv2t        d1npri
3.32 c     d1bpri        mult      wpmv2t        d1bpri
3.32 c     d1nbel        mult      wpmv2t        d1nbel
3.32 c                   else
3.14 c     D1NPRI        MULT      WPMVAT        D1NPRI
3.14 c     D1BPRI        MULT      WPMVAT        D1BPRI
3.14 c     D1NBEL        MULT      WPMVAT        D1NBEL
3.32 C                   endif
3.14 C                   ENDIF
      *
      * Akkumuler til overføringssum
      *
     C                   ADD       D1NBEL        O1TRSP
      *
      * Skriv varelinjer
      *
     C     TAG02A        TAG
     C     VALKTX        IFNE      1
     C     FKSALT        IFNE      5
     C                   ADD       1             WLTELL
     C                   WRITE     D1DET                                30
     C                   END
     C                   ELSE
     C                   MOVEL     FDTEK1        D1TEK1
     C                   MOVEL     FDTEK2        D1TEK2
     C                   MOVE      *ON           *IN44
     C     FKSALT        IFNE      5
     C                   ADD       1             WLTELL
     C                   WRITE     D2DET                                30
     C                   END
     C                   END
      *
      * Er det overflow ?
      *
     C     FKSALT        IFNE      5
     C     *IN30         CASEQ     *ON           XOVRFL
     C                   ENDCS
     C                   END
      *
     C                   GOTO      XSLUTT
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Behandling av tekstlinjer                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     TAG03         TAG
      *
3.32 C                   move      *blank        d1info
     C                   MOVE      FKSONR        WWNUMM
     C                   MOVE      FKSSUF        WWSUFF
     C                   MOVE      FKSLIN        WWLINE
     C     KEY03         CHAIN     FOTXL1R                            63
      *
      * Gå ut dersom det
      * ikke er oppsamling,
      * og linje-nummer er
      * større enn 5000.
      * Da skal print fore-
      * gå tilslutt på L4.
      *
     C     VAOSAM        IFEQ      0
     C     FKSLIN        IFGE      5000
     C                   GOTO      XSLUTT
     C                   END
     C                   ENDIF
      *
     C                   MOVEL     FTTEXT        E1TEXT
     C     FKSALT        IFNE      5
     C                   ADD       1             WLTELL
     C                   WRITE     E1DET                                30
     C                   END
      *
      * Er det overflow ?
      *
     C     FKSALT        IFNE      5
     C     *IN30         CASEQ     *ON           XOVRFL
     C                   ENDCS
     C                   END
      *
     C     XSLUTT        TAG
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * T1TOT Behandle total T1                                         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Null-stille variabler
      *
     CL4                 Z-ADD     0             T1TOTB
     CL4                 Z-ADD     0             T1TMVA
     CL4                 Z-ADD     0             T1MVAS
3.32 cl4                 z-add     0             t2mvas
      *
      * Netto beløp
      *
     CL4   FOTOTS        SUB       FOTOTR        T1TNET
     CL4                 Z-ADD     FOTOTK        T1KOST
     CL4   T1TNET        SUB       T1KOST        T1DEKN
      *
     CL4                 Z-ADD     0             WWDEKN
     CL4   T1TNET        IFNE      0
     CL4   T1DEKN        DIV(H)    T1TNET        WWDEKN
     CL4   WWDEKN        MULT(H)   100           T1GRAD
     CL4                 END
      *
      * Skal mva. beregnes ?
      *
3.32 C**   FOMKOD        IFEQ      *ZERO
3.32 C**                 MOVE      *ON           *IN37
3.32 C**   T1TNET        MULT      WPMVAT        T1TOTB
3.32 C**   T1TOTB        SUB       T1TNET        T1TMVA
3.32 C**                 Z-ADD     WPMVAS        T1MVAS
3.32 C**                 ENDIF
3.32  *
3.32 cl4                 if        fomkod = 0
3.32 cl4                 eval      *in37  = *on
3.32 cl4                 eval      t1mvas = wpmvas
3.32 cl4                 eval      t1totb = t1tnet * wpmvat
3.32 cl4                 eval      t1tmva = t1totb - t1tnet
3.32 cl4                 endif
3.32  *
3.32 cl4                 if        t2grln <> 0
3.32 cl4                 if        fomkod = 0
3.32 cl4                 eval      *in38  = *on
3.32 cl4                 eval      t2mvas = wpmv2s
3.32 cl4                 eval      t1tmva = t1grln * wpmvas / 100
3.32 cl4                 eval      t2tmva = t2grln * wpmv2s / 100
3.32 cl4                 eval      t1totb = t1tnet + t1tmva + t2tmva
3.32 cl4                 endif
3.32 cl4                 endif
      *
      * Finne ut om det er
      * plass til å skrive
      * avslutning uten å
      * skifte til ny side
      * først.
      *
      * Trekker ut heading
      *
     CL4   DSALIN        SUB       24            WLTEST
      *
      * Trekker ut antall
      * linjer skrevet
      *
     CL4                 SUB       WLTELL        WLTEST
      *
      * Trekker ut plassbehov
      * for avslutning
      *
     CL4                 SUB       10            WLTEST
      *
      * Skrive ny heading ?
      *
     CL4   WLTEST        IFLT      0
     CL4   FKSALT        IFNE      5
     CL4                 WRITE     A1HDR
     CL4                 WRITE     A1HDR1
     CL4                 WRITE     A1HDR2
     CL4                 Z-ADD     0             WLTELL
     CL4                 END
     CL4                 ENDIF
      *
      * Skrive totalsum
      *
     CL4   FKSALT        IFNE      5
     CL4   LDVAL7        IFEQ      *ZERO
     CL4   LDPROS        IFEQ      *ZERO
     CL4                 WRITE     T1TOT                                30
     CL4                 ENDIF
     CL4                 ENDIF
     CL4                 ENDIF
      *
      * Skrive tekstlinjer
      * dersom det ikke er
      * oppsamling og linje
      * nummer er større enn
      * 5000.
      *
     CL4   VAOSAM        IFEQ      0                                                     .....
     CL4                 MOVE      FKSONR        WWNUMM                                      .
     CL4                 MOVE      FKSSUF        WWSUFF                                      .
     CL4                 Z-ADD     5000          WWLINE                                      .
     CL4   KEY03         SETLL     FOTXL1R                                                   .
     CL4                 READ      FOTXL1R                                63                 .
     CL4   *IN63         CABEQ     *ON           TAG99                                       .
      *                                                                 .
     CL4   WWNUMM        DOWEQ     FTNUMM                                                ... .
     CL4   WWSUFF        IFEQ      FTSUFF                                                  . .
      *                                                               . .
     CL4                 MOVEL     FTTEXT        E1TEXT                                    . .
     CL4                 ADD       1             WLTELL                                    . .
     CL4   FKSALT        IFNE      5
     CL4                 WRITE     E1DET                                30                 . .
     CL4                 ENDIF                                                             . .
      *                                                               . .
      * Er det overflow ?                                             . .
      *                                                               . .
     CL4   *IN30         IFEQ      *ON                                                     . .
     CL4   FKSALT        IFNE      5
     CL4                 WRITE     A1HDR                                                   . .
     CL4                 WRITE     A1HDR1                                                  . .
     CL4                 WRITE     A1HDR2                                                  . .
     CL4                 Z-ADD     0             WLTELL                                    . .
     CL4                 ENDIF                                                             . .
     CL4                 ENDIF                                                             . .
      *                                                               . .
3.31 CL4                 ENDIF                                                             . .
     CL4                 READ      FOTXL1R                                63               . .
     CL4   *IN63         CABEQ     *ON           TAG99                                     . .
     CL4                 ENDDO                                                           ... .
     CL4                 ENDIF                                                           .....
      *
     CL4   TAG99         TAG
pdf   * Generer xml for videre utskrift
pdf  clr                 if        l_poff = '1'
pdf  clr                 exsr      spoolnbr
6.31 clr                 close     fo636p
6.31 clr                 exsr      xml_create
pdf  clr                 exsr      pdf_preview
pdf   * Avslutt jobbiden
pdf  clr                 call      'AB710R'
pdf  c                   parm                    l_bach
pdf  clr                 endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Sjekk/Hent tekst for prosjekt                     RS770R       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     SBPROJ        BEGSR
      *
     C                   MOVE      *ZERO         WPKONT
     C                   MOVE      *ZERO         WPSPES
      *
6.20 C**                 CALL      'RS770R'
6.20 C                   call      'RS880R'
     C                   PARM                    WPRETK
     C                   PARM                    FOPROJ
6.20 C                   PARM                    W_UPRO
6.20 C**                 PARM                    FOAKTI
6.20 C**                 PARM                    WPKONT
6.20 C**                 PARM                    WPSPES
     C                   PARM                    WPPTX1
     C                   PARM                    WPPTX2
      *
     C     WPRETK        IFEQ      *BLANK
     C                   MOVE      FOPROJ        A1PROJ
     C                   MOVE      FOAKTI        A1AKTI
     C                   MOVEL     WPPTX1        A1PRTX
     C                   ENDIF
      *
     C                   ENDSR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for overflow                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XOVRFL        BEGSR
      *
     C     FKSALT        IFNE      5
     C                   MOVE      *ON           *IN45
     C                   WRITE     O1DET
     C                   END
      *
     C                   WRITE     A1HDR
      *
     C     FKSALT        IFNE      5
     C                   WRITE     A1HDR1
     C                   WRITE     A1HDR2
     C                   END
      *
     C     FKSALT        IFNE      5
     C                   MOVE      *OFF          *IN45
     C                   WRITE     O1DET
     C                   END
      *
     C                   Z-ADD     0             WLTELL
      *
     C                   ENDSR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for oppstart                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XSTRSR        BEGSR
      *
     C                   Z-ADD     1             FCYCLE            1 0
      *
      * Legger inn firma i nøkkel
      *
     C                   MOVE      FKSFIR        WWFIRM
     C                   MOVE      FKSFIR        A1FIRM
      *
      * Klargjøring dersom firma -
      * opplysninger skal printes ut
      *
     C     KEY06         CHAIN     AFIRL1R                            66
     C                   MOVE      AAFNAV        A1FNAV
      *
      * Hente Kunde-tilbuds-nummer
      *
     C     KEY09         CHAIN     FSTSL1R                            69
      *
     C     *IN69         IFEQ      *OFF
     C                   MOVE      FAAKUN        W0KUND
     C                   END
      *
      * Slår opp ordretype
      *
     C                   MOVE      FKSOTY        WWOTYP
     C     KEY07         CHAIN     VOTYL1R                            67
      *
     C                   MOVE      VAOTXT        A1TXT1
     C                   MOVE      FKSOTY        A1OTYP
      *
      * Er ordretype negativ ?
      *
     C     VAOKRE        IFEQ      '-'
     C                   MOVE      *ON           *IN36
     C                   ENDIF
      *
      * Skrive heading
      *
     C     FKSALT        IFEQ      5
     C                   Z-ADD     0             WLTELL
     C                   WRITE     A1HDR
     C                   ENDIF
      *
     C                   ENDSR
      *
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Finner spool som er generert av jobben
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     spoolnbr      begsr
pdf   /Free
pdf
pdf            QSPRILSP( SPRL0100
pdf                    : %size(SPRL0100)
pdf                    : 'SPRL0100'
pdf                    : errorcode );
pdf   /End-free
pdf  c*
pdf   *
pdf  c                   endsr
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Kaller eget program for generering av xml fil. Eget pgm         *
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     xml_create    begsr
pdf   *
pdf  c                   eval      splfname2 = *blanks
pdf  c                   eval      splfname3 = *blanks
pdf  c                   eval      jobname2 = *blanks
pdf  c                   eval      jobname3 = *blanks
pdf  c                   eval      username2 = *blanks
pdf  c                   eval      username3 = *blanks
pdf  c                   eval      jobnbr2 = *blanks
pdf  c                   eval      jobnbr3 = *blanks
pdf  c                   eval      splfnbr2 = *zeros
pdf  c                   eval      splfnbr3 = *zeros
pdf  c                   eval      p_btyp = 'KONTROLLISTE'
6.21 c                   eval      p_dspl = *blanks
6.21 c                   eval      p_refn = 'KONTROLLISTE ' + %char(a1date)
6.21 c                   eval      p_tagg0 = *blanks
6.21 c                   eval      p_tagg0val = *blanks
6.21 c                   eval      p_tagg1 = *blanks
6.21 c                   eval      p_tagg1val = *blanks
6.21 c                   eval      p_tagg2 = *blanks
6.21 c                   eval      p_tagg2val = *blanks
6.21 c                   eval      p_tagg3 = *blanks
6.21 c                   eval      p_tagg3val = *blanks
6.21 c                   eval      p_tagg4 = *blanks
6.21 c                   eval      p_tagg4val = *blanks
6.21 c                   eval      p_tagg5 = *blanks
6.21 c                   eval      p_tagg5val = *blanks
6.21 c                   eval      p_tagg6 = *blanks
6.21 c                   eval      p_tagg6val = *blanks
6.21 c                   eval      p_tagg7 = *blanks
6.21 c                   eval      p_tagg7val = *blanks
6.21 c                   eval      p_tagg8 = *blanks
6.21 c                   eval      p_tagg8val = *blanks
6.21 c                   eval      p_tagg9 = *blanks
6.21 c                   eval      p_tagg9val = *blanks
pdf   *
6.21 c**                 call      'AP620R'
6.21 c                   call      'AP627R'
pdf  c                   parm                    splfname
pdf  c                   parm                    jobname
pdf  c                   parm                    username
pdf  c                   parm                    jobnbr
pdf  c                   parm                    splfnbr
pdf  c                   parm                    splfname2
pdf  c                   parm                    jobname2
pdf  c                   parm                    username2
pdf  c                   parm                    jobnbr2
pdf  c                   parm                    splfnbr2
pdf  c                   parm                    splfname3
pdf  c                   parm                    jobname3
pdf  c                   parm                    username3
pdf  c                   parm                    jobnbr3
pdf  c                   parm                    splfnbr3
6.21 c                   parm                    p_tagg0
6.21 c                   parm                    p_tagg0val
6.21 c                   parm                    p_tagg1
6.21 c                   parm                    p_tagg1val
6.21 c                   parm                    p_tagg2
6.21 c                   parm                    p_tagg2val
6.21 c                   parm                    p_tagg3
6.21 c                   parm                    p_tagg3val
6.21 c                   parm                    p_tagg4
6.21 c                   parm                    p_tagg4val
6.21 c                   parm                    p_tagg5
6.21 c                   parm                    p_tagg5val
6.21 c                   parm                    p_tagg6
6.21 c                   parm                    p_tagg6val
6.21 c                   parm                    p_tagg7
6.21 c                   parm                    p_tagg7val
6.21 c                   parm                    p_tagg8
6.21 c                   parm                    p_tagg8val
6.21 c                   parm                    p_tagg9
6.21 c                   parm                    p_tagg9val
pdf  c                   parm                    l_bach
pdf  c                   PARM                    p_btyp
6.21 c                   PARM                    p_refn
6.21 c                   PARM                    p_dspl
pdf   *
pdf  c                   endsr
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Subrutiner for XML/jasper integrasjon - Skjermvisning           *
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     pdf_preview   begsr
pdf  c                   if        l_skje = 1
pdf   * Vi kaller program for launch av PDF i browser
pdf  c                   call      'AP621R'
pdf  c                   parm                    l_bach
pdf   *
pdf  c                   endif
pdf   *
pdf  c                   endsr
pdf   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     *INZSR        BEGSR
      *
     C     *LIKE         DEFINE    FAAKUN        W0KUND
     C     *LIKE         DEFINE    VAOTYP        WWOTYP
     C     *LIKE         DEFINE    FOKUND        WWKUND
     C     *LIKE         DEFINE    FOFIRM        WWFIRM
     C     *LIKE         DEFINE    FOWSID        WWWSID
     C     *LIKE         DEFINE    FONUMM        WWNUMM
     C     *LIKE         DEFINE    FOSUFF        WWSUFF
     C     *LIKE         DEFINE    FDLINE        WWLINE
     C     *LIKE         DEFINE    FDLTYP        WWLTYP
     C     *LIKE         DEFINE    VAPKOD        WWPKOD
     C     *LIKE         DEFINE    FDNUMM        SANUMM
     C     *LIKE         DEFINE    VVVARE        WWVARE
     C     *LIKE         DEFINE    FDANTA        W1ANTA
     C     *LIKE         DEFINE    FDsapr        W1RAB1
     C     *LIKE         DEFINE    FDsapr        W1NPRI
     C     *LIKE         DEFINE    FDsapr        D1NPRI
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     c                   eval      w_pdat = *loval
      *
     C                   MOVE      *BLANK        WPRETK            1
      *
     C                   MOVE      *ZERO         WPMVAS            6 2
     C                   MOVE      *ZERO         WPMVAT            5 4
     C                   MOVE      *ZERO         WPMVAF           10 9
3.32  *
3.32 c                   move      *zero         wpmv2s            6 2
3.32 c                   move      *zero         wpmv2t            5 4
3.32 c                   move      *zero         wpmv2f           10 9
      *
     C                   MOVE      *ZERO         WPPOMR           10 6
      *
     C                   MOVE      *ZERO         WPKONT            5 0
     C                   MOVE      *ZERO         WPSPES            4 0
     C                   MOVE      *BLANK        WPPTX1           30
     C                   MOVE      *BLANK        WPPTX2           30
      *
     C                   MOVE      *ZERO         WWDEKN           13 4
      *
     C                   MOVE      *BLANK        WWRUTI           10
     C                   MOVE      *ZERO         WLTELL            3 0
     C                   MOVE      *ZERO         WLTEST            3 0
      *
      * Key for Heading-file
      *
     C     KEY01         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWNUMM
     C                   KFLD                    WWSUFF
      *
      * Key for Detalj-file
      *
     C     KEY02         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWNUMM
     C                   KFLD                    WWSUFF
     C                   KFLD                    WWLINE
      *
      * Key for Tekst-linjer
      *
     C     KEY03         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWNUMM
     C                   KFLD                    WWSUFF
     C                   KFLD                    WWLINE
      *
      * Key for kunderegister
      *
     C     KEY04         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWKUND
      *
      * Key for firmaregister
      *
     C     KEY06         KLIST
     C                   KFLD                    WWFIRM
      *
      * Key for ordretyper
      *
     C     KEY07         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWOTYP
      *
      * Key for linjetyper
      *
     C     KEY08         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWLTYP
      *
      * Key for statusregister
      *
     C     KEY09         KLIST
     C                   KFLD                    WWFIRM
      *
      * Key for formular-register
      *
     C     KEY10         KLIST
     C                   KFLD                    WWRUTI
      *
      * Key for vare-register
      *
     C     KEY11         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWVARE
      *
      * Key for priskode-register
      *
     C     KEY19         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWPKOD
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     *ENTRY        PLIST
6.30 C**                 PARM                    WWPARM           80
6.30 C                   PARM                    WWPARM           84
      *
pdf  c                   open      fo636p
     C                   MOVE      WWPARM        LDPREC
      *
      * Legger inn arb.stasjon og bruker i faste felter
      *
     C                   MOVE      DSUSER        A1USER
     C                   MOVE      DSWSID        A1WSID
     C                   MOVE      UDATE         A1DATE
     C                   TIME                    UTIME             6 0
     C                   MOVE      UTIME         A1TIME
      *
      * Hente inn rutinenavn
      * og meldingstekst
      *
     C                   MOVE      DSRUTI        WWRUTI
     C     KEY10         CHAIN     AFORL1R                            69
     C  N69              MOVE      AFMLD1        T1MLD1
      *
     C                   ENDSR
