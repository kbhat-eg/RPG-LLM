     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VE711R
      * Beskrivelse . . : Henter ean.nr ut fra gitt varenr + enhet
      *                   bruker nobb-lev. som ligger på vvarpf
      *
      *
      *                   Input : firma, varenr., enhet
      *                   Output: ean.nr  status (1 = ok)
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       080709 bof  Nytt program
      *       300610 ask  Korrigert test på funn i vareregister
6.11  *       150910 bof  Justert test på enhet
6.12  *       041010 bof  Lagt på test på 0 i gtin fra veanpf
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarlr    if   e           k disk    rename(vvarpfr:vvarlrr)
     fvveplr    if   e           k disk    rename(vveppfr:vveplrr)
     fveanl2    if   e           k disk    rename(veanpfr:veanl2r)
      *
      * Definering av variable i nøkler
      *
     d vvarlr_firm     s                   like(vvfirm)
     d vvarlr_vare     s                   like(vvvare)
     d vveplr_pfir     s                   like(vepfir)
     d vveplr_pvar     s                   like(vepvar)
     d vveplr_penh     s                   like(vepenh)
     d vveplr_pldo     s                   like(vepldo)
     d veanl2_firm     s                   like(vxfirm)
     d veanl2_vare     s                   like(vxvare)
      *
      * Definering av pamametre
      *
     d p_firm          s                   like(vxfirm)
     d p_eann          s                   like(vxeann)
     d p_vare          s                   like(vxvare)
     d p_enhe          s                   like(vxenhe)
     d p_stat          s              1
      *
      *
     d                uds
     d l_user                911    920
     d l_sfir                944    946  0
     d l_savd                947    950  0
     d l_lagr               1001   1002  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      * Initierer output-parametre
      *
     c                   eval      p_eann = 0
     c                   eval      p_stat = *blank
      *
     c                   eval      vvarlr_firm = p_firm
     c                   eval      vvarlr_vare = p_vare
     c     vvarlr_key    chain     vvarlr
     c                   if        not %found(vvarlr)
     c                   goto      avslutt
     c                   endif
      *
      * Henter eannr.
      *
     c                   eval      vveplr_pfir = p_firm
     c                   eval      vveplr_pvar = p_vare
     c                   eval      vveplr_penh = p_enhe
     c                   eval      vveplr_pldo = vvldor
     c     vveplr_key    chain     vveplr
     c                   if        %found(vveplr) and
     c                             vepgti <> 0
     c                   eval      p_eann = vepgti
     c                   eval      p_stat = '1'
     c                   endif
      *
     c                   if        p_stat = *blank
     c                   eval      veanl2_firm = p_firm
     c                   eval      veanl2_vare = p_vare
     c     veanl2_key    chain     veanl2
     c                   if        %found(veanl2) and
6.12 c                             p_enhe = vxenhe and
6.12 c                             vxeann <> 0
     c                   eval      p_eann = vxeann
     c                   eval      p_stat = '1'
     c                   endif
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
      *
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_enhe
     c                   parm                    p_eann
     c                   parm                    p_stat
      *
      * Key for les på vareenhets-register
      *
     c     vveplr_key    klist
     c                   kfld                    vveplr_pfir
     c                   kfld                    vveplr_pvar
     c                   kfld                    vveplr_penh
     c                   kfld                    vveplr_pldo
      *
      * Key for les på ean-register
      *
     c     veanl2_key    klist
     c                   kfld                    veanl2_firm
     c                   kfld                    veanl2_vare
      *
      * Key for les på vareregister
      *
     c     vvarlr_key    klist
     c                   kfld                    vvarlr_firm
     c                   kfld                    vvarlr_vare
      *
      *
     c                   endsr
