     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FL100R
      * Beskrivelse . . : Kundeprosjekt, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       120499 HKO  Nytt program
      *       270300 HKO  Utvidet E-mail til 50 posisjoner
      *       080801 HKO  Utvidet selgernummer i subfile
4.01  * R4.01 200302 AMD  Endring fra spes til kostnadsbærer
      *       151203 HKO  Utvidet med valg av privatkjøp
5.31  * R5.31 221004 AMD  Lagt inn kundens eksterne prosjektnummer
5.40  * R5.40 140305 AMD  Lagt mulighet for filter v/F1 (Ønske Per Strand)
      *                   + mulighet for posisjonering på eksternt prosj
5.40  * R5.40 110805 HKO  Lagt inn bonuskunde og opprettet dato
      *                   Endret layout i bildet
5.42  * R5.42 080905 KOB  Lagt inn redigering av telefon, faks og mobil
      *       121005 HKO  Skilt ut C2BLD i eget program
5.61  * R5.61 190908 AMD  Dersom et prosjekt ble kopiert og etterpå
      *                   endret, ble det ved endring laget enda et
      *                   prosjekt. Dette er korrigert
5.62  * R5.62 111011 AMD  Litt merkelig sak. I programmet ligger det
5.62  *                   sjekk på blank og '-' mens det i skjermbildet
5.62  *                   er 0 og 1 ? Endrer til 0 og 1 i programmet for
5.62  *                   F1 = filter, utelate utgåtte prosjekt
5.62  *                   I tillegg er det lagt inn mulighet for F4 =
5.62  *                   spørring på kunde.
6.3q  *       260515 bof  Datoformat sql
7.xx  * 7.00  230216 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  *       040615 ASK  Fjernet vedlikehold av rabattmatrise og
7.01  *                   spesialpriser.
7.02  *K000   201120 bsa  Vasker bort tegn som skaper "rot" i subfile.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffkpspf    if   e           k disk
     ffkprlr    if   e           k disk    rename(fkprpfr:fkprlrr)
     ffkprlu    uf a e           k disk    rename(fkprpfr:fkprlur)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     ffkprl2    if   e           k disk    rename(fkprpfr:fkprl2r)
     ffkprl3    if   e           k disk    rename(fkprpfr:fkprl3r)
     ffkprl5    if   e           k disk    rename(fkprpfr:fkprl5r)
5.40 ffkprl6    if   e           k disk    rename(fkprpfr:fkprl6r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
      *
     ffl100d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
7.01 d l_filg                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d fkprlr_kund     s                   like(flkund)
     d fkprlr_kpro     s                   like(flkpro)
     d fkprlu_kund     s                   like(flkund)
     d fkprlu_kpro     s                   like(flkpro)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
     d fkprl2_navn     s                   like(flnavn)
     d fkprl3_kpro     s                   like(flkpro)
     d fkprl5_selg     s                   like(flselg)
5.40 d fkprl6_epro     s                   like(flepro)
     d rkunl1_kund     s                   like(rkkund)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_open_sok      s              1    inz(*off)
      *
     d w_udat          s               d   datfmt(*iso)
     d w_ste1          s             35
     d w_ste2          s             35
     d w_ste3          s             35
     d w_ste4          s             35
     d w_ste5          s             35
     d w_posi          s              3  0
     d w_snav          s             20
     d s_stek          s             35
      *
     d w_firm          s                   like(flfirm)
     d w_knav          s                   like(rknavn)
5.42  *
     d w_navn          s                   like(flnavn)
     d w_adr1          s                   like(fladr1)
     d w_adr2          s                   like(fladr2)
7.02 d w_len           s              3  0
7.02 d w_teller        s              3  0
5.42  *
     d s_kund          s                   like(flkund)
     d s_kpro          s                   like(flkpro)
      *
7.01 d u_s701          s              1    inz(*off)                            Rabatt og spespriser
      *
7.01   dcl-s co402_verdi1  char(1);
7.01   dcl-s co402_verdi2  char(2048);
7.01   DCL-PR CO402R EXTPGM('CO402R');
7.01     p_filgrp            char(3)     const;
7.01     p_firm              packed(3:0) const;
7.01     p_lager             packed(2:0) const;
7.01     p_nokkel            char(50)    const;
7.01     p_verdi1            char(1);
7.01     p_verdi2            char(2048);
7.01   END-PR;
      *
      * Konstanter
      *
     d c_sfil          s              2  0 inz(11)
      *
     d Lo              c                   'abcdefghijklmnopqrstuvwxyzæøå'
     d Up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅ'
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for å opprette ny rad
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.3q C/Exec SQL
6.3q C+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR
6.3q C/End-Exec
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile                                  DSP SFL
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2taga
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile                                  CRT SFL
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
5.40  *
5.40  * F1=Filter
5.40  *
5.40 c                   if        *inka
5.40 c                   exsr      xh1win
5.40 c                   exsr      forny
5.40 c                   goto      b2taga
5.40 c                   endif
      *
      * F9=Gjenta søk
      *
     c                   if        *inki = *on
     c                   eval      *in22 = *on
     c                   eval      b2stek = s_stek
     c                   goto      b2tagb
     c                   endif
      *
      * F13=Spesialpriser
      *
     c                   if        *inkm = *on
7.01 c                   if        u_s701 = *on
     c                   call      'FP100R'
     c                   goto      b2tagb
7.01 c                   endif
     c                   endif
      *
      * F14=Rabattmatrise
      *
     c                   if        *inkn = *on
7.01 c                   if        u_s701 = *on
     c                   call      'FR100R'
     c                   goto      b2tagb
7.01 c                   endif
     c                   endif
      *
      * F23=Vis kun utgåtte / vis alle
      *
     c                   if        *inkx = *on
     c                   if        *in80 = *on
     c                   eval      *in80 = *off
     c                   else
     c                   eval      *in80 = *on
     c                   endif
     c                   exsr      forny
     c                   goto      b2taga
     c                   endif
      *
      * Søking
      *
     c                   if        b2stek <> *blank
     c                   exsr      sØk
     c                   goto      b2tagb
     c                   endif
      *
      * Posisjonering
      *
     c                   if        b2kund <> 0 or
     c                             b2kpro <> 0 or
     c                             b2navn <> *blank or
5.40 c                             b2epro <> *blank or
     c                             b2selg <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'S'
     c                   eval      b_open_sok = *on
     c                   when      w_seqe = 'P'
     c                   eval      fkprl3_kpro = b1kpro
     c     fkprl3_key    setll     fkprl3
     c                   when      w_seqe = 'N'
     c                   eval      fkprl2_navn = b1navn
     c     fkprl2_key    setll     fkprl2
     c                   when      w_seqe = 'E'
     c                   eval      fkprl5_selg = b1selg
     c     fkprl5_key    setll     fkprl5
5.40 c                   when      w_seqe = 'X'
5.40 c                   eval      fkprl6_epro = b1epro
5.40 c     fkprl6_key    setll     fkprl6
     c                   other
     c                   eval      fkprl1_kund = b1kund
     c                   eval      fkprl1_kpro = b1kpro
     c     fkprl1_key    setll     fkprl1
     c                   endsl
      *
     c                   exsr      clr_subfile                                  TØM SFL
     c                   exsr      crt_subfile                                  CRT SFL
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for søking i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sØk           begsr
      *
     c                   eval      w_seqe = 'S'
     c                   eval      b_open_sok = *on
      *
     c                   eval      s_stek = b2stek
      *
      * Utleder søketekster ut av søketekst
      *
     c                   call      'AS700R'
     c                   parm                    b2stek
     c                   parm                    w_ste1
     c                   parm                    w_ste2
     c                   parm                    w_ste3
     c                   parm                    w_ste4
     c                   parm                    w_ste5
      *
      * Blanker subfile, og fyller opp i.h.t. søk
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker søkefelt
      *
     c                   eval      b2stek = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjonerer på kundenummer
      *
     c                   if        b2kund <> 0
     c                   eval      w_seqe = *blank
     c                   eval      fkprl1_kund = b2kund
     c                   eval      fkprl1_kpro = b2kpro
     c     fkprl1_key    setll     fkprl1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjonerer på kundeprosjekt
      *
     c                   if        b2kpro <> 0
     c                   eval      w_seqe = 'P'
     c                   eval      fkprl3_kpro = b2kpro
     c     fkprl3_key    setll     fkprl3
     c                   goto      end_pos
     c                   endif
      *
      * Posisjonerer på navn
      *
     c                   if        b2navn <> *blank
     c                   eval      w_seqe = 'N'
     c                   eval      fkprl2_navn = b2navn
     c     fkprl2_key    setll     fkprl2
     c                   goto      end_pos
     c                   endif
      *
      * Posisjonerer på selger
      *
     c                   if        b2selg <> 0
     c                   eval      w_seqe = 'E'
     c                   eval      fkprl5_selg = b2selg
     c     fkprl5_key    setll     fkprl5
     c                   goto      end_pos
     c                   endif
5.40  *
5.40  * Posisjonerer på eksternt prosjekt
5.40  *
5.40 c                   if        b2epro <> *blank
5.40 c                   eval      w_seqe = 'X'
5.40 c                   eval      fkprl6_epro = b2epro
5.40 c     fkprl6_key    setll     fkprl6
5.40 c                   goto      end_pos
5.40 c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile                                  TØM SFL
     c                   exsr      crt_subfile                                  CRT SFL
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2kund = 0
     c                   eval      b2kpro = 0
     c                   eval      b2navn = *blank
5.40 c                   eval      b2epro = *blank
     c                   eval      b2selg = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1kund = b1kund
     c                   eval      c1kpro = b1kpro
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1kund = b1kund
     c                   eval      k1kpro = b1kpro
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1kund = b1kund
     c                   eval      d1kpro = b1kpro
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   call      'FL505R'
     c                   parm                    w_firm
     c                   parm                    b1kund
     c                   parm                    b1kpro
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1kund = b2kund
     c                   eval      c1kpro = 0
      *
     c                   eval      c1knav = *blank
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      c1taga
     c                   endsl
      *
      * Avbryter dersom kunde ikke er valgt
      *
     c                   if        c1kund = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Validering
      *
     c                   eval      c1knav = *blank
     c                   eval      rkunl1_kund = c1kund
     c     rkunl1_key    chain     rkunl1
     c                   if        not %found
     c                   eval      *in32 = *on
     c                   goto      c1taga
     c                   endif
     c                   eval      c1knav = rknavn
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2kund = 0
     c                   eval      b2kpro = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle skjermbilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   call      'FL105R'
     c                   parm                    w_firm
     c                   parm                    c1kund
     c                   parm                    c1kpro
     c                   parm                    w_navn
     c                   parm                    w_adr1
     c                   parm                    w_adr2
     c                   parm                    k1kund
     c                   parm                    k1kpro
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      fkprlr_kund = c1kund
     c                   eval      fkprlr_kpro = c1kpro
     c     fkprlr_key    chain     fkprlr
     c                   if        %found
     c                   eval      b1navn = flnavn
     c                   eval      b1selg = flselg
5.31  *                  eval      b1fdat = flfdat
5.31 c                   eval      b1epro = flepro
     c                   eval      b1tdat = 0
     c                   if        fltdat <> *loval
     c     *dmy          move      fltdat        b1tdat
     c                   endif
     c                   endif
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
5.61  *
5.61  * Nullstilling etter kopiering
5.61  *
5.61 c                   eval      k1kund = 0
5.61 c                   eval      k1kpro = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      fkprlr_kund = d1kund
     c                   eval      fkprlr_kpro = d1kpro
     c     fkprlr_key    chain     fkprlr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Kaller program for sletting av kundeprosjekt m/relasjoner
      *
     c                   call      'FL400R'
     c                   parm                    w_firm
     c                   parm                    d1kund
     c                   parm                    d1kpro
      *
     c                   endsr
5.40  *
5.40  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.40  * xh1win  Bilde for behandling av filter
5.40  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.40  *
5.40 c     xh1win        begsr
5.40  *
5.40 c     h1taga        tag
5.40  *
5.40 c                   write     h1win
5.40 c                   exfmt     h1win
5.40  *
5.40  * Behandling av funksjonstaster
5.40  *
5.40 c                   select
5.40 c                   when      *inkc or *inkl
5.40 c                   leavesr
5.62 c                   when      *inkd
5.62 c                   exsr      spØrring
5.62 c                   goto      h1taga
5.40 c                   endsl
5.40  *
5.40  * Fyller subfile på nytt
5.40  *
5.40 c                   eval      fkprl1_kund = 0
5.40 c                   eval      fkprl1_kpro = 0
5.40 c     fkprl1_key    setll     fkprl1
5.40 c                   eval      fkprl2_navn = *blank
5.40 c     fkprl2_key    setll     fkprl2
5.40 c                   eval      fkprl3_kpro = 0
5.40 c     fkprl3_key    setll     fkprl3
5.40 c                   eval      fkprl5_selg = 0
5.40 c     fkprl5_key    setll     fkprl5
5.40  *
5.40 c                   exsr      clr_subfile
5.40 c                   exsr      crt_subfile
5.40  *
5.40 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_kund = k1kund
     c                   eval      s_kpro = k1kpro
      *
      * Fyller kopierings-bildet
      *
     c                   eval      fkprlr_kund = k1kund
     c                   eval      fkprlr_kpro = k1kpro
     c     fkprlr_key    chain     fkprlr
      *
     c                   eval      k1kpro = 0
      *
     c     k1tagb        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   exsr      spØrring
     c                   goto      k1tagb
     c                   endif
      *
      * Validering
      *
     c                   if        k1kund = 0
     c                   eval      *in31 = *on
     c                   goto      k1tagb
     c                   endif
      *
     c                   eval      rkunl1_kund = k1kund
     c     rkunl1_key    chain     rkunl1
     c                   if        not %found
     c                   eval      *in32 = *on
     c                   goto      k1tagb
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1kund = s_kund
     c                   eval      c1kpro = s_kpro
     c                   exsr      xc2bld
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil                     SFL END
     c                   eval      w_srrn = w_ssrn
      *
      * Initierer SQL for søk
      *
     c                   if        w_seqe = 'S' and
     c                             b_open_sok = *on
     c/exec sql
     c+                  declare sok cursor for
     c+                  select flsfir,
     c+                         flskun,
     c+                         flskpr,
     c+                         flstek
     c+                  from   fkpspf
     c+                  where  flsfir = :w_firm and
     c+                         flstek like :w_ste1
     c+                  order by flsfir, flskun, flskpr
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok
     c/end-exec
     c/exec sql
     c+                  open sok
     c/end-exec
     c                   endif
      *
      * Fyller opp subfile
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'S'
     c/exec sql
     c+                  fetch next
     c+                  from  sok
     c+                  into  :flsfir,
     c+                        :flskun,
     c+                        :flskpr,
     c+                        :flstek
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'P'
     c                   read      fkprl3                                 15
     c                   when      w_seqe = 'N'
     c                   read      fkprl2                                 15
     c                   when      w_seqe = 'E'
     c                   read      fkprl5                                 15
5.40 c                   when      w_seqe = 'X'
5.40 c                   read      fkprl6                                 15
     c                   other
     c                   read      fkprl1                                 15
     c                   endsl
      *
     c                   if        *in15                                        EOF
     c                   leave
     c                   endif
      *
     c                   if        w_seqe = 'S'
      *
     c                   if        w_ste2 <> *blank
      *
     c                   eval      w_posi = %scan(%trim(w_ste2):%trim(flstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
      *
     c                   if        w_ste3 <> *blank
     c                   eval      w_posi = %scan(%trim(w_ste3):%trim(flstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        w_ste4 <> *blank
     c                   eval      w_posi = %scan(%trim(w_ste4):%trim(flstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        w_ste5 <> *blank
     c                   eval      w_posi = %scan(%trim(w_ste5):%trim(flstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
     c                   endif
      *
     c                   endif
      *
     c                   eval      fkprlr_kund = flskun
     c                   eval      fkprlr_kpro = flskpr
     c     fkprlr_key    chain     fkprlr
     c                   if        not %found
     c                   iter
     c                   endif
      *
     c                   endif
      *
      * Test på firma
      *
     c                   if        flfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
      * Test på om kun utgåtte kundeprosjekt skal vises
      *
     c                   if        *in80 = *on
     c                   if        fltdat = *loval or
     c                             fltdat >= w_udat
     c                   iter
     c                   endif
     c                   endif
5.40  *
5.40  * Skal utgåtte kundeprosjekt droppes ?
5.40  *
5.62 c                   if        h1utla = '1'
5.40 c                   if        fltdat <> *loval and
5.40 c                             fltdat < w_udat
5.40 c                   iter
5.40 c                   endif
5.40 c                   endif
5.40  *
5.40  * Skal bare en bestemt kunde vises ?
5.40  *
5.40 c                   if        h1kund <> 0 and
5.40 c                             flkund <> h1kund
5.40 c                   iter
5.40 c                   endif
5.40  *
5.40  * Skal bare et bestemt ekstern prosjekt vises ?
5.40  *
5.40 c                   if        h1epro <> *blank and
5.40 c                             flepro <> h1epro
5.40 c                   iter
5.40 c                   endif
      *
      * Skriver til subfile
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'P'
     c                   eval      fkprl3_kpro = flkpro
     c                   when      w_seqe = 'N'
     c                   eval      fkprl2_navn = flnavn
     c                   when      w_seqe = 'E'
     c                   eval      fkprl5_selg = flselg
5.40 c                   when      w_seqe = 'X'
5.40 c                   eval      fkprl6_epro = flepro
     c                   other
     c                   eval      fkprl1_kund = flkund
     c                   eval      fkprl1_kpro = flkpro
     c                   endsl
      *
     c                   eval      b1kund = flkund
     c                   eval      b1kpro = flkpro
     c                   eval      b1navn = flnavn
     c                   eval      b1selg = flselg
5.31  *                  eval      b1fdat = 0
5.31 c                   eval      b1epro = flepro
     c                   eval      b1tdat = 0
      *
      * Henter kundenavn
      *
     c                   eval      b1knav = *blank
     c                   eval      rkunl1_kund = b1kund
     c     rkunl1_key    chain     rkunl1
     c                   if        %found
     c                   eval      b1knav = rknavn
     c                   endif
      *
5.31  *                  if        flfdat <> *loval
5.31  *    *dmy          move      flfdat        b1fdat
5.31  *                  endif
      *
     c                   if        fltdat <> *loval
     c     *dmy          move      fltdat        b1tdat
     c                   endif
7.02  *  Vasker tegn som newlook ikke tåler
7.02 c                   exsr      newlo_vask
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   eval      b_open_sok = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        w_seqe = 'S'
     c                   exsr      forny
     c                   leavesr
     c                   endif
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'P'
     c     fkprl3_key    setgt     fkprl3
     c                   readp     fkprl3
     c                   when      w_seqe = 'N'
     c     fkprl2_key    setgt     fkprl2
     c                   readp     fkprl2
     c                   when      w_seqe = 'E'
     c     fkprl5_key    setgt     fkprl5
     c                   readp     fkprl5
5.40 c                   when      w_seqe = 'X'
5.40 c     fkprl6_key    setgt     fkprl6
5.40 c                   readp     fkprl6
     c                   other
     c     fkprl1_key    setgt     fkprl1
     c                   readp     fkprl1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'P'
     c                   readp     fkprl3
     c                   when      w_seqe = 'N'
     c                   readp     fkprl2
     c                   when      w_seqe = 'E'
     c                   readp     fkprl5
5.40 c                   when      w_seqe = 'X'
5.40 c                   readp     fkprl6
     c                   other
     c                   readp     fkprl1
     c                   endsl
      *
     c                   if        %eof or
     c                             flfirm <> w_firm
     c                   leave
     c                   endif
      *
      * Test på om kun utgåtte kundeprosjekt skal vises
      *
     c                   if        *in80 = *on
     c                   if        fltdat = *loval or
     c                             fltdat >= w_udat
     c                   iter
     c                   endif
     c                   endif
5.40  *
5.40  * Skal utgåtte kundeprosjekt droppes ?
5.40  *
5.40 c                   if        h1utla = '-'
5.40 c                   if        fltdat <> *loval and
5.40 c                             fltdat < w_udat
5.40 c                   iter
5.40 c                   endif
5.40 c                   endif
5.40  *
5.40  * Skal bare en bestemt kunde vises ?
5.40  *
5.40 c                   if        h1kund <> 0 and
5.40 c                             flkund <> h1kund
5.40 c                   iter
5.40 c                   endif
5.40  *
5.40  * Skal bare et bestemt ekstern prosjekt vises ?
5.40  *
5.40 c                   if        h1epro <> *blank and
5.40 c                             flepro <> h1epro
5.40 c                   iter
5.40 c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'P'
     c                   eval      fkprl3_kpro = flkpro
     c                   when      w_seqe = 'N'
     c                   eval      fkprl2_navn = flnavn
     c                   when      w_seqe = 'E'
     c                   eval      fkprl5_selg = flselg
5.40 c                   when      w_seqe = 'X'
5.40 c                   eval      fkprl6_epro = flepro
     c                   other
     c                   eval      fkprl1_kund = flkund
     c                   eval      fkprl1_kpro = flkpro
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'P'
     c     fkprl3_key    setll     fkprl3
     c                   when      w_seqe = 'N'
     c     fkprl2_key    setll     fkprl2
     c                   when      w_seqe = 'E'
     c     fkprl5_key    setll     fkprl5
5.40 c                   when      w_seqe = 'X'
5.40 c     fkprl6_key    setll     fkprl6
     c                   other
     c     fkprl1_key    setll     fkprl1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Kunde
      *
     c                   if        w_afld = 'B2KUND'
     c                   eval      b2kpro = 0
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    b2kund
     c                   parm                    w_knav
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1KUND'
     c                   eval      c1kpro = 0
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    c1kund
     c                   parm                    c1knav
     c                   leavesr
     c                   endif
5.62  *
5.62 c                   if        w_afld = 'H1KUND'
5.62 c                   call      'RK500R'
5.62 c                   parm                    w_firm
5.62 c                   parm                    h1kund
5.62 c                   parm                    w_knav
5.62 c                   leavesr
5.62 c                   endif
      *
     c                   if        w_afld = 'K1KUND'
     c                   eval      k1kpro = 0
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    k1kund
     c                   parm                    w_knav
     c                   leavesr
     c                   endif
      *
      * Selger
      *
     c                   if        w_afld = 'B2SELG'
     c                   call      'RA509R'
     c                   parm                    w_firm
     c                   parm                    b2selg
     c                   parm                    w_snav
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
7.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.02  * Newlook vask av data
7.02  *  Fjerner tegn som gjør at Newlook feiltolker
7.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.02  *
7.02  /FREE
7.02
7.02   begsr newlo_vask;
7.02     for w_teller = 1 to 25;
7.02       w_len = %len(%trim(b1navn));
7.02       if (w_len > 0);
7.02       b1navn = %ScanRpl('.':'':b1navn:w_len); // newlook tåler ikke . på slutten
7.02       endif;
7.02       w_len = %len(%trim(b1navn));
7.02       if (w_len > 0);
7.02       b1navn = %ScanRpl(':':'':b1navn:w_len); // newlook tåler ikke : på slutten
7.02       endif;
7.02     endfor ;
7.02   endsr;
7.02  /END-FREE
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på kundeprosjekt-register
      *
     c     fkprlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprlr_kund
     c                   kfld                    fkprlr_kpro
      *
      * Key for oppdatering av kundeprosjekt-register
      *
     c     fkprlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprlu_kund
     c                   kfld                    fkprlu_kpro
      *
      * Key for posisjonering på kunde
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      * Key for posisjonering på kundeprosjekt
      *
     c     fkprl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl3_kpro
      *
      * Key for posisjonering på navn
      *
     c     fkprl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl2_navn
      *
      * Key for posisjonering på selger
      *
     c     fkprl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl5_selg
5.40  *
5.40  * Key for posisjonering på
5.40  * eksternt prosjektnummer
5.40  *
5.40 c     fkprl6_key    klist
5.40 c                   kfld                    w_firm
5.40 c                   kfld                    fkprl6_epro
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
      * Initierer skjermbildet
      *
     c                   eval      *in22 = *on
      *
      * Henter dagens dato
      *
     c                   time                    w_udat
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c     fkprl1_key    setll     fkprl1
     c                   exsr      crt_subfile
7.01  *
7.01  * Skal vi vise vedlikehold av spesialpriser og rabattmatrise
7.01  *
7.01 c                   eval      *in33 = *off
7.01 c                   eval      *in34 = *off
7.01  *
7.01   CallP CO402R(l_filg:w_firm:0:'FL100_701':
7.01                    co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01     u_s701 = *on;
7.01     *in33 = *on;
7.01     *in34 = *on;
7.01   endif;
7.01  *
      *
     c                   endsr
