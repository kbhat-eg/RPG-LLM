     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : VG707R
      * Beskrivelse . . : Henter ut prosentsats for beregningsavik
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       270521 ASK  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *                                                                                 4
      *       LR = Avslutt program
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvgkair    if   e           k disk    rename(vgkast:vgkairr)
      *
      * Definering av parametre
      *
     d p_aogr          s                   like(vgaogr)
     d p_ahgr          s                   like(vgahgr)
     d p_augr          s                   like(vgaugr)
     d p_aldo          s                   like(vgaldo)
     d p_amod          s                   like(vgamod)
     d p_aart          s                   like(vgaart)
     d p_apro          s                   like(vgapro)
     d p_inlr          s              1
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variabler i nøkler
      *
     d vgkair_aogr     s                   like(vgaogr)
     d vgkair_ahgr     s                   like(vgahgr)
     d vgkair_augr     s                   like(vgaugr)
     d vgkair_aldo     s                   like(vgaldo)
     d vgkair_amod     s                   like(vgamod)
     d vgkair_aart     s                   like(vgaart)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vgafrm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedrutine - hent aktuell prosentsats
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Sjekk om siste runde for å lukke filer
      *
     c                   if        p_inlr = '1'
     c                   eval      *inlr = *on
     c                   goto      avslutt
     c                   endif
      *
      * Finn avviksprosent
      *
     c                   eval      p_apro = 0
     c                   exsr      finn_pros
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne avviksprosent
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_pros     begsr
      *
      * Sjekker med leverandør
      *  - alle parameter
      *
     c                   if        p_aldo <> 0
     c                   eval      vgkair_aogr = p_aogr
     c                   eval      vgkair_ahgr = p_ahgr
     c                   eval      vgkair_augr = p_augr
     c                   eval      vgkair_aldo = p_aldo
     c                   eval      vgkair_amod = 0
     c                   eval      vgkair_aart = 0
     c     vgkair_key    chain     vgkairr
     c                   if        %found(vgkair)
     c                   eval      p_apro = vgapro
     c                   goto      end_pros
     c                   endif
      *
      *  - med ogr, hgr og lev
      *
     c                   eval      vgkair_augr = 0
     c     vgkair_key    chain     vgkairr
     c                   if        %found(vgkair)
     c                   eval      p_apro = vgapro
     c                   goto      end_pros
     c                   endif
      *
      *  - med ogr og lev
      *
     c                   eval      vgkair_ahgr = 0
     c     vgkair_key    chain     vgkairr
     c                   if        %found(vgkair)
     c                   eval      p_apro = vgapro
     c                   goto      end_pros
     c                   endif
      *
      *  - med bare lev
      *
     c                   eval      vgkair_aogr = 0
     c     vgkair_key    chain     vgkairr
     c                   if        %found(vgkair)
     c                   eval      p_apro = vgapro
     c                   goto      end_pros
     c                   endif
     c                   endif
      *
      * Sjekker med undergruppe
      *
     c                   if        p_augr <> 0
     c                   eval      vgkair_aogr = p_aogr
     c                   eval      vgkair_ahgr = p_ahgr
     c                   eval      vgkair_augr = p_augr
     c                   eval      vgkair_aldo = 0
     c                   eval      vgkair_amod = 0
     c                   eval      vgkair_aart = 0
     c     vgkair_key    chain     vgkairr
     c                   if        %found(vgkair)
     c                   eval      p_apro = vgapro
     c                   goto      end_pros
     c                   endif
     c                   endif
      *
      * Sjekker med hovedgruppe
      *
     c                   if        p_ahgr <> 0
     c                   eval      vgkair_aogr = p_aogr
     c                   eval      vgkair_ahgr = p_ahgr
     c                   eval      vgkair_augr = 0
     c                   eval      vgkair_aldo = 0
     c                   eval      vgkair_amod = 0
     c                   eval      vgkair_aart = 0
     c     vgkair_key    chain     vgkairr
     c                   if        %found(vgkair)
     c                   eval      p_apro = vgapro
     c                   goto      end_pros
     c                   endif
     c                   endif
      *
      * Sjekker med overgruppe
      *
     c                   if        p_aogr <> 0
     c                   eval      vgkair_aogr = p_aogr
     c                   eval      vgkair_ahgr = 0
     c                   eval      vgkair_augr = 0
     c                   eval      vgkair_aldo = 0
     c                   eval      vgkair_amod = 0
     c                   eval      vgkair_aart = 0
     c     vgkair_key    chain     vgkairr
     c                   if        %found(vgkair)
     c                   eval      p_apro = vgapro
     c                   goto      end_pros
     c                   endif
     c                   endif
      *
      * Dersom ingen funnet - sjekk om generell prosent
      *
     c                   eval      vgkair_aogr = 0
     c                   eval      vgkair_ahgr = 0
     c                   eval      vgkair_augr = 0
     c                   eval      vgkair_aldo = 0
     c                   eval      vgkair_amod = 0
     c                   eval      vgkair_aart = 0
     c     vgkair_key    chain     vgkairr
     c                   if        %found(vgkair)
     c                   eval      p_apro = vgapro
     c                   else
     c                   eval      p_apro = 0
     c                   endif
      *
     c     end_pros      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_aogr
     c                   parm                    p_ahgr
     c                   parm                    p_augr
     c                   parm                    p_aldo
     c                   parm                    p_amod
     c                   parm                    p_aart
     c                   parm                    p_apro
     c                   parm                    p_inlr
      *
      * Key for posisjonering på faktortype
      *
     c     vgkair_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vgkair_aogr
     c                   kfld                    vgkair_ahgr
     c                   kfld                    vgkair_augr
     c                   kfld                    vgkair_aldo
     c                   kfld                    vgkair_amod
     c                   kfld                    vgkair_aart
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
