     H/TITLE  ASLAGR: Flytter parameter til ny member i nytt register
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  System. . . . . : ASLAGR                                       *
      *  Program . . . . : LO112R                                       *
      *  Beskrivelse . . : Flytter parameter til member i nytt register *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  Spesialversjon. :                                              *
      *  Tilpasset for . :                                              *
      *                                                                 *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.*
      *  --------- ------  ---------------------------------------------*
6.nu  *  R6.30     110614  Sjekket mtp. nummerutvidelse            bof  *
      *                                                                 *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     FLPM1L1    IP   E           K DISK    RENAME(LPM1PFR:LPM1L1R)
      *
     FLPM1LU    UF   E           K DISK    RENAME(LPM1PFR:LPM1LUR)
     FLPM2LU    UF A E           K DISK    RENAME(LPM2PFR:LPM2LUR)
      *
      *
     D                 DS
     D  LDAMBR                 1     10
     D  LDABOK                 1      1
     D  LDAFIR                 2      4  0
     D  LDALST                 5     10  0
      *
      *
     D                UDS
     D  LDVLG1               500    500  0
     D  LDVLG2               501    501  0
     D  LDMEMB               502    511
     D  LDVLG3               599    599  0
      *
     D  DSWSID               901    910
     D  DSUSER               911    920
      *
      *
     ILPM1L1R       01
     I                                          LK1FIR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandler oppstart av utskrift, en pr. gang                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      *  Blanker verdier hver gang, verdier som også benyttes i CL-pros.
      *
     C                   MOVE      *ZERO         LDVLG1
     C                   MOVE      *ZERO         LDVLG2
     C                   MOVE      *BLANK        LDMEMB
      *
      *  Hopper ut dersom record ikke funnet
      *
     C     *IN01         CABEQ     *OFF          XSLUTT
      *
      *  Sjekk,  At kun parameter for denne SKJERM taes med
      *
     C     LK1WSD        CABNE     DSWSID        XSLUTT
      *
      *  Sjekk,  At kun parameter for denne BRUKER taes med
      *
     C     LK1USR        CABNE     DSUSER        XSLUTT
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandler flytting av PARAM1 til PARAM2    (Record funnet)     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *  Sett inn 1, som sier at rutinen skal starte LOORDC
      *
     C                   MOVE      1             LDVLG1
      *
      *  Legger inn 2 som sier at jobben  skal kjøres fra jobbkø
      *
     C     LK1VG0        IFEQ      2
     C                   MOVE      2             LDVLG1
     C                   END
      *
      *  Legger over record fra PARAM1 til PARAM2 (Like registre)
      *
     C                   MOVE      LK1FIR        PM2FIR
     C     PM2KEY        CHAIN     LPM2LUR                            90
     C     *IN90         IFEQ      *ON
     C                   MOVE      LK1FIR        LK2FIR
     C                   MOVE      LK1LST        LK2LST
     C                   MOVE      LK1OTY        LK2OTY
     C                   MOVE      LK1WSD        LK2WSD
     C                   MOVE      LK1USR        LK2USR
     C                   MOVE      LK1UTQ        LK2UTQ
     C                   MOVE      LK1ONR        LK2ONR
     C                   MOVE      LK1SUF        LK2SUF
     C                   MOVE      LK1VG0        LK2VG0
     C                   MOVE      LK1VG1        LK2VG1
     C                   MOVE      LK1VG2        LK2VG2
     C                   MOVE      LK1PER        LK2PER
     C                   MOVE      LK1DAT        LK2DAT
     C                   WRITE     LPM2LUR
     C                   END
      *
      *  Sletter recorden i PLUKK-1-LINJE-REGISTER
      *
     C                   MOVE      LK1FIR        PM1FIR
     C                   MOVE      LK1WSD        PM1WSD
     C                   MOVE      LK1USR        PM1USR
     C                   MOVE      LK1LST        PM1LST
     C     PM1KEY        CHAIN     LPM1LUR                            90
     C     *IN90         IFEQ      *OFF
     C                   DELETE    LPM1LUR
     C                   END
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Sjekker om rutinen skal kjøres flere ganger,                   *
      *  den skal kun kjøres flere ganger dersom bestillings-nr er null *
      *  Best_nummer lik null, er utskrift av mange best_nummer         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     LK1ONR        IFEQ      *ZERO
      *
      *  Henter siste nummer for File-member
      *
     C                   CALL      'LÅ900R'
     C                   PARM                    WCQNNN
     C                   PARM                    WCNUMM
      *
      *  Danner navn på Filemembers for NESTE (NESTE) utskrift
      *
     C                   MOVE      'A'           LDABOK
     C                   MOVE      LK1FIR        LDAFIR
     C                   MOVE      WCNUMM        LDALST
      *
     C                   MOVE      LDAMBR        LDMEMB
      *
      *  Sett inn 1, som sier at rutinen skal starte pånytt igjen
      *
     C                   MOVE      1             LDVLG2
     C                   MOVE      1             LDVLG3
     C                   END
      *
      *  Sett på LR for at record er funnet og programmet skal avsluttes
      *
     C                   SETON                                        LR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     XSLUTT        TAG
      *
     CLR                 RETURN
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     CSR   *INZSR        BEGSR
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     C                   MOVE      *ZERO         WCQNNN            1
     C                   MOVE      *ZERO         WCNUMM            6 0
      *
      *  Redefinering av nøkler
      *
     C     *LIKE         DEFINE    LK1FIR        PM1FIR
     C     *LIKE         DEFINE    LK1WSD        PM1WSD
     C     *LIKE         DEFINE    LK1USR        PM1USR
     C     *LIKE         DEFINE    LK1LST        PM1LST
      *
     C     *LIKE         DEFINE    LK2FIR        PM2FIR
      *
      *  Key for file : PLUKK-1-LINJE-REGISTER
      *
     C     PM1KEY        KLIST
     C                   KFLD                    PM1FIR
     C                   KFLD                    PM1WSD
     C                   KFLD                    PM1USR
     C                   KFLD                    PM1LST
      *
      *  Key for file : PLUKK-2-LINJE-REGISTER
      *
     C     PM2KEY        KLIST
     C                   KFLD                    PM2FIR
      *
     CSR                 ENDSR
