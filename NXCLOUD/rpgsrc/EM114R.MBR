     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : EM114R
      * Beskrivelse . . : Bonus, sjekker om kunden har innfridd krav til bonus
      *
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       261011 bsa  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fembhi1    if   e           k disk    rename(embhst:embhi1r)
     frkbpl1    if   e           k disk    rename(rkbppfr:rkbpl1r)
      *
      * Definering av Local data area
      *
     d                uds
     d  l_user               911    920
     d  l_firm               944    946  0
     d  l_fnav               951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d embhi1_hbot     s                   like(emhbot)
     d embhi1_hmnr     s                   like(emhmnr)
      *
     d rkbpl1_bkun     s                   like(rkbkun)
      *
      * Definering av variable
      *
      *
     d w_firm          s                   like(emhfir)
      *
     d p_kund          s                   like(rkbkun)
     d p_hmnr          s                   like(emhmnr)
     d p_tbot          s                   like(emhbot)
     d p_ok            s              1
      *
      * Definering av konstanter
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å kopiere av bonusmal
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      p_ok = '1'
      *
      * Er dette mal av type som krever godkjennelse
      *
     c                   eval      embhi1_hbot = p_tbot
     c                   eval      embhi1_hmnr = p_hmnr
     c     embhi1_key    chain     embhi1
      *
     c                   if        %found(embhi1)
      * 2=Lojalitetsbonus 3=Betalingsbonus
     c                   if        emhbka = 2 or
     c                             emhbka = 3
      *
      * Sjekk mot kunden om betingelser er oppfylt
      *
     c                   eval      rkbpl1_bkun = p_kund
     c     rkbpl1_key    chain     rkbpl1
      *
     c                   if        %found(rkbpl1)
      * Lajalitetsbonus
     c                   if        emhbka = 2 and
     c                             rkbloj <> 1
     c                   eval      p_ok = '0'
     c                   endif
      * Betalingsbonus
     c                   if        emhbka = 3 and
     c                             rkbbet <> 1
     c                   eval      p_ok = '0'
     c                   endif
      * Kunde ikke funnet med betingelser
     c                   else
     c                   eval      p_ok = '0'
     c                   endif
      * Ingen spesialkode på bonustypen
     c                   endif
      * Finner ikke malen ???
     c                   else
     c                   eval      p_ok = '0'
     c                   endif
      *
     c                   eval      *inlr = *on
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Tar inn parameter fra forrige program
      *
     c     *entry        plist
     c                   parm                    p_kund
     c                   parm                    p_tbot
     c                   parm                    p_hmnr
     c                   parm                    p_ok
      *
      * Key for oppslag på bonusregister - elementer
      *
     c     embhi1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    embhi1_hbot
     c                   kfld                    embhi1_hmnr
      *
      * Key for oppslag på kunderegister
      *
     c     rkbpl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkbpl1_bkun
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
