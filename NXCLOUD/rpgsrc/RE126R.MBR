     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RE126R                                        *
      * beskrivelse . . : Danne rengskapspostering.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       300502 bø   Nytt program
6.20  *  6.20 011210 nho  Prosjekt er alfa                               NHO  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       30 = Protect av felter ved vise
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer
      *    60-69 = Fileindikatorer chain etc.
      *    70-79 = Arbeidsindikatorer i sub-rutiner
      *    80-89 = Arbeidsindikatorer ordinære
      *    90-99 = Benyttes ved std. sub-rutiner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     Frfw2pf    ip   f  700        disk
     frtralu    uf a e           k disk    rename(rtrapfr:rtralur)
     frbunlu    uf a e           k disk    rename(rbunpfr:rbunlur)
     frbunl1    if   e           k disk    rename(rbunpfr:rbunl1r)
     fraa1l1    if   e           k disk    rename(raa1pfr:raa1l1r)
     fanumlu    uf a e           k disk    rename(anumpfr:anumlur)
      *
      * Definering av array
      *
     d a_text          s             30    dim(2) ctdata perrcd(1)
      *
      * Variabler
610  d rtralu_nivo     s                   like(rtnivo)
610  d rtralu_memb     s                   like(rtmemb)
610  d rbunlu_nivo     s                   like(rsnivo)
610  d rbunlu_memb     s                   like(rsmemb)
610  d rbunl1_nivo     s                   like(rsnivo)
610  d rbunl1_memb     s                   like(rsmemb)
610  d w_memb          s                   like(rsmemb)
      *
     d b_upd_bunt      s              1    inz(*off)
     d b_bilag         s              1    inz(*off)
      *
     Dwdato1           s               d   datfmt(*iso)
     d w_mm            s              2  0
     d w_aa            s              2  0
     d w_aaaa          s              4  0
     d w_mmaa          s              4  0
     d w_dato          s              6  0
     d w_per           s              4
     d w_tot_bunt      s             11  2
     d w_forste        s              1
     d p_firmx         s              3
     d w_firm          s                   like(rtfirm)
     d w_rsfØrs        s                   like(rsfØrs)
     d w_rssist        s                   like(rssist)
     d w_resn          s                   like(rtresn)
     d w_biln          s                   like(rtbiln)
     d w_trlinj        s                   like(rtlinj)
     d w_text          s             30
     d w_bilk          s              2  0
     d w_avde          s              4  0
     d w_kont          s              6  0
     d w_spes          s              2  0
     d w_mvak          s              1
     d w_belo          s             11  2
     d w_sign          s              1
      *
      *
      * Definering av variabler i nøkler
      *
     d rtralu_bunt     s                   like(rtbunt)
     d rtralu_linj     s                   like(rtlinj)
     d rbunlu_bunt     s                   like(rsbunt)
     d rbunl1_bunt     s                   like(rsbunt)
     d anumlu_fell     s                   like(anfell)
     d anumlu_type     s                   like(antype)
     d anumlu_fast     s                   like(anfast)
      *
     d w_bunt_dannet   s              1    inz(' ')
      *
     d w_datoinn       ds
     d w_ddi                          2
     d w_1                            1
     d w_mmi                          2
     d w_2                            1
     d w_aai                          2
      *
     d w_datout        ds
     d w_ddu                          2
     d w_mmu                          2
     d w_aau                          2
      *
      *
      * Definering av Local data area
      *
     d                uds
     d  xgrup                201    203
     d l_user                911    920
     d l_firm                944    946  0
      *
      *
      *
     Irfw2pf    NS  01
     I                                  1    4  peri
     I                                  5   12  dato
     I                                 13   42  text
     I                                 43   44  bilk
     I                                 45   48  avde
     I                                 49   54  kont
     I                                 55   56  spes
     I                                 57   57  mvak
     I                                 58   68  belo
     I                                 69   69  sign
      /eject
      *
      * tester at periode er en gyldig periode
      *
     c                   movel     peri          w_mm
     c                   if        w_mm < 1 and
     c                             w_mm > 12
     c                   eval      *inlr  = *on
     c                   endif
     c                   move      peri          w_aa
     c                   if        w_aa > 80
     c                   move      1900          w_aaaa
     c                   else
     c                   move      2000          w_aaaa
     c                   endif
     c                   move      w_aa          w_aaaa
      *
      * Henter siste bilagsnummer - ved første les
      *
     c                   if        b_bilag    = *off
4.31  *
4.31 c                   eval      anumlu_fell = 'HBNUMM'
4.31 c                   eval      anumlu_type = *blank
4.31 c                   eval      anumlu_fast = *blank
4.31 c     anumlu_key    chain     anumlur                            60
4.31 c                   if        *in60  = *off
4.31 c                   add       1             ansist
4.31 c                   update    anumlur
4.31 c                   else
4.31 c                   move      w_firm        anfirm
4.31 c                   eval      antype     =  anumlu_type
4.31 c                   eval      anfast     =  anumlu_fast
4.31 c                   eval      anfell     =  anumlu_fell
4.31 c                   movel     a_text(1)     antxt1
4.31 c                   movel     a_text(2)     antxt2
4.31 c                   MOVEL     '1'           anwrap
6.NU c                   eval      anntom     =  9999999
4.31 c                   eval      ansist     =  1
4.31 c                   write     anumlur
4.31 c                   endif
4.31 c                   eval      w_biln     = ansist
     c                   eval      b_bilag = *on
     c                   endif
      *
      * tester av dato
      *
     c                   move      dato          w_datoinn
     c                   eval      w_ddu   = w_ddi
     c                   eval      w_mmu   = w_mmi
     c                   eval      w_aau   = w_aai
     c                   move      w_datout      w_dato
     c     *dmy          test (d)                w_dato                 33
     c                   if        *in33 = *on
     c                   eval      *inlr  = *on
     c                   endif
      *
      * Flytter felter
      *
     c                   move      peri          w_mmaa
     c     *dmy          move      w_dato        wdato1
     c                   move      bilk          w_bilk
     c                   move      text          w_text
     c                   move      avde          w_avde
     c                   move      kont          w_kont
     c                   move      spes          w_spes
     c                   move      mvak          w_mvak
     c                   move      belo          w_belo
     c                   move      sign          w_sign
      *
     c                   if        w_sign = '1'
     c                   eval      w_belo = w_belo * -1
     c                   endif
      *
      * sjekker konto-grense - avgjør om det en h.bok eller reskontro-konto
      *
     c                   if        w_kont > ra1hkt
     c                   exsr      dann_resk
     c                   else
     c                   exsr      dann_trans
     c                   endif
      *
      *
     clr                 if        b_upd_bunt  =  *on
     clr                 exsr      upd_bunt
     clr                 endif
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å oppdatere bunt på økonomi-transaksjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     upd_bunt      begsr
      *
610  c                   eval      rbunlu_nivo = *blank
610  c                   eval      rbunlu_memb = w_memb
     c     rbunlu_key    chain     rbunlu                             63
      *
     c                   if        *in63 = *off
     c                   eval      rsbsum = w_tot_bunt
     c                   eval      rspsum = w_tot_bunt
      *
     c                   if        w_tot_bunt >= 0
     c                   eval      rspdeb = w_tot_bunt
     c                   eval      rspkre = *zero
     c                   else
     c                   eval      rspdeb = *zero
     c                   eval      rspkre = w_tot_bunt
     c                   endif
     c                   eval      rsrper = w_mmaa
     c                   move      wdato1        rspdat
      *
610  c                   time                    rsedat
610  c                   time                    rsetim
610  c                   eval      rseusr = l_user
     c                   update    rbunlur
      *
     c                   endif
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å lage bunt-record
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dann_bunt     begsr
      *
610  c                   eval      rbunl1_nivo = *blank
610  c                   eval      rbunl1_memb = w_memb
     c                   eval      rbunl1_bunt =  *hival
     c     rbunl1_key    setll     rbunl1
      *
     c                   readp     rbunl1                                 63
      *
     c                   if        *in63 =  *on
     c                   eval      rbunlu_bunt = 1
     c                   else
     c                   eval      rbunlu_bunt = rsbunt + 1
     c                   endif
      *
610  c                   eval      rbunlu_nivo = *blank
610  c                   eval      rbunlu_memb = w_memb
     c     rbunlu_key    chain     rbunlu                             63
      *
     c                   if        *in63  = *on
     c                   eval      rsfirm = w_firm
     c                   eval      rsbunt = rbunlu_bunt
     c                   eval      rsstat = ' '
     c                   eval      rsfØrs = *zero
     c                   eval      rssist = *zero
     c                   eval      rspdat = *loval
     c                   eval      rsbsum = *zero
     c                   eval      rspsum = *zero
     c                   eval      rspdeb = *zero
     c                   eval      rspkre = *zero
610  c                   eval      rsnivo = *blank
610  c                   eval      rsmemb = w_memb
610  c                   time                    rsedat
610  c                   time                    rsetim
610  c                   eval      rseusr = l_user
      *
     c                   write     rbunlur
      *
     c                   eval      w_bunt_dannet = 'J'
     c                   endif
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å oppdatere leverandør-transen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     dann_resk     begsr
      *
     c                   eval      rtralu_bunt = rsbunt
4.11 c                   eval      rtralu_linj = w_trlinj + 1
      *
     c     rtralu_key    chain     rtralu                             63
      *
     c                   if        *in63 = *on
     c                   eval      w_trlinj = w_trlinj + 1
     c                   eval      rtreid = 'T'
     c                   eval      rtfirm = w_firm
     c                   eval      rtbunt = rtralu_bunt
     c                   eval      rtlinj = rtralu_linj
      *
     c                   if        w_forste = *blank
     c                   eval      w_rsfØrs = 1
     c                   eval      w_forste = 'J'
     c                   endif
      *
     c                   eval      w_rssist = w_rssist + 1
     c                   eval      rtstat = *blank
     c                   move      wdato1        rtbdat
      *
     c                   if        w_belo >= 0
     c                   eval      rtbelØ = w_belo
     c                   else
     c                   eval      rtbelØ = w_belo * -1
     c                   endif
      *
     c                   eval      rtrper = w_mmaa
     c                   eval      rtbiln = w_biln
      *
     c                   if        w_belo >= 0
      *
      * positivt beløp
      * debet
      *
     c                   eval      rtcavd = w_avde
     c                   eval      rtckto = w_kont
     c                   eval      rtcsps = *zero
     c                   eval      rtcmva = *blank
      * kredit
     c                   eval      rtdavd = *zero
     c                   eval      rtdkto = *zero
     c                   eval      rtdsps = *zero
     c                   eval      rtdmva = *blank
      *
     c                   else
      *
      * negativt beløp
      * kredit
      *
     c                   eval      rtdavd = w_avde
     c                   eval      rtdkto = w_kont
     c                   eval      rtdsps = *zero
     c                   eval      rtdmva = *blank
      * debet
     c                   eval      rtcavd = *zero
     c                   eval      rtckto = *zero
     c                   eval      rtcsps = *zero
     c                   eval      rtcmva = *blank
     c                   endif
      *
6.20 c**                 eval      rtpros = *zero
6.20 c                   eval      rtpros = *blank
     c                   move      wdato1        rtfdat
     c                   eval      rtkrab = *zero
     c                   eval      rtvalk = *blank
      *
     c                   eval      rtakti = *blank
     c                   eval      rtmngd = *zero
     c                   eval      rtmngk = *zero
     c                   eval      rtresn = w_resn
     c                   eval      rtvirk = *blank
     c                   eval      rtatte = *zero
     c                   eval      rtkrys = *blank
     c                   eval      rtkidi = *blank
     c                   eval      rtbilk = w_bilk
      *
     c                   movel     w_text        rttext
      *
     c                   eval      rtbetb = 0
     c                   eval      rtbref = *blank
     c                   eval      rtdeck = *blank
     c                   eval      rtautx = *blank
      *
610  c                   eval      rtnivo = *blank
610  c                   eval      rtmemb = w_memb
610  c                   time                    rtedat
610  c                   time                    rtetim
610  c                   eval      rteusr = l_user
     c                   write     rtralur
      *
     c                   eval      b_upd_bunt = '1'
     c                   eval      w_tot_bunt = w_tot_bunt + rtbelØ
     c                   endif
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å oppdatere økonomi-transaksjoner
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     dann_trans    begsr
      *
     c                   eval      rtralu_bunt = rsbunt
4.11 c                   eval      rtralu_linj = w_trlinj + 1
     c     rtralu_key    chain     rtralu                             63
      *
     c                   if        *in63 = *on
     c                   eval      w_trlinj = w_trlinj + 1
     c                   eval      rtreid = 'T'
     c                   eval      rtfirm = w_firm
     c                   eval      rtbunt = rtralu_bunt
     c                   eval      rtlinj = rtralu_linj
      *
     c                   if        w_forste = *blank
     c                   eval      w_rsfØrs = 1
     c                   eval      w_forste = 'J'
     c                   endif
      *
     c                   eval      w_rssist = w_rssist + 1
     c                   eval      rtstat = *blank
     c                   move      wdato1        rtbdat
      *
     c                   if        w_belo >= 0
     c                   eval      rtbelØ = w_belo
     c                   else
     c                   eval      rtbelØ = w_belo * -1
     c                   endif
      *
     c                   eval      rtrper = w_mmaa
     c                   eval      rtbiln = w_biln
      *
     c                   if        w_belo >= 0
      *
      * positivt beløp
      * debet
      *
     c                   eval      rtdavd = w_avde
     c                   eval      rtdkto = w_kont
     c                   eval      rtdsps = w_spes
     c                   eval      rtdmva = w_mvak
      *
      * kredit
      *
     c                   eval      rtcavd = *zero
     c                   eval      rtckto = *zero
     c                   eval      rtcsps = *zero
     c                   eval      rtcmva = *blank
     c                   else
      *
      * negativt beløp
      * kredit
      *
     c                   eval      rtcavd = w_avde
     c                   eval      rtckto = w_kont
     c                   eval      rtcsps = w_spes
     c                   eval      rtcmva = w_mvak
      *
      * debet
      *
     c                   eval      rtdavd = *zero
     c                   eval      rtdkto = *zero
     c                   eval      rtdsps = *zero
     c                   eval      rtdmva = *blank
     c                   endif
      *
6.20 c**                 eval      rtpros = *zero
6.20 c                   eval      rtpros = *blank
     c                   move      wdato1        rtfdat
     c                   eval      rtkrab = *zero
     c                   eval      rtvalk = *blank
      *
     c                   eval      rtakti = *blank
     c                   eval      rtmngd = *zero
     c                   eval      rtmngk = *zero
     c                   eval      rtresn = *zero
     c                   eval      rtvirk = *blank
     c                   eval      rtatte = *zero
     c                   eval      rtkrys = *blank
     c                   eval      rtkidi = *blank
     c                   eval      rtbilk = w_bilk
      *
     c                   eval      rttext = w_text
      *
     c                   eval      rtbetb = 0
     c                   eval      rtbref = *blank
     c                   eval      rtdeck = *blank
     c                   eval      rtautx = *blank
      *
610  c                   eval      rtnivo = *blank
610  c                   eval      rtmemb = w_memb
610  c                   time                    rtedat
610  c                   time                    rtetim
610  c                   eval      rteusr = l_user
     c                   write     rtralur
      *
     c                   eval      b_upd_bunt = '1'
     c                   eval      w_tot_bunt = w_tot_bunt + rtbelØ
     c                   endif
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *
      * Key for oppdatering av økonomi transer
      *
     c     rtralu_key    klist
610  c                   kfld                    rtralu_nivo
610  c                   kfld                    rtralu_memb
     c                   kfld                    w_firm
     c                   kfld                    rtralu_bunt
     c                   kfld                    rtralu_linj
      *
      * Key for oppdatering av bunt
      *
     c     rbunlu_key    klist
610  c                   kfld                    rbunl1_nivo
610  c                   kfld                    rbunl1_memb
     c                   kfld                    w_firm
     c                   kfld                    rbunlu_bunt
      *
      * Key for oppdatering av bunt
      *
     c     rbunl1_key    klist
610  c                   kfld                    rbunl1_nivo
610  c                   kfld                    rbunl1_memb
     c                   kfld                    w_firm
     c                   kfld                    rbunl1_bunt
      *
      * Key for les av raa1l1
      *
     c     raa1l1_key    klist
     c                   kfld                    w_firm
      *
      * Key for tellerfiler:
      *
     C     anumlu_key    klist
     C                   kfld                    w_firm
     C                   kfld                    anumlu_fell
     C                   kfld                    anumlu_type
     C                   kfld                    anumlu_fast
      *
     c     *entry        plist
     c                   parm                    p_firmx
610  c                   parm                    w_memb
      *
     c                   move      p_firmx       w_firm
      *
     c     raa1l1_key    chain     raa1l1
      *
     c                   if        not %found
     c                   eval      *inlr = *on
     c                   endif
      *
      * parametere
      *
     c                   exsr      dann_bunt
     c                   eval      w_forste = *blank
      *
     c                   endsr
**
Teller for Elektronisk bilag
ASOKON Oppdateres i RF141R
