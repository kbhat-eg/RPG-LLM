     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASPGPL
      * Program . . . . : AVVELR
      * Beskrivelse . . : Sikkerhetsgruppe, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       010507 KRK  Nytt program
7.xx  * 7.xx      140618  Endringer reltart til ny GUI. NB Kan kun gjelde BKV
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fasgrl1    if   e           k disk    rename(asgrpfr:asgrl1r)
     fasgrlr    if   e           k disk    rename(asgrpfr:asgrlrr)
     fasgrlu    uf a e           k disk    rename(asgrpfr:asgrlur)
     fausrl1    if   e           k disk    rename(ausrpfr:ausrl1r)
      *
     fat120d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d asgrl1_sgrn     s                   like(atsgrn)
     d asgrl1_sgrs     s                   like(atsgrs)
     d asgrl1_sgru     s                   like(atsgru)
     d asgrlr_sgrn     s                   like(atsgrn)
     d asgrlr_sgrs     s                   like(atsgrs)
     d asgrlr_sgru     s                   like(atsgru)
     d asgrlu_sgrn     s                   like(atsgrn)
     d asgrlu_sgrs     s                   like(atsgrs)
     d asgrlu_sgru     s                   like(atsgru)
     d ausrl1_user     s                   like(abuser)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d s_sgrn          s                   like(atsgrn)
     d s_sgrs          s                   like(atsgrs)
     d s_sgru          s                   like(atsgru)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2sgrn <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      asgrl1_sgrn = b1sgrn
     c                   eval      asgrl1_sgrs = b1sgrs
     c                   eval      asgrl1_sgru = b1sgru
     c     asgrl1_key    setll     asgrl1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på overgruppe
      *
     c                   if        b2sgrn <> *blank
     c                   eval      w_seqe = ' '
     c                   eval      asgrl1_sgrn = b2sgrn
     c     asgrl1_key    setll     asgrl1
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2sgrn = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1sgrn = b1sgrn
     c                   eval      c1sgrs = b1sgrs
     c                   eval      c1sgru = b1sgru
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1sgrn = b1sgrn
     c                   eval      k1sgrs = b1sgrs
     c                   eval      k1sgru = b1sgru
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1sgrn = b1sgrn
     c                   eval      d1sgrs = b1sgrs
     c                   eval      d1sgru = b1sgru
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1sgrn = b1sgrn
     c                   eval      c1sgrs = b1sgrs
     c                   eval      c1sgru = b1sgru
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1sgrn = b2sgrn
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      *......................................................
      * Spørring
      *
     c                   if        *inkd = *on
      *
     c                   if        w_afld = 'c1sgru'
     c*>                 call      'VA520R'
     c*>                 parm                    c1sgru
     c*>                 parm                    w_ktxt
     c*>                 eval      c2ktxt = w_ktxt
     c                   goto      c1taga
     c                   endif
      *
     c                   endif
      *
      *................................................................
      *
      * Validering av input
      *
     c                   if        c1sgrn = *blank
     c                   if        c1sgrs = *blank
     c                   if        c1sgru = *blank
     c                   goto      end_c1bld
     c                   endif
     c                   endif
     c                   endif
      *
     c                   if        c1sgrs = *blank
     c                   if        c1sgru = *blank
     c                   eval      *in31 = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
     c                   if        c1sgrs <> *blank
     c                   if        c1sgru <> *blank
     c                   eval      *in31 = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
     c                   if        c1sgrs = c1sgrn
     c                   eval      *in31 = *on
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1sgru <> *blank
     c                   eval      ausrl1_user = c1sgru
     c     ausrl1_key    chain     ausrl1                             91
     c                   if        *in91 = *on
     c                   eval      *in32 = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      asgrlr_sgrn = c1sgrn
     c                   eval      asgrlr_sgrs = c1sgrs
     c                   eval      asgrlr_sgru = c1sgru
     c     asgrlr_key    chain     asgrlr                             90
     c                   if        *in90 = *off
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1sgrn = *blank
     c                   eval      c1sgrs = *blank
     c                   eval      c1sgru = *blank
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2sgrn = *blank
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      asgrlr_sgrn = c1sgrn
     c                   eval      asgrlr_sgrs = c1sgrs
     c                   eval      asgrlr_sgru = c1sgru
     c     asgrlr_key    chain     asgrlr                             90
      *
     c                   if        *in90 = *off
     c                   eval      c2sgrt = atsgrt
      *
     c                   else
     c                   eval      c2sgrt = *blank
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1sgrn = k1sgrn
     c                   eval      c1sgrs = k1sgrs
     c                   eval      c1sgru = k1sgru
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2sgrt = *blank
     c                   eval      *in31 = *on
     c*>                 goto      c2taga
     c                   endif
      *
      * Oppdatering av gruppe-kode-register
      *
     c                   clear                   asgrlur
      *
     c                   eval      asgrlu_sgrn = c1sgrn
     c                   eval      asgrlu_sgrs = c1sgrs
     c                   eval      asgrlu_sgru = c1sgru
     c     asgrlu_key    chain     asgrlur                            90
      *
     c                   eval      atsgrt = c2sgrt
      *
     c                   if        *in90 = *off
     c                   update    asgrlur
     c                   else
     c                   eval      atsgrn = c1sgrn
     c                   eval      atsgrs = c1sgrs
     c                   eval      atsgru = c1sgru
     c                   write     asgrlur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c*>                 eval      b1otxt = atsgrs
     c*>                 eval      b1okhv = atsgru
     c*>                 eval      b1opra = vgopra
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      asgrlr_sgrn = d1sgrn
     c                   eval      asgrlr_sgrs = d1sgrs
     c                   eval      asgrlr_sgru = d1sgru
     c     asgrlr_key    chain     asgrlr                             90
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter overgruppe
      *
     c                   eval      asgrlu_sgrn = d1sgrn
     c                   eval      asgrlu_sgrs = d1sgrs
     c                   eval      asgrlu_sgru = d1sgru
     c     asgrlu_key    chain     asgrlur                            90
     c                   delete    asgrlur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_sgrn = k1sgrn
     c                   eval      s_sgrs = k1sgrs
     c                   eval      s_sgru = k1sgru
      *
      * Fyller kopierings-bildet
      *
     c                   eval      asgrlr_sgrn = k1sgrn
     c                   eval      asgrlr_sgrs = k1sgrs
     c                   eval      asgrlr_sgru = k1sgru
     c     asgrlr_key    chain     asgrlr                             90
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1sgrn = *blank
     c                   if        k1sgrs = *blank
     c                   if        k1sgru = *blank
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
     c                   endif
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      asgrlr_sgrn = k1sgrn
     c                   eval      asgrlr_sgrs = k1sgrs
     c                   eval      asgrlr_sgru = k1sgru
     c     asgrlr_key    chain     asgrlr                             90
     c                   if        *in90 = *off
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1sgrn = s_sgrn
     c                   eval      c1sgrs = s_sgrs
     c                   eval      c1sgru = s_sgru
     c                   exsr      xc2bld
      *
      * Dersom overgruppe er opprettet, kopieres hovedgrupper og
      * undergrupper
      *
     c                   eval      asgrlr_sgrn = k1sgrn
     c                   eval      asgrlr_sgrs = k1sgrs
     c                   eval      asgrlr_sgru = k1sgru
     c     asgrlr_key    chain     asgrlr                             90
      *
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      asgrl1
      *
     c                   if        %eof
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      asgrl1_sgrn = atsgrn
     c                   eval      asgrl1_sgrs = atsgrs
     c                   eval      asgrl1_sgru = atsgru
      *
     c                   eval      b1valg = 0
     c                   eval      b1sgrn = atsgrn
     c                   eval      b1sgrs = atsgrs
     c                   eval      b1sgru = atsgru
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     asgrl1_key    setgt     asgrl1
     c                   readp     asgrl1
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     asgrl1
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   eval      asgrl1_sgrn = atsgrn
     c                   eval      asgrl1_sgrs = atsgrs
     c                   eval      asgrl1_sgru = atsgru
      *
     c                   enddo
      *
     c                   if        %eof
     c     asgrl1_key    setll     asgrl1
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på gruppe-kode
      *
     c     asgrl1_key    klist
     c                   kfld                    asgrl1_sgrn
     c                   kfld                    asgrl1_sgrs
     c                   kfld                    asgrl1_sgru
      *
      * Key for oppslag på gruppe-kode
      *
     c     asgrlr_key    klist
     c                   kfld                    asgrlr_sgrn
     c                   kfld                    asgrlr_sgrs
     c                   kfld                    asgrlr_sgru
      *
      * Key for oppdatering av gruppe-kode
      *
     c     asgrlu_key    klist
     c                   kfld                    asgrlu_sgrn
     c                   kfld                    asgrlu_sgrs
     c                   kfld                    asgrlu_sgru
      *
      * Key for oppslag på bruker-register
      *
     c     ausrl1_key    klist
     c                   kfld                    ausrl1_user
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      asgrl1_sgrn = *blank
     c                   eval      asgrl1_sgrs = *blank
     c                   eval      asgrl1_sgru = *blank
     c     asgrl1_key    setll     asgrl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
