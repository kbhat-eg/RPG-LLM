     H decedit('0.') datedit(*dmy-) option(*nodebugio)
     h DftActgrp(*No) Actgrp(*NEW)  bnddir('YAJL/YAJL')
      /copy hspecsbnd
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASNAVIKO
      * Program . . . . : IS110R
      * Beskrivelse . . : Spørring på kundeposter D365
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
7.xx  *       290321 bsa  Nytt program - bruker bibl fra Scott Klement for
7.xx  *                   json handling.
9.xx  *       120421 bsa  Lagt inn egne stmt for test.
7.01  * 7.01  110621 bsa  Varsler hvis tom retur.
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frkunlr    if   e           k disk    rename(rkunpfr:rkunlrr)
     frkmel1    if   e           k disk    rename(rkmepfr:rkmel1r)
     filnkir    if   e           k disk    rename(ilnkst:ilnkirr)
      *
     fis110d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Parameter - program RF020R
      *
     d                 ds
     d rxulda                       178
     d  rxstat                        1    overlay(rxulda)
     d  rxfirm                        3  0 overlay(rxulda:2)
     d  rxsyst                        1    overlay(rxulda:5)
     d  rxpros                        6    overlay(rxulda:6)
     d  rxakti                        6    overlay(rxulda:12)
     d  rxf001                        3    overlay(rxulda:18)
     d  rxkont                        6  0 overlay(rxulda:21)
     d  rxspes                        4  0 overlay(rxulda:27)
     d  rxavde                        4  0 overlay(rxulda:31)
     d  rxlinj                        4  0 overlay(rxulda:35)
     d  rxtgrp                        3    overlay(rxulda:39)
     d  rxtext                       78    overlay(rxulda:42)
     d  rxf002                       13    overlay(rxulda:120)
     d  rxvalg                        1    overlay(rxulda:133)
     d  rxtist                         z   overlay(rxulda:134)
     d  rxbiln                        6  0 overlay(rxulda:161)
     d  rxrper                        2  0 overlay(rxulda:167)
     d  rxraar                        4  0 overlay(rxulda:169)
     d  rxonr                         6  0 overlay(rxulda:173)
      *
      * Definering av variabler i nøkler
      *
     d rkunlr_kund     s                   like(rkkund)
      *
      * Definering av variable
      *
     d p_kund          s              6  0
      *
     d XML_Input       s            256a   Varying
     d XML_Output      s            256a   Varying
     d XML_path        s            256a
     d w_xmlp          s             50
     d options         s            200a   Varying
     d jsonData        s            256a   Varying
      *
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
     d w_spge          s              4  0
     d w_srrn          s              4  0
      *
     d w_url           s            200
     d w_reqf          s            100
     d w_rspf          s            100
     d w_xmlf          s            100    Varying
     d w_usrn          s             20
     d w_pass          s             50
     d w_autt          s              5
     d w_stat          s              5
     d w_tell          s              4  0 inz(0)
     d w_date          s               d
      *
     d w_firm          s                   like(rkfirm)
     d w_kund          s              6
     d w_kode          s              1
     d w_fell          s              6
     d w_type          s              2
     d w_raar          s              4  0
     d w_kunn          s              6  0
     d w_biln          s              8  0
     d w_typa          s             50
     d w_refn          s             50
     d w_aar2          s              2  0
     d w_aar4          s              4  0
     d w_aara          s              4
      *
     d w_stal          s              1
     d w_mvak          s              1
     d w_mvtx          s             30
     d w_dato          s               d   datfmt(*iso)
     d w_mvas          s              6  2
     d w_mvat          s              5  4
     d w_mvaf          s             10  9
     d w_bpro          s              5  2
     d w_year          s              4  0
     d w_fsum1         s                   like(rkkjØp)
     d w_fsum2         s                   like(rkkjØp)
     d w_path          s            256
     d w_resp          s            256
     d w_mld01         s             30
     d w_mld02         s             30
     d w_ctli          s              2  0
     d w_ctpo          s              3  0
      *
     d b_alle          s              1    inz(*off)
     d b_eof           s              1    inz(*off)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      /copy prototypeb
      /copy usec
      *
      * Datastruktur for json fra kundepost WS
      *
     d*Kundeposter     ds                  qualified
     d*                                    dim(9999)
     d*  Posting_Date                10a
     d*  Document_Type...
     d*                              10a
     d*  Document_No                 15a
     d*  Customer_No                  6a
     d*  Description                 26a
     d*  Amount                      11a
     d*  Remaining_Amount...
     d*                              11a
     d*  Due_Date                    10a
     d*  Document_date...
     d*                              10a
      *

       Dcl-ds CustomerLedgerEntries Dim(9999) Qualified;
           Posting_Date char(10);
           Document_Type  char(10);
           Document_No  char(15);
           Customer_No  char(6);
           Description  varchar(30);
           Amount   packed(11:1);
           Remaining_Amount  packed(11:1);
           Due_Date  char(10);
           Document_Date varchar(10);
       end-ds  CustomerLedgerEntries;
      *

       dcl-ds *N psds;
           count  int(20) pos(372);
       end-ds;
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   eval      b2aarr = %char(*year)
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   if        b_alle = *off
     c                   eval      b2ptyp = 'Åpne poster'
     c                   else
     c                   eval      b2ptyp = 'Alle poster'
     c                   endif
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkb
     c                   move      *loval        rxtist
     c                   eval      rxbiln = *zero
     c                   eval      rxrper = *zero
     c                   eval      rxraar = *zero
     c                   eval      rxonr  = *zero
     c                   exsr      notat
     c                   goto      b2tagb
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkf
     c                   if        b_alle = *off
     c                   eval      b_alle = *on
     c                   else
     c                   eval      b_alle = *off
     c                   endif
     c                   eval      w_tell = 0
     c                   eval      w_ssrn = 0
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *inkh = *on
     c                   exsr      xe2win
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
      * Filen er tom
      *
     c*                  if        b1binr = 0
     c*                  goto      avslutt
     c*                  endif
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Vise faktura
      *
     c                   if        b1valg = 8
     c                   eval      *in30  = *on
     c                   eval      w_biln = %dec(b1binr:8:0)
     c                   eval      w_kunn = %dec(b2knum:6:0)
     c                   move      b1bida        w_aar2
     c                   eval      w_aar4 = 2000 + w_aar2
     c                   eval      w_raar = w_aar4
      *
     c                   call      'RK110C'
     c                   parm                    w_firm
     c                   parm                    w_raar
     c                   parm                    w_kunn
     c                   parm                    w_biln
     c                   endif
      *
      * Vis arkiv
      *
     c                   if        b1valg = 9
      *
     c                   eval      w_typa = 'FAKTURA'
     c                   move      b1bida        w_aar2
     c                   eval      w_aar4 = 2000 + w_aar2
     c                   eval      w_aara = %char(w_aar4)
     c                   eval      w_refn = %trimr(w_aara)+%trimr(b1binr)
      *
     c                   call      'AP622C'
     c                   parm                    w_typa
     c                   parm                    w_refn
      *
     c                   endif
      *
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      *
     c     end_c1bld     tag
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
     c                   eval      w_tell = w_tell + 1
      /Free
             if CustomerLedgerEntries(w_tell).Posting_Date = *blank;
             leave;
             endif;

               if b_alle = *off;

                if  CustomerLedgerEntries(w_tell).Remaining_Amount <> 0;
                 eval      w_srrn = w_srrn + 1;
                 w_date  = %date(CustomerLedgerEntries(w_tell)
                    .Posting_Date:*ISO);
                 b1bida  = %dec(%char(w_date:*DMY0):6:0);
                 w_date  = %date(CustomerLedgerEntries(w_tell)
                    .Due_Date:*ISO);
                 b1ffda  = %dec(%char(w_date:*DMY0):6:0);
                 b1binr  = CustomerLedgerEntries(w_tell).Document_No;
                 b1bitx  = CustomerLedgerEntries(w_tell).Description;
                 b1obel  = CustomerLedgerEntries(w_tell).Amount;
                 b1rbel  = CustomerLedgerEntries(w_tell).Remaining_Amount;
                 b1tell  = w_tell;
                 eval      b1valg = 0;
                 write     b1sfl;
                endif;

               else;
                 eval      w_srrn = w_srrn + 1;
                 w_date  = %date(CustomerLedgerEntries(w_tell)
                    .Posting_Date:*ISO);
                 b1bida  = %dec(%char(w_date:*DMY0):6:0);
                 w_date  = %date(CustomerLedgerEntries(w_tell)
                    .Due_Date:*ISO);
                 b1ffda  = %dec(%char(w_date:*DMY0):6:0);
                 b1binr  = CustomerLedgerEntries(w_tell).Document_No;
                 b1bitx  = CustomerLedgerEntries(w_tell).Description;
                 b1obel  = CustomerLedgerEntries(w_tell).Amount;
                 b1rbel  = CustomerLedgerEntries(w_tell).Remaining_Amount;
                 b1tell  = w_tell;
                 eval      b1valg = 0;
                 write     b1sfl;

               endif;

      /End-free
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   endif
      *
      * Leser tilbake en side
      *
      *
      *
      * Blanker og fyller opp subfile
      *
     c                   eval      w_tell = 0
     c                   eval      w_ssrn = 0
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppkall av notatark
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     notat         begsr
      *
     c                   eval      rxfirm = w_firm
     c                   eval      rxsyst = 'K'
     c                   eval      rxpros = *blank
     c                   eval      rxakti = ' '
     c                   eval      rxkont = rkkund
     c                   eval      rxspes = 0
     c                   eval      rxavde = 0
     c                   eval      rxlinj = *zeros
     c                   eval      rxtext = *blank
     c                   eval      rxvalg = *blank
      *
     c                   call      'RF020R'
     c                   parm                    rxulda
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for visning av reskontro statistikk
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xe2win        begsr
      *
      * Henter kjøp ifjor fra fakturahistorikk
      *
     c                   eval      w_year = *year - 1
      *
     c/exec sql
     c+ declare sok1 cursor for
     c+ select sum(sofsum) as w_fsum1
     c+ from sohepf
     c+ where  soaarr  = :w_year and
     c+        sokund  = :p_kund
     c+ for read only
     c/end-exec
      *
      * Utfør uthenting av poster
      *
     c/exec sql
     c+ open sok1
     c/end-exec
      *
      * Legger resultat i arbeidsfelt
      *
     c/exec sql
     c+ fetch sok1
     c+ into :w_fsum1
     c/end-exec
      *
     c                   eval      e2kjØf = w_fsum1
      *
      * Henter kjøp iår fra fakturahistorikk
      *
     c                   eval      w_year = *year
      *
     c/exec sql
     c+ declare sok2 cursor for
     c+ select sum(sofsum) as w_fsum2
     c+ from sohepf
     c+ where  soaarr  = :w_year and
     c+        sokund  = :p_kund
     c+ for read only
     c/end-exec
      *
      * Utfør uthenting av poster
      *
     c/exec sql
     c+ open sok2
     c/end-exec
      *
      * Legger resultat i arbeidsfelt
      *
     c/exec sql
     c+ fetch sok2
     c+ into :w_fsum2
     c/end-exec
      *
     c                   eval      e2kjØp = w_fsum2
      *
     c                   move      rksald        e2sald
     c                   move      rkgrns        e2grns
      *
      * Hent mva % og beregn memosaldo inkl. mva dersom kunden er avg.pliktig
      *
     c                   if        rkmkod = 1
      *
     c                   eval      w_stal = *blank
     c                   eval      w_mvak = '3'
      *
     c                   call      'RS205R'
     c                   parm                    w_stal
     c                   parm                    w_mvak
     c                   parm                    w_mvtx
     c                   parm                    w_dato
     c                   parm                    w_mvas
     c                   parm                    w_mvat
     c                   parm                    w_mvaf
     c                   parm                    w_bpro
      *
     c                   eval(h)   e2mems = rkmems * w_mvat
     c                   else
     c                   eval      e2mems = rkmems
     c                   endif
      *
      *
     c                   exfmt     e2win
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Henter inn parameter
      *
     c     *entry        plist
     c                   parm                    p_kund
      *
      * Key for les av kundeinformasjon
      *
     c     rkunlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunlr_kund
      *
      * Key for les av opsett tabell
      *
     c     ilnkir_key    klist
     c                   kfld                    w_firm
      *
     c                   eval      w_kund = %char(p_kund)
     c                   eval      w_firm = l_firm
     c                   move      *date         w_dato
      *
      * Kaller program for å requester og svar - Når vi returnerer skal
      * vi enten lese responsefil eller sende feilmelding.
      *
9.xx c**                 if        1=2
      *
     c                   call      'AP060R'
     c                   parm                    w_firm
     c                   parm                    w_kund
     c                   parm                    w_path
     c                   parm                    w_resp
     c                   parm                    w_stat
      *
9.xx c**                 endif
      *
      * Sjekk om innhenting av data gikk bra
9.xx  * PS hardkodes til å lese inn fra eksempelfil
9.xx  *
9.xx c**                 eval      w_stat = 'TRUE '
      *
     c                   if        w_stat = 'FALSE'
      *
     c                   eval      w_ctli = 12
     c                   eval      w_ctpo = 42
     c                   eval      w_mld01 = 'Innhenting av reskontroposter'
     c                   eval      w_mld02 = 'feilet. Kontakt support      '
      *
     c                   call      'AA006C'
     c                   parm                    w_ctli
     c                   parm                    w_ctpo
     c                   parm                    w_mld01
     c                   parm                    w_mld02
      *
7.01 c                   elseif    w_stat = 'TOM  '
7.01  *
7.01 c                   eval      w_ctli = 12
7.01 c                   eval      w_ctpo = 42
7.01 c                   eval      w_mld01 = 'Ingen poster i retur fra D365'
7.01 c                   eval      w_mld02 = '                             '
7.01  *
7.01 c                   call      'AA006C'
7.01 c                   parm                    w_ctli
7.01 c                   parm                    w_ctpo
7.01 c                   parm                    w_mld01
7.01 c                   parm                    w_mld02
7.01  * Da har vi fått returfiler
7.01 c                   else
      *
      * Henter inn opppsett
      *
     c     ilnkir_key    chain     ilnkir
      *
      * Finner aktuell kunde
      *
     c                   eval      rkunlr_kund = p_kund
     c     rkunlr_key    chain     rkunlr
     c                   if        %found
     c                   eval      b2knum = w_kund
     c                   eval      b2knav = rknavn
     c                   eval      b2sald = rksald
     c                   else
     c                   eval      b2knum = w_kund
     c                   eval      b2knav = 'Kunde ikke funnet'
     c                   endif
      * Hent inn informasjon om memosaldo
     c     rkunlr_key    chain     rkmel1                             61
      *
     c                   eval      *in22 = *on
     c**                 eval      jsonData = %trim(w_resp)
     c                   eval      jsonData = %trim(%trim(w_path) +
     c                                        %trim(w_resp))
      *
      * Leser JSON og fyller opp subfile -
      *
      /Free
       //         data-into Kundeposter %Data(jsonData:

       //    jsonData = '/home/adbg0t/d365/D365_100002.json';

                  options  = 'doc=file +
                              case=any +
                              allowextra=yes +
                              path=JsonRoot/CustomerLedgerEntries';

          //  specify root name - "JsonRoot" is any name you like
               data-into CustomerLedgerEntries %Data( jsonData : options)
                                        %Parser( 'YAJL/YAJLINTO'
                                      : '{"document_name": "JsonRoot"}');

      /End-free
      *
      * Sletter responsen når vi er sikre på at dette funger som det skal.
      * Inntill videre får den leve videre.
9.xx c                   callp     unlink(jsonData)
      *
     c                   endif
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
