     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLOGI
      * Program . . . . : MM702R
      * Beskrivelse . . : Pr. varenummer hentes salg fra salgsstatistikk
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       251104 bø   Nytt program
5.50  *       281106 bø   Justert les av varesortiment
5.50  *       150307 bø   hvis abc-kode er blank - tas den ikke med
5.50  *       160307 bø   Hvis vare er utgått skal den ikke med
      *       310507 bø   Utvidet b.punkt, m.meng,mini til 3 desimaler
      *       310507 bø   Hvis stat.og lag.forskj.- omberegn til lag.enhet
      *       050607 bø   Legger også ut antall i stat.enhet
      *       061207 bø   Feilretting vedr. antall /stat.antall
5.51  *       050508 tsv  Feilretting les pr leverandør.
5.52  *       060508 tsv  Mulig justering 2-9 uker eldre på trend
5.60  *       070806 bof  Muliggjøre å ta abc-kode fra lager
5.61  *       130806 bof  Feilretting vedr. pr. leverandør - parm.rekkefølge
5.62  *       020409 bof  Rydde i prognose pr.vare/lage fram i tid
5.64  *       170609 bof  Justert les av parameter
5.70  *  PRGR 021208 bof  Utvidet med prisgruppe
      * 6.10  220609 BSA  Oppdatert med sporingsinformasjon
6.11  *       260112 bof  Fjernet test mot vvkabc
6.12  *       260413 bof  Feil vedr. vsort. og les av leverandør
6.20  * 6.20  021110 bof  Utvidet med utg.dato pr. lager
6.21  * 6.20  071110 bof  Utvidet med lager på sortiment - pr. nå alltid 0
6.22  * 6.20  050814 bof  Test mot vlkabc på lager, justering
8.01  *PRG2   220622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner - COMP
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsstali    if   e           k disk    rename(sstapfr:sstalir)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvarlr    if   e           k disk    rename(vvarpfr:vvarlrr)
     fvvarl8    if   e           k disk    rename(vvarpfr:vvarl8r)
     fvvarla    if   e           k disk    rename(vvarpfr:vvarlar)
     fvverlr    if   e           k disk    rename(vverpfr:vverlrr)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     fvsdtl6    if   e           k disk    rename(vsdtpfr:vsdtl6r)
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     fmmatlr    if   e           k disk    rename(mmatpfr:mmatlrr)
     fmprulr    if   e           k disk    rename(mprupfr:mprulrr)
     fmprulu    uf a e           k disk    rename(mprupfr:mprulur)
      *
      *
     d mmatlr_vsor     s                   like(mmvsor)
     d mmatlr_ogrp     s                   like(mmogrp)
     d mmatlr_hgrp     s                   like(mmhgrp)
     d mmatlr_ugrp     s                   like(mmugrp)
     d mmatlr_ldor     s                   like(mmldor)
     d mmatlr_vare     s                   like(mmvare)
     d mmatlr_crdo     s                   like(mmcrdo)
     d mprulu_aar      s                   like(mraar)
     d mprulu_uke      s                   like(mruke)
     d mprulu_lage     s                   like(mrlage)
     d mprulu_vare     s                   like(mrvare)
     d mprulr_aar      s                   like(mraar)
     d mprulr_uke      s                   like(mruke)
     d mprulr_lage     s                   like(mrlage)
     d mprulr_vare     s                   like(mrvare)
     d vlagl1_vare     s                   like(vlvare)
     d vvarl8_ogrp     s                   like(vvogrp)
     d vvarl8_hgrp     s                   like(vvhgrp)
     d vvarl8_ugrp     s                   like(vvugrp)
     d vvarla_ldor     s                   like(vvldor)
     d vvarlx_ogrp     s                   like(vvogrp)
     d vvarlx_hgrp     s                   like(vvhgrp)
     d vvarly_ogrp     s                   like(vvogrp)
     d vvarlr_vare     s                   like(vvvare)
     d vverlr_envr     s                   like(vvenvr)
     d vsdtl6_vsor     s                   like(vdvsor)
     d vvenl1_vare     s                   like(vevare)
      *
      * Parameter-inn
     d p_dato          s               d   datfmt(*iso)
     d p_antu          s              3  0
      *
      * Local Data Area
      *
     d                uds
      *
6.10 d l_user                911    920
     d l_firm                944    946  0
      *
     d                 ds
     d d_list                       128
     d  d_vsor                       10    overlay(d_list)
     d  d_ogrp                        2  0 overlay(d_list:11)
     d  d_hgrp                        2  0 overlay(d_list:13)
     d  d_ugrp                        3  0 overlay(d_list:15)
     d  d_ldor                        6  0 overlay(d_list:18)
     d  d_vare                        8  0 overlay(d_list:24)
     d  d_crdo                        1  0 overlay(d_list:32)
     d  d_dato                        6  0 overlay(d_list:33)
     d  d_antu                        2  0 overlay(d_list:39)
      *
     d d_lagr          ds
     d  d_la01                        2  0
     d  d_la02                        2  0
     d  d_la03                        2  0
     d  d_la04                        2  0
     d  d_la05                        2  0
     d  d_la06                        2  0
     d  d_la07                        2  0
     d  d_la08                        2  0
     d  d_la09                        2  0
     d  d_la10                        2  0
      *
      *
      * Definering av variable i nøkler
      *
     d sstali_puke     s                   like(sfpuke)
     d sstali_paar     s                   like(sfpaar)
     d sstali_vare     s                   like(sfvare)
     d sstali_lage     s                   like(sflage)
      *
      * Definering av variable
     d ar              s             11  3 DIM(100)
     d ar_st           s             11  3 DIM(100)
     d a_uke           s              2  0 DIM(100)
     d a_aar           s              4  0 DIM(100)
     d a_ukeg          s              2  0 DIM(100)
     d a_aarg          s              4  0 DIM(100)
     d i               s              3  0
     d j               s              3  0
     d y               s              3  0
     d g               s              3  0
     d h               s              3  0
      *
     d w_dato          s               d   datfmt(*iso)
     d w_pdat          s               d   datfmt(*iso)
     d w_ddat          s               d   datfmt(*iso)
6.20 d w_udat          s               d   datfmt(*iso)
     d w_paar          s              4  0
     d w_dat60         s              6  0
     d w_puke          s              2  0
     d w_psts          s              1
     d w_firm          s                   like(sffirm)
     d w_ant           s              9  0
     d w_antp          s             11  3
     d w_snitt         s             11  3
     d w_tot           s                   like(sfanta)
     d w_tot_st        s                   like(sfanta)
     d w_beho          s                   like(sfanta)
     d w_beho_st       s                   like(sfanta)
     d w_salg          s                   like(sfanta)
     d w_salg_st       s                   like(sfanta)
     d w_salg1         s                   like(sfanta)
     d w_salg2         s                   like(sfanta)
     d w_vare          s                   like(vvvare)
     d w_stat          s              1
     d w_uaar          s              4  0
     d w_uke           s              2  0
     d w_dgr           s              5  0
     d w_date          s               d   datfmt(*iso)
     d w_aar           s              4  0
     d w_taar          s              4  0
     d w_tuke          s              2  0
     d w_tilau         s              6
     d w_tilaug        s              6
     d w_gpro          s             11  2
     d w_115           s             11  5
     d w_112           s             11  2
     d w_uant          s                   like(mruant)
     d w_glpr          s                   like(mrglpr)
     d w_faar          s              4  0
     d w_fuke          s              2  0
     d w_list          s            128
     d w_ogrp          s                   like(mrogrp)
     d w_hgrp          s                   like(mrhgrp)
     d w_ugrp          s                   like(mrugrp)
     d w_ldor          s                   like(mrldor)
     d w_omrl          s                   like(veomre)
     d w_omrs          s                   like(veomre)
5.60 d w_kabc          s                   like(mrkabc)
5.70 d w_vtyp          s                   like(vvvtyp)
      *
     d b_ok            s              1    inz(*off)
     d b_matr          s              1    inz(*off)
     d b_stat          s              1    inz(*off)
6.20 d b_inlr          s              1    inz(*off)
      *
     d w_aaruk         ds
     d sfpaar                         4  0
     d sfpuke                         2  0
      *
     d w_aarukg        ds
     d mraar                          4  0
     d mruke                          2  0
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * sletter avviksregister
     c                   call      'MM403R '
      *
     c                   exsr      ukeaar
      *
     c                   if        b_ok = *off
     c                   goto      avslutt
     c                   endif
      *
     c                   if        d_vsor <> *blank or
     c                             d_ogrp <> 0 or
     c                             d_hgrp <> 0 or
     c                             d_ugrp <> 0 or
     c                             d_vare <> 0 or
5.51 c                             d_ldor <> 0
     c                   exsr      les_utvalg
     c                   else
      *
     c                   read      vvarl1
     c                   dow       not %eof
      *
     c                   exsr      beh_vare
      *
     c                   read      vvarl1
     c                   enddo
      *
     c                   endif
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
      *
     c                   return
      * --------------------------------------------------------------
      *  leser utvalg av varer
      * --------------------------------------------------------------
      *
     c     les_utvalg    begsr
      *
      *
     c                   if        d_vare <> 0
     c                   exsr      les_vare
     c                   goto      end_utvalg
     c                   endif
     c                   if        d_vsor <> *blank
     c                   exsr      les_vsor
     c                   goto      end_utvalg
     c                   endif
      *
     c                   if        d_ugrp <> 0
     c                   eval      w_ogrp = d_ogrp
     c                   eval      w_hgrp = d_hgrp
     c                   eval      w_ugrp = d_ugrp
     c                   exsr      les_ugr
     c                   goto      end_utvalg
     c                   endif
      *
     c                   if        d_hgrp <> 0
     c                   eval      w_ogrp = d_ogrp
     c                   eval      w_hgrp = d_hgrp
     c                   exsr      les_hgr
     c                   goto      end_utvalg
     c                   endif
      *
     c                   if        d_ogrp <> 0
     c                   eval      w_ogrp = d_ogrp
     c                   exsr      les_ogr
     c                   goto      end_utvalg
     c                   endif
      *
5.51 c                   if        d_ldor <> 0
 .   c                   eval      w_ldor = d_ldor
 .   c                   exsr      les_ldor
 .   c                   goto      end_utvalg
5.51 c                   endif
      *
     c     end_utvalg    endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Leser tom leverandør på vareregister kaller oppdatering av lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_vare      begsr
      *
     c                   movel     d_vare        vvarlr_vare
     c     vvarlr_key    chain     vvarlr
     c                   if        %found(vvarlr)
     c                   exsr      beh_vare
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Leser varesortiment
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_vsor      begsr
      *
     c                   eval      vsdtl6_vsor = d_vsor
     c     vsdtl6_key    setll     vsdtl6
     c     vsdtl6_key    reade     vsdtl6
     c                   dow       not %eof(vsdtl6)
      *
     c                   select
     c                   when      vdvare <> *blank
     c                   exsr      beh_vare
     c                   eval      vvarlr_vare = vdvare
     c     vvarlr_key    chain     vvarlr
     c                   if        %found(vvarlr)
     c                   exsr      beh_vare
     c                   endif
      *
     c                   when      vdugrp <> 0
     c                   eval      w_ogrp = vdogrp
     c                   eval      w_hgrp = vdhgrp
     c                   eval      w_ugrp = vdugrp
     c                   exsr      les_ugr
     c                   when      vdhgrp <> 0
     c                   eval      w_ogrp = vdogrp
     c                   eval      w_hgrp = vdhgrp
     c                   exsr      les_hgr
     c                   when      vdogrp <> 0
     c                   eval      w_ogrp = vdogrp
     c                   exsr      les_ogr
     c                   when      vdldor <> 0
     c                   eval      w_ldor = vdldor
6.12 c                   exsr      les_ldor
     c                   endsl
      *
     c     vsdtl6_key    reade     vsdtl6
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Leser tom ugr på vareregister kaller behandling av vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_ugr       begsr
      *
     c                   eval      vvarl8_ogrp = w_ogrp
     c                   eval      vvarl8_hgrp = w_hgrp
     c                   eval      vvarl8_ugrp = w_ugrp
     c     vvarl8_key    setll     vvarl8
     c     vvarl8_key    reade     vvarl8
     c                   dow       not %eof(vvarl8)
     c                   exsr      beh_vare
     c     vvarl8_key    reade     vvarl8
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Leser tom hgr på vareregister kaller behandling av vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_hgr       begsr
      *
     c                   eval      vvarlx_ogrp = w_ogrp
     c                   eval      vvarlx_hgrp = w_hgrp
     c     vvarlx_key    setll     vvarl8
     c     vvarlx_key    reade     vvarl8
     c                   dow       not %eof(vvarl8)
     c                   exsr      beh_vare
     c     vvarlx_key    reade     vvarl8
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Leser tom ogr på vareregister kaller oppdatering av lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_ogr       begsr
      *
     c                   eval      vvarly_ogrp = w_ogrp
     c     vvarly_key    setll     vvarl8
     c     vvarly_key    reade     vvarl8
     c                   dow       not %eof(vvarl8)
     c                   exsr      beh_vare
     c     vvarly_key    reade     vvarl8
     c                   enddo
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Leser varer pr. leverandør - behandling av vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_ldor      begsr
      *
     c                   eval      vvarla_ldor = w_ldor
     c     vvarla_key    setll     vvarla
     c     vvarla_key    reade     vvarla
     c                   dow       not %eof(vvarla)
     c                   exsr      beh_vare
     c     vvarla_key    reade     vvarla
     c                   enddo
      *
     c                   endsr
      *
      *
      *
      * --------------------------------------------------------------
      *  behandler lest vare
      * --------------------------------------------------------------
      *
     c     beh_vare      begsr
      *
     c                   exsr      rec_ktrl
     c                   if        b_ok   = *on
      *
      *  hvis lagerenhet og salgsenhet er forskj. hentes omregningsfakt
     c                   eval      w_omrs = 0
     c                   eval      w_omrl = 0
     c                   if        vvenhl <> vvenhs
     c                   exsr      hent_omr
     c                   endif
      *
      *
     c                   exsr      les_parm
     c                   if        mmlagr   <> 0
     c                   move      mmlagr        d_lagr
     c                   endif
      *
     c                   if        b_ok   = *on and
     c                             b_matr = *on
      *
      * Sjekk om riktig leverandør - hvis leverandør er valgt
      *
     c                   if        mmldor <> *zero
     c                             and mmldor <> vvldor
     c                   leavesr
     c                   endif
      *
      * behandler for hvert lager
     c                   eval      vlagl1_vare = vvvare
     c     vlagl1_key    setll     vlagl1
     c     vlagl1_key    reade     vlagl1
     c                   dow       not %eof(vlagl1)
5.65 c                   if        vllage > 0
5.62 c                   exsr      rydd_prognose
5.60  * hvis lager har abc-kode brukes denne
6.22 c*                  if        vlkabc <> *blank
6.22 c*                  eval      w_kabc = vlkabc
6.22 c*                  endif
6.22  * hvis abckode ikke ligger på lager så skal det ikke dannes progn
6.22 c                   if        vlkabc = *blank
6.22 c                   goto      nxt_vlag
6.22 c                   endif
6.22 c                   eval      w_kabc = vlkabc
6.20  *   Hent utg.dato fra subprogram
6.20 c                   call      'VL720R'
6.20 c                   parm                    w_firm
6.20 c                   parm                    vvvare
6.20 c                   parm                    vllage
6.20 c                   parm                    w_udat
6.20 c                   parm                    b_inlr
6.20  *
6.20 c                   if        w_udat <> *loval and
6.20 c                             w_udat < w_ddat
6.20 c                   goto      nxt_vlag
6.20 c                   endif
      *
      * Sjekk først om mange lager er utfylt
      *
     c                   if        mmlagr   <> 0
     c                   if        vllage <> d_la01 and
     c                             vllage <> d_la02 and
     c                             vllage <> d_la03 and
     c                             vllage <> d_la04 and
     c                             vllage <> d_la05 and
     c                             vllage <> d_la06 and
     c                             vllage <> d_la07 and
     c                             vllage <> d_la08 and
     c                             vllage <> d_la09 and
     c                             vllage <> d_la10 and
     c                             vllage <> mmlage
     c                   goto      nxt_vlag
     c                   endif
     c                   else
     c                   if        mmlage  <> 0 and
     c                             vllage <> mmlage
     c                   goto      nxt_vlag
     c                   endif
     c                   endif
      *
      * sjekker varetype her
5.70  *
5.70 c                   if        vlvtyp <> 'L' and
5.70 c                             vlvtyp <> 'S'
5.70 c                   goto      nxt_vlag
5.70 c                   endif
      *
      *
      * beregner glatnings%
5.52 c*                  if        mmbrku = 1
5.52 c                   if        mmbrku > 0
     c                   exsr      glatp
     c                   else
     c                   eval      w_gpro = 0
     c                   endif
      *
      * finner salg pr. uke
     c                   eval      j = 1
     c                   dow       j < 100
     c                   eval      w_uke  = a_uke(j)
     c                   eval      w_uaar = a_aar(j)
     c                   if        w_uke  <> 0 and
     c                             w_uaar <> 0
     c                   exsr      finn_salg
     c                   endif
     c                   eval      j = j + 1
     c                   enddo
      *
     c                   exsr      finn_gl_ant
      *
5.65 c                   endif
      *
     c     nxt_vlag      tag
     c     vlagl1_key    reade     vlagl1
     c                   enddo
      *
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for kontroll mot parameter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     rec_ktrl      begsr
      *
     c                   eval      b_ok = *off
      *
     c                   if        vvfirm <> l_firm
     c                   leavesr
     c                   endif
5.60  * hvis abc-kode på vare er blank - tas den ikke med
5.60  * abc-kode for lager kan også benyttes                           re
6.11 c*                  if        vvkabc = *blank
6.11 c*                  leavesr
6.11 c*                  endif
6.11 c*                  eval      w_kabc = vvkabc
      * hvis utgått dato er passert skal den ikke med
6.20  * sjekker dette når man vet hvilket lager
6.20 c*                  if        vvudat <> *loval and
6.20 c*                            vvudat < w_ddat
6.20 c*                  leavesr
6.20 c*                  endif
      *
5.70 c*                  if        vvvtyp <> 'L' and
5.70 c*                            vvvtyp <> 'S'
5.70 c*                  leavesr
5.70 c*                  endif
      * varegruppe
     c                   if        d_ogrp <> 0 and
     c                             vvogrp <> d_ogrp
     c                   leavesr
     c                   endif
     c                   if        d_hgrp <> 0 and
     c                             vvhgrp <> d_hgrp
     c                   leavesr
     c                   endif
     c                   if        d_ugrp <> 0 and
     c                             vvugrp <> d_ugrp
     c                   leavesr
     c                   endif
      *
      * Sett OK-indikator - record skal være med
      *
     c                   eval      b_ok = *on
      *
     c     end_ktrl      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for les av parameter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_parm      begsr
      *
     c                   eval      b_matr = *off
      *
      *  vare  - crossdocket
     c                   eval      mmatlr_vsor  = *blank
     c                   eval      mmatlr_ogrp  = 0
     c                   eval      mmatlr_hgrp  = 0
     c                   eval      mmatlr_ugrp  = 0
     c                   eval      mmatlr_ldor  = 0
     c                   eval      mmatlr_vare  = vvvare
     c                   eval      mmatlr_crdo  = d_crdo
     c     mmatlr_key    chain     mmatlr
     c                   if        %found and
     c                             mmakti = 0
     c                   eval      b_matr   = *on
     c                   leavesr
     c                   endif
      *  vare  -
     c                   eval      mmatlr_vsor  = *blank
     c                   eval      mmatlr_ogrp  = 0
     c                   eval      mmatlr_hgrp  = 0
     c                   eval      mmatlr_ugrp  = 0
     c                   eval      mmatlr_ldor  = 0
     c                   eval      mmatlr_vare  = vvvare
     c                   eval      mmatlr_crdo  = 0
     c     mmatlr_key    chain     mmatlr
     c                   if        %found and
     c                             mmakti = 0
     c                   eval      b_matr   = *on
     c                   leavesr
     c                   endif
      *varesortiment - crossdocket
     c                   if        d_vsor <> *blank
     c                   eval      mmatlr_vsor  = d_vsor
     c                   eval      mmatlr_vare  = *blank
     c                   eval      mmatlr_crdo  = d_crdo
     c     mmatlr_key    chain     mmatlr
     c                   if        %found  and
     c                             mmakti  = 0
     c                   eval      b_matr  = *on
     c                   leavesr
     c                   endif
     c                   endif
      *varesortiment -
     c                   if        d_vsor <> *blank
     c                   eval      mmatlr_crdo  = 0
     c     mmatlr_key    chain     mmatlr
     c                   if        %found  and
     c                             mmakti  = 0
     c                   eval      b_matr  = *on
     c                   leavesr
     c                   endif
     c                   endif
5.64  * bare leverandør
5.64 c*                  if        d_ldor <> 0 and
5.64 c*                            d_ogrp =  0 and
5.64 c*                            d_hgrp =  0 and
5.64 c*                            d_ugrp =  0
5.64 c*                  eval      mmatlr_vare  = *blank
5.64 c*                  eval      mmatlr_vsor  = *blank
5.64 c*                  eval      mmatlr_crdo  = 0
5.64 c*                  eval      mmatlr_ldor  = d_ldor
5.64 c*    mmatlr_key    chain     mmatlr
5.64 c*                  if        %found  and
5.64 c*                            mmakti  = 0
5.64 c*                  eval      b_matr  = *on
5.64 c*                  leavesr
5.64 c*                  endif
5.64 c*                  endif
      *
      *  leverandør  m /varegrupper
5.64 c                   if        vvldor <> 0
     c                   eval      mmatlr_vsor  = *blank
     c                   eval      mmatlr_ogrp  = vvogrp
     c                   eval      mmatlr_hgrp  = vvhgrp
     c                   eval      mmatlr_ugrp  = vvugrp
     c                   eval      mmatlr_ldor  = vvldor
     c                   eval      mmatlr_vare  = *blank
     c     mmatlr_key    chain     mmatlr
     c                   if        %found  and
     c                             mmakti  = 0
     c                   eval      b_matr   = *on
     c                   leavesr
     c                   endif
      *
     c                   eval      mmatlr_ugrp  = 0
     c     mmatlr_key    chain     mmatlr
     c                   if        %found  and
     c                             mmakti  = 0
     c                   eval      b_matr   = *on
     c                   leavesr
     c                   endif
      *
     c                   eval      mmatlr_hgrp  = 0
     c     mmatlr_key    chain     mmatlr
     c                   if        %found  and
     c                             mmakti  = 0
     c                   eval      b_matr  = *on
     c                   leavesr
     c                   endif
      * bare leverandør
     c                   eval      mmatlr_ogrp  = 0
     c     mmatlr_key    chain     mmatlr
     c                   if        %found  and
     c                             mmakti  = 0
     c                   eval      b_matr  = *on
     c                   leavesr
     c                   endif
      *
     c                   endif
      *
      * Bare varegrupper
     c                   eval      mmatlr_vsor  = *blank
     c                   eval      mmatlr_ogrp  = vvogrp
     c                   eval      mmatlr_hgrp  = vvhgrp
     c                   eval      mmatlr_ugrp  = vvugrp
     c                   eval      mmatlr_ldor  = 0
     c                   eval      mmatlr_vare  = *blank
     c     mmatlr_key    chain     mmatlr
     c                   if        %found  and
     c                             mmakti  = 0
     c                   eval      b_matr   = *on
     c                   leavesr
     c                   endif
      *
     c                   eval      mmatlr_ugrp  = 0
     c     mmatlr_key    chain     mmatlr
     c                   if        %found  and
     c                             mmakti  = 0
     c                   eval      b_matr  = *on
     c                   leavesr
     c                   endif
      *
     c                   eval      mmatlr_hgrp  = 0
     c     mmatlr_key    chain     mmatlr
     c                   if        %found  and
     c                             mmakti  = 0
     c                   eval      b_matr  = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finne statistikkinformasjon salg
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_salg     begsr
     c                   eval      b_stat = *off
     c                   eval      i = 1
     c                   eval      w_tot     = 0
     c                   eval      w_tot_st  = 0
     c                   eval      w_ant     = 0
      *
     c                   dow       i <= mmantp
      *
     c                   eval      w_aar  = w_uaar - i
     c                   eval      w_salg = 0
     c                   eval      w_salg_st = 0
     c                   eval      w_vare = vvvare
     c                   exsr      les_stat
     c                   exsr      sjekk_erstv
     c                   eval      ar(i) = w_salg
     c                   eval      ar_st(i) = w_salg_st
     c                   eval      i = i + 1
      *
     c                   enddo
      *
      * Beregner gjennomsnitt - hvis statistikk er funnet
      *
     c                   if        b_stat = *on
      *
     c                   eval      i     = 1
     c                   eval      w_ant = 0
     c                   dow       i < 100
     c                   if        ar(i) <> 0
     c                   eval      w_ant    = w_ant + 1
     c                   eval      w_tot    = w_tot + ar(i)
     c                   eval      w_tot_st = w_tot_st + ar_st(i)
     c                   endif
     c                   eval      i = i + 1
     c                   enddo
      * alle år som har verdi tas med i beregning.
     c                   if        w_ant > 0
     c                   eval(h)   w_beho    = w_tot / w_ant
     c                   eval(h)   w_beho_st = w_tot_st / w_ant
     c                   else
     c                   eval      w_beho    = 0
     c                   eval      w_beho_st = 0
     c                   endif
      *
      * justerer for trend-prosent
     c                   eval      w_uant = w_beho
     c                   eval      w_glpr = w_gpro
     c                   if        w_gpro <> 0
     c                   eval(h)   w_112    = w_beho  * w_gpro / 100
     c                   eval      w_beho   = w_beho + w_112
     c                   eval(h)   w_112    = w_beho_st  * w_gpro / 100
     c                   eval      w_beho_st  = w_beho_st + w_112
     c                   endif
      *
      *
      * oppdaterer ukeprognose
      *
     c                   clear                   mprulur
     c                   eval      mprulu_vare = vvvare
     c                   eval      mprulu_lage = vllage
     c                   eval      mprulu_aar  = w_uaar
     c                   eval      mprulu_uke  = w_uke
     c     mprulu_key    chain     mprulu
     c                   eval      mrantp = w_beho
     c                   eval      mrantt = w_beho_st
     c                   eval      mruant = w_uant
     c                   eval      mrglpr = w_glpr
5.60 c                   eval      mrkabc = w_kabc
     c                   eval      mrvsor = mmvsor
     c                   eval      mrogrp = mmogrp
     c                   eval      mrhgrp = mmhgrp
     c                   eval      mrugrp = mmugrp
     c                   eval      mrldor = mmldor
     c                   movel     mmvare        mrvark
     c                   eval      mrcrdo = mmcrdo
     c                   if        %found(mprulu)
6.10 c                   time                    mredat
6.10 c                   time                    mretim
6.10 c                   eval      mreusr = l_user
     c                   update    mprulur
     c                   else
     c                   eval      mrfirm = l_firm
     c                   eval      mrvare = mprulu_vare
     c                   eval      mrlage = mprulu_lage
     c                   eval      mraar  = mprulu_aar
     c                   eval      mruke  = mprulu_uke
6.10 c                   time                    mrodat
6.10 c                   time                    mrotim
6.10 c                   eval      mruser = l_user
6.10 c                   time                    mredat
6.10 c                   time                    mretim
6.10 c                   eval      mreusr = l_user
     c                   write     mprulur
     c                   endif
     c                   endif
      *
     c                   endsr
      *c
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finne statistikkinformasjon salg
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_stat      begsr
      *
      * Les statistikkposter
      *
     c                   eval      sstali_vare = w_vare
     c                   eval      sstali_lage = vllage
     c                   eval      sstali_paar = w_aar
     c                   eval      sstali_puke = w_uke
     c     sstali_key    setll     sstali
     c     sstali_key    reade     sstali
     c                   dow       not %eof(sstali)
      *
     c                   if        sflety <> mmlt01 and
     c                             sflety <> mmlt02 and
     c                             sflety <> mmlt03 and
     c                             sflety <> mmlt04 and
     c                             sflety <> mmlt05 and
     c                             sflety <> mmlt06 and
     c                             sflety <> mmlt07 and
     c                             sflety <> mmlt08 and
     c                             sflety <> mmlt09 and
     c                             sflety <> mmlt10
     c                   goto      nxt_ssta
     c                   endif
      *
     c                   if        sflage =  vllage
      * akk. alltid salg i stat.enhet- i tillegg til evt. omregn.antall
     c                   eval(h)   w_salg_st = w_salg_st + sfanta
     c                   if        vvenhs <> vvenhl  and
     c                             w_omrl <> 0 and
     c                             w_omrs <> 0
     c                   eval      sfanta = sfanta * w_omrs / w_omrl
     c                   endif
     c                   eval(h)   w_salg = w_salg + sfanta
     c                   eval      b_stat = *on
     c                   endif
      *
     c     nxt_ssta      tag
     c     sstali_key    reade     sstali
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finne trendfaktoren
      *  sammenligner et gitt antall uker i år og i fjor
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     glatp         begsr
      *
     c                   eval      w_salg1 = 0
     c                   eval      w_salg2 = 0
     c                   eval      w_gpro  = 0
      * finner til uke / år
     c     *dmy          move      UDATE         w_dato
      *
5.52  * Justerer ukenr/år hvis det er ønsket
 .   c                   if        mmbrku > 1
 .   c                   eval      w_ant = 7 * mmbrku
 .   c                   subdur    w_ant:*D      w_dato
 .   c                   endif
5.52  *
      *
     c                   eval      w_pdat = w_dato
     c                   call      'AX711R  '
     c                   parm                    w_pdat
     c                   parm                    w_paar
     c                   parm                    w_puke
     c                   parm                    w_psts
      *
     c                   if        w_psts = *blank
     c                   eval      w_taar   = w_paar
     c                   eval      w_tuke   = w_puke
     c                   movel     w_taar        w_tilau
     c                   move      w_tuke        w_tilau
     c
      * finner startdato i år  + fra uke år
     c     *dmy          move      UDATE         w_dato
     c                   eval      w_ant = 7 * mmantu
      *
5.52  * Justerer ukenr/år hvis det er ønsket
 .   c                   if        mmbrku > 1
 .   c                   eval      w_ant = w_ant + (7 * mmbrku)
 .   c                   endif
5.52  *
      *
     c                   subdur    w_ant:*D      w_dato
     c                   eval      w_pdat = w_dato
     c                   call      'AX711R  '
     c                   parm                    w_pdat
     c                   parm                    w_paar
     c                   parm                    w_puke
     c                   parm                    w_psts
      *
     c                   if        w_psts = *blank
     c                   eval      w_faar   = w_paar
     c                   eval      w_fuke   = w_puke
     c                   endif
      *                            w_ldor
      * Les statistikkposter for i år
      *
     c                   eval      sstali_vare = vvvare
     c                   eval      sstali_lage = vllage
     c                   eval      sstali_paar = w_faar
     c                   eval      sstali_puke = w_fuke
     c     sstali_key    setll     sstali
     c                   read      sstali
     c                   dou       %eof(sstali)
      *
     c                   if        sfvare <> vvvare or
     c                             sflage <> vllage or
     c                             w_aaruk > w_tilau
     c                   leave
     c                   endif
      *
     c                   if        sflety = mmlt01 or
     c                             sflety = mmlt02 or
     c                             sflety = mmlt03 or
     c                             sflety = mmlt04 or
     c                             sflety = mmlt05 or
     c                             sflety = mmlt06 or
     c                             sflety = mmlt07 or
     c                             sflety = mmlt08 or
     c                             sflety = mmlt09 or
     c                             sflety = mmlt10
      *
     c                   eval(h)   w_salg1 = w_salg1 + sfanta
     c                   eval      b_stat = *on
     c                   endif
      *
     c                   read      sstali
     c                   enddo
      *
      * finner for samme uker et år tilbake
      *
     c                   eval      w_taar = w_taar - 1
     c                   movel     w_taar        w_tilau
     c                   move      w_tuke        w_tilau
      *
     c                   eval      sstali_vare = vvvare
     c                   eval      sstali_lage = vllage
     c                   eval      sstali_paar = w_faar - 1
     c                   eval      sstali_puke = w_fuke
     c     sstali_key    setll     sstali
     c                   read      sstali
     c                   dou       %eof(sstali)
      *
     c                   if        sfvare <> vvvare or
     c                             sflage <> vllage or
     c                             w_aaruk > w_tilau
     c                   leave
     c                   endif
      *
     c                   if        sflety = mmlt01 or
     c                             sflety = mmlt02 or
     c                             sflety = mmlt03 or
     c                             sflety = mmlt04 or
     c                             sflety = mmlt05 or
     c                             sflety = mmlt06 or
     c                             sflety = mmlt07 or
     c                             sflety = mmlt08 or
     c                             sflety = mmlt09 or
     c                             sflety = mmlt10
     c                   eval(h)   w_salg2 = w_salg2 + sfanta
     c                   eval      b_stat = *on
     c                   endif
      *
     c                   read      sstali
     c                   enddo
      *
     c                   endif
      *
      * beregner glatnings%
      * salg1 = årets, salg2 = fjorårets
      * glat% = salg1 - salg2 / salg2
      *
     c                   if        w_salg1 <> 0 and
     c                             w_salg2 <> 0
     c                   eval(h)   w_115   = (w_salg1 - w_salg2) / w_salg2
     c                   eval      w_gpro  = w_115 * 100
     c                   if        w_gpro  > 100
     c                   eval      w_gpro  = 100
     c                   endif
     c                   else
     c                   eval      w_gpro  = 0
     c                   endif
     c
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finn hvilke uke man skal kjøre prognose for.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ukeaar        begsr
     c                   eval      b_ok = *off
     c     *dmy          move      p_dato        w_dat60
     c     *dmy          test (d)                w_dat60                33
     c                   if        *in33 = *on
     c                   goto      end_uke
     c                   endif
      *
     c                   eval      w_dato = p_dato
      *
     c                   eval      i = 1
     c                   dow       i <= p_antu
     c                   adddur    7:*d          w_dato
      *
     c                   eval      w_pdat = w_dato
     c                   call      'AX711R  '
     c                   parm                    w_pdat
     c                   parm                    w_paar
     c                   parm                    w_puke
     c                   parm                    w_psts
      * uke 53 tas ikke med i MPRUPF er uke 53 gjort om til uke 52
      * dette pga. sammenligningsgrunnlag fra år til år.
     c                   if        w_puke <> 53
     c                   if        w_psts = *blank
     c                   eval      a_aar(i) = w_paar
     c                   eval      a_uke(i) = w_puke
     c                   else
     c                   eval      a_aar(i) = 0
     c                   eval      a_uke(i) = 0
     c                   endif
      *
     c                   eval      i = i + 1
     c                   endif
     c                   enddo
     c                   eval      b_ok = *on
      *
     c     end_uke       endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å sjekke om denne varen har erstatningsvare
      *            akkumulerer dette inn i samme akkumulatorer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_erstv   begsr
      *
     c                   eval      vverlr_envr = vvvare
     c     vverlr_key    setll     vverlr
     c     vverlr_key    reade     vverlr
     c                   dow       not %eof(vverlr)
     c                   eval      w_vare = vvegvr
     c                   exsr      les_stat
     c     vverlr_key    reade     vverlr
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finne glattet antall, dvs. gjennomsnittsantall
      *            over et antall (fra parameter) uker.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_gl_ant   begsr
      *
     c                   if        mmantg <> 0
      *
     c                   eval      y = mmantg
     c                   eval      i = 1
      *
     c                   dow       i <= p_antu
     c                   movel     a_aar(y)      w_tilaug
     c                   move      a_uke(y)      w_tilaug
     c*                  eval      g = i
      *
     c                   if        a_aar(y) > 0 and
     c                             a_uke(y) > 0
      *
     c                   eval      w_antp = 0
      *
     c                   eval      mprulr_vare = vvvare
     c                   eval      mprulr_lage = vllage
     c                   eval      mprulr_aar  = a_aar(i)
     c                   eval      mprulr_uke  = a_uke(i)
     c     mprulr_key    setll     mprulr
     c                   read      mprulr
     c                   dou       %eof(mprulr)
      *
     c                   if        mrvare <> vvvare or
     c                             mrlage <> vllage or
     c                             w_aarukg >  w_tilaug
     c                   leave
     c                   endif
      *
     c                   eval      w_antp  = w_antp + mrantp
      *
     c                   read      mprulr
     c                   enddo
      *
      * finner snitt(glattet) for leste uker
      *
     c                   eval(h)   w_snitt = w_antp / mmantg
      *
      *
      * oppdatere glattet antall
     c                   clear                   mprulur
     c                   eval      mprulu_vare = vvvare
     c                   eval      mprulu_lage = vllage
     c                   eval      mprulu_aar  = a_aar(i)
     c                   eval      mprulu_uke  = a_uke(i)
     c     mprulu_key    chain     mprulu
     c                   eval(h)   mrantg = w_snitt
5.60 c                   eval      mrkabc = w_kabc
     c                   eval      mrvsor = mmvsor
     c                   eval      mrogrp = mmogrp
     c                   eval      mrhgrp = mmhgrp
     c                   eval      mrugrp = mmugrp
     c                   eval      mrldor = mmldor
     c                   movel     mmvare        mrvark
     c                   eval      mrcrdo = mmcrdo
     c                   if        %found(mprulu)
6.10 c                   time                    mredat
6.10 c                   time                    mretim
6.10 c                   eval      mreusr = l_user
     c                   update    mprulur
     c                   else
     c                   eval      mrfirm = l_firm
     c                   eval      mrvare = mprulu_vare
     c                   eval      mrlage = mprulu_lage
     c                   eval      mraar  = mprulu_aar
     c                   eval      mruke  = mprulu_uke
6.10 c                   time                    mrodat
6.10 c                   time                    mrotim
6.10 c                   eval      mruser = l_user
6.10 c                   time                    mredat
6.10 c                   time                    mretim
6.10 c                   eval      mreusr = l_user
     c                   write     mprulur
     c                   endif
      *
      * går til neste periode
     c                   endif
     c                   eval      i = i + 1
     c                   eval      y = y + 1
     c                   enddo
      *
      * Finner antall for resten av ukene. 52 - mmantg * X
      * eval
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finne omregningsfaktor
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_omr      begsr
      *
     c                   eval      vvenl1_vare = vvvare
     c     vvenl1_key    setll     vvenl1
     c     vvenl1_key    reade     vvenl1                                 91
     c                   dow       *in91 = *off
     c                   if        vvenhl = veenhe
     c                   eval      w_omrl = veomre
     c                   endif
     c                   if        vvenhs = veenhe
     c                   eval      w_omrs = veomre
     c                   endif
     c     vvenl1_key    reade     vvenl1                                 91
     c                   enddo
      *
     c                   if        w_omrs = 0
     c                   eval      w_omrs = 1
     c                   endif
      *
     c                   if        w_omrl = 0
     c                   eval      w_omrl = 1
     c                   endif
      *
     c                   endsr
      *
5.62  *
5.62  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.62  *  Subrutine for å å rydde i prognose fram i tid pr. vare/lage
5.62  *            Dette for å unngå at gamle tall ligger igjen
5.62  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.62  *
5.62 c     rydd_prognose begsr
5.62  *
5.62  *
5.62 c                   eval      mprulr_vare = vvvare
5.62 c                   eval      mprulr_lage = vllage
5.62 c                   eval      mprulr_aar  = a_aar(1)
5.62 c                   eval      mprulr_uke  = a_uke(1)
5.62 c     mprulr_key    setll     mprulr
5.62 c                   read      mprulr
5.62 c                   dou       %eof(mprulr)
5.62  *
5.62 c                   if        mrvare <> vvvare or
5.62 c                             mrlage <> vllage
5.62 c                   leavesr
5.62 c                   endif
5.62  *
5.62 c                   eval      mprulu_vare = mrvare
5.62 c                   eval      mprulu_lage = mrlage
5.62 c                   eval      mprulu_aar  = mraar
5.62 c                   eval      mprulu_uke  = mruke
5.62 c     mprulu_key    chain     mprulu
5.62 c                   if        %found(mprulu)
5.62 c                   delete    mprulur
5.62 c                   endif
5.62  *
5.62 c                   read      mprulr
5.62 c                   enddo
5.62  *
5.62 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for les av salgsstatistikk
      *
     c     sstali_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sstali_vare
     c                   kfld                    sstali_lage
     c                   kfld                    sstali_paar
     c                   kfld                    sstali_puke
      *
      * Key for les av prognose matrise
      *
     c     mmatlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mmatlr_vsor
     c                   kfld                    mmatlr_ogrp
     c                   kfld                    mmatlr_hgrp
     c                   kfld                    mmatlr_ugrp
     c                   kfld                    mmatlr_ldor
     c                   kfld                    mmatlr_vare
     c                   kfld                    mmatlr_crdo
      *
      * Key for les av lager
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl1_vare
      *
      * Key for å oppdatere ukesprognose
      *
     c     mprulu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mprulu_vare
     c                   kfld                    mprulu_lage
     c                   kfld                    mprulu_aar
     c                   kfld                    mprulu_uke
      *
      * Key for å lese ukesprognose
      *
     c     mprulr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mprulr_vare
     c                   kfld                    mprulr_lage
     c                   kfld                    mprulr_aar
     c                   kfld                    mprulr_uke
      *
      *  Key for les av varesortiment
      *
     c     vsdtl6_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vsdtl6_vsor
      *
      *  Key for les av vareregister
      *
     c     vvarlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlr_vare
      *
      *  Key for les av vareregister
      *
     c     vvarl8_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl8_ogrp
     c                   kfld                    vvarl8_hgrp
     c                   kfld                    vvarl8_ugrp
      *
      *  Key for les av vareregister  pr. lev.
      *
     c     vvarla_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarla_ldor
      *
      *  Key for les av vareregister
      *
     c     vvarlx_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlx_ogrp
     c                   kfld                    vvarlx_hgrp
      *
      *  Key for les av vareregister
      *
     c     vvarly_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarly_ogrp
      *
      * Key for oppslag på erstatningsvare
      *
     c     vverlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vverlr_envr
      *
      * Key for les av vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
      *
      *
     c     *entry        plist
     c                   parm                    w_list
     c                   eval      d_list = w_list
     c
     c                   time                    w_ddat
     c                   eval      w_firm  = l_firm
     c     *dmy          move      d_dato        p_dato
     c                   eval      p_antu = 52
      *
     c                   endsr
