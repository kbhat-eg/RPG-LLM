     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JR819R
      * Beskrivelse . . : Leser fil på recordformat NOBB_INT
      *                   (nobb Integ.dat format)
      *                   og oppretter/oppdaterer priser på eksisterende
      *                   varer i LVR.
      *                   Forutsetter at vare med nobbnr finnes i VA
      *
      *                   I tillegg opprettes vare på ikke nobb-nr 9000-serie
      *
      *
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       060512 bof  Nytt program
6.30  * 6.30  280113 bof  Omskriving i forb. endring pakning VA (fjernet
6.30  * 6.30              jvpppf)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjrvapf    if   e           k disk
      *
     fjrapl1    if   e           k disk    rename(jrappfr:jrapl1r)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fjvarl3    if   e           k disk    rename(jvarpfr:jvarl3r)
     fjvarl8    if   e           k disk    rename(jvarpfr:jvarl8r)
     fjvprl1    if   e           k disk    rename(jvprpfr:jvprl1r)
     fjvprlu    uf a e           k disk    rename(jvprpfr:jvprlur)
     fjpfal6    uf a e           k disk    rename(jpfapfr:jpfal6r)
     fjpfalu    uf a e           k disk    rename(jpfapfr:jpfalur)
     fjvarlu    uf a e           k disk    rename(jvarpfr:jvarlur)
     fjvdtlu    o  a e           k disk    rename(jvdtpfr:jvdtlur)
     fjvpklu    uf a e           k disk    rename(jvpkpfr:jvpklur)
     fjstslu    uf   e           k disk    rename(jstspfr:jstslur)
      *
      * Array
      *
     d a_erec          s            297    dim(99)
     d a_prec          s            297    dim(99)
      *
      *
      * Definering av parametre
      *
     d p_list          s             32
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
      *
      * Definering av parameterliste -inn
      *
     d                 ds
     d d_list                        32
     d  d_firm                        3  0 overlay(d_list)
     d  d_rapp                        2  0 overlay(d_list:4)
     d  d_dato                         d   overlay(d_list:6) datfmt(*iso)
      *
      * Definering av varerecord
      *
     d                 ds
     d d_vrec                       297
     d  d_nobb                        8  0 overlay(d_vrec:3)
     d  d_modn                        8  0 overlay(d_vrec:12)
     d  d_vgrp                        7    overlay(d_vrec:21)
     d   d_ogrp                       2  0 overlay(d_vgrp)
     d   d_hgrp                       2  0 overlay(d_vgrp:3)
     d   d_ugrp                       3  0 overlay(d_vgrp:5)
     d  d_tek1                       35    overlay(d_vrec:30)
     d  d_tek2                       35    overlay(d_vrec:68)
     d  d_mte1                       35    overlay(d_vrec:106)
     d  d_mte2                       35    overlay(d_vrec:144)
     d  d_eann                       13  0 overlay(d_vrec:181)
     d  d_pvar                       15    overlay(d_vrec:196)
     d  d_lvar                       15    overlay(d_vrec:214)
     d  d_ldor                        6  0 overlay(d_vrec:231)
     d  d_prod                        6  0 overlay(d_vrec:238)
     d  d_adat                        6  0 overlay(d_vrec:247)
     d  d_udatx                       6    overlay(d_vrec:256)
     d  d_udat                        6  0 overlay(d_vrec:256)
     d  d_merk                       30    overlay(d_vrec:264)
     d  d_leve_alf                    1    overlay(d_vrec:297)
     d   d_leve                       1  0 overlay(d_leve_alf:1)
      *
      * Definering av pakningsrecord
      *
     d                 ds
     d d_erec                       297
     d  d_pakn                        3    overlay(d_erec:4)
     d  d_enhe                        3    overlay(d_erec:10)
     d  d_bred                        6  0 overlay(d_erec:23)
     d  d_leng                        6  0 overlay(d_erec:30)
     d  d_hoyd                        6  0 overlay(d_erec:37)
     d  d_vekt                        9  0 overlay(d_erec:44)
     d  d_volu                        8  5 overlay(d_erec:54)
     d  d_anta                        8  2 overlay(d_erec:63)
     d  d_eean                       13  0 overlay(d_erec:72)
     d  d_ofak                       10  4 overlay(d_erec:86)
     d  d_edun                       14  0 overlay(d_erec:97)
     d  d_best                        1  0 overlay(d_erec:112)
     d  d_paks                        3    overlay(d_erec:115)
     d  d_antp_alf                    8    overlay(d_erec:120)
     d   d_antp                       8  0 overlay(d_antp_alf:1)
     d  d_psei_alf                    1    overlay(d_erec:129)
     d   d_psei                       1  0 overlay(d_psei_alf:1)
      *
      * Definering av prisrecord
      *
     d                 ds
     d d_prec                       297
     d  d_pris                        9  2 overlay(d_prec:3)
     d  d_gdat                        6  0 overlay(d_prec:15)
     d  d_pldo                        6  0 overlay(d_prec:22)
     d  d_plva                       15    overlay(d_prec:30)
     d  d_ipri                        9  2 overlay(d_prec:47)
     d  d_kpri                        9  2 overlay(d_prec:57)
     d  d_spri                        9  2 overlay(d_prec:67)
     d  d_fdat                        6  0 overlay(d_prec:79)
     d  d_tdat                        6  0 overlay(d_prec:79)
     d  d_valu                        3    overlay(d_prec:86)
     d  d_ppva                       15    overlay(d_prec:91)
     d  d_pean_alf                   13    overlay(d_prec:180)
     d   d_pean                      13  0 overlay(d_pean_alf:1)
      *
      * Definering av variable i nøkler
      *
     d jrapl1_rapp     s                   like(jrrapp)
     d jlevl1_ldor     s                   like(jlldor)
     d jvarl3_nobb     s                   like(jvnobb)
     d jvarl8_ldor     s                   like(jvldor)
     d jvarl8_lvar     s                   like(jvlvar)
     d jvprlu_vare     s                   like(jxvare)
     d jvprlu_ldor     s                   like(jxldor)
     d jvprl1_vare     s                   like(jxvare)
     d jvprl1_ldor     s                   like(jxldor)
     d jpfal6_vare     s                   like(jcvare)
     d jvarlu_vare     s                   like(jvvare)
     d jvpklu_vare     s                   like(jwvare)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d x               s              2  0
     d w_etel          s              2  0
     d w_ptel          s              2  0
     d w_rect          s              1
      *
     d w_pris          s                   like(jxpris)
      *
     d b_inlr          s              3    inz(*off)
     d b_frst          s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter rapportør-informasjon
      *
     c                   eval      jrapl1_rapp = d_rapp
     c     jrapl1_key    chain     jrapl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
      * Henter leverandørinformasjon
      *
     c                   eval      jlevl1_ldor = jrldor
     c     jlevl1_key    chain     jlevl1
      *
     c                   exsr      les_fil
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av varer/priser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_fil       begsr
      *
     c                   eval      b_frst = *on
     c                   eval      w_etel = 0
     c                   eval      w_ptel = 0
      *
     c                   read      jrvapf
     c                   dow       not %eof
      *
     c                   eval      w_rect = %subst(jrvrad:1:1)
      *
     c                   if        w_rect = '1' and
     c                             b_frst = *off
     c                   exsr      behandling
     c                   endif
      *
     c                   select
     c                   when      w_rect = '1'
     c                   eval      d_vrec = jrvrad
     c                   eval      b_frst = *off
     c                   eval      w_etel = 0
     c                   eval      w_ptel = 0
     c                   when      w_rect = '2'
     c                   eval      w_etel = w_etel + 1
     c                   eval      a_erec(w_etel) = jrvrad
     c                   when      w_rect = '3'
     c                   eval      w_ptel = w_ptel + 1
     c                   eval      a_prec(w_ptel) = jrvrad
     c                   endsl
      *
     c     les_neste     tag
      *
     c                   read      jrvapf
     c                   enddo
      *
      * Behandler siste vare-forsendelse
      *
     c                   if        b_frst = *off and
     c                             d_vrec <> *blank
     c                   exsr      behandling
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av vare-forsendelse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
     c                   if        d_nobb <> 0
      *
     c                   if        d_nobb >= 10000000 and
     c                             d_nobb <  90000000
      * nobb-nr
     c                   move      d_nobb        jvarl3_nobb
     c     jvarl3_key    chain     jvarl3
     c                   if        not %found
     c                   leavesr
     c                   endif
      *
     c                   else
      * optimera-vare
      * sjekker lev. varenr
     c                   eval      jvarl8_ldor = jrldor
     c                   eval      jvarl8_lvar = d_lvar
     c     jvarl8_key    chain     jvarl8
     c                   if        not %found(jvarl8)
     c                   exsr      ny_vare
     c                   endif
     c                   exsr      vare
     c                   exsr      pakning
     c                   endif
      *
     c                   eval      d_prec = a_prec(1)
      *
     c                   eval      jvprl1_ldor = jrldor
     c                   eval      jvprl1_vare = jvvare
     c     jvprl1_key    chain     jvprl1
     c                   if        %found
     c                   exsr      endr_pris
     c                   else
     c                   exsr      oppr_pris
     c                   endif
      *
      * Oppdaterer søketekst
      *
     c                   if        jxvare <> *blank
     c                   eval      b_inlr = *on
     c                   call      'JV790R'
     c                   parm                    w_firm
     c                   parm                    jxvare
     c                   parm                    b_inlr
     c                   endif
      *
     c                   endif
      *
     c     end_beh       tag
     c                   eval      w_etel = 0
     c                   eval      w_ptel = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting av pris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppr_pris     begsr
      *
      * Sletter eventuelt eksisterende priser for leverandør på varen
      *
     c                   eval      jvprlu_vare = jvvare
     c                   eval      jvprlu_ldor = jrldor
     c     jvprlu_key    setll     jvprlu
     c     jvprlu_key    reade     jvprlur
     c                   dow       not %eof
     c                   delete    jvprlur
     c     jvprlu_key    reade     jvprlur
     c                   enddo
      *
      * Oppretter pris på vare for leverandør
      *
     c                   clear                   jvprlur
      *
     c                   eval      jxvare = jvvare
     c                   eval      jxldor = jrldor
     c                   eval      jxlvar = d_lvar
     c                   eval      jxpris = d_ipri
     c                   eval      jxgdat = d_dato
     c                   eval      jxtdat = *loval
     c                   eval      jxvalu = *blank
     c                   eval      jxeann = 0
     c                   eval      jxcrdo = 0
     c                   eval      jxrsts = 1
     c                   eval      jxeusr = l_user
      *
     c                   time                    jxodat
     c                   time                    jxedat
     c                   time                    jxetim
      *
     c                   write     jvprlur
      *
     c                   exsr      dann_paslag
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for endring av pris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     endr_pris     begsr
      *
     c                   eval      w_pris = d_ipri
      *
     c                   if        w_pris = jxpris
     c                   leavesr
     c                   endif
      *
     c                   eval      jvprlu_vare = jxvare
     c                   eval      jvprlu_ldor = jrldor
     c     jvprlu_key    chain     jvprlur
      *
     c                   eval      jxlvar = jvvare
     c                   eval      jxpris = w_pris
     c                   eval      jxgdat = d_dato
     c                   eval      jxtdat = *loval
     c                   eval      jxeann = 0
     c                   eval      jxcrdo = 0
     c                   eval      jxeusr = l_user
      *
     c                   time                    jxedat
     c                   time                    jxetim
      *
     c                   update    jvprlur
      *
     c                   exsr      dann_paslag
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å danne påslag
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dann_paslag   begsr
      *
      * Sletter påslag
      *
     c                   eval      jpfal6_vare = jvvare
     c     jpfal6_key    chain     jpfal6r
     c                   if        %found
     c                   delete    jpfal6r
     c                   endif
      *
     c                   clear                   jpfalur
      *
     c                   eval      jcfirm = w_firm
5.70 c                   eval      jcprgr = *blank
     c                   eval      jcogrp = jvogrp
     c                   eval      jchgrp = jvhgrp
     c                   eval      jcugrp = jvugrp
     c                   eval      jcldor = jrldor
     c                   eval      jcmodn = jvmodn
     c                   eval      jcvare = jvvare
     c                   eval      jckofa = 1
     c                   eval      jckobe = 0
      *
     c                   if        d_spri <> 0 and
     c                             d_ipri <> 0
      *
     c                   if        (d_spri / d_ipri) < 100
      *
     c                   eval(h)   jcsafa = d_spri / d_ipri
     c                   eval      jceusr = l_user
      *
     c                   time                    jcodat
     c                   time                    jcedat
     c                   time                    jcetim
      *
     c                   write     jpfalur
      *
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av varerecord
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     vare          begsr
      *
      * Behandler varerecord
      *
     c                   eval      d_tek1 = %trim(%xlate(x'46':'Æ':d_tek1))
     c                   eval      d_tek1 = %trim(%xlate(x'77':'Ø':d_tek1))
     c                   eval      d_tek1 = %trim(%xlate(x'2A':'Å':d_tek1))
      *
     c                   eval      d_tek2 = %trim(%xlate(x'46':'Æ':d_tek2))
     c                   eval      d_tek2 = %trim(%xlate(x'77':'Ø':d_tek2))
     c                   eval      d_tek2 = %trim(%xlate(x'2A':'Å':d_tek2))
      *
     c                   eval      d_mte1 = %trim(%xlate(x'46':'Æ':d_mte1))
     c                   eval      d_mte1 = %trim(%xlate(x'77':'Ø':d_mte1))
     c                   eval      d_mte1 = %trim(%xlate(x'2A':'Å':d_mte1))
      *
     c                   eval      d_mte2 = %trim(%xlate(x'46':'Æ':d_mte2))
     c                   eval      d_mte2 = %trim(%xlate(x'77':'Ø':d_mte2))
     c                   eval      d_mte2 = %trim(%xlate(x'2A':'Å':d_mte2))
      *
     c                   eval      d_merk = %trim(%xlate(x'46':'Æ':d_merk))
     c                   eval      d_merk = %trim(%xlate(x'77':'Ø':d_merk))
     c                   eval      d_merk = %trim(%xlate(x'2A':'Å':d_merk))
      *
     c                   eval      d_leve_alf = %xlate(' ':'0':d_leve_alf)
      *
     c                   clear                   jvarlur
     c                   eval      jvarlu_vare = jvvare
     c     jvarlu_key    chain     jvarlur
      *
      *
      * Oppretter eller oppdaterer vare
      *
     c                   eval      jvnobb = 0
     c                   eval      jvmodn = 0
     c                   eval      jvogrp = d_ogrp
     c                   eval      jvhgrp = d_hgrp
     c                   eval      jvugrp = d_ugrp
     c                   eval      jvtek1 = d_tek1
     c                   eval      jvtek2 = d_tek2
     c                   eval      jvldor = jrldor
     c                   eval      jvlvar = %trim(d_lvar)
     c                   eval      jvprod = d_prod
     c                   eval      jvpvar = %trim(d_pvar)
     c                   eval      jvean1 = d_eann
     c                   eval      jvleve = 0
     c                   eval      jvdoki = *blank
     c                   eval      jvadat = *loval
     c                   eval      jvudat = *loval
     c                   eval      jveusr = l_user
      *
     c                   time                    jvedat
     c                   time                    jvetim
      *
     c                   if        d_leve = 0
     c                   eval      jvleve = 1
     c                   endif
      *
     c                   if        d_adat <> 0
     c     *ymd          move      d_adat        jvadat
     c                   endif
      *
     c                   if        d_udatx <> *blank and
     c                             d_udat <> 0
     c     *ymd          move      d_udat        jvudat
     c                   endif
      *
     c                   if        %found(jvarlu)
     c                   eval      jvstat = 2
     c                   update    jvarlur
     c                   else
     c                   write     jvarlur
     c                   endif
      *
     c                   if        not %found(jvarlu)
     c                   clear                   jvdtlur
     c                   eval      jvdfir = w_firm
     c                   eval      jvdvar = jvvare
     c                   eval      jvdhko = 1
     c                   write     jvdtlur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av pakningsrecord
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     pakning       begsr
      *
      * rydder pakning
     c                   eval      jvpklu_vare = jvvare
     c     jvpklu_key    setll     jvpklu
     c     jvpklu_key    reade     jvpklur
     c                   dow       not %eof
     c                   if        jwrsts = 0
     c                   delete    jvpklur
     c                   else
     c                   unlock    jvpklu
     c                   endif
     c     jvpklu_key    reade     jvpklur                                91
     c                   enddo
      *
      *
     c                   eval      x = 0
      *
     c                   do        w_etel
      *
     c                   eval      x = x + 1
     c                   eval      d_erec = a_erec(x)
      *
     c                   eval      d_antp_alf = %xlate(' ':'0':d_antp_alf)
     c                   eval      d_psei_alf = %xlate(' ':'0':d_psei_alf)
      *
     c                   clear                   jvpklur
      *
     c                   eval      jwvare = jvvare
6.30 c                   eval      jwldor = jrldor
     c                   eval      jwpakn = d_pakn
     c                   eval      jwenhe = d_enhe
     c                   eval      jwofak = d_ofak
     c                   eval      jwbred = d_bred
     c                   eval      jwleng = d_leng
     c                   eval      jwhoyd = d_hoyd
     c                   eval      jwvekt = d_vekt
     c                   eval      jwvolu = d_volu
     c***                eval      jwanta = d_anta
6.30 c                   eval      jwgtin = d_eean
6.30 c                   eval      jwbest = d_best
     c                   eval      jwpsen = 0
     c                   eval      jweusr = l_user
      *
     c                   time                    jwodat
     c                   time                    jwedat
     c                   time                    jwetim
      *
     c***                z-add     d_anta        jwanta
      *
     c                   if        d_psei >= 1 and
     c                             d_psei <= 3
     c                   eval      jwpsen = 1
     c                   endif
      *
     c                   write     jvpklur
      *
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av ny vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ny_vare       begsr
      *
     c                   eval      jvstat = 2
      *
      * Henter neste ledige varenummer, og oppdaterer statusregisteret
      *
     c     jstslu_key    chain     jstslu
     c                   eval      jaavsi = jaavsi + 1
     c                   update    jstslur
      *
     c                   movel     jaavsi        jvvare
      *
      * Opprettelsesdato
      *
     c                   time                    jvodat
     c                   time                    jvadat
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_list
      *
      * Key for oppslag på rapportør
      *
     c     jrapl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jrapl1_rapp
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for oppslag på vare med NOBB-nummer
      *
     c     jvarl3_key    klist
     c                   kfld                    jvarl3_nobb
      *
      * Key for oppslag på vare med vareeier, lev.varenr
      *
     c     jvarl8_key    klist
     c                   kfld                    jvarl8_ldor
     c                   kfld                    jvarl8_lvar
      *
      * Key for oppdatering av vare-pris
      *
     c     jvprlu_key    klist
     c                   kfld                    jvprlu_vare
     c                   kfld                    jvprlu_ldor
      *
      * Key for oppdatering av vare
      *
     c     jvarlu_key    klist
     c                   kfld                    jvarlu_vare
      *
      * Key for oppdatering av vare-pakning
      *
     c     jvpklu_key    klist
     c                   kfld                    jvpklu_vare
      *
      * Key for oppdatering av kode/status
      *
     c     jstslu_key    klist
     c                   kfld                    w_firm
      *
      * Key for les av av vare-pris
      *
     c     jvprl1_key    klist
     c                   kfld                    jvprl1_vare
     c                   kfld                    jvprl1_ldor
      *
      * Key for oppdatering av påslag
      *
     c     jpfal6_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jpfal6_vare
      *
      *
      * Fyller datastruktur fra parameter, og henter firmakode
      *
     c                   eval      d_list = p_list
     c                   eval      w_firm = d_firm
      *
     c                   endsr
