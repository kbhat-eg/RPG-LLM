      /TITLE  ASLAGR Faktura, vise ordrehode (fra fakturahode-register)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : SH415R
      * Beskrivelse . . : Faktura, vise ordrehode (fra fakturahode-register)
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       220699 HKO  Nytt program
6.10  * R6.10 250809 BSA  Utvidet ref. felter fra 20 til 30 posisjoner
6.nu  * R6.30 040614 bof  Gjennomgått mtp. nummerutvidelsen
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsihel1    if   e           k disk    rename(sihepfr:sihel1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
      *
     fsh415d    cf   e             workstn
      *
      * Definering av parametre
      *
     d p_firm          s                   like(shfirm)
     d p_aarr          s                   like(shaarr)
     d p_binr          s                   like(shbinr)
     d p_numm          s                   like(shnumm)
     d p_suff          s                   like(shsuff)
      *
      * Definering av Local data area
      *
     d                uds
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d sihel1_aarr     s                   like(shaarr)
     d sihel1_binr     s                   like(shbinr)
     d sihel1_numm     s                   like(shnumm)
     d sihel1_suff     s                   like(shsuff)
     d rlevl1_levr     s                   like(rllevr)
      *
      * Definering av variable
      *
     d w_firm          s                   like(shfirm)
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle bildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer skjermbildet
      *
     c                   eval      sihel1_aarr = p_aarr
     c                   eval      sihel1_binr = p_binr
     c                   eval      sihel1_numm = p_numm
     c                   eval      sihel1_suff = p_suff
     c     sihel1_key    chain     sihel1                             90
     c                   if        *in90 = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      b1numm = shnumm
     c                   eval      b1suff = shsuff
     c                   eval      b1otyp = shotyp
      *
     c                   eval      b1innk = shinnk
     c                   eval      b1avde = shavde
     c                   eval      b1proj = shproj
     c                   eval      b1lage = shlage
      *
     c                   eval      b1vref = shvref
     c                   eval      b1dref = shdref
     c                   eval      b1rekv = shrekv
     c                   eval      b1betb = shbetb
      *
     c                   eval      b1ldor = shldor
     c***                eval      b1kpro = shkpro
     c                   eval      b1lnav = shnavn
     c                   eval      b1adr1 = shadr1
     c                   eval      b1adr2 = shadr2
     c                   eval      b1sted = shsted
      *
     c                   eval      b1totk = shkstp + shkstf
     c                   eval      b1totr = shrk1p + shrk1f +
     c                                      shrk2p + shrk2f
     c                   eval      b1tots = shkjop + shkjof - b1totr
      *
     c                   eval      b1ornr = shornr
     c                   eval      b1sped = shsped
     c                   eval      b1lmat = shlmat
     c                   eval      b1lmtx = shlmtx
     c                   eval      b1lbet = shlbet
     c                   eval      b1lbt1 = %subst(shlbtx :  1 : 25)
     c                   eval      b1lbt2 = %subst(shlbtx : 26 : 25)
     c                   eval      b1lbt3 = %subst(shlbtx : 51 : 25)
      *
     c                   eval      b1bdat = 0
     c                   eval      b1ldat = 0
     c                   eval      b1fkda = 0
      *
     c                   if        shbdat <> *loval
     c     *dmy          move      shbdat        b1bdat
     c                   endif
      *
     c                   if        shldat <> *loval
     c     *dmy          move      shldat        b1ldat
     c                   endif
      *
     c                   if        shfkda <> *loval
     c     *dmy          move      shfkda        b1fkda
     c                   endif
      *
     c                   if        shldor <> 0
     c                   eval      rlevl1_levr = shldor
     c     rlevl1_key    chain     rlevl1                             91
     c                   if        *in91 = *off
     c                   eval      b1lnav = rlnavn
     c                   eval      b1kgat = rlgate
     c                   eval      b1kad2 = rladr2
     c                   eval      b1kste = rlsted
     c                   endif
     c                   endif
      *
      * Behandler bildet
      *
     c     b1taga        tag
     c                   exfmt     b1bld
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   endsl
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_aarr
     c                   parm                    p_binr
     c                   parm                    p_numm
     c                   parm                    p_suff
      *
      * Key for oppslag på faktura-hode-register
      *
     c     sihel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sihel1_aarr
     c                   kfld                    sihel1_binr
     c                   kfld                    sihel1_numm
     c                   kfld                    sihel1_suff
      *
      * Key for oppslag på kunde
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      *
      * Henter firmakode fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
