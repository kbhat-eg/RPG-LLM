     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . :
      *  Program . . . . : AX400R
      *  Beskrivelse . . : MVA_Oppgavr Legger ut som XML-fil
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      *         Fortsett på starther    01.02.2022
      *
      *
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
Versj *Uts   Dato Usr Referanse Beskrivelse
      *_________________________________________________________________________
      *K00-220112               START FOR VERSJON K00
      *K00 220112 sst NXS-11016 Nytt program for momsoppgave ut som XML-fil
      *K00 220302 sst           Hente beskrivelse fra SAFT_MVA  (*dataara)
      *K00 220307 sst           Konv av spesialtegn
      *K00 220307 sst NXS-12334 Under spes. av noen momskoder
8.00  *K00 221031 sst NXS-13172 Flere satser pr momskode i perioden.
      *
      *
      *_________________________________________________________________________
      *
      *  Dette programmet henter data fra ADBxxx/rmvxPF m/rxfirm = Firma
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     fra04l1    if   e           k disk    rename(ra04pfr:ra04l1r)
     fras5pf    if   e           k disk    rename(ras5pfr:ras5pfr)
     frat5pf    if   e           k disk    rename(rat5pfr:rat5pfr)
     fra05l1    if   e           k disk    rename(ra05pfr:ra05l1r)
     frmvxl1    if   e           k disk    rename(rmvxpfr:rmvxl1r)
     frxmll1    if   e           k disk    rename(rxmlpfr:rxmll1r)
     frhtrlf    if   e           k disk    rename(rhtrpfr:rhtrlfr)
      *
     faxmlut    o    e             disk
      *
     d dig1            c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅ'
     d dig2            c                   'abcdefghijklmnopqrstuvwxyzæøå'
     d dig3            c                   '-_+=\/%&() 0123456789.,*@?:;!'
      *
     d ana             S             80    DIM(9) CTDATA PERRCD(1)
      *
     d ra04l1_firm     s                       like(radfir)
      *
     d afirl1_firm     s                       like(aaffir)
      *
     d ras5pf_mvak     s                       like(rsesmk)
     d ra05l1_firm     s                       like(raefir)
     d ra05l1_mvak     s                       like(raekod)
     d rat5pf_firm     s                       like(rtefir)
     d rat5pf_mvak     s                       like(rtekod)
      *
     d rmvxl1_firm     s                       like(rxfirm)
     d rmvxl1_smvk     s                       like(rxsmvk)
      *
     d rxmll1_head     s                       like(axhead)
     d rxmll1_linj     s                       like(axlinj)
      *
     d rhtrlf_firm     s                       like(rbfirm)
     d rhtrlf_hpro     s                       like(rbhpro)
     d rhtrlf_raar     s                       like(rbraar)
     d rhtrlf_rper     s                       like(rbrper)
      *
      *
     d XML_Input       s            256a   Varying
     d XML_Output      s            256a   Varying
     d XML_Outhead     s            256a   Varying
     d XML_path        s            256a
     d w_xmlp          s             50
     d options         s            200a   Varying
     d w_per_x         s             20    inz('november-Desember')
     d w_mk            s              2
      *
     d w_firma         s              3A   inz('000')
     d w_firm          s              3S 0
      *
     d w_neto          s             11  2
     d w_nx_mva        s             11  2
     d text            s             50
     d txt2            s             50
     d stmt            s            500A
     d xmlut           s            500A
     d w_po1           s              3  0
     d w_po2           s              3  0
     d w_po3           s              3  0
      *
     d i               s              9S 0
     d j               s              9S 0
     d s_i             s              9S 0
     d f               s              3
     d t               s              3
     d z               s              1
     d s               s              3S 0
     d fn              s              3S 0
     d tn              s              3S 0
     d s_head          s                   like(axhead)
     d s_linj          s                   like(axlinj)
     d w_rnkund        s              6
     d w_rolevr        s              6
      *
     d s_linja         s                   like(axlinj)
     d s_kontx         s              5
     d w_iso_dat       s               d   datfmt(*iso)
     d w_x_dat         s             10A
     d w_salix         s             18
     d w_salux         s             18
     d w_test          s            100
     d w_02            s              2S 2
     d w_s             s             15S 2
     d w_110           s             15S 0
     d w_110x          s             15
     d w_02x           s              2
     d w_saldoi        s             15S 2
     d w_saldou        s             15S 2
     d w_kurs          s              6S 3
     d w_kx            s              6
      *
     d w_type          s              1
      *
     d w_str_i         s            100
     d w_str_u         s            100
     d w_posi          s              2  0
     d w_posu          s              2  0
     d w_str_x         s              1
     d c_apos          c                   x'7D'
      *
     d p_perfm         s              2
     d p_perfa         s              4
     d p_pertm         s              2
     d p_perta         s              4
     d p_pfm           s              2S 0
     d p_pfa           s              4S 0
     d p_ptm           s              2S 0
     d p_pta           s              4S 0
     d p_hcom          s             50
      *
      *
     d w_sum_ant       s             15S 0
     d w_sum_deb       s             15S 3
     d w_sum_kre       s             15S 3
     d w_sum_alfa      s             15
     d w_tot_mva       s             11  2
      *
      *
     d                uds
     d l_raar                306    309  0
     d l_rper                310    311  0
     d l_mper                312    312  0
     d l_test                315    315
     d l_firm                944    946  0
     d l_user                911    920
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Start
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      *    Posisjoner Hovedbok på fra periode
      *
     c     STARTP        tag
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *    Finner totalt beregne mva for denne periode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   read      rmvxl1
     c                   dow       not %eof(rmvxl1)
     c                   eval      w_tot_mva = w_tot_mva + rximva + rxumva
     c                   read      rmvxl1
     c                   enddo
     c                   close     rmvxl1
     c                   open      rmvxl1
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *    Skriv ut ev heading xml - linjer før loop pr momskoden until 'S'
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c                   dow       not %eof(rxmll1)
     c                             and axmaop <> 'S'
     c                   exsr      sxmlut
     c                   eval      text = text
     c                   read      rxmll1
     c                   enddo
      *
     c                   eval      s_linja = axlinj
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *   Behandle alle bilagstransaksjoner
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     HOVEDP        tag
      *
      *    Legger ut xml-linjer pr momskode
      *
     c                   read      rmvxl1
     c                   dow       not %eof(rmvxl1)
     c                   exsr      mvakode
     c                   eval      rxsmvk = rxsmvk
      *ssdebug
     c                   exsr      underspes
     c                   read      rmvxl1
     c                   enddo
      *
      *    Siste mva-kode er nå behandlet
      *    Legg ut ev faste linjer som avlsutning på Hovedbok
      *
     c     xsiste        tag
      *
     c                   dow       not %eof(rxmll1) and
     c                             axhead = 'MVA_MELD'
     c                   exsr      sxmlut
     c                   read      rxmll1
     c                   if        axlinj = 350
     c                   eval      axlinj = axlinj
     c                   endif
     c                   enddo
      *
     c                   eval      *inlr = *on
     c                   return
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine = mvakode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     Mvakode       begsr
      *
      *    Her skal man start på nytt på nytt bilag   linje 20
      *
     c                   eval      rxmll1_linj = s_linja
     c     rxmll1_key    setll     rxmll1
     c                   read      rxmll1
     c                   if        axmaop = 'S'
     c                   exsr      sxmlut
     c                   read      rxmll1
     c                   endif
      *
      *  Skriv xml for start bilag
      *
     c                   dow       axmaop <> 'S' and
     c                             axmaop <> 'E'
     c                   exsr      sxmlut
     c                   read      rxmll1
     c                   enddo
      *
      *   Lagre start xml for ny konto(postering)
      *
     c                   eval      s_linj = axlinj
      *
      *  Skrive slutt xml for mvakoden
      *
     c                   dow       not %eof(rxmll1) and
     c                             axmaop <> 'E'
     c                   exsr      sxmlut
     c                   read      rxmll1
     c                   enddo
      *
     c                   if        axmaop = 'E'
     c                   exsr      sxmlut
     c                   read      rxmll1
     c                   endif
      *
     c                   endsr
      *
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  SXMLUT  legger ut linja etter koder fra alle nivåer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     SXMLUT        begsr
      *
      *  Hvis type er merket med 'X' er ikke denne linja klargjort enda
      *
     c                   if        axtype = 'X'
     c                   goto      EXMLUT
     c                   endif
      *
      *
      *  Sjekk om bare xml linja skal ut
      *
     c                   if        axtype = 'F'
     c                   eval      xmlrec = axxmlt
     c                   goto      skrivxml
     c                   endif
      *
      *  Skal det hentes inn et variabelt felt?
      *
     c                   eval      i = %scan('?V?':axxmlt)
     c                   if        i > 0
      *
      *  Dagens dato hentes dersom axtype = 'C' (current)
      *
     c                   if        axtype = 'C'
     c     *dmy          move      udate         w_iso_dat
     c                   move      w_iso_dat     w_x_dat
     c                   eval      xmlrec = %subst(axxmlt:1:i-1) +
     c                             %trim(w_x_dat) +
     c                             %subst(axxmlt:i+3:50)
     c                   goto      skrivxml
     c                   endif
      *
     c                   eval      text = *blank
      *
      *  Hente inn tekst felt
      *
     c                   if        axtype = 'T'
      * starther
     c                   select
     c                   when      axfelt = 'AAFFTN'
     c                   movel     l_firm        afirl1_firm
     c     afirl1_key    chain     afirl1
     c                   if        %found(afirl1)
     c                   movel     aafftn        text
     c                   else
     c                   move      *blank        text
     c                   endif
      *  Legg ut forteaksnummer for bruk i filnavnet
     c
     c     *lock         in        *dtaara
     c                   eval      %subst(ax000par:90:9) = %char(aafftn)
     c                   out       *dtaara
      *
     c                   when      axfelt = 'PERIODE'
     c                   select
     c                   when      l_mper = 1
     c                   eval      text = 'Januar-februar'
     c                   when      l_mper = 2
     c                   eval      text = 'Mars-April'
     c                   when      l_mper = 3
     c                   eval      text = 'Mai-Juni'
     c                   when      l_mper = 4
     c                   eval      text = 'Juli-August'
     c                   when      l_mper = 5
     c                   eval      text = 'September-Oktober'
     c                   when      l_mper = 6
     c                   eval      text = 'November-Desember'
     c                   endsl
      *
     c                   when      axfelt = 'RXSMVK'
     c                   movel     rxsmvk        text
      *
     c                   when      axfelt = 'RSETXT'
     c                   movel     rxsmvk        ras5pf_mvak
     c     ras5pf_key    chain     ras5pf
     c                   if        %found(ras5pf)
     c                   movel     rsetxt        text
     c                   else
     c                   move      *blank        text
     c                   endif
      *
     c                   when      axfelt = 'RAETXT'
     c                   movel     rtekod        ra05l1_mvak
     c     ra05l1_key    chain     ra05l1
     c                   if        %found(ra05l1)
     c                   movel     raetxt        text
     c                   else
     c                   move      *blank        text
     c                   endif
      *   Beskrivelse legges ut fra parameter når det ikke kjøres test.  *curlib
     c                   when      axfelt = 'SAFT_MVA'
     c     *dtaara       define                  saft_mva        100
     c                   in        *dtaara
     c                   eval      text = %subst(saft_mva:1:30)
      *
     c                   when      axfelt = 'RXGRLA'
     c                   movel     rxgrla        text
     c                   endsl
      *
     c                   if        %subst(axfast:1:1) = '('
     c                   eval      j = %scan(',':axfast)
     c                   if        j > 0
     c                   eval      f = %subst(axfast:2:3)
     c                   eval      t = %subst(axfast:6:3)
     c                   move      f             fn
     c                   move      t             tn
     c                   eval      txt2 = %subst(text:fn:tn-fn+1)
     c                   eval      xmlrec = %subst(axxmlt:1:i-1) +
     c                             %trim(txt2) +
     c                             %subst(axxmlt:i+3:50)
     c                   goto      skrivxml
     c                   endif
     c                   endif
      *
     c                   exsr      spstgn
     c                   eval      i = %scan('?V?':axxmlt)
     c                   if        i > 0
     c                   eval      xmlrec = %subst(axxmlt:1:i-1) +
     c                             %trim(text) +
     c                             %subst(axxmlt:i+3:50)
     c                   endif
     c                   endif
      *
      *  Hente inn numerisk felt
      *
     c                   if        axtype = 'N'
     c                   select
      *
     c                   when      axfelt = 'P_AAR'
     c                   eval      text   =  %char(l_raar)
      *
     c                   when      axfelt = 'TOTAL_MVA'
     c                   eval      w_saldoi = w_tot_mva
     c                   exsr      konvtall
     c                   movel     w_salix       text
      *
     c                   when      axfelt = 'RXGRLA'
     c                   eval      w_saldoi = rxgrla
     c                   exsr      konvtall
     c                   movel     w_salix       text
      *
     c                   when      axfelt = 'RSESAT'
8.00 c*                  eval      w_saldoi = rsesat
8.00 c                   eval      w_saldoi = rxmvap
     c                   exsr      konvtall
     c                   movel     w_salix       text
      *
     c                   when      axfelt = 'RXIMVA'
     c                   if        rximva <> 0
     c                   eval      w_saldoi = rximva
     c                   else
     c                   eval      w_saldoi = rxumva
     c                   endif
     c                   exsr      konvtall
     c                   movel     w_salix       text
      *
     c                   when      axfelt = 'W_NX_MVA'
     c                   eval      w_saldoi = w_nx_mva
     c                   exsr      konvtall
     c                   movel     w_salix       text
      *
     c                   other
     c                   goto      Exmlut
      *
     c                   endsl
      *
     c**                 exsr      spstgn
     c                   eval      xmlrec = %subst(axxmlt:1:i-1) +
     c                             %trim(text) +
     c                             %subst(axxmlt:i+3:50)
     c                   endif
      *
      *  Hente inn dato felt
      *
     c                   if        axtype = 'D'
     c                   select
     c*                  when      axfelt = 'RBBDAT'
     c*                  movel     rbbdat        text
     c                   endsl
      *
     c                   eval      w_x_dat = text
     c                   eval      xmlrec = %subst(axxmlt:1:i-1) +
     c                             %trim(w_x_dat) +
     c                             %subst(axxmlt:i+3:50)
     c                   endif
      *
      *  Hente inn parameter via p_xxxxx felt fra axfelt
      *
     c                   if        axtype = 'P'
     c                   eval      text = *blank
     c                   select
     c*                  when      axfelt = 'P_PERFM'
     c*                  movel     p_perfm       text
     c                   when      axfelt = 'L_USER'
     c                   movel     l_user        text
     c                   endsl
      *
     c                   exsr      spstgn
     c                   eval      xmlrec = %subst(axxmlt:1:i-1) +
     c                             %trim(text) +
     c                             %subst(axxmlt:i+3:50)
     c                   endif
      *
     c                   goto      skrivxml
      *  END ?V?
     c                   endif
      *
      *  Sjekk om faste felter skal legges ut
      *
     c                   eval      i = %scan('?F?':axxmlt)
     c                   if        i > 0
     c                   eval      xmlrec = %subst(axxmlt:1:i-1) +
     c                             %trim(axfast) +
     c                             %subst(axxmlt:i+3:50)
     c                   goto      skrivxml
     c                   endif
      *
      *  Sjekk xml linja skal ut med parameter
      *
     c                   if        axtype = 'F'
     c                   eval      xmlrec = %subst(axxmlt:1:i-1) +
     c                             %trim(axfast) +
     c                             %subst(axxmlt:i+3:50)
     c                   goto      skrivxml
     c                   endif
      *
     c     Skrivxml      tag
     c                   exsr      spstgn
     c                   write     axmlutr
      *
     c     EXMLUT        tag
      * Sjekk om Saft-momskode skal underspesifiseres.
     c**                 exsr      underspes
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *     Spesial-tegn
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     spstgn        begsr
      *
     c                   eval      s_i = i
      *
     c                   eval      i = s_i
      *
     c                   eval      w_posi = 0
     c                   eval      w_posu = 0
     c                   eval      w_str_u = *blank
     c                   eval      w_str_i = text
      *
     c                   dow       w_posi < 70
     c                   eval      w_posi = w_posi + 1
     c                   eval      w_str_x = %subst(w_str_i:w_posi:1)
     c                   if        w_posu < 90
     c                   select
     c                   when      w_str_x = '&'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:5) = '&amp;'
     c                   eval      w_posu = w_posu + 4
      *
     c                   when      w_str_x = '<'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:4) = '&lt;'
     c                   eval      w_posu = w_posu + 3
      *
     c                   when      w_str_x = '>'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:4) = '&gt;'
     c                   eval      w_posu = w_posu + 3
      *
     c                   when      w_str_x = c_apos
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:6) = '&apos;'
     c                   eval      w_posu = w_posu + 5
      *
     c                   when      w_str_x = '"'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:6) = '&quot;'
     c                   eval      w_posu = w_posu + 5
      *
     c                   other
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:1) = w_str_x
      *
     c                   endsl
     c                   endif
     c                   enddo
      *
     c                   eval      text   =  w_str_u
      * Andre tegn
     c     x'20':'A'     xlate     text          text
     c     x'75':'I'     xlate     text          text
     c     x'DB':'Y'     xlate     text          text
     c     x'7B':'U'     xlate     text          text
     c     x'ED':'O'     xlate     text          text
     c     x'a1':'U'     xlate     text          text
     c     x'CF':'O'     xlate     text          text
     c     x'44':'A'     xlate     text          text
     c     x'43':'A'     xlate     text          text
     c     x'45':'A'     xlate     text          text
     c     x'52':'E'     xlate     text          text
     c     x'72':'E'     xlate     text          text
     c     x'36':' '     xlate     text          text
     c     x'BE':x'7D'   xlate     text          text
     c     x'00':' '     xlate     text          text
     c     x'DE':'U'     xlate     text          text
     c     x'63':'E'     xlate     text          text
      *  À
     c     x'64':'A'     xlate     text          text
     c     x'D0':'a'     xlate     text          text
      * SAFT
     c     'Æ':'E'       xlate     text          text
     c     'Ø':'O'       xlate     text          text
     c     'Å':'A'       xlate     text          text
     c     'æ':'e'       xlate     text          text
     c     'ø':'o'       xlate     text          text
     c     'å':'a'       xlate     text          text
      *
  ss  *
  ss  *  Dersom det fortsatt er et ukjent tegn så erstattes det med $
  ss  *
  ss  *
     c                   eval      i = 1
     c                   dow       i < 50
     c                             and %subst(text:i:51-i) <> ' '
  ss c                   eval      z = %subst(text:i:1)
  ss c     dig1          check     z             w_po1
  ss c                   if        w_po1 > 0
  ss c     dig2          check     z             w_po2
  ss c                   if        w_po2 > 0
  ss c     dig3          check     z             w_po3
  ss c                   if        w_po3 > 0
  ss c                   eval      %subst(text:i:1) = '%'
  ss c                   endif
  ss c                   endif
  ss c                   endif
     c                   eval      i = i + 1
     c                   enddo
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *     SUBrutine Redefinering av nummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     redefnum      begsr
      *
     c                   dow       %subst(text:1:1) = '0'
     c                             and %subst(text:3:1) <> ' '
     c                   eval      %subst(text:1:20) = %subst(text:2:20)
     c                   enddo
     c     ' '           scan      text          j
     c                   if        j > 3
     c                   eval      text = %subst(text:1:j-3) + '.' +
     c                                    %subst(text:j-2:2)
     c                   else
     c                   eval      text = '0.' + %subst(text:1:2)
     c                   endif
     c                   if        text = '0.'
     c                   eval      text = '0 '
     c                   endif
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for konvertering av tall som kan legges ut i fil
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     konvtall      begsr
      *
     c                   eval      w_s = w_saldoi
     c                   if        w_s < 0
     c                   eval      w_s = w_s * -1
     c                   endif
     c                   eval      w_110 = w_s
     c                   eval      w_02  = w_s - w_110
     c                   move      w_110         w_110x
     c                   move      w_02          w_02x
     c                   eval      w_salix = w_110x + '.' + w_02x
     c                   dow       %subst(w_salix:1:1) = '0'
     c                   eval      %subst(w_salix:1:17) = %subst(w_salix:2:17)
     c                   eval      %subst(w_salix:18:1) = ' '
     c                   enddo
      *
     c                   eval      w_s = w_saldou
     c                   if        w_s < 0
     c                   eval      w_s = w_s * -1
     c                   endif
     c                   eval      w_110 = w_s
     c                   eval      w_02  = w_s - w_110
     c                   move      w_110         w_110x
     c                   move      w_02          w_02x
     c                   eval      w_salux = w_110x + '.' + w_02x
     c                   dow       %subst(w_salux:1:1) = '0'
     c                   eval      %subst(w_salux:1:17) = %subst(w_salux:2:17)
     c                   eval      %subst(w_salux:18:1) = ' '
     c                   enddo
      *
     c                   eval      w_saldoi =  0
     c                   eval      w_saldou =  0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for underspesifisering av saft-momskode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     UnderSpes     begsr
      *
     c                   eval      rat5pf_firm = w_firm
     c                   eval      rat5pf_mvak = *blank
     c     rat5pf_key    setll     rat5pf
     c                   read      rat5pf
     c                   eval      rtekod = rtekod
     c                   eval      rtesmk = rtesmk
     c                   dow       not %eof(rat5pf)
     c                   move      rtesmk        w_mk
     c                   if        w_mk = rxsmvk
     c                             and rtspss = 'J'
     c                             and rtprut <> *blank
     c                   exsr      RapNxMvak
     c                   endif
     c                   read      rat5pf
     c                   enddo
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å rapportere NX-momskode m/spes prosjekt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     RapNxMvak     begsr
      *
     c                   eval      w_nx_mva = 0
     c                   eval      rhtrlf_firm = w_firm
     c                   eval      rhtrlf_hpro = rtprut
     c                   eval      rhtrlf_raar = p_pfa
     c                   eval      rhtrlf_rper = p_pfm -1
     c     rhtrlf_key    setll     rhtrlf
     c                   read      rhtrlf
     c                   dow       not %eof(rhtrlf)
     c                             and rbraar = p_pfa
     c                             and rbrper <= p_ptm
     c                             and rbhpro = rtprut
     c                   eval      w_nx_mva = w_nx_mva + rbmvab
     c                   read      rhtrlf
     c                   enddo
      *
     c                   if        w_nx_mva <> 0
      *
     c                   eval      rxmll1_head = 'MVA_MELDX'
     c                   eval      rxmll1_linj = 1000
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1)
     c                   exsr      sxmlut
     c                   endif
      *
     c                   eval      rxmll1_linj = 1010
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1)
     c                   exsr      sxmlut
     c                   endif
      *
     c                   eval      rxmll1_linj = 1020
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1)
     c                   exsr      sxmlut
     c                   endif
      *
     c                   eval      rxmll1_linj = 1030
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1)
     c                   exsr      sxmlut
     c                   endif
      *
     c                   eval      rxmll1_linj = 1040
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1)
     c                   exsr      sxmlut
     c                   endif
      *
     c                   eval      rxmll1_head = 'MVA_MELD'
     c                   endif
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     *inzsr        begsr
      *
      *
     c     afirl1_key    klist
     c                   kfld                    afirl1_firm
      *
     c     ra04l1_key    klist
     c                   kfld                    ra04l1_firm
      *
     c     ras5pf_key    klist
     c                   kfld                    ras5pf_mvak
      *
     c     ra05l1_key    klist
     c                   kfld                    ra05l1_firm
     c                   kfld                    ra05l1_mvak
      *
     c     rat5pf_key    klist
     c                   kfld                    rat5pf_firm
     c                   kfld                    rat5pf_mvak
      *
     c     rmvxl1_key    klist
     c                   kfld                    rmvxl1_firm
     c                   kfld                    rmvxl1_smvk
      *
     c     rxmll1_key    klist
     c                   kfld                    rxmll1_head
     c                   kfld                    rxmll1_linj
      *
     c     rhtrlf_key    klist
     c                   kfld                    rhtrlf_firm
     c                   kfld                    rhtrlf_hpro
     c                   kfld                    rhtrlf_raar
     c                   kfld                    rhtrlf_rper
      *
      *  Hent Balansekonti for kunder og leverandører
      *
     c                   move      w_firma       ra04l1_firm
     c     ra04l1_key    chain     ra04l1                             90
      *
      *
     c     *dtaara       define                  ax000par        100
     c     *lock         in        *dtaara
     c                   eval      p_perfm = %subst(ax000par:1:2)
     c                   eval      p_perfa = %subst(ax000par:3:4)
     c                   eval      p_pertm = %subst(ax000par:7:2)
     c                   eval      p_perta = %subst(ax000par:9:4)
     c                   eval      p_hcom  = %subst(ax000par:13:50)
      *
     c                   move      l_rper        p_pfm
     c                   move      l_raar        p_pfa
     c                   move      l_rper        p_ptm
     c                   move      p_perta       p_pta
      *
     c                   move      l_firm        w_firm
     c                   movel     'MVA_MELD'    rxmll1_head
     c                   movel     'MVA_MELD'    s_head
     c                   move      *zero         rxmll1_linj
     c     rxmll1_key    setll     rxmll1
     c                   read      rxmll1
      *
     c                   endsr
      *
      *
      *
**     A N A L Y S I S
                  <nl:Analysis>
                     <nl:AnalysisType>?P?</nl:AnalysisType>
                     <nl:AnalysisID>?P?</nl:AnalysisID>
                        <nl:AnalysisAmount>
                        <nl:Amount>?B?<nl:Amount>
                        </nl:AnalysisAmount>
                   </nl:Analysis>
                  <nl:CustomerID>?P?</nl:CustomerID>
                  <nl:SupplierID>?P?</nl:SupplierID>
