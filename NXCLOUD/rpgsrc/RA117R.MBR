     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RA117R
      * Beskrivelse . . : Forsendelsesmåter, vedlikehold
      *
7.01  * Spesialversjon. : Nytt felt, direkte valgt
7.01  * Tilpasset for . : Skattum
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       260302 KOB  Nytt program
6.20  *       181110 bof  Utvidet med kode for behandle i kjørekontor
6.30  *       120215 bsa  Utvidet med smsvarsling til kunde og selger
7.xx  * 7.00  020316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 7.01  300811 bof  Nytt felt - direkte valgt   (tatt inn fra Byggmakker)
      *
8.01  * 800   010422 ask  Lagt inn kobling til Regionalt Distribusjons Senter (RDS)
8.02  * 800   180321 bsa  Utvidet med sideregister
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fra17l1    if   e           k disk    rename(ra17pfr:ra17l1r)
     fra17l2    if   e           k disk    rename(ra17pfr:ra17l2r)
     fra17lr    if   e           k disk    rename(ra17pfr:ra17lrr)
     fra17lu    uf a e           k disk    rename(ra17pfr:ra17lur)
8.02 fraf2lu    uf a e           k disk    rename(raf2pfr:raf2lur)
8.02 fraf2l1    if   e           k disk    rename(raf2pfr:raf2l1r)
      *
     fra117d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d ra17l1_qkod     s                   like(raqkod)
     d ra17l2_qtxt     s                   like(raqtxt)
     d ra17lr_qkod     s                   like(raqkod)
     d ra17lu_qkod     s                   like(raqkod)
8.02 d raf2lu_2kod     s                   like(ra2kod)
8.02 d raf2l1_2kod     s                   like(ra2kod)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(raqfir)
      *
     d s_qkod          s                   like(raqkod)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste recordnummer på siden
      * w_curn - Current recordnummer på siden
      * w_virn - Siden som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2qkod <> 0 or
     c                             b2qtxt <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_sfrn <> 0
     c     w_sfrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      ra17l2_qtxt = b1qtxt
     c     ra17l2_key    setll     ra17l2
     c                   other
     c                   eval      ra17l1_qkod = b1qkod
     c     ra17l1_key    setll     ra17l1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Forsendelsesmåte
      *
     c                   if        b2qkod <> 0
     c                   eval      w_seqe = *blank
     c                   eval      ra17l1_qkod = b2qkod
     c     ra17l1_key    setll     ra17l1
     c                   goto      end_pos
     c                   endif
      *
      * Beskrivelse
      *
     c                   if        b2qtxt <> *blank
     c                   eval      w_seqe = 'B'
     c                   eval      ra17l2_qtxt = b2qtxt
     c     ra17l2_key    setll     ra17l2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2qkod = 0
     c                   eval      b2qtxt = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1qkod = b1qkod
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1qkod = b1qkod
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1qkod = b1qkod
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1qkod = b1qkod
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
8.01  *
8.01  * Vedlikehold av RDS kobling
8.01  *
8.01 c                   if        b1valg = 8
8.01 c                   call      'RA136R'
8.01 c                   parm                    b1qkod
8.01 c                   eval      b1valg = 0
8.01 c                   update    b1sfl
8.01 c                   iter
8.01 c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1qkod = b2qkod
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1qkod = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Validering
      *
     c***                if        c1qkod = 0
     c***                eval      *in31  = *on
     c***                goto      c1taga
     c***                endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      ra17lr_qkod = c1qkod
     c     ra17lr_key    chain     ra17lr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1qkod = 0
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2qkod = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      ra17lr_qkod = c1qkod
     c     ra17lr_key    chain     ra17lr
      *
     c                   if        %found
     c                   eval      c2qtxt = raqtxt
6.20 c                   eval      c2qkjo = raqkjo
7.01 c                   eval      c2qdir = raqdir
6.30 c                   eval      c2qsmk = raqsmk
6.30 c                   eval      c2qsms = raqsms
     c                   eval      c2eusr = raqusr
     c     *dmy          move      raqdat        c2edat
     c                   else
     c                   eval      c2qtxt = *blank
6.20 c                   eval      c2qkjo = 0
7.01 c                   eval      c2qdir = 1
6.30 c                   eval      c2qsmk = 0
6.30 c                   eval      c2qsms = 0
     c                   eval      c2eusr = *blank
     c                   eval      c2edat = 0
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1qkod = k1qkod
     c                   endif
8.02  * Sideregister
8.02 c                   eval      raf2l1_2kod = c1qkod
8.02 c     raf2l1_key    chain     raf2l1
8.02  *
8.02 c                   if        %found
8.02 c                   eval      c22flt = ra2flt
8.02 c                   eval      c22fid = ra2fid
8.02 c                   else
8.02 c                   eval      c22flt = 0
8.02 c                   eval      c22fid = *blanks
8.02 c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c2taga
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2qtxt = *blank
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   ra17lur
      *
     c                   eval      ra17lu_qkod = c1qkod
     c     ra17lu_key    chain     ra17lur
      *
     c                   eval      raqtxt = c2qtxt
6.20 c                   eval      raqkjo = c2qkjo
7.01 c                   eval      raqdir = c2qdir
6.30 c                   eval      raqsmk = c2qsmk
6.30 c                   eval      raqsms = c2qsms
      *
     c                   time                    raqdat
     c                   time                    raqtim
     c                   eval      raqusr = l_user
      *
     c                   if        %found
     c                   update    ra17lur
     c                   else
     c                   eval      raqfir = w_firm
     c                   eval      raqkod = c1qkod
     c                   write     ra17lur
     c                   endif
8.02  *
8.02  * Ny sidetabell
8.02  *
8.02 c                   clear                   raf2lur
8.02  *
8.02 c                   eval      raf2lu_2kod = c1qkod
8.02 c     raf2lu_key    chain     raf2lur
8.02  *
8.02 c                   eval      ra2flt = c22flt
8.02 c                   eval      ra2fid = c22fid
8.02  *
8.02 c                   time                    ra2eda
8.02 c                   time                    ra2eti
8.02 c                   eval      ra2eus = l_user
8.02  *
8.02 c                   if        %found
8.02 c                   update    raf2lur
8.02 c                   else
8.02 c                   eval      ra2fir = w_firm
8.02 c                   eval      ra2kod = c1qkod
8.02 c                   write     raf2lur
8.02 c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1qtxt = raqtxt
6.20 c                   eval      b1qkjo = raqkjo
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      ra17lr_qkod = d1qkod
     c     ra17lr_key    chain     ra17lr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      ra17lu_qkod = d1qkod
     c     ra17lu_key    chain     ra17lur
      *
     c                   delete    ra17lur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_qkod = k1qkod
      *
      * Fyller kopierings-bildet
      *
     c                   eval      ra17lr_qkod = k1qkod
     c     ra17lr_key    chain     ra17lr
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1qkod = 0
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      ra17lr_qkod = k1qkod
     c     ra17lr_key    chain     ra17lr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1qkod = s_qkod
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      ra17l2
     c                   other
     c                   read      ra17l1
     c                   endsl
      *
     c                   if        %eof or
     c                             raqfir <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      ra17l2_qtxt = raqtxt
     c                   other
     c                   eval      ra17l1_qkod = raqkod
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1qkod = raqkod
     c                   eval      b1qtxt = raqtxt
6.20 c                   eval      b1qkjo = raqkjo
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'B'
     c     ra17l2_key    setgt     ra17l2
     c                   readp     ra17l2
     c                   other
     c     ra17l1_key    setgt     ra17l1
     c                   readp     ra17l1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   readp     ra17l2
     c                   other
     c                   readp     ra17l1
     c                   endsl
      *
     c                   if        %eof or
     c                             raqfir <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      ra17l2_qtxt = raqtxt
     c                   other
     c                   eval      ra17l1_qkod = raqkod
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'B'
     c     ra17l2_key    setll     ra17l2
     c                   other
     c     ra17l1_key    setll     ra17l1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for posisjonering på forsendelsesmåte
      *
     c     ra17l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra17l1_qkod
      *
      * Key for posisjonering på beskrivelse
      *
     c     ra17l2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra17l2_qtxt
      *
      * Key for les på forsendelsesmåte
      *
     c     ra17lr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra17lr_qkod
      *
      * Key for oppdatering av forsendelsesmåte
      *
     c     ra17lu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra17lu_qkod
8.02  *
8.02  * Key for les på forsendelsesmåte - sideregister
8.02  *
8.02 c     raf2l1_key    klist
8.02 c                   kfld                    w_firm
8.02 c                   kfld                    raf2l1_2kod
8.02  *
8.02  * Key for oppdatering av forsendelsesmåte  - sideregister
8.02  *
8.02 c     raf2lu_key    klist
8.02 c                   kfld                    w_firm
8.02 c                   kfld                    raf2lu_2kod
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      ra17l1_qkod = 0
     c     ra17l1_key    setll     ra17l1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
