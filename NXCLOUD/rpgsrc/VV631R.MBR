     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : VV631R
      * Beskrivelse . . : Vare, utskrift av varer uten EAN-nr.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       070600 KOB  Nytt program
6.11  * 6.10  290609 bof  Utivdet eannr til 14 og brukt std. oppslag
6.31  * 6.30  081018 bof  Utvidet med trelast sortiment
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl8    if   e           k disk    rename(vvarpfr:vvarl8r)
      *
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
      *
     fvv631p    o    e             printer
      *
      * Local data area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
      *
      * Datastruktur for parameterliste -inn
      *
     d                 ds
     d d_list                        32
     d  d_firm                        3  0 overlay(d_list:1)
     d  d_ogrp                        2  0 overlay(d_list:4)
     d  d_hgrp                        2  0 overlay(d_list:6)
     d  d_ugrp                        3  0 overlay(d_list:8)
     d  d_dato                         d   overlay(d_list:11) datfmt(*iso)
     d                 ds
      *
      * Definering av parametre
      *
     d p_list          s             32
      *
      * Definering av variabler i nøkler
      *
     d vvarl8_ogrp     s                   like(vvogrp)
     d vvarl8_hgrp     s                   like(vvhgrp)
     d vvarl8_ugrp     s                   like(vvugrp)
     d rlevl1_levr     s                   like(rllevr)
      *
      * Definering av variable
      *
     d w_timestamp     s             12  0
     d w_udat          s               d   datfmt(*iso)
      *
     d w_firm          s                   like(vvfirm)
6.11 d w_east          s              1
6.11 d w_eann          s             14  0
6.31 d w_lv_nobb       s                   like(vvnobb)
6.31 d w_lv_lvar       s                   like(vvlvar)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.31 C/Exec SQL
6.31 C+  Set Option DatFmt=*ISO
6.31 C/End-Exec
      * Utskrift av header
      *
     c                   exsr      hode
      *
      * Utskrift av vareregister
      *
     c                   exsr      behandling
      *
      * Avslutt
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering og utskrift av hode-opplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode          begsr
      *
      * Legger inn arb.stasjon og bruker i faste felter
      *
     c                   eval      a1firm = w_firm
     c                   eval      a1user = l_user
     c                   eval      a1wsid = l_wsid
     c                   time                    w_timestamp
     c                   move      w_timestamp   a1date
     c                   movel     w_timestamp   a1time
      *
      * Henter firmaopplysninger
      *
     c     afirl1_key    chain     afirl1r                            90
     c                   eval      a1fnav = aafnav
      *
      * Henter parametre
      *
     c                   eval      a2ogrp = d_ogrp
     c                   eval      a2hgrp = d_hgrp
     c                   eval      a2ugrp = d_ugrp
      *
      * Skriver heading
      *
     c                   write     a1hdr
     c                   write     a2hdr
     c                   write     a3hdr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Leser vareregsiter, og behandler utskrift av detaljlinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
     c                   eval      vvarl8_ogrp = d_ogrp
     c                   eval      vvarl8_hgrp = d_hgrp
     c                   eval      vvarl8_ugrp = d_ugrp
      *
      * Ingen utvalg
      *
     c                   if        d_ogrp = *zeros and
     c                             d_hgrp = *zeros and
     c                             d_ugrp = *zeros
     c     vvarl8_key    setll     vvarl8                                 90
     c                   endif
      *
      * Valgt overgruppe
      *
     c                   if        d_ogrp <> *zeros and
     c                             d_hgrp = *zeros and
     c                             d_ugrp = *zeros
     c     vvarl8_key1   setll     vvarl8                                 90
     c                   endif
      *
      * Valgt overgruppe/hovedgruppe
      *
     c                   if        d_ogrp <> *zeros and
     c                             d_hgrp <> *zeros and
     c                             d_ugrp = *zeros
     c     vvarl8_key2   setll     vvarl8                                 90
     c                   endif
      *
      * Valgt overgruppe/hovedgruppe/undergruppe
      *
     c                   if        d_ogrp <> *zeros and
     c                             d_hgrp <> *zeros and
     c                             d_ugrp <> *zeros
     c     vvarl8_key3   setll     vvarl8                                 90
     c                   endif
      *
      * Ingen utvalg
      *
     c                   if        d_ogrp = *zeros and
     c                             d_hgrp = *zeros and
     c                             d_ugrp = *zeros
     c     vvarl8_key    reade     vvarl8                                 90
     c                   endif
      *
      * Valgt overgruppe
      *
     c                   if        d_ogrp <> *zeros and
     c                             d_hgrp = *zeros and
     c                             d_ugrp = *zeros
     c     vvarl8_key1   reade     vvarl8                                 90
     c                   endif
      *
      * Valgt overgruppe/hovedgruppe
      *
     c                   if        d_ogrp <> *zeros and
     c                             d_hgrp <> *zeros and
     c                             d_ugrp = *zeros
     c     vvarl8_key2   reade     vvarl8                                 90
     c                   endif
      *
      * Valgt overgruppe/hovedgruppe/undergruppe
      *
     c                   if        d_ogrp <> *zeros and
     c                             d_hgrp <> *zeros and
     c                             d_ugrp <> *zeros
     c     vvarl8_key3   reade     vvarl8                                 90
     c                   endif
      *
     c                   dow       *in90 = *off
      *
      * Sjekk om varen har EAN-nr
6.11  *
6.11  * Kaller program for henting av ean-nr
6.11  *
6.11 c                   call      'VE712R'
6.11 c                   parm                    w_firm
6.11 c                   parm                    vvvare
6.11 c                   parm                    w_eann
6.11 c                   parm                    w_east
      *
      * Skriver detaljlinje
      *
     c                   if        w_east = *blank
     c                   exsr      detalj
     c                   endif
      *
      * Leser neste rad
      *
      * Ingen utvalg
     c                   if        d_ogrp = *zeros and
     c                             d_hgrp = *zeros and
     c                             d_ugrp = *zeros
     c     vvarl8_key    reade     vvarl8                                 90
     c                   endif
      *
      * Valgt overgruppe
      *
     c                   if        d_ogrp <> *zeros and
     c                             d_hgrp = *zeros and
     c                             d_ugrp = *zeros
     c     vvarl8_key1   reade     vvarl8                                 90
     c                   endif
      *
      * Valgt overgruppe/hovedgruppe
      *
     c                   if        d_ogrp <> *zeros and
     c                             d_hgrp <> *zeros and
     c                             d_ugrp = *zeros
     c     vvarl8_key2   reade     vvarl8                                 90
     c                   endif
      *
      * Valgt overgruppe/hovedgruppe/undergruppe
      *
     c                   if        d_ogrp <> *zeros and
     c                             d_hgrp <> *zeros and
     c                             d_ugrp <> *zeros
     c     vvarl8_key3   reade     vvarl8                                 90
     c                   endif
      *
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine utskrift av detaljlinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     detalj        begsr
6.31  * sjekker om logistikk-vare
6.31 c                   eval      w_lv_nobb = 0
6.31 c/exec sql
6.31 c+ select vvlnob,
6.31 c+        vvllva
6.31 c+ into   :w_lv_nobb,
6.31 c+        :w_lv_lvar
6.31 c+ from   vvlepf
6.31 c+ where  vvlvnr = :vvvare and
6.31 c+        vvlldo = :vvldor
6.31 c+ fetch first 1 rows only
6.31 c/end-exec
      *
      * Henter vareinformasjon
      *
     c                   eval      d1ogrp = vvogrp
     c                   eval      d1hgrp = vvhgrp
     c                   eval      d1ugrp = vvugrp
     c                   eval      d1ldor = vvldor
     c                   eval      d1tek1 = vvtek1
     c                   eval      d1vare = vvvare
6.31 c                   if        w_lv_nobb <> 0
6.31 c                   eval      d1nobb = w_lv_nobb
6.31 c                   eval      d1lvar = w_lv_lvar
6.31 c                   else
     c                   eval      d1nobb = vvnobb
     c                   eval      d1lvar = vvlvar
6.31 c                   endif
     c                   eval      d1lnav = *blank
      *
      * Henter leverandørnavn
      *
     c                   eval      rlevl1_levr = vvldor
     c     rlevl1_key    chain     rlevl1                             91
     c                   if        *in91 = *off
     c                   eval      d1lnav = rlnavn
     c                   endif
      *
     c                   write     d1dtl                                30
     c                   exsr      overflow
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av overflow
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     overflow      begsr
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   write     a3hdr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_list
      *
      * Key for oppslag på firma
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for les på vare-register (hele)
      *
     c     vvarl8_key    klist
     c                   kfld                    w_firm
      *
      * Key for les på vare-register (Overgruppe)
      *
     c     vvarl8_key1   klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl8_ogrp
      *
      * Key for les på vare-register (Overgruppe/Hovedgruppe)
      *
     c     vvarl8_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl8_ogrp
     c                   kfld                    vvarl8_hgrp
      *
      * Key for les på vare-register (Overgruppe/Hovedgruppe/Undergruppe)
      *
     c     vvarl8_key3   klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl8_ogrp
     c                   kfld                    vvarl8_hgrp
     c                   kfld                    vvarl8_ugrp
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      *
      * Kopierer parameterliste til datastruktur
      *
     c                   eval      d_list = p_list
      *
      * Henter dagens dato og firma fra parameter
      *
     c                   time                    w_udat
      *
     c                   eval      w_firm = d_firm
      *
     c                   endsr
