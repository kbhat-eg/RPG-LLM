     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : NG761R
      * Beskrivelse . . : Skrive til loggfile at bestilling er sendt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.
      * --------- ------  --------------------------------------------------
      *    8.xx   100822  Nytt program                              bsa
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
      *
      * Datastruktur
      *
     d                 ds
     d d_urec                        80
     d  d_firm                        3  0 overlay(d_urec)
     d  d_numm                        8  0 overlay(d_urec:4)
     d  d_suff                        2  0 overlay(d_urec:12)
     d  d_kode                        1    overlay(d_urec:14)
     d  d_date                         d   overlay(d_urec:15) datfmt(*iso)
     d  d_time                         t   overlay(d_urec:25) timfmt(*iso)
     d  d_user                       10    overlay(d_urec:33)
     d  d_wsid                       10    overlay(d_urec:43)
      *
      * Local Data Area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d lohel1_numm     s                   like(lonumm)
     d lohel1_suff     s                   like(losuff)
      *
      * Definering av variable i parametre
      *
     d p_numm          s                   like(lonumm)
     d p_suff          s                   like(losuff)
      *
      * Definering av øvrige variable
      *
     d w_firm          s                   like(lofirm)
     d w_stat          s              1
     d w_parm          s             80
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
      *
      * Definering av konstanter
      *
     d c_null          s              6    inz('000000')
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram - logg informasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Finn bestilling
      *
     c                   eval      lohel1_numm = p_numm
     c                   eval      lohel1_suff = p_suff
     c     lohel1_key    chain     lohel1r
     c                   if        not %found
     c                   goto      pgm_end
     c                   endif
      *
      * Logger i loghistorikk
      *
     c                   eval      d_firm = w_firm
     c                   eval      d_numm = p_numm
     c                   eval      d_suff = p_suff
     c                   eval      d_kode = '2'
     c                   eval      d_date = w_date
     c                   eval      d_time = w_time
     c                   eval      d_user = l_user
     c                   eval      d_wsid = l_wsid
     c                   eval      w_parm = d_urec
      *
     c                   call      'LM900R'
     c                   parm                    w_stat
     c                   parm                    w_parm
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     pgm_end       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_numm
     c                   parm                    p_suff
      *
      * Key for oppslag på ordre/suffix
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
     c                   kfld                    lohel1_suff
      *
     c                   eval      w_firm = l_firm
     c                   time                    w_date
     c                   time                    w_time
      *
     c                   endsr
