     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JV510R
      * Beskrivelse . . : Vare, vise
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       201196 HKO  Nytt program
      *       170904 HKO  Viser info om slettet i Nobb
      *       310505 HKO  Fjernet priskode og sortimentskode, og lagt inn
      *                   dokumentasjonsindikator og flagg for best.vare
      *       030605 HKO  Endret parameter ved kall av JS500R
      *       130605 HKO  Endret parameter ved kall av JP500R
      *       120905 HKO  Viser EPIC-nr, UPC-nr, andre strekkoder,
      *                   opprettet dato, endret dato og endret av
6.10  * R6.10 080909 bof  Fjernet EPIC-nr, UPC-nr, ean2
6.11  * R6.10 080909 bof  Lagt til nye koder
6.30  * R6.30 140314 bof  Dato sjekk før ut på skjerm
6.31  * R6.30 010414 bof  Utvidet med levernadør for kall til JW510R
6.32  * R6.30 251114 Bsa  Innfører F tast for visning av NOBB info. Erstatter
6.32  *                   koding i jacada.
6.3x  * 6.30  040615 bof  utvidet parm i forb. med prgr. på betingelse
7.xx  * 7.00  211116 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
8.01  *PRG2   150622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjvdtl1    if   e           k disk    rename(jvdtpfr:jvdtl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fjprol1    if   e           k disk    rename(jpropfr:jprol1r)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
      *
     fjv510d    cf   e             workstn
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_vare          s                   like(jvvare)
6.32 d p_nobb          s              8
      *
      * Definering av Local data area
      *
     d                uds
     d l_fnav                951    980
      *
      * Definering av variable i nøkler
      *
     d jvarl1_vare     s                   like(jvvare)
     d jvdtl1_dvar     s                   like(jvdvar)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d jprol1_prod     s                   like(jqprod)
     d jlevl1_ldor     s                   like(jlldor)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_lety          s              1  0
8.01 d w_prgr          s              2
      *
     d w_pldo          s                   like(jvldor)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      jvarl1_vare = p_vare
     c     jvarl1_key    chain     jvarl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      *in80 = jvstat = 4
      *
     c                   eval      c2vare = p_vare
     c                   eval      c2nobb = jvnobb
     c                   eval      c2modn = jvmodn
     c                   eval      c2ogrp = jvogrp
     c                   eval      c2hgrp = jvhgrp
     c                   eval      c2ugrp = jvugrp
     c                   eval      c2eogr = jveogr
     c                   eval      c2ehgr = jvehgr
     c                   eval      c2eugr = jveugr
     c                   eval      c2tek1 = jvtek1
     c                   eval      c2tek2 = jvtek2
     c                   eval      c2ete1 = jvete1
     c                   eval      c2ete2 = jvete2
     c                   eval      c2prod = jvprod
     c                   eval      c2pvar = jvpvar
     c                   eval      c2ldor = jvldor
     c                   eval      c2lvar = jvlvar
     c                   eval      c2ean1 = jvean1
6.10 c*                  eval      c2ean2 = jvean2
6.10 c*                  eval      c2epic = jvepic
6.10 c*                  eval      c2upcn = jvupcn
     c                   eval      c2stre = jvstre
     c                   eval      c2doki = jvdoki
     c                   eval      c2leve = jvleve
6.11 c                   eval      c2sort = jvsort
6.11 c                   eval      c2utko = jvutko
6.11 c                   eval      c2mmk1 = jvmmk1
6.11 c                   eval      c2mlk1 = jvmlk1
6.11 c                   eval      c2mmk2 = jvmmk2
6.11 c                   eval      c2mlk2 = jvmlk2
     c                   eval      c2eusr = jveusr
     c                   eval      c2dpko = *blank
     c                   eval      c2dsko = 0
      *
6.30 c                   if        jvodat <> *loval
     c     *dmy          move      jvodat        c2odat
6.30 c                   endif
6.30 c                   if        jvedat <> *loval
     c     *dmy          move      jvedat        c2edat
6.30 c                   endif
      *
     c                   if        jvadat <> *loval
     c     *dmy          move      jvadat        c2adat
     c                   endif
      *
     c                   if        jvudat <> *loval
     c     *dmy          move      jvudat        c2udat
     c                   endif
      *
     c                   eval      jvdtl1_dvar = p_vare
     c     jvdtl1_key    chain     jvdtl1
     c                   if        %found
     c                   eval      c2dpko = jvdpko
     c                   eval      c2dsko = jvdsko
     c                   endif
      *
     c                   exsr      hent_kontr
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc = *on or *inkl = *on
     c                   goto      avslutt
     c                   endif
6.32  *
6.32  * F13=NOBB informasjon
6.32  *
6.32 c                   if        *inkm = *on
6.32 c                   exsr      vis_nobb
6.32 c                   goto      c2tagb
6.32 c                   endif
      *
      * Vise forpakningskode
      *
     c                   if        *inkn = *on
     c                   call      'JW510R'
     c                   parm                    p_firm
     c                   parm                    p_vare
6.31 c                   parm                    jvldor
     c                   goto      c2tagb
     c                   endif
      *
      * Vise prisinformasjon
      *
     c                   if        *inkp = *on
     c                   call      'JX510R'
     c                   parm                    p_vare
     c                   goto      c2tagb
     c                   endif
      *
      * Vise søketekster
      *
     c                   if        *inkq = *on
     c                   call      'JS500R'
     c                   parm                    c2vare
     c                   goto      c2tagb
     c                   endif
      *
      * Vise betingelser for vare
      *
     c                   if        *inkr = *on
     c                   eval      w_pldo = 0
     c                   eval      w_lety = 0
6.3x c                   eval      w_prgr = *blank
     c                   call      'JP500R'
     c                   parm                    p_firm
     c                   parm                    c2vare
     c                   parm                    w_pldo
6.3x c                   parm                    w_prgr
     c                   parm                    w_lety
     c                   goto      c2tagb
     c                   endif
      *
      * Vise kampanjepriser
      *
     c                   if        *inks = *on
     c                   call      'JM510R'
     c                   parm                    p_firm
     c                   parm                    c2vare
     c                   parm                    c2tek1
     c                   goto      c2tagb
     c                   endif
      *
      * Vise modultekst/produktinformasjon
      *
     c                   if        *inkt = *on
     c                   call      'JU510R'
     c                   parm                    p_firm
     c                   parm                    c2vare
     c                   goto      c2tagb
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av kontroll-informasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_kontr    begsr
      *
     c                   eval      c2utxt = *blank
     c                   eval      c2eutx = *blank
     c                   eval      c2pnav = *blank
     c                   eval      c2lnav = *blank
      *
     c                   eval      vugrl1_uogr = c2ogrp
     c                   eval      vugrl1_uhgr = c2hgrp
     c                   eval      vugrl1_uugr = c2ugrp
     c     vugrl1_key    chain     vugrl1
     c                   if        %found
     c                   eval      c2utxt = vgutxt
     c                   endif
      *
     c                   if        c2eogr <> 0
     c                   eval      vugrl1_uogr = c2eogr
     c                   eval      vugrl1_uhgr = c2ehgr
     c                   eval      vugrl1_uugr = c2eugr
     c     vugrl1_key    chain     vugrl1
     c                   if        %found
     c                   eval      c2eutx = vgutxt
     c                   endif
     c                   endif
      *
     c                   eval      jprol1_prod = c2prod
     c     jprol1_key    chain     jprol1
     c                   if        %found
     c                   eval      c2pnav = jqnavn
     c                   endif
      *
     c                   eval      jlevl1_ldor = c2ldor
     c     jlevl1_key    chain     jlevl1
     c                   if        %found
     c                   eval      c2lnav = jlnavn
     c                   endif
      *
     c                   endsr
      *
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  * Vis NOBB informasjon for varen. Samme prinsipp som preview av utskrifter
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  *
6.32 c     vis_nobb      begsr
6.32 c
6.32 c                   eval      p_nobb = %char(c2nobb)
6.32 c                   call      'AP633R'
6.32 c                   parm                    p_nobb
6.32  *
6.32 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
      *
      * Key for oppslag på vare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for oppslag på vare-detalj
      *
     c     jvdtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jvdtl1_dvar
      *
      * Key for oppslag mot undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag mot produsent
      *
     c     jprol1_key    klist
     c                   kfld                    jprol1_prod
      *
      * Key for oppslag mot leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
