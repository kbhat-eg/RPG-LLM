      /TITLE  Generering av poster utfra fordelingsparametre
     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RH192R                                              *
      * Beskrivelse . . : Foretar B = Beregning                               *
      *                           P = Prisberegning (ikke i bruk foreløpig)   *
      *                           O = Omfordeling                             *
      *                   dersom konto finnes i fordelingsparametre           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      * V2.04     120398  Bygget om fordelinger i h.t. fordelingsregiter  JT  *
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frtrapf    ip   e           k disk    rename(rtrapfr:rtrapfr)
     frbunl1    if   e           k disk    rename(rbunpfr:rbunl1r)
     fraa1l1    if   e           k disk    rename(raa1pfr:raa1l1r)
     fraa4l1    uf   e           k disk    rename(raa4pfr:raa4l1r)
     frfosl1    if   e           k disk    rename(rforpfr:rfosl1r)
     frbunlu    uf a e           k disk    rename(rbunpfr:rbunlur)
     frtral1    o    e           k disk    rename(rtrapfr:rtral1r)
      *
      *  Fordelings-parametre
      *
     d rtbrud          ds                                                       Bruddb
     d  wtfirm                        3  0                                      Firma
     d  wtkont                        5  0                                      Konto
     d  wtspes                        4  0                                      Spes
     d  wtavde                        4  0                                      Avd
      *
     d rfbrud          ds                                                       Bruddb
     d  wffirm                        3  0                                      Firma
     d  wfkont                        5  0                                      Konto
     d  wfspes                        4  0                                      Spes
     d  wfavde                        4  0                                      Avd
610  d rbunlu_nivo     s                   like(rsnivo)
610  d rbunlu_memb     s                   like(rsmemb)
610  d w_mem1          s                   like(rsmemb)
610  d w_mem2          s                   like(rsmemb)
      *
     d                uds
     d rhpkda                300    305  0
     d rhprÅr                         4  0
     d rhpper                         2  0
     d rhppef                         2  0
     d rhppet                         2  0
     d rhpavf                         4  0
     d rhpavt                         4  0
     d rhpktf                         5  0
     d rhpktt                         5  0
     d rhpsrt                         1
     d rhpsml                         1
     d rhpavv                         1
     d rhpkgs                         1
     d rhpkks                         1
      *
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d dekr            s              1
     d wkraar          s              4  0
     d wsraar          s              2  0
     d wtbelØ          s                   like(rtbelØ)
     d wtmngd          s                   like(rtmngd)
     d wtvalb          s                   like(rtvalb)
     d wwbelØ          s                   like(rtbelØ)
     d wwbiln          s                   like(rtbiln)
     d wwbunt          s                   like(rsbunt)
     d wwcavd          s                   like(rtcavd)
     d wwckto          s                   like(rtckto)
     d wwcmva          s                   like(rtcmva)
     d wwcsps          s                   like(rtcsps)
     d wwdavd          s                   like(rtdavd)
     d wwdkto          s                   like(rtdkto)
     d wwdmva          s                   like(rtdmva)
     d wwdsps          s                   like(rtdsps)
     d wwfØrs          s                   like(rsfØrs)
     d wwfakt          s              7  4
     d wwlinj          s              5  0
     d wwmngd          s                   like(rtmngd)
     d wwpdeb          s                   like(rspdeb)
     d wwpkre          s                   like(rspkre)
     d wwpros          s              5  4
     d wwprot          s              5  4
     d wwpsum          s                   like(rspsum)
     d wwsist          s                   like(rssist)
     d wwtext          s                   like(rttext)
     d wwtx17          s             17
     d wwvalb          s                   like(rtvalb)
     d xbelØp          s                   like(rtbelØ)
     d xmengd          s                   like(rtmngd)
     d xvalbe          s                   like(rtvalb)
      *
     d p_stat          s              1
     d p_akod          s              2
     d p_atxt          s             20
      *
      *  Posteringer inn
      *
     irtrapfr
     i                                          rtfirm        l3
     i                                          rtbunt        l1
      *
      /eject
      *
     c     dummy         tag
      *
610   *
610  c                   if        rtmemb > w_mem1
610  c                   move      *on           *inlr
610  c                   goto      ut
610  c                   endif
610   *
610  c                   if        rtmemb <> w_mem1
610  c                   move      *off          *inl1
610  c                   move      *off          *inl3
610  c                   goto      ut
610  c                   endif
      *  Hent statusopplysninger
      *
     c                   if        *inl3 = *on
     c                   move      rtfirm        ra1fir
     c     keyaa1        chain     raa1l1                             52
     c                   endif
      *
      *  Sjekk om bunt finnes/utgått
      *
     c                   if        *inl1 = *on
     c                   movel     rtfirm        rsfirm
     c                   movel     rtbunt        rsbunt
610  c                   eval      rbunlu_nivo = *blank
610  c                   eval      rbunlu_memb = w_mem1
     c     keybun        chain     rbunl1                             53
     c                   endif
      *
     c                   if        *inl1 = *on and
     c                             *in53 = *off
     c                   eval      *in53 = rsstat = 'U'
     c                   endif
      *
      *  Finn ledig buntnr.
      *
     c                   if        *inl1 = *on
610  c                   eval      rbunlu_nivo = *blank
610  c                   eval      rbunlu_memb = w_mem1
     c                   move      rtfirm        rsfirm                         Firma
     c                   move      99999         rsbunt                         Type
     c     keybun        setgt     rbunlu
     c                   readp     rbunlu                                 10
     c                   endif
      *
     c                   if        *inl1 = *on
      *
     c                   if        *in10 = *on
     c                   eval      wwbunt = 1
     c                   else
     c                   eval      wwbunt = rsbunt + 1
     c                   endif
      *
     c                   eval      wwpsum = 0                                                .
     c                   eval      wwfØrs = 2                                                .
     c                   eval      wwsist = 1                                                .
     c                   eval      wwlinj = 1
      *
      * Hent sist brukte bilagsnummer
      *
     c                   move      rsrper        wsraar
      *
     c                   if        wsraar > 80
     c                   move      1900          wkraar
     c                   else
     c                   move      2000          wkraar
     c                   endif
      *
     c                   move      wsraar        wkraar
     c                   endif
      *
      * Hent sist brukte bilagsnummer
      *
     c                   if        *inl3 = *on
     c                   move      rtfirm        ra4fir
     c                   move      wkraar        ra4aar
     c     keyaa4        chain     raa4l1                             60
      *
     c                   if        *in60 = *off                                          .....
     c                   eval      wwbiln = ra4sgb + 1                                       .
     c                   move      wwbiln        ra4sgb                                      .
     c                   update    raa4l1r                                                   .
     c                   endif                                                           .....
     c                   endif
      *
     c                   if        *in53 = *on
     c                   goto      ut
     c                   endif
      *
      *  Hent bilagstekst hvis blank
      *
     c                   if        rttext = *blanks                                      .....
     c                   move      rtbilk        p_akod                                      .
     c                   call      'RS701R'                                      bilags-
     c                   parm                    p_stat
     c                   parm                    p_akod
     c                   parm                    p_atxt
      *
     c                   if        p_stat <> 'N'                                Kode
     c                   move      p_atxt        rttext                          FINNES      .
     c                   endif                                                               .
      *
     c                   endif                                                           .....
      *
      *  LEGGER UT PERIODE PR. TRANS
      *
     c                   move      rsrper        rtrper                                      .
     c                   move      wwbiln        rtbiln                                      .
      *
      * Ta vare på kontobegrep/mva/tekst
      *
     c                   move      rtdkto        wwdkto
     c                   move      rtdsps        wwdsps
     c                   move      rtdavd        wwdavd
     c                   move      rtdmva        wwdmva
     c                   move      rtbelØ        wwbelØ
     c                   move      rtvalb        wwvalb
     c                   move      rtmngd        wwmngd
     c                   move      rtckto        wwckto
     c                   move      rtcsps        wwcsps
     c                   move      rtcavd        wwcavd
     c                   move      rtcmva        wwcmva
     c                   move      rttext        wwtext
     c                   movel     rttext        wwtx17
      *
     c     debet         tag
      *
      *   Kontroller om debetkonto finnes                  *
      *   i fordelingsregisteret                           *
      *
     c                   eval      wtbelØ = 0
     c                   eval      wtvalb = 0
     c                   eval      wtmngd = 0
      *
     c                   if        wwdkto <> *zero                                       .....
     c                   move      wwdkto        wtkont                                      .
     c                   move      wwdsps        wtspes                                      .
     c                   move      wwdavd        wtavde                                      .
     c                   move      rtfirm        rffirm                                      .
     c                   move      wwdkto        rfkont                                      .
     c                   move      wwdsps        rfspes                                      .
     c                   move      wwdavd        rfavde                                      .
     c                   eval      rflinj = *zero                                            .
     c                   move      'D'           dekr                                        .
     c     keyfor        setll     rfosl1                             54                     .
      *                                                                 .
      * Les fordelingsregister                                          .
      *                                                                 .
     c                   read      rfosl1                                 54                 .
      *                                                                 .
     c                   if        *in54 = *off                                        ..... .
     c                   move      rfkont        wfkont                                    . .
     c                   move      rfspes        wfspes                                    . .
     c                   move      rfavde        wfavde                                    . .
      *                                                               . .
      *   Konto finnes i fordelingsregister                           . .
      *                                                               . .
     c                   if        rfbrud = rtbrud                                     ... . .
      *                                                             . . .
      * Beregn faktor basert på %-andel                             . . .
      *                                                             . . .
     c                   if        rfkode = 'B'                                  ....    . . .
     c                   eval      wwpros = rfpros / 100                            .    . . .
     c                   endif                                                   ....    . . .
     c                   if        rfkode = 'O'                                  .....   . . .
     c                   eval      wwpros = rfpros / 100                             .   . . .
     c                   endif                                                   .....   . . .
     c                   endif                                                         ... . .
     c                   endif                                                         ..... .
      *                                                                 .
      * Les neste fordeling-rec for B, O eller P                        .
      *                                                                 .
     c     nydeb         tag                                                                 .
      *                                                                 .
     c                   read      rfosl1                                 54                 .
      *
     c                   if        *in54 = *off                                        ..... .
     c                   move      rfkont        wfkont                                    . .
     c                   move      rfspes        wfspes                                    . .
     c                   move      rfavde        wfavde                                    . .
      *                                                               . .
      * Sjekk om fortsatt innenfor samme frabegrep                    . .
      *                                                               . .
     c                   if        rfbrud = rtbrud                                     ... . .
      *                                                             . . .
      * Test funksjon                                               . . .
      *                                                             . . .
      * Beregning                                                   . . .
      *                                                             . . .
     c                   if        rfkode = 'B'                                          . . .
     c                   exsr      beregn                                                . . .
     c                   endif                                                           . . .
      *                                                             . . .
      * Prissetting                                                 . . .
      *                                                             . . .
     c                   if        rfkode = 'P'                                          . . .
     c                   exsr      pris                                                  . . .
     c                   endif                                                           . . .
      *                                                             . . .
      * Omfordeling                                                 . . .
      *                                                             . . .
     c                   if        rfkode = 'O'                                          . . .
     c                   eval      wwprot = rfprot / 100                                 . . .
     c                   eval      wwfakt = wwpros * wwprot                              . . .
     c                   exsr      omford                                                . . .
     c                   endif                                                           . . .
      *                                                             . . .
     c                   goto      nydeb                                                 . . .
     c                   endif                                                         ... . .
     c                   endif                                                         ..... .
     c                   endif                                                           .....
      *
      *DEBET************************************************
      *   Opprinnelig post blir skrevet ved B og P mens    *
      *   rest blir skrevet ved O (Omfordeling)            *
      ******************************************************
      *
     c                   eval      rtbelØ = wwbelØ - wtbelØ
     c                   eval      rtvalb = wwvalb - wtvalb
     c                   eval      rtmngd = wwmngd - wtmngd
     c                   move      wwdkto        rtdkto
     c                   move      wwdsps        rtdsps
     c                   move      wwdavd        rtdavd
     c                   move      wwdmva        rtdmva
     c                   eval      rtckto = *zero                                            .
     c                   eval      rtcsps = *zero                                            .
     c                   eval      rtcavd = *zero                                            .
     c                   move      wwtext        rttext
      *
     c     kredit        tag
      *
      ******************************************************
      *   Kontroller om kreditkonto finnes                 *
      *   i fordelingsregisteret                           *
      ******************************************************
      *
     c                   eval      wtbelØ = 0
     c                   eval      wtvalb = 0
     c                   eval      wtmngd = 0
      *
     c                   if        wwckto <> *zero                                       .....
     c                   move      rtfirm        rffirm                                      .
     c                   move      wwckto        wtkont                                      .
     c                   move      wwcsps        wtspes                                      .
     c                   move      wwcavd        wtavde                                      .
     c                   move      wwckto        rfkont                                      .
     c                   move      wwcsps        rfspes                                      .
     c                   move      wwcavd        rfavde                                      .
     c                   eval      rflinj = *zero                                            .
     c                   move      'K'           dekr                                        .
     c     keyfor        setll     rfosl1                             54                     .
      *                                                                 .
      * Les fordelingsregister                                          .
      *                                                                 .
     c                   read      rfosl1                                 54                 .
      *                                                                 .
     c                   if        *in54 = *off                                        ..... .
     c                   move      rfkont        wfkont                                    . .
     c                   move      rfspes        wfspes                                    . .
     c                   move      rfavde        wfavde                                    . .
      *                                                               . .
      *   Konto finnes i fordelingsregister                           . .
      *                                                               . .
     c                   if        rfbrud = rtbrud                                     ... . .
      *                                                             . . .
      * Beregn faktor basert på %-andel                             . . .
      *                                                             . . .
     c                   if        rfkode = 'B'                                  ....    . . .
     c                   eval      wwpros = rfpros / 100                            .    . . .
     c                   endif                                                   ....    . . .
      *
     c                   if        rfkode = 'O'                                  .....   . . .
     c                   eval      wwpros = rfpros / 100                             .   . . .
     c                   endif                                                   .....   . . .
      *                                                             . . .
     c                   endif                                                         ... . .
     c                   endif                                                         ..... .
      *                                                                 .
      * Les neste fordeling-rec for B, O eller P                        .
      *                                                                 .
     c     nykre         tag                                                                 .
      *                                                                 .
     c                   read      rfosl1                                 54                 .
      *
     c                   if        *in54 = *off                                        ..... .
     c                   move      rfkont        wfkont                                    . .
     c                   move      rfspes        wfspes                                    . .
     c                   move      rfavde        wfavde                                    . .
      *                                                               . .
      * Sjekk om fortsatt innenfor samme frabegrep                    . .
      *                                                               . .
     c                   if        rfbrud = rtbrud                                     ... . .
      *                                                             . . .
      * Test funksjon                                               . . .
      *                                                             . . .
      * Beregning                                                   . . .
      *                                                             . . .
     c                   if        rfkode = 'B'                                          . . .
     c                   exsr      beregn                                                . . .
     c                   endif                                                           . . .
      *                                                             . . .
      * Prissetting                                                 . . .
      *                                                             . . .
     c                   if        rfkode = 'P'                                          . . .
     c                   exsr      pris                                                  . . .
     c                   endif                                                           . . .
      *                                                             . . .
      * Omfordeling                                                 . . .
      *                                                             . . .
     c                   if        rfkode = 'O'                                          . . .
     c                   eval      wwprot = rfprot / 100                                 . . .
     c                   eval      wwfakt = wwpros * wwprot                              . . .
     c                   exsr      omford                                                . . .
     c                   endif                                                           . . .
      *                                                             . . .
     c                   goto      nykre                                                 . . .
     c                   endif                                                         ... . .
     c                   endif                                                         ..... .
     c                   endif                                                           .....
      *
      *KREDIT***********************************************
      *   Opprinnelig post blir skrevet ved B og P mens    *
      *   rest blir skrevet ved O (Omfordeling)            *
      ******************************************************
      *
     c                   eval      rtbelØ = wwbelØ - wtbelØ
     c                   eval      rtvalb = wwvalb - wtvalb
     c                   eval      rtmngd = wwmngd - wtmngd
     c                   move      wwcavd        rtcavd
     c                   move      wwckto        rtckto
     c                   move      wwcsps        rtcsps
     c                   move      wwcmva        rtcmva
     c                   eval      rtdkto = *zero                                            .
     c                   eval      rtdsps = *zero                                            .
     c                   eval      rtdavd = *zero                                            .
     c                   move      wwtext        rttext
      *
      ******************************************************
      *  AVSLUTT DENNE TRANS                               *
      ******************************************************
      *
     c     ut            tag                                                    ****
      *
      *  Oppdater buntrecord i posteringsfile
      *
     c***                move      'B'           rsreid
     cl1                 move      rtfirm        rsfirm
     cl1                 move      wwbunt        rsbunt
     cl1                 eval      rsstat = *blank
     cl1                 move      rhprÅr        rsrper
     cl1                 movel     rhpper        rsrper
     cl1                 move      wwfØrs        rsfØrs
     cl1                 move      wwsist        rssist
     cl1   *dmy          move      rhpkda        rspdat
     cl1                 move      wwpsum        rsbsum
     cl1                 move      wwpsum        rspsum
     cl1                 move      wwpdeb        rspdeb
     cl1                 move      wwpkre        rspkre
610  cl1                 eval      rsnivo = *blank
610  cl1                 eval      rsmemb = w_mem1
610  cl1                 time                    rsedat
610  cl1                 time                    rsetim
610  cl1                 eval      rseusr = l_user
      *
     cl1                 write     rbunlur                                       SKRIV BUNT
      /eject
      *
     c     beregn        begsr
      *
      ******************************************************
      * Foretar beregning og legger ut                     *
      * en ny postering med debet- eller kreditkonto       *
      ******************************************************
      *
     c                   eval(h)   rtbelØ = wwbelØ * wwpros
     c                   eval      rtvalb = *zero
     c                   eval      rtmngd = *zero
      *
     c                   move      wwtx17        rttext
     c                   movel     'B+ '         rttext
     c                   move      wwdmva        rtdmva
     c                   move      wwcmva        rtcmva
      *
     c                   if        dekr = 'D'                                            .....
     c                   eval      rtdkto = *zero
     c                   move      rfkto1        rtdkto
     c                   move      rfavd1        rtdavd
     c                   move      rfsps1        rtdsps
     c                   eval      rtckto = *zero
     c                   move      rfkto2        rtckto
     c                   move      rfavd2        rtcavd
     c                   move      rfsps2        rtcsps
      *
     c                   else
      *
     c                   eval      rtckto = *zero
     c                   move      rfkto1        rtckto
     c                   move      rfavd1        rtcavd
     c                   move      rfsps1        rtcsps
     c                   eval      rtdkto = *zero
     c                   move      rfkto2        rtdkto
     c                   move      rfavd2        rtdavd
     c                   move      rfsps2        rtdsps
     c                   endif                                                           ...
      *
     c                   if        rtdkto = *zero                                        .....
     c                             and rtdkto = *zero                                        .
     c                   goto      endber                                                    .
     c                   endif                                                           .....
      *
     c                   if        rtbelØ > *zero                                        .....
     c                             or rtvalb > *zero                                         .
     c                             or rtmngd > *zero                                         .
     c                   eval      wwsist = wwsist + 1                                       .
     c                   eval      wwlinj = wwlinj + 1                                       .
     c                   eval      rtbunt = wwbunt                                           .
     c                   eval      rtlinj = wwlinj                                           .
      *
610  c                   eval      rtnivo = *blank
610  c                   eval      rtmemb = w_mem1
610  c                   time                    rtedat
610  c                   time                    rtetim
610  c                   eval      rteusr = l_user
     c                   write     rtral1r                                                   .
      *
     c                   if        rtdkto <> *zero                                       ... .
     c                   eval      wwpsum = wwpsum + rtbelØ                                . .
     c                   eval      wwpdeb = wwpdeb + rtbelØ                                . .
     c                   endif                                                           ... .
      *
     c                   if        rtckto <> *zero                                       ... .
     c                   eval      wwpkre = wwpkre + rtbelØ                                . .
     c                   endif                                                           ... .
     c                   endif                                                           .....
      *
     c     endber        endsr
      *
      /eject
      *
     c     pris          begsr
      *
      ******************************************************
      * Foretar prisberegning og legger ut                 *
      * en ny postering med debet- eller kreditkonto       *
      ******************************************************
      *
     c                   eval(h)   rtbelØ = wwmngd * rfpris
     c                   eval      rtvalb = *zero
     c                   move      wwmngd        rtmngd
      *
     c                   move      wwtx17        rttext
     c                   movel     'P+ '         rttext
      *
     c                   if        rtbelØ > *zero                                        .....
     c                             or rtvalb > *zero                                         .
     c                             or rtmngd > *zero                                         .
610  c                   eval      rtnivo = *blank
610  c                   eval      rtmemb = w_mem1
610  c                   time                    rtedat
610  c                   time                    rtetim
610  c                   eval      rteusr = l_user
     c                   write     rtral1r                                                   .
     c                   endif                                                           .....
      *
     c                   endsr
      *
     c/eject
      *
     c     omford        begsr
      *
      ******************************************************
      * Foretar omfordeling og legger ut                   *
      * en ny postering med debet- eller kreditkonto       *
      ******************************************************
      *
     c                   eval(h)   rtbelØ = wwbelØ * wwfakt
     c                   eval(h)   rtvalb = wwvalb * wwfakt
     c                   eval(h)   rtmngd = wwmngd * wwfakt
      *
      * Akkumuler fordelte summer
      *
     c                   eval      wtbelØ = wtbelØ + rtbelØ
     c                   eval      wtvalb = wtvalb + rtvalb
     c                   eval      wtmngd = wtmngd + rtmngd
      *
      * Sjekk om avrunding
      *
     c                   exsr      avrund
      *
     c                   move      wwtx17        rttext
     c                   movel     'O+ '         rttext
      *
     c                   if        dekr = 'D'
     c                   eval      rtdkto = *zero
     c                   move      rfkto1        rtdkto
     c                   move      rfavd1        rtdavd
     c                   move      rfsps1        rtdsps
     c                   move      rfkont        rtckto
     c                   move      rfavde        rtcavd
     c                   move      rfspes        rtcsps
     c                   eval      rtcmva = *blank
      *
     c                   else
      *
     c                   eval      rtckto = *zero
     c                   move      rfkto1        rtckto
     c                   move      rfavd1        rtcavd
     c                   move      rfsps1        rtcsps
     c                   move      rfkont        rtdkto
     c                   move      rfavde        rtdavd
     c                   move      rfspes        rtdsps
     c                   eval      rtdmva = *blank
     c                   endif
      *
     c                   if        rtbelØ > *zero                                        .....
     c                             or rtvalb > *zero                                         .
     c                             or rtmngd > *zero                                         .
     c                   eval      wwsist = wwsist + 1                                       .
     c                   eval      wwlinj = wwlinj + 1                                       .
     c                   eval      rtbunt = wwbunt                                           .
     c                   eval      rtlinj = wwlinj                                           .
610  c                   eval      rtnivo = *blank
610  c                   eval      rtmemb = w_mem1
610  c                   time                    rtedat
610  c                   time                    rtetim
610  c                   eval      rteusr = l_user
     c                   write     rtral1r                                                   .
      *
     c                   if        rtdkto <> *zero                                       ... .
     c                   eval      wwpsum = wwpsum + rtbelØ                                . .
     c                   eval      wwpdeb = wwpdeb + rtbelØ                                . .
     c                   endif                                                           ... .
      *
     c                   if        rtckto <> *zero                                       ... .
     c                   eval      wwpkre = wwpkre + rtbelØ                                . .
     c                   endif                                                           ... .
     c                   endif                                                           .....
      *
     c                   endsr
      *
     c     avrund        begsr
      *
      *-------------------------------------------------------------------
      *  A V R U N D :  Hvis beløp/mengde som gjenstår til fordeling er
      *                 mindre enn 1, betraktes dette som avrunding
      *-------------------------------------------------------------------
      *
     c                   eval      xbelØp = wwbelØ - wtbelØ
     c                   eval      xvalbe = wwvalb - wtvalb
     c                   eval      xmengd = wwmngd - wtmngd
      *
     c                   if        xbelØp <= 1                                           .....
     c                   eval      rtbelØ = rtbelØ + xbelØp                                  .
     c                   eval      wtbelØ = wtbelØ + xbelØp                                  .
     c                   eval      xbelØp = 0                                                .
     c                   endif                                                           .....
      *
     c                   if        xvalbe <= 1                                           .....
     c                   eval      rtvalb = rtvalb + xvalbe                                  .
     c                   eval      wtvalb = wtvalb + xvalbe                                  .
     c                   eval      xvalbe = 0                                                .
     c                   endif                                                           .....
      *
     c                   if        xmengd <= 1                                           .....
     c                   eval      rtmngd = rtmngd + xmengd                                  .
     c                   eval      wtmngd = wtmngd + xmengd                                  .
     c                   eval      xmengd = 0                                                .
     c                   endif                                                           .....
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
610  c     *entry        plist
610  c                   parm                    w_mem1                         user
610  c                   parm                    w_mem2                         user
      *
     c                   eval      wwlinj = 1
     c                   eval      rtbrud = *zero
     c                   eval      rfbrud = *zero
      *
      * Key for fordelings-parametre
      *
     c     keyfor        klist
     c                   kfld                    rffirm
     c                   kfld                    rfkont
     c                   kfld                    rfspes
     c                   kfld                    rfavde
     c                   kfld                    rflinj
      *
      * Key for buntregister
      *
     c     keybun        klist
610  c                   kfld                    rbunlu_nivo
610  c                   kfld                    rbunlu_memb
     c                   kfld                    rsfirm
     c                   kfld                    rsbunt
      *
      * Key for statusopplysninger
      *
     c     keyaa1        klist
     c                   kfld                    ra1fir
      *
     c     keyaa4        klist
     c                   kfld                    ra4fir
     c                   kfld                    ra4aar
      *
     c                   endsr
