     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VD772R
      * Beskrivelse . . : Varesortiment-sjekker om vare finnes i et sortiment
      *                    av type = P og om den er innen for perioden
      *                    p_stat  = 1 = vare er i en sess.vare og den er
      *                    utenfor periode
      *
      *                   LR-flagg må være lik '1' for at *inLR ikke skal
      *                   settes
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       091110 bof  Nytt program
6.20  *       041111 bof  Skal bruke uke i stedet for fra til dato
6.21  *       300415 bof  Setter LR flagg slik at program ikke lukkes for
6.21  *                   hvert kall.
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvsdtl6    if   e           k disk    rename(vsdtpfr:vsdtl6r)
     fvsdtl1    if   e           k disk    rename(vsdtpfr:vsdtl1r)
     fvshel3    if   e           k disk    rename(vshepfr:vshel3r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
      *
      * Definering av Local data area *LDA
      *
     d                uds
     d l_fgrp                931    933
     d l_user                911    920
      *
      * Definering av variable i nøkler
      *
     d vshel3_type     s                   like(vctype)
     d vsdtl6_vsor     s                   like(vdvsor)
     d vsdtl6_lage     s                   like(vdlage)
     d vsdtl6_vare     s                   like(vdvare)
     d vsdtl1_vsor     s                   like(vdvsor)
     d vsdtl1_lage     s                   like(vdlage)
     d vsdtl1_ldor     s                   like(vdldor)
     d vsdtl1_ogrp     s                   like(vdogrp)
     d vsdtl1_hgrp     s                   like(vdhgrp)
     d vsdtl1_ugrp     s                   like(vdugrp)
     d vsdtl1_modn     s                   like(vdmodn)
     d vsdtl1_vare     s                   like(vdvare)
     d vvarl1_vare     s                   like(vvvare)
      *
     d b_sess          s              1    inz(*off)
     d b_funn          s              1    inz(*off)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vdfirm)
     d w_udat          s                   like(vdedat)
6.20 d w_fdat          s                   like(vdedat)
6.20 d w_tdat          s                   like(vdedat)
     d w_ldor          s                   like(vvldor)
     d w_ogrp          s                   like(vvogrp)
     d w_hgrp          s                   like(vvhgrp)
     d w_ugrp          s                   like(vvugrp)
     d w_modn          s                   like(vvnmod)
     d w_vare          s                   like(vvvare)
      *
     d p_firm          s                   like(vdfirm)
     d p_vare          s                   like(vdvare)
     d p_lage          s                   like(vdlage)
     d p_stat          s              1
     d p_inlr          s              1
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1
      *
     c                   eval      p_stat = *blank
     c                   eval      b_sess = *off
     c                   eval      b_funn = *off
      *
      *leser all sortiment med type = C   - med evt.lager
      *
     c                   eval      vshel3_type = 'P'
     c     vshel3_key    setll     vshel3
     c     vshel3_key    reade     vshel3
     c                   dow       not %eof(vshel3)
      *
     c                   if        vclage = p_lage
6.20 c                   exsr      sjekk_dato
     c                   exsr      sjekk_vsor
     c                   if        b_sess = *on
     c                   eval      p_stat = '1'
     c                   goto      avslutt
     c                   endif
     c                   if        b_funn = *on
     c                   goto      avslutt
     c                   endif
     c                   endif
      *
     c     vshel3_key    reade     vshel3
     c                   enddo
      *
      *leser all sortiment med type = C   - evt. uten lager
      *
     c                   eval      vshel3_type = 'P'
     c     vshel3_key    setll     vshel3
     c     vshel3_key    reade     vshel3
     c                   dow       not %eof(vshel3)
      *
     c                   if        vclage = 0
     c                   exsr      sjekk_vsor
     c                   if        b_sess = *on
     c                   eval      p_stat = '1'
     c                   goto      avslutt
     c                   endif
     c                   endif
      *
     c     vshel3_key    reade     vshel3
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   if        p_inlr <> *on
6.21 c                   call      'VC701R  '
6.21 C                   parm                    vcfuke
6.21 C                   parm                    vctuke
6.21 C                   parm                    w_fdat
6.21 C                   parm                    w_tdat
6.21 C                   parm                    p_inlr
     c                   eval      *inlr = *on
     c                   endif
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sjekke om varen er i sortiment for P - periodestyrte
      * for valgt sortiment og lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_vsor    begsr
      *
      * sjekker først vare
      *
     c                   eval      b_sess = *off
     c                   eval      vsdtl6_vsor = vcvsor
     c                   eval      vsdtl6_lage = vclage
     c                   eval      vsdtl6_vare = p_vare
     c     vsdtl6_key    setll     vsdtl6
     c     vsdtl6_key    reade     vsdtl6
     c                   dow       not %eof(vsdtl6)
      *
     c                   eval      b_funn = *on
6.20 c                   if        w_udat < w_fdat or
6.20 c                             w_udat > w_tdat
     c                   eval      b_sess = *on
     c                   leavesr
     c                   endif
      *
     c     vsdtl6_key    reade     vsdtl6
     c                   enddo
     c                   eval      vsdtl1_vsor = vcvsor
     c                   eval      vsdtl1_lage = vclage
     c                   eval      vsdtl1_ldor = vvldor
     c                   eval      vsdtl1_ogrp = vvogrp
     c                   eval      vsdtl1_hgrp = vvhgrp
     c                   eval      vsdtl1_ugrp = vvugrp
     c                   eval      vsdtl1_modn = vvnmod
     c                   eval      vsdtl1_vare = *blank
     c     vsdtl1_key    chain     vsdtl1
     c                   if        not %found
     c                   eval      vsdtl1_modn = 0
     c     vsdtl1_key    chain     vsdtl1
     c                   if        not %found
     c                   eval      vsdtl1_ugrp = 0
     c     vsdtl1_key    chain     vsdtl1
     c                   if        not %found
     c                   eval      vsdtl1_hgrp = 0
     c     vsdtl1_key    chain     vsdtl1
     c                   if        not %found
     c                   eval      vsdtl1_ogrp = 0
     c     vsdtl1_key    chain     vsdtl1
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
     c                   if        not %found
     c                   leavesr
     c                   endif
      *
     c                   eval      b_funn = *on
6.20 c                   if        w_udat < w_fdat or
6.20 c                             w_udat > w_tdat
     c                   eval      b_sess = *on
     c                   leavesr
     c                   endif
      *
      *
     c                   endsr
6.20  *
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  * Subrutine for å finne ekstakte  datoer ut fra uke nr.
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  *
6.20 c     sjekk_dato    begsr
6.20  *
6.20 c                   call      'VC701R  '
6.20 C                   parm                    vcfuke
6.20 C                   parm                    vctuke
6.20 C                   parm                    w_fdat
6.20 C                   parm                    w_tdat
6.21 C                   parm                    p_inlr
6.20  *
6.20 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_lage
     c                   parm                    p_vare
     c                   parm                    p_stat
     c                   parm                    p_inlr
      *
      * Key for les av varer fra sortiment
      *
     c     vsdtl6_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vsdtl6_vsor
     c                   kfld                    vsdtl6_lage
     c                   kfld                    vsdtl6_vare
      *
      * Key for les av soritment hode pr. type
      *
     c     vshel3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vshel3_type
      *
      * Key for les/oppslag på varesortiment-linje
      *
     c     vsdtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vsdtl1_vsor
     c                   kfld                    vsdtl1_lage
     c                   kfld                    vsdtl1_ldor
     c                   kfld                    vsdtl1_ogrp
     c                   kfld                    vsdtl1_hgrp
     c                   kfld                    vsdtl1_ugrp
     c                   kfld                    vsdtl1_modn
     c                   kfld                    vsdtl1_vare
      *
      * Key for les av vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   time                    w_udat
      *
     c                   endsr
