     H decedit('0.') datedit(*dmy-) option(*nodebugio)
     h DftActgrp(*No) BndDir('CGIDEV2/CGIDEV2')
      /copy hspecsbnd
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASPGPL
      * Program . . . . : AF728R
      * Beskrivelse . . : Lager xml til mail utsendelse med vedlegg
      *
      * eksempel på bruk:
      *
      *  c                   eval      p_filn = '/home/adb630/af728test.xlsx'
      *  c                   eval      p_fnav = 'excelVedlegg.xlsx'
      *  c                   eval      p_mime = 'application/vnd.ms-excel'
      *  c                   call      'AF729R'
      *  c                   parm                    p_filn
      *  c                   parm                    p_fnav
      *  c                   parm                    p_mime
      *
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       090419 bkv  Nytt program. Kopiert fra AF728R
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fra09lr    if   e           k disk    rename(ra09pfr:ra09lrr)
     ffusrl1    if   e           k disk    rename(fusrpfr:fusrl1r)
     fafpslr    if   e           k disk    rename(afpspfr:afpslrr)
      *
     d p_filn          s            256
     d p_fnav          s             50
     d p_mime          s            150
     d p_filg          s             10
     d p_xmlf          s            256
      *
      *
      * Definering av variabler i nøkler
      *
     d afpslr_ufil     s                   like(afufil)
     d afpslr_uide     s                   like(afuide)
     d fusrl1_user     s                   like(fbuser)
     d ra09lr_ikod     s                   like(raikod)
      *
     d w_path          s             50
     d w_xmlp          s             50
     d w_kode          s              1
     d w_firm          s              3  0
     d w_fell          s              6
     d w_type          s              2
     d w_fast          s             10
6.nu d w_numm          s              8  0
     d w_jnav          s             15
     d w_jkey          s             41
     d w_jstat         s              2  0
     d w_jtext         s            200
     d w_jtxt          s             35
     d w_tims          s               z
     d w_dtaq          s              1
     d w_attac         s            256a
     d w_anavn         s            256a
     d w_mime          s            150
     d jasper_logo     s            256a   varying
     d w_rtyp          s             50
     d w_spol          s              5
     d d_pdf_ds        ds
     d  d_xmlm                       75
     d  d_jasm                       75
     d  d_logo                       75
     d  d_path                      100
     d  d_emal                       50
     d  d_fifo                       75
     d  d_fkop                        1
     d  d_dtaq                        1
     d  d_sign                        1
      *
      *  UDS Local Data Area
      *
     d                uds
     d l_bach                595    635
     d l_user                911    920
     d l_filg                931    933
     d l_firm                944    946  0
      *
      *
     d XML_Output      s            256a   Varying
     d XML_path        s            256a
     d Out_path        s            256a
      *
     d crlf            c                   const(X'0d25')
      *
     d                 ds
     d w_dato                        26
     d  w_date                       10    overlay(w_dato:1)
     d  w_ski1                        1    overlay(w_dato:11)
     d  w_time                        2    overlay(w_dato:12)
     d  w_ski2                        1    overlay(w_dato:14)
     d  w_minu                        2    overlay(w_dato:15)
     d  w_ski3                        1    overlay(w_dato:17)
     d  w_seku                        9    overlay(w_dato:18)
      *
      *
      /copy prototypeb
      /copy usec
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hvis vi ikke har path til xml eller IFS avslutter vi uten videre
     c                   if        XML_path = *blanks or
     c                             OUT_path = *blanks
     c                   goto      avslutt
     c                   endif
      * Hent inn mal
     c                   exsr      xml_env
      *
      * Skriv rapport type
      *
     c*                   eval      w_rtyp = 'XmlExcelDocumentReportCommand'
     c*                   eval      d_jasm = *blank
     c*                   eval      w_spol = 'true'
     c*                   eval      jasper_logo = *blanks
       //        UpdHTMLVar('Reportcommandtype':   w_rtyp);
       //        UpdHTMLVar('Jaspertemplate':      d_jasm);
       //        UpdHTMLVar('Logo':           jasper_logo);
       //        UpdHTMLVar('Keepspool':           w_spol);
       //        WrtSection('ReportCommand');
      *
      * Legger ut informasjon om vedlegg
      *
               if (w_attac <> *blank);
                 // Start med vedlegg
                 WrtSection('StartAttachment');
                 // Vedleggs informasjon
                 UpdHTMLVar('attachmenturl':     %trim(w_attac));
                 UPDHTMLVar('failifnotfound':    'false');
                 UPDHTMLVar('documentmimetype':  %trim(w_mime));
                 UPDHTMLVar('name':              %trim(w_anavn));
                 WrtSection('AttachmentLine');
                 // Slutt med vedlegg
                 WrtSection('EndAttachment');
               endif;
      *
      *
      * Skriv xml til IFS
      *
     c                   exsr      xml_end
     c                   eval      l_bach = w_jkey
      *
6.30  * Vi kaller program for launch av PDF i browser
6.30 c                   call      'AP621R'
6.30 c                   parm                    l_bach
      *
      * Avslutt jobbiden
      *
     c                   call      'AB710R'
     c                   parm                    w_jkey
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av mal
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xml_env       begsr
      *
      * Hent inn xml templaten
      *
     c                   eval      w_xmlp = %trim(XML_path)
      *
     c                   callp     GetHTMLIFS(w_xmlp :'<AS400>')
     c                   callp     ClrHtmlBuffer
      *
      /Free
           // Finne nytt batch nummer
               UpdHTMLVar('BatchNo':                  w_jkey);
               UpdHTMLVar('Batchtype':               'VEDLEGG');
               UpdHTMLVar('Username':                 l_user);
               UpdHTMLVar('Batchtime':                w_dato);
               WrtSection('Topp');

           // Finne credentials
               UpdHTMLVar('Schema':             'ADB'+l_filg);
               UpdHTMLVar('Companyno':         %char(l_firm));
               WrtSection('Credential');
      /End-free
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for håndtering av linjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xml_end       begsr
      *
      /Free
           // Skriv siste tagg
               WrtSection('Footer');

      /End-free
      *
      * Lag navn til xml fila
      *
     c                   eval      w_path = %trim(Out_path)
     c                   eval      XML_Output = %trim(w_path)+l_filg+'/'+
     c                             'Vedlegg_' + %trim(w_jkey) + '.xml'
      *
     c                   callp     WrtHtmlToStmf(XML_Output: 819)
      * Skriver til datakø for å aktivere sniffer, hvis dataq er aktiv
     c                   if        w_dtaq = '1'
     c                   eval      p_xmlf = xml_output
     c                   eval      p_filg = 'ADB'+l_filg
     c                   call      'AP629C'
     c                   parm                    p_filg
     c                   parm                    p_xmlf
     c                   endif
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_filn
     c                   parm                    p_fnav
     c                   parm                    p_mime
      *
      * Key for oppslag på bruker
      *
     c     fusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fusrl1_user
      *
      * Key for oppslag på selger-register
      *
     c     ra09lr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra09lr_ikod
      *
      * Key for oppslag på AFPS
      *
     c     afpslr_key    klist
     c                   kfld                    afpslr_ufil
     c                   kfld                    afpslr_uide
      *
     c                   eval      XML_path = *blanks
     c                   eval      afpslr_ufil = l_filg
6.31 c                   eval      afpslr_uide = 'VISVEDLEGG'
     c     afpslr_key    chain     afpslrr
     c                   if        %found
     c                   movel     afudss        XML_path
     c                   endif
     c                   eval      XML_path = '/home/asp/print/630/+
     c                                          maler/visvedlegg.XML'
     c
      *
     c                   eval      OUT_path = *blanks
     c                   eval      afpslr_ufil = l_filg
     c                   eval      afpslr_uide = 'XMLPATH   '
     c     afpslr_key    chain     afpslrr
     c                   if        %found
     c                   movel     afudss        OUT_path
     c                   endif
      *
      * Finn ut om vi skal skrive til datakø
      *
     c                   eval      w_dtaq = '0'
     c                   eval      afpslr_uide = 'DATAQ     '
     c     afpslr_key    chain     afpslrr
     c                   if        %found
     c                   movel     afudss        w_dtaq
     c                   endif
      *
      * Lager batchnummer for denne utskriften
     c                   eval      w_kode = '0'
     c                   eval      w_firm = l_firm
     c                   eval      w_fell = 'NXTKLI'
     c                   eval      w_type = *blank
     c                   eval      w_fast = *blank
     c                   eval      w_numm = *zero
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
     c                   parm                    w_numm
      *
      * Vi logger at jobben har startet
      *
     c                   eval      w_jnav = 'PDF' + %char(w_numm)
     c                   eval      w_jtxt = 'Vedl.generering har startet !'
      *
     c                   call      'AB700R'
     c                   parm                    w_jnav
     c                   parm                    w_jkey
     c                   parm                    w_jtxt
      *
      * Vi logger at vi genererer xml for utskriften
      *
     c                   eval      w_jstat = 10
     c                   eval      w_jtext = 'Generering av mail for ' + w_jnav
      *
     c                   call      'AB705R'
     c                   parm                    w_jkey
     c                   parm                    w_jstat
     c                   parm                    w_jtext
      *
     c                   time                    w_tims
     c                   movel     w_tims        w_dato
     c                   eval      w_ski1 = 'T'
     c                   eval      w_ski2 = ':'
     c                   eval      w_ski3 = ':'
      *
     c                   eval      w_attac = p_filn
     c                   if        %subst(w_attac:1:4) <> 'ifs:'
     c                   eval      w_attac = 'ifs:' + %trim(w_attac)
     c                   endif
     c                   eval      w_anavn = p_fnav
     c                   eval      w_mime = p_mime
     c                   if        (w_mime = *blank)
     c                   eval      w_mime = 'text/plain'
     c
     c                   endif
      *
     c                   endsr
