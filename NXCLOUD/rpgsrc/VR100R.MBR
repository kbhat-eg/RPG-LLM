     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VR100R
      * Beskrivelse . . : Vare-plakat, vedlikehold og utskrift
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       230506 HKO  Nytt program
5.70  * PRGR  111107 bof  Utvidet med prisgruppe
5.71  * prgr  080210 bof  Prisgruppe i kall til VP700r                 lnr
6.11  * 6.10  290609 bof  Utivdet eannr til 14 og brukt std. oppslag
      *
8.01  *PRG2   150622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvplal1    if   e           k disk    rename(vplapfr:vplal1r)
     fvplalu    uf a e           k disk    rename(vplapfr:vplalur)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fvenhl1    if   e           k disk    rename(venhpfr:venhl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
      *
     fvr100d    cf   e             workstn
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vrfirm)
     d p_vare          s                   like(vrvare)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_fnav                951    980
      *
      * Datastruktur for parameterliste -ut
      *
     d                 ds
     d d_list                        80
     d  d_firm                        3  0 overlay(d_list)
     d  d_vare                       15    overlay(d_list:4)
     d  d_nobb                        8  0 overlay(d_list:19)
     d  d_eann                       13  0 overlay(d_list:27)
     d  d_saim                        9  2 overlay(d_list:40)
      *
      * Definering av variabler i nøkler
      *
     d vplal1_vare     s                   like(vrvare)
     d vplalu_vare     s                   like(vrvare)
     d vvarl1_vare     s                   like(vvvare)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d venhl1_eenh     s                   like(vaeenh)
     d rlevl1_levr     s                   like(rllevr)
      *
      * Definering av variable
      *
     d b_oppr          s              1    inz(*off)
     d b_inlr          s              1    inz(*off)
      *
     d w_udat          s               d   datfmt(*iso)
     d w_utim          s               t   timfmt(*iso)
     d w_mvap          s              6  2
     d w_mvat          s              5  4
     d w_mvaf          s             10  9
     d w_kode          s              1  0
     d w_lety          s              1  0
     d w_enhe          s              3
     d w_bupr          s              9  2
     d w_tipr          s              9  2
     d w_kopr          s              9  2
8.01 d w_prgr          s              2
6.11 d w_east          s              1
6.11 d w_eann          s             14  0
      *
     d w_firm          s                   like(vrfirm)
     d w_etxt          s                   like(vaetxt)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      vplal1_vare = p_vare
     c     vplal1_key    chain     vplal1
      *
     c                   if        %found
      *
     c                   eval      b_oppr = *off
      *
     c                   eval      c2type = vrtype
     c                   eval      c2form = vrform
     c                   eval      c2enhe = vrenhe
     c                   eval      c2pris = vrpris
     c                   eval      c2pnav = vrpnav
     c                   eval      c2merk = vrmerk
     c                   eval      c2tek1 = vrtek1
     c                   eval      c2tek2 = vrtek2
     c                   eval      c2tek3 = vrtek3
     c                   eval      c2tek4 = vrtek4
     c                   eval      c2tek5 = vrtek5
     c                   eval      c2tek6 = vrtek6
     c                   eval      c2bes1 = vrbes1
     c                   eval      c2bes2 = vrbes2
     c                   eval      c2bes3 = vrbes3
     c                   eval      c2eusr = vreusr
      *
     c     *dmy          move      vrodat        c2odat
     c     *dmy          move      vredat        c2edat
      *
     c                   else
      *
     c                   eval      b_oppr = *on
      *
     c                   eval      c2type = *blank
     c                   eval      c2form = *blank
     c                   eval      c2enhe = *blank
     c                   eval      c2pris = 0
     c                   eval      c2pnav = *blank
     c                   eval      c2merk = *blank
     c                   eval      c2tek1 = *blank
     c                   eval      c2tek2 = *blank
     c                   eval      c2tek3 = *blank
     c                   eval      c2tek4 = *blank
     c                   eval      c2tek5 = *blank
     c                   eval      c2tek6 = *blank
     c                   eval      c2bes1 = *blank
     c                   eval      c2bes2 = *blank
     c                   eval      c2bes3 = *blank
     c                   eval      c2odat = 0
     c                   eval      c2edat = 0
     c                   eval      c2eusr = *blank
      *
     c                   endif
      *
     c                   if        p_vare = *blank
     c                   goto      default
     c                   endif
      *
      * Henter vareinformasjon
      *
     c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
6.11  *
6.11  * Kaller program for henting av ean-nr
6.11  *
6.11 c                   call      'VE711R'
6.11 c                   parm                    w_firm
6.11 c                   parm                    p_vare
6.11 c                   parm                    c2enhe
6.11 c                   parm                    w_eann
6.11 c                   parm                    w_east
      *
      * Henter hovedgruppe-informasjon
      *
     c                   if        vvhgrp <> 0
     c                   eval      vhgrl1_hogr = vvogrp
     c                   eval      vhgrl1_hhgr = vvhgrp
     c     vhgrl1_key    chain     vhgrl1
     c                   endif
      *
      * Henter undergruppe-informasjon
      *
     c                   if        vvugrp <> 0
     c                   eval      vugrl1_uogr = vvogrp
     c                   eval      vugrl1_uhgr = vvhgrp
     c                   eval      vugrl1_uugr = vvugrp
     c     vugrl1_key    chain     vugrl1
     c                   endif
      *
      * Henter leverandørinformasjon
      *
     c                   if        vvldor <> 0
     c                   eval      rlevl1_levr = vvldor
     c     rlevl1_key    chain     rlevl1
     c                   endif
      *
      * Henter priser og viser veil.salgspris inkl.mva
      *
     c                   eval      w_udat = %date
     c                   eval      w_utim = %time
      *
     c                   if        b_oppr = *on
     c                   call      'VP710R'
     c                   parm                    w_firm
     c                   parm                    p_vare
5.70 c                   parm                    w_prgr
     c                   parm                    w_lety
     c                   parm                    w_udat
     c                   parm                    w_utim
     c                   parm                    w_enhe
     c                   parm                    c2saim
     c                   parm                    w_bupr
     c                   parm                    w_tipr
     c                   else
     c                   call      'VP700R'
     c                   parm                    w_firm
     c                   parm                    p_vare
5.71 c                   parm                    w_prgr
     c                   parm                    c2enhe
     c                   parm                    w_lety
     c                   parm                    w_udat
     c                   parm                    w_utim
     c                   parm                    b_inlr
     c                   parm                    c2saim
     c                   parm                    w_bupr
     c                   parm                    w_tipr
     c                   parm                    w_kopr
     c                   endif
      *
     c                   eval      w_kode = 4
     c                   eval      c2saim = c2saim * w_mvat
      *
     c                   call      'FA909R'
     c                   parm                    w_firm
     c                   parm                    p_vare
     c                   parm                    w_kode
     c                   parm                    c2saim
      *
      * Viser standardinformasjon
      *
     c                   eval      c2eann = w_eann
     c                   eval      c2nobb = vvnobb
      *
      * Legger ut default-verdier i bildet dersom ny plakat-vare
      *
     c     default       tag
      *
     c                   if        b_oppr = *on
     c                   eval      c2type = 'STD'
     c                   eval      c2form = 'A4'
     c                   if        p_vare <> *blank
     c                   eval      c2enhe = w_enhe
     c                   eval      c2pris = c2saim
     c                   eval      c2pnav = rlnavn
     c                   eval      c2tek1 = vvtek1
     c                   eval      c2tek2 = vvtek2
     c                   eval      c2tek3 = vghtxt
     c                   eval      c2tek4 = vgutxt
     c                   if        w_tipr <> 0
     c                   eval      c2type = 'TILB'
     c                   eval      c2pris = w_tipr
     c                   eval      c2pris = c2pris * w_mvat
     c                   call      'FA909R'
     c                   parm                    w_firm
     c                   parm                    p_vare
     c                   parm                    w_kode
     c                   parm                    c2pris
     c                   endif
     c                   endif
     c                   endif
      *
      * Behandler bildet
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl
     c                   goto      avslutt
     c                   endif
      *
      * F4=Spørring
      *
     c                   if        *inkd
     c                   if        w_afld = 'C2ENHE'
     c                   if        p_vare <> *blank
     c                   call      'VE550R'
     c                   parm                    w_firm
     c                   parm                    p_vare
     c                   parm                    c2enhe
     c                   parm                    w_etxt
     c                   else
     c                   call      'VA510R'
     c                   parm                    w_firm
     c                   parm                    c2enhe
     c                   parm                    w_etxt
     c                   endif
     c                   endif
     c                   goto      c2tagb
     c                   endif
      *
      * F8=Vareinformasjon
      *
     c                   if        *inkh
     c                   if        p_vare <> *blank
     c                   call      'VV510R'
     c                   parm                    w_firm
     c                   parm                    p_vare
     c                   endif
     c                   goto      c2tagb
     c                   endif
      *
      * F9=Tilbudspriser
      *
     c                   if        *inki
     c                   if        p_vare <> *blank
     c                   call      'VT510R'
     c                   parm                    w_firm
     c                   parm                    p_vare
     c                   endif
     c                   goto      c2tagb
     c                   endif
      *
      * Validering
      *
     c                   if        c2form <> 'A3' and
     c                             c2form <> 'A4' and
     c                             c2form <> 'A5' and
     c                             c2form <> 'A6'
     c                   eval      *in31 = *on
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        p_vare <> *blank
     c                   eval      venhl1_eenh = c2enhe
     c     venhl1_key    chain     venhl1
     c                   if        not %found
     c                   eval      *in32 = *on
     c                   goto      c2tagb
     c                   endif
     c                   endif
      *
     c                   if        c2pris = 0
     c                   eval      *in33 = *on
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        c2tek1 = *blank
     c                   eval      *in34 = *on
     c                   goto      c2tagb
     c                   endif
      *
      * Oppdaterer tabell
      *
     c                   clear                   vplalur
      *
     c                   eval      vplalu_vare = p_vare
     c     vplalu_key    chain     vplalur
      *
     c                   eval      vrtype = c2type
     c                   eval      vrform = c2form
     c                   eval      vrenhe = c2enhe
     c                   eval      vrpris = c2pris
     c                   eval      vrpnav = c2pnav
     c                   eval      vrmerk = c2merk
     c                   eval      vrtek1 = c2tek1
     c                   eval      vrtek2 = c2tek2
     c                   eval      vrtek3 = c2tek3
     c                   eval      vrtek4 = c2tek4
     c                   eval      vrtek5 = c2tek5
     c                   eval      vrtek6 = c2tek6
     c                   eval      vrbes1 = c2bes1
     c                   eval      vrbes2 = c2bes2
     c                   eval      vrbes3 = c2bes3
     c                   eval      vredat = %date
     c                   eval      vretim = %time
     c                   eval      vreusr = l_user
      *
     c                   if        %found(vplalu)
     c                   update    vplalur
     c                   else
     c                   eval      vrfirm = w_firm
     c                   eval      vrvare = p_vare
     c                   eval      vrodat = %date
     c                   write     vplalur
     c                   endif
      *
      * F7=Utskrift
      *
     c                   if        *inkg
     c                   eval      d_firm = w_firm
     c                   eval      d_vare = p_vare
     c                   eval      d_nobb = c2nobb
     c                   eval      d_eann = c2eann
     c                   eval      d_saim = c2saim
     c                   call      'VR600C'
     c                   parm                    d_list
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
      *
      * Key for oppslag på vare-plakat
      *
     c     vplal1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vplal1_vare
      *
      * Key for oppdatering av vare-plakat
      *
     c     vplalu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vplalu_vare
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag på enhet
      *
     c     venhl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    venhl1_eenh
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      *
      * Henter verdier fra parameter, og initierer skjermbilde
      *
     c                   eval      w_firm = p_firm
      *
     c                   eval      c2vare = p_vare
      *
      * Finner moms-sats
      *
     c                   call      'FÅ705R'
     c                   parm                    w_firm
     c                   parm                    w_udat
     c                   parm                    w_mvap
     c                   parm                    w_mvat
     c                   parm                    w_mvaf
5.70  *
5.70  * Finner prisgruppe
5.70  *
5.70 c                   call      'VL711R  '
5.70 c                   parm                    w_prgr
      *
     c                   endsr
