     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JM111R
      * Beskrivelse . . : Vedlikehold av kampanjepriser, finn statuskode
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       131097 HKO  Nytt program
      *       290405 HKO  Test på om nobbnr finnes i EVR dersom vare med
      *                   samme varenr ikke finnes
      *       190905 HKO  Fjernet test på at innkjøpspris i kampanje-
      *                   perioden må være utfylt
      *                   Lagt inn test på at kampanjepris ikke er høyere
      *                   enn veiledende salgspris på varen
5.70  * PRGR  280409 bof  Utvidet med prisgruppe
6.10  *       250809 bof  Fjernet 000 fra pakning, ligger på vvarpf
6.30  * 6.30  260113 bof  Omskriving i forb. endring pakning VA
6.32  *       140314 bof  Lagt inn test på fra /til dato på pakning
6.33  *       220914 bof  feilretting 6.32, udat må ha verdi.
6.20  *       131114 bkv  Feil sjekk på enhet for å finne pris
6.21  *       261114 bkv  Feil sjekk på enhet for å finne pris II
6.34  * 6.30  030815 bof  Bruke dagens dato for å sammenligne med pris
6.34  *                   samtidig sjekke at denne er innenfor kampanjeperiode
6.35  * 6.30  081018 bof  Utvidet med trelast sortiment
6.36  * 6.30  160320 bof  Tillate at enheter finnes i evr,bare minst 1 finnes
      *
7.01  * 7.00  081018 bof  Utvidet VA kampanje med prisgr.
7.02  * 7.00  260821 bof  Feilretting vedr. prisgruppe
      *
8.01  *       250422 bof  Flyttet sjekk av logistikk vare
8.02  *PRG2   130622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjkavlr    if   e           k disk    rename(jkavpfr:jkavlr1)
     fjkahl1    if   e           k disk    rename(jkahpfr:jkahl11)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvarl3    if   e           k disk    rename(vvarpfr:vvarl3r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjvpkl2    if   e           k disk    rename(jvpkpfr:jvpkl2r)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(jmvfir)
     d p_vkam          s                   like(jmvkam)
7.01 d p_vprg          s                   like(jmvprg)
     d p_vvar          s                   like(jmvvar)
     d p_stat          s              1  0
      *
      * Definering av variabler i nøkler
      *
     d jkavlr_vkam     s                   like(jmvkam)
7.01 d jkavlr_vprg     s                   like(jmvprg)
     d jkavlr_vvar     s                   like(jmvvar)
     d jkahl1_kamp     s                   like(jmkamp)
7.01 d jkahl1_prgr     s                   like(jmprgr)
     d vvarl1_vare     s                   like(vvvare)
     d vvarl3_nobb     s                   like(vvnobb)
     d vvenl1_vare     s                   like(vevare)
     d jvarl1_vare     s                   like(jvvare)
     d jvpkl2_vare     s                   like(jwvare)
6.30 d jvpkl2_ldor     s                   like(jwldor)
     d jvpkl2_enhe     s                   like(jwenhe)
      *
      * Definering av variable
      *
     d b_inlr          s              1    inz(*off)
     d b_enhe_ok       s              1    inz(*off)
6.36 d b_enhe_en       s              1    inz(*off)
6.32 d b_pakn          s              1    inz(*off)
      *
     d w_lety          s              1  0
     d w_sapr          s              9  2
     d w_inpr          s              9  2
     d w_kopr          s              9  2
     d w_bupr          s              9  2
8.02 d w_prgr          s              2
6.32 d w_udat          s               d   datfmt(*iso)
6.34 d w_dato          s               d   datfmt(*iso)
6.35 d w_lv_vare       s                   like(vvvare)
6.35 d w_lv_nlev       s                   like(vvldor)
6.35 d w_nobb          s                   like(jvnobb)
      *
     d w_firm          s                   like(jmvfir)
     d w_vvar          s                   like(jmvvar)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.35 C/Exec SQL
6.35 C+  Set Option DatFmt=*ISO
6.35 C/End-Exec
      *
     c                   eval      p_stat = 0
      *
     c                   eval      w_vvar = p_vvar
      *
      * Vare finnes ikke i EVR
      *
6.21 c                   clear                   jvarl1r
6.21 c                   eval      jvarl1_vare = p_vvar
6.21 c     jvarl1_key    chain     jvarl1
6.21 c                   if        not %found
6.21 c                   clear                   jvarl1r
6.21 c                   endif
8.01  * leser kampanje her for å ha leverandør i sjekk av logistikkvare
8.01 c                   eval      jkavlr_vkam = p_vkam
8.01 c                   eval      jkavlr_vprg = w_prgr
8.01 c                   eval      jkavlr_vvar = p_vvar
8.01 c     jkavlr_key    chain     jkavlr
      *
6.35  * sjekker om logistikk-vare
6.35 c                   eval      w_lv_vare = *blank
6.35 c                   eval      w_lv_nlev = 0
8.01 c                   movel     p_vvar        w_nobb
6.35 c/exec sql
6.35 c+ select jvqvnr,
6.35 c+        jvqldo
6.35 c+ into   :w_lv_vare,
6.35 c+        :w_lv_nlev
6.35 c+ from   jvklpf
6.35 c+ where  jvquno = :w_nobb and jvqldo = :jmvldo
6.35 c+ fetch first 1 rows only
6.35 c/end-exec
6.35 c                   if        w_lv_vare <> *blank
6.35 c                   move      w_lv_vare     vvarl1_vare
8.01 c                   eval      w_vvar = w_lv_vare
6.35 c                   else
     c                   eval      vvarl1_vare = p_vvar
6.35 c                   endif
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
6.35 c                   if        w_lv_vare <> *blank
6.35 c                   move      w_lv_vare     jvarl1_vare
8.01 c                   eval      w_vvar = w_lv_vare
6.35 c                   else
     c                   eval      jvarl1_vare = p_vvar
6.35 c                   endif
     c     jvarl1_key    chain     jvarl1
     c                   if        %found and
     c                             jvnobb <> 0
     c                   eval      vvarl3_nobb = jvnobb
     c     vvarl3_key    chain     vvarl3
     c                   if        %found
     c                   eval      w_vvar = vvvare
     c                   else
     c                   eval      p_stat = 1
     c                   goto      avslutt
     c                   endif
     c                   else
     c                   eval      p_stat = 1
     c                   goto      avslutt
     c                   endif
     c                   endif
      *
      * Sjekker om varens enheter i EVR finnes i LVR
      *
     c                   eval      b_enhe_ok = *off
6.36 c                   eval      b_enhe_en = *off
      *
     c                   eval      vvenl1_vare = w_vvar
     c     vvenl1_key    setll     vvenl1
     c     vvenl1_key    reade     vvenl1
     c                   dow       not %eof
      *
     c                   eval      b_enhe_ok = *off
      *
     c                   eval      jvpkl2_vare = p_vvar
6.35 c                   if        w_lv_nlev <> 0
6.35 c                   eval      jvpkl2_ldor = w_lv_nlev
6.35 c                   else
6.30 c                   eval      jvpkl2_ldor = vvnlev
6.35 c                   endif
     c                   eval      jvpkl2_enhe = veenhe
     c     jvpkl2_key    setll     jvpkl2
     c     jvpkl2_key    reade     jvpkl2
     c                   dow       not %eof
6.32  *
6.32  * sjekker fra / tildato
6.32 c                   exsr      sjekk_pkdato
6.32 c                   if        b_pakn = *on
     c                   if        jwofak > 0 or
     c                             jwofap > 0
     c                   eval      b_enhe_ok = *on
6.36 c                   if        jwenhe = jvpenh
6.36 c                   eval      b_enhe_en = *on
6.36 c                   endif
     c                   leave
     c                   endif
6.32 c                   endif
     c     jvpkl2_key    reade     jvpkl2
     c                   enddo
      *
6.36 c*                   if        b_enhe_ok = *off
6.36 c*                   eval      p_stat = 2
6.36 c*                   goto      avslutt
6.36 c*                   endif
      *
     c     vvenl1_key    reade     vvenl1
     c                   enddo
      *
6.36 c                   if        b_enhe_en = *on
6.36 c                   eval      p_stat = 0
6.36 c                   else
     c                   if        b_enhe_ok = *off
     c                   eval      p_stat = 2
     c                   goto      avslutt
     c                   endif
6.36 c                   endif
      *
      * Sjekker om pris mangler
      *
     c*                   eval      jkavlr_vkam = p_vkam
7.02 c*                   eval      jkavlr_vprg = w_prgr
     c*                   eval      jkavlr_vvar = p_vvar
     c*     jkavlr_key    chain     jkavlr
     c***                if        jmvkai = 0 or
     c                   if        jmvkau = 0 and jmvkaa = 0
     c                   eval      p_stat = 3
     c                   goto      avslutt
     c                   endif
      *
      * Sjekker om kampanjepris er høyere enn veiledende salgspris
      *
6.21 c                   if        jvpenh <> *blank
6.34 c                   if        w_udat >= jmfdat and
6.34 c                             w_udat <= jmtdat
6.34 c                   eval      w_dato = w_udat
6.34 c                   else
6.34 c                   eval      w_dato = jmfdat
6.34 c                   endif
     c                   call      'VP701R'
     c                   parm                    w_firm
     c                   parm                    w_vvar
5.70 c                   parm                    w_prgr
6.21 c                   parm                    jvpenh
     c                   parm                    w_lety
6.34 c                   parm                    w_dato
     c                   parm                    b_inlr
     c                   parm                    w_sapr
     c                   parm                    w_bupr
     c                   parm                    w_kopr
     c                   parm                    w_inpr
     c                   if        jmvkau <> 0 and
     c                             jmvkau > w_sapr or
     c                             jmvkaa <> 0 and
     c                             jmvkaa > w_sapr
     c                   eval      p_stat = 4
     c                   goto      avslutt
     c                   endif
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  * Subrutine for å sjekke om dato er innenfor fra /til dato
6.32  * hvis *loval i begge datoer = ok
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  *
6.32 c     sjekk_pkdato  begsr
6.32  *
6.32 c                   eval      b_pakn = *on
6.32  *
6.32 c                   if        jwfdat = *loval and
6.32 c                             jwtdat = *loval
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   if        jwfdat <= w_udat and
6.32 c                             jwtdat >  w_udat
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   if        jwfdat = *loval  and
6.32 c                             jwtdat >  w_udat
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   if        jwfdat <= w_udat and
6.32 c                             jwtdat =  *loval
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   eval      b_pakn = *off
6.32  *
6.32 c                   endsr
6.32  *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vkam
7.01 c                   parm                    p_vprg
     c                   parm                    p_vvar
     c                   parm                    p_stat
      *
      * Key for oppslag på kampanje-vare
      *
     c     jkavlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jkavlr_vkam
7.01 c                   kfld                    jkavlr_vprg
     c                   kfld                    jkavlr_vvar
      *
      * Key for oppslag på kampanje-hode
      *
     c     jkahl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jkahl1_kamp
7.01 c                   kfld                    jkahl1_prgr
      *
      * Key for oppslag på vare (EVR)
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
     c     vvarl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl3_nobb
      *
      * Key for oppslag på vare-enhet (EVR)
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
      *
      * Key for oppslag på vare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for oppslag på vare-pakning
      *
      *
     c     jvpkl2_key    klist
     c                   kfld                    jvpkl2_vare
6.30 c                   kfld                    jvpkl2_ldor
     c                   kfld                    jvpkl2_enhe
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
7.01 c                   eval      w_prgr = p_vprg
7.01  *
7.01  * Henter prisgruppe
7.01  *
7.01 c*                  call      'VL711R '
7.01 c*                  parm                    w_prgr
      *
6.33 c                   time                    w_udat
      *
      * Henter kampanje-hode informasjon
      *
     c                   eval      jkahl1_kamp = p_vkam
     c     jkahl1_key    chain     jkahl1
      *
     c                   endsr
