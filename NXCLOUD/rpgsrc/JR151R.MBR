     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JR151R
      * Beskrivelse . . : Rapp. vareregister, innlesing - parameter 2
      *
6.31  * Spesialversjon. :
6.31  * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       111099 HKO  Nytt program
6.31  *       171115 bof  Utvidet med prisgruppe
8.01  *PRG2   140622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjrapl1    if   e           k disk    rename(jrappfr:jrapl1r)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fjvrcl1    if   e           k disk    rename(jvrcpfr:jvrcl1r)
6.31 fra30l1    if   e           k disk    rename(ra30pfr:ra30l1r)
      *
     fjr151d    cf   e             workstn
      *
      * Definering av parametre
      *
     d p_firm          s                   like(jrfirm)
     d p_rapp          s                   like(jrrapp)
     d p_dato          s               d   datfmt(*iso)
      *
      * Definering av Local data area
      *
     d                uds
     d l_fnav                951    980
      *
      * Definering av parameterliste -ut
      *
     d                 ds
     d d_list                        32
     d  d_firm                        3  0 overlay(d_list)
     d  d_rapp                        2  0 overlay(d_list:4)
     d  d_rfmt                       10    overlay(d_list:6)
     d  d_dato                         d   overlay(d_list:16) datfmt(*iso)
8.01 d  d_prgr                        2    overlay(d_list:30)
      *
      * Definering av variabler i nøkler
      *
     d jrapl1_rapp     s                   like(jrrapp)
     d jvrcl1_rfmt     s                   like(jfrfmt)
     d jlevl1_ldor     s                   like(jlldor)
6.31 d ra30l1_dkod     s                   like(redkod)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(jrfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Behandler bildet
      *
     c     b1taga        tag
      *
     c                   exfmt     b1bld
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   endsl
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1taga
     c                   endif
      *
      * Kaller program for henting av data, avbryter dersom feil
      *
     c                   call      'JR151C'
     c                   parm                    b1file
     c                   parm                    b1fldr
     c                   parm                    b_feil
      *
     c                   if        b_feil = *on
     c                   eval      *in35 = *on
     c                   goto      b1taga
     c                   endif
      *
      * Kaller styreprogram
      *
     c                   eval      d_firm = w_firm
     c                   eval      d_rapp = b1rapp
     c                   eval      d_rfmt = b1rfmt
     c                   eval      d_dato = p_dato
6.31 c                   eval      d_prgr = b1prgr
      *
     c                   call      'JR152C'
     c                   parm                    d_list
      *
      * Sender melding om batch-jobb
      *
     c                   exfmt     b1sbm
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
     c                   if        b1file = *blank
     c                   eval      *in31  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
6.31  *
6.31  * Prisgruppe
6.31  *
6.31 c                   if        b1prgr <> *blank
6.31 c                   eval      ra30l1_dkod = b1prgr
6.31 c     ra30l1_key    chain     ra30l1                             46
6.31 c                   if        *in46 = *on
6.31 c                   eval      b_feil = *on
6.31 c                   leavesr
6.31 c                   endif
6.31 c                   endif
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_rapp
     c                   parm                    p_dato
      *
      * Key for oppslag på rapportør
      *
     c     jrapl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jrapl1_rapp
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for oppslag på vare-recordformat
      *
     c     jvrcl1_key    klist
     c                   kfld                    jvrcl1_rfmt
6.31  *
6.31  * Key for oppslag på prisgruppe
6.31  *
6.31 c     ra30l1_key    klist
6.31 c                   kfld                    w_firm
6.31 c                   kfld                    ra30l1_dkod
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
      * Initierer skjerm-felter
      *
     c                   eval      jrapl1_rapp = p_rapp
     c     jrapl1_key    chain     jrapl1                             90
      *
     c                   eval      b1rapp = p_rapp
     c                   eval      b1navn = jrnavn
     c                   eval      b1ldor = jrldor
     c                   eval      b1rfmt = jrrfmt
     c                   eval      b1file = jrfile
     c                   eval      b1fldr = jrfldr
     c                   eval      b1eusr = jreusr
     c                   eval      b1edat = 0
      *
     c                   if        jredat <> *loval
     c     *dmy          move      jredat        b1edat
     c                   endif
      *
     c                   if        b1ldor <> 0
     c                   eval      jlevl1_ldor = b1ldor
     c     jlevl1_key    chain     jlevl1                             90
     c                   if        *in90 = *off
     c                   eval      b1lnav = jlnavn
     c                   endif
     c                   endif
      *
     c                   if        b1rfmt <> *blank
     c                   eval      jvrcl1_rfmt = b1rfmt
     c     jvrcl1_key    chain     jvrcl1                             90
     c                   if        *in90 = *off
     c                   eval      b1rbes = jfbesk
     c                   endif
     c                   endif
      *
     c                   endsr
