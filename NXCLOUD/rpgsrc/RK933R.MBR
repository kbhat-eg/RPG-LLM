      /TITLE  ASOKON Salderer kunder med 0 i saldo
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * System. . . . . : ASOKON
      * Program . . . . : RK933R
      * Beskrivelse . . : Salderer kunder med 0 i saldo
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
6.10  * V6.10 210909 BSA  Lagt inn sporingsfelter
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     frktrlu    uf   e           k disk    rename(rktrpfr:rktrlur)
      *
     d rktrlu_firm     s                   like(rnfirm)
     d rktrlu_kund     s                   like(rnkund)
      *
     d w_mld1          c                   'Vennligst vent .........     '
     d w_mld2          c                   'Saldering pågår.........     '
     d wactli          s              2  0
     d wactpo          s              3  0
     d wmld01          s             30
     d wmld02          s             30
      *
6.10 d                uds
6.10 d l_user                911    920
      *
      * Send meldingsbildet
      *
     c                   eval      wactli = 4
     c                   eval      wactpo = 44
     c                   eval      wmld01 = w_mld1
     c                   eval      wmld02 = w_mld2
      *
     c                   call      'AA006C'
     c                   parm                    wactli
     c                   parm                    wactpo
     c                   parm                    wmld01
     c                   parm                    wmld02
      *
      * Leser alle kunder
      *
     c                   read      rkunl1                                 90
     c                   dow       *in90 = *off
      *
     c                   if        rksald = *zeros
     c                   exsr      poster
     c                   endif
      *
     c                   read      rkunl1                                 90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av poster
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     poster        begsr
      *
      * Leser kundeposter
      *
     c                   eval      rktrlu_firm = rkfirm
     c                   eval      rktrlu_kund = rkkund
      *
     c     rktrlu_key    setll     rktrlur                                90
     c     rktrlu_key    reade     rktrlur                                90
      *
     c                   dow       *in90 = *off
      *
     c                   eval      rnrest = *zeros
6.10 c                   time                    rnedat
6.10 c                   time                    rnetim
6.10 c                   eval      rneusr = l_user
     c                   update    rktrlur
      *
     c     rktrlu_key    reade     rktrlur                                90
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for kundeposter
      *
     c     rktrlu_key    klist
     c                   kfld                    rktrlu_firm
     c                   kfld                    rktrlu_kund
      *
     c                   endsr
