     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VA120R
      * Beskrivelse . . : Kontohenvisning, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
5.20  * R5.20 141003 AMD  Utvidet med mulighet for å kunne kontere på
5.20  *                   leveringstype
      * 6.10  260609 BSA  Oppdatert med sporingsinformasjon
6.10  *       050110 bof  Lagt inn spørring på div. felter
7.xx  * 7.00  110316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvkhvl1    if   e           k disk    rename(vkhvpfr:vkhvl1r)
     fvkhvla    if   e           k disk    rename(vkhvpfr:vkhvlar)
     fvkhvlr    if   e           k disk    rename(vkhvpfr:vkhvlrr)
     fvkhvlu    uf a e           k disk    rename(vkhvpfr:vkhvlur)
      *
     fva120d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d vkhvl1_kkod     s                   like(vakkod)
     d vkhvla_ktxt     s                   like(vaktxt)
     d vkhvlr_kkod     s                   like(vakkod)
5.20 d vkhvlr_klty     s                   like(vaklty)
     d vkhvlr_koty     s                   like(vakoty)
     d vkhvlu_kkod     s                   like(vakkod)
5.20 d vkhvlu_klty     s                   like(vaklty)
     d vkhvlu_koty     s                   like(vakoty)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_stat          s              1
     d w_kont          s              5  0
     d w_spes          s              4  0
     d w_avde          s              4  0
     d w_text          s             30
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(vakfir)
6.10 d w_ktxt          s             30
      *
     d s_kkod          s                   like(vakkod)
5.20 d s_klty          s                   like(vaklty)
     d s_koty          s                   like(vakoty)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meleingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2kkod <> *blank or
     c                             b2ktxt <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      vkhvla_ktxt = b1ktxt
     c     vkhvla_key    setll     vkhvla
     c                   other
     c                   eval      vkhvl1_kkod = b1kkod
     c     vkhvl1_key    setll     vkhvl1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Kontohenvisning
      *
     c                   if        b2kkod <> *blank
     c                   eval      w_seqe = *blank
     c                   eval      vkhvl1_kkod = b2kkod
     c     vkhvl1_key    setll     vkhvl1
     c                   goto      end_pos
     c                   endif
      *
      * Beskrivelse
      *
     c                   if        b2ktxt <> *blank
     c                   eval      w_seqe = 'B'
     c                   eval      vkhvla_ktxt = b2ktxt
     c     vkhvla_key    setll     vkhvla
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2kkod = *blank
     c                   eval      b2ktxt = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1kkod = b1kkod
5.20 c                   eval      c1klty = b1klty
     c                   eval      c1koty = b1koty
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1kkod = b1kkod
5.20 c                   eval      k1klty = b1klty
     c                   eval      k1koty = b1koty
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1kkod = b1kkod
5.20 c                   eval      d1klty = b1klty
     c                   eval      d1koty = b1koty
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1kkod = b1kkod
5.20 c                   eval      c1klty = b1klty
     c                   eval      c1koty = b1koty
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1kkod = b2kkod
5.20 c                   eval      c1klty = *zero
     c                   eval      c1koty = *blank
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
6.10 c                   when      *inkd
6.10 c                   exsr      spØrring
6.10 c                   goto      c1taga
     c                   endsl
      *
     c                   if        c1kkod = *blank
     c                   goto      end_c1bld
     c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      vkhvlr_kkod = c1kkod
5.20 c                   eval      vkhvlr_klty = c1klty
     c                   eval      vkhvlr_koty = c1koty
     c     vkhvlr_key    chain     vkhvlr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1kkod = *blank
5.20 c                   eval      c1klty = *zero
     c                   eval      c1koty = *blank
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2kkod = *blank
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      vkhvlr_kkod = c1kkod
5.20 c                   eval      vkhvlr_klty = c1klty
     c                   eval      vkhvlr_koty = c1koty
     c     vkhvlr_key    chain     vkhvlr
      *
     c                   if        %found
     c                   eval      c2ktxt = vaktxt
     c                   eval      c2kav1 = vakav1                                            .
     c                   eval      c2kko1 = vakko1                                            .
     c                   eval      c2ksp1 = vaksp1                                            .
     c                   eval      c2kav2 = vakav2                                            .
     c                   eval      c2kko2 = vakko2                                            .
     c                   eval      c2ksp2 = vaksp2                                            .
     c                   eval      c2kav3 = vakav3                                            .
     c                   eval      c2kko3 = vakko3                                            .
     c                   eval      c2ksp3 = vaksp3                                            .
     c                   eval      c2kav4 = vakav4                                            .
     c                   eval      c2kko4 = vakko4                                            .
     c                   eval      c2ksp4 = vaksp4                                            .
     c                   eval      c2kav5 = vakav5                                            .
     c                   eval      c2kko5 = vakko5                                            .
     c                   eval      c2ksp5 = vaksp5                                            .
     c                   eval      c2kav6 = vakav6                                            .
     c                   eval      c2kko6 = vakko6                                            .
     c                   eval      c2ksp6 = vaksp6                                            .
     c                   eval      c2kav7 = vakav7                                            .
     c                   eval      c2kko7 = vakko7                                            .
     c                   eval      c2ksp7 = vaksp7                                            .
     c                   else
     c                   eval      c2ktxt = *blank
     c                   eval      c2kav1 = 0                                                 .
     c                   eval      c2kko1 = 0                                                 .
     c                   eval      c2ksp1 = 0                                                 .
     c                   eval      c2kav2 = 0                                                 .
     c                   eval      c2kko2 = 0                                                 .
     c                   eval      c2ksp2 = 0                                                 .
     c                   eval      c2kav3 = 0                                                 .
     c                   eval      c2kko3 = 0                                                 .
     c                   eval      c2ksp3 = 0                                                 .
     c                   eval      c2kav4 = 0                                                 .
     c                   eval      c2kko4 = 0                                                 .
     c                   eval      c2ksp4 = 0                                                 .
     c                   eval      c2kav5 = 0                                                 .
     c                   eval      c2kko5 = 0                                                 .
     c                   eval      c2ksp5 = 0                                                 .
     c                   eval      c2kav6 = 0                                                 .
     c                   eval      c2kko6 = 0                                                 .
     c                   eval      c2ksp6 = 0                                                 .
     c                   eval      c2kav7 = 0                                                 .
     c                   eval      c2kko7 = 0                                                 .
     c                   eval      c2ksp7 = 0                                                 .
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1kkod = k1kkod
5.20 c                   eval      c1klty = k1klty
     c                   eval      c1koty = k1koty
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Validering av input
      *
      * Konto 1
      *
     c                   if        c2kav1 <> 0 or
     c                             c2kko1 <> 0 or
     c                             c2ksp1 <> 0
      *
     c                   eval      w_avde = c2kav1
     c                   eval      w_kont = c2kko1
     c                   eval      w_spes = c2ksp1
      *
     c                   exsr      sjekk_konto
      *
     c                   if        w_stat <> *blank
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
     c                   endif
      *
      * Konto 2
      *
     c                   if        c2kav2 <> 0 or
     c                             c2kko2 <> 0 or
     c                             c2ksp2 <> 0
      *
     c                   eval      w_avde = c2kav2
     c                   eval      w_kont = c2kko2
     c                   eval      w_spes = c2ksp2
      *
     c                   exsr      sjekk_konto
      *
     c                   if        w_stat <> *blank
     c                   eval      *in32 = *on
     c                   goto      c2taga
     c                   endif
      *
     c                   endif
      *
      * Konto 3
      *
     c                   if        c2kav3 <> 0 or
     c                             c2kko3 <> 0 or
     c                             c2ksp3 <> 0
      *
     c                   eval      w_avde = c2kav3
     c                   eval      w_kont = c2kko3
     c                   eval      w_spes = c2ksp3
      *
     c                   exsr      sjekk_konto
      *
     c                   if        w_stat <> *blank
     c                   eval      *in33 = *on
     c                   goto      c2taga
     c                   endif
      *
     c                   endif
      *
      * Konto 4
      *
     c                   if        c2kav4 <> 0 or
     c                             c2kko4 <> 0 or
     c                             c2ksp4 <> 0
      *
     c                   eval      w_avde = c2kav4
     c                   eval      w_kont = c2kko4
     c                   eval      w_spes = c2ksp4
      *
     c                   exsr      sjekk_konto
      *
     c                   if        w_stat <> *blank
     c                   eval      *in34 = *on
     c                   goto      c2taga
     c                   endif
      *
     c                   endif
      *
      * Konto 5
      *
     c                   if        c2kav5 <> 0 or
     c                             c2kko5 <> 0 or
     c                             c2ksp5 <> 0
      *
     c                   eval      w_avde = c2kav5
     c                   eval      w_kont = c2kko5
     c                   eval      w_spes = c2ksp5
      *
     c                   exsr      sjekk_konto
      *
     c                   if        w_stat <> *blank
     c                   eval      *in35 = *on
     c                   goto      c2taga
     c                   endif
      *
     c                   endif
      *
      * Konto 6
      *
     c                   if        c2kav6 <> 0 or
     c                             c2kko6 <> 0 or
     c                             c2ksp6 <> 0
      *
     c                   eval      w_avde = c2kav6
     c                   eval      w_kont = c2kko6
     c                   eval      w_spes = c2ksp6
      *
     c                   exsr      sjekk_konto
      *
     c                   if        w_stat <> *blank
     c                   eval      *in36 = *on
     c                   goto      c2taga
     c                   endif
      *
     c                   endif
      *
      * Konto 7
      *
     c                   if        c2kav7 <> 0 or
     c                             c2kko7 <> 0 or
     c                             c2ksp7 <> 0
      *
     c                   eval      w_avde = c2kav7
     c                   eval      w_kont = c2kko7
     c                   eval      w_spes = c2ksp7
      *
     c                   exsr      sjekk_konto
      *
     c                   if        w_stat <> *blank
     c                   eval      *in37 = *on
     c                   goto      c2taga
     c                   endif
      *
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   vkhvlur
      *
     c                   eval      vkhvlu_kkod = c1kkod
5.20 c                   eval      vkhvlu_klty = c1klty
     c                   eval      vkhvlu_koty = c1koty
     c     vkhvlu_key    chain     vkhvlur
      *
     c                   eval      vaktxt = c2ktxt
     c                   eval      vakav1 = c2kav1
     c                   eval      vakko1 = c2kko1
     c                   eval      vaksp1 = c2ksp1
     c                   eval      vakav2 = c2kav2
     c                   eval      vakko2 = c2kko2
     c                   eval      vaksp2 = c2ksp2
     c                   eval      vakav3 = c2kav3
     c                   eval      vakko3 = c2kko3
     c                   eval      vaksp3 = c2ksp3
     c                   eval      vakav4 = c2kav4
     c                   eval      vakko4 = c2kko4
     c                   eval      vaksp4 = c2ksp4
     c                   eval      vakav5 = c2kav5
     c                   eval      vakko5 = c2kko5
     c                   eval      vaksp5 = c2ksp5
     c                   eval      vakav6 = c2kav6
     c                   eval      vakko6 = c2kko6
     c                   eval      vaksp6 = c2ksp6
     c                   eval      vakav7 = c2kav7
     c                   eval      vakko7 = c2kko7
     c                   eval      vaksp7 = c2ksp7
      *
6.10 c                   time                    vakeda
6.10 c                   time                    vaketi
6.10 c                   eval      vakeus = l_user
     c                   if        %found
     c                   update    vkhvlur
     c                   else
     c                   eval      vakfir = w_firm
     c                   eval      vakkod = c1kkod
5.20 c                   eval      vaklty = c1klty
     c                   eval      vakoty = c1koty
6.10 c                   time                    vakoda
6.10 c                   time                    vakoti
6.10 c                   eval      vakous = l_user
     c                   write     vkhvlur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1ktxt = vaktxt
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      vkhvlr_kkod = d1kkod
5.20 c                   eval      vkhvlr_klty = d1klty
     c                   eval      vkhvlr_koty = d1koty
     c     vkhvlr_key    chain     vkhvlr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      vkhvlu_kkod = d1kkod
5.20 c                   eval      vkhvlu_klty = d1klty
     c                   eval      vkhvlu_koty = d1koty
     c     vkhvlu_key    chain     vkhvlur
      *
     c                   delete    vkhvlur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_kkod = k1kkod
5.20 c                   eval      s_klty = k1klty
     c                   eval      s_koty = k1koty
      *
      * Fyller kopierings-bildet
      *
     c                   eval      vkhvlr_kkod = k1kkod
5.20 c                   eval      vkhvlr_klty = k1klty
     c                   eval      vkhvlr_koty = k1koty
     c     vkhvlr_key    chain     vkhvlr
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1kkod = *blank
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      vkhvlr_kkod = k1kkod
5.20 c                   eval      vkhvlr_klty = k1klty
     c                   eval      vkhvlr_koty = k1koty
     c     vkhvlr_key    chain     vkhvlr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1kkod = s_kkod
5.20 c                   eval      c1klty = s_klty
     c                   eval      c1koty = s_koty
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      vkhvla
     c                   other
     c                   read      vkhvl1
     c                   endsl
      *
     c                   if        %eof or
     c                             vakfir <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      vkhvla_ktxt = vaktxt
     c                   other
     c                   eval      vkhvl1_kkod = vakkod
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1kkod = vakkod
5.20 c                   eval      b1klty = vaklty
     c                   eval      b1koty = vakoty
     c                   eval      b1ktxt = vaktxt
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'B'
     c     vkhvla_key    setgt     vkhvla
     c                   readp     vkhvla
     c                   other
     c     vkhvl1_key    setgt     vkhvl1
     c                   readp     vkhvl1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   readp     vkhvla
     c                   other
     c                   readp     vkhvl1
     c                   endsl
      *
     c                   if        %eof or
     c                             vakfir <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      vkhvla_ktxt = vaktxt
     c                   other
     c                   eval      vkhvl1_kkod = vakkod
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'B'
     c     vkhvla_key    setll     vkhvla
     c                   other
     c     vkhvl1_key    setll     vkhvl1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sjekke konto (RS740R)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_konto   begsr
      *
     c                   eval      *in41 = *off
     c                   eval      *in42 = *off
     c                   eval      *in43 = *off
     c                   eval      *in44 = *off
     c                   eval      *in45 = *off
      *
     c                   eval      w_stat = *blank
      *
     c                   call      'RS740R'
     c                   parm                    w_stat
     c                   parm                    w_kont
     c                   parm                    w_spes
     c                   parm                    w_avde
     c                   parm                    w_text
      *
     c                   if        w_stat = 'N'
     c                   eval      *in41 = *on                                  Konto ikke funnet
     c                   endif
      *
     c                   if        w_stat = 'A'
     c                   eval      *in42 = *on                                  Avd.  ikke funnet
     c                   endif
      *
     c                   if        w_stat = 'M'                                 Avd. men ikke kto
     c                   eval      *in43 = *on
     c                   endif
      *
     c                   if        w_stat = 'K'                                 Spes men ikke kto
     c                   eval      *in44 = *on
     c                   endif
      *
     c                   if        w_stat = 'S'                                 Konto er sperret
     c                   eval      *in45 = *on
     c                   endif
      *
     c                   endsr
6.10  *
6.10  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.10  * Subrutine for behandling av spørring
6.10  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.10  *
6.10 c     spØrring      begsr
6.10  *
6.10  * Spørring på ordretyper
6.10  *
6.10 c                   if        w_afld = 'C1KOTY'
6.10 c                   call      'VA550R'
6.10 c                   parm                    w_firm
6.10 c                   parm                    c1koty
6.10 c                   leavesr
6.10 c                   endif
6.10  *
6.10 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på kontohenvisning
      *
     c     vkhvl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vkhvl1_kkod
      *
      * Key for posisjonering på beskrivelse
      *
     c     vkhvla_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vkhvla_ktxt
      *
      * Key for les på kontohenvisning
      *
     c     vkhvlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vkhvlr_kkod
5.20 c                   kfld                    vkhvlr_klty
     c                   kfld                    vkhvlr_koty
      *
      * Key for oppdatering av kontohenvisning
      *
     c     vkhvlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vkhvlu_kkod
5.20 c                   kfld                    vkhvlu_klty
     c                   kfld                    vkhvlu_koty
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
      * Initierer skjermbildet
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      vkhvl1_kkod = *blank
     c     vkhvl1_key    setll     vkhvl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
