     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : VP601R
      * Beskrivelse . . : Vare-pris, utskrift av prisbok
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       270599 HKO  Nytt program
      *       280200 HKO  Utvidet lengde på overskrift
      *       230101 HKO  Leser varesortiment via utplukksfil
      *                   Dersom leverandør er valgt utelatt ved varesort-
      *                   iment tas heller ikke leverandør med i sorteringen
      *       161103 HKO  Fjernet behandling av rabattgruppe
      *       070205 HKO  Utelater utgåtte varer
      *       020805 HKO  Endret les mot vare-enhet for å finne prisbok-
      *                   enheter og salgsenheter
      *       270606 HKO  Lagt inn valg av leveringstype
5.70  * PRGR  051108 BOF  Utvidet med prisgruppe
6.20  * 6.20  021110 bof  Utvidet med utg.dato pr. lager
6.21  * 6.20  030411 bof  Utvidet vvprpf med prisgiver
6.22  * 6.20  020511 bof  Utvidet med varegr./lev.nr + prosent på vtilpf
6.23  * 6.20  190511 bø   Utvidet vl710r med div. info
6.24  *       070312 BOF  Justering på hente tilbudspris vareruppe og leverandør
6.30  * 6.30  170918 bkv  Utvidet med trelast sortiment
      *
8.01  *PRG2   150622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl9    if   e           k disk    rename(vvarpfr:vvarl9r)
     fvshel1    if   e           k disk    rename(vshepfr:vshel1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fvltpl1    if   e           k disk    rename(vltppfr:vltpl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fvvenl3    if   e           k disk    rename(vvenpfr:vvenl3r)
     fvvenl2    if   e           k disk    rename(vvenpfr:vvenl2r)
     fvvprl1    if   e           k disk    rename(vvprpfr:vvprl1r)
     fvtill1    if   e           k disk    rename(vtilpfr:vtill1r)
     fvpkol1    if   e           k disk    rename(vpkopfr:vpkol1r)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     fvvawpf    if   e           k disk
6.30 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
     fvp601p    o    e             printer infds(d_infds)
      *
      * Array
      *
     d a_index         s            100    dim(5000) ctdata perrcd(1)            Array søketekst
      *
      * Definering av Local data area
      *
     d                uds
     d l_ruti                800    809
     d l_alin                856    858  0
     d l_wsid                901    910
     d l_user                911    920
5.70 d l_lage               1001   1002  0
      *
      * Definering av datastrukturer
      *
      * Datastruktur for parameterliste -inn
      *
     d                 ds
     d d_list                        64
     d  d_firm                        3  0 overlay(d_list)
     d  d_vsor                       10    overlay(d_list:4)
     d  d_ogrp                        2  0 overlay(d_list:14)
     d  d_hgrp                        2  0 overlay(d_list:16)
     d  d_ugrp                        3  0 overlay(d_list:18)
     d  d_ldor                        6  0 overlay(d_list:21)
     d  d_sval                        1  0 overlay(d_list:27)
     d  d_tval                        1  0 overlay(d_list:28)
     d  d_rval                        1  0 overlay(d_list:29)
     d  d_2val                        1  0 overlay(d_list:30)
     d  d_lval                        1  0 overlay(d_list:31)
     d  d_lety                        1  0 overlay(d_list:32)
     d  d_dato                         d   overlay(d_list:33) datfmt(*iso)
5.70 d  d_lage                        2  0 overlay(d_list:45)
      *
      * Informasjon fra printer-file
      *
     d d_infds         ds
     d  d_pagnbr             369    372B 0
      *
      * Innhold i index-array
      *
     d                 ds
     d d_index                      100
     d  d_text                       96    overlay(d_index)
     d  d_side                        4  0 overlay(d_index:97)
      *
      * Definering av parametre
      *
     d p_parm          s             64
      *
      * Definering av variabler i nøkler
      *
     d vvarl9_ogrp     s                   like(vvogrp)
     d vvarl9_hgrp     s                   like(vvhgrp)
     d vvarl9_ugrp     s                   like(vvugrp)
     d vvarl9_ldor     s                   like(vvldor)
     d vshel1_vsor     s                   like(vcvsor)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d vltpl1_lety     s                   like(vylety)
     d rlevl1_levr     s                   like(rllevr)
     d vvenl3_vare     s                   like(vevare)
     d vvenl3_pben     s                   like(vepben)
     d vvenl2_vare     s                   like(vevare)
     d vvenl2_saen     s                   like(vesaen)
     d vvprl1_vare     s                   like(vpvare)
6.20 d vvprl1_ldor     s                   like(vpldor)
5.70 d vvprl1_prgr     s                   like(vpprgr)
     d vvprl1_enhe     s                   like(vpenhe)
     d vvprl1_lety     s                   like(vplety)
6.22 d vtill1_ogrp     s                   like(vtogrp)
6.22 d vtill1_hgrp     s                   like(vthgrp)
6.22 d vtill1_ugrp     s                   like(vtugrp)
6.22 d vtill1_ldor     s                   like(vtldor)
     d vtill1_vare     s                   like(vtvare)
5.70 d vtill1_prgr     s                   like(vtprgr)
     d vtill1_lety     s                   like(vtlety)
     d vpkol1_pkod     s                   like(vapkod)
      *
      * Definering av variable
      *
     d b_frst          s              1    inz(*off)
     d b_pris          s              1    inz(*off)
     d b_tilb          s              1    inz(*off)
     d b_svgr          s              1
     d b_sldo          s              1
     d b_stek          s              1
6.20 d b_inlr          s              1    inz(*off)
      *
     d w_ddat          s               d   datfmt(*iso)
6.20 d w_udat          s               d   datfmt(*iso)
6.23 d w_ldor          s                   like(vvldor)
6.30 d w_lv_nobb       s                   like(vvlnob)
6.30 d w_lv_modn       s                   like(vvlmod)
6.30 d w_lv_lldo       s                   like(vvlnle)
6.30 d w_lv_llva       s                   like(vvllva)
     d w_mvap          s              6  2
     d w_mvat          s              5  4
     d w_mvaf          s             10  9
     d w_timestamp     s             12  0
     d w_ogrp_alf      s              2
     d w_hgrp_alf      s              2
     d w_ugrp_alf      s              3
     d w_ldor_alf      s              6
     d w_i1            s              4  0
     d w_i2            s              4  0
     d w_kode          s              1  0
     d x               s              1  0
8.01 d w_prgr          s              2
5.70 d w_lage          s              2  0
5.70 d w_avde          s              4  0
6.22 d w_pldo          s                   like(vpldor)
5.70 d p_vtyp          s                   like(vvvtyp)
      *
     d w_firm          s                   like(vvfirm)
     d w_otxt          s                   like(vgotxt)
     d w_htxt          s                   like(vghtxt)
     d w_utxt          s                   like(vgutxt)
     d w_lnav          s                   like(rlnavn)
     d w_pkod          s                   like(vapkod)
     d w_enh1          s                   like(vpenhe)
     d w_enh2          s                   like(vpenhe)
     d w_enh3          s                   like(vpenhe)
     d w_sap1          s                   like(vpsapr)
     d w_sap2          s                   like(vpsapr)
     d w_sap3          s                   like(vpsapr)
     d w_tip1          s                   like(vttip1)
     d w_tip2          s                   like(vttip2)
     d w_tip3          s                   like(vttip3)
     d w_ogrp_gml      s                   like(vvogrp)
     d w_hgrp_gml      s                   like(vvhgrp)
     d w_ugrp_gml      s                   like(vvugrp)
     d w_ldor_gml      s                   like(vvldor)
     d w_vare_gml      s                   like(vvvare)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initiering og utskrift av hode
      *
     c                   exsr      hode
      *
     c                   eval      b_frst = *on
      *
      * Behandler utskift
      *
     c                   select
     c                   when      d_vsor <> *blank
     c                   exsr      behandl_vsor
     c                   when      d_ogrp <> 0
     c                   exsr      behandl_vgrp
     c                   when      d_ldor <> 0
     c                   exsr      behandl_ldor
     c                   other
     c                   exsr      behandl_tot
     c                   endsl
      *
      * Skiver innholdsfortegnelse / index
      *
     c                   exsr      index
      *
      * Avslutt
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av hode-opplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode          begsr
      *
      * Legger inn arb.stasjon og bruker i faste felter
      *
     c                   time                    w_timestamp
     c                   move      w_timestamp   a1date
      *
      * Henter firmaopplysninger
      *
     c     afirl1_key    chain     afirl1r                            90
     c                   eval      a1fna1 = aafnav
     c                   eval      a1fna2 = aafnav
      *
      * Dersom det er valgt å ha med rabattgruppe, skal denne være med i
      * overskriften
      *
     c                   if        d_rval = 1
     c                   eval      a2rtxt = 'R-grp'
     c                   endif
      *
      * Henter utplukksparametre
      *
     c                   eval      a5vsor = d_vsor
     c                   eval      a5ogrp = d_ogrp
     c                   eval      a5hgrp = d_hgrp
     c                   eval      a5ugrp = d_ugrp
     c                   eval      a5ldor = d_ldor
     c                   eval      a5lety = d_lety
      *
     c                   if        d_tval = 1
     c                   eval      a5tval = 'Ja'
     c                   else
     c                   eval      a5tval = 'Nei'
     c                   endif
      *
     c                   if        d_rval = 1
     c                   eval      a5rval = 'Ja'
     c                   else
     c                   eval      a5rval = 'Nei'
     c                   endif
      *
     c                   if        d_2val = 1
     c                   eval      a52val = 'Ja'
     c                   else
     c                   eval      a52val = 'Nei'
     c                   endif
      *
     c                   if        d_lval = 1
     c                   eval      a5lval = 'Ja'
     c                   else
     c                   eval      a5lval = 'Nei'
     c                   endif
      *
     c     *dmy          move      d_dato        a5dato
      *
     c                   if        a5vsor <> *blank
     c                   eval      vshel1_vsor = a5vsor
     c     vshel1_key    chain     vshel1                             90
     c                   if        *in90 = *off
     c                   eval      a5vbes = vcbesk
     c                   endif
     c                   endif
      *
     c                   if        a5ogrp <> 0
     c                   eval      vogrl1_oogr = a5ogrp
     c     vogrl1_key    chain     vogrl1                             90
     c                   if        *in90 = *off
     c                   eval      a5otxt = vgotxt
     c                   endif
     c                   endif
      *
     c                   if        a5hgrp <> 0
     c                   eval      vhgrl1_hogr = a5ogrp
     c                   eval      vhgrl1_hhgr = a5hgrp
     c     vhgrl1_key    chain     vhgrl1                             90
     c                   if        *in90 = *off
     c                   eval      a5htxt = vghtxt
     c                   endif
     c                   endif
      *
     c                   if        a5ugrp <> 0
     c                   eval      vugrl1_uogr = a5ogrp
     c                   eval      vugrl1_uhgr = a5hgrp
     c                   eval      vugrl1_uugr = a5ugrp
     c     vugrl1_key    chain     vugrl1                             90
     c                   if        *in90 = *off
     c                   eval      a5utxt = vgutxt
     c                   endif
     c                   endif
      *
     c                   if        a5ldor <> 0
     c                   eval      rlevl1_levr = a5ldor
     c     rlevl1_key    chain     rlevl1                             90
     c                   if        *in90 = *off
     c                   eval      a5lnav = rlnavn
     c                   endif
     c                   endif
      *
     c                   eval      vltpl1_lety = a5lety
     c     vltpl1_key    chain     vltpl1
     c                   if        %found
     c                   eval      a5lbes = vybesk
     c                   endif
      *
      * Skriver header
      *
     c                   write     a1hdr
     c                   write     a2hdr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av utskrift - utplukk på varesortiment
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandl_vsor  begsr
      *
      * Kaller utplukksprogram
      *
     c                   eval      b_svgr = *on
     c                   eval      b_sldo = *on
     c                   eval      b_stek = *on
      *
     c                   if        d_lval = 1
     c                   eval      b_sldo = *off
     c                   endif
      *
     c                   call      'VD700R'
     c                   parm                    w_firm
     c                   parm                    d_vsor
     c                   parm                    b_svgr
     c                   parm                    b_sldo
     c                   parm                    b_stek
6.20 c                   parm                    w_lage
      *
      * Leser utplukksfil som inneholder alle varer i sortimentet
      *
     c                   eval      w_vare_gml = *blank
      *
     c                   read      vvawpf                                 90
     c                   dow       *in90 = *off
      *
     c                   if        vvvare <> w_vare_gml
     c                   exsr      behandling
     c                   endif
      *
     c                   eval      w_vare_gml = vvvare
      *
     c                   read      vvawpf                                 90
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av utskrift - varegruppe og leverandør
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandl_vgrp  begsr
      *
      * Posisjonerer og leser i vareregister i.h.t. utplukk
      *
     c                   eval      vvarl9_ogrp = d_ogrp
     c                   eval      vvarl9_hgrp = d_hgrp
     c                   eval      vvarl9_ugrp = d_ugrp
     c                   eval      vvarl9_ldor = d_ldor
      *
     c                   select
     c                   when      d_ldor <> 0
     c     vvarl9_key1   setll     vvarl9
     c     vvarl9_key1   reade     vvarl9                                 90
     c                   when      d_ugrp <> 0
     c     vvarl9_key2   setll     vvarl9
     c     vvarl9_key2   reade     vvarl9                                 90
     c                   when      d_hgrp <> 0
     c     vvarl9_key3   setll     vvarl9
     c     vvarl9_key3   reade     vvarl9                                 90
     c                   other
     c     vvarl9_key4   setll     vvarl9
     c     vvarl9_key4   reade     vvarl9                                 90
     c                   endsl
      *
     c                   dow       *in90 = *off
      *
     c                   exsr      behandling
      *
     c                   select
     c                   when      d_ldor <> 0
     c     vvarl9_key1   reade     vvarl9                                 90
     c                   when      d_ugrp <> 0
     c     vvarl9_key2   reade     vvarl9                                 90
     c                   when      d_hgrp <> 0
     c     vvarl9_key3   reade     vvarl9                                 90
     c                   other
     c     vvarl9_key4   reade     vvarl9                                 90
     c                   endsl
      *
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av utskrift - kun leverandør
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandl_ldor  begsr
      *
      * Leser hele vareregisteret, og plukker varer tilhørende leverandør
      *
     c     vvarl9_key5   setll     vvarl9
     c     vvarl9_key5   reade     vvarl9                                 90
      *
     c                   dow       *in90 = *off
      *
     c                   if        vvldor <> d_ldor
     c                   goto      les_neste
     c                   endif
      *
     c                   exsr      behandling
      *
     c     les_neste     tag
      *
     c     vvarl9_key5   reade     vvarl9                                 90
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av utskrift - hele vareregisteret
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandl_tot   begsr
      *
     c     vvarl9_key5   setll     vvarl9
     c     vvarl9_key5   reade     vvarl9                                 90
      *
     c                   dow       *in90 = *off
      *
     c                   exsr      behandling
      *
     c     vvarl9_key5   reade     vvarl9                                 90
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
      * Siling av varer.
      * Diversevarer utelates
      * Skaffevarer skal kun være med dersom dette er valgt.
      * Varer merket 'ikke prisdistribusjon' og utgåtte varer utelates.
      *
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    vvvare
5.70 c                   parm                    w_lage
5.70 c                   parm                    p_vtyp
6.23 c                   parm                    w_udat
6.23 c                   parm                    w_ldor
6.23 c                   parm                    b_inlr
6.30 c                   parm                    w_lv_nobb
6.30 c                   parm                    w_lv_modn
6.30 c                   parm                    w_lv_lldo
6.30 c                   parm                    w_lv_llva
      *
     c                   if        p_vtyp = 'D'
     c                   leavesr
     c                   endif
      *
     c                   if        d_sval = 0 and
     c                             p_vtyp = 'S'
     c                   leavesr
     c                   endif
      *
     c                   if        vvkpdi = 1
     c                   leavesr
     c                   endif
6.20 c                   if        w_udat <> *loval and
6.20 c                             w_udat <= w_ddat
     c                   leavesr
     c                   endif
      *
      * Behandler brudd
      *
     c                   if        vvogrp <> w_ogrp_gml
     c                   exsr      hode_ogrp
     c                   endif
      *
     c                   if        vvogrp <> w_ogrp_gml or
     c                             vvhgrp <> w_hgrp_gml
     c                   exsr      hode_hgrp
     c                   endif
      *
     c                   if        vvogrp <> w_ogrp_gml or
     c                             vvhgrp <> w_hgrp_gml or
     c                             vvugrp <> w_ugrp_gml
     c                   exsr      hode_ugrp
     c                   endif
      *
     c                   if        vvogrp <> w_ogrp_gml or
     c                             vvhgrp <> w_hgrp_gml or
     c                             vvugrp <> w_ugrp_gml or
     c                             vvldor <> w_ldor_gml
     c                   exsr      hode_ldor
     c                   endif
      *
      * Henter priser fra vare
      *
     c                   exsr      priser
      *
      * Skriver detaljlinje
      *
     c                   exsr      detalj
      *
      * Dersom varetekst-2 eller enhet-2 finnes på vare, kalles subrutine
      * for utskrift av varetekst-2 og priser for enhet-2
      *
     c                   if        vvtek2 <> *blank and d_2val = 1 or
     c                             w_enh2 <> *blank
     c                   exsr      detalj_enh2
     c                   endif
      *
      * Dersom enhet-3 finnes på vare, kalles subrutine for utskrift av
      * priser for enhet-3
      *
     c                   if        w_enh3 <> *blank
     c                   exsr      detalj_enh3
     c                   endif
      *
      * Dersom det er valgt å ta med varetekst-2 på utskriften, skrives
      * blank linje for å få bedre oversikt i prisboken
      *
     c                   if        d_2val = 1
     c                   write     space                                30
     c                   exsr      overflow
     c                   endif
      *
      * Tar vare på brudd-variable
      *
     c                   eval      w_ogrp_gml = vvogrp
     c                   eval      w_hgrp_gml = vvhgrp
     c                   eval      w_ugrp_gml = vvugrp
     c                   eval      w_ldor_gml = vvldor
      *
     c                   eval      b_frst = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av priser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     priser        begsr
      *
     c                   eval      w_enh1 = *blank
     c                   eval      w_enh2 = *blank
     c                   eval      w_enh3 = *blank
     c                   eval      w_sap1 = 0
     c                   eval      w_sap2 = 0
     c                   eval      w_sap3 = 0
     c                   eval      w_tip1 = 0
     c                   eval      w_tip2 = 0
     c                   eval      w_tip3 = 0
      *
      * Leser prisbok-enheter på vare, og henter prisinformasjon
      *
     c                   eval      x = 0
      *
     c                   eval      vvenl3_vare = vvvare
     c                   eval      vvenl3_pben = 1
     c     vvenl3_key    setll     vvenl3
     c     vvenl3_key2   reade     vvenl3                                 92
     c                   dow       *in92 = *off and
     c                             x < 3
      *
6.20 c                   eval      vvprl1_ldor = vvldor
5.70 c                   eval      vvprl1_prgr = w_prgr
     c                   exsr      hent_pris
5.70 c                   if        b_pris = *off
5.70 c                   eval      vvprl1_prgr = *blank
5.70 c                   exsr      hent_pris
5.70 c                   endif
      *
     c     vvenl3_key2   reade     vvenl3                                 92
     c                   enddo
      *
      * Dersom prisbokenheter ikke ble funnet, eller dersom priser ikke
      * ble funnet på prisbokenheter, hentes priser på salgsenheter
      *
     c                   if        x = 0
      *
     c                   eval      vvenl2_vare = vvvare
     c                   eval      vvenl2_saen = 1
     c     vvenl2_key    setll     vvenl2
     c     vvenl2_key2   reade     vvenl2                                 92
     c                   dow       *in92 = *off and
     c                             x < 3
      *
6.20 c                   eval      vvprl1_ldor = vvldor
5.70 c                   eval      vvprl1_prgr = w_prgr
     c                   exsr      hent_pris
5.70 c                   if        b_pris = *off
5.70 c                   eval      vvprl1_prgr = *blank
5.70 c                   exsr      hent_pris
5.70 c                   endif
      *
     c     vvenl2_key2   reade     vvenl2                                 92
     c                   enddo
      *
     c                   endif
      *
      * Henter tilbudspriser dersom dette er valgt
      * Dersom tilbudspris ikke finnes på valgt leveringstype, hentes
      * tilbudsprisen fra leveringstype 0
      *
     c                   eval      b_tilb = *off
      *
     c                   if        d_tval = 1
      *
6.22 c                   eval      vtill1_ogrp = 0
6.22 c                   eval      vtill1_hgrp = 0
6.22 c                   eval      vtill1_ugrp = 0
6.22 c                   eval      vtill1_ldor = 0
     c                   eval      vtill1_vare = vvvare
5.70 c                   eval      vtill1_prgr = w_prgr
     c                   eval      vtill1_lety = d_lety
     c     vtill1_key    setll     vtill1
     c     vtill1_key    reade     vtill1
     c                   dow       not %eof
     c                   if        d_dato >= vtfdat and
     c                             d_dato <= vttdat
      *
     c                   eval      b_tilb = *on
      *
     c                   select
     c                   when      w_enh1 = vtenh1
     c                   eval      w_tip1 = vttip1
     c                   when      w_enh1 = vtenh2
     c                   eval      w_tip1 = vttip2
     c                   when      w_enh1 = vtenh3
     c                   eval      w_tip1 = vttip3
     c                   endsl
      *
     c                   if        w_enh2 <> *blank
     c                   select
     c                   when      w_enh2 = vtenh1
     c                   eval      w_tip2 = vttip1
     c                   when      w_enh2 = vtenh2
     c                   eval      w_tip2 = vttip2
     c                   when      w_enh2 = vtenh3
     c                   eval      w_tip2 = vttip3
     c                   endsl
     c                   endif
      *
     c                   if        w_enh3 <> *blank
     c                   select
     c                   when      w_enh3 = vtenh1
     c                   eval      w_tip3 = vttip1
     c                   when      w_enh3 = vtenh2
     c                   eval      w_tip3 = vttip2
     c                   when      w_enh3 = vtenh3
     c                   eval      w_tip3 = vttip3
     c                   endsl
     c                   endif
      *
     c                   leave
      *
     c                   endif
     c     vtill1_key    reade     vtill1
     c                   enddo
      *
     c                   if        d_lety <> 0 and
     c                             b_tilb = *off
      *
     c                   eval      vtill1_lety = 0
     c     vtill1_key    setll     vtill1
     c     vtill1_key    reade     vtill1
     c                   dow       not %eof
     c                   if        d_dato >= vtfdat and
     c                             d_dato <= vttdat
      *
     c                   select
     c                   when      w_enh1 = vtenh1
     c                   eval      w_tip1 = vttip1
     c                   when      w_enh1 = vtenh2
     c                   eval      w_tip1 = vttip2
     c                   when      w_enh1 = vtenh3
     c                   eval      w_tip1 = vttip3
     c                   endsl
      *
     c                   if        w_enh2 <> *blank
     c                   select
     c                   when      w_enh2 = vtenh1
     c                   eval      w_tip2 = vttip1
     c                   when      w_enh2 = vtenh2
     c                   eval      w_tip2 = vttip2
     c                   when      w_enh2 = vtenh3
     c                   eval      w_tip2 = vttip3
     c                   endsl
     c                   endif
      *
     c                   if        w_enh3 <> *blank
     c                   select
     c                   when      w_enh3 = vtenh1
     c                   eval      w_tip3 = vttip1
     c                   when      w_enh3 = vtenh2
     c                   eval      w_tip3 = vttip2
     c                   when      w_enh3 = vtenh3
     c                   eval      w_tip3 = vttip3
     c                   endsl
     c                   endif
      *
     c                   leave
      *
     c                   endif
     c     vtill1_key    reade     vtill1
     c                   enddo
      *
     c                   endif
      *
5.70  * Dersom det ikke ble funnet noe med prisgruppe prøver man
5.70  * med prisgruppe lik blank
5.70  *
5.70 c                   if        w_prgr <> *blank and
5.70 c                             b_tilb = *off
5.70 c                   eval      vtill1_vare = vvvare
5.70 c                   eval      vtill1_prgr = *blank
5.70 c                   eval      vtill1_lety = d_lety
5.70 c     vtill1_key    setll     vtill1
5.70 c     vtill1_key    reade     vtill1
5.70 c                   dow       not %eof
5.70 c                   if        d_dato >= vtfdat and
5.70 c                             d_dato <= vttdat
5.70  *
5.70 c                   eval      b_tilb = *on
5.70  *
5.70 c                   select
5.70 c                   when      w_enh1 = vtenh1
5.70 c                   eval      w_tip1 = vttip1
5.70 c                   when      w_enh1 = vtenh2
5.70 c                   eval      w_tip1 = vttip2
5.70 c                   when      w_enh1 = vtenh3
5.70 c                   eval      w_tip1 = vttip3
5.70 c                   endsl
5.70  *
5.70 c                   if        w_enh2 <> *blank
5.70 c                   select
5.70 c                   when      w_enh2 = vtenh1
5.70 c                   eval      w_tip2 = vttip1
5.70 c                   when      w_enh2 = vtenh2
5.70 c                   eval      w_tip2 = vttip2
5.70 c                   when      w_enh2 = vtenh3
5.70 c                   eval      w_tip2 = vttip3
5.70 c                   endsl
5.70 c                   endif
5.70  *
5.70 c                   if        w_enh3 <> *blank
5.70 c                   select
5.70 c                   when      w_enh3 = vtenh1
5.70 c                   eval      w_tip3 = vttip1
5.70 c                   when      w_enh3 = vtenh2
5.70 c                   eval      w_tip3 = vttip2
5.70 c                   when      w_enh3 = vtenh3
5.70 c                   eval      w_tip3 = vttip3
5.70 c                   endsl
5.70 c                   endif
5.70  *
5.70 c                   leave
5.70  *
5.70 c                   endif
5.70 c     vtill1_key    reade     vtill1
5.70 c                   enddo
5.70  *
5.70 c                   if        d_lety <> 0 and
5.70 c                             b_tilb = *off
5.70  *
5.70 c                   eval      vtill1_lety = 0
5.70 c     vtill1_key    setll     vtill1
5.70 c     vtill1_key    reade     vtill1
5.70 c                   dow       not %eof
5.70 c                   if        d_dato >= vtfdat and
5.70 c                             d_dato <= vttdat
5.70  *
5.70 c                   select
5.70 c                   when      w_enh1 = vtenh1
5.70 c                   eval      w_tip1 = vttip1
5.70 c                   when      w_enh1 = vtenh2
5.70 c                   eval      w_tip1 = vttip2
5.70 c                   when      w_enh1 = vtenh3
5.70 c                   eval      w_tip1 = vttip3
5.70 c                   endsl
5.70  *
5.70 c                   if        w_enh2 <> *blank
5.70 c                   select
5.70 c                   when      w_enh2 = vtenh1
5.70 c                   eval      w_tip2 = vttip1
5.70 c                   when      w_enh2 = vtenh2
5.70 c                   eval      w_tip2 = vttip2
5.70 c                   when      w_enh2 = vtenh3
5.70 c                   eval      w_tip2 = vttip3
5.70 c                   endsl
5.70 c                   endif
5.70  *
5.70 c                   if        w_enh3 <> *blank
5.70 c                   select
5.70 c                   when      w_enh3 = vtenh1
5.70 c                   eval      w_tip3 = vttip1
5.70 c                   when      w_enh3 = vtenh2
5.70 c                   eval      w_tip3 = vttip2
5.70 c                   when      w_enh3 = vtenh3
5.70 c                   eval      w_tip3 = vttip3
5.70 c                   endsl
5.70 c                   endif
5.70  *
5.70 c                   leave
5.70  *
5.70 c                   endif
5.70 c     vtill1_key    reade     vtill1
5.70 c                   enddo
5.70  *
5.70 c                   endif
5.70  *
5.70 c                   endif
6.22  * Dersom tilbud ikke ble funnet på oppgitt vare/prisgruppe/leveringstupe
6.22  * sjekkes varegruppe / leverandør
6.22  *
6.22  *  overgruppe,hovedgruppe,undergruppe
6.22  *
6.22 c                   if        b_tilb = *off
6.24 c                   eval      vtill1_prgr = w_prgr
6.24 c                   exsr      tilb_sjekk
6.24 c                   if        b_tilb = *off and
6.24 c                             w_prgr <> *blank
6.24 c                   eval      vtill1_prgr = *blank
6.24 c                   exsr      tilb_sjekk
6.24 c                   endif
6.24 c                   endif
      *
     c                   endif
      *
     c                   endsr
      *
6.24  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.24  * Subrutine for å hente sjekke om det finnes tilbud på varegr.nivå
6.24  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.24  *
6.24 c     tilb_sjekk    begsr
6.24  *
6.24 c                   if        b_tilb = *off
6.24 c                   eval      vtill1_ogrp = vvogrp
6.24 c                   eval      vtill1_hgrp = vvhgrp
6.24 c                   eval      vtill1_ugrp = vvugrp
6.24 c                   eval      vtill1_ldor = 0
6.24 c                   eval      vtill1_lety = 0
6.24 c                   eval      vtill1_vare = *blank
6.24 c                   exsr      hent_tilb
6.24 c                   endif
      *
6.24  *  overgruppe,hovedgruppe
6.24 c                   if        b_tilb = *off
6.24 c                   eval      vtill1_ugrp = 0
6.24 c                   exsr      hent_tilb
6.24 c                   endif
6.24  *  overgruppe
6.24 c                   if        b_tilb = *off
6.24 c                   eval      vtill1_hgrp = 0
6.24 c                   exsr      hent_tilb
6.24 c                   endif
6.24  *  overgruppe,hovedgruppe,undergruppe, leverandør
6.24 c                   if        b_tilb = *off
6.24 c                   eval      vtill1_ogrp = vvogrp
6.24 c                   eval      vtill1_hgrp = vvhgrp
6.24 c                   eval      vtill1_ugrp = vvugrp
6.24 c                   eval      vtill1_ldor = w_pldo
6.24 c                   exsr      hent_tilb
6.24 c                   endif
6.24  *  overgruppe,hovedgruppe              leverandør
6.24 c                   if        b_tilb = *off
6.24 c                   eval      vtill1_ugrp = 0
6.24 c                   exsr      hent_tilb
6.24 c                   endif
6.24  *  overgruppe                          leverandør
6.24 c                   if        b_tilb = *off
6.24 c                   eval      vtill1_hgrp = 0
6.24 c                   exsr      hent_tilb
6.24 c                   endif
6.24  *  leverandør
6.24 c                   if        b_tilb = *off
6.24 c                   eval      vtill1_ogrp = 0
6.24 c                   exsr      hent_tilb
6.24 c                   endif
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av priser på vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_pris     begsr
      *
     c                   eval      b_pris = *off
      *
     c                   eval      vvprl1_vare = vvvare
     c                   eval      vvprl1_enhe = veenhe
     c                   eval      vvprl1_lety = d_lety
     c     vvprl1_key    setll     vvprl1
     c     vvprl1_key    reade     vvprl1
      *
     c                   if        not %eof
     c                   eval      x = x + 1
     c                   endif
      *
     c                   dow       not %eof
      *
     c                   eval      b_pris = *on
      *
     c                   select
     c                   when      x = 1
     c                   eval      w_enh1 = vpenhe
     c                   eval      w_sap1 = vpsapr
     c                   if        vpbupr <> 0 and
     c                             vpbupr < vpsapr
     c                   eval      w_sap1 = vpbupr
     c                   endif
     c                   when      x = 2
     c                   eval      w_enh2 = vpenhe
     c                   eval      w_sap2 = vpsapr
     c                   if        vpbupr <> 0 and
     c                             vpbupr < vpsapr
     c                   eval      w_sap2 = vpbupr
     c                   endif
     c                   when      x = 3
     c                   eval      w_enh3 = vpenhe
     c                   eval      w_sap3 = vpsapr
     c                   if        vpbupr <> 0 and
     c                             vpbupr < vpsapr
     c                   eval      w_sap3 = vpbupr
     c                   endif
     c                   endsl
6.22 c                   eval      w_pldo = vpldor
      *
     c                   if        d_dato >= vppdat
     c                   leave
     c                   endif
      *
     c     vvprl1_key    reade     vvprl1
     c                   enddo
      *
      * Dersom pris ikke ble funnet på valgt leveringstype, hentes pris fra
      * leveringstype 0
      *
     c                   if        b_pris = *off and
     c                             d_lety <> 0
      *
     c                   eval      vvprl1_lety = 0
     c     vvprl1_key    setll     vvprl1
     c     vvprl1_key    reade     vvprl1
      *
     c                   if        not %eof
     c                   eval      x = x + 1
     c                   endif
      *
     c                   dow       not %eof
      *
     c                   select
     c                   when      x = 1
     c                   eval      w_enh1 = vpenhe
     c                   eval      w_sap1 = vpsapr
     c                   if        vpbupr <> 0 and
     c                             vpbupr < vpsapr
     c                   eval      w_sap1 = vpbupr
     c                   endif
     c                   when      x = 2
     c                   eval      w_enh2 = vpenhe
     c                   eval      w_sap2 = vpsapr
     c                   if        vpbupr <> 0 and
     c                             vpbupr < vpsapr
     c                   eval      w_sap2 = vpbupr
     c                   endif
     c                   when      x = 3
     c                   eval      w_enh3 = vpenhe
     c                   eval      w_sap3 = vpsapr
     c                   if        vpbupr <> 0 and
     c                             vpbupr < vpsapr
     c                   eval      w_sap3 = vpbupr
     c                   endif
     c                   endsl
6.22 c                   eval      w_pldo = vpldor
      *
     c                   if        d_dato >= vppdat
     c                   leave
     c                   endif
      *
     c     vvprl1_key    reade     vvprl1
     c                   enddo
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift ved brudd på overgruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode_ogrp     begsr
      *
     c                   eval      w_otxt = *blank
      *
     c                   eval      vogrl1_oogr = vvogrp
     c     vogrl1_key    chain     vogrl1                             91
     c                   if        *in91 = *off
     c                   eval      w_otxt = vgotxt
     c                   endif
      *
     c                   if        b_frst = *off
     c                   write     a1hdr
     c                   write     a2hdr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift ved brudd på hovedgruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode_hgrp     begsr
      *
     c                   eval      w_htxt = *blank
      *
     c                   eval      vhgrl1_hogr = vvogrp
     c                   eval      vhgrl1_hhgr = vvhgrp
     c     vhgrl1_key    chain     vhgrl1                             91
     c                   if        *in91 = *off
     c                   eval      w_htxt = vghtxt
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift ved brudd på undergruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode_ugrp     begsr
      *
     c                   eval      w_utxt = *blank
      *
     c                   eval      vugrl1_uogr = vvogrp
     c                   eval      vugrl1_uhgr = vvhgrp
     c                   eval      vugrl1_uugr = vvugrp
     c     vugrl1_key    chain     vugrl1                             91
     c                   if        *in91 = *off
     c                   eval      w_utxt = vgutxt
     c                   endif
      *
     c                   move      vvogrp        w_ogrp_alf
     c                   move      vvhgrp        w_hgrp_alf
     c                   move      vvugrp        w_ugrp_alf
      *
     c                   eval      a3txt1 = %trim( %trim(w_otxt) + ' - ' +
     c                                             %trim(w_htxt) + ' - ' +
     c                                             %trim(w_utxt) + ' (' +
     c                                             w_ogrp_alf + ' ' +
     c                                             w_hgrp_alf + ' ' +
     c                                             w_ugrp_alf + ')')
      *
     c                   eval      a3txt2 = a3txt1
      *
     c                   write     a3hdr                                30
     c                   exsr      overflow
      *
      * Tar vare på sidenummer i index-array
      *
     c                   eval      d_text = a3txt2
     c                   eval      d_side = d_pagnbr
      *
     c                   eval      w_i1 = w_i1 + 1
     c                   eval      a_index(w_i1) = d_index
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift ved brudd på leverandøre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode_ldor     begsr
      *
      * Dersom leverandør er utelatt eller mangler på vare, skrives blank
      * linje
      *
     c                   if        d_lval = 1 or
     c                             vvldor = 0
     c                   write     space                                30
     c                   exsr      overflow
     c                   leavesr
     c                   endif
      *
      * Dersom leverandør ikke er utelatt, skrives leverandørnavn
      *
     c                   eval      w_lnav = *blank
      *
     c                   eval      rlevl1_levr = vvldor
     c     rlevl1_key    chain     rlevl1                             91
     c                   if        *in91 = *off
     c                   eval      w_lnav = rlnavn
     c                   endif
      *
     c                   move      vvldor        w_ldor_alf
      *
     c                   eval      a4txt1 = %trim( %trim(w_lnav) + ' (' +
     c                                      w_ldor_alf + ')')
      *
     c                   eval      a4txt2 = a4txt1
      *
     c                   write     a4hdr                                30
     c                   exsr      overflow
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine utskrift av detaljlinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     detalj        begsr
      *
     c                   eval      d1tek1 = vvtek1
     c                   eval      d1vare = vvvare
6.30 c                   if        w_lv_nobb > 0
6.30 c                   eval      d1nobb = w_lv_nobb
6.30 c                   else
     c                   eval      d1nobb = vvnobb
6.30 c                   endif
     c                   eval      d1enh1 = w_enh1
     c                   eval      d1vtyp = *blank
     c                   eval      d1ptxt = *blank
     c                   eval      d1sapr = 0
     c                   eval      d1saim = 0
      *
     c                   if        p_vtyp <> 'L'
     c                   eval      d1vtyp = p_vtyp
     c                   endif
      *
      * Finner priser
      *
     c                   if        w_tip1 <> 0
     c                   eval      d1sapr = w_tip1
     c                   else
     c                   eval      d1sapr = w_sap1
     c                   endif
      *
     c                   eval      d1saim = d1sapr * w_mvat
      *
     c                   if        d1saim <> 0
     c                   eval      w_kode = 4
     c                   call      'FA909R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   parm                    w_kode
     c                   parm                    d1saim
     c                   endif
      *
      * Henter beskrivelse av priskode
      *
     c                   if        w_tip1 <> 0
     c                   eval      w_pkod = 'T'
     c                   else
     c                   eval      w_pkod = vvkpri
     c                   endif
      *
     c                   if        w_pkod <> *blank
     c                   eval      vpkol1_pkod = w_pkod
     c     vpkol1_key    chain     vpkol1                             92
     c                   if        *in92 = *off
     c                   eval      d1ptxt = vaptxt
     c                   endif
     c                   endif
      *
      * Skriv detaljlinje
      *
     c                   write     d1dtl                                30
     c                   exsr      overflow
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine utskrift av detaljlinje for enhet-2
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     detalj_enh2   begsr
      *
     c                   eval      d2tek2 = *blank
     c                   eval      d2ptxt = *blank
     c                   eval      d2enh2 = w_enh2
     c                   eval      d2sapr = 0
     c                   eval      d2saim = 0
      *
      * Varetekst-2
      *
     c                   if        d_2val = 1
     c                   eval      d2tek2 = vvtek2
     c                   endif
      *
      * Finner priser for enhet-2
      *
     c                   if        w_enh2 <> *blank
      *
     c                   if        w_tip2 <> 0
     c                   eval      d2sapr = w_tip2
     c                   else
     c                   eval      d2sapr = w_sap2
     c                   endif
      *
     c                   eval      d2saim = d2sapr * w_mvat
      *
     c                   if        d2saim <> 0
     c                   eval      w_kode = 4
     c                   call      'FA909R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   parm                    w_kode
     c                   parm                    d2saim
     c                   endif
      *
     c                   if        w_tip2 <> 0
     c                   eval      w_pkod = 'T'
     c                   else
     c                   eval      w_pkod = vvkpri
     c                   endif
      *
     c                   if        w_pkod <> *blank
     c                   eval      vpkol1_pkod = w_pkod
     c     vpkol1_key    chain     vpkol1                             92
     c                   if        *in92 = *off
     c                   eval      d2ptxt = vaptxt
     c                   endif
     c                   endif
      *
     c                   endif
      *
      * Skriv detaljlinje
      *
     c                   write     d2dtl                                30
     c                   exsr      overflow
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine utskrift av detaljlinje for enhet-3
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     detalj_enh3   begsr
      *
     c                   eval      d3ptxt = *blank
     c                   eval      d3enh3 = w_enh3
     c                   eval      d3sapr = 0
     c                   eval      d3saim = 0
      *
      * Finner priser for enhet-3
      *
     c                   if        w_tip3 <> 0
     c                   eval      d3sapr = w_tip3
     c                   else
     c                   eval      d3sapr = w_sap3
     c                   endif
      *
     c                   eval      d3saim = d3sapr * w_mvat
      *
     c                   if        d3saim <> 0
     c                   eval      w_kode = 4
     c                   call      'FA909R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   parm                    w_kode
     c                   parm                    d3saim
     c                   endif
      *
     c                   if        w_tip3 <> 0
     c                   eval      w_pkod = 'T'
     c                   else
     c                   eval      w_pkod = vvkpri
     c                   endif
      *
     c                   if        w_pkod <> *blank
     c                   eval      vpkol1_pkod = w_pkod
     c     vpkol1_key    chain     vpkol1                             92
     c                   if        *in92 = *off
     c                   eval      d3ptxt = vaptxt
     c                   endif
     c                   endif
      *
      * Skriv detaljlinje
      *
     c                   write     d3dtl                                30
     c                   exsr      overflow
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for håndtering av overflow
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     overflow      begsr
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   write     a2hdr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift av innholdsfortegnelse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     index         begsr
      *
      * Fjerner sidenummer fra innholdsfortegnelse
      *
     c                   eval      *in80 = *on
      *
      * Utskrift av parametre
      *
     c                   write     a1hdr
     c                   write     a5hdr
      *
      * Utskrift av innholdsfortegnelse
      *
     c                   write     a6hdr
      *
      * Skriver linjer i innholdsfortegnelsen
      *
     c                   dow       w_i2 < w_i1
      *
     c                   eval      w_i2 = w_i2 + 1
      *
     c                   eval      d_index = a_index(w_i2)
      *
     c                   eval      d4text = %trim(d_text) + '   ' +
     c                                     '..............................'+
     c                                     '..............................'+
     c                                     '..............................'+
     c                                     '..............................'
     c                   eval      d4side = d_side
      *
     c                   write     d4dtl                                30
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   write     a6hdr
     c                   endif
      *
     c                   enddo
      *
     c                   endsr
6.22  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.22  * Subrutine for å hente tilbudspris på varegruppe/leverandørnivå
6.22  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.22  *
6.22 c     hent_tilb     begsr
6.22  *
6.22 c                   eval      b_tilb = *off
6.22 c     vtill1_key    setll     vtill1
6.22 c     vtill1_key    reade     vtill1                                 90
6.22 c                   dow       *in90 = *off
     c                   if        d_dato >= vtfdat and
     c                             d_dato <= vttdat
6.22  *
6.22 c                   eval      b_tilb = *on
6.22 c                   eval(h)   w_tip1 = w_sap1 -
6.22 c                                      (w_sap1 * vtpro1)/100
6.22  *
6.22 c                   if        w_enh2 <> *blank
6.22 c                   eval(h)   w_tip2 = w_sap2 -
6.22 c                                      (w_sap2 * vtpro1)/100
6.22 c                   endif
6.22  *
6.22 c                   if        w_enh3 <> *blank
6.22 c                   eval(h)   w_tip3 = w_sap3 -
6.22 c                                      (w_sap3 * vtpro1)/100
6.22 c                   endif
6.22  *
6.22 c                   leave
6.22  *
6.22 c                   endif
6.22 c     vtill1_key    reade     vtill1                                 90
6.22 c                   enddo
6.22  *
6.22 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_parm
      *
      * Key for les på vareregister med varegruppe og leverandør
      *
     c     vvarl9_key1   klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl9_ogrp
     c                   kfld                    vvarl9_hgrp
     c                   kfld                    vvarl9_ugrp
     c                   kfld                    vvarl9_ldor
      *
     c     vvarl9_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl9_ogrp
     c                   kfld                    vvarl9_hgrp
     c                   kfld                    vvarl9_ugrp
      *
     c     vvarl9_key3   klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl9_ogrp
     c                   kfld                    vvarl9_hgrp
      *
     c     vvarl9_key4   klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl9_ogrp
      *
     c     vvarl9_key5   klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på firmaregister
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på varesortiment-hode
      *
     c     vshel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vshel1_vsor
      *
      * Key for oppslag på overgruppe-register
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe-register
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe-register
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag på leveringstype
      *
     c     vltpl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltpl1_lety
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for les på vare-enhet-register m/prisbok-enhet
      *
     c     vvenl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl3_vare
     c                   kfld                    vvenl3_pben
      *
     c     vvenl3_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl3_vare
      *
      * Key for les på vare-enhet-register m/salgsenhet
      *
     c     vvenl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
     c                   kfld                    vvenl2_saen
      *
     c     vvenl2_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
      *
      * Key for les på vare-pris-register
      *
     c     vvprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvprl1_vare
6.20 c                   kfld                    vvprl1_ldor
5.70 c                   kfld                    vvprl1_prgr
     c                   kfld                    vvprl1_enhe
     c                   kfld                    vvprl1_lety
      *
      * Key for les på tilbuds-register
      *
     c     vtill1_key    klist
     c                   kfld                    w_firm
6.22 c                   kfld                    vtill1_ogrp
6.22 c                   kfld                    vtill1_hgrp
6.22 c                   kfld                    vtill1_ugrp
6.22 c                   kfld                    vtill1_ldor
     c                   kfld                    vtill1_vare
5.70 c                   kfld                    vtill1_prgr
     c                   kfld                    vtill1_lety
      *
      * Key for oppslag på priskode-register
      *
     c     vpkol1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vpkol1_pkod
      *
      *
      * Splitter datastruktur i variable
      *
     c                   eval      d_list = p_parm
      *
     c                   eval      w_firm = d_firm
      *
      * Henter mva-faktor
      *
     c                   call      'FÅ705R'
     c                   parm                    w_firm
     c                   parm                    d_dato
     c                   parm                    w_mvap
     c                   parm                    w_mvat
     c                   parm                    w_mvaf
      *
      * Henter dagens dato
      *
     c                   time                    w_ddat
      *
5.70  * Henter prisgruppe
5.70  *
5.70 c                   eval      w_lage = d_lage
5.70 c                   eval      w_avde = 0
5.70  *
5.70 c                   call      'VL712R   '
5.70 c                   parm                    w_firm
5.70 c                   parm                    w_lage
5.70 c                   parm                    w_avde
5.70 c                   parm                    w_prgr
      *
     c                   endsr
