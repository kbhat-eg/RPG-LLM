      /title  ASOFAK Oppretting av ordretrans. v/ alt.fakturering
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FO190R
      * Beskrivelse . . : Oppretting av ordretrans. v/ alt.fakturering
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       020804 GRO  Nytt program
6.10  *       021210 bsa  Innført flere korttyper. Legger kun ut VISA og Europay
6.10  *                   på FNTRPF. Info om kort legges på ordrehodet for vedli
6.10  *                   Nytt program for innhenting av kortinfo
6.10  *                   Vi benytter ikke lenger FMKAPF som hovedfile for kort.
6.10  *                   av RUKRPF. Klarer ikke holde to like registre i synk.
6.21  *       070613 bif  Teste på lik måte etter kall av fm501r (som i fo101r)
6.nu  * R6.30 030614 bsa  Programmet gjennomgått mtp nummerutvidelser.
7.01  * R6.30 171117 bsa  Hvis kredit ordretype, fjernes kortmerke og sperrekode
      *
8.01  * 8.xx  050522 bsa  Feil logikk rundt sjekk mot kredit ordretype.(7.01)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffntrl1    if   e           k disk    rename(fntrpfr:fntrl1r)
     ffmkal1    if   e           k disk    rename(fmkapfr:fmkal1r)
     ffntrlu    uf a e           k disk    rename(fntrpfr:fntrlur)
6.10 ffohelu    uf   e           k disk    rename(fohepfr:fohelur)
7.01 ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
6.10 frukrlr    if   e           k disk    rename(rukrpfr:rukrlrr)
6.10 fruksl1    if   e           k disk    rename(rukspfr:ruksl1r)
7.01 fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
      *
      * Definering av parametre
      *
     d p_stat          s              1
     d p_firm          s                   like(fnfirm)
6.nu d p_numm          s                   like(fnnumm)
     d p_suff          s                   like(fnsuff)
     d p_kund          s                   like(fnkund)
     d p_kpro          s                   like(fnkpro)
     d p_kftp          s                   like(fnkftp)
     d p_ktsq          s                   like(fnktsq)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
7.01 d l_filg                931    933
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d rkunl1_kund     s                   like(rkkund)
     d fntrl1_numm     s                   like(fnnumm)
     d fntrl1_suff     s                   like(fnsuff)
     d fmkal1_kund     s                   like(fmkund)
     d fntrlu_numm     s                   like(fnnumm)
     d fntrlu_suff     s                   like(fnsuff)
6.10 d fohelu_numm     s                   like(fonumm)
6.10 d fohelu_suff     s                   like(fosuff)
6.10 d ruksl1_skty     s                   like(ruskty)
6.10 d ruksl1_slag     s                   like(ruslag)
6.10 d rukrlr_kkun     s                   like(rukkun)
7.01 d votyl1_otyp     s                   like(vaotyp)
7.01 d fohel1_numm     s                   like(fonumm)
7.01 d fohel1_suff     s                   like(fosuff)
      *
      * Definering av variable
      *
     d w_firm          s                   like(fnfirm)
     d b_avta          s              1    inz(*off)
6.10 d w_kort          s              1
      *
7.01 d u_s701          s              1    inz(*off)
      *
7.01   dcl-s co402_verdi1  char(1);
7.01   dcl-s co402_verdi2  char(2048);
7.01   dcl-pr co402r extpgm('CO402R');
7.01     p_filgrp            char(3)     const;
7.01     p_firm              packed(3:0) const;
7.01     p_lager             packed(2:0) const;
7.01     p_nokkel            char(50)    const;
7.01     p_verdi1            char(1);
7.01     p_verdi2            char(2048);
7.01   end-pr;
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   if        p_suff <> *zero
     c                   exsr      gen_suff
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      fntrl1_numm = p_numm
     c                   eval      fntrl1_suff = *zero
     c     fntrl1_key    chain     fntrl1r
     c                   if        %found
     c                   goto      avslutt
     c                   endif
      *
6.10 c                   if        1 = 2
     c                   exsr      sjk_avt
6.10 c                   endif
6.10 c                   exsr      sjk_kavt
     c                   if        b_avta = *off
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      p_kftp = *zero
     c                   eval      p_ktsq = *zero
      *
6.10 c**                 call      'FM500R'
6.10 c                   call      'FM501R'
     c                   parm                    p_firm
     c                   parm                    p_kund
     c                   parm                    p_kpro
     c                   parm                    p_kftp
     c                   parm                    p_ktsq
      *
6.21 c                   if        p_kftp <> *zero
6.21 c*                            p_ktsq <> *zero
6.10 c                   if        p_kftp = 1 or
6.10 c                             p_kftp = 2
     c                   exsr      skr_trans
6.10 c                   endif
8.01 c**                 if        u_s701 = *on
7.01 c                   eval      fohel1_numm = p_numm
7.01 c                   eval      fohel1_suff = p_suff
7.01 c     fohel1_key    chain     fohel1
7.01 c                   if        %found
7.01 c                   eval      votyl1_otyp = footyp
7.01 c     votyl1_key    chain     votyl1
8.01 c**                 if        %found and
8.01 c**                           vaokre <> '-'
8.10 c**                 exsr      opd_ohead
8.01 c**                 endif
7.01 c                   endif
7.01  * Sjekk om ordretypen ikke er kredit
8.01 c                   if        u_s701 = *off
8.01 c                   exsr      opd_ohead
8.01 c                   else
8.01 c                   if        vaokre <> '-'
8.01 c                   exsr      opd_ohead
8.01 c                   endif
8.01 c                   endif
8.01 c**                 endif
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
6.10  * Subrutine for oppdatering av ordrehode
6.10  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.10  *
6.10 c     opd_ohead     begsr
6.10  *
6.10 c                   eval      fohelu_numm = p_numm
6.10 c                   eval      fohelu_suff = p_suff
6.10 c     fohelu_key    chain     fohelu
6.10  *
6.10 c                   if        %found
6.10 c                   eval      fopsuf = p_kftp
6.10 c                   update    fohelur
6.10 c                   endif
6.10  *
6.10 c                   endsr
6.10  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekk av aktuelle avtaler på kunde/prosjekt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_avt       begsr
      *
     c                   eval      b_avta = *off
      *
     c                   eval      fmkal1_kund = p_kund
     c     fmkal1_key    setll     fmkal1
     c     fmkal1_key    reade     fmkal1
      *
     c                   dow       not %eof
     c                   if        fmkpro = *zero or
     c                             fmkpro = p_kpro
     c                   eval      b_avta = *on
     c                   endif
      *
     c                   if        b_avta = *on
     c                   leave
     c                   endif
      *
     c     fmkal1_key    reade     fmkal1
     c                   enddo
      *
     c                   endsr
      *
6.10  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.10  * Subrutine for sjekk av aktuelle avtaler på kunde/prosjekt
6.10  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.10  *
6.10 c     sjk_kavt      begsr
6.10  *
6.10 c                   eval      b_avta = *off
6.10  *
6.10 c                   eval      rukrlr_kkun = p_kund
6.10 c     rukrlr_key    setll     rukrlr
6.10 c     rukrlr_key    reade     rukrlr
6.10  *
6.10 c                   dow       not %eof
6.10 c                   if        rukpro = *zero or
6.10 c                             rukpro = p_kpro
6.10 c                   eval      b_avta = *on
6.10 c                   endif
6.10  *
6.10 c                   if        b_avta = *on
6.10 c                   leave
6.10 c                   endif
6.10  *
6.10 c     rukrlr_key    reade     rukrlr
6.10 c                   enddo
6.10  *
6.10 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av trans.record
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_trans     begsr
      *
     c                   clear                   fntrlur
      *
     c                   eval      fnfirm = w_firm
     c                   eval      fnnumm = p_numm
     c                   eval      fnsuff = p_suff
     c                   eval      fnkftp = p_kftp
     c                   eval      fnktsq = p_ktsq
     c                   eval      fnkund = p_kund
     c                   eval      fnkpro = p_kpro
      *
     c                   time                    fnetim
     c                   time                    fnedat
     c                   eval      fneusr = l_user
     c                   eval      fnaarr = %subdt(fnedat:*years)
      *
     c                   write     fntrlur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for gen. av trans.post v/suffix <> 0
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     gen_suff      begsr
      *
     c                   eval      fntrl1_numm = p_numm
     c                   eval      fntrl1_suff = *zero
     c     fntrl1_key    chain     fntrl1r
      *
     c                   if        %found
     c                   eval      fnsuff = p_suff
     c                   eval      fnkdat = *loval
     c                   eval      fnktim = *loval
     c                   time                    fnetim
     c                   time                    fnedat
     c                   eval      fneusr = l_user
     c                   eval      fnaarr = %subdt(fnedat:*years)
     c                   write     fntrlur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_stat
     c                   parm                    p_firm
6.nu c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_kund
     c                   parm                    p_kpro
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for kort/fakt trans/numm
      *
     c     fntrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fntrl1_numm
     c                   kfld                    fntrl1_suff
      *
      * Key for les av kort/fakt.avtalereg.
      *
     c     fmkal1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fmkal1_kund
      *
      * Key for oppdat. av trans.-register
      *
     c     fntrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fntrlu_numm
     c                   kfld                    fntrlu_suff
6.10  *
6.10  * Key for oppslag på kunde
6.10  *
6.10 c     rukrlr_key    klist
6.10 c                   kfld                    w_firm
6.10 c                   kfld                    rukrlr_kkun
6.10  *
6.10  * Key for oppdat. av ordrehodet
6.10  *
6.10 c     fohelu_key    klist
6.10 c                   kfld                    w_firm
6.10 c                   kfld                    fohelu_numm
6.10 c                   kfld                    fohelu_suff
7.01  *
7.01  * Key for oppdat. av ordrehodet
7.01  *
7.01 c     fohel1_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    fohel1_numm
7.01 c                   kfld                    fohel1_suff
7.01  *
7.01  * Key for oppdat. av ordretype
7.01  *
7.01 c     votyl1_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    votyl1_otyp
      *
      * Henter firma parameter
      *
     c                   eval      w_firm = p_firm
7.01  *
7.01  * Hvis kredit ordretype, fjernes kortmerke og sperrekode
7.01  *
7.01   CallP CO402R(l_filg:w_firm:0:'FO190_701':
7.01                    co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01     u_s701 = *on;
7.01   endif;
      *
     c                   endsr
