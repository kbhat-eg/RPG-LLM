     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : JO721R
      * Beskrivelse . . : Maskinell matching mot EAN-nummer.
      *                   Programmet leser EAN-nummer register (VEANPF), og
      *                   matcher dette mot EAN-nummer i LVR. Dersom match
      *                   oppdateres matche-register med oppdat.kode '3'.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       101196 HKO  Nytt program
6.11  *       110709 bof  Matcher både mot ean på vare-enhet og ean-reg.
6.12  *       250909 bof  Endret fra VVENPF VVENPF
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fveanl1    if   e           k disk    rename(veanpfr:veanl1r)
6.12 fvvepl2    if   e           k disk    rename(vveppfr:vvepl2r)
     fjmatlu    uf   e           k disk    rename(jmatpfr:jmatlur)
     fjvarld    if   e           k disk    rename(jvarpfr:jvarldr)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(jofirm)
      *
      * Definering av variable i nøkler
      *
     d jmatlu_var1     s                   like(jovar1)
     d jvarld_ean1     s                   like(jvean1)
      *
      * Definering av variable
      *
     d w_firm          s                   like(jofirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser EVR ean-register
      *
     c     veanl1_key    setll     veanl1
     c                   read      veanl1                                 90
     c                   dow       *in90 = *off
      *
     c                   if        vxeann = 0
     c                   goto      les_neste
     c                   endif
      *
      * Gjør oppslag på vare i LVR
      *
     c                   eval      jvarld_ean1 = vxeann
     c     jvarld_key    chain     jvarld                             91
     c                   if        *in91 = *on
     c                   goto      les_neste
     c                   endif
      *
      * Oppdaterer matche-register med matchet vare
      *
     c                   if        vxvare <> *blank
      *
     c                   eval      jmatlu_var1 = vxvare
     c     jmatlu_key    chain     jmatlur                            92
     c                   if        *in92 = *off
     c                   if        jokode <> 0 or
     c                             jovkod <> 0
     c                   unlock    jmatlu
     c                   else
     c                   eval      jovar2 = jvvare
     c                   eval      joldo2 = jvldor
     c                   eval      jokode = 3
     c                   update    jmatlur
     c                   endif
     c                   endif
      *
     c                   endif
      *
     c     les_neste     tag
      *
     c                   read      veanl1                                 90
     c                   enddo
6.11  *
6.11  * Leser EVR vare-enhet med ean-nr
6.11  *
6.12 c     vvepl2_key    setll     vvepl2
6.12 c                   read      vvepl2                                 90
6.11 c                   dow       *in90 = *off
6.11  *
6.12 c                   if        vepgti = 0
6.11 c                   goto      les_neste2
6.11 c                   endif
6.11  *
6.11  * Gjør oppslag på vare i LVR
6.11  *
6.12 c                   eval      jvarld_ean1 = vepgti
6.11 c     jvarld_key    chain     jvarld                             91
6.11 c                   if        *in91 = *on
6.11 c                   goto      les_neste2
6.11 c                   endif
6.11  *
6.11  * Oppdaterer matche-register med matchet vare
6.11  *
6.12 c                   if        vepvar <> *blank
6.11  *
6.12 c                   eval      jmatlu_var1 = vepvar
6.11 c     jmatlu_key    chain     jmatlur                            92
6.11 c                   if        *in92 = *off
6.11 c                   if        jokode <> 0 or
6.11 c                             jovkod <> 0
6.11 c                   unlock    jmatlu
6.11 c                   else
6.11 c                   eval      jovar2 = jvvare
6.11 c                   eval      joldo2 = jvldor
6.11 c                   eval      jokode = 3
6.11 c                   update    jmatlur
6.11 c                   endif
6.11 c                   endif
6.11  *
6.11 c                   endif
6.11  *
6.11 c     les_neste2    tag
6.11  *
6.12 c                   read      vvepl2                                 90
6.11 c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
      *
      * Key for les på matche-register
      *
     c     veanl1_key    klist
     c                   kfld                    w_firm
6.11  *
6.11  * Key for les på vare-enhet på ean
6.11  *
6.12 c     vvepl2_key    klist
6.11 c                   kfld                    w_firm
      *
      * Key for oppdatering av matche-register
      *
     c     jmatlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jmatlu_var1
      *
      * Key for oppslag på vare
      *
     c     jvarld_key    klist
     c                   kfld                    jvarld_ean1
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
