     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : LO647R
      * Beskrivelse . . : Utskrift av bestilling-slettet-liste
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset Lor . :
      *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.
      * --------- ------  -------------------------------------------------
      *  6.10  170609 BSA  Oppdatert med sporingsinformasjon
6.nu  *  6.30  120614 bof  Endret ihht. nummerutidelse
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
     fldell1    if   e           k disk    rename(ldelpfr:ldell1r)
     fldellu    uf   e           k disk    rename(ldelpfr:ldellur)
     flo647p    o    e             printer
      *
      *  Local Data Area
      *
     d                uds
6.10 d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Parameter-inn
      *
     d                 ds
     d d_prec                        64
     d  d_val1                        1  0 overlay(d_prec)
      *                                                                     -
      * Definering av variable i nøkler
      *                                                                     -
     d rlevl1_levr     s                   like(rllevr)
     d ldell1_aarr     s                   like(lyaarr)
     d ldell1_numm     s                   like(lynumm)
     d ldell1_suff     s                   like(lysuff)
     d ldellu_aarr     s                   like(lyaarr)
     d ldellu_numm     s                   like(lynumm)
     d ldellu_suff     s                   like(lysuff)
      *                                                                     -
      * Definering av variable
      *                                                                     -
     d w_firm          s                   like(lyfirm)
     d w_fast          s             10
     d w_fell          s              6
     d w_kode          s              1
6.nu d w_numm          s              8  0
     d w_type          s              2
     d w_parm          s             64
      *
      * Benyttede formater
      * - - - - - - - - - -
      *
      * A1HDR  - Heading A1 på rapport
      * D1DET  - Detail  D1 på rapport
      * T1TOT  - Total   T1 på rapport
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * A1HDR Behandle heading A1
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Henter neste ledige SLETTE-LISTE-nummer fra Nummer-register
      *
     c                   if        d_val1 = 1
     c                   eval      w_numm = 0
     c                   eval      w_kode = '0'
     c                   exsr      hntnum
     c                   eval      d1list = w_numm
     c                   endif
      *
      * Skrive heading
      *
     c                   write     a1hdr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * D1DET Behandle file
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Lese file
      *
     c     ldell1_key    setll     ldell1
     c                   read      ldell1
      *
     c                   dow       not %eof
     c                   if        lyprtk <> 1
      *
      * Hent opplysninger fra LEVERANDØR-REGISTER
      *
     c                   eval      rlevl1_levr = lyldor
     c     rlevl1_key    chain     rlevl1r
     c                   eval      d1navn = rlnavn
      *
      * Oppdaterer BESTILLING-SLETTET-REGISTER, Posten er tatt med på liste
      *
     c                   if        d_val1 = 1
     c                   eval      ldellu_aarr = lyaarr
     c                   eval      ldellu_numm = lynumm
     c                   eval      ldellu_suff = lysuff
     c     ldellu_key    chain     ldellur
     c                   if        %found
     c                   eval      lyprtk = 1
6.10 c                   time                    lyedat
6.10 c                   time                    lyetim
6.10 c                   eval      lyeusr = l_user
     c                   update    ldellur
     c                   endif
     c                   endif
      * Behandle record
      *
     c                   eval      t1post = t1post + 1
     c                   write     d1det                                30
      *
      * Er det overflow ?
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
      *
     c                   endif
     c                   read      ldell1
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * T1TOT Behandle total T1
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Skrive total
      *
     c                   write     t1tot                                30
     c                   goto      end_pgm
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
     c                   eval      *inlr = *on
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Henter/Oppdatering av NUMMER-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hntnum        begsr
      *
     c                   eval      w_fell = laadel
     c                   eval      w_type = *blank
     c                   eval      w_fast = *blank
      *
      *  Kaller opp subprogram for henting av nummer fra NUMMER-REGISTER
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
     c                   parm                    w_numm
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Tar inn parameter fra forrige program
      *
     c     *entry        plist
     c                   parm                    w_parm
      *
     c                   eval      d_prec = w_parm
      *
      * Key for les av leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for les av status
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for les av slettet
      *
     c     ldellu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ldellu_aarr
     c                   kfld                    ldellu_numm
     c                   kfld                    ldellu_suff
      *
      * Key for oppdatering av slettet
      *
     c     ldell1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ldell1_aarr
     c                   kfld                    ldell1_numm
     c                   kfld                    ldell1_suff
      *
     c                   eval      a1fnav = l_fnav
      * Legge inn key
      *
     c                   eval      w_firm = l_firm
     c                   eval      a1firm = l_firm
      *
      * Hent opplysninger fra LAGER-STATUS-KODER
      *
     c     lstsl1_key    chain     lstsl1r
      *
     c                   endsr
