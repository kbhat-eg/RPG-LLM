     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JR840R
      * Beskrivelse . . : Leser std. csv format Frakttabell fra  fra byggtjenesten
      *
      *                   Innlesing til frakttabellen
      *                   Match på nobb-nr
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       281016 bof  Nytt program
8.01  *PRG2   140622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjrvapf    if   e           k disk
      *
     fjrapl1    if   e           k disk    rename(jrappfr:jrapl1r)
     fjvlel1    if   e           k disk    rename(jvlepfr:jvlel1r)
     fjvarl3    if   e           k disk    rename(jvarpfr:jvarl3r)
     fjfbel5    uf   e           k disk    rename(jfbepfr:jfbel5r)
     fjfbelu    uf a e           k disk    rename(jfbepfr:jfbelur)
     fjwrkpf    uf a e           k disk    rename(jwrkpfr:jwrkpfr)
      *
      * Definering av parametre
      *
     d p_list          s             32
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
      *
      * Definering av parameterliste -inn
      *
     d                 ds
     d d_list                        32
     d  d_firm                        3  0 overlay(d_list)
     d  d_rapp                        2  0 overlay(d_list:4)
     d  d_dato                         d   overlay(d_list:6) datfmt(*iso)
8.01 d  d_prgr                        2    overlay(d_list:30)
      *
      * Definering av recordbeskrivelse - Ahlsell
      *
     d                 ds
     d d_inpu                       140
     d  d_nobb                        8    overlay(d_inpu:1)
     d  d_nobbn                       8  0 overlay(d_nobb:1)
     d  d_frak                       12    overlay(d_inpu:93)
     d   d_frak_kr                    5  0 overlay(d_frak:1)
     d   d_frak_Øre                   6  6 overlay(d_frak:7)
     d  d_gtin                       14    overlay(d_inpu:117)
     d  d_gtinn                      14  0 overlay(d_gtin:1)
     d  d_ldor                        6    overlay(d_inpu:130)
     d  d_ldorn                       6  0 overlay(d_ldor:1)
      *
     d  d_ex           s            512
     d ew92            c                   '       ,  -'
      *
      * Definering av variable i nøkler
      *
     d jrapl1_rapp     s                   like(jrrapp)
     d jvlel1_lvnr     s                   like(jvlvnr)
     d jvlel1_lldo     s                   like(jvlldo)
     d jvarl3_nobb     s                   like(jvnobb)
     d jfbel5_ldor     s                   like(jpfldo)
     d jfbel5_vare     s                   like(jpfvar)
      *
      * Definering av variable
      *
     d b_inlr          s              1
     d b_ok            s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s              3  0
     d w_udat          s                   like(jvleda)
     d w_netp          s              9  2
     d w_leng          s              2  0
     d w_posi          s              2  0
     d w_navn          s             12
     d w_6x            s              6
     d w_14x           s             14
     d len             s              3  0
     d st              s              3  0
      *
      * Definering av konstanter
      *
     d c_digi          s             15    inz('0123456789, ')
     d c_null          s              7    inz('0000000')
      *
     d Lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d Up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter rapportør-informasjon
      *
     c                   eval      jrapl1_rapp = d_rapp
     c     jrapl1_key    chain     jrapl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
      * Leser fil.
      *
     c                   read      jrvapf
     c                   dow       not %eof
     c                   exsr      behandling
     c                   read      jrvapf
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av varer/priser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
     c                   eval      d_inpu = *blank
     c                   exsr      utled_ds
      *
     c                   if        d_nobb = *blank or b_ok = *off
     c                   leavesr
     c                   endif
      *
      * Starter oppdatering
      *
      * finner nobb-nr og vare-leverandør
     c                   eval      jvarl3_nobb = d_nobbn
     c     jvarl3_key    chain     jvarl3
     c                   if        %found(jvarl3)
      *
     c                   eval      jvlel1_lvnr = jvvare
     c                   eval      jvlel1_lldo = d_ldorn
     c     jvlel1_key    chain     jvlel1
     c                   if        %found and
     c                             (jvluda = *loval or
     c                             jvluda > w_udat)
     c                   exsr      oppd_frakt
     c                   endif
     c                   endif
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting av betingelse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_frakt    begsr
      *
      * Sletter betinglese på vare-leveradnør
      *
     c                   eval      jfbel5_ldor = d_ldorn
     c                   eval      jfbel5_vare = jvlvnr
     c     jfbel5_key    setll     jfbel5r
     c     jfbel5_key    reade     jfbel5r
     c                   dow       not %eof
     c                   if        jpfprg = d_prgr
     c                   delete    jfbel5r
     c                   endif
     c     jfbel5_key    reade     jfbel5r
     c                   enddo
      *
      * Oppretter betingelse
      *
     c                   clear                   jfbelur
      *
     c                   eval      jpffir = w_firm
     c                   eval      jpfldo = d_ldorn
     c                   eval      jpfprg = d_prgr
     c                   eval      jpfogr = jvogrp
     c                   eval      jpfhgr = jvhgrp
     c                   eval      jpfugr = jvugrp
     c                   eval      jpfmod = jvmodn
     c                   eval      jpfvar = jvvare
     c                   eval      jpfvty = *blank
     c                   eval      jpflet = 0
     c                   eval(h)   jpfkrt = d_frak_kr + d_frak_Øre

      *
     c                   time                    jpfoda
     c                   time                    jpfeda
     c                   time                    jpfeti

     c                   eval      jpfeus  = l_user
     c                   write     jfbelur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utleding av datastruktur
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     utled_ds      begsr
      *
     c     '"':' '       xlate     jrvrad        jrvrad
     c                   eval      b_ok = *off
      *
      *gtin
      *
     c                   eval      w_posi = %scan(';':jrvrad)
     c                   eval      d_gtin = %trim(%subst(jrvrad:1:w_posi-1))
      * kun blanke hoppes over
     c                   if        d_gtin = *blank
     c                   leavesr
     c                   endif
     c                   eval      len     = %len(%trim(d_gtin))
     c                   if        len     > 0
     c                   eval      st = 15 - len
     c                   eval      w_14x  = *blank
     c                   eval      %subst(w_14x:st:len) = %subst(d_gtin:1:len)
     c                   eval      d_gtin = w_14x
     c                   endif
     c     ' ':'0'       xlate     d_gtin        d_gtin
      *
      * ubrukt  B og C
      *
     c                   eval      jrvrad = %subst(jrvrad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':jrvrad)
      *
     c                   eval      jrvrad = %subst(jrvrad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':jrvrad)
      *

      *
      * VA levn.nr
      *
     c                   eval      jrvrad = %subst(jrvrad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':jrvrad)
     c                   eval      d_ldor = %trim(%subst(jrvrad:1:w_posi-1))
     c                   if        d_ldor <> *blank
     c                   eval      len     = %len(%trim(d_ldor))
     c                   if        len     > 0
     c                   eval      st = 7 - len
     c                   eval      w_6x  = *blank
     c                   eval      %subst(w_6x:st:len) = %subst(d_ldor:1:len)
     c                   eval      d_ldor = w_6x
     c                   endif
     c                   endif
     c     ' ':'0'       xlate     d_ldor        d_ldor
      *
      * enhet - brukes ikke
      *
     c                   eval      jrvrad = %subst(jrvrad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':jrvrad)
      *
      * nobb.nr
      *
     c                   eval      jrvrad = %subst(jrvrad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':jrvrad)
     c                   eval      d_nobb = %trim(%subst(jrvrad:1:w_posi-1))
     c                   if        %check(c_digi : d_nobb) > *zero
     c                   leavesr
     c                   endif
      *
      *  enhet (ikke i bruk)
      *
     c                   eval      jrvrad = %subst(jrvrad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':jrvrad)
      *
      *  nettopris
      *
     c                   eval      jrvrad = %subst(jrvrad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':jrvrad)
     c                   eval      d_frak = %trim(%subst(jrvrad:1:w_posi-1))
     c                   if        %check(c_digi : d_frak) > *zero
     c                   leavesr
     c                   endif
      *
      * Utleder  nettopris
      *
     c                   if        d_frak <> *blank
      *
     c                   eval      d_frak = %trim(d_frak)
     c                   eval      w_posi = %scan(',':d_frak)
     c                   eval      w_leng = %len(%trim(d_frak))
      *
     c                   if        w_posi = 0
     c                   eval      w_posi = w_leng + 1
     c                   eval      d_frak = %trim(d_frak) + '.00'
     c                   endif
      *
     c     ' ':'0'       xlate     d_frak        d_frak
      *
     c                   if        (w_posi-1) < 5
     c                   eval      d_frak = %subst(c_null:1:5-w_posi+1) +
     c                                      %subst(d_frak:1:w_posi-1) +
     c                                      '.' +
     c                                      %subst(d_frak:w_posi+1:6)
     c                   endif
      *
     c                   else
     c                   eval      d_frak = '00000.000000'
     c                   leavesr
     c                   endif
      *
     c                   eval      b_ok = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_list
      *
      * Key for oppslag på rapportør
      *
     c     jrapl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jrapl1_rapp
      *
      * Key for oppslag på vare m/lev.varenummer
      *
     c     jvlel1_key    klist
     c                   kfld                    jvlel1_lvnr
     c                   kfld                    jvlel1_lldo
      *
      * Key for oppslag på vare
      *
     c     jvarl3_key    klist
     c                   kfld                    jvarl3_nobb
      *
      * Key for oppdatering av betingelse
      *
     c     jfbel5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jfbel5_ldor
     c                   kfld                    jfbel5_vare
      *
      * Key for oppdatering av kode/status
      *
     c     jstslu_key    klist
     c                   kfld                    w_firm
      *
      *
      * Fyller datastruktur fra parameter, og henter firma
      *
     c                   eval      d_list = p_list
     c                   eval      w_firm = d_firm
      *
     c                   endsr
