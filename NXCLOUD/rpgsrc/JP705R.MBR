     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JP705R
      * Beskrivelse . . : Innlesing av NOBB-kontrakter, parameterbilde
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       310717 ARENG  Nytt program
8.01  *       240622 bjmar  Endret mappe til /asp/xxx/nobb
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjp705d    cf   e             workstn
      *
      *
     d w_fldr          s            255
     d w_stat          s              1
     d w_mld1          s             50
     d w_ldorx         s              6
     d w_firm          s              3  0
     d w_ldor          s              6  0
     d w_ldora         s          12000
      * Definering av Local data area *LDA
      *
     d                uds
     d l_user                911    920
6.25 d l_filg                931    933
     d l_firm                944    946  0

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Behandler bildet
      *
     c     b1taga        tag
      *
     c                   exfmt     b1bld
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   goto      avslutt
     c                   endif
      *
      *
      * Kaller program for henting av data, avbryter dersom feil
      *
     c                   if        b1fldr = *blank
     c                   eval      *in31 = *on
     c                   goto      b1taga
     c                   endif
      *
     c                   eval      w_fldr = *Blank
     c                   eval      w_fldr = '/asp/' + %trim(l_filg) +
8.01 c                                      '/nobb/' + b1fldr
      *
     c                   eval      w_stat = *blank
     c                   eval      w_ldora = *blank
     c                   call      'JP742C'
     c                   parm                    w_fldr
     c                   parm                    w_stat
     c                   parm                    w_ldora
      *
      * hvis status forskjellig fra blank, så gi melding
      *
     c                   if        w_stat <> *blank
     c                   eval      w_mld1 = *blank
     c                   select
     c                   when      w_stat = 'N'
     c                   eval      w_mld1 = 'XML ikke funnet i mappen'
     c                   when      w_stat = 'F'
     c                   eval      w_mld1 = 'Feil, XML ikke lest inn'
     c                   when      w_stat = 'C'
     c                   eval      w_mld1 = 'Feil karakter i filen'
     c                   other
     c                   eval      w_mld1 = 'Udefinert feil'
     c                   endsl
     c                   call      'AA005R'
     c                   parm                    w_mld1
     c                   goto      avslutt
     c                   endif
      * leser inn jbeipf / jraipf inn i  jbetpf / jraepf
     c                   eval      w_ldorx = %subst(w_ldora:1:6)
     c                   move      w_ldorx       w_ldor
     c                   call      'JP703R'
     c                   parm                    w_firm
     c                   parm                    w_ldor
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *
      * Initierer skjermbildet
      *
     c                   eval      b1fldr = 'EKSPORT.XML'
     c                   eval      w_firm = l_firm
     c                   eval      w_ldor = 0
      *
     c                   endsr
