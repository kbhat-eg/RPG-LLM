     h option(*nodebugio : *srcstmt) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JK110R
      * Beskrivelse . . : Logistikkvare linje, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       011018 BKV  Nytt program
7.01  *       160819 BKV  Rettet feil i pageing
7.02  *       200520 bof  Feilretting ved spørring
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjvkll1    if   e           k disk    rename(jvklpfr:jvkll1r)
     fjvkllr    if   e           k disk    rename(jvklpfr:jvkllrr)
     fjvkllu    uf a e           k disk    rename(jvklpfr:jvkllur)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fjvkhl1    if   e           k disk    rename(jvkhpfr:jvkhl1r)
     fjvlelr    if   e           k disk    rename(jvlepfr:jvlelrr)
      *
     fjk110d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_logv          s                   like(jvqvnr)                         Logistikkvare
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d jvkll1_quno     s                   like(jvquno)
     d jvkllr_quno     s                   like(jvquno)
     d jvkllr_qldo     s                   like(jvqldo)
     d jvkllu_quno     s                   like(jvquno)
     d jvkllu_qldo     s                   like(jvqldo)
     d jvarl1_vare     s                   like(jvvare)
     d jlevl1_ldor     s                   like(jlldor)
     d jvlelr_lvnr     s                   like(jvlvnr)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
      *
     d w_firm          s                   like(jvqfir)
     d w_vare          s                   like(jvqvnr)
     d w_varenobb      s                   like(jvvare)
     d w_logv          s                   like(jvqvnr)                         Logistikkvare
     d w_nobb          s                   like(jvquno)
     d w_nvar          s                   like(jvvare)
     d w_ldor          s                   like(jvqldo)
     d w_hlev          s              2  0
     d w_vdpl          s              3  0                                      vare duplikat
     d w_ldpl          s              3  0                                      lev deuplikat
     d w_evrv          s              2  0                                      finnes i evr
     d b_akti          s              1    inz(*off)
     d w_udat          s                   like(jvluda)
      *
     d sqlquery        s           4096
7.01 d b_open_sok      s              1    inz(*off)
     d w_vtek1         s                   like(b1tek1)
     d w_lnavn         s                   like(b1lnav)
     d w_teller        s              3  0
     d
      *
      *  Tabell DS
      *
     d* ds_jvpkpf     E ds                  extName(jvpkpf)
     d ds_jvprpf     E ds                  extName(jvprpf)
     d ds_jvlepf     E ds                  extName(jvlepf)
      *
      * Definering av konstanter
      *
7.01 d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * C3BLD  - C3 Skjermbilde for å vise melding/tekstlinje
      * D1WIN  - D1 Vindu for slette (Delete)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     C/Exec SQL
     C+  Set Option DatFmt=*ISO , commit=*none
     C/End-Exec
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   eval      b2qvnr = w_logv
     c                   eval      jvarl1_vare = w_logv
     c     jvarl1_key    chain     jvarl1
     c                   if        %found
     c                   eval      b2logt = jvtek1
     c                   eval      c2penh = jvpenh
     c                   eval      b2ogrp = jvogrp
     c                   eval      b2hgrp = jvhgrp
     c                   eval      b2ugrp = jvugrp
     c                   eval      c2ogrp = jvogrp
     c                   eval      c2hgrp = jvhgrp
     c                   eval      c2ugrp = jvugrp
     c                   endif
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   eval      c2quno = 0
     c                   exsr      xc2bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c*                   when      *in11
     c*                   exsr      bck_subfile
     c*                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2nobb <> *zero
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'V'
     c                   eval      jvkll1_quno = b1quno
     c     jvkll1_key    setll     jvkll1
     c                   other
     c                   eval      jvkll1_quno = b1quno
     c     jvkll1_key    setll     jvkll1
     c                   endsl
      *
7.01 c                   eval      b_open_sok = *on
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
     c                   eval      w_seqe = *blank
      *
      * Varenummer
      *
     c                   if        b2nobb <> *zero
     c                   eval      w_seqe = 'V'
     c                   eval      jvkll1_quno = b2nobb
     c     jvkll1_key    setll     jvkll1
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
7.01 c                   eval      b_open_sok = *on
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2nobb = *zero
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c2quno = b1quno
     c                   eval      c2tek1 = b1tek1
     c                   eval      c2qldo = b1qldo
     c                   eval      c2qlna = b1lnav
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1quno = b1quno
     c                   eval      d1tek1 = b1tek1
     c                   eval      d1qldo = b1qldo
     c                   eval      d1lnav = b1lnav
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c2quno = b1quno
7.02 c                   eval      c2qldo = b1qldo
     c                   eval      c2tek1 = b1tek1
     c                   eval      c2qlna = b1lnav
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis varekort
      *
     c                   if        b1valg = 8
     c                   movel     b1quno        w_vare
     c                   call      'JV510R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      c2qvnr = b2qvnr
     c                   eval      c2logt = b2logt
     c                   eval      *in43 = *on
      *
     c                   eval      w_hlev = 0
      *
     c                   eval      jvkllr_quno = c2quno
     c                   eval      jvkllr_qldo = c2qldo
     c     jvkllr_key    chain     jvkllr
     c                   if        %found
      *
      * Initierer bildet
      *
     c                   eval      *in40 = *on
     c                   eval      c2quno = jvquno
     c                   eval      c2qldo = jvqldo
     c                   eval      c2qlna = b1lnav
     c                   eval      c2qofa = jvqofa
     c                   eval      c2eusr = jvqeus
     c                   eval      c2upeh = b1penh
     c                   exsr      oppdatkt
      *
     c     *dmy          move      jvqeda        c2edat
      *
     c/exec sql
     c+ select COUNT(*) into :w_hlev from jvkhpf
     c+ where jvkfir = :w_firm and
     c+   jvkvnr = :jvqvnr and jvkldo = :jvqldo
     c/end-exec
     c                   else
      *
      * Initierer bildet
      *
     c                   eval      *in40 = *off
     c                   eval      c2quno = 0
     c                   eval      c2tek1 = ''
     c                   eval      c2qldo = 0
     c                   eval      c2qlna = ''
     c                   eval      c2qofa = 1
     c                   eval      c2upeh = ''
     c                   eval      c2uogr = 0
     c                   eval      c2uhgr = 0
     c                   eval      c2uugr = 0
     c                   eval      c2eusr = l_user
     c                   eval      c2edat = 0
     c                   endif
      *
     c                   if        w_hlev > 0
     c                   eval      *in30 = *on
     c                   eval      *in40 = *on
     c                   eval      *in32 = *on
     c                   else
     c                   eval      *in32 = *off
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   eval      *in40 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * F4=Spørring
      *
     c                   if        *inkd
      *
     c                   if        w_afld = 'C2QUNO'
     c                   movel     c2quno        w_nvar
     c                   call      'JV500R'
     c                   parm                    w_firm
     c                   parm                    w_nvar
     c                   parm                    c2tek1
     c                   movel     w_nvar        c2quno
     c                   exsr      oppdatkt
     c                   goto      c2taga
     c                   endif
      *
     c                   if        w_afld = 'C2QLDO'
     c                   movel     c2quno        w_nvar
     c                   call      'JW507R'
     c                   parm                    w_firm
     c                   parm                    w_nvar
     c                   parm                    c2qldo
     c                   exsr      oppdatkt
     c                   goto      c2taga
     c                   endif
      *
     c                   goto      c2taga
      *
     c                   endif
      *
     c                   exsr      oppdatkt
     c                   if        *in29 = *on
     c                   eval      *in29 = *off
     c                   goto      c2taga
     c                   endif
     c
      *
      * Validering av input
      *
      *sjekk om varen finnes på annen logsitkkvare
     c/exec sql
     c+ select COUNT(*) into :w_vdpl from jvklpf
     c+ where jvqfir = :w_firm and
     c+   jvquno = :c2quno and jvqvnr <> :c2qvnr
     c/end-exec
      * sjekk om samme leverandør finnes på logistikkvare fra før
     c/exec sql
     c+ select COUNT(*) into :w_ldpl from jvklpf
     c+ where jvqfir = :w_firm and
     c+   jvqldo = :c2qldo and jvqvnr = :c2qvnr
     c+   and  jvquno <> :c2quno
     c/end-exec
      * sjekk at undervarer ikke finnes i EVR (ok hvis utgått)
      * aktiv hvis utgått er 0 eller beholding > 0
     c/exec sql
     c+ select COUNT(*) into :w_evrv from vvarpf
     c+ where vvfirm = :w_firm and
     c+   vvvare = :c2quno  and ( vvudat = '0001-01-01'
     c+     or (SELECT sum(VLBEHL) FROM vlagpf where vlvare = :c2quno) > 0
     c+   )
     c/end-exec
      * hvis enheter er <> så kan ikke omregnfaktor være 1
     c                   if        c2upeh <> c2penh and c2qofa = 1
     c                   eval      *in41 = *on
     c                   goto      c2taga
     c                   endif
     c                   if        c2quno = *zero
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
     c                   if        b_akti = *off
     c                   eval      *in35 = *on
     c                   goto      c2taga
     c                   endif
     c                   if        w_vdpl > 0
     c                   eval      *in36 = *on
     c                   goto      c2taga
     c                   endif
     c                   if        w_ldpl > 0
     c                   eval      *in37 = *on
     c                   goto      c2taga
     c                   endif
     c                   if        w_evrv > 0
     c                   eval      *in38 = *on
     c                   goto      c2taga
     c                   endif
     c                   if        c2uogr <> c2ogrp or
     c                             c2uhgr <> c2hgrp or
     c                             c2uugr <> c2ugrp
     c                   eval      *in42 = *on
     c                   goto      c2taga
     c                   endif
     c                   if        c2qofa = 0
     c                   eval      *in39 = *on
     c                   goto      c2taga
     c                   endif
      *
      *
      * Oppdatering av ekstern ordre-linje
      *
     c                   clear                   jvkllur
      *
     c                   eval      jvkllu_quno = c2quno
     c                   eval      jvkllu_qldo = c2qldo
     c     jvkllu_key    chain     jvkllur
      *
     c                   eval      jvqvnr = w_logv
     c                   eval      jvquno = c2quno
     c                   eval      jvqldo = c2qldo
     c                   eval      jvqofa = c2qofa
      *
     c                   time                    jvqeda
     c                   time                    jvqeti
     c                   eval      jvqeus = l_user
      *
     c                   if        %found(jvkllu)
     c                   update    jvkllur
     c                   exsr      nobbkopi
     c                   else
     c                   time                    jvqoda
     c                   time                    jvqoti
     c                   eval      jvqous = l_user
     c                   eval      jvqfir = w_firm
     c                   write     jvkllur
     c                   exsr      nobbkopi
     c                   exsr      forny
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1quno = jvquno
     c                   movel     c2tek1        b1tek1
     c                   eval      b1qldo = jvqldo
     c                   movel     c2qlna        b1lnav
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30 = *off
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å oppdatere tekster
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdatkt      begsr
     c
      * Henter varetekst-1
     c                   if        c2quno <> *zero
     c
     c                   movel     c2quno        w_vare
     c
     c/exec sql
     c+ select jvtek1, jvpenh, jvogrp, jvhgrp, jvugrp
     c+   , IFNULL(jlldor, 0)
     c+   into :c2tek1, :c2upeh, :c2uogr, :c2uhgr, :c2uugr, :w_ldor
     c+   from jvarpf
     c+ left outer join jlevpf on  jvldor = jlldor
     c+ where jvvare = :w_vare
     c/end-exec
     c                   if        c2qldo = 0
     c                   eval      c2qldo = w_ldor
     c                   eval      *in29 = *on
     c                   endif
     c/exec sql
     c+ select IFNULL(jlnavn, '''')
     c+   into  :c2qlna
     c+   from jlevpf
     c+ where jlldor = :c2qldo
     c/end-exec
      *
     c/exec sql
     c+ select count(*)
     c+   into :w_teller
     c+   from jvlepf
     c+ where jvlvnr = :w_vare
     c/end-exec
     c                   if        (w_teller > 1)
     c                   eval      *in43 = *off
     c                   else
     c                   eval      *in43 = *on
     c                   endif
     c
     c*                   eval      c2tek1      = *blank
     c*                   movel     c2quno        jvarl1_vare
     c*     jvarl1_key    chain     jvarl1
     c*                   if        %found
     c*                   eval      c2tek1 = jvtek1
     c*                   endif
      *
      * sjekk om vare er utgått hos alle prisgivere /vareeiere
      *
     c                   eval      b_akti = *off
     c                   movel     c2quno        jvlelr_lvnr
     c     jvlelr_key    setll     jvlelr
     c     jvlelr_key    reade     jvlelr
     c                   dow       not %eof(jvlelr)
      * hvis minst 1 ikke utgått-> aktiv
     c                   if        jvluda = *loval
     c                   eval      b_akti = *on
     c                   eval      *in35 = *off
     c                   endif
      * hvis utgått dato, men ikke passert i tid--> aktiv
     c                   if        jvluda <> *loval and
     c                             jvluda > w_udat
     c                   eval      b_akti = *on
     c                   eval      *in35 = *off
     c                   endif
     c     jvlelr_key    reade     jvlelr
     c                   enddo
     c                   if        b_akti = *off
     c                   eval      c2tek1 = %subst(c2tek1:1:21) +
     c                                      ' ** UTGÅTT  **'
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for melding/tekstlinje (C3BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      jvkllr_quno = d1quno
     c                   eval      jvkllr_qldo = d1qldo
     c     jvkllr_key    chain     jvkllr
      *
     c     d1taga        tag
      *
     c                   if        b1hlev = 1
     c                   eval      *in32 = *on
     c                   else
     c                   eval      *in32 = *off
     c                   endif
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
      * Validering av input
      *
     c                   if        b1hlev = 1
     c                   goto      d1taga
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sett vare - leverandør utgått
      *
     c/exec sql
     c+  UPDATE JVLEPF SET JVLUDA = CURRENT_DATE,
     c+   JVLEDA=CURRENT_DATE,JVLETI=CURRENT_TIME,
     c+   JVLEUS=:l_user
     c+   WHERE (JVLVNR=:jvqvnr AND
     c+     JVLLDO = :jvqldo AND JVLUDA = '0001-01-01')
     c/end-exec
     c/exec sql
     c+  UPDATE JVPRPF SET JXTDAT = CURRENT_DATE,
     c+   JXEDAT=CURRENT_DATE,JXETIM=CURRENT_TIME,
     c+   JXEUSR=:l_user
     c+   WHERE (JXVARE = :jvqvnr AND
     c+     JXLDOR = :jvqldo AND JXTDAT = '0001-01-01')
     c/end-exec
     c/exec sql
     c+  UPDATE JVPKPF SET JWTDAT = CURRENT_DATE,
     c+   JWEDAT=CURRENT_DATE, JWETIM=CURRENT_TIME, JWEUSR=:l_user
     c+   WHERE (JWVARE = :jvqvnr AND JWTDAT = '0001-01-01'
     c+     AND JWLDOR = :jvqldo)
     c/end-exec
      *
      * Sletter undervare
      *
     c                   eval      jvkllu_quno = d1quno
     c                   eval      jvkllu_qldo = d1qldo
     c     jvkllu_key    chain     jvkllur
     c                   delete    jvkllur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
7.01 c                   if        b_open_sok = *on
     c/exec sql
     c+ close STMT
     c/end-exec
      *
     c                   eval      sqlquery  = 'select +
     c                              A.jvqfir +
     c                             ,A.jvquno +
     c                             ,IFNULL(B.jvtek1, '''') +
     c                             ,A.jvqldo +
     c                             ,IFNULL((select C1.jlnavn from +
     c                              jlevpf C1 where C1.jlldor = A.jvqldo) +
     c                                , '''') +
     c                             ,A.jvqofa +
     c                             ,(select count(*) from jvkhpf D1 where  +
     c                              D1.jvkfir = A.jvqfir and D1.jvkldo = +
     c                              A.jvqldo and D1.jvkvnr = A.jvqvnr +
     c                                 ) as hlev +
     c                             ,IFNULL(B.jvpenh, '''') +
     c                               from   jvklpf A  +
     c                             left outer join jvarpf B on +
     c                                 b.jvnobb = a.jvquno +
     c                             where A.jvqfir = ? and A.jvqvnr = ? +
     c                             order by 7 desc, 2 +
     c                               '
     c/exec sql
     c+ PREPARE SQL_STMT FROM :sqlquery
     c/end-exec
     c
     c/exec sql
     c+ DECLARE STMT CURSOR FOR SQL_STMT
     c/end-exec
     c
7.01 c                   if        sqlcod < 0
     c                   eval      *in15 = *on
     c                   leavesr
     c                   endif
     c
     c/exec sql
     c+ OPEN STMT using :w_firm, :w_logv
     c/end-exec
     c                   endif
     c
     c                   dou       w_srrn = w_spge
      *
     c/exec sql
     c+ fetch STMT
     c+ into  :jvqfir,:jvquno,:w_vtek1,:jvqldo,:w_lnavn,:jvqofa,:b1hlev,:b1penh
     c/end-exec
7.01 c                   if        sqlcod = 100 or sqlcod < 0
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   if        *in15 = *on or
     c                             jvqfir <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1quno = jvquno
     c                   movel     w_vtek1       b1tek1
     c                   eval      b1qldo = jvqldo
     c                   movel     w_lnavn       b1lnav
     c                   movel     jvqofa        b1qofa
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
7.01 c                   eval      b_open_sok = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c*     bck_subfile   begsr
      **
     c*                   if        *in15
     c*                   select
     c*                   when      w_seqe = 'V'
     c*     jvkll1_key    setgt     jvkll1
     c*                   readp     jvkll1
     c*                   other
     c*     jvkll1_key    setgt     jvkll1
     c*                   readp     jvkll1
     c*                   endsl
     c*                   endif
      **
      ** Leser tilbake en side
      **
     c*                   eval      w_stel = 0
      **
     c*                   dou       w_stel = w_ssrn + c_sfil
      **
     c*                   select
     c*                   when      w_seqe = 'V'
     c*                   readp     jvkll1
     c*                   other
     c*                   readp     jvkll1
     c*                   endsl
      **
     c*                   if        %eof or
     c*                             jvqfir <> w_firm
     c*                   leave
     c*                   endif
      **
     c*                   eval      w_stel = w_stel + 1
      **
     c*                   select
     c*                   when      w_seqe = 'V'
     c*                   eval      jvkll1_quno = jvquno
     c*                   other
     c*                   eval      jvkll1_quno = jvquno
     c*                   endsl
      **
     c*                   enddo
      **
     c*                   if        %eof
     c*                   select
     c*                   when      w_seqe = 'V'
     c*     jvkll1_key    setll     jvkll1
     c*                   other
     c*     jvkll1_key    setll     jvkll1
     c*                   endsl
     c*                   endif
      **
      ** Blanker og fyller opp subfile
      **
     c*                   exsr      clr_subfile
     c*                   exsr      crt_subfile
      **
     c*                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å nobb kopi av undervare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     nobbkopi      begsr
     c
      *
      *
     c                   movel     jvquno        w_varenobb
      /free

       // Kopier/oppdater vare-leverandør kobling
       exec sql DECLARE CLE CURSOR FOR
                 SELECT * FROM JVLEPF
                 WHERE JVLVNR = :w_varenobb AND JVLLDO = :jvqldo;
       exec sql OPEN CLE ;

       dow (2 = 2) ;
         exec sql FETCH NEXT FROM CLE INTO :ds_JVLEPF ;
         if (SQLCOD <> 0) ;
           leave ;
         endif ;

         exec sql
          select count(*) into :w_teller from JVLEPF
          WHERE (JVLVNR=:jvqvnr AND JVLLDO=:jvqldo);

         if (w_teller = 0);
           exec sql
             INSERT INTO JVLEPF
             (JVLVNR,JVLLDO,JVLVEI,JVLLVA,JVLMR1,JVLMR2,JVLMR3
             ,JVLUDA,JVLODA,JVLOTI,JVLOUS,JVLEDA,JVLETI,JVLEUS)
             SELECT
             :jvqvnr,:jvqldo,'0',JVLLVA,JVLMR1,JVLMR2,JVLMR3
             ,JVLUDA,JVLODA,JVLOTI,JVLOUS,CURRENT_DATE,CURRENT_TIME,:l_user
             FROM JVLEPF B WHERE
                (JVLVNR=:w_varenobb AND JVLLDO=:jvqldo);
         else;
           exec sql
             update JVLEPF set
               JVLVEI='0',
               JVLLVA=:JVLLVA,
               JVLMR1=:JVLMR1,
               JVLMR2=:JVLMR2,
               JVLMR3=:JVLMR3,
               JVLUDA=:JVLUDA,
               JVLODA=:JVLODA,
               JVLOTI=:JVLOTI,
               JVLOUS=:JVLOUS,
               JVLEDA=CURRENT_DATE,
               JVLETI=CURRENT_TIME,
               JVLEUS=:l_user
            WHERE
             (JVLVNR=:jvqvnr AND JVLLDO=:jvqldo );
         endif;
       enddo ;
       exec sql CLOSE CLE ;

       // Kopier pakninger fra NOBB vare til logistikkvare
       exec sql
         DELETE FROM JVPKPF
           WHERE JWVARE = :jvqvnr AND JWLDOR = :jvqldo and JWRSTS = 0;

       exec sql
         INSERT INTO JVPKPF
         (JWVARE,JWLDOR,JWPAKN,JWENHE,JWOFAK,JWOFAP
         ,JWBRED,JWLENG,JWHOYD,JWVEKT,JWVOLU,JWBEST
         ,JWPKLA,JWGTIN,JWPSEN,JWPAKS,JWANTP,JWPAPP
         ,JWPMSB,JWLFOR,JWNBRE,JWNLEN,JWNHOY,JWNVEK
         ,JWNVOL,JWGTYP,JWEPSE,JWRSTS,JWFDAT,JWTDAT
         ,JWNODA,JWNEDA,JWNSDA,JWODAT,JWEDAT,JWETIM
         ,JWEUSR)
         SELECT
         :jvqvnr,:jvqldo,JWPAKN,JWENHE,JWOFAK,JWOFAP
         ,JWBRED,JWLENG,JWHOYD,JWVEKT,JWVOLU,JWBEST
         ,JWPKLA,JWGTIN,JWPSEN,JWPAKS,JWANTP,JWPAPP
         ,JWPMSB,JWLFOR,JWNBRE,JWNLEN,JWNHOY,JWNVEK
         ,JWNVOL,JWGTYP,JWEPSE,JWRSTS,JWFDAT,JWTDAT
         ,JWNODA,JWNEDA,JWNSDA,JWODAT,CURRENT_DATE,CURRENT_TIME
         ,:l_user
         FROM JVPKPF B WHERE
            ( JWVARE = :w_varenobb AND JWLDOR = :jvqldo and JWRSTS = 0 );


       //exec sql DECLARE CPK CURSOR FOR
       //          SELECT * FROM JVPKPF
       //          WHERE JWVARE = :jvquno AND JWLDOR = :jvqldo;
       //exec sql OPEN CPK ;
       //
       //dow (2 = 2) ;
       //  exec sql FETCH NEXT FROM CPK INTO :ds_jvpkpf ;
       //  if (SQLCOD <> 0) ;
       //    leave ;
       //  endif ;
       //
       //  exec sql
       //   select count(*) into :w_teller from jvpkpf
       //   WHERE (JWVARE=:jvqvnr AND JWLDOR=:jvqldo AND JWPAKN=:JWPAKN);
       //
       //  if (w_teller = 0);
       //    exec sql
       //      INSERT INTO JVPKPF
       //      (JWVARE,JWLDOR,JWPAKN,JWENHE,JWOFAK,JWOFAP
       //      ,JWBRED,JWLENG,JWHOYD,JWVEKT,JWVOLU,JWBEST
       //      ,JWPKLA,JWGTIN,JWPSEN,JWPAKS,JWANTP,JWPAPP
       //      ,JWPMSB,JWLFOR,JWNBRE,JWNLEN,JWNHOY,JWNVEK
       //      ,JWNVOL,JWGTYP,JWEPSE,JWRSTS,JWFDAT,JWTDAT
       //      ,JWNODA,JWNEDA,JWNSDA,JWODAT,JWEDAT,JWETIM
       //      ,JWEUSR)
       //      SELECT
       //      :jvqvnr,:jvqldo,JWPAKN,JWENHE,JWOFAK,JWOFAP
       //      ,JWBRED,JWLENG,JWHOYD,JWVEKT,JWVOLU,JWBEST
       //      ,JWPKLA,JWGTIN,JWPSEN,JWPAKS,JWANTP,JWPAPP
       //      ,JWPMSB,JWLFOR,JWNBRE,JWNLEN,JWNHOY,JWNVEK
       //      ,JWNVOL,JWGTYP,JWEPSE,JWRSTS,JWFDAT,JWTDAT
       //      ,JWNODA,JWNEDA,JWNSDA,JWODAT,CURRENT_DATE,CURRENT_TIME
       //      ,:l_user
       //      FROM JVPKPF B WHERE
       //         ( JWVARE = :jvquno AND JWLDOR = :jvqldo AND JWPAKN=:JWPAKN);
       //  else;
       //    exec sql
       //      update jvpkpf set
       //        JWENHE=:JWENHE,
       //        JWOFAK=:JWOFAK,
       //        JWOFAP=:JWOFAP,
       //        JWBRED=:JWBRED,
       //        JWLENG=:JWLENG,
       //        JWHOYD=:JWHOYD,
       //        JWVEKT=:JWVEKT,
       //        JWVOLU=:JWVOLU,
       //        JWBEST=:JWBEST,
       //        JWPKLA=:JWPKLA,
       //        JWGTIN=:JWGTIN,
       //        JWPSEN=:JWPSEN,
       //        JWPAKS=:JWPAKS,
       //        JWANTP=:JWANTP,
       //        JWPAPP=:JWPAPP,
       //        JWPMSB=:JWPMSB,
       //        JWLFOR=:JWLFOR,
       //        JWNBRE=:JWNBRE,
       //        JWNLEN=:JWNLEN,
       //        JWNHOY=:JWNHOY,
       //        JWNVEK=:JWNVEK,
       //        JWNVOL=:JWNVOL,
       //        JWGTYP=:JWGTYP,
       //        JWEPSE=:JWEPSE,
       //        JWRSTS=:JWRSTS,
       //        JWFDAT=:JWFDAT,
       //        JWTDAT=:JWTDAT,
       //        JWNODA=:JWNODA,
       //        JWNEDA=:JWNEDA,
       //        JWNSDA=:JWNSDA,
       //        JWEDAT=CURRENT_DATE,
       //        JWETIM=CURRENT_TIME,
       //        JWEUSR=:l_user
       //     WHERE
       //      (JWVARE=:jvqvnr AND JWLDOR=:jvqldo AND JWPAKN=:JWPAKN);
       //  endif;
       //enddo ;
       //exec sql CLOSE CPK ;

       // KOPIERE PRISER
       exec sql DECLARE CPR CURSOR FOR
                 SELECT * FROM JVPRPF
                 WHERE JXVARE = :w_varenobb AND JXLDOR = :jvqldo;
       exec sql OPEN CPR ;

       dow (2 = 2) ;
         exec sql FETCH NEXT FROM CPR INTO :ds_jvprpf ;
         if (SQLCOD <> 0) ;
           leave ;
         endif ;
         exec sql
          select count(*) into :w_teller from jvprpf
          WHERE (JXVARE=:jvqvnr AND JXLDOR=:jvqldo
                 AND JXGDAT=:JXGDAT AND JXRSTS=:JXRSTS);

         if (w_teller = 0);
           exec sql
             INSERT INTO JVPRPF
             (JXVARE,JXLDOR,JXLVAR,JXPRIS,JXGDAT,JXTDAT,JXGPRI
             ,JXVALU,JXFBEL,JXCRDO,JXRSTS,JXEANN,JXNODA,JXNEDA
             ,JXNSDA,JXODAT,JXEDAT,JXETIM,JXEUSR)
             SELECT
             :jvqvnr,:jvqldo,JXLVAR,(JXPRIS*:jvqofa),JXGDAT,JXTDAT
             ,(JXGPRI*:jvqofa)
             ,JXVALU,JXFBEL,JXCRDO,JXRSTS,JXEANN,JXNODA,JXNEDA
             ,JXNSDA,JXODAT,CURRENT_DATE,CURRENT_TIME,:l_user
             FROM JVPRPF B WHERE
                (JXVARE=:w_varenobb AND JXLDOR=:jvqldo
                 AND JXGDAT=:JXGDAT AND JXRSTS=:JXRSTS);
         else;
           exec sql
             update jvprpf set
               JXLVAR=:JXLVAR,
               JXPRIS=(:JXPRIS*:jvqofa),
               JXTDAT=:JXTDAT,
               JXGPRI=(:JXGPRI*:jvqofa),
               JXVALU=:JXVALU,
               JXFBEL=:JXFBEL,
               JXCRDO=:JXCRDO,
               JXEANN=:JXEANN,
               JXNODA=:JXNODA,
               JXNEDA=:JXNEDA,
               JXNSDA=:JXNSDA,
               JXODAT=:JXODAT,
               JXEDAT=CURRENT_DATE,
               JXETIM=CURRENT_TIME,
               JXEUSR=:l_user
            WHERE
             (JXVARE=:jvqvnr AND JXLDOR=:jvqldo
               AND JXGDAT=:JXGDAT AND JXRSTS=:JXRSTS);
         endif;
       enddo ;
       exec sql CLOSE CPR ;
      /end-free
     c
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekk vareguppe forskjeller
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_vargrp  begsr
     c
     c                   eval      *in28 = *off
     c
     c*/exec sql
     c*+                  SELECT COUNT(*) into :b_vgrpsjekk FROM (
     c*+                   SELECT DISTINCT JVOGRP,JVHGRP,JVUGRP
     c*+                   FROM JVARPF WHERE JVVARE IN (
     c*+                    select JVQUNO from jvklpf
     c*+                    WHERE JVQVNR = :jvkvnr
     c*+                   )
     c*+                  ) A
     c*/end-exec
     c
     c*                   if        w_vgrpsjekk > 1
     c*                   eval      *in28 = *on
     c*                   endif
     c
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_logv
      *
      * Key for posisjonering på nobbnummer
      *
     c     jvkll1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_logv
     c                   kfld                    jvkll1_quno
      *
      * Key for posisjonering på nobbnummer
      *
     c     jvkllr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_logv
     c                   kfld                    jvkllr_quno
     c                   kfld                    jvkllr_qldo
      *
      * Key for oppdatering av logistikk undervare
      *
     c     jvkllu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_logv
     c                   kfld                    jvkllu_quno
     c                   kfld                    jvkllu_qldo
      *
      * Key for oppslag på vare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for les på vare-leverandør
      *
     c     jvlelr_key    klist
     c                   kfld                    jvlelr_lvnr
      *
      * Henter firma og referanse fra parameter
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_logv = p_logv
      *
      * Initierer skjermbildet
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      jvkllu_quno = 0
     c     jvkll1_key    setll     jvkll1
      *
6.36 c                   time                    w_udat
      *
7.01 c                   eval      b_open_sok = *on
     c                   exsr      crt_subfile
      *
     c                   endsr
