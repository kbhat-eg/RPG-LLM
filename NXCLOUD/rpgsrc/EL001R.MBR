     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : EL001R
      * Beskrivelse . . : Endringslogg, spørring
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsforerivelse
      * ----- ------ ---- -------------------------------------------------
      *       020616 sst  Nytt program
7.xx  * 7.00  131216 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fslogl1    if   e           k disk    rename(slogpfr:slogl1r)
     fslogl2    if   e           k disk    rename(slogpfr:slogl2r)
     fslogl3    if   e           k disk    rename(slogpfr:slogl3r)
     fsloglr    if   e           k disk    rename(slogpfr:sloglrr)
     fsloglu    uf a e           k disk    rename(slogpfr:sloglur)
     fra02l1    if   e           k disk    rename(ra02pfr:ra02l1r)
     fra03l1    if   e           k disk    rename(ra03pfr:ra03l1r)
     fra07l1    if   e           k disk    rename(ra07pfr:ra07l1r)
      *
     fel001d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
4.44 d lo              c                   'abcdefghijklmnopqrstuvwxyzæøå'
4.44 d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅ'
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d slogl1_regi     s                   like(elregi)
     d slogl1_fore     s                   like(elfore)
     d slogl1_fort     s                   like(elfort)
     d slogl1_felt     s                   like(elfelt)
     d slogl1_rdat     s                   like(elrdat)
     d slogl1_rtim     s                   like(elrtim)
     d slogl2_regi     s                   like(elregi)
     d slogl2_fore     s                   like(elfore)
     d slogl2_fort     s                   like(elfort)
     d slogl2_rdat     s                   like(elrdat)
     d slogl2_felt     s                   like(elfelt)
     d slogl2_rtim     s                   like(elrtim)
     d slogl3_regi     s                   like(elregi)
     d slogl3_fore     s                   like(elfore)
     d slogl3_fort     s                   like(elfort)
     d slogl3_user     s                   like(eluser)
     d slogl3_rdat     s                   like(elrdat)
     d slogl3_rtim     s                   like(elrtim)
     d slogl3_felt     s                   like(elfelt)
     d sloglr_regi     s                   like(elregi)
     d sloglr_fore     s                   like(elfore)
     d sloglr_fort     s                   like(elfort)
     d sloglr_felt     s                   like(elfelt)
     d sloglr_rdat     s                   like(elrdat)
     d sloglr_user     s                   like(eluser)
     d sloglr_rtim     s                   like(elrtim)
     d sloglu_regi     s                   like(elregi)
     d sloglu_fore     s                   like(elfore)
     d sloglu_fort     s                   like(elfort)
     d sloglu_felt     s                   like(elfelt)
     d sloglu_rdat     s                   like(elrdat)
     d sloglu_user     s                   like(eluser)
     d sloglu_rtim     s                   like(elrtim)
     d ra02l1_frm      s                   like(rabfir)
     d ra02l1_kod      s                   like(rabkod)
     d ra03l1_frm      s                   like(racfir)
     d ra03l1_kod      s                   like(rackod)
     d ra07l1_frm      s                   like(ragfir)
     d ra07l1_kod      s                   like(ragkod)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
     d b_oppr          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_subf          s              1    inz(*off)
      *
     d w_firm          s              3  0
     d w_besk          s             30
     d s_regi          s                   like(b2regi)
     d s_fore          s                   like(b2fore)
     d s_fort          s                   like(b2fore)
     d s_felt          s                   like(b2felt)
     d s_dato          s                   like(b2fdat)
     d s_user          s                   like(b2user)
      *
     d w_dato          s             10
     d w_datd          s                   like(eldato)
     d w_hv            s                   like(eldato)
     d w_datx          s             10
     d w_datn          s              6  0
     d p_tx30          s             30
     d p_rapx          s              4
     d w_fper          s              6  0
     d p_rapt          s             30
     d p_firm          s              3
     d w3avde          s              4
     d w3prol          s              5
6.22 d w_sumi          s              7  0
6.22 d w_sumix         s             11  0
6.22 d w_sumk          s              7  0
6.22 d w_sumkx         s             11  0
6.22 d w_buo           s              7  0
6.22 d w_but           s              7  0
6.22 d w_mld1          s             50
6.22 d w_stat          s              1
6.22 d wymd            s              6
6.22 d wymdn           s              6  0
      *
      * Definering av konstanter
      *
7.xx d c_sfil          s              2  0 inz(12)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd = *on and
     c                             w_afld = 'B2FREG'
     c                   call      'EL010R'
     c                   parm                    b2freg
     c                   parm                    w_besk
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkd = *on and
     c                             w_afld = 'B2REGI'
     c                   call      'EL010R'
     c                   parm                    b2regi
     c                   parm                    w_besk
     c                   eval      s_regi = b2regi
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkd = *on and
     c                             w_afld = 'B2FFEL'
     c                   call      'EL011R'
     c                   parm                    b2ffel
     c                   parm                    w_besk
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkd = *on and
     c                             w_afld = 'B2FELT'
     c                   call      'EL011R'
     c                   parm                    b2felt
     c                   parm                    w_besk
     c                   eval      s_felt = b2felt
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Utvalg   Register
      *
     c                   if        b2regi  <> s_regi
     c                   eval      s_regi   = b2regi
     c                   exsr      forny
     c                   goto      b2tagb
     c                   endif
      *
      * Utvalg   forekomst
      *
     c                   if        b2fore  <> s_fore
     c                   eval      s_fore   = b2fore
     c                   exsr      forny
     c                   goto      b2tagb
     c                   endif
      *
      * Utvalg   felt
      *
     c                   if        b2felt  <> s_felt
     c                   eval      s_felt   = b2felt
     c                   exsr      forny
     c                   goto      b2tagb
     c                   endif
      *
      * Utvalg   bruker
      *
     c                   if        b2user  <> s_user
     c                   eval      s_user   = b2user
     c                   exsr      forny
     c                   goto      b2tagb
     c                   endif
      *
      * Posisjonering
      *
     c                   if        b2freg <> *blank or
     c                             b2ffor <> *blank or
     c                             b2ffel <> *blank or
     c                             b2fdat <> *blank or
     c                             b2fusr <> *blank
     c                   exsr      posisjoner
     c                   eval      b2freg = *blank
     c                   eval      b2ffor = *blank
     c                   eval      b2ffel = *blank
     c                   eval      b2fdat = *blank
     c                   eval      b2fusr = *blank
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   if        b_subf = *on
     c                   goto      b2taga
     c                   endif
      *
      * Retur til første bilde
      *
     c                   goto      b2tagb
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   select
     c                   when      w_seqe = 'B'
     c     slogl2_key    setll     slogl2
     c                   when      w_seqe = 'C'
     c     slogl3_key    setll     slogl3
     c                   other
     c     slogl1_key    setll     slogl1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på register
      *
     c                   if        b2freg <> *blank
     c                   eval      w_seqe = ' '
     c                   eval      slogl1_regi = b2freg
     c                   eval      slogl1_fore = *blank
     c                   eval      slogl1_fort = *blank
     c                   eval      slogl1_felt = *blank
     c                   eval      slogl1_rdat = w_hv
     c     slogl1_key    setll     slogl1
     c                   endif
      *
      * Posisjoner startverdi på forekomst
      *
     c                   if        b2ffor <> *blank
     c                   eval      w_seqe = ' '
     c                   eval      slogl1_regi = b1regi
     c                   eval      slogl1_fore = b2ffor
     c                   eval      slogl1_fort = *blank
     c                   eval      slogl1_felt = *blank
     c                   eval      slogl1_rdat = w_hv
     c     slogl1_key    setll     slogl1
     c                   endif
      *
      * Posisjoner startverdi på felt
      *
     c                   if        b2ffel <> *blank
     c                   eval      w_seqe = ' '
     c********           eval      slogl1_regi = b2freg
     c********           eval      slogl1_fore = b2ffor
     c                   eval      slogl1_felt = b2ffel
     c                   eval      slogl1_rdat = w_hv
     c     slogl1_key    setll     slogl1
     c                   endif
      *
      * Posisjoner startverdi på dato
      *
     c                   if        b2fdat <> *blank
     c                   eval      w_seqe = 'B'
     c                   eval      slogl2_regi = slogl1_regi
     c                   eval      slogl2_fore = slogl1_fore
     c                   eval      slogl2_fort = *blank
      *  debug b2fdat
     c                   movel     b2fdat        wymdn
     c     *ymd          movel     wymdn         slogl2_rdat
     c                   eval      slogl2_rtim = slogl2_rtim
     c                   eval      slogl2_felt = *blank
     c     slogl2_key    setll     slogl2
     c                   endif
      *
      * Posisjoner startverdi på user
      *
     c                   if        b2fusr <> *blank
     c                   eval      w_seqe = 'C'
     c                   eval      slogl3_regi = slogl1_regi
     c                   eval      slogl3_fore = slogl1_fore
     c                   eval      slogl3_fort = *blank
     c                   eval      slogl3_user = b2user
     c                   eval      slogl3_felt = *blank
     c                   eval      slogl3_rdat = *hival
     c     slogl3_key    setll     slogl3
     c                   endif
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2freg = *blank
     c                   eval      b2ffor = *blank
     c                   eval      b2ffel = *blank
     c                   eval      b2fdat = *blank
     c                   eval      b2fusr = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
     c                   eval      b_subf = *off
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      b_subf = *on
     c                   eval      w_virn = w_srrn
      *
      * Vise post
      *   sstdebug
     c                   if        b1valg = '5'
     c                   eval      c1regi = b1regi
     c                   eval      c1fore = b1forx
     c                   eval      c1fort = b1fort
     c                   eval      c1felt = b1felx
     c                   eval      c1dato = b1dato
     c                   eval      c1rdat = b1dato
     c                   eval      c1user = b1user
     c                   move      b1rtim        c1rtim
     c                   exsr      xc2bld
     c                   eval      b1valg = *blank
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      sloglr_regi = c1regi
     c                   eval      sloglr_fore = c1fore
     c                   eval      sloglr_fort = c1fort
     c                   eval      sloglr_felt = c1felt
      *    debug c1rdat
     c                   eval      wymd = %subst(c1rdat:1:2) +
     c                                    %subst(c1rdat:4:2) +
     c                                    %subst(c1rdat:7:2)
     c                   move      wymd          wymdn
     c     *ymd          move      wymdn         sloglr_rdat
      *     debug   c1rdat
     c                   move      c1rtim        sloglr_rtim
     c     sloglr_key    chain     sloglr
      *
     c                   if        %found
     c                   eval      c2firm = elfirm
     c                   eval      c2regi = elregi
     c                   eval      c2fore = elfore
     c                   eval      c2fort = elfort
     c                   eval      c2felt = elfelt
     c                   eval      c2user = eluser
     c                   move      eldato        c2dato
     c                   eval      c2frav = elfrav
     c                   eval      c2tilv = eltilv
     c                   eval      c2rtim = elrtim
     c                   move      elrtim        c2time
      *
     c                   if        elfelt = 'AVDE'
     c                   eval      ra07l1_frm  = elfirm
     c                   movel     elfrav        ra07l1_kod
     c     ra07l1_key    chain     ra07l1
     c                   if        %found(ra07l1)
     c                   eval      c2frav = %trim(c2frav) + ' ' +
     c                                      %trim(ragtxt)
     c                   endif
     c                   movel     eltilv        ra07l1_kod
     c     ra07l1_key    chain     ra07l1
     c                   if        %found(ra07l1)
     c                   eval      c2tilv = %trim(c2tilv) + ' ' +
     c                                      %trim(ragtxt)
     c                   endif
     c                   endif
      *
     c                   if        elfelt = 'RKBETM'
     c                   movel     elfrav        ra02l1_kod
     c     ra02l1_key    chain     ra02l1
     c                   if        %found(ra02l1)
     c                   eval      c2frav = %trim(c2frav) + ' ' +
     c                                      %trim(rabtxt)
     c                   endif
     c                   movel     eltilv        ra02l1_kod
     c     ra02l1_key    chain     ra02l1
     c                   if        %found(ra02l1)
     c                   eval      c2tilv = %trim(c2tilv) + ' ' +
     c                                      %trim(rabtxt)
     c                   endif
     c                   endif
      *
     c                   endif
      *
     c                   if        b1valg = '5'
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
     c                   setoff                                       232425
     c                   setoff                                       262728
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
      *
      * Oppdatering
      *
     c***                clear                   sloglur
      *
      * Oppdater subfile
      *
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å redigere inntastet dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     reddato       begsr
      *
     c                   if        %subst(w_dato:5:1) = '-' and
     c                             %subst(w_dato:8:1) = '-'
     c                   goto      redend
     c                   endif
      *
     c                   movel     udate         w_datx
     c                   if        %subst(w_dato:2:9) = *blank
     c                   eval      %subst(w_datx:01:1) = '0'
     c                   eval      %subst(w_datx:02:1) = %subst(w_dato:1:1)
     c                   elseif    %subst(w_dato:3:8) = *blank
     c                   eval      %subst(w_datx:01:2) = %subst(w_dato:1:2)
     c                   elseif    %subst(w_dato:5:6) = *blank
     c                   eval      %subst(w_datx:01:2) = %subst(w_dato:1:2)
     c                   eval      %subst(w_datx:03:2) = %subst(w_dato:3:2)
     c                   elseif    %subst(w_dato:7:4) = *blank
     c                   eval      %subst(w_datx:01:2) = %subst(w_dato:1:2)
     c                   eval      %subst(w_datx:03:2) = %subst(w_dato:3:2)
     c                   eval      %subst(w_datx:05:2) = %subst(w_dato:5:2)
     c                   endif
     c                   movel     w_datx        w_datn
     c     redend        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      slogl2                                 15
     c                   when      w_seqe = 'C'
     c                   read      slogl3                                 15
     c                   other
     c                   read      slogl1                                 15
     c                   endsl
      *
5.61 c                   if        *in15  or
5.61 c                             elfirm <> w_firm
     c                   leave
     c                   endif
      *
      *        Valgt register
5.61 c                   if        b2regi <> *blank  and
     c                             elregi <> b2regi
     c                   iter
     c                   endif
      *        Valgt forekomst
5.61 c                   if        b2fore <> *blank and
     c                             elfore <> b2fore
     c                   iter
     c                   endif
      *        Valgt felt
5.61 c                   if        b2felt <> *blank and
     c                             elfelt <> b2felt
     c                   iter
     c                   endif
      *        Valgt bruker
5.61 c                   if        b2user <> *blank and
     c                             eluser <> b2user
     c                   iter
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   if        slogl1_regi = *blank
     c                   eval      slogl1_regi = elregi
     c                   endif
     c                   if        slogl1_fore = *blank
     c                   eval      slogl1_fore = elfore
     c                   eval      slogl1_fort = *blank
     c                   endif
      *  Ved start på felt/bruker skal dette skje på current register
     c                   if        w_srrn = 1
     c                   eval      slogl2_regi = elregi
     c                   eval      slogl3_regi = elregi
     c                   endif
     c                   eval      b1valg = *blank
     c                   eval      b1regi = elregi
     c                   eval      b1fore = elfore
     c                   eval      b1forx = elfore
     c                   eval      b1fort = elfort
     c                   eval      b1felt = elfelt
     c                   eval      b1felx = elfelt
     c                   eval      b1user = eluser
     c     *ymd          move      eldato        b1dato
     c                   eval      b1frti = elfrav
6.22 c                   eval      %subst(b1frti:19:17) = eltilv
      *                  -----------------------------------------
     c                   if        b1felt = 'RKBETB'
     c                   eval      ra03l1_frm = elfirm
     c                   movel     elfrav        ra03l1_kod
     c     ra03l1_key    chain     ra03l1
     c                   if        %found(ra03l1)
     c                   eval      %subst(b1frti:4:14) =
     c                             %subst(ractxt:1:14)
     c                   endif
     c                   eval      ra03l1_frm = elfirm
     c                   movel     eltilv        ra03l1_kod
     c     ra03l1_key    chain     ra03l1
     c                   if        %found(ra03l1)
     c                   eval      %subst(b1frti:22:14) =
     c                             %subst(ractxt:1:14)
     c                   endif
     c                   endif
      *                  -----------------------------------------
     c                   if        b1felt = 'RKBETM'
     c                   movel     elfrav        ra02l1_kod
     c     ra02l1_key    chain     ra02l1
     c                   if        %found(ra02l1)
     c                   eval      %subst(b1frti:4:14) =
     c                             %subst(rabtxt:1:14)
     c                   endif
     c                   movel     eltilv        ra02l1_kod
     c     ra02l1_key    chain     ra02l1
     c                   if        %found(ra02l1)
     c                   eval      %subst(b1frti:22:14) =
     c                             %subst(rabtxt:1:14)
     c                   endif
     c                   endif
      *                  -----------------------------------------
     c                   if        b1felt = 'RKFRIE'
     c                   eval      b1frti = '>' +
     c                             %subst(b1frti:1:05) + '<           >' +
     c                             %subst(b1frti:19:05) + '<'
     c                   endif
      *
6.22 c                   eval      b1rdat = elrdat
6.22 c                   move      elrtim        b1rtim
6.22  *
6.22 c*                  if        *in51  = *off
6.22  * dato informasjon
6.22 c*                  if        b1fsd <> *loval
6.22 c*    *dmy          move      b1fsd         w_datn
6.22 c*                  eval      b1flt1 = %editw(w_datn : '  .  .  ')
6.22 c*                  endif
     c*                  eval      b1flt3 = %editw(b1avd: '    0')
     c*                  eval      b1flt4 = %editw(b1prl: '    0')
6.22 c*                  else
6.22  * beløpsinformasjon
6.22 c*                  eval      b1flt1 = *blank
6.22 c*                  eval      b1flt2 = *blank
6.22 c*                  eval      b1flt3 = *blank
6.22 c*                  eval      b1flt4 = *blank
6.22 c*                  exsr      finn_sum
6.22 c*                  eval      w_buo  = b1buo
6.22 c*                  eval      w_but  = b1but
6.22 c*                  eval      b1flt1 = %editw(w_buo :' .   .   -')
6.22 c*                  eval      b1flt2 = %editw(w_but :' .   .   -')
6.22 c*                  eval      b1flt3 = %editw(w_sumi:' .   .   -')
6.22 c*                  eval      b1flt4 = %editw(w_sumk:' .   .   -')
6.22 c*                  endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
     c                   eval      *in22 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_regi           10
     c                   parm                    p_fore           10
     c                   parm                    p_felt           10
      *
      * Key for posisjonering på regijekt
      *
     c     slogl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    slogl1_regi
     c                   kfld                    slogl1_fore
     c                   kfld                    slogl1_fort
     c                   kfld                    slogl1_felt
     c                   kfld                    slogl1_rdat
     c                   kfld                    slogl1_rtim
      *
      * Key for posisjonering på dato innen register og forekomst
      *
     c     slogl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    slogl2_regi
     c                   kfld                    slogl2_fore
     c                   kfld                    slogl2_fort
     c                   kfld                    slogl2_rdat
     c                   kfld                    slogl2_felt
     c                   kfld                    slogl2_rtim
      *
      * Key for posisjonering på bruker innen register og forekomst
      *
     c     slogl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    slogl3_regi
     c                   kfld                    slogl3_user
     c                   kfld                    slogl3_rdat
     c                   kfld                    slogl3_rtim
     c                   kfld                    slogl3_felt
     c                   kfld                    slogl3_fore
     c                   kfld                    slogl3_fort
      *
      * Key for les på regijekt
      *
     c     sloglr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sloglr_regi
     c                   kfld                    sloglr_fore
     c                   kfld                    sloglr_fort
     c                   kfld                    sloglr_felt
     c                   kfld                    sloglr_rdat
     c                   kfld                    sloglr_rtim
      *
      * Key for oppdatering av regijket
      *
     c     sloglu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sloglu_regi
     c                   kfld                    sloglu_fore
     c                   kfld                    sloglu_fort
     c                   kfld                    sloglu_felt
     c                   kfld                    sloglu_rdat
     c                   kfld                    sloglu_rtim
      *
      * Key for oppslag på avdeling
      *
     c     ra07l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra07l1_kod
      *
      * Key for oppslag på bet.måte
      *
     c     ra02l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra02l1_kod
      *
      * Key for oppslag på bet.bet.
      *
     c     ra03l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra03l1_kod
      *
     c                   eval      w_firm = l_firm
      *
      * Initierer skjermbildet
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c*                  eval      b2regi      = p_regi
     c*                  eval      s_regi      = p_regi
     c*                  eval      b2fore      = p_fore
     c*                  eval      s_fore      = p_fore
     c                   eval      b2felt      = p_felt
     c                   eval      s_felt      = p_felt
     c                   eval      b2fdat      = *blank
     c                   eval      s_dato      = *blank
     c                   eval      b2user      = *blank
     c                   eval      s_user      = *blank
     c                   eval      w_seqe      = *blank
     c                   eval      slogl1_regi = p_regi
     c                   eval      slogl1_fore = p_fore
     c                   eval      slogl1_fort = *blank
     c                   eval      slogl1_felt = *blank
     c     *ymd          move      391231        w_hv
     c                   eval      slogl1_rdat = w_hv
     c                   eval      slogl1_rtim = *hival
     c     slogl1_key    setll     slogl1
      *
7.xx c**                 eval      b2head = 'Fra verdi         Til verdi   '
     c                   exsr      crt_subfile
      *
     c                   endsr
