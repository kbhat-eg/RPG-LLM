     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : LI306R
      * Beskrivelse . . : Samkjøre best. tilbake til basisbestilling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       131219 bsa  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flohelu    uf   e           k disk    rename(lohepfr:lohelur)
     flodtl1    if   e           k disk    rename(lodtpfr:lodtl1r)
     flodtlu    uf   e           k disk    rename(lodtpfr:lodtlur)
      *
      * Datastruktur ved oppdatering av lager
      *
     d                 ds
     d d_lrec                       128
     d  d_vare                       15    overlay(d_lrec)
     d  d_lage                        2  0 overlay(d_lrec:16)
     d  d_dato                         d   overlay(d_lrec:18) datfmt(*iso)
     d  d_time                         t   overlay(d_lrec:28) timfmt(*iso)
     d  d_otyp                        2    overlay(d_lrec:36)
     d  d_ltyp                        2    overlay(d_lrec:38)
     d  d_anta                       11  3 overlay(d_lrec:40)
     d  d_kopr                        9  2 overlay(d_lrec:51)
     d  d_refr                       20    overlay(d_lrec:60)
     d  d_syst                        1    overlay(d_lrec:80)
     d  d_altk                        2    overlay(d_lrec:81)
     d  d_rest                        1    overlay(d_lrec:83)
     d  d_type                        1    overlay(d_lrec:84)
     d  d_enhe                        3    overlay(d_lrec:85)
     d  d_inlr                        1    overlay(d_lrec:88)
     d  d_lety                        1    overlay(d_lrec:89)
     d  d_onum                        8  0 overlay(d_lrec:90)
     d  d_olin                        4  0 overlay(d_lrec:98)
      *
      * Definering av Local data area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av parametre
      *
     d p_numm          s                   like(lonumm)
     d p_bsuf          s                   like(losuff)
     d p_ssuf          s                   like(losuff)
      *
      * Definering av variabler i nøkler
      *
     d lohel1_numm     s                   like(lonumm)
     d lohel1_suff     s                   like(losuff)
     d lohelu_numm     s                   like(lonumm)
     d lohelu_suff     s                   like(losuff)
     d lodtl1_numm     s                   like(ldnumm)
     d lodtl1_suff     s                   like(ldsuff)
     d lodtlu_numm     s                   like(ldnumm)
     d lodtlu_suff     s                   like(ldsuff)
     d lodtlu_line     s                   like(ldline)
      *
      * Definering av variable
      *
     d w_firm          s                   like(lofirm)
     d w_line          s                   like(ldline)
     d w_enor          s              1
     d w_sald          s             11  2
     d w_numa          s              8
     d w_lina          s              4
     d w_mld1          s             50    inz(*blank)
      *
      * Merk av at basisordre nå er til behandling for samkjøring
      *
     c                   eval      lohelu_numm = p_numm
     c                   eval      lohelu_suff = p_bsuf
     c                   exsr      xmerk_ord
      *
      * Sjekk om ordre med oppgitt suffix ble funnet
      *
     c                   eval      lohel1_numm = p_numm
     c                   eval      lohel1_suff = p_ssuf
     c     lohel1_key    chain     lohel1r
      *
     c                   if        not %found
     c                   goto      xslutt
     c                   endif
      *
      * Er suffix ordre til behandling ?
      *
     c                   if        lokode <> 0
     c                   goto      xslutt
     c                   endif
      *
      * Merk av at suffix-ordre nå er til behandling for samkjøring
      *
     c                   eval      lohelu_numm = p_numm
     c                   eval      lohelu_suff = p_ssuf
     c                   exsr      xmerk_ord
      *
      * Behandle samkjøringen
      *
     c                   exsr      xoppd_ord
      *
      * Merk av at ordre nå er ferdigbehandlet
      *
     c                   eval      lohelu_numm = p_numm
     c                   eval      lohelu_suff = p_bsuf
     c                   exsr      xopph_ord
      *
      * Avslutt program
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for merking av at basisordre er i bruk
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xmerk_ord     begsr
      *
     C                   call      'LO709R'
     C                   parm                    lohelu_numm
     C                   parm                    lohelu_suff
      *
     C     lohelu_key    chain     lohelur                            91
     C                   if        %found
     C                   eval      lokode = 1
     c                   time                    loedat
     c                   time                    loetim
     c                   eval      loeusr = l_user
     C                   update    lohelur
     C                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppheving av merking
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xopph_ord     begsr
      *
     c                   call      'LO709R'
     c                   parm                    lohelu_numm
     c                   parm                    lohelu_suff
      *
     c     lohelu_key    chain     lohelur                            91
     c                   eval      lokode = 0
     c                   time                    loedat
     c                   time                    loetim
     c                   eval      loeusr = l_user
     C                   update    lohelur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av ordre (sam-kjøring)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xoppd_ord     begsr
      *
      * Tilbakefør basisordre
      *
     c                   eval      lohel1_numm = p_numm
     c                   eval      lohel1_suff = p_bsuf
     c     lohel1_key    chain     lohel1r                            90
      *
     c                   eval      lodtl1_numm = p_numm
     c                   eval      lodtl1_suff = p_bsuf
     c     lodtl1_key    setll     lodtl1
     c     lodtl1_key    reade     lodtl1r                                90
      *
     c                   dow       *in90  = *off
     c                   eval      ldanta =  (ldanta * -1)
     c                   exsr      xtilbake
     c     lodtl1_key    reade     lodtl1r                                90
     c                   enddo
      *
      * Oppdatere memosaldo basisordre
      *
      ***>               eval      w_sald = lototr - lototi
      *
      ***>               call      'FA922R'
      ***>               parm                    w_firm
      ***>               parm                    lootyp
      ***>               parm                    loldor
      ***>               parm                    w_sald
      *
      * Tilbakefør suffix-ordre
      *
     c                   eval      lohel1_numm = p_numm
     c                   eval      lohel1_suff = p_ssuf
     c     lohel1_key    chain     lohel1r                            90
      *
     c                   eval      lodtl1_numm = p_numm
     c                   eval      lodtl1_suff = p_ssuf
     c     lodtl1_key    setll     lodtl1
     c     lodtl1_key    reade     lodtl1r                                90
      *
     c                   dow       *in90  = *off
     c                   eval      ldanta =  (ldanta * -1)
     c                   exsr      xtilbake
     c     lodtl1_key    reade     lodtl1r                                90
     c                   enddo
      *
      * Oppdatere memosaldo suffix-ordre
      *
      ***>               eval      w_sald = lototr - lototi
      *
      ***>               call      'FA922R'
      ***>               parm                    w_firm
      ***>               parm                    lootyp
      ***>               parm                    loldor
      ***>               parm                    w_sald
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hente suffix-linjer inn til basis-ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Finn siste linjenr på basisordre
      *
     c                   eval      lodtlu_numm = p_numm
     c                   eval      lodtlu_suff = p_bsuf
     c                   eval      lodtlu_line = 9999
     c     lodtlu_key    setll     lodtl1r
     c                   eval      *in90 = *off
     c                   readp     lodtl1r                                90
      *
      * Feilmelding dersom linjenr for stort for reversering
      *
     c                   if        ldline > 9900 or
     c                             ldline = 9900
     c                   goto      xslutt
     c                   endif
      *
     c                   eval      w_line = 0010
     c                   if        *in90  = *off
     c                   if        ldfirm = w_firm
     c                   if        ldnumm = p_numm
     c                   if        ldsuff = p_bsuf
     c                   eval      w_line = ldline
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
      * Les suffixlinjer, og flytt de over til basisordre
      *
     c                   eval      lodtl1_numm = p_numm
     c                   eval      lodtl1_suff = p_ssuf
     c     lodtl1_key    setll     lodtlu
     c     lodtl1_key    reade     lodtlur                                90
      *
     c                   dow       *in90  = *off
     c                   eval      w_line = w_line + 10
     c                   eval      ldsuff = p_bsuf
     c                   eval      ldline = w_line
     c                   time                    ldedat
     c                   time                    ldetim
     c                   eval      ldeusr = l_user
     c                   update    lodtlur
     c     lodtl1_key    reade     lodtlur                                90
     c                   enddo
      *
      * Slett deretter ordrehode på suffix-ordre
      *
     c                   eval      lohelu_numm = p_numm
     c                   eval      lohelu_suff = p_ssuf
     c     lohelu_key    chain     lohelur                            90
     c                   if        *in90 = *off
     c                   delete    lohelur
     c                   endif
      *
      * Oppdatere ny sammenkjørt basisordre
      *
     c                   eval      lohel1_numm = p_numm
     c                   eval      lohel1_suff = p_bsuf
     c     lohel1_key    chain     lohel1r                            90
      *
     c                   eval      lodtl1_numm = p_numm
     c                   eval      lodtl1_suff = p_bsuf
     c     lodtl1_key    setll     lodtl1
     c     lodtl1_key    reade     lodtl1r                                90
      *
     c                   dow       *in90  = *off
     c                   exsr      xtilbake
     c     lodtl1_key    reade     lodtl1r                                90
     c                   enddo
      *
      * Oppdater ny sammenkjørt basis-ordre
      *
      ***> Ikke oppdater hent inn pris på nytt
      ***>               eval      w_enor = '1'
     c                   eval      w_enor = ' '
     c                   call      'LO730R'
     c                   parm                    w_enor
     c                   parm                    w_firm
     c                   parm                    p_numm
     c                   parm                    p_bsuf
      *
      * Oppdatere memosaldo basisordre
      *
      ***>               eval      w_sald = lototi - lototr
      *
      ***>               call      'FA922R'
      ***>               parm                    w_firm
      ***>               parm                    lootyp
      ***>               parm                    loldor
      ***>               parm                    w_sald
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tilbakeføring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xtilbake      begsr
      *
      * Tilbakeføring
      *
     c                   if        laalag <> 0
      *
     c                   eval      d_vare =  ldvare
     c                   eval      d_lage =  ldlage
     c                   eval      d_enhe =  ldenh1
     c                   eval      d_dato =  *loval
     c                   eval      d_time =  *loval
     c                   eval      d_otyp =  lootyp
     c                   eval      d_ltyp =  ldltyp
      *
     c                   eval      d_anta =  ldanta
     c                   eval      d_kopr =  ldkpny
      *
     c                   eval      d_refr =  *blank
     c                   eval      d_syst =  'L'
     c                   eval      d_altk =  *blank
     c                   eval      d_rest =  *blank
     c                   move      ldnumm        w_numa
     c                   move      ldline        w_lina
     c                   eval      d_refr = 'B:' + w_numa +
     c                                      ' L:' + w_lina
     c                   eval      d_onum = ldnumm
     c                   eval      d_olin = ldline
     c                   move      ldlety        d_lety
      *
      * Kaller opp lageroppdaterings-program
      *
     c                   eval      w_lrec =  d_lrec
      *
     c                   call      'VL001R'
     c                   parm                    w_v001            1
     c                   parm                    w_lrec          128
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for Ordre-hode-tabell les
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
     c                   kfld                    lohel1_suff
      *
      * Key for Ordre-hode-tabell update
      *
     c     lohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelu_numm
     c                   kfld                    lohelu_suff
      *
      * Key for Ordre-linje-tabell les
      *
     c     lodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtl1_numm
     c                   kfld                    lodtl1_suff
      *
      * Key for Ordre-linje-tabell update
      *
     c     lodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlu_numm
     c                   kfld                    lodtlu_suff
     c                   kfld                    lodtlu_line
      *
      * Key for Status-tabell les
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     *entry        plist
     c                   parm                    p_numm
     c                   parm                    p_bsuf
     c                   parm                    p_ssuf
      *
      * Henter firmanummer fra LDA
      *
     c                   eval      w_firm      = l_firm
      *
      * Henter informasjon fra status-tabell
      *
     c     lstsl1_key    chain     lstsl1r                            90
      *
     c                   endsr
      *
