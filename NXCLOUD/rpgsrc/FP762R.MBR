     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FP762R
      * Beskrivelse . . : Spesialpris, innlesing fra fil - varegr, modul, varenr
      *                       - deltakernr, prisdato nobb, rabatt skal gi
      *                         spes.priser på varer under gitt struktur
      *
      * Det er tatt utg.pkt. i FP754R
      * Legger ut spesialpris på alle salgsenheter
      *
      * Programmet leser valgt fil, og oppretter spesialpriser for valgt
      * rabattkategori, kunde eller kundeprosjekt.
      *
9.01  * Spesialversjon. : Skattum, leser et regneark med info som gir spes.pris
9.01  * Tilpasset for . : Skattum
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       130519 BOF  Nytt program
8.01  *PRG2   100622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fflvwpf    uf   e           k disk    rename(flvwpfr:flvwpfr)
      *
     fvvarl3    if   e           k disk    rename(vvarpfr:vvarl3r)
     fvvenl2    if   e           k disk    rename(vvenpfr:vvenl2r)
     ffpril3    uf   e           k disk    rename(fpripfr:fpril3r)
     ffprilu    uf a e           k disk    rename(fpripfr:fprilur)
     ffprnlu    uf   e           k disk    rename(fprnpfr:fprnlur)
     fjvprl1    if   e           k disk    rename(jvprpfr:jvprl1r)
     fjmodl1    if   e           k disk    rename(jmodpfr:jmodl1r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
6.13 ffp6wst    uf a e           k disk    rename(fp6wst:fp6wstr)
      *
      * Definering av Local data area *LDA
      *
     d                uds
     d l_user                911    920
      *
     d ds_fp6wst     E ds                  extName(fp6wst)
      * Definering av Nobb prisformat
      *
     d                 ds
9.01 d d_list                        80
     d  d_delt                        6    overlay(d_list: 1)
     d   d_deltn                      6  0  overlay(d_delt:1)
     d  d_ogrp                        2    overlay(d_list:7)
     d   d_ogrpn                      2  0  overlay(d_ogrp:1)
     d  d_hgrp                        2    overlay(d_list:9)
     d   d_hgrpn                      2  0  overlay(d_hgrp:1)
     d  d_ugrp                        3    overlay(d_list:11)
     d   d_ugrpn                      3  0  overlay(d_ugrp:1)
     d  d_modn                        8    overlay(d_list:14)
     d   d_modnn                      8  0  overlay(d_modn:1)
     d  d_nobb                        8    overlay(d_list:24)
     d   d_nobbn                      8  0  overlay(d_nobb:1)
     d  d_rabp                        6    overlay(d_list:32)
     d   d_rabp_hel                   2  0  overlay(d_rabp:1)
     d   d_rabp_des                   3  3  overlay(d_rabp:4)
     d  d_ltyp                        1    overlay(d_list:38)
     d   d_ltypn                      1  0  overlay(d_ltyp:1)
9.01 d  d_gdat                        6  0 overlay(d_list:39)
9.01 d  d_fdat                        6  0 overlay(d_list:45)
9.01 d  d_tdat                        6  0 overlay(d_list:51)
      *
     d                 ds
     d d_dato                        10
     d  d_dd                          2    overlay(d_dato:1)
     d  d_mm                          2    overlay(d_dato:4)
     d  d_aa                          4    overlay(d_dato:7)
     d                 ds
     d w_dat6                         6
     d  w_dd                          2    overlay(w_dat6:1)
     d  w_mm                          2    overlay(w_dat6:3)
     d  w_aa                          2    overlay(w_dat6:5)
      *
      *
     d w_2x            s              2
     d w_3x            s              3
     d w_6x            s              6
     d w_8x            s              8
     d w_hel           s              7
     d w_des           s              2
     d w_rabp          s              5  3
     d w_pris          s              6
      *
      * Definering av parametre
      *
     d p_firm          s                   like(fpfirm)
5.70 d p_prgr          s                   like(fpprgr)
     d p_rkat          s                   like(fprkat)
     d p_kund          s                   like(fpkund)
     d p_kpro          s                   like(fpkpro)
     d p_slet          s              1  0
      *
      * Definering av variable i nøkler
      *
     d vvarl3_nobb     s                   like(vvnobb)
     d vvenl2_vare     s                   like(vevare)
     d vvenl2_saen     s                   like(vesaen)
     d fpril3_kund     s                   like(fpkund)
     d fpril3_kpro     s                   like(fpkpro)
5.70 d fprilu_prgr     s                   like(fpprgr)
     d fprilu_rkat     s                   like(fprkat)
     d fprilu_kkat     s                   like(fpkkat)
     d fprilu_kund     s                   like(fpkund)
     d fprilu_kpro     s                   like(fpkpro)
     d fprilu_vare     s                   like(fpvare)
     d fprilu_enhe     s                   like(fpenhe)
     d fprilu_lety     s                   like(fplety)
     d fprnlu_nkun     s                   like(fpnkun)
     d fprnlu_nkpr     s                   like(fpnkpr)
     d jvprl1_vare     s                   like(jxvare)
     d jvprl1_ldor     s                   like(jxldor)
     d jmodl1_modn     s                   like(jumodn)
     d jvarl1_vare     s                   like(jvvare)
      *
      * Definering av variable
      *
     d w_dato          s               d   datfmt(*iso)
      *
     d w_firm          s                   like(fpfirm)
     d w_enh1          s                   like(veenhe)
     d w_enh2          s                   like(veenhe)
     d w_enh3          s                   like(veenhe)
     d w_omr1          s                   like(veomre)
     d w_omr2          s                   like(veomre)
     d w_omr3          s                   like(veomre)
     d w_sap1          s                   like(fpsapr)
     d w_sap2          s                   like(fpsapr)
     d w_sap3          s                   like(fpsapr)
     d w_kop1          s                   like(fpkopr)
     d w_kop2          s                   like(fpkopr)
     d w_kop3          s                   like(fpkopr)
     d w_penh          s                   like(vvenhe)
     d w_posi          s              2  0
     d w_leng          s              2  0
     d w_posp          s              2  0
     d st              s              2  0
     d w_nobx          s              8
6.31 d w_lv_vare       s                   like(vvvare)

     d len             s              3  0
     d w_fakt          s              6  5
     d w_gdat          s                   like(vvedat)
     d w_nopr          s                   like(jxpris)
     d w_sapr          s                   like(fpsapr)
      *
     d b_feil          s              1    inz(*off)
     d b_ok            s              1    inz(*off)
     d b_eof           s              1    inz(*off)
      *
      * Konstanter
      *
     d c_form          s              1    inz('1')
     d c_digi          s             15    inz('0123456789 ')
     d c_null          s             15    inz('000000000000000')
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.31 C/Exec SQL
6.3q C+  Set Option DatFmt=*ISO , commit=*none, DlyPrp=*YES
     C+  , SRTSEQ=*LANGIDUNQ, LANGID=NOR
6.31 C/End-Exec
      * Dersom sletting er valgt, slettes alle eksisterende spesialpriser
      * for rabattkategori eller kunde/kundeprosjekt
      *
      * sletter arbeidsfil
     c/exec sql
     c+ delete
     c+ from   fp6wst
6.31 c+ where  fpwfir = :w_firm
     c/end-exec

     c                   if        p_slet = 1
      *
     c                   if        p_rkat <> 0
5.70 c                   eval      fprilu_prgr = p_prgr
     c                   eval      fprilu_rkat = p_rkat
     c     fprilu_key2   setll     fprilu
     c     fprilu_key2   reade     fprilur
     c                   dow       not %eof
     c                   delete    fprilur
     c     fprilu_key2   reade     fprilur
     c                   enddo
     c                   else
      *
     c                   eval      fpril3_kund = p_kund
     c                   eval      fpril3_kpro = p_kpro
     c     fpril3_key    setll     fpril3
     c     fpril3_key    reade     fpril3r
     c                   dow       not %eof
     c                   delete    fpril3r
     c     fpril3_key    reade     fpril3r
     c                   enddo
      *
     c                   eval      fprnlu_nkun = p_kund
     c                   eval      fprnlu_nkpr = p_kpro
     c     fprnlu_key    setll     fprnlu
     c     fprnlu_key    reade     fprnlur
     c                   dow       not %eof
     c                   delete    fprnlur
     c     fprnlu_key    reade     fprnlur
     c                   enddo
      *
     c                   endif
      *
     c                   endif
      *
      * Leser og danner arbeidstabell
      *
     c                   read      flvwpfr
     c                   dow       not %eof
     c                   exsr      utled_sdv
     c                   if        b_feil = *off
      *
     c                   if        d_nobbn <> 0
     c                   movel     d_nobb        jvarl1_vare
     c     jvarl1_key    chain     jvarl1
     c                   if        %found(jvarl1)
     c                   eval      d_ogrpn = jvogrp
     c                   eval      d_hgrpn = jvhgrp
     c                   eval      d_ugrpn = jvugrp
     c                   eval      d_modnn = jvmodn
     c                   else
     c                   leave
     c                   endif
     c                   endif
     c                   if        d_modnn <> 0
     c                   eval      jmodl1_modn = d_modnn
     c     jmodl1_key    chain     jmodl1
     c                   if        %found(jmodl1)
     c                   eval      d_ogrpn = juogrp
     c                   eval      d_hgrpn = juhgrp
     c                   eval      d_ugrpn = juugrp
     c                   else
     c                   leave
     c                   endif
     c                   endif

     c                   clear                   fp6wstr
     c                   eval      fpwfir = w_firm
     c                   eval      fpwldo = d_deltn
     c                   eval      fpwogr = d_ogrpn
     c                   eval      fpwhgr = d_hgrpn
     c                   eval      fpwugr = d_ugrpn
     c                   eval      fpwmod = d_modnn
     c                   eval      fpwnob = d_nobbn
     c                   eval      fpwnob = d_nobbn
     c                   eval      fpwlty = d_ltypn
     c                   eval      fpwrab =  d_rabp_hel + d_rabp_des
     c     *DMY          move      d_gdat        fpwgda
     c     *DMY          move      d_fdat        fpwfda
     c     *DMY          move      d_tdat        fpwtda
     c                   time                    fpwoda
     c                   time                    fpwoti
     c                   eval      fpwous = l_user
     c                   time                    fpweti
     c                   time                    fpweti
     c                   eval      fpweus = l_user
     c                   write     fp6wstr
     c                   endif
     c                   read      flvwpfr
     c                   enddo
      *
      * Leser leser arbeidstabell  i riktig rekkefølge, oppdaterer
      *
      *
      *sql
9.06 c/exec sql
9.06 c+ declare trans cursor for
9.06 c+ select fpwldo,
9.06 c+        fpwogr,
9.06 c+        fpwhgr,
9.06 c+        fpwugr,
9.06 c+        fpwmod,
9.06 c+        fpwnob,
9.06 c+        fpwrab,
9.06 c+        fpwlty,
9.06 c+        fpwgda,
9.06 c+        fpwfda,
9.06 c+        fpwtda
9.06 c+ from   fp6wst
9.06 c+ where  fpwfir = :w_firm
     c+ order by fpwldo,
     c+          fpwogr,
     c+          fpwhgr,
     c+          fpwugr,
     c+          fpwmod,
     c+          fpwnob,
     c+          fpwrab desc
9.06 c+ for read only
9.06 c/end-exec
9.06 c/exec sql
9.06 c+ open trans
9.06 c/end-exec
     c                   eval      b_eof = *off
     c                   dow       b_EOF = *off
      *
9.06 c/exec sql
9.06 c+ fetch trans
     c+ into  :fpwldo,
     c+       :fpwogr,
     c+       :fpwhgr,
     c+       :fpwugr,
     c+       :fpwmod,
     c+       :fpwnob,
     c+       :fpwrab,
     c+       :fpwlty,
     c+       :fpwgda,
     c+       :fpwfda,
     c+       :fpwtda
     c/end-exec
9.06 c                   if        sqlcod = 100 or sqlstt = '02000'
9.06 c/exec sql
9.06 c+  close trans
     c/end-exec
9.06 c                   leave
     c                   endif
     c                   exsr      oppdater
     c                   enddo
      *
      * Kaller program i VA over varer som ikke ble lest inn i
      * spesialpris-registeret.
      *
     c*                  call      'FO719C'
     c*                  parm                    c_form
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utleding av datastruktur
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     utled_sdv     begsr
      *
     c                   eval      d_list = *blank
     c                   eval      b_feil = *off
      *
     c     '"':' '       xlate     fwirad        fwirad
      * Div. felter
      *
      *
      * Deltakernr
      *
     c                   eval      w_posi = %scan(';':fwirad)
     c                   eval      d_delt = %trim(%subst(fwirad:1:w_posi-1))
     c                   eval      len     = %len(%trim(d_delt))
     c                   if        len     > 0
     c                   eval      st = 7 - len
     c                   eval      w_6x  = *blank
     c                   eval      %subst(w_6x:st:len) = %subst(d_delt:1:len)
     c                   eval      d_delt = w_6x
     c                   endif
     c     ' ':'0'       xlate     d_delt        d_delt
      *
     c     c_digi        check     d_delt                                 70
     c                   if        *in70 = *on
     c                   eval      b_feil = *on
     c                   goto      end_utled
     c                   endif
      * deltaker navn
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
      * overgr.
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
     c                   eval      d_ogrp = %trim(%subst(fwirad:1:w_posi-1))
     c                   eval      len     = %len(%trim(d_ogrp))
     c                   if        len     > 0
     c                   eval      st = 3 - len
     c                   eval      w_2x  = *blank
     c                   eval      %subst(w_2x:st:len) = %subst(d_ogrp:1:len)
     c                   eval      d_ogrp = w_2x
     c                   endif
     c     ' ':'0'       xlate     d_ogrp        d_ogrp
      * hovedgr.
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
     c                   eval      d_hgrp = %trim(%subst(fwirad:1:w_posi-1))
     c                   eval      len     = %len(%trim(d_hgrp))
     c                   if        len     > 0
     c                   eval      st = 3 - len
     c                   eval      w_2x  = *blank
     c                   eval      %subst(w_2x:st:len) = %subst(d_hgrp:1:len)
     c                   eval      d_hgrp = w_2x
     c                   endif
     c     ' ':'0'       xlate     d_hgrp        d_hgrp
      * undergruppe
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
     c                   eval      d_ugrp = %trim(%subst(fwirad:1:w_posi-1))
     c                   eval      len     = %len(%trim(d_ugrp))
     c                   if        len     > 0
     c                   eval      st = 4 - len
     c                   eval      w_3x  = *blank
     c                   eval      %subst(w_3x:st:len) = %subst(d_ugrp:1:len)
     c                   eval      d_ugrp = w_3x
     c                   endif
     c     ' ':'0'       xlate     d_ugrp        d_ugrp
      * modul
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
     c                   eval      d_modn = %trim(%subst(fwirad:1:w_posi-1))
     c                   eval      len     = %len(%trim(d_modn))
     c                   if        len     > 0
     c                   eval      st = 9 - len
     c                   eval      w_8x  = *blank
     c                   eval      %subst(w_8x:st:len) = %subst(d_modn:1:len)
     c                   eval      d_modn = w_8x
     c                   endif
     c     ' ':'0'       xlate     d_modn        d_modn
      * nobbnr.
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
     c                   eval      d_nobb = %trim(%subst(fwirad:1:w_posi-1))
     c                   eval      len     = %len(%trim(d_nobb))
     c                   if        len     > 0
     c                   eval      st = 9 - len
     c                   eval      w_8x  = *blank
     c                   eval      %subst(w_8x:st:len) = %subst(d_nobb:1:len)
     c                   eval      d_nobb = w_8x
     c                   endif
     c     ' ':'0'       xlate     d_nobb        d_nobb
     c                   if        d_nobb = '10599991'
     c     ' ':'0'       xlate     d_nobb        d_nobb
     c                   endif

      * rab.prosent
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
     c                   eval      d_rabp = %trim(%subst(fwirad:1:w_posi-1))
      * lety
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
     c                   eval      d_ltyp = %trim(%subst(fwirad:1:w_posi-1))
9.01  * gyldig dato
9.01 c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
9.01 c                   eval      w_posi = %scan(';':fwirad)
9.01 c                   eval      d_dato = %trim(%subst(fwirad:1:w_posi-1))
     c                   if        %len(%trim(d_dato)) = 9
     c                   eval      d_dato = '0' + %subst(d_dato:1:9)
     c                   endif
9.01 c                   exsr      konv_dato
9.01 c                   move      w_dat6        d_gdat
9.01  * fra dato
9.01 c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
9.01 c                   eval      w_posi = %scan(';':fwirad)
9.01 c                   eval      d_dato = %trim(%subst(fwirad:1:w_posi-1))
     c                   if        %len(%trim(d_dato)) = 9
     c                   eval      d_dato = '0' + %subst(d_dato:1:9)
     c                   endif
9.01 c                   exsr      konv_dato
9.01 c                   move      w_dat6        d_fdat
9.01  * til dato
9.01 c                   eval      fwirad = %subst(fwirad:w_posi+1:11-1)
9.01 c                   eval      w_posi = %scan(';':fwirad)
9.01 c                   eval      d_dato = %trim(%subst(fwirad:1:10))
     c                   if        %len(%trim(d_dato)) = 9
     c                   eval      d_dato = '0' + %subst(d_dato:1:9)
     c                   endif
9.01 c                   exsr      konv_dato
9.01 c                   move      w_dat6        d_tdat
      *
      * resten ubrukte felter
      * Utleder rabprosent
      *
     c                   if        d_rabp <> *blank
      *
     c                   eval      d_rabp = %trim(d_rabp)
     c                   eval      w_posi = %scan(',':d_rabp)
     c                   eval      w_leng = %len(%trim(d_rabp))
      *
     c                   if        w_posi = 0
     c                   eval      w_posi = w_leng + 1
     c                   eval      d_rabp = %trim(d_rabp) + '.000'
     c                   endif
      *
     c     ' ':'0'       xlate     d_rabp        d_rabp
      *
     c                   if        (w_posi-1) < 2
     c                   eval      d_rabp = %subst(c_null:1:2-w_posi+1) +
     c                                      %subst(d_rabp:1:w_posi-1) +
     c                                      '.' +
     c                                      %subst(d_rabp:w_posi+1:3)
     c                   endif
      *
     c                   else
     c                   eval      d_rabp = '00.000'
     c                   endif
      *
     c     end_utled     endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting/oppdatering av spesialpriser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdater      begsr
      *
      * Avbryter dersom nobb-nummer eller pris ikke er utfylt
      *
     c                   exsr      sjekk_input
     c                   if        b_ok = *off
     c                   leavesr
     c                   endif
      *
     c                   select
     c                   when      fpwnob  <> 0
      * -- vare
     c/exec sql
     c+                  declare vare  cursor for
     c+                  select vvvare,
     c+                         jvogrp,
     c+                         jvhgrp,
     c+                         jvugrp,
     c+                         vvldor,
     c+                         vvnlev,
     c+                         jvpenh
     c+                  from   vvarpf
     c+                  inner  join jvarpf on vvvare = jvvare
     c+                  where  vvfirm = :w_firm and
     c+                         vvnobb = :fpwnob and
     c+                         vvnlev = :fpwldo
     c+                  order by vvfirm, vvvare
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close vare
     c/end-exec
     c/exec sql
     c+                  open vare
     c/end-exec
     c                   when      fpwmod  <> 0
      * -- modul
     c/exec sql
     c+                  declare modn  cursor for
     c+                  select vvvare,
     c+                         jvogrp,
     c+                         jvhgrp,
     c+                         jvugrp,
     c+                         vvldor,
     c+                         vvnlev,
     c+                         jvpenh
     c+                  from   vvarpf
     c+                  inner  join jvarpf on vvvare = jvvare
     c+                  where  vvfirm = :w_firm and
     c+                         jvmodn = :fpwmod and
     c+                         vvnlev = :fpwldo
     c+                  order by vvfirm, vvvare
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close modn
     c/end-exec
     c/exec sql
     c+                  open modn
     c/end-exec
     c                   when      fpwogr  <> 0 and
     c                             fpwhgr  <> 0 and
     c                             fpwugr  <> 0
      * -- undergruppe
     c/exec sql
     c+                  declare ugrp  cursor for
     c+                  select vvvare,
     c+                         jvogrp,
     c+                         jvhgrp,
     c+                         jvugrp,
     c+                         vvldor,
     c+                         vvnlev,
     c+                         jvpenh
     c+                  from   vvarpf
     c+                  inner  join jvarpf on vvvare = jvvare
     c+                  where  vvfirm = :w_firm and
     c+                         jvogrp = :fpwogr and
     c+                         jvhgrp = :fpwhgr and
     c+                         jvugrp = :fpwugr and
     c+                         vvnlev = :fpwldo
     c+                  order by vvfirm, vvvare
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close ugrp
     c/end-exec
     c/exec sql
     c+                  open ugrp
     c/end-exec
     c                   when      fpwogr  <> 0 and
     c                             fpwhgr  <> 0
      * hovedgruppe
     c/exec sql
     c+                  declare hgrp  cursor for
     c+                  select vvvare,
     c+                         jvogrp,
     c+                         jvhgrp,
     c+                         jvugrp,
     c+                         vvldor,
     c+                         vvnlev,
     c+                         jvpenh
     c+                  from   vvarpf
     c+                  inner  join jvarpf on vvvare = jvvare
     c+                  where  vvfirm = :w_firm and
     c+                         jvogrp = :fpwogr  and
     c+                         jvhgrp = :fpwhgr  and
     c+                         vvnlev = :fpwldo
     c+                  order by vvfirm, vvvare
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close hgrp
     c/end-exec
     c/exec sql
     c+                  open hgrp
     c/end-exec
     c                   when      fpwogr  <> 0
      * -- overgruppe
     c/exec sql
     c+                  declare ogrp  cursor for
     c+                  select vvvare,
     c+                         jvogrp,
     c+                         jvhgrp,
     c+                         jvugrp,
     c+                         vvldor,
     c+                         vvnlev,
     c+                         jvpenh
     c+                  from   vvarpf
     c+                  inner  join jvarpf on vvvare = jvvare
     c+                  where  vvfirm = :w_firm and
     c+                         vvogrp = :fpwogr  and
     c+                         vvnlev = :fpwldo
     c+                  order by vvfirm, vvvare
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close ogrp
     c/end-exec
     c/exec sql
     c+                  open ogrp
     c/end-exec
      *
     c                   when      fpwogr  = 0 and
     c                             fpwhgr  = 0 and
     c                             fpwugr  = 0 and
     c                             fpwmod  = 0 and
     c                             fpwnob  = 0
      * -- alt på leverandør
     c/exec sql
     c+                  declare ldor  cursor for
     c+                  select vvvare,
     c+                         jvogrp,
     c+                         jvhgrp,
     c+                         jvugrp,
     c+                         vvldor,
     c+                         vvnlev,
     c+                         jvpenh
     c+                  from   vvarpf
     c+                  inner  join jvarpf on vvvare = jvvare
     c+                  where  vvfirm = :w_firm and
     c+                         vvnlev = :fpwldo
     c+                  order by vvfirm, vvvare
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close ldor
     c/end-exec
     c/exec sql
     c+                  open ldor
     c/end-exec

     c
     c                   endsl
      *
      *
     c                   eval      b_eof = *off
     c                   dow       b_Eof = *off
     c                   select
     c                   when      fpwnob  <> 0
      * -- vare
     c/exec sql
     c+                  fetch next
     c+                  from  vare
     c+                  into  :vvvare,
     c+                        :vvogrp,
     c+                        :vvhgrp,
     c+                        :vvugrp,
     c+                        :vvldor,
     c+                        :vvnlev,
     c+                        :w_penh
     c/end-exec
     c                   when      fpwmod  <> 0
      * -- modul
     c/exec sql
     c+                  fetch next
     c+                  from  modn
     c+                  into  :vvvare,
     c+                        :vvogrp,
     c+                        :vvhgrp,
     c+                        :vvugrp,
     c+                        :vvldor,
     c+                        :vvnlev,
     c+                        :w_penh
     c/end-exec
     c                   when      fpwogr  <> 0 and
     c                             fpwhgr  <> 0 and
     c                             fpwugr  <> 0
      * -- undergruppe
     c/exec sql
     c+                  fetch next
     c+                  from  ugrp
     c+                  into  :vvvare,
     c+                        :vvogrp,
     c+                        :vvhgrp,
     c+                        :vvugrp,
     c+                        :vvldor,
     c+                        :vvnlev,
     c+                        :w_penh
     c/end-exec
     c                   when      fpwogr  <> 0 and
     c                             fpwhgr  <> 0
      * hovedgruppe
     c/exec sql
     c+                  fetch next
     c+                  from  hgrp
     c+                  into  :vvvare,
     c+                        :vvogrp,
     c+                        :vvhgrp,
     c+                        :vvugrp,
     c+                        :vvldor,
     c+                        :vvnlev,
     c+                        :w_penh
     c/end-exec
     c                   when      fpwogr  <> 0
      * -- overgruppe
     c/exec sql
     c+                  fetch next
     c+                  from  ogrp
     c+                  into  :vvvare,
     c+                        :vvogrp,
     c+                        :vvhgrp,
     c+                        :vvugrp,
     c+                        :vvldor,
     c+                        :vvnlev,
     c+                        :w_penh
     c/end-exec
     c                   when      fpwogr  = 0 and
     c                             fpwhgr  = 0 and
     c                             fpwugr  = 0 and
     c                             fpwmod  = 0 and
     c                             fpwnob  = 0
      * -- alt på leverandør
     c/exec sql
     c+                  fetch next
     c+                  from  ldor
     c+                  into  :vvvare,
     c+                        :vvogrp,
     c+                        :vvhgrp,
     c+                        :vvugrp,
     c+                        :vvldor,
     c+                        :vvnlev,
     c+                        :w_penh
     c/end-exec
     c                   endsl
     c                   if        sqlcod = 100 or sqlstt = '02000'
      *
     c                   select
     c                   when      fpwnob  <> 0
      * vare
6.31 c/exec sql
6.31 c+ close vare
6.31 c/end-exec
     c                   leave
     c                   when      fpwmod  <> 0
      * modulnr
6.31 c/exec sql
6.31 c+ close modn
6.31 c/end-exec
     c                   leave
     c                   when      fpwogr  <> 0 and
     c                             fpwhgr  <> 0 and
     c                             fpwugr  <> 0
      * undergruppe
6.31 c/exec sql
6.31 c+ close ugrp
6.31 c/end-exec
     c                   leave
     c                   when      fpwogr  <> 0 and
     c                             fpwhgr  <> 0
      * hovedgruppe
6.31 c/exec sql
6.31 c+ close hgrp
6.31 c/end-exec
     c                   leave

     c                   when      fpwogr  <> 0
      *overgruppe
6.31 c/exec sql
6.31 c+ close ogrp
6.31 c/end-exec
     c                   leave
      *
     c                   when      fpwogr  = 0 and
     c                             fpwhgr  = 0 and
     c                             fpwugr  = 0 and
     c                             fpwmod  = 0 and
     c                             fpwnob  = 0
      * -- alt på leverandør
6.31 c/exec sql
6.31 c+ close ldor
6.31 c/end-exec
     c                   leave

      *
     c                   endsl
      *
     c                   endif

     c                   exsr      hent_nobb
     c                   if        w_nopr <> 0
      *
      * Henter tre salgsenheter fra varen
      *
     c                   eval      w_enh1 = *blank
     c                   eval      w_enh2 = *blank
     c                   eval      w_enh3 = *blank
     c                   eval      w_omr1 = 0
     c                   eval      w_omr2 = 0
     c                   eval      w_omr3 = 0
      *
     c                   eval      vvenl2_vare = vvvare
     c                   eval      vvenl2_saen = 1
     c     vvenl2_key    setll     vvenl2
     c     vvenl2_key2   reade     vvenl2
     c                   dow       not %eof
      *
     c                   select
     c                   when      w_enh1 = *blank
     c                   eval      w_enh1 = veenhe
     c                   eval      w_omr1 = veomre
     c                   when      w_enh1 <> *blank and
     c                             w_enh2 = *blank
     c                   eval      w_enh2 = veenhe
     c                   eval      w_omr2 = veomre
     c                   when      w_enh1 <> *blank and
     c                             w_enh2 <> *blank and
     c                             w_enh3 = *blank
     c                   eval      w_enh3 = veenhe
     c                   eval      w_omr3 = veomre
     c                   leave
     c                   endsl
      *
     c     vvenl2_key2   reade     vvenl2
     c                   enddo
      *
      * Avbryter dersom enhet fra ark ikke finnes på varen
      *
     c                   if        w_enh1 <> w_penh and
     c                             w_enh2 <> w_penh and
     c                             w_enh3 <> w_penh
     c                   goto      neste_vare
     c                   endif
      *
      * Finner priser for alle salgsenheter
     c                   if        vvvare = '43990743' or
     c                             vvvare = '43990796'
     c                   eval      w_Sap1 = 0
     c                   endif
      *
      *rabattfaktor
     c                   eval      w_rabp = fpwrab
     c                   eval      w_fakt = 1 - (w_rabp/100)
      *
     c                   eval(h)   w_sapr = jxpris * w_fakt

     c                   eval      w_sap1 = 0
     c                   eval      w_sap2 = 0
     c                   eval      w_sap3 = 0
     c                   eval      w_kop1 = 0
     c                   eval      w_kop2 = 0
     c                   eval      w_kop3 = 0
      *
     c                   select
     c                   when      w_penh = w_enh1
     c                   eval      w_sap1 = w_sapr
     c                   eval      w_sap2 = w_sap1 * w_omr2 / w_omr1
     c                   eval      w_sap3 = w_sap1 * w_omr3 / w_omr1
     c                   when      w_penh = w_enh2
     c                   eval      w_sap2 = w_sapr
     c                   eval      w_sap1 = w_sap2 * w_omr1 / w_omr2
     c                   eval      w_sap3 = w_sap2 * w_omr3 / w_omr2
     c                   when      w_penh = w_enh3
     c                   eval      w_sap3 = w_sapr
     c                   eval      w_sap1 = w_sap3 * w_omr1 / w_omr3
     c                   eval      w_sap2 = w_sap3 * w_omr2 / w_omr3
     c                   endsl
      *
      * Oppdaterer/oppretter spesialpris
      *
      * - enhet 1
      *
     c                   clear                   fprilur
      *
5.70 c                   eval      fprilu_prgr = p_prgr
     c                   eval      fprilu_rkat = p_rkat
     c                   eval      fprilu_kkat = 0
     c                   eval      fprilu_kund = p_kund
     c                   eval      fprilu_kpro = p_kpro
     c                   eval      fprilu_vare = vvvare
     c                   eval      fprilu_enhe = w_enh1
     c                   eval      fprilu_lety = fpwlty
     c     fprilu_key    chain     fprilur
      *
     c                   eval      fpfirm = w_firm
5.70 c                   eval      fpprgr = p_prgr
     c                   eval      fprkat = p_rkat
     c                   eval      fpkkat = 0
     c                   eval      fpkund = p_kund
     c                   eval      fpkpro = p_kpro
     c                   eval      fpvare = vvvare
     c                   eval      fpenhe = w_enh1
     c                   eval      fplety = fpwlty
     c                   eval      fpfdat = fpwfda
     c                   eval      fptdat = fpwtda
     c                   eval      fpsapr = w_sap1
     c                   eval      fpkopr = 0
     c                   eval      fprab1 = 0
     c                   eval      fprab2 = 0
     c                   eval      fpnumm = 0
      *
     c                   eval      fpeusr = l_user
     c                   time                    fpedat
     c                   time                    fpetim
      *
     c                   if        %found(fprilu)
6.10 c                   time                    fpedat
6.10 c                   time                    fpetim
6.10 c                   eval      fpeusr = l_user
     c                   update    fprilur
     c                   else
6.10 c                   time                    fpodat
6.10 c                   time                    fpotim
6.10 c                   eval      fpousr = l_user
6.10 c                   time                    fpedat
6.10 c                   time                    fpetim
6.10 c                   eval      fpeusr = l_user
     c                   write     fprilur
     c                   endif
      *
      * - enhet 2
      *
     c                   if        w_enh2 <> *blank
      *
     c                   clear                   fprilur
      *
     c                   eval      fprilu_enhe = w_enh2
     c     fprilu_key    chain     fprilur
      *
     c                   eval      fpfirm = w_firm
5.70 c                   eval      fpprgr = p_prgr
     c                   eval      fprkat = p_rkat
     c                   eval      fpkkat = 0
     c                   eval      fpkund = p_kund
     c                   eval      fpkpro = p_kpro
     c                   eval      fpvare = vvvare
     c                   eval      fpenhe = w_enh2
     c                   eval      fplety = fpwlty
     c                   eval      fpfdat = fpwfda
     c                   eval      fptdat = fpwtda
     c                   eval      fpsapr = w_sap2
     c                   eval      fpkopr = 0
     c                   eval      fprab1 = 0
     c                   eval      fprab2 = 0
     c                   eval      fpnumm = 0
      *
     c                   eval      fpeusr = l_user
     c                   time                    fpedat
     c                   time                    fpetim
      *
     c                   if        %found(fprilu)
6.10 c                   time                    fpedat
6.10 c                   time                    fpetim
6.10 c                   eval      fpeusr = l_user
     c                   update    fprilur
     c                   else
6.10 c                   time                    fpodat
6.10 c                   time                    fpotim
6.10 c                   eval      fpousr = l_user
6.10 c                   time                    fpedat
6.10 c                   time                    fpetim
6.10 c                   eval      fpeusr = l_user
     c                   write     fprilur
     c                   endif
      *
     c                   endif
      *
      * - enhet 3
      *
     c                   if        w_enh3 <> *blank
      *
     c                   clear                   fprilur
      *
     c                   eval      fprilu_enhe = w_enh3
     c     fprilu_key    chain     fprilur
      *
     c                   eval      fpfirm = w_firm
5.70 c                   eval      fpprgr = p_prgr
     c                   eval      fprkat = p_rkat
     c                   eval      fpkkat = 0
     c                   eval      fpkund = p_kund
     c                   eval      fpkpro = p_kpro
     c                   eval      fpvare = vvvare
     c                   eval      fpenhe = w_enh3
     c                   eval      fplety = fpwlty
     c                   eval      fpfdat = fpwfda
     c                   eval      fptdat = fpwtda
     c                   eval      fpsapr = w_sap3
     c                   eval      fpkopr = 0
     c                   eval      fprab1 = 0
     c                   eval      fprab2 = 0
     c                   eval      fpnumm = 0
      *
     c                   eval      fpeusr = l_user
     c                   time                    fpedat
     c                   time                    fpetim
      *
     c                   if        %found(fprilu)
6.10 c                   time                    fpedat
6.10 c                   time                    fpetim
6.10 c                   eval      fpeusr = l_user
     c                   update    fprilur
     c                   else
6.10 c                   time                    fpodat
6.10 c                   time                    fpotim
6.10 c                   eval      fpousr = l_user
6.10 c                   time                    fpedat
6.10 c                   time                    fpetim
6.10 c                   eval      fpeusr = l_user
     c                   write     fprilur
     c                   endif
      *
     c                   endif
     c                   endif
     c     neste_vare    tag
     c                   enddo
      *
      * Sletter rad i arbeidsfil
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for konvertere dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     konv_dato     begsr
      *
     c                   eval      w_dd = d_dd
     c                   eval      w_mm = d_mm
     c                   move      d_aa          w_aa
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å hente nobbpris på valgt dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_nobb     begsr
     c                   eval      w_nopr = 0
     c                   eval      w_gdat = fpwgda
      *
     c                   eval      jvprl1_vare = vvvare
     c                   eval      jvprl1_ldor = fpwldo
     c     jvprl1_key    setll     jvprl1
     c     jvprl1_key    reade     jvprl1
     c                   dow       not %eof
6.38 c                   if        w_gdat >= jxgdat
      *
6.36 c                   if        (jxtdat =  *loval) or
6.36 c                             (jxtdat <> *loval and
6.3i c                             jxtdat >= w_gdat)
     c                   eval      w_nopr = jxpris
     c                   leavesr
     c                   endif
     c                   endif
     c     jvprl1_key    reade     jvprl1
     c                   enddo
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sjekke input
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
     c                   eval      b_ok = *off
      * hvis leverandør er 0
     c                   if        fpwldo  = 0
     c                   leavesr
     c                   endif
      * sjekk at  modul hører til riktig deltakernr
     c                   if        fpwmod  <> 0
     c                   eval      jmodl1_modn = fpwmod
     c     jmodl1_modn   chain     jmodl1
     c                   if        not %found(jmodl1)
     c                   leavesr
     c                   endif
     c*                  if        juldor <> fpwldo
     c*                  leavesr
     c*                  endif
     c                   endif
     c                   eval      b_ok = *on
     c                   endsr


      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
5.70 c                   parm                    p_prgr
     c                   parm                    p_rkat
     c                   parm                    p_kund
     c                   parm                    p_kpro
     c                   parm                    p_slet
      *
      * Key for oppslag på vare
      *
     c     vvarl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl3_nobb
      *
      * Key for oppdatering av spesialpris
      *
     c     fpril3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fpril3_kund
     c                   kfld                    fpril3_kpro
      *
     c     fprilu_key    klist
     c                   kfld                    w_firm
5.70 c                   kfld                    fprilu_prgr
     c                   kfld                    fprilu_rkat
     c                   kfld                    fprilu_kkat
     c                   kfld                    fprilu_kund
     c                   kfld                    fprilu_kpro
     c                   kfld                    fprilu_vare
     c                   kfld                    fprilu_enhe
     c                   kfld                    fprilu_lety
      *
     c     fprilu_key2   klist
     c                   kfld                    w_firm
5.70 c                   kfld                    fprilu_prgr
     c                   kfld                    fprilu_rkat
      *
      * Key for oppdatering av spesialpris notat
      *
     c     fprnlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fprnlu_nkun
     c                   kfld                    fprnlu_nkpr
      *
      * Key for les på vare-enhet
      *
     c     vvenl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
     c                   kfld                    vvenl2_saen
      *
     c     vvenl2_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
      *
      * Key for oppslag på vare-pris
      *
     c     jvprl1_key    klist
     c                   kfld                    jvprl1_vare
     c                   kfld                    jvprl1_ldor
      *
      *
      * Key for oppslag på modul
      *
     c     jmodl1_key    klist
     c                   kfld                    jmodl1_modn
      *
      * Key for oppslag på va vare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare

      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
