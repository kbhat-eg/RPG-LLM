     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : ms703R
      * Beskrivelse . . : Syklisk telling - starte ny telling - i batch
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       131210 bof  Nytt program
6.nu  * 6.30  170614 bof  Endring ihht. nummerutvidelse
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flbchl1    if   e           k disk    rename(lbchpfr:lbchl1r)
     flbchlu    uf a e           k disk    rename(lbchpfr:lbchlur)
     fmthhlu    uf a e           k disk    rename(mthhpfr:mthhlur)
     fltell1    if   e           k disk    rename(ltelpfr:ltell1r)
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av parametre
      *
5.70 d p_firm          s              3  0
5.70 d p_sykl          s              4
5.70 d p_dag           s              2  0
5.70 d p_lage          s              2  0
      *
      * Datastruktur for file-member
      *
     d                 ds
     d d_memb                        10
     d  d_mbok                        1    overlay(d_memb)
     d  d_mfir                        3  0 overlay(d_memb:2)
     d  d_mnum                        6  0 overlay(d_memb:5)
      *
      * Datastruktur for parameterliste
      *
     d                 ds
     d d_prec                       128
     d  d_batc                       10    overlay(d_prec)
     d  d_fvar                       15    overlay(d_prec:11)
     d  d_tvar                       15    overlay(d_prec:26)
     d  d_fogr                        2  0 overlay(d_prec:41)
     d  d_togr                        2  0 overlay(d_prec:43)
     d  d_fhgr                        2  0 overlay(d_prec:45)
     d  d_thgr                        2  0 overlay(d_prec:47)
     d  d_fugr                        3  0 overlay(d_prec:49)
     d  d_tugr                        3  0 overlay(d_prec:52)
     d  d_fpla                        8    overlay(d_prec:55)
     d  d_tpla                        8    overlay(d_prec:63)
     d  d_flag                        2  0 overlay(d_prec:71)
     d  d_tlag                        2  0 overlay(d_prec:73)
     d  d_list                        1  0 overlay(d_prec:75)
     d  d_hovd                        1  0 overlay(d_prec:76)
     d  d_val1                        1  0 overlay(d_prec:77)
     d  d_val2                        1  0 overlay(d_prec:78)
     d  d_val3                        1  0 overlay(d_prec:79)
     d  d_opda                        6  0 overlay(d_prec:80)
     d  d_vtyp                        1    overlay(d_prec:86)
     d  d_pans                        4    overlay(d_prec:87)
     d  d_levr                        6  0 overlay(d_prec:92)
     d  d_sykl                        1  0 overlay(d_prec:98)
      *
      * Definering av variabler i nøkler
      *
     d lbchl1_batc     s                   like(lcbatc)
     d lbchlu_batc     s                   like(lcbatc)
     d ltell1_batc     s                   like(lebatc)
      *
      * Definering av variable
      *
     d w_firm          s                   like(lcfirm)
     d w_batc          s                   like(lcbatc)
     d w_fast          s             10
     d w_fell          s              6
     d w_kode          s              1
     d w_type          s              2
6.nu d w_numm          s              8  0
     d w_numa          s              6
     d w_time_6        s              6  0
     d w_qnnn          s              1
     d w_tell          s              6  0
     d w_telx          s              6
5.70 d w_dag           s              2  0
     d w_bdat_iso      s               d   datfmt(*iso)
     d w_edat_iso      s               d   datfmt(*iso)
     d w_bdat_alf      s              8
     d w_edat_alf      s              8
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     b2tagb        tag
      *
     c                   exsr      behandle
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å starte syklisk telling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandle      begsr
      *
      * Oppdatering
      *
     c                   clear                   lbchlur
      *
      * Bygg nytt batcnummer
      *
     c                   eval      w_fell = p_sykl
     c                   exsr      hntnum
     c                   move      w_numm        w_numa
     c                   eval      w_batc = p_sykl + w_numa
      *
     c                   eval      lbchlu_batc = w_batc
     c     lbchlu_key    chain     lbchlur
      *
      * Hent tellelistenummer ved ny batch
      *
     c                   if        not %found
     c                   eval      w_fell = laatel
     c                   eval      w_kode = '0'
     c                   eval      w_numm = 0
     c                   exsr      hntnum
     c                   eval      lcnumm = w_numm
      *
      *
     c                   time                    w_bdat_iso
     c     *iso0         move      w_bdat_iso    w_bdat_alf
     c                   eval      lcbdat = w_bdat_iso
     c     w_bdat_iso    adddur    w_dag:*D      w_edat_iso
     c                   eval      lcedat = w_edat_iso
     c     *iso0         move      w_edat_iso    w_edat_alf
     c
     c                   eval      lcansv = %trim(w_bdat_alf) + ' ' +
     c                                      %trim(w_edat_alf)
     c                   eval      lcsper = 0
     c                   eval      lcstel = 1
     c                   eval      lcahte = 0
     c                   eval      lclope = 'DIVTEL'
      *
     c                   eval      w_time_6 = 063000
     c     *hms          move      w_time_6      lcbtim
     c                   eval      w_time_6 = 233000
     c     *hms          move      w_time_6      lcetim
      *
6.10 c                   time                    lcodat
6.10 c                   time                    lcotim
6.10 c                   eval      lcousr = l_user
     c                   eval      lcfirm = w_firm
     c                   eval      lcbatc = w_batc
     c                   write     lbchlur
     c                   endif
      *
     c                   exsr      lag_tel
     c                   exsr      lag_hh
      *
     c     end_xc1       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Kaller program for å danne telle liste
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lag_tel       begsr
      *
      *  Legger inn opplysninger i Hjelpe-felter for Parameter
      *
     c                   eval      d_batc = w_batc
     c                   eval      d_sykl = 1
     c                   eval      d_list = 1
     c                   eval      d_hovd = 1
     c                   eval      d_val1 = 0
     c                   eval      d_val2 = 0
     c                   eval      d_val3 = 5
     c                   eval      d_opda = 0
     c                   eval      d_vtyp = 'L'
      *
     c                   movel     '00000000'    d_fvar
     c                   movel     '99999999'    d_tvar
     c                   eval      d_fogr = 0
     c                   eval      d_togr = 99
     c                   eval      d_fhgr = 0
     c                   eval      d_thgr = 99
     c                   eval      d_fugr = 0
     c                   eval      d_tugr = 999
     c                   eval      d_fpla = '        '
     c                   eval      d_tpla = '99999999'
     c                   eval      d_pans = *blank
     c                   eval      d_levr = 0
      *
     c                   eval      d_flag = p_lage
     c                   eval      d_tlag = p_lage
      *
      *  Henter siste nummer for File-member
      *
     c                   call      'LÅ900R'
     c                   parm                    w_qnnn
     c                   parm                    w_numm
      *
     c                   eval      d_mbok = 'K'
     c                   eval      d_mfir = w_firm
     c                   eval      d_mnum = w_numm
      *
      *  Kaller opp program for videre behandling
      *
     c                   call      'LE601CS'
     c                   parm                    d_prec
     c                   parm                    d_memb
      *
     c     end_tel       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Legger ut telling på håndholdt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lag_hh        begsr
      *
      * Sjekk om HH-transer skal legges ut
      *
     c     end_hh        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Henter/Oppdatering av NUMMER-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hntnum        begsr
      *
     c                   eval      w_type = *blank
     c                   eval      w_fast = *blank
     c                   eval      w_numm = *zero
     c                   eval      w_kode = '0'
      *
      *  Kaller opp subprogram for henting av nummer fra NUMMER-REGISTER
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
     c                   parm                    w_numm
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_sykl
     c                   parm                    p_lage
     c                   parm                    p_dag
      *
      * Key for oppdatering av BATCH
      *
     c     lbchlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbchlu_batc
      *
      *  Key for les av LAGER-TELLE
      *
     c     ltell1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ltell1_batc
      *
      *  Key for les av LAGER-KODE
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
     c     lstsl1_key    chain     lstsl1r
      *
     c                   move      p_dag         w_dag
      *
     c                   endsr
