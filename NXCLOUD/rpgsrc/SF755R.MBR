     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASSTAT
      * Program . . . . : SF755R
      * Beskrivelse . . : Statistikk, bytter navn på enhet på vare
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       161003 HKO  Nytt program
8.01  *       030222 ask  Rekompilert pga. lever check på SSTAL2
8.01  *PRG2   220622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner - COMP
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsstal2    uf   e           k disk    rename(sstapfr:sstal2r)
     fsistl2    uf   e           k disk    rename(sistpfr:sistl2r)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(sffirm)
     d p_vare          s                   like(sfvare)
     d p_enhg          s                   like(sfenhe)
     d p_enhn          s                   like(sfenhe)
      *
      * Definering av variable
      *
     d w_firm          s                   like(sffirm)
     d w_vare          s                   like(sfvare)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Salgsstatistikk
      *
     c     key01         setll     sstal2
     c     key01         reade     sstal2r
     c                   dow       not %eof
      *
     c                   if        sfenho = p_enhg
     c                   eval      sfenho = p_enhn
     c                   endif
     c                   if        sfenhe = p_enhg
     c                   eval      sfenhe = p_enhn
     c                   endif
      *
     c                   update    sstal2r
      *
     c     key01         reade     sstal2r
     c                   enddo
      *
      * Innkjøpsstatistikk
      *
     c     key01         setll     sistl2
     c     key01         reade     sistl2r
     c                   dow       not %eof
      *
     c                   if        sivenh = p_enhg
     c                   eval      sivenh = p_enhn
     c                   endif
     c                   if        sioenh = p_enhg
     c                   eval      sioenh = p_enhn
     c                   endif
      *
     c                   update    sistl2r
      *
     c     key01         reade     sistl2r
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_enhg
     c                   parm                    p_enhn
      *
      * Key for oppdatering etter oppslag på vare
      *
     c     key01         klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
      *
      *
      * Henter firma og vare fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_vare = p_vare
      *
     c                   endsr
