     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : AK700R
      * Beskrivelse . . : Oppdatering av poster i kidd-referanse-fil
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- - - - - - - - - - - - - - - - - - - - - - - - - -
      *       111002 AMD  Nytt program
5.41  * R5.41 130206 AMD  Lagt inn oppdatering av egendefinert KIDD
5.41  *                   (Eker/Ikas med fakturanummer som KIDD
6.nu  * R6.30 040614 bsa  Programmet gjennomgått mtp nummerutvidelser.
6.nu  *                   Må utvide parameterliste til AS100R, da nummer
6.nu  *                   er 8 siffer, men for KIDref skal det bare være
6.nu  *                   lov med 6 siffer i retur. Det betyr at vi ikke
6.nu  *                   utvider referansekid med 2 siffer.
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     fakidl1    if   e           k disk    rename(akidpfr:akidl1r)
     fakidlu    uf a e           k disk    rename(akidpfr:akidlur)
     fanumlu    uf a e           k disk    rename(anumpfr:anumlur)
      *
      * Modulus 10 utregninger.
      *
     d a10             s              1    dim(24)
     d m10             s              1  0 dim(24)
     d u10             s              1  0 dim(12)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
      *
      * Definering av parametre
      *
     d p_firm          s                   like(akfirm)
     d p_kidd          s                   like(akkidd)
     d p_kidr          s                   like(akkidr)
     d p_ksif          s                   like(akksif)
5.41 d p_kid2          s                   like(akkidd)
      *
      * Definering av variabler i nøkler
      *
     d akidl1_kidr     s                   like(akkidr)
     d akidlu_kidr     s                   like(akkidr)
     d anumlu_fell     s                   like(anfell)
     d anumlu_type     s                   like(antype)
     d anumlu_fast     s                   like(anfast)
      *
      * Definering av variable
      *
     d w_firm          s                   like(akfirm)
     d w_kode          s              1
     d w_fell          s              6
     d w_type          s              2
     d w_fast          s             10
6.nu d w_numm          s              8  0
     d kidd10          s             24
     d w_ksif          s              1  0
5.41 d w_dato          s               d   datfmt(*iso)
5.41 d w_aarr          s              4  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedrutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Kommentar til programmet :
      * --------------------------
      * Input-parametere til programmet er firma og kiddnummer (langt).
      * Tilbake returneres de samme felter + referansenr og sjekksiffer
      * Nummersystemet er basert på 6 siffer.  Derfor legges sjekk-
      * sifferet i et eget felt.  Referansnummer og sjekksiffer
      * returneres til kallende program.
      *
      *
      *
      *
      * Henter inn nummer
      * fra nummerregister
      *
     c     hent_pany     tag
     c                   exsr      hent_numm
      *
      * Avslutt program dersom
      * feil med nummer-tildeling
      *
     c                   if        w_kode <> '0'
     c                   eval      w_numm =  0
     c                   goto      avslutt
     c                   endif
      *
      * Finnes dette nr
      * fra før i kidd-ref-reg ?
      *
     c                   eval      akidl1_kidr = w_numm
     c     akidl1_key    chain     akidl1r
      *
     c                   if        %found
     c                   goto      hent_pany
     c                   endif
      *
      * Beregne kidd på ref.nr.
      *
     c                   eval      kidd10 = *blank
     c                   move      w_numm        kidd10
     c                   exsr      mod10
     c                   eval      w_ksif = ksif10
      *
      * Oppdater Kidd-referanse-register
      *
     c                   eval      akidlu_kidr = w_numm
     c     akidlu_key    chain     akidlur
      *
     c                   if        not %found
      *
     c                   eval      akfirm = w_firm
     c                   eval      akkidr = w_numm
     c                   eval      akksif = w_ksif
     c                   eval      akkidd = p_kidd
5.41 c                   eval      akkid2 = p_kid2
5.41 c                   eval      akaarr = w_aarr
      *
     c                   time                    akedat
     c                   time                    aketim
     c                   eval      akeusr = l_user
      *
     c                   write     akidlur
     c                   endif
      *
     c                   eval      p_kidr = w_numm
     c                   eval      p_ksif = w_ksif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Henter/Oppdatering av NUMMER-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     csr   hent_numm     begsr
      *
     c                   eval      w_kode = '0'
     c                   eval      w_fell = 'AKIDDR'
     c                   eval      w_type = *blank
     c                   eval      w_fast = *blank
     c                   eval      w_numm = *zero
      *
      *  Kaller opp subprogram for henting av nummer fra NUMMER-REGISTER
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
6.nu c                   parm                    w_numm
      *
     csr                 endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Husk!! inn før /COPY av MODUS10 i ditt program følgende
      * statmemnt:
      *                    MOVE *BLANK    KIDD10 24
      *                    MOVE 'felt'    KIDD10
      *  'felt' må byttes ut med det felt som det i hvert tilfelle
      *         skal beregnes MODULUS 10 av.
      *
      *     Kall av subrutine MOD10 for modulus-10 utregning.
      *
      *                    EXSR MOD10
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Husk!! Felt KSIF10, KIDD10 inneholder h.h.v. sjekksiffer/
      *        utregningssiffer (beløp-kundeident.felt e.l.).
      *        Dersom det i programmet er flere modulus 10 sjekker
      *        må disse feltene bevares i egne opprettede felt, fordi
      *        de vil endres ved hver modulus 10 sjekk da de brukes
      *        omigjen.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     mod10         begsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *        Start subrutinen for modulus-10 utregning.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     C                   Z-ADD     1             XX                2 0                         R2.04
     C                   Z-ADD     2             YY                2 0                         R2.04
     C                   Z-ADD     2             FA10              1 0
     C                   Z-ADD     0             MODS10            3 0
     C                   Z-ADD     0             MODR10            1 0
     C                   Z-ADD     0             KSIF10            1 0
      *
      * På grunn av forskjeller mellom S/36 og AS400
      * må MOVEA gå via et alfa-array før det legges
      * over i et nummerisk array.
      *
     C                   MOVEA     KIDD10        A10                            Tollegg
     C                   MOVE      A10           M10                            Endring
      *
     C     UTR10         TAG
      *
     C     XX            IFGT      12                                                          R2.04
     C                   Z-ADD     1             XX                                            R2.04
     C                   Z-ADD     1             YY                                            R2.04
     C                   GOTO      ADD10
     C                   ELSE
     C     M10(YY)       MULT      FA10          U10(XX)                                       R2.04
     C     M10(YY)       IFGT      4                                                           R2.04
     C                   ADD       1             MODS10
     C                   END
     C                   ADD       1             XX                                            R2.04
     C                   ADD       2             YY                                            R2.04
     C                   GOTO      UTR10
     C                   END
      *
     C     ADD10         TAG
      *
     C     XX            IFGT      12                                                          R2.04
     C                   GOTO      SIF10
     C                   END
     C                   ADD       U10(XX)       MODS10                                        R2.04
     C                   ADD       M10(YY)       MODS10                                        R2.04
     C                   ADD       1             XX                                            R2.04
     C                   ADD       2             YY                                            R2.04
      *
     C                   GOTO      ADD10
      *
     C     SIF10         TAG
     C                   MOVE      MODS10        MODR10
     C     10            SUB       MODR10        KSIF10
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_kidd
     c                   parm                    p_kidr
     c                   parm                    p_ksif
5.41 c                   parm                    p_kid2
      *
      * Key for kidd-referanse-register
      *
     c     akidl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    akidl1_kidr
      *
     c     akidlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    akidlu_kidr
      *
      * Key for file : NUMMER-REGISTER
      *
     c     anumlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    anumlu_fell
     c                   kfld                    anumlu_type
     c                   kfld                    anumlu_fast
      *
      * Legg inn firmanummer i key
      *
     c                   eval      w_firm = p_firm
      *
      * Oppretter post automatisk i nummerregister
      * dersom den ikke er lagt inn
      *
     c                   eval      anumlu_fell = 'AKIDDR'
     c                   eval      anumlu_type = *blank
     c                   eval      anumlu_fast = *blank
     c     anumlu_key    chain     anumlur
      *
     c                   if        %found
     c                   unlock    anumlu
     c                   else
     c                   eval      anfirm = w_firm
     c                   eval      anfell = 'AKIDDR'
     c                   eval      antype = *blank
     c                   eval      anfast = *blank
     c                   eval      antxt1 = 'Kiddreferanse-nummer'
     c                   eval      antxt2 = *blank
     c                   eval      anwrap = '1'
     c                   eval      anÅrnu = '0'
     c                   eval      anauto = 'A'
     c                   eval      annfom = 100
     c                   eval      anntom = 999999
     c                   eval      ansist = 100
     c                   write     anumlur
     c                   endif
5.41  *
5.41  * Henter inn aktuelt årstall
5.41  *
5.41 c                   time                    w_dato
5.41 c                   extrct    w_dato:*y     w_aarr
      *
     c                   endsr
      *
