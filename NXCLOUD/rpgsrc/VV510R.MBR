     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VV510R
      * Beskrivelse . . : Vare, vise - bilde 1
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       111199 HKO  Nytt program
      *       210300 HKO  Lagt inn notat
      *       220300 HKO  Takler DG > 99,99
      *       250300 HKO  Endret parametre ved kall av VV102R
      *       161103 HKO  Fjernet behandling av rabattgruppe
      *       281203 HKO  Lagt inn omregningsfaktor til nominell
      *       011204 HKO  Lagt inn prissammenligningsenhet
      *       130905 HKO  Fjernet ROLLDOWN fra DDS'en
5.70  * PRGR  241008 bof  Utvidet med prisgruppe
      *
6.10  *       021009 bof  Byttet program for å se på ean-nr (GTIN)
6.11  * 6.10  060110 bof  Tatt bort alle lager-relaterte felter
6.11  *                   og la på varesortiment
6.20  * 6.20  010411 bof  Utvidet vvprpf med prisgiver
6.30  * 6.30  080814 bof  Viser erstattet av nobbnr.
6.32  * 6.30  120918 bof  Utvidet med trelast-sortiment
      *
7.xx  * 7.00  110117 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 7.00  271119 bkv  Skal ikke vise prisgruppe blank pris hvis pris med prisgr.
7.02  * 6.30  270616 bsa  Lagt inn F tast for visning av VA info.
      *
8.01  *       221222 bof Hindre for store beløp vises i subfile
8.02  *PRG2   150622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
8.02  *PRG2               Endringer består kun i kompilering, da alle
8.02  *PRG2               referanser er eksternt definert.
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvasl1    if   e           k disk    rename(vvaspfr:vvasl1r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     fvvprl1    if   e           k disk    rename(vvprpfr:vvprl1r)
7.01 fvvprlr    if   e           k disk    rename(vvprpfr:vvprlrr)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fanotl1    if   e           k disk    rename(anotpfr:anotl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
6.30 fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
      *
     fvv510d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vvfirm)
     d p_vare          s                   like(vvvare)
      *
      * Definering av Local data area
      *
     d                uds
7.00 d l_filg                931    933
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variable i nøkler
      *
     d vvprl1_enhe     s                   like(vpenhe)
     d vvprl1_prgr     s                   like(vpprgr)
6.20 d vvprl1_ldor     s                   like(vpldor)
7.01 d vvprlr_prgr     s                   like(vpprgr)
7.01 d vvprlr_ldor     s                   like(vpldor)
7.01 d vvprlr_enhe     s                   like(vpenhe)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d anotl1_syst     s                   like(ausyst)
     d anotl1_noid     s                   like(aunoid)
     d rlevl1_levr     s                   like(rllevr)
6.30 d jvarl1_vare     s                   like(jvvare)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
7.01 d b_prgr          s              1    inz(*off)
      *
     d w_valg          s              1  0
     d w_syst          s              4
      *
     d w_firm          s                   like(vvfirm)
     d w_vare          s                   like(vvvare)
5.70 d w_prgr          s                   like(vpprgr)
      *
     d w_tek1          s                   like(vvtek1)
     d w_kbon          s                   like(vvkbon)
     d w_kord          s                   like(vvkord)
     d w_kenh          s                   like(vvkenh)
     d w_kmva          s                   like(vvkmva)
     d w_kres          s                   like(vvkres)
     d w_kpme          s                   like(vvkpme)
     d w_keti          s                   like(vvketi)
     d w_kpdi          s                   like(vvkpdi)
     d w_kjus          s                   like(vvkjus)
     d w_desi          s                   like(vvdesi)
     d w_kstr          s                   like(vvkstr)
     d w_kabc          s                   like(vvkabc)
     d w_ahgr          s                   like(vvahgr)
     d w_augr          s                   like(vvaugr)
     d w_avar          s                   like(vvavar)
     d w_kfri          s                   like(vvkfri)
     d w_fako          s                   like(vvfako)
     d w_dekn          s                   like(vvdekn)
     d w_omrn          s                   like(vvomrn)
     d w_godj          s                   like(vvgodj)
     d w_khev          s                   like(vvkhev)
6.11 d w_sort          s                   like(vvsort)
7.02 d w_udat          s               d   datfmt(*iso)
7.02 d w_lety          s              1  0
7.02 d w_prig          s                   like(vpprgr)
      *
     d w_lety_gml      s                   like(vplety)
      *
      * Konstanter
      *
     d c_sfil          s              2  0 inz(18)
7.00   dcl-s co402_verdi1  char(1);
7.00   dcl-s co402_verdi2  char(2048);
7.00   DCL-PR CO402R EXTPGM('CO402R');
7.00     p_filgrp            char(3)     const;
7.00     p_firm              packed(3:0) const;
7.00     p_lager             packed(2:0) const;
7.00     p_nokkel            char(50)    const;
7.00     p_verdi1            char(1);
7.00     p_verdi2            char(2048);
7.00   END-PR;
7.01 d u_701           s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierering av skjermbildet
      *
     c     vvarl1_key    chain     vvarl1                             90
     c                   if        *in90 = *on
     c     vvasl1_key    chain     vvasl1                             90
     c                   endif
      *
     c                   if        *in90 = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   exsr      hent_vare
      *
6.30  * henter info fra VA
6.30 c                   eval      jvarl1_vare = vvvare
6.30 c     jvarl1_key    chain     jvarl1
6.30 c                   if        %found(jvarl1)
6.30 c                   eval      b2enob = jvenob
6.30 c                   else
6.30 c                   eval      b2enob = 0
6.30 c                   endif
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *in10
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * F2=Notat
      *
     c                   if        *inkb = *on
     c                   eval      w_syst = 'VVAR'
     c                   call      'AU500R'
     c                   parm                    w_firm
     c                   parm                    w_syst
     c                   parm                    vvvare
     c                   goto      b2tagb
     c                   endif
7.02  *
7.02  * F10=Vis VA info
7.02  *
7.02 c                   if        *inkj = *on
7.02 c                   exsr      vis_VA
7.02 c                   goto      b2tagb
7.02 c                   endif
      *
      * F13=Bilde 2
      *
     c                   eval      w_valg = 5
      *
     c                   if        *inkm = *on
     c                   call      'VV102R'
     c                   parm                    p_firm
     c                   parm                    w_vare
     c                   parm                    w_valg
     c                   parm                    w_tek1
     c                   parm                    w_kbon
     c                   parm                    w_kord
     c                   parm                    w_kenh
     c                   parm                    w_kmva
     c                   parm                    w_kres
     c                   parm                    w_kpme
     c                   parm                    w_keti
     c                   parm                    w_kpdi
     c                   parm                    w_kjus
     c                   parm                    w_desi
     c                   parm                    w_kstr
     c                   parm                    w_kabc
     c                   parm                    w_ahgr
     c                   parm                    w_augr
     c                   parm                    w_avar
     c                   parm                    w_kfri
     c                   parm                    w_fako
     c                   parm                    w_dekn
     c                   parm                    w_omrn
     c                   parm                    w_godj
     c                   parm                    w_khev
6.11 c                   parm                    w_sort
     c                   goto      b2tagb
     c                   endif
      *
      * F14=Enheter
      *
     c                   if        *inkn = *on
     c                   call      'VE510R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   goto      b2tagb
     c                   endif
      *
      * F15=Priser
      *
     c                   if        *inkp = *on
6.20 c                   call      'VP505R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   goto      b2tagb
     c                   endif
      *
      * F16=Søketekster
      *
     c                   if        *inkq = *on
     c                   call      'VS500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   goto      b2tagb
     c                   endif
      *
      * F17=EAN-nummer
      *
     c                   if        *inkr = *on
6.10 c                   call      'VE515R'
6.10 c                   parm                    w_firm
6.10 c                   parm                    w_vare
     c                   goto      b2tagb
     c                   endif
      *
      * F18=Tilbudspriser
      *
     c                   if        *inks = *on
     c                   call      'VT510R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   goto      b2tagb
     c                   endif
6.32  *
6.32  * F21=Overgang til vare-leverandør
6.32  *
6.32 c                   if        *inkv = *on
6.32 c                   call      'VV130R'
6.32 c                   parm                    w_firm
6.32 c                   parm                    w_vare
6.32 c                   goto      b2tagb
6.32 c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av vareinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_vare     begsr
      *
      * Henter vareinformasjon
      *
     c                   eval      b2vare = vvvare
     c                   eval      b2tek1 = vvtek1
     c                   eval      b2tek2 = vvtek2
     c                   eval      b2ogrp = vvogrp
     c                   eval      b2hgrp = vvhgrp
     c                   eval      b2ugrp = vvugrp
     c                   eval      b2ldor = vvldor
     c                   eval      b2lvar = vvlvar
     c                   eval      b2nmod = vvnmod
     c                   eval      b2enps = vvenps
     c                   eval      b2enhs = vvenhs
     c                   eval      b2enhl = vvenhl
     c                   eval      b2enhe = vvenhe
     c                   eval      b2kpri = vvkpri
     c                   eval      b2vtyp = vvvtyp
     c                   eval      b2nobb = vvnobb
     c                   eval      b2nlev = vvnlev
     c                   eval      b2nuda = 0
     c                   eval      b2udat = 0
     c                   eval      b2eusr = vveusr
      *
     c                   if        vvnuda <> *loval
     c     *dmy          move      vvnuda        b2nuda
     c                   endif
      *
     c                   if        vvudat <> *loval
     c     *dmy          move      vvudat        b2udat
     c                   endif
      *
     c     *dmy          move      vvodat        b2odat
     c     *dmy          move      vvedat        b2edat
      *
      * Henter verdier til bilde-2
      *
     c                   eval      w_tek1 = vvtek1
     c                   eval      w_kbon = vvkbon
     c                   eval      w_kord = vvkord
     c                   eval      w_kenh = vvkenh
     c                   eval      w_kmva = vvkmva
     c                   eval      w_kres = vvkres
     c                   eval      w_kpme = vvkpme
     c                   eval      w_keti = vvketi
     c                   eval      w_kpdi = vvkpdi
     c                   eval      w_kjus = vvkjus
     c                   eval      w_desi = vvdesi
     c                   eval      w_kstr = vvkstr
     c                   eval      w_kabc = vvkabc
     c                   eval      w_ahgr = vvahgr
     c                   eval      w_augr = vvaugr
     c                   eval      w_avar = vvavar
     c                   eval      w_kfri = vvkfri
     c                   eval      w_fako = vvfako
     c                   eval      w_dekn = vvdekn
     c                   eval      w_omrn = vvomrn
     c                   eval      w_godj = vvgodj
     c                   eval      w_khev = vvkhev
6.11 c                   eval      w_sort = vvsort
      *
      * Henter beskrivelse av felter i bildet
      *
     c                   exsr      hent_kontr
      *
      * Setter eventuelt notat-flagg
      *
     c                   eval      *in80 = *off
      *
     c                   eval      anotl1_syst = 'VVAR'
     c                   eval      anotl1_noid = vvvare
     c     anotl1_key    chain     anotl1                             91
     c                   if        *in91 = *off
     c                   eval      *in80 = *on
     c                   endif
      *
      * Fyller bildet
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile med priser og reg.linjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
      * Fyller subfile med priser
      * Henter nyeste pris innenfor enhet og leveringstype
      *
     c     vvenl1_key    setll     vvenl1
     c     vvenl1_key    reade     vvenl1                                 91
     c                   dow       *in91 = *off
7.01  *
7.01 c                   if        u_701 = *on
7.01 c                   exsr      sjekk_prisgr
7.01 c                   endif
5.70  *
5.70  * 1. prøve med prisgruppek
5.70  *
5.70 c                   if        w_prgr <> *blank
     c                   eval      w_lety_gml = -1
      *
6.20 c                   eval      vvprl1_ldor = vvldor
5.70 c                   eval      vvprl1_prgr = w_prgr
     c                   eval      vvprl1_enhe = veenhe
     c     vvprl1_key    setll     vvprl1
     c     vvprl1_key    reade     vvprl1                                 92
     c                   dow       *in92 = *off
      *
5.70 c                   if        vplety <> w_lety_gml
8.01  * alle beløp må være max 9999999,99
8.01 c                   if        vpsapr < 1000000 and
8.01 c                             vpinpr < 1000000 and
8.01 c                             vpkopr < 1000000 and
8.01 c                             vpbupr < 1000000
      *
7.01 c                   if        u_701 = *on
7.01 c                   if        vpprgr = *blank
7.01 c                   exsr      sjekk_prisgr
7.01 c                   if        b_prgr = *on
7.01 c                   iter
7.01 c                   endif
7.01 c                   endif
7.01 c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
5.70 c                   eval      b1prgr = vpprgr
     c                   eval      b1enhe = vpenhe
     c                   eval      b1lety = vplety
     c                   eval      b1sapr = vpsapr
     c                   eval      b1kopr = vpkopr
     c                   eval      b1inpr = vpinpr
     c                   eval      b1bupr = vpbupr
     c                   eval      b1omre = veomre
     c                   eval      b1dekn = 0
      *
     c     *dmy          move      vppdat        b1pdat
      *
     c                   exsr      kalk_dekn
      *
     c                   write     b1sfl
      *
8.01 c                   endif
     c                   endif
      *
     c                   eval      w_lety_gml = vplety
      *
     c     vvprl1_key    reade     vvprl1                                 92
     c                   enddo
      *
5.70 c                   endif
5.70  *
5.70  * 2. prøve med prisgruppe like blank
5.70  *
7.01 c                   if        b_prgr = *off or u_701 = *off
      *
5.70 c                   eval      w_lety_gml = -1
5.70  *
6.20 c                   eval      vvprl1_ldor = vvldor
5.70 c                   eval      vvprl1_prgr = *blank
5.70 c                   eval      vvprl1_enhe = veenhe
5.70 c     vvprl1_key    setll     vvprl1
5.70 c     vvprl1_key    reade     vvprl1                                 92
5.70 c                   dow       *in92 = *off
5.70  *
5.70 c                   if        vplety <> w_lety_gml
8.01  * alle beløp må være max 9999999,99
8.01 c                   if        vpsapr < 1000000 and
8.01 c                             vpinpr < 1000000 and
8.01 c                             vpkopr < 1000000 and
8.01 c                             vpbupr < 1000000
5.70  *
7.01 c                   if        u_701 = *on
7.01 c                   if        vpprgr = *blank
7.01 c                   exsr      sjekk_prisgr
7.01 c                   if        b_prgr = *on
7.01 c                   iter
7.01 c                   endif
7.01 c                   endif
7.01 c                   endif
      *
5.70 c                   eval      w_srrn = w_srrn + 1
5.70  *
5.70 c                   eval      b1prgr = vpprgr
5.70 c                   eval      b1enhe = vpenhe
5.70 c                   eval      b1lety = vplety
5.70 c                   eval      b1sapr = vpsapr
5.70 c                   eval      b1kopr = vpkopr
5.70 c                   eval      b1inpr = vpinpr
5.70 c                   eval      b1bupr = vpbupr
5.70 c                   eval      b1omre = veomre
5.70 c                   eval      b1dekn = 0
5.70  *
5.70 c     *dmy          move      vppdat        b1pdat
5.70  *
5.70 c                   exsr      kalk_dekn
5.70  *
5.70 c                   write     b1sfl
5.70  *
8.01 c                   endif
5.70 c                   endif
5.70  *
5.70 c                   eval      w_lety_gml = vplety
5.70  *
5.70 c     vvprl1_key    reade     vvprl1                                 92
5.70 c                   enddo
      *
7.01 c                   endif                                                  b_prgr = *off or u..
      *
     c     vvenl1_key    reade     vvenl1                                 91
     c                   enddo
      *
     c                   eval      *in15 = *on
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
     c                   eval      *in22 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av kontroll-informasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_kontr    begsr
      *
     c                   eval      b2utxt = *blank
     c                   eval      b2lnav = *blank
      *
      * Varegruppe-beskrivelse
      *
     c                   eval      vugrl1_uogr = b2ogrp
     c                   eval      vugrl1_uhgr = b2hgrp
     c                   eval      vugrl1_uugr = b2ugrp
     c     vugrl1_key    chain     vugrl1                             91
     c                   if        *in91 = *off
     c                   eval      b2utxt = vgutxt
     c                   endif
      *
      * Leverandørnavn
      *
     c                   eval      rlevl1_levr = b2ldor
     c     rlevl1_key    chain     rlevl1                             91
     c                   if        *in91 = *off
     c                   eval      b2lnav = rlnavn
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kalkulering av dekningsgrad
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kalk_dekn     begsr
      *
     c                   eval      b1dekn = 0
      *
      * Avbryter dersom salgspris = 0
      *
     c                   if        b1sapr = 0
     c                   goto      end_dekn
     c                   endif
      *
      * Beregner DG utfra kostpris på vare
      *
     c                   if        b1kopr <> 0
     c                   select
     c                   when      (100 - (b1kopr * 100 / b1sapr)) < -99,99
     c                   eval      b1dekn = -99,99
     c                   when      (100 - (b1kopr * 100 / b1sapr)) > 99,99
     c                   eval      b1dekn = 99,99
     c                   other
     c                   eval(h)   b1dekn = 100 - (b1kopr * 100 / b1sapr)
     c                   endsl
     c                   goto      end_dekn
     c                   endif
      *
      * Dersom kostpris = 0 hentes DG fra varen eller undergruppe
      *
     c                   if        w_dekn > 99,99
     c                   eval      b1dekn = 99,99
     c                   else
     c                   eval      b1dekn = w_dekn
     c                   endif
      *
     c                   if        b1dekn = 0
     c                   if        vgudek > 99,99
     c                   eval      b1dekn = 99,99
     c                   else
     c                   eval      b1dekn = vgudek
     c                   endif
     c                   endif
      *
     c     end_dekn      endsr
7.01  *
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  * Subrutine for å sjekke om prisgruppe finnes
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *
7.01 c     sjekk_prisgr  begsr
7.01  *
7.01 c                   eval      b_prgr = *off
7.01 c                   eval      vvprlr_prgr = w_prgr
7.01 c                   eval      vvprlr_enhe = veenhe
7.01 c                   eval      vvprlr_ldor = vvldor
7.01 c     vvprlr_key2   chain     vvprlr
7.01 c                   if        %found(vvprlr)
7.01 c                   eval      b_prgr = *on
7.01 c                   endif
7.01 c                   endsr
      *
7.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.02  * Viser VA info
7.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.02  *
7.02 c     vis_VA        begsr
7.02  *
7.02 c                   eval      w_lety = 0
7.02 c                   eval      w_prig = *blanks
7.02  *
7.02 c                   call      'JV145R'
7.02 c                   parm                    w_firm
7.02 c                   parm                    vvvare
7.02 c                   parm                    w_prig
7.02 c                   parm                    w_lety
7.02 c                   parm                    w_udat
7.02 c                   parm                    vvldor
7.02  *
7.02 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
      *
      * Key for oppslag på slettet vare
      *
     c     vvasl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
      *
      * Key for les på vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
      *
      * Key for les på vare-pris
      *
     c     vvprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
6.20 c                   kfld                    vvprl1_ldor
5.70 c                   kfld                    vvprl1_prgr
     c                   kfld                    vvprl1_enhe
7.01  *
7.01 c     vvprlr_key2   klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    w_vare
7.01 c                   kfld                    vvprlr_ldor
7.01 c                   kfld                    vvprlr_prgr
7.01 c                   kfld                    vvprlr_enhe
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på notat
      *
     c     anotl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    anotl1_syst
     c                   kfld                    anotl1_noid
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
6.30  *
6.30  * Key for oppslag på vare i VA
6.30  *
6.30 c     jvarl1_key    klist
6.30 c                   kfld                    jvarl1_vare
6.30  *
      *
      * Henter firma, vare og valg fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_vare = p_vare
7.02 c                   time                    w_udat
7.01  *
7.01  * Endring 7.01
7.01  *
7.01   CallP CO402R(l_filg:w_firm:0:'IKKE_VIS_PG_BLANK':
7.01                    co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01     u_701 = *on;
7.01   endif;
5.70  *
5.70  * Henter prisgruppe
5.70  *
5.70 c                   call      'VL711R   '
5.70 c                   parm                    w_prgr
      *
      *
     c                   endsr
