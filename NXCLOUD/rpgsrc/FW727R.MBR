     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : FW727R
      * Beskrivelse . . : Oppdaterer fra nobb integrasjonsfil
      *
      *                   Oppdaterer varer, priser, enheter fra Optimera
      *                   Regler :
      *                    - danner priser på alle enheter som ligger på vare-en
      *                    - Prisdato tas fra fila.
      *                    - Forkaster varer med alfa tegn  i varenr.
      *                    - Forkaster varer med feil varegruppe
      *                    - Forkaster varer med feil enhet
5.61  *                    - Nye varer får varetype = 'L'
      *
      *
      * Spesialversjon. : Leser fil på INTEG.DAT format fra Optimera
      *                   Oppdaterer direkte i ADB - registre
      * Tilpasset for . : Optimera
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       090309 bof  Nytt program
5.61  *       171009 bof  Feil-liste på varer som ikke blir lest inn
5.62  *       221009 bof  Heneter pris fra pos. 67 istedet for pos 3
5.63  *       221009 bof  Ekstra test på basisenhet
5.64  *       021209 bof  Ekstra test på for høye priser
5.65  *       041209 bof  Setter prisdato lik dagens dato for å takle
      *                   forskjellige dato på direkte pris og lagerpris
5.66  *       040310 bof  Tar ean-nr også fra enhet
5.67  *       040310 bof  Tar ean-nr også fra pris
5.68  *       010610 bof  Legger ut nobb-lev.også
5.68  *       250610 bof  Antall desimaler lik 2
5.68  *       250610 bof  Legger på pakning kun 1. gang
5.68  *       010710 bof  Hindre oppdatering hvis VVKJUS er satt
5.69  *       110810 bof  Hvis ikke prisendring, så legges ikke pris inn
6.10  *       100311 bof  Utvidet varepris med prisgruppe
6.11  *       220311 bof  Utvidet med vveppf
6.12  *       280411 bof  Utvidet med tilbudspriser
6.13  *       221210 bsa  Utvider GTIN kode til 14, og legger inn prisgruppe
6.20  *       290811 bof  Utvider vvprpf med leverandør
6.21  *       290811 bof  Utvider vtilpf med varegrupper og leverandør
6.22  *       220413 bof  D_EANN må være 13 pos, er ikke utvidet i formatet
8.01  *PRG2   100622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fflvwpf    if   e           k disk
      *
     fvvarlu    uf a e           k disk    rename(vvarpfr:vvarlur)
     fvvenlu    uf a e           k disk    rename(vvenpfr:vvenlur)
6.11 fvveplu    uf a e           k disk    rename(vveppfr:vveplur)
     fvvprlu    uf a e           k disk    rename(vvprpfr:vvprlur)
     fveanlu    uf a e           k disk    rename(veanpfr:veanlur)
6.12 fvtillu    uf a e           k disk    rename(vtilpfr:vtillur)
     fvenhl1    if   e           k disk    rename(venhpfr:venhl1r)
     fvvprl1    if   e           k disk    rename(vvprpfr:vvprl1r)
     fvvenl2    if   e           k disk    rename(vvenpfr:vvenl2r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvvenl5    if   e           k disk    rename(vvenpfr:vvenl5r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
5.63 fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
      *
     ffw727p    o    e             printer
      *
      * Array
      *
     d a_erec          s            297    dim(99)
     d a_prec          s            297    dim(99)
      *
      * Definering av parametre
      *
      *
      * Definering av Local Data Area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Datastrukturer
      *
      * Definering av varerecord
      *
     d                 ds
     d d_vrec                       297
     d  d_nobb                        8  0 overlay(d_vrec:3)
     d  d_modn                        8  0 overlay(d_vrec:12)
     d  d_vgrp                        7    overlay(d_vrec:21)
     d   d_ogrp                       2  0 overlay(d_vgrp)
     d   d_hgrp                       2  0 overlay(d_vgrp:3)
     d   d_ugrp                       3  0 overlay(d_vgrp:5)
     d  d_tek1                       35    overlay(d_vrec:30)
     d  d_tek2                       35    overlay(d_vrec:68)
     d  d_mte1                       35    overlay(d_vrec:106)
     d  d_mte2                       35    overlay(d_vrec:144)
6.22 d  d_eann                       13  0 overlay(d_vrec:181)
     d  d_pvar                       15    overlay(d_vrec:196)
     d  d_lvar                       15    overlay(d_vrec:214)
     d  d_ldor                        6  0 overlay(d_vrec:231)
     d  d_prod                        6  0 overlay(d_vrec:238)
     d  d_adat                        6  0 overlay(d_vrec:247)
     d  d_udat                        6  0 overlay(d_vrec:256)
     d  d_merk                       30    overlay(d_vrec:264)
     d  d_leve_alf                    1    overlay(d_vrec:297)
     d   d_leve                       1  0 overlay(d_leve_alf:1)
      *
      * Definering av pakningsrecord
      *
     d                 ds
     d d_erec                       297
     d  d_pakn                        3    overlay(d_erec:4)
     d  d_enhe                        3    overlay(d_erec:10)
     d  d_bred                        6  0 overlay(d_erec:23)
     d  d_leng                        6  0 overlay(d_erec:30)
     d  d_hoyd                        6  0 overlay(d_erec:37)
     d  d_vekt                        9  0 overlay(d_erec:44)
     d  d_volu                        8  5 overlay(d_erec:54)
     d  d_anta                        8  2 overlay(d_erec:63)
6.22 d  d_eean                       13  0 overlay(d_erec:72)
     d  d_ofak                       10  4 overlay(d_erec:86)
     d  d_edun                       14  0 overlay(d_erec:97)
     d  d_best                        1  0 overlay(d_erec:112)
     d  d_paks                        3    overlay(d_erec:115)
     d  d_antp_alf                    8    overlay(d_erec:120)
     d   d_antp                       8  0 overlay(d_antp_alf:1)
     d  d_psei_alf                    1    overlay(d_erec:129)
     d   d_psei                       1  0 overlay(d_psei_alf:1)
      *
      * Definering av prisrecord
      *
     d                 ds
     d d_prec                       297
     d  d_pris                        9  2 overlay(d_prec:3)
     d  d_gdat                        6  0 overlay(d_prec:15)
     d  d_pldo                        6  0 overlay(d_prec:22)
     d  d_plva                       15    overlay(d_prec:30)
     d  d_ipri                        9  2 overlay(d_prec:47)
     d  d_kpri                        9  2 overlay(d_prec:57)
     d  d_spri                        9  2 overlay(d_prec:67)
     d  d_tdat                        6  0 overlay(d_prec:79)
     d  d_valu                        3    overlay(d_prec:86)
     d  d_ppva                       15    overlay(d_prec:91)
6.22 d  d_pean_alf                   13    overlay(d_prec:108)
6.22 d   d_pean                      13  0 overlay(d_pean_alf:1)
6.12  * kampanje-del
6.12 d  d_v_vkai                      9  2 overlay(d_prec:123)
6.12 d  d_v_fil3                      1    overlay(d_prec:132)
6.12 d  d_v_ifda                      8  0 overlay(d_prec:133)
6.12 d  d_v_fil4                      1    overlay(d_prec:141)
6.12 d  d_v_itda                      8  0 overlay(d_prec:142)
6.12 d  d_v_fil5                      1    overlay(d_prec:150)
6.12 d  d_v_kpri                      9  2 overlay(d_prec:151)
6.12 d  d_v_fil6                      1    overlay(d_prec:160)
6.12 d  d_v_kfda                      8  0 overlay(d_prec:161)
6.12 d  d_v_fil7                      1    overlay(d_prec:169)
6.12 d  d_v_ktda                      8  0 overlay(d_prec:170)
6.12 d  d_v_fil8                      1    overlay(d_prec:178)
6.12 d  d_v_vkaa                      9  2 overlay(d_prec:179)
6.12 d  d_v_fil9                      1    overlay(d_prec:188)
6.12 d  d_v_efda                      8  0 overlay(d_prec:189)
6.12 d  d_v_fil10                     1    overlay(d_prec:197)
6.12 d  d_v_etda                      8  0 overlay(d_prec:198)
6.12 d  d_v_fil11                     1    overlay(d_prec:206)
6.12 d  d_v_kamp                      5    overlay(d_prec:207)
      *
      * Definering av variable i nøkler
      *
     d vvarlu_vare     s                   like(vvvare)
     d vvenlu_vare     s                   like(vevare)
     d vvenlu_enhe     s                   like(veenhe)
6.11 d vveplu_pvar     s                   like(vepvar)
6.11 d vveplu_penh     s                   like(vepenh)
6.11 d vveplu_pldo     s                   like(vepldo)
     d vvenl2_vare     s                   like(vevare)
     d vvenl2_saen     s                   like(vesaen)
     d vvenl5_vare     s                   like(vevare)
     d vvprlu_vare     s                   like(vpvare)
6.20 d vvprlu_ldor     s                   like(vpldor)
6.10 d vvprlu_prgr     s                   like(vpprgr)
     d vvprlu_enhe     s                   like(vpenhe)
     d vvprlu_lety     s                   like(vplety)
     d vvprlu_pdat     s                   like(vppdat)
6.21 d vtillu_ogrp     s                   like(vtogrp)
6.21 d vtillu_hgrp     s                   like(vthgrp)
6.21 d vtillu_ugrp     s                   like(vtugrp)
6.21 d vtillu_ldor     s                   like(vtldor)
6.12 d vtillu_vare     s                   like(vtvare)
6.12 d vtillu_prgr     s                   like(vtprgr)
6.12 d vtillu_lety     s                   like(vtlety)
6.12 d vtillu_fdat     s                   like(vtfdat)
6.12 d vtillu_ftim     s                   like(vtftim)
6.12 d vtillu_tdat     s                   like(vttdat)
6.12 d vtillu_ttim     s                   like(vtttim)
     d vvprl1_vare     s                   like(vpvare)
6.20 d vvprl1_ldor     s                   like(vpldor)
6.10 d vvprl1_prgr     s                   like(vpprgr)
     d vvprl1_enhe     s                   like(vpenhe)
     d vvprl1_lety     s                   like(vplety)
     d vvprl1_pdat     s                   like(vppdat)
     d veanlu_eann     s                   like(vxeann)
5.63 d vvarl1_vare     s                   like(vvvare)
     d jvarl1_vare     s                   like(jvvare)
     d jlevl1_ldor     s                   like(jlldor)
     d venhl1_eenh     s                   like(vaeenh)
     d vogrl1_ogrp     s                   like(vgoogr)
     d vhgrl1_ogrp     s                   like(vgoogr)
     d vhgrl1_hgrp     s                   like(vghhgr)
     d vugrl1_ogrp     s                   like(vgoogr)
     d vugrl1_hgrp     s                   like(vghhgr)
     d vugrl1_ugrp     s                   like(vguugr)
      *
      * Definering av variable
      *
     d b_frst          s              1    inz(*off)
     d b_inlr          s              1
     d b_ok            s              1
     d b_pris          s              1
     d b_inen          s              1    inz(*off)
6.12 d b_kamp          s              1    inz(*off)
      *
     d w_firm          s              3  0
     d w_rect          s              1
     d x               s              2  0
     d w_etel          s              2  0
     d w_ptel          s              2  0
6.12 d w_dato_6        s              6  0
6.12 d w_fdat          s              6  0
6.12 d w_tdat          s              6  0
     d w_dato          s                   like(vppdat)
     d w_enh1          s                   like(veenhe)
     d w_enh2          s                   like(veenhe)
     d w_enh3          s                   like(veenhe)
     d w_enh4          s                   like(veenhe)
     d w_enh5          s                   like(veenhe)
     d w_enhp          s                   like(veenhe)
     d w_enh_000       s                   like(veenhe)
     d w_omr1          s                   like(veomre)
     d w_omr2          s                   like(veomre)
     d w_omr3          s                   like(veomre)
     d w_omr4          s                   like(veomre)
     d w_omr5          s                   like(veomre)
     d w_omre          s                   like(veomre)
     d w_stel          s                   like(vesaen)
     d w_inen          s                   like(veinen)
      *
     d w_gdat          s                   like(vppdat)
     d w_penh          s                   like(vpenhe)
     d b_pdup          s              1
     d w_vare          s                   like(vvvare)
     d w_sapr          s                   like(vpsapr)
     d w_kopr          s                   like(vpkopr)
     d w_inpr          s                   like(vpinpr)
     d w_bupr          s                   like(vpbupr)
     d w_stat          s              1
     d w_timestamp     s             12  0
     d w_pris          s             11  2
8.01 d w_prgr          s              2
      *
     d digits          c                   ' 0123456789'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      b_frst = *on
     c                   eval      w_etel = 0
     c                   eval      w_ptel = 0
     c                   eval      w_enh_000 = *blank
     c                   time                    w_dato
      *
     c                   exsr      hode
      *
      * Leser integrasjonsfil
      *
6.12 c                   eval      b_kamp = *off
      *
     c                   read      flvwpf
     c                   dow       not %eof
      *
     c                   eval      w_rect = %subst(fwirad:1:1)
      *
     c                   if        w_rect = '1' and
     c                             b_frst = *off
     c                   exsr      behandling
     c                   eval      w_enh_000 = *blank
     c                   endif
      *
     c                   select
     c                   when      w_rect = '1'
     c                   eval      d_vrec = fwirad
     c                   eval      b_frst = *off
     c                   when      w_rect = '2'
     c                   eval      w_etel = w_etel + 1
     c                   eval      a_erec(w_etel) = fwirad
     c                   if        %subst(fwirad:4:3) = '000'
     c                   eval      w_enh_000 = %subst(fwirad:10:3)
     c                   endif
     c                   when      w_rect = '3'
     c                   eval      w_ptel = w_ptel + 1
     c                   eval      a_prec(w_ptel) = fwirad
6.12 c                   if        %subst(fwirad:208:5) <> *blank
6.12 c                   eval      b_kamp = *on
6.12 c                   endif
     c                   endsl
      *
     c                   read      flvwpf
     c                   enddo
      *
      * Behandler siste vare-forsendelse
      *
     c                   if        b_frst = *off and
     c                             d_vrec <> *blank
     c                   exsr      behandling
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av vare-forsendelse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
6.12  *
6.12  * kampanje ?
6.12  *
      *
      * Vare
      *
     c                   exsr      vare
      *
     c                   if        b_ok = *off
     c                   goto      end_behandle
     c                   endif
      *
      * Eannr
      *
     c                   exsr      eannr
      *
      * Pakning
      *
     c                   if        w_etel > 0
     c                   exsr      pakning
     c                   endif
      *
      * kampanje ?
      *
6.12 c                   if        b_kamp = *on
6.12 c                   exsr      upd_kamp
6.12 c                   goto      end_behandle
6.12 c                   endif
      *
      * Pris
      *
     c                   if        w_ptel > 0
     c                   exsr      pris
     c                   endif
      *
      * Søketekst
      *
     c                   exsr      sØketekst
      *
     c     end_behandle  tag
      *
     c                   eval      w_etel = 0
     c                   eval      w_ptel = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av varerecord
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     vare          begsr
      *
     c                   eval      w_vare = *blank
     c                   eval      b_ok = *off
      *
      * sjekk om varenr er 0
      *
     c                   if        d_nobb = 0
     c                   eval      d1feil = 'Varenr = 0 '
     c                   eval      w_stat = 'V'
     c                   exsr      feil
     c                   goto      avslutt_vare
     c                   endif
      *
      * sjekk om vare finnes fra før og om den skal prisoppdateres
      *
5.63 c                   movel     d_nobb        vvarl1_vare
5.63 c     vvarl1_key    chain     vvarl1
5.63 c                   if        %found(vvarl1)  and
     c                             vvkjus = 1
     c                   goto      avslutt_vare
     c                   endif
      *
      * Sjekker mulige feilsituasjoner
      *
      * sjekker om varegrupper er gyldig -  overgruppe
     c                   eval      vogrl1_ogrp = d_ogrp
     c     vogrl1_key    chain     vogrl1
     c                   if        not %found(vogrl1)
     c                   eval      d1feil = 'Feil overgruppe ' +
     c                                      %char(d_ogrp)
     c                   eval      w_stat = 'V'
     c                   exsr      feil
     c                   leavesr
     c                   endif
      * overgruppe
     c                   eval      vhgrl1_ogrp = d_ogrp
     c                   eval      vhgrl1_hgrp = d_hgrp
     c     vhgrl1_key    chain     vhgrl1
     c                   if        not %found(vhgrl1)
     c                   eval      d1feil = 'Feil hovedgruppe ' +
     c                                      %char(d_hgrp)
     c                   eval      w_stat = 'V'
     c                   exsr      feil
     c                   leavesr
     c                   endif
      * undergruppe
     c                   eval      vugrl1_ogrp = d_ogrp
     c                   eval      vugrl1_hgrp = d_hgrp
     c                   eval      vugrl1_ugrp = d_ugrp
     c     vugrl1_key    chain     vugrl1
     c                   if        not %found(vugrl1)
     c                   eval      d1feil = 'Feil undergruppe ' +
     c                                      %char(d_ugrp)
     c                   eval      w_stat = 'V'
     c                   exsr      feil
     c                   leavesr
     c                   endif
      *
5.63  * sjekker om basis-enhet er forskjellige fra evt. eksisterende
5.63  * vare
5.63  *
5.63 c                   movel     d_nobb        vvarl1_vare
5.63 c     vvarl1_key    chain     vvarl1
5.63 c                   if        %found(vvarl1)  and
     c                             vvenhe <> w_enh_000
5.63 c                   eval      d1feil = 'Feil basisenhet på fil V ' +
5.63 c                                      w_enh_000
5.63 c                   eval      w_stat = 'V'
5.63 c                   exsr      feil
5.63 c                   leavesr
5.63 c                   endif
      *
      * sjekker om basis-enhet finnes i enhetsregisteret
      *
     c                   eval      venhl1_eenh = w_enh_000
     c     venhl1_key    chain     venhl1
     c                   if        not %found(venhl1)
     c                   eval      d1feil = 'Feil basisenhet på fil E' +
     c                                      w_enh_000
     c                   eval      w_stat = 'V'
     c                   exsr      feil
     c                   leavesr
     c                   endif
      *
      * Finner reskontro leverandør
     c                   eval      jlevl1_ldor = d_ldor
     c     jlevl1_key    chain     jlevl1
     c                   if        not  %found(jlevl1)
     c                   eval      d1feil = 'Feil leverandør  ' +
     c                                      %char(d_ldor)
     c                   eval      w_stat = 'V'
     c                   exsr      feil
     c                   goto      avslutt_vare
     c                   endif
     c                   if        jlenum = 0
     c                   eval      d1feil = 'Leverandør er ikke knytttet ' +
     c                                      %char(d_ldor)
     c                   eval      w_stat = 'V'
     c                   exsr      feil
     c                   goto      avslutt_vare
     c                   endif
      *
     c                   if        d_nobb < 10000000
      * ikke nobbnr.
     c                   movel     d_nobb        w_vare
     c                   eval      d_nobb = 0
     c                   eval      d_modn = 0
      *
     c                   else
      * sannsynligvis nobb-nr
     c                   movel     d_nobb        w_vare
     c                   endif
      *
     c                   if        %check(digits : w_vare) <> 0
     c                   eval      d1feil = 'Ugyldig varenr.        ' +
     c                                      w_vare
     c                   eval      w_stat = 'V'
     c                   exsr      feil
     c                   goto      avslutt_vare
     c                   endif
      *
      * Behandler varerecord
      *
     c                   eval      d_tek1 = %trim(%xlate(x'46':'Æ':d_tek1))
     c                   eval      d_tek1 = %trim(%xlate(x'77':'Ø':d_tek1))
     c                   eval      d_tek1 = %trim(%xlate(x'2A':'Å':d_tek1))
      *
     c                   eval      d_tek2 = %trim(%xlate(x'46':'Æ':d_tek2))
     c                   eval      d_tek2 = %trim(%xlate(x'77':'Ø':d_tek2))
     c                   eval      d_tek2 = %trim(%xlate(x'2A':'Å':d_tek2))
      *
     c                   eval      d_mte1 = %trim(%xlate(x'46':'Æ':d_mte1))
     c                   eval      d_mte1 = %trim(%xlate(x'77':'Ø':d_mte1))
     c                   eval      d_mte1 = %trim(%xlate(x'2A':'Å':d_mte1))
      *
     c                   eval      d_mte2 = %trim(%xlate(x'46':'Æ':d_mte2))
     c                   eval      d_mte2 = %trim(%xlate(x'77':'Ø':d_mte2))
     c                   eval      d_mte2 = %trim(%xlate(x'2A':'Å':d_mte2))
      *
     c                   eval      d_merk = %trim(%xlate(x'46':'Æ':d_merk))
     c                   eval      d_merk = %trim(%xlate(x'77':'Ø':d_merk))
     c                   eval      d_merk = %trim(%xlate(x'2A':'Å':d_merk))
      *
     c                   eval      d_leve_alf = %xlate(' ':'0':d_leve_alf)
     c
     c                   if        w_vare <> *blank
     c                   clear                   vvarlur
      *
     c                   eval      vvarlu_vare = w_vare
     c     vvarlu_key    chain     vvarlur
      *
      * Oppretter eller oppdaterer vare
      *
     c                   eval      vvnobb = d_nobb
     c                   eval      vvnmod = d_modn
     c                   eval      vvogrp = d_ogrp
     c                   eval      vvhgrp = d_hgrp
     c                   eval      vvugrp = d_ugrp
     c                   eval      vvtek1 = d_tek1
     c                   eval      vvtek2 = d_tek2
     c                   eval      vvldor = jlenum
5.68 c                   eval      vvnlev = jlldor
     c                   eval      vvlvar = %trim(d_lvar)
     c                   eval      vvdesi = 2
     c                   eval      vveusr = l_user
      *
     c                   time                    vvedat
     c                   time                    vvetim
      *
     c                   if        %found(vvarlu)
     c                   update    vvarlur
     c                   else
     c                   eval      vvvare  = w_vare
     c                   eval      vvvtyp = 'L'
     c                   time                    vvodat
     c                   write     vvarlur
     c                   endif
      *
     c                   eval      b_ok = *on
      *
     c                   endif
      *
     c     avslutt_vare  endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av pakningsrecord
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     pakning       begsr
      *
     c                   eval      w_stel = 0
     c                   eval      x = 0
     c                   eval      w_inen = 0
     c                   eval      b_inen = *off
      *
     c                   do        w_etel
      *
     c                   eval      x = x + 1
     c                   eval      d_erec = a_erec(x)
      *
     c                   eval      d_antp_alf = %xlate(' ':'0':d_antp_alf)
     c                   eval      d_psei_alf = %xlate(' ':'0':d_psei_alf)
      *
      * sjekker om basis-enhet finnes i enhetsregisteret
      *
     c                   eval      venhl1_eenh = d_enhe
     c     venhl1_key    chain     venhl1
     c                   if        not %found(venhl1)
     c                   eval      d1feil = 'Ugyldig enhet          ' +
     c                                      d_enhe
     c                   eval      w_stat = 'P'
     c                   exsr      feil
     c                   leavesr
     c                   endif
      *
      * Oppdaterer enhetsinformasjon på varen
      *
     c                   if        d_pakn = '000'
     c                   eval      vvarlu_vare = vvvare
     c     vvarlu_key    chain     vvarlur
     c                   eval      vvenhe = d_enhe
     c                   eval      vvenhs = d_enhe
     c                   eval      vvenhl = d_enhe
     c                   update    vvarlur
     c                   eval      w_penh = d_enhe
     c                   endif
      *
     c                   if        d_vekt > 999999
     c                   eval      d_vekt = 1
     c                   endif
      *
     c                   eval      vvenlu_vare = vvvare
     c                   eval      vvenlu_enhe = d_enhe
     c     vvenlu_key    chain     vvenlur
      *
      * finner siste salgsenhet
      * kun 1. gang
     c                   if        d_pakn <> '000' and
     c                             not %found(vvenlu)
     c                   eval      vvenl5_vare = vvvare
     c     vvenl5_key    chain     vvenl5
     c                   if        %found
     c                   if        vesaen < 9
     c                   eval      vesaen = vesaen + 1
     c                   else
     c                   eval      vesaen = 9
     c                   endif
     c                   endif
     c                   endif
     c                   eval      veomre = d_ofak
     c                   eval      vebred = d_bred
     c                   eval      veleng = d_leng
     c                   eval      vehoyd = d_hoyd
     c                   eval      vevekt = d_vekt
     c                   eval      vevolu = d_volu
     c                   eval      veeusr = l_user
     c                   time                    veedat
     c                   time                    veetim
     c                   if        d_best = 1
     c                   eval      b_inen = *on
     c                   eval      w_inen = w_inen + 1
     c                   eval      veinen = w_inen
     c                   endif
      *
      * kun 1. gang
     c                   if        d_pakn = '000'  and
     c                             not %found(vvenlu)
     c                   eval      vesaen = 1
     c                   eval      w_stel = 1
     c                   endif
      *
     c                   if        %found(vvenlu)
     c                   update    vvenlur
      *
     c                   else
     c                   eval      vevare = vvvare
     c                   eval      veenhe = d_enhe
      *
     c                   time                    veodat
      *
     c                   write     vvenlur
     c                   endif
5.66  *
5.66 c                   if        d_eean > 0
5.66 c                   exsr      eann_enhe
5.66 c                   endif
      *
     c                   enddo
      *
      * hvis det ikke er satt minst 1 innkjøpsenhet, så sette
      * innkjøpsenhet lik salgenhet-rekkefølge
      *
     c                   if        b_inen = *off
     c                   eval      vvenl2_vare = vvvare
5.41 c     vvenl2_key2   setll     vvenl2
5.41 c     vvenl2_key2   reade     vvenl2
     c                   dow       not %eof(vvenl2)
      *
     c                   eval      vvenlu_vare = vevare
     c                   eval      vvenlu_enhe = veenhe
     c     vvenlu_key    chain     vvenlur
     c                   if        %found(vvenlu)
     c                   eval      veinen = vesaen
     c                   update    vvenlur
     c                   endif
5.41 c     vvenl2_key2   reade     vvenl2
     c                   enddo
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av prisrecord
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     pris          begsr
      *
     c                   exsr      finn_enhet
      *
     c                   eval      x = 0
      *
     c                   do        w_ptel
      *
     c                   eval      x = x + 1
     c                   eval      d_prec = a_prec(x)
      *
      * sjekker at prisen inneholder minst salgspris og kostpris
      *
     c                   if        d_spri > 0 and
     c                             d_kpri > 0
      *
      * pr. enhet
      *
      *
     c                   if        d_ppva = *blank
     c                   eval      d_ppva = d_plva
     c                   endif
      *
      * bruker ikke dato fra filen. Dato blir beregnet til 01 i måneden
     c*                  if        d_gdat <> 0
     c*    *ymd          move      d_gdat        w_gdat
     c*                  else
     c*                  eval      w_gdat = *loval
     c*                  endif
      *
     c                   if        w_enh1 <> *blank
     c                   eval      w_enhp = w_enh1
     c                   eval      w_omre = w_omr1
     c                   exsr      pris_enhet
     c                   endif
      *
     c                   if        w_enh2 <> *blank
     c                   eval      w_enhp = w_enh2
     c                   eval      w_omre = w_omr2
     c                   exsr      pris_enhet
     c                   endif
      *
     c                   if        w_enh3 <> *blank
     c                   eval      w_enhp = w_enh3
     c                   eval      w_omre = w_omr3
     c                   exsr      pris_enhet
     c                   endif
      *
     c                   if        w_enh4 <> *blank
     c                   eval      w_enhp = w_enh4
     c                   eval      w_omre = w_omr4
     c                   exsr      pris_enhet
     c                   endif
      *
     c                   if        w_enh5 <> *blank
     c                   eval      w_enhp = w_enh5
     c                   eval      w_omre = w_omr5
     c                   exsr      pris_enhet
     c                   endif
5.67  *
5.67  * legger evt. inn ean-nr fra pris-record
5.67  *
5.67 c                   if        d_pean > 0
5.67 c                   exsr      eann_pris
5.67 c                   endif
      *
     c                   endif
      *
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle hver enhet i prisregisteret
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     pris_enhet    begsr
      *
      * sjekker om prisen kan være for høy
      * test salgsris
     c                   if        d_spri > 999999,99
     c                   eval      d1feil = 'For høy salgspris '
     c                   eval      w_stat = 'P'
     c                   exsr      feil
     c                   leavesr
     c                   endif
     c                   eval(h)   w_pris = d_spri * w_omre
     c                   if        w_pris > 999999,99
     c                   eval      d1feil = 'For høy salgspris '
     c                   eval      w_stat = 'P'
     c                   exsr      feil
     c                   leavesr
     c                   endif
      * test kostpris
     c                   if        d_kpri > 999999,99
     c                   eval      d1feil = 'For høy kostpris  '
     c                   eval      w_stat = 'P'
     c                   exsr      feil
     c                   leavesr
     c                   endif
     c                   eval(h)   w_pris = d_kpri * w_omre
     c                   if        w_pris > 999999,99
     c                   eval      d1feil = 'For høy kostpris  '
     c                   eval      w_stat = 'P'
     c                   exsr      feil
     c                   leavesr
     c                   endif
      * test innkjøpspris
     c                   if        d_ipri > 999999,99
     c                   eval      d1feil = 'For høy kostpris  '
     c                   eval      w_stat = 'P'
     c                   exsr      feil
     c                   leavesr
     c                   endif
     c                   eval(h)   w_pris = d_ipri * w_omre
     c                   if        w_pris > 999999,99
     c                   eval      d1feil = 'For høy innk.pris '
     c                   eval      w_stat = 'P'
     c                   exsr      feil
     c                   leavesr
     c                   endif
      *
     c                   eval(h)   w_sapr = d_spri * w_omre
     c                   eval      w_kopr = d_kpri * w_omre
     c                   eval      w_inpr = d_ipri * w_omre
      *
      * sjekker om det er reell  prisendring
      *
     c                   eval      b_pris = *off
      *
     c                   eval      vvprl1_vare = vvvare
6.20 c                   eval      vvprl1_ldor = vvldor
6.13 c                   eval      vvprl1_prgr = w_prgr
     c                   eval      vvprl1_enhe = w_enhp
     c                   eval      vvprl1_lety = 0
     c     vvprl1_key    setll     vvprl1
     c     vvprl1_key    reade     vvprl1                                 90
     c                   dow       *in90 = *off
      *
     c                   eval      b_pris = *on
      *
     c                   if        w_dato >= vppdat
     c                   leave
     c                   endif
      *
     c     vvprl1_key    reade     vvprl1                                 90
     c                   enddo
      *
     c                   if        b_pris = *on and
     c                             vpsapr = w_sapr and
     c                             vpkopr = w_kopr and
     c                             vpinpr = w_inpr
     c                   leavesr
     c                   endif
      *
     c                   clear                   vvprlur
      *
     c                   eval      vvprlu_vare = vvvare
6.20 c                   eval      vvprlu_ldor = vvldor
6.13 c                   eval      vvprlu_prgr = w_prgr
     c                   eval      vvprlu_enhe = w_enhp
     c                   eval      vvprlu_lety = 0
     c                   time                    vvprlu_pdat
     c*    *ymd          move      d_gdat        vvprlu_pdat
     c     vvprlu_key    chain     vvprlu
     c                   eval      vpsapr = w_sapr
     c                   eval      vpkopr = w_kopr
     c                   eval      vpinpr = w_inpr
     c                   eval      vpeusr = l_user
     c                   time                    vpedat
     c                   time                    vpetim
      *
     c                   if        %found
     c                   update    vvprlur
     c                   else
     c                   eval      vpvare = vvvare
6.20 c                   eval      vpldor = vvldor
6.13 c                   eval      vpprgr = w_prgr
     c                   eval      vpenhe = w_enhp
     c                   eval      vplety = 0
     c*    *ymd          move      d_gdat        vppdat
     c                   time                    vppdat
     c                   write     vvprlur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering/oppretting av søketekst på vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sØketekst     begsr
      *
     c                   eval      b_inlr = *on
      *
     c                   call      'VV750R'
     c                   parm                    w_firm
     c                   parm                    vvvare
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering/oppretting av ean nummer på vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     eannr         begsr
      *
5.66 c                   clear                   veanlur
     c                   eval      veanlu_eann = d_eann
     c     veanlu_key    chain     veanlur
     c                   if        not %found
     c                   eval      vxfirm = w_firm
     c                   eval      vxeann = d_eann
     c                   eval      vxvare = vvvare
     c                   eval      vxeusr = l_user
      *
     c                   time                    vxtime
     c                   time                    vxetim
     c                   time                    vxedat
      *
     c                   write     veanlur
     c                   endif
      *
     c                   endsr
5.66  *
5.66  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.66  * Subrutine for oppdatering/oppretting av ean med enhet
5.66  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.66  *
5.66 c     eann_enhe     begsr
6.11  *
6.11  * legges i vare enhet pr. prisgiver hvis enhet <> blank
6.11  *
6.11 c                   if        d_enhe <> *blank
6.11 c                   eval      vveplu_pvar = vvvare
6.11 c                   eval      vveplu_penh = d_enhe
6.11 c                   eval      vveplu_pldo = vvldor
6.11 c     vveplu_key    chain     vveplur
6.11 c                   eval      vepgti = d_eean
6.11 c                   eval      vepbes = d_best
6.11 c                   eval      vepeus = l_user
6.11 c                   time                    vepeda
6.11 c                   time                    vepeti
6.11 c                   if        %found(vveplu)
6.11 c                   time                    vepoda
6.11 c                   time                    vepoti
6.11 c                   eval      vepous = l_user
6.11 c                   update    vveplur
6.11 c                   else
6.11 c                   eval      vepfir = w_firm
6.11 c                   eval      vepvar = vveplu_pvar
6.11 c                   eval      vepenh = vveplu_penh
6.11 c                   eval      vepldo = vveplu_pldo
6.11 c                   write     vveplur
6.11 c                   endif
6.11 c                   exsr      rydd_eann
6.11 c                   else
6.11  *  legges inn hvis enhet ikke er utfylt
5.66 c                   clear                   veanlur
5.66 c                   eval      veanlu_eann = d_eean
5.66 c     veanlu_key    chain     veanlur
5.66 c                   if        not %found
5.66 c                   eval      vxfirm = w_firm
5.66 c                   eval      vxeann = d_eean
6.11 c                   eval      vxenhe = *blank
5.66 c                   eval      vxvare = vvvare
5.66 c                   eval      vxeusr = l_user
5.66  *
5.66 c                   time                    vxtime
5.66 c                   time                    vxetim
5.66 c                   time                    vxedat
5.66  *
5.66 c                   write     veanlur
5.66 c                   endif
6.11  *
6.11 c                   endif
5.66  *
5.66 c                   endsr
6.11  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.11  * Subrutine for å rydde ean-nr på veanpf som ligger på vvenpf
6.11  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.11  *
6.11 c     rydd_eann     begsr
6.11  *
6.11 c                   eval      veanlu_eann = vepgti
6.11 c     veanlu_key    chain     veanlu
6.11 c                   if        %found(veanlu)
6.11 c                   delete    veanlur
6.11 c                   endif
6.11  *
6.11 c                   endsr
5.67  *
5.67  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.67  * Subrutine for oppdatering/oppretting av ean med enhet
5.67  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.67  *
5.67 c     eann_pris     begsr
5.67  *
5.67 c                   clear                   veanlur
5.67 c                   eval      veanlu_eann = d_pean
5.67 c     veanlu_key    chain     veanlur
5.67 c                   if        not %found
5.67 c                   eval      vxfirm = w_firm
5.67 c                   eval      vxeann = d_pean
5.67 c                   eval      vxvare = vvvare
5.67 c                   eval      vxeusr = l_user
5.67  *
5.67 c                   time                    vxtime
5.67 c                   time                    vxetim
5.67 c                   time                    vxedat
5.67  *
5.67 c                   write     veanlur
5.67 c                   endif
5.67  *
5.67 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne inntil tre salgsenheter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_enhet    begsr
      *
      * Finner tre salgsenheter på varen
      *
     c                   eval      w_enh1 = *blank
     c                   eval      w_enh2 = *blank
     c                   eval      w_enh3 = *blank
     c                   eval      w_enh4 = *blank
     c                   eval      w_enh5 = *blank
     c                   eval      w_omr1 = 0
     c                   eval      w_omr2 = 0
     c                   eval      w_omr3 = 0
     c                   eval      w_omr4 = 0
     c                   eval      w_omr5 = 0
      *
     c                   eval      vvenl2_vare = vvvare
     c                   eval      vvenl2_saen = 1
     c     vvenl2_key    setll     vvenl2
5.41 c     vvenl2_key2   reade     vvenl2
5.41 c                   dow       not %eof and
5.41 c                             vesaen > 0
      *
     c                   select
     c                   when      w_enh1 = *blank
     c                   eval      w_enh1 = veenhe
     c                   eval      w_omr1 = veomre
     c                   when      w_enh1 <> *blank and
     c                             w_enh2 = *blank
     c                   eval      w_enh2 = veenhe
     c                   eval      w_omr2 = veomre
     c                   when      w_enh1 <> *blank and
     c                             w_enh2 <> *blank and
     c                             w_enh3 = *blank
     c                   eval      w_enh3 = veenhe
     c                   eval      w_omr3 = veomre
     c                   when      w_enh1 <> *blank and
     c                             w_enh2 <> *blank and
     c                             w_enh3 <> *blank and
     c                             w_enh4 = *blank
     c                   eval      w_enh4 = veenhe
     c                   eval      w_omr4 = veomre
     c                   when      w_enh1 <> *blank and
     c                             w_enh2 <> *blank and
     c                             w_enh3 <> *blank and
     c                             w_enh4 <> *blank and
     c                             w_enh5 = *blank
     c                   eval      w_enh5 = veenhe
     c                   eval      w_omr5 = veomre
     c                   leave
     c                   endsl
      *
5.41 c     vvenl2_key2   reade     vvenl2
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å skrive ut feil-liste
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     feil          begsr
      *
      *
     c                   eval      d1stat = w_stat
     c                   eval      d1tek1 = d_tek1
     c                   movel     d_nobb        d1vare
      *
      * Skriv detaljlinje
      *
     c                   write     d1dtl                                30
     c                   exsr      overflow
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering og utskrift av hode-opplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode          begsr
      *
      * Legger inn arb.stasjon og bruker i faste felter
      *
     c                   eval      a1firm = w_firm
     c                   eval      a1user = l_user
     c                   eval      a1wsid = l_wsid
     c                   time                    w_timestamp
     c                   move      w_timestamp   a1date
     c                   movel     w_timestamp   a1time
      *
      * Henter firmaopplysninger
      *
     c     afirl1_key    chain     afirl1r                            90
     c                   eval      a1fnav = aafnav
      *
      * Skriver heading
      *
     c                   write     a1hdr
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av overflow
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     overflow      begsr
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
      *
     c                   endsr
6.12  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.12  * Subrutine for å oppdatere kampanje hode med fra - til dato
6.12  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.12  *
6.12 c     upd_kamp      begsr
      *
     c                   eval      x = 0
      *
     c                   do        w_ptel
      *
     c                   eval      x = x + 1
     c                   eval      d_prec = a_prec(x)
      *
6.12  *
6.12 c                   move      d_v_efda      w_dato_6
6.12 c     *ymd          test(d)                 w_dato_6               33
6.12 c                   if        *in33 = *on
6.12 c                   leavesr
6.12 c                   endif
6.12 c                   eval      w_fdat = w_dato_6
6.12 c                   move      d_v_etda      w_dato_6
6.12 c     *ymd          test(d)                 w_dato_6               33
6.12 c                   if        *in33 = *on
6.12 c                   leavesr
6.12 c                   endif
6.12 c                   eval      w_tdat = w_dato_6
6.12  *
6.12 c                   clear                   vtillur
6.21 c                   eval      vtillu_ogrp = 0
6.21 c                   eval      vtillu_hgrp = 0
6.21 c                   eval      vtillu_ugrp = 0
6.21 c                   eval      vtillu_ldor = 0
6.12 c                   eval      vtillu_vare = vvvare
6.12 c                   eval      vtillu_prgr = *blank
6.12 c                   eval      vtillu_lety = 0
6.12 c     *ymd          move      w_fdat        vtillu_fdat
6.12 c                   eval      vtillu_ftim = *loval
6.12 c     *ymd          move      w_tdat        vtillu_tdat
6.12 c                   eval      vtillu_ttim = *hival
6.12 c     vtillu_key    chain     vtillur
6.12  *
6.12 c                   eval      vtkamp = d_v_kamp
6.12 c                   eval      vtenh1 = w_enh_000
6.12 c                   eval      vtkop1 = d_v_vkai
6.12 c                   eval      vttip1 = d_v_vkaa
6.12 c                   eval      vteusr = l_user
6.12 c                   time                    vtedat
6.12 c                   time                    vtetim
6.12  *
6.12 c                   if        not %found(vtillu)
6.12 c                   eval      vtfirm = w_firm
6.21 c                   eval      vtogrp = vtillu_ogrp
6.21 c                   eval      vthgrp = vtillu_hgrp
6.21 c                   eval      vtugrp = vtillu_ugrp
6.21 c                   eval      vtldor = vtillu_ldor
6.12 c                   eval      vtvare = vtillu_vare
6.12 c                   eval      vtprgr = vtillu_prgr
6.12 c                   eval      vtlety = vtillu_lety
6.12 c                   eval      vtfdat = vtillu_fdat
6.12 c                   eval      vtftim = vtillu_ftim
6.12 c                   eval      vttdat = vtillu_tdat
6.12 c                   eval      vtttim = vtillu_ttim
6.12 c                   eval      vtousr = l_user
6.12 c                   time                    vtodat
6.12 c                   time                    vtotim
6.12 c                   write     vtillur
6.12 c                   else
6.12 c                   update    vtillur
6.12 c                   endif
      *
     c                   enddo
6.12  *
6.12 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *
      * Key for oppdatering av vare
      *
     c     vvarlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlu_vare
      *
      * Key for oppdatering av vare-pakning
      *
     c     vvenlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenlu_vare
     c                   kfld                    vvenlu_enhe
6.11  *
6.11  * Key for oppdatering av vare-pakning-prisgiver
6.11  *
6.11 c     vveplu_key    klist
6.11 c                   kfld                    w_firm
6.11 c                   kfld                    vveplu_pvar
6.11 c                   kfld                    vveplu_penh
6.11 c                   kfld                    vveplu_pldo
6.12  *
6.12  * Key for oppdatering av tilbud
6.12  *
6.12 c     vtillu_key    klist
6.12 c                   kfld                    w_firm
6.21 c                   kfld                    vtillu_ogrp
6.21 c                   kfld                    vtillu_hgrp
6.21 c                   kfld                    vtillu_ugrp
6.21 c                   kfld                    vtillu_ldor
6.12 c                   kfld                    vtillu_vare
6.12 c                   kfld                    vtillu_prgr
6.12 c                   kfld                    vtillu_lety
6.12 c                   kfld                    vtillu_fdat
6.12 c                   kfld                    vtillu_ftim
6.12 c                   kfld                    vtillu_tdat
6.12 c                   kfld                    vtillu_ttim
      *
      * Key for les på vare-enhet
      *
     c     vvenl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
     c                   kfld                    vvenl2_saen
      *
      * Key for les på vare-enhet
      *
     c     vvenl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl5_vare
5.41  *
5.41  * Key2 for les på vare-enhet
5.41  *
5.41 c     vvenl2_key2   klist
5.41 c                   kfld                    w_firm
5.41 c                   kfld                    vvenl2_vare
      *
      * Key for oppdatering av vare-pris
      *
     c     vvprlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvprlu_vare
6.20 c                   kfld                    vvprlu_ldor
6.13 c                   kfld                    vvprlu_prgr
     c                   kfld                    vvprlu_enhe
     c                   kfld                    vvprlu_lety
     c                   kfld                    vvprlu_pdat
      *
      * Key for les av vare-pris
      *
     c     vvprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvprl1_vare
6.20 c                   kfld                    vvprl1_ldor
6.13 c                   kfld                    vvprl1_prgr
     c                   kfld                    vvprl1_enhe
     c                   kfld                    vvprl1_lety
      *
      * Key for oppdatering av ean-register
      *
     c     veanlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    veanlu_eann
      *
      * Key for oppdatering av enhetsregister
      *
     c     venhl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    venhl1_eenh
      *
      * Key for les av varegrupper
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_ogrp
     c                   kfld                    vugrl1_hgrp
     c                   kfld                    vugrl1_ugrp
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_ogrp
     c                   kfld                    vhgrl1_hgrp
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_ogrp
      *
      * Key for les av vare på VA
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
5.63  *
5.63  * Key for les av vare
5.63  *
5.63 c     vvarl1_key    klist
5.63 c                   kfld                    w_firm
5.63 c                   kfld                    vvarl1_vare
      *
      * Key for les av leverandør i VA
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for oppslag på firmaregister
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      * Henter firma fra LDA
     c                   eval      w_firm = l_firm
6.13  * Henter prisgruppe
6.13 c                   call      'VL711R    '
6.13 c                   parm                    w_prgr
      *
      *
     c                   endsr
