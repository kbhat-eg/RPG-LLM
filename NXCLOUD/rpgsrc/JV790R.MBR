     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JV790R
      * Beskrivelse . . : Vare, oppdaterer søketekst
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       300505 HKO  Nytt program
5.60  *       150808 bof  Tar med ean-nr fra jvpkpf for frisøk
6.10  *       190809 bof  Fjernet eannr fra vare-pris JVPRPF
6.11  *       190809 bof  Endret JWEANN til JWGTIN
6.12  *       210909 bsa  GTIN nummer er flyttet til nytt register. Dette
6.12  *                   reverserer mye av endringene over. 5.60 + 6.11
6.13  *       120510 bof  gjeninnført jxeann inntil videre - skal utgå senere
6.20  *       050314 bkv  lagt inn siste kall til JS700R for å lukke filer
6.30  *       050314 bof  fjernet behandling rundt jvpppf, jvpkpf har gtin nå
6.32  * 6.30  270314 bof  test om varepris VA er utgått
6.33  * 6.30  140714 bof  Takle priser pr. dato
6.34  * 6.30  170714 bof  Ryddet i felter fra vare-pris, lagt inn vare-leverandør
6.35  * 6.30  020816 bof  Feilretting vedr. test på til prisdato.
6.36  * 6.30  180317 bof  Feilretting vedr. lev.varenr til frisøk
6.37  * 6.30  121118 bof  Logistikk-varenr. Hvis prod.varenr = log.varenr ikke oppdater.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fjprol1    if   e           k disk    rename(jpropfr:jprol1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fjmodl1    if   e           k disk    rename(jmodpfr:jmodl1r)
     fjmstl1    if   e           k disk    rename(jmstpfr:jmstl1r)
     fjsokl1    if   e           k disk    rename(jsokpfr:jsokl1r)
     fjvprl1    if   e           k disk    rename(jvprpfr:jvprl1r)
6.30 fjvpkl1    if   e           k disk    rename(jvpkpfr:jvpkl1r)
6.34 fjvlel1    if   e           k disk    rename(jvlepfr:jvlel1r)
6.37 fjvkhl1    if   e           k disk    rename(jvkhpfr:jvkhl1r)
6.37 fjvkll1    if   e           k disk    rename(jvklpfr:jvkll1r)
     fjvsoiu    uf a e           k disk    rename(jvsost:jvsoiur)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vgufir)
     d p_vare          s                   like(jvvare)
     d p_inlr          s              1
      *
      * Definering av variable i nøkler
      *
     d jvarl1_vare     s                   like(jvvare)
     d jlevl1_ldor     s                   like(jlldor)
     d jprol1_prod     s                   like(jqprod)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d jmodl1_modn     s                   like(jumodn)
     d jmstl1_smod     s                   like(jusmod)
     d jsokl1_vare     s                   like(jsvare)
     d jsokl1_okod     s                   like(jsokod)
     d jvprl1_vare     s                   like(jxvare)
6.30 d jvpkl1_vare     s                   like(jwvare)
6.34 d jvlel1_lvnr     s                   like(jvlvnr)
     d jvsoiu_svar     s                   like(jvsvar)
6.37 d jvkhl1_kvnr     s                   like(jvkvnr)
6.37 d jvkll1_qvnr     s                   like(jvqvnr)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vgufir)
     d w_stek          s                   like(jvstek)
6.32 d w_udat          s                   like(jxtdat)
6.32 d b_logi          s              1    inz(*off)
      *
     d s_navn          s                   like(jlnavn)
     d s_sord          s                   like(jusord)
     d s_ldor          s                   like(jxldor)
6.34 d s_lvar          s                   like(jvllva)
6.30 d s_eann          s                   like(jwgtin)
      *
      * Definering av konstanter
      *
     d Lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d Up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
      * Initierer arbeidsvariable
      *
     c                   eval      s_navn = *blank
     c                   eval      s_sord = *blank
     c                   eval      s_ldor = 0
     c                   eval      s_lvar = *blank
     c                   eval      s_eann = 0
      *
     c                   eval      w_stek = *blank
      *
      * Oppdaterer søketekst
      *
     c                   eval      jvarl1_vare = p_vare
     c     jvarl1_key    chain     jvarl1
     c                   if        %found
     c                   exsr      oppdater
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   if        p_inlr <> *on
6.20  * Kaller program for oppdatering av søketekst-register. Lukker filer
6.20 c                   eval                    jvvare = ''
6.20 c                   eval                    jvmodn = 0
6.20 c                   eval                    jvprod = 0
6.20 c                   eval                    jvldor = 0
6.20 c                   call      'JS700R'
6.20 c                   parm                    jvvare
6.20 c                   parm                    jvmodn
6.20 c                   parm                    jvtek1
6.20 c                   parm                    jvprod
6.20 c                   parm                    jvldor
6.20 c                   parm                    p_inlr

     c                   eval      *inlr = *on
     c                   endif
      *
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer søketekst
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdater      begsr
      *
6.37  * sjekk om det er logistikkvare
6.37 c                   eval      b_logi = *off
6.37 c                   eval      jvkhl1_kvnr = p_vare
6.37 c     jvkhl1_key    chain     jvkhl1
6.37 c                   if        %found(jvkhl1)
6.37 c                   eval      b_logi = *on
     c                   endif
      *
      * Varetekst-1
      *
     c                   eval      w_stek = jvtek1
      *
      * Varetekst-2
      *
     c                   if        jvtek2 <> *blank
     c                   eval      w_stek = %trimr(w_stek) + ' ' +
     c                                      %trim(jvtek2)
     c                   endif
      *
6.37 c                   if        b_logi = *off
      *
      * Leverandørnavn
      *
     c                   if        jvldor <> 0
     c                   eval      jlevl1_ldor = jvldor
     c     jlevl1_key    chain     jlevl1
     c                   if        %found and
     c                             jlnavn <> *blank
     c                   eval      s_navn = jlnavn
     c                   eval      jlnavn = %xlate(Lo:Up:jlnavn)
     c                   eval      w_stek = %trimr(w_stek) + ' ' +
     c                                      %trim(jlnavn)
     c                   endif
     c                   endif
6.37 c                   endif
      *
      * Produsentnavn
      *
6.37 c                   if        b_logi = *off
     c                   if        jvprod <> 0
     c                   eval      jprol1_prod = jvprod
     c     jprol1_key    chain     jprol1
     c                   if        %found and
     c                             jqnavn <> *blank and
     c                             jqnavn <> jlnavn
     c                   eval      jqnavn = %xlate(Lo:Up:jqnavn)
     c                   eval      w_stek = %trimr(w_stek) + ' ' +
     c                                      %trim(jqnavn)
     c                   endif
     c                   endif
6.37 c                   endif
      *
      * Undergruppetekst
      *
     c                   eval      vugrl1_uogr = jvogrp
     c                   eval      vugrl1_uhgr = jvhgrp
     c                   eval      vugrl1_uugr = jvugrp
     c     vugrl1_key    chain     vugrl1
     c                   if        %found and
     c                             vgutxt <> *blank
     c                   eval      vgutxt = %xlate(Lo:Up:vgutxt)
     c                   eval      w_stek = %trimr(w_stek) + ' ' +
     c                                      %trim(vgutxt)
     c                   endif
      *
      * Modultekst/Modulstikkord
      *
     c                   if        jvmodn <> 0
      *
     c                   eval      jmodl1_modn = jvmodn
     c     jmodl1_key    chain     jmodl1
     c                   if        %found and
     c                             jumte1 <> *blank
     c                   eval      jumte1 = %xlate(Lo:Up:jumte1)
     c                   eval      w_stek = %trimr(w_stek) + ' ' +
     c                                      %trim(jumte1)
     c                   endif
      *
     c                   eval      jmstl1_smod = jvmodn
     c     jmstl1_key    setll     jmstl1
     c     jmstl1_key    reade     jmstl1
     c                   dow       not %eof
     c                   if        jusord <> vgutxt and
     c                             jusord <> jumte1 and
     c                             jusord <> s_sord
     c                   eval      s_sord = jusord
     c                   eval      jusord = %xlate(Lo:Up:jusord)
     c                   eval      w_stek = %trimr(w_stek) + ' ' +
     c                                      %trim(jusord)
     c                   endif
     c     jmstl1_key    reade     jmstl1
     c                   enddo
      *
     c                   endif
      *
      * Varenr
      *
     c                   eval      w_stek = %trimr(w_stek) + ' ' + jvvare
      *
      * Nobbnr
      *
     c                   if        jvnobb <> 0 and
     c                             %char(jvnobb) <> jvvare
     c                   eval      w_stek = %trimr(w_stek) + ' ' +
     c                                      %trim(%editc(jvnobb:'Z'))
     c                   endif
6.37  * hvis logistikk-vare - legg inn alle undernobbnr her
6.37 c                   if        b_logi = *on
6.37 c                   eval      jvkll1_qvnr = p_vare
6.37 c     jvkll1_key    setll     jvkll1
6.37 c     jvkll1_key    reade     jvkll1
6.37 c                   dow       not %eof(jvkll1)
6.37 c                   if        jvquno <> 0 and
6.37 c                             %char(jvquno) <> jvvare
6.37 c                   eval      w_stek = %trimr(w_stek) + ' ' +
6.37 c                                      %trim(%editc(jvquno:'Z'))
6.37 c                   endif
6.37 c     jvkll1_key    reade     jvkll1
6.37 c                   enddo
6.37 c                   endif
     c
      *
      * Modulnr
      *
     c                   if        jvmodn <> 0
     c                   eval      w_stek = %trimr(w_stek) + ' ' +
     c                                      %trim(%editc(jvmodn:'Z'))
     c                   endif
      *
      * Lev. varenr
      *
6.34 c*                  if        jvlvar <> *blank
6.34 c*                  eval      w_stek = %trimr(w_stek) + ' ' + jvlvar
6.34 c*                  endif
      *
      * Prod. varenr
      *
6.36 c                   if        jvpvar <> *blank
6.37  *
6.37 c                   if        b_logi = *off
6.36 c                   eval      w_stek = %trimr(w_stek) + ' ' + jvpvar
     c                   endif
6.37 c                   endif
      *
      * EAN-nr
      *
     c                   if        jvean1 <> 0
     c                   eval      w_stek = %trimr(w_stek) + ' ' +
     c                                      %trim(%editc(jvean1:'Z'))
     c                   endif
      *
      * Egne søketekster
      *
     c                   eval      jsokl1_vare = jvvare
     c                   eval      jsokl1_okod = 'L'
     c     jsokl1_key    setll     jsokl1
     c     jsokl1_key    reade     jsokl1
     c                   dow       not %eof
     c                   eval      w_stek = %trimr(w_stek) + ' ' +
     c                                      %trim(jsstek)
     c     jsokl1_key    reade     jsokl1
     c                   enddo
      *
      * Navn, Lev.varenr og EAN-nr på alle prisgivende leverandører
      *
6.36 c*                  eval      jvprl1_vare = jvvare
6.36 c*    jvprl1_key    setll     jvprl1
6.36 c*    jvprl1_key    reade     jvprl1
6.36 c*                  dow       not %eof
6.36 c*                  if        w_udat >= jxgdat
6.36 c*                  if        (jxtdat =  *loval) or
6.36 c*                            (jxtdat <> *loval and
6.36 c*                            jxtdat >= w_udat)
      *
6.36 c*                  if        jxldor <> jvldor and
6.36 c*                            jxldor <> s_ldor
      *
6.36 c*                  eval      jlevl1_ldor = jxldor
6.36 c*    jlevl1_key    chain     jlevl1
6.36 c*                  if        %found and
6.36 c*                            jlnavn <> *blank and
6.36 c*                            jlnavn <> s_navn
6.36 c*                  eval      jlnavn = %xlate(Lo:Up:jlnavn)
6.36 c*                  eval      w_stek = %trimr(w_stek) + ' ' +
6.36 c*                                     %trim(jlnavn)
6.36 c*                  endif
      *
6.34 c*                  if        jxlvar <> *blank and
6.34 c*                            jxlvar <> jvlvar and
6.34 c*                            jxlvar <> s_lvar
6.34 c*                  eval      w_stek = %trimr(w_stek) + ' ' + jxlvar
6.34 c*                  endif
6.13  *
6.34 c*                  if        jxeann <> 0 and
6.34 c*                            jxeann <> jvean1 and
6.34 c*                            jxeann <> s_eann
6.34 c*                  eval      w_stek = %trimr(w_stek) + ' ' +
6.34 c*                                     %trim(%editc(jxeann:'Z'))
6.34 c*                  endif
      *
      *
6.36 c*                  eval      s_ldor = jxldor
6.34 c*                  eval      s_lvar = jxlvar
6.36 c*                  eval      s_eann = jxeann
      *
6.36 c*                  endif
      *
6.36 c*                  endif
6.36 c*                  endif
6.36 c*    jvprl1_key    reade     jvprl1
6.36 c*                  enddo
6.34  *
6.34  * Lev.varenr fra nytt register Vare-leverandør
6.34  *
6.34 c                   eval      jvlel1_lvnr = jvvare
6.34 c     jvlel1_key    setll     jvlel1
6.34 c     jvlel1_key    reade     jvlel1
6.34 c                   dow       not %eof
6.34  *
6.36 c                   if        jvluda = *loval or
6.36 c                             w_udat >= jvluda
6.36  *
6.36 c                   eval      jlevl1_ldor = jvlldo
6.36 c     jlevl1_key    chain     jlevl1
6.36 c                   if        %found and
6.36 c                             jlnavn <> *blank and
6.36 c                             jlnavn <> s_navn
6.36 c                   eval      jlnavn = %xlate(Lo:Up:jlnavn)
6.36 c                   eval      w_stek = %trimr(w_stek) + ' ' +
6.36 c                                      %trim(jlnavn)
6.36 c                   endif
6.36 c
6.34 c                   if        jvllva <> *blank and
6.36 c                             jvllva <> jvpvar and
6.36 c                             jvllva <> s_lvar
6.34 c                   eval      w_stek = %trimr(w_stek) + ' ' +
6.36 c                                      %trim(jvllva)
6.34 c                   endif
6.36 c                   endif
6.34  *
6.34 c                   eval      s_lvar = jvllva
6.34  *
6.34 c     jvlel1_key    reade     jvlel1
6.34 c                   enddo
      *
5.60  *
6.12  * EAN-nr fra nytt register
5.60  *
6.30 c                   eval      jvpkl1_vare = jvvare
6.30 c     jvpkl1_key    setll     jvpkl1
6.30 c     jvpkl1_key    reade     jvpkl1
5.60 c                   dow       not %eof
5.60  *
6.30 c                   if        jwgtin <> 0 and
6.30 c                             jwgtin <> jvean1 and
6.30 c                             jwgtin <> s_eann
5.60 c                   eval      w_stek = %trimr(w_stek) + ' ' +
6.30 c                                      %trim(%editc(jwgtin:'Z'))
5.60 c                   endif
5.60  *
6.30 c                   eval      s_eann = jwgtin
5.60  *
6.30 c     jvpkl1_key    reade     jvpkl1
5.60 c                   enddo
      *
      * Oppdaterer søketekst
      *
     c                   eval      jvsoiu_svar = jvvare
     c     jvsoiu_key    chain     jvsoiur
     c                   eval      jvstek = w_stek
     c                   if        %found
     c                   update    jvsoiur
     c                   else
     c                   eval      jvsvar = jvvare
     c                   write     jvsoiur
     c                   endif
      *
      * Kaller program for oppdatering av søketekst-register
      *
     c                   call      'JS700R'
     c                   parm                    jvvare
     c                   parm                    jvmodn
     c                   parm                    jvtek1
     c                   parm                    jvprod
     c                   parm                    jvldor
     c                   parm                    p_inlr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_inlr
      *
      * Key for oppslag på vare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for oppslag på produsent
      *
     c     jprol1_key    klist
     c                   kfld                    jprol1_prod
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag på modul
      *
     c     jmodl1_key    klist
     c                   kfld                    jmodl1_modn
      *
      * Key for oppslag på modulstikkord
      *
     c     jmstl1_key    klist
     c                   kfld                    jmstl1_smod
      *
      * Key for les på søketekst
      *
     c     jsokl1_key    klist
     c                   kfld                    jsokl1_vare
     c                   kfld                    jsokl1_okod
      *
      * Key for les på vare-pris
      *
     c     jvprl1_key    klist
     c                   kfld                    jvprl1_vare
5.60  *
5.60  * Key for les på pakningsregister
5.60  *
6.30 c     jvpkl1_key    klist
6.30 c                   kfld                    jvpkl1_vare
6.34  *
6.34  * Key for les på pakningsregister
6.34  *
6.34 c     jvlel1_key    klist
6.34 c                   kfld                    jvlel1_lvnr
6.37  *
6.37  * Key for les av logistikkvare
6.37  *
6.37 c     jvkhl1_key    klist
6.37 c                   kfld                    w_firm
6.37 c                   kfld                    jvkhl1_kvnr
6.37 c     jvkll1_key    klist
6.37 c                   kfld                    w_firm
6.37 c                   kfld                    jvkll1_qvnr
      *
      * Key for oppdatering av vare
      *
     c     jvsoiu_key    klist
     c                   kfld                    jvsoiu_svar
      *
6.32 c                   time                    w_udat
     c                   endsr
