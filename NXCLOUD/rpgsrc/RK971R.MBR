     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RK971R                                              *
      * Beskrivelse . . : Kunder - oppdaterer saldo/kjøp i år
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Ref.  Dato   Sign Endringsbeskrivelse                                 *
      * ----- ------ ---  --------------------------------------------------- *
6.10  * V6.10 230909 BSA  Lagt inn sporingsfelter
6.11  * V6.10 070510 BSA  Endret sporingsfunksjonalitet
6.12  * V6.10 190111 sst  Rettet oppdatering saldo fra beløp til Rest
6.20  *       060214 nho  Fjernet endring 6.12, ref Mariann
      *                   Skal bruker RNBELØ og ikke RNREST                   *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frktrl1    if   e           k disk    rename(rktrpfr:rktrl1r)
     frkunl1    uf   e           k disk    rename(rkunpfr:rkunl1r)
     frkunlu    uf   e           k disk    rename(rkunpfr:rkunlur)
     fra01l1    if   e           k disk    rename(ra01pfr:ra01l1r)
      *
     d                uds
     d rpraar                300    303  0
     d rpsald                         1
     d rpkjØp                         1
     d rpkjØf                         1
      *
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d w_akod          s                   like(raakod)
     d w_faar          s                   like(rpraar)
     d w_firm          s                   like(rkfirm)
     d w_kjØf          s                   like(rkkjØf)
     d w_kjØp          s                   like(rkkjØp)
     d w_kund          s                   like(rkkund)
     d w_saif          s                   like(rksaif)
     d w_sald          s                   like(rksald)
     d w_kund_gml      s                   like(rnkund)
      *
     d rkunlu_kund     s                   like(rkkund)
     d ra01l1_akod     s                   like(raakod)
      *
     c     dummy         tag
      *
      * Les kundeposteringer
      *
     c     rktrl1_key    setll     rktrl1                                 61
     c     rktrl1_key    reade     rktrl1                                 61
      *
     c                   dow       *in61 = *off
      *
      * Oppdater kunder ved brudd
      *
     c                   if        rnkund <> w_kund_gml
     c                   exsr      opdkun
     c                   eval      w_sald = 0
     c                   eval      w_saif = 0
     c                   eval      w_kjØp = 0
     c                   eval      w_kjØf = 0
     c                   eval      w_kund_gml = rnkund
     c                   endif
      *
      * Oppdater saldo/saldo i fjor
      *
     c                   if        rpsald = 'J'
6.12 c**620              eval      w_sald = w_sald + rnrest
6.20 c                   eval      w_sald = w_sald + rnbelØ
      *
     c                   if        rnraar < rpraar
6.12 c**620              eval      w_saif = w_saif + rnrest
6.20 c                   eval      w_saif = w_saif + rnbelØ
     c                   endif
      *
     c                   endif
      *
      * Sjekk om bilagskode skal
      * oppdatere kjøp
      *
     c                   move      rnbilk        ra01l1_akod
     c     ra01l1_key    chain     ra01l1                             61
      *
     c                   if        raakjØ = '1'
      *
      * Oppdater kjøp i år
      *
     c                   if        rpkjØp = 'J' and
     c                             rnraar = rpraar
     c                   eval      w_kjØp = w_kjØp + rnbelØ
     c                   endif
      *
      * Oppdater kjøp i fjor
      *
     c                   if        rpkjØf = 'J' and
     c                             rnraar = w_faar
     c                   eval      w_kjØf = w_kjØf + rnbelØ
     c                   endif
      *
     c                   endif
      *
     c     rktrl1_key    reade     rktrl1                                 61
      *
     c                   enddo
      *
     c                   exsr      opdkun
      *
     c                   seton                                        lr
      *
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for nullstilling av kunder                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     nulkun        begsr
      *
     c     rkunl1_key    setll     rkunl1                                 60
     c     rkunl1_key    reade     rkunl1                                 60
      *
     c                   dow       *in60 = *off
      *
      * Nullstill felt
      *
     c                   if        rpsald = 'J'
     c                   eval      rksald = *zeros
     c                   eval      rksaif = *zeros
     c                   endif
      *
     c                   if        rpkjØp = 'J'
     c                   eval      rkkjØp = *zeros
     c                   endif
      *
     c                   if        rpkjØf = 'J'
     c                   eval      rkkjØf = *zeros
     c                   endif
      *
6.10 c*                  time                    rkedat
6.10 c*                  time                    rketim
6.10 c*                  eval      rkesig = l_user
6.11 c                   time                    rk2dpt
6.11 c                   eval      rkepgm = 'RK971R    '
     c                   update    rkunl1r
      *
     c     rkunl1_key    reade     rkunl1                                 60
      *
     c                   enddo
      *
     c                   endsr
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for oppdatering av kunder                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     opdkun        begsr
      *
     c                   eval      rkunlu_kund = w_kund_gml
     c     rkunlu_key    chain     rkunlu                             62
      *
     c                   if        *in62 = *off
      *
      * Oppdater saldo
      *
     c                   if        rpsald = 'J'
     c                   eval      rksald = w_sald
     c                   eval      rksaif = w_saif
     c                   endif
      *
      * Oppdater kjøp i år
      *
     c                   if        rpkjØp = 'J'
     c                   eval      rkkjØp = w_kjØp
     c                   endif
      *
      * Oppdater kjøp i fjor
      *
     c                   if        rpkjØf = 'J'
     c                   eval      rkkjØf = w_kjØf
     c                   endif
      *
6.10 c*                  time                    rkedat
6.10 c*                  time                    rketim
6.10 c*                  eval      rkesig = l_user
6.11 c                   time                    rk2dpt
6.11 c                   eval      rkepgm = 'RK971R    '
     c                   update    rkunlur
     c                   endif
      *
     c                   endsr
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c                   move      l_firm        w_firm
     c                   eval      w_kund_gml = *hival
      *
      * Beregn fjorår
      *
     c                   eval      w_faar = rpraar - 1
      *
      * Nullstill kunderegister
      *
     c                   exsr      nulkun
      *
      * Key for kundeposteringer
      *
     c     rktrl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for kunder
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for kunder
      *
     c     rkunlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunlu_kund
      *
      * Key for bilagskode
      *
     c     ra01l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra01l1_akod
      *
     c                   endsr
