     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LO406R
      *  Beskrivelse . . : Sletter bestillinger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
6.nu  * R6.30  110614 bof Sjekket mtp. nummerutvidelse
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flodtl1    if   e           k disk    rename(lodtpfr:lodtl1r)
     fvltyl1    if   e           k disk    rename(vltypfr:vltyl1r)
      *
      * Parameter-inn
      *
     d                 ds
     d d_prec                        64
     d  d_bdat                        6  0 overlay(d_prec)
     d  d_uvar                        1  0 overlay(d_prec:7)
     d  d_ldor                        6  0 overlay(d_prec:8)
     d  d_otyp                        2    overlay(d_prec:14)
      *
      * Local Data Area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d lohel1_numm     s                   like(lonumm)
     d lodtl1_numm     s                   like(ldnumm)
     d lodtl1_suff     s                   like(ldsuff)
     d vltyl1_ltyp     s                   like(valtyp)
      *
      * Definering av parametre
      *
     d p_parm          s             64
      *
      * Definering av variable
      *
     d w_firm          s                   like(lofirm)
     d w_bdat          s               d   datfmt(*iso)
     d w_retu          s              1
     d w_typd          s              4
     d w_kom1          s             30
     d w_kom2          s             30
      *
     d b_slet          s              1    inz(*off)
     d b_vlin          s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Behandle bestillinger ihht riktige parametere for sletting
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      lohel1_numm = *zero
      *
     c     lohel1_key    setll     lohel1
     c                   read      lohel1
      *
     c                   dow       not %eof(lohel1)
      *
     c                   exsr      sjk_best
     c                   if        b_slet = *on
     c                   exsr      kall_slet
     c                   endif
      *
     c                   read      lohel1
     c                   enddo
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kontroll av bestilling mot parameter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_best      begsr
      *
     c                   eval      b_slet = *off
     c                   eval      b_vlin = *off
      *
     c     *dmy          move      d_bdat        w_bdat
     c                   if        lobdat >= w_bdat
     c                   leavesr
     c                   endif
      *
     c                   if        d_otyp <> *blank
     c                   if        lootyp <> d_otyp
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        d_ldor <> *zero
     c                   if        loldor <> d_ldor
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        d_uvar = *zero
     c                   eval      b_slet = *on
     c                   leavesr
     c                   endif
      *
     c                   eval      lodtl1_numm = lonumm
     c                   eval      lodtl1_suff = losuff
      *
     c     lodtl1_key    setll     lodtl1
     c     lodtl1_key    reade     lodtl1
     c                   dow       not %eof(lodtl1)
      *
     c                   eval      vltyl1_ltyp = ldltyp
     c     vltyl1_key    chain     vltyl1
     c                   if        not %found(vltyl1)
     c                   eval      b_vlin = *on
     c                   leave
     c                   endif
      *
     c                   if        valktx <> 1
     c                   eval      b_vlin = *on
     c                   leave
     c                   endif
      *
     c     lodtl1_key    reade     lodtl1
     c                   enddo
      *
     c                   if        b_vlin = *off
     c                   eval      b_slet = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kall av bestillingssletterutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kall_slet     begsr
      *
     c                   eval      w_typd = '*STX'
     c                   eval      w_kom1 = 'Slettet av kall fra std.prog.'
     c                   eval      w_kom2 = *blank
     c                   call      'LO400R'
     c                   parm                    w_retu
     c                   parm                    w_firm
     c                   parm                    lonumm
     c                   parm                    losuff
     c                   parm                    w_typd
     c                   parm                    w_kom1
     c                   parm                    w_kom2
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for les av bestillingshode
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
      *
      *  Key for les av bestillingslinje
      *
     c     lodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtl1_numm
     c                   kfld                    lodtl1_suff
      *
      * Key for oppslag på linjetype
      *
     c     vltyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltyl1_ltyp
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    p_parm
      *
     c                   eval      d_prec = p_parm
      *
      *  Legge inn firmanummer i nøklene
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
