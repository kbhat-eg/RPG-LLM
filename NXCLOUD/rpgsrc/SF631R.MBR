      /TITLE  Utskrift av etiketter utfra statistikk
     h datedit(*dmy.) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  System. . . . . : ASSTAT                                       *
      *  Program . . . . : SF631R                                       *
      *  Beskrivelse . . : Utskrift av etiketter utfra statistikk       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  Spesialversjon. :                                              *
      *  Tilpasset for . :                                              *
      *                                                                 *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.*
      *  --------- ------  ---------------------------------------------*
      *                                                                 *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     fskwwpf    ip   e           k disk
     fskpml1    if   f  512    15aidisk    keyloc(1)
     frkunl1    if   e           k disk
     fsf631p    o    e             printer infds(d_infds)
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tabeller                                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     d a_kund          s            100    dim(9) ctdata perrcd(1)               Array søketekst
     d a_navn          s            100    dim(9) ctdata perrcd(1)               Array søketekst
     d a_adr1          s            100    dim(9) ctdata perrcd(1)               Array søketekst
     d a_adr2          s            100    dim(9) ctdata perrcd(1)               Array søketekst
     d a_padr          s            100    dim(9) ctdata perrcd(1)               Array søketekst
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Data Strukturer                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     d                 ds
     d skpml1_key                    15
     d  w1firm                        3  0 overlay(skpml1_key:1)
     d  wwtype                       10    overlay(skpml1_key:4)
     d  wwkode                        2    overlay(skpml1_key:14)
      *
     d                 ds
     d beg                           15
     d  begnum                       15  0 overlay(beg)
     d   k6                           6  0 overlay(begnum)
      *
      * Informasjon fra printer-file
      *
     d d_infds         ds
     d  d_linnbr             367    368B 0
     d  d_pagnbr             369    372B 0
      *
      * Definering av variable
      *
     d w_etik          s              1  0
     d w_line          s              2  0
      *
     d  w_ante         s              1  0
     d  w_bred         s              2  0
     d  w_hoyd         s              2  0
      *
     d                uds
     d l_ruti                800    809
     d l_alin                856    858  0
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
      *
     d wwkund          s                   like(rkkund)
     d wwfirm          s                   like(spfirm)
     *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Input section                                                  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     Iskwwpfr       01
     I                                          sffirm        l7
     I                                          swsrt1        l6
     I                                          swsrt2        l5
     I                                          swsrt3        l4
      *
     Iskpml1    ns  01   14 c0   15 c0
     I                                  1    3 0spfirm
     I                                  4   13  sptype
     I                                 14   15 0spkode
     I                                 31   60  sptext
     I                                 61   62 0rp1
     I                                 63   64 0rp2
     I                                 65   66 0rp3
     I                                 67   67  spes1
     I                                 68   68  spes2
     I                                 69   69  spes3
     I                                 70   70  spes4
     I                                 71   71  spes5
     I                                 73   74 0spmnd
     I                                 75   78 0spaar
     I                                 79   79  spsmli
     I                                 80   80  spsps7
     I                                 81   81  spsss1
     I                                 82   82  spsss2
     I                                 83   83  spsss3
     I                                 84   84  spslet
      *
     I          ns  01   14 c4   15 c1
     I                                  1    3 0spfirm
     I                                  4   13  sptype
     I                                 14   15 0spkode
     I                                 31   60  sptext
     I                                 61   64 0spfaar
     I                                 65   66 0spfmnd
     I                                 67   70 0sptaar
     I                                 71   72 0sptmnd
      *
     C/eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Calculationn                                                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C   l6              do                                                               .....
     C                   move      swsrt1        beg                                          .
     C                   movel     k6            wwkund
     C     rkunl1_key    chain     rkunl1                             50
     C                   enddo                                                            .....
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     xslutt        tag
      *
      * BRUDD PÅ L6
      *
     CL6                 exsr      behandling
      *
     CLR                 exsr      detalj
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av utskrift
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
      * Finner etikett-kolonne
      *
     c                   eval      w_etik = w_etik + 1
      *
      * Dersom alle etiketter i bredden er utfylt med navn og adresse,
      * skrives etikettraden
      *
     c                   if        w_etik > w_ante
     c                   exsr      detalj
     c                   eval      w_etik = 1
     c                   endif
      *
      * Fyller array med kunde/kundeprosjekt-informasjon
      *
     c                   movel     rkkund        a_kund(w_etik)
      *
     c                   eval      a_navn(w_etik) = rknavn
     c                   eval      a_adr1(w_etik) = rkgate
     c                   eval      a_adr2(w_etik) = rkadr2
     c                   eval      a_padr(w_etik) = rksted
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine utskrift av detaljlinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     detalj        begsr
      *
      * Leser array, og fyller etikettrad
      *
     c                   eval      d1kund = %subst(a_kund(1):1:w_bred) +
     c                                      %subst(a_kund(2):1:w_bred) +
     c                                      %subst(a_kund(3):1:w_bred) +
     c                                      %subst(a_kund(4):1:w_bred) +
     c                                      %subst(a_kund(5):1:w_bred) +
     c                                      %subst(a_kund(6):1:w_bred) +
     c                                      %subst(a_kund(7):1:w_bred) +
     c                                      %subst(a_kund(8):1:w_bred) +
     c                                      %subst(a_kund(9):1:w_bred)
      *
     c                   eval      d1navn = %subst(a_navn(1):1:w_bred) +
     c                                      %subst(a_navn(2):1:w_bred) +
     c                                      %subst(a_navn(3):1:w_bred) +
     c                                      %subst(a_navn(4):1:w_bred) +
     c                                      %subst(a_navn(5):1:w_bred) +
     c                                      %subst(a_navn(6):1:w_bred) +
     c                                      %subst(a_navn(7):1:w_bred) +
     c                                      %subst(a_navn(8):1:w_bred) +
     c                                      %subst(a_navn(9):1:w_bred)
      *
     c                   eval      d1adr1 = %subst(a_adr1(1):1:w_bred) +
     c                                      %subst(a_adr1(2):1:w_bred) +
     c                                      %subst(a_adr1(3):1:w_bred) +
     c                                      %subst(a_adr1(4):1:w_bred) +
     c                                      %subst(a_adr1(5):1:w_bred) +
     c                                      %subst(a_adr1(6):1:w_bred) +
     c                                      %subst(a_adr1(7):1:w_bred) +
     c                                      %subst(a_adr1(8):1:w_bred) +
     c                                      %subst(a_adr1(9):1:w_bred)
      *
     c                   eval      d1adr2 = %subst(a_adr2(1):1:w_bred) +
     c                                      %subst(a_adr2(2):1:w_bred) +
     c                                      %subst(a_adr2(3):1:w_bred) +
     c                                      %subst(a_adr2(4):1:w_bred) +
     c                                      %subst(a_adr2(5):1:w_bred) +
     c                                      %subst(a_adr2(6):1:w_bred) +
     c                                      %subst(a_adr2(7):1:w_bred) +
     c                                      %subst(a_adr2(8):1:w_bred) +
     c                                      %subst(a_adr2(9):1:w_bred)
      *
     c                   eval      d1padr = %subst(a_padr(1):1:w_bred) +
     c                                      %subst(a_padr(2):1:w_bred) +
     c                                      %subst(a_padr(3):1:w_bred) +
     c                                      %subst(a_padr(4):1:w_bred) +
     c                                      %subst(a_padr(5):1:w_bred) +
     c                                      %subst(a_padr(6):1:w_bred) +
     c                                      %subst(a_padr(7):1:w_bred) +
     c                                      %subst(a_padr(8):1:w_bred) +
     c                                      %subst(a_padr(9):1:w_bred)
      *
      * Skriver hode dersom det ikke er plass til ny etikettrad på siden.
      *
     c                   if        (d_linnbr + w_hoyd) > l_alin
     c                   write     a1hdr
     c                   endif
      *
      * Skriver etikettrad
      *
     c                   write     d1dtl                                30
      *
      * Skriver blanke linjer for å fylle opp etiketthøyden
      *
     c                   eval      w_line = 5
     c                   dow       w_line < w_hoyd
     c                   write     d2dtl                                30
     c                   eval      w_line = w_line + 1
     c                   enddo
      *
      * Tømmer array
      *
     c                   movea     *blank        a_kund(*)
     c                   movea     *blank        a_navn(*)
     c                   movea     *blank        a_adr1(*)
     c                   movea     *blank        a_adr2(*)
     c                   movea     *blank        a_padr(*)
      *
     c                   endsr
      *
      /eject
     * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     * Subrutine for initiering av program                             *
     * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     *
     *
     C     *inzsr        begsr
     *
      *
      * Splitter datastruktur i variable
      *
     c                   eval      w_ante = 3
     c                   eval      w_bred = 36
     c                   eval      w_hoyd = 6
      *
      * Behandle kriterier for valg
      *
     C     *entry        plist
     C                   parm                    dstype           10
      *
     C                   eval      skpml1_key = *blank
      *
     C                   move      l_firm        w1firm
     C                   move      l_firm        wwfirm
     C                   movel     dstype        wwtype
     C                   move      '00'          wwkode
     C     skpml1_key    chain     skpml1                             50
      *
     C                   write     a1hdr
      *
      * Key for kunderegister
      *
     C     rkunl1_key    klist
     C                   kfld                    wwfirm
     C                   kfld                    wwkund
      *
     C                   endsr
