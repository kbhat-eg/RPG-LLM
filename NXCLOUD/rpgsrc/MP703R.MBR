     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLOGI
      * Program . . . . : MP101R
      * Beskrivelse . . : Danner bestillingsforslag - parameter
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       260404 bø   Nytt program
5.60  *       020209 bø   Justert, når man ikke finner parameter
      * 6.10  190609 BSA  Oppdatert med sporingsinformasjon
6.31  *       180618 bof  Henter fra adresse fra AFPSPF
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fmppalr    if   e           k disk    rename(mppapfr:mppalrr)
     fmppalu    uf   e           k disk    rename(mppapfr:mppalur)
6.31 fafpslr    if   e           k disk    rename(afpspfr:afpslrr)
      *
      *
      * Definering av parametre
      *
     d p_ldor          s                   like(mpldor)
     d p_lnum          s                   like(mplnum)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
6.31 d l_filg                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      *
     d mppalr_ldor     s                   like(mpldor)
     d mppalr_lnum     s                   like(mplnum)
     d mppalu_ldor     s                   like(mpldor)
     d mppalu_lnum     s                   like(mplnum)
6.31 d afpslr_ufil     s                   like(l_filg)
6.31 d afpslr_uide     s                   like(afuide)
      *
      * Definering av variable
      *
     d w_firm          s                   like(mpfirm)
     d w_dato          s               d   datfmt(*iso)
     d w_dagens        s               d   datfmt(*iso)
     d w_dager         s              4  0
     d w_xnum          s              6
     d w_xlev          s              6
     d w_til           s             50
     d w_fra           s             50
     d w_numm          s              6  0
     d w_subj          s            250
     d w_retk          s              1
     d w_levr          s                   like(mpldor)
     d w_navn          s             30
      *
      *
      *  Definering av datastrukturer
      *
      * Parameter-inn
     d                 ds
     d d_prec                       256
     d  d_flev                        6  0 overlay(d_prec)
     d  d_tlev                        6  0 overlay(d_prec:7)
     d  d_fogr                        2  0 overlay(d_prec:13)
     d  d_fhgr                        2  0 overlay(d_prec:15)
     d  d_fugr                        3  0 overlay(d_prec:17)
     d  d_fvar                       15    overlay(d_prec:20)
     d  d_togr                        2  0 overlay(d_prec:35)
     d  d_thgr                        2  0 overlay(d_prec:37)
     d  d_tugr                        3  0 overlay(d_prec:39)
     d  d_tvar                       15    overlay(d_prec:42)
     d  d_sakb                        4    overlay(d_prec:57)
     d  d_lage                        2  0 overlay(d_prec:61)
     d  d_vval                        1  0 overlay(d_prec:63)
     d  d_sats                        3  0 overlay(d_prec:64)
     d  d_aval                        1  0 overlay(d_prec:67)
     d  d_tdat                        6  0 overlay(d_prec:68)
     d  d_selg                        4  0 overlay(d_prec:74)
     d  d_gvek                       11  3 overlay(d_prec:78)
     d  d_gbel                       11  2 overlay(d_prec:89)
     d  d_numm                        6  0 overlay(d_prec:100)
     d  d_lnum                        3  0 overlay(d_prec:106)
     d  d_kora                        1    overlay(d_prec:109)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c                   eval      mppalr_ldor = p_ldor
     c                   eval      mppalr_lnum = p_lnum
     c     mppalr_key    chain     mppalr
     c                   if        %found
      *
     c                   if        mpthor > 0
     c                   eval      w_dager = mpthor * 7
     c                   time                    w_dagens
     c                   eval      w_dato  = w_Dagens + %days(w_dager)
     c     *dmy          move      w_dato        d_tdat
     c                   else
     c                   eval      d_tdat = 0
     c                   endif
      *
     c                   eval      d_flev  = mpldor
     c                   eval      d_tlev  = mpldor
     c                   eval      d_fogr  = mpogrf
     c                   eval      d_fhgr  = mphgrf
     c                   eval      d_fugr  = mpugrf
     c                   eval      d_fvar  = mpvarf
     c                   eval      d_togr  = mpogrt
     c                   eval      d_thgr  = mphgrt
     c                   eval      d_tugr  = mpugrt
     c                   eval      d_tvar  = mpvart
     c                   eval      d_sakb  = mppran
     c                   eval      d_lage  = mplage
     c                   eval      d_vval  = mpvval
     c                   eval      d_sats  = mpsats
     c                   eval      d_aval  = mpaval
     c                   eval      d_selg  = mpselg
     c                   eval      d_gvek  = mpgvek
     c                   eval      d_gbel  = mpgbel
     c                   eval      d_lnum  = mplnum
     c                   eval      d_kora  = mpkora
     c                   eval      d_numm  = 0
5.60 c                   else
5.60 c                   goto      avslutt
     c                   endif
      *
     c                   eval      mppalu_ldor = p_ldor
     c                   eval      mppalu_lnum = p_lnum
     c     mppalu_key    chain     mppalu
     c                   if        %found
     c                   time                    mpsdat
6.10 c                   time                    mpedat
6.10 c                   time                    mpetim
6.10 c                   eval      mpeusr = l_user
     c                   update    mppalur
     c                   endif
      *
      * Sende mail
      *
     c                   eval      w_til  = *blank
     c                   eval      w_fra  = *blank
     c                   eval      w_subj = *blank
     c                   if        mpmail <> *blank
     c                   movel     mpmail        w_til
     c                   move      mpldor        w_xlev
     c                   eval      w_retk = *blank
     c                   eval      w_levr = mpldor
     c                   eval      w_navn = *blank
     c                   call      'RS760R  '
     c                   parm                    w_retk
     c                   parm                    w_levr
     c                   parm                    w_navn
     c                   move      d_numm        w_xnum
6.31  * henter mailadresse fra
6.31 c                   eval      afudss = *blank
6.31 c                   eval      afpslr_ufil = l_filg
6.31 c                   eval      afpslr_uide = 'NOMAIL   '
6.31 c     afpslr_key    chain     afpslrr
6.31 c                   eval      w_fra   = afudss
6.31 c*                  eval      w_fra   = 'firmapost@asp-as.no'
     c                   eval      w_subj  = 'Bestillingsforslag' + ' ' +
     c                                        %trim(w_xnum) + ' ' +
     c                                        'for leverandør :' + ' ' +
     c                                         %trim(w_navn) + ' ' +
     c                                         'er kjørt'
      *
     c                   endif
     c                   call      'MP701C'
     c                   parm                    d_prec
     c                   parm                    w_til
     c                   parm                    w_fra
     c                   parm                    w_subj
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_ldor
     c                   parm                    p_lnum
      *
      * Key for les på parameter pr. lev.
      *
     c     mppalr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mppalr_ldor
     c                   kfld                    mppalr_lnum
      *
      * Key for oppdatering av parameter pr. lev.
      *
     c     mppalu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mppalu_ldor
     c                   kfld                    mppalu_lnum
6.31  *
6.31  * Key for propertiesfil
6.31  *
6.31 c     afpslr_key    klist
6.31 c                   kfld                    afpslr_ufil
6.31 c                   kfld                    afpslr_uide
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
      *
     c                   endsr
