     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RK661R                                              *
      * Beskrivelse . . : Kunder, utplukk av poster til rentenota             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      * R5.00     070803  Utplukk av enkeltkunder fungerte ikke.         HFL  *
      * R5.30     010604  Lagt inn beregning av rente på poster som er   KOB  *
      *                   delvis saldert uten referansesetting           KOB  *
      * R5.30     100105  Korrigert endring 1.6.04.                      HFL  *
      * R5.30     070605  Forfalt ikke betalt, rettet                    HFL  *
      * R5.41     080806  Skrevet om rutinen for behandling av delbet.   KOB  *
      * R5.50     010807  Utelater bilagskode 99- uthentet saldo.        HFL  *
      * R5.60     010808  Tester negative poster mot rentegrense.        KOB  *
      * R5.60     120808  Henter overstyrt renteprosent fra parammeter   KOB  *
      * R5.60     030908  Gjort flere endringer for å kunne behandle     KOB  *
      *                   salderinger mot flere poster på en bedre måte  KOB  *
      * R5.60     030908  Lagt inn rutiner for automatisk merking av     KOB  *
      *                   poster som er ferdigbehandlet rentemessig      KOB  *
      * R5.60     171008  Lagt inn egen timestamp for arbeidsfil         KOB  *
6.10  * V6.10     220909  Lagt inn sporingsfelter                        BSA  *
6.12  * V6.12     111110  Fra dato ble feil når beløp og rest hadde      NHO  *
      *                   samme beløp.  Lagt til en dag på forf.dato     NHO  *
      *                   Samme gjaldt for minus poster                  NHO
9.xx  * 7.xx  040219 bsa  Ligger her pga endringer i RA11PF. Kun kompilert
7.01  *K00    010720 bof  Utvidet w_sumr feltet
      *
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     frktrl2    if   e           k disk    rename(rktrpfr:rktrl2r)
     frktrlu    uf   e           k disk    rename(rktrpfr:rktrlur)
     frktrl8    if   e           k disk    rename(rktrpfr:rktrl8r)
     f                                     prefix(x:1)
     frktrlc    if   e           k disk    rename(rktrpfr:rktrlcr)
     f                                     prefix(x:1)
     fraa3l1    if   e           k disk    rename(raa3pfr:raa3l1r)
     fraa6l1    if   e           k disk    rename(raa6pfr:raa6l1r)
     fra01l1    if   e           k disk    rename(ra01pfr:ra01l1r)
     fra11l1    if   e           k disk    rename(ra11pfr:ra11l1r)
     frkwbpf    uf a e           k disk    rename(rkwbpfr:rkwbpfr)
     frkwblu    uf   e           k disk    rename(rkwbpfr:rkwblur)
      *
     d w_kdat          s               d   datfmt(*iso)
     d w_rfda          s               d   datfmt(*iso)
     d w_rtda          s               d   datfmt(*iso)
     d w_fmot          s              1  0
     d w_refn          s                         like(rnrefn)
     d x_refn          s                         like(rnrefn)
      *
     d wtom            s               d   datfmt(*iso)
     d wfra            s               d   datfmt(*iso)
     d wtil            s               d   datfmt(*iso)
      *
     d  rktrlc_kund    s                         like(rnkund)
     d  rktrlc_refn    s                         like(rnrefn)
     d  rktrl8_kund    s                         like(rnkund)
     d  rktrl8_biln    s                         like(rnbiln)
     d  rktrl2_kund    s                         like(rnkund)
     d  rktrlu_kund    s                         like(rnkund)
     d  rktrlu_bdat    s                         like(rnbdat)
     d  rktrlu_tist    s                         like(rntist)
     d  ra01l1_akod    s                         like(raakod)
     d  ra11l1_kkod    s                         like(rakkod)
     d  rkwblu_kund    s                         like(rnkund)
      *
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      * LOCAL DATA
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      *
     d                uds
     d  pfirm                300    302  0
     d  pnavn                303    332
     d  puser                333    342
     d  pkdat                343    348  0
     d  praar                349    352  0
     d  prper                353    354  0
     d  pavdf                355    358  0
     d  pavdt                359    362  0
     d  pkatf                363    366  0
     d  pkatt                367    370  0
     d  pself                371    374  0
     d  pselt                375    378  0
     d  pknrf                379    384  0
     d  pknrt                385    390  0
     d  kun                  391    450  0 dim(10)
     d  pknr1                391    396  0
     d  pknr2                397    402  0
     d  pknr3                403    408  0
     d  pknr4                409    414  0
     d  pknr5                415    420  0
     d  pknr6                421    426  0
     d  pknr7                427    432  0
     d  pknr8                433    438  0
     d  pknr9                439    444  0
     d  pknr0                445    450  0
     d  pfdat                451    456  0
     d  plope                457    459  0
     d  ppkod                460    460
     d  pgrns                464    465  0
     d  pgbyr                466    466
     d  pmini                467    468  0
     d  pbskr                469    469
     d  pegrn                470    470
     d  pajor                471    471
     d  ptext                472    472  0
     d  ppunr                473    473  0
     d  ppbeh                474    474
     d  palfa                475    475
     d  pdfgg                476    481  0
     d  pgeby                482    484  0
     d  ppros                485    488  2
     d  pparam               300    490
      *
     d  l_user               911    920
     d  l_firm               944    946  0
     d  l_fnav               951    980
      *
     d n               s              3  0
     d pdatx           s              8  0
     d prdagx          s              1
     d savbel          s                   like(rnbelØ)
     d savbeu          s                   like(rnbelØ)
     d srente          s                   like(rnbelØ)
     d tstkt           s              7  0
     d wdg             s              5  0
      *
     d w_firm          s                   like(rkfirm)
     d w_rent          s              9  2
     d w_rgrl          s             11  2
7.01 d w_sumr          s             12  2
     d w_rpro          s              4  2
     d wrx             s             11  4
      *
      * Definering av variable
      *
     d w_tmst          s               z
     d w_tmst_20       s             20
     d w_tell          s              6  0
     d w_tell_alf      s              6
      *
     c/eject
      *
      * Leser kunde-register
      *
     c     rkunl1_key    setll     rkunl1                                 60
     c     rkunl1_key    reade     rkunl1                                 60
      *
     c                   dow       *in60 = *off
      *
     c                   eval      w_sumr = 0
      *
      * Renetberegnes?!   1 = Ikke renter, men purres
      *                   2 = Ikke renter og ikke purres
      *
     c                   if        rkrenk = 1 or
     c                             rkrenk = 2
     c                   goto      nykund
     c                   endif
      *
      * Seleksjon av ønsket avdeling
      *
     c                   if        rkavde < pavdf or
     c                             rkavde > pavdt
     c                   goto      nykund
     c                   endif
      *
      * Seleksjon av ønsket kategori
      *
     c                   if        rkkatg < pkatf or
     c                             rkkatg > pkatt
     c                   goto      nykund
     c                   endif
      *
      * Seleksjon av ønsket selger
      *
     c                   if        rkslgr < pself or
     c                             rkslgr > pselt
     c                   goto      nykund
     c                   endif
      *
      * Seleksjon av ønsket kundenr.
      *
     c                   if        rkkund < pknrf or
     c                             rkkund > pknrt
     c                   goto      enkund
     c                   endif
      *
     c                   goto      okkund
      *
      * Hvis kundenr. ligger utenfor  fra - til  sjekk tabell
      *
     c     enkund        tag
      *
      * Hvis valg ikke utfylt, les neste kunde
      *
     c                   if        tstkt = *zeros
     c                   goto      nykund
     c                   endif
      *
      * Hvis valg utfylt, sjekk mot tabell
      *
     c                   z-add     1             n
     c                   dou       n > 10
      *
     c                   if        kun(n) <> *zeros
      *
     c                   if        rkkund = kun(n)
     c                   goto      okkund
     c                   endif
      *
     c                   endif
      *
     c                   add       1             n
     c                   enddo
      *
      * Kunde ikke i listen
      *
     c                   goto      nykund
      *
     c     okkund        tag
      *
      * Bruk rentesats fra parameterbildet
      *
     c                   eval      w_rpro = ppros
      *
      * Henter avvikende rentesats fra kundekategori
      *
     c                   eval      ra11l1_kkod = rkkatg
     c     ra11l1_key    chain     ra11l1
      *
     c                   if        %found and
     c                             rakavp <> 0
     c                   eval      w_rpro= rakavp
     c                   endif
      *
      * Sortering
      *
     c                   if        palfa = 'J'
     c                   eval      rwkalf = rkalfa
     c                   else
     c                   eval      rwkalf = *blank
     c                   endif
      *
     c     kundok        tag
      *
     c                   exsr      les_post
      *
     c     nykund        tag
      *
      * Sjekk om sum renter < rentegrense
      *
     c                   if        w_sumr < ra6pgr and
     c                             w_sumr <> 0
     c                   exsr      slett_post
     c                   endif
      *
     c     rkunl1_key    reade     rkunl1                                 60
     c                   enddo
      *
     c/space 3
      *
     c     slutt         tag
      *
     c                   eval      *inlr = *on
     c/eject
      *
      * Subrutine for behandling av kundeposter
      *
     c     les_post      begsr
      *
     c                   eval      rktrl2_kund = rkkund
     c     rktrl2_key    setll     rktrl2                                 61
     c     rktrl2_key    reade     rktrl2                                 61
      *
     c                   dow       *in61 = *off
      *
      * Posten skal ikke renteberegnes
      *
     c                   if        rnbiln = 68982
     c                   move      '1'           kai               1
     c                   endif
      *
     c                   if        rnrenk = 1
     c                   goto      nypost
     c                   endif
      *
      * Posten er ferdig saldert og renteberegnet tidligere - rentekoden settes = 1
      *
E5.30c                   if        rnrdat <> *loval and
     c                             rnrest = 0
     c                   exsr      oppdat_post
     c                   goto      nypost
     c                   endif
      *
      * Fjern småposter - sjekk mot rentegrense
      *
     c                   if        rnbelØ < ra6pfg and
     c                             rnbelØ > ra6pfg * -1
     c                   exsr      oppdat_post
     c                   goto      nypost
     c                   endif
      *
      * Sjekk regnskapsperiode
      *
     c                   if        rnraar > praar
     c                   goto      nypost
     c                   endif
      *
     c                   if        rnraar = praar
     c                             and rnrper > prper
     c                   goto      nypost
     c                   endif
      *
      * Reklamasjon skal ikke rentebelastes
      *
     c                   if        rnrekl = 'J'
     c                   goto      nypost
     c                   endif
      *
      * Poster som er sendt til inkasso skal ikke rentebelastes
      *
     c                   if        rninko = 'J'
     c                   goto      nypost
     c                   endif
      *
      * Sjekk om posten er forfalt
      *
     c                   if        rnfdat > w_kdat
     c                   goto      nypost
     c                   endif
      *
T5.50 * Uthentet saldo lager bilagskode 99
      *
T5.50c                   if        rnbilk = 99
T5.50c                   goto      nypost
T5.50c                   endif
      *
      * Sjekk om bilagskode skal renteberegnes
      *
     c                   eval      ra01l1_akod = rnbilk
     c     ra01l1_key    chain     ra01l1
      *
     c                   if        %found and
     c                             raarnk = '1'
     c                   goto      nypost
     c                   endif
      *
      * Forfalt - ikke betalt
      *
E5.30c                   if        ra6pib = *blank and
     c                             rnrest <> 0
     c                   goto      nypost
     c                   endif
      *
      /eject
      *
      * Behandle negative poster
      *
     c                   if        rnbelØ < 0 and
     c                             rnsamf = 0
      *
      * Negative poster skal ikke rentebelastes
      *
     c                   if        ra6pnu = ' '
     c                   goto      nypost
     c                   endif
      *
      * Restbeløp = 0
      *
     c                   if        rnrest = 0 and
     c                             rnsamf = 0
     c                   goto      nypost
MEST c                   endif
      *
      * Beregn renter på restbeløp negativ post
      *
     c                   if        rnrest < 0 and
     c                             rnsamf = 0
      *
     c                   if        rnrdat > rnfdat
     c                   eval      w_rfda = rnrdat
     c                   else
     c                   eval      w_rfda = rnfdat
     c                   endif
      *
     c                   eval      w_rtda = w_kdat
     c                   eval      w_rgrl = rnrest
6.11  *
6.11 c     w_rfda        adddur    1 :*d         w_rfda
6.11  *
     c                   exsr      beregn
      *
      * Sjekk mot antall løpedager
      *
     c                   if        wdg <= ra6pld
     c                   goto      nypost
     c                   endif
      *
      * Sjekk mot rentegrense faktura
      *
     c                   if        w_rent  * -1 < ra6pfg or
     c                             w_rent = 0
     c                   goto      nypost
     c                   endif
      *
      * Summer rente for senere kontroll
      *
     c                   eval      w_sumr = w_sumr + w_rent
      *
      * Legg ut transaksjon
      *
     c                   eval      rwrfda = w_rfda
     c                   eval      rwrtda = w_rtda
     c                   eval      rwrent = w_rent
     c                   eval      rwdagr = wdg
     c                   eval      rwrgrl = w_rgrl
      *
      *
      * Teller/Timestamp
      *
     c                   eval      w_tell = w_tell + 1
     c                   move      w_tell        w_tell_alf
     c                   time                    rwtist
     c     *iso0         move      rwtist        w_tmst_20
     c                   move      w_tell_alf    w_tmst_20
     c     *iso0         move      w_tmst_20     rwtist
      *
     c                   write     rkwbpfr
     c                   goto      nypost
      *
     c                   else
     c                   goto      nypost
      *
     c                   endif
      *
     c                   endif
      /eject
      *
      * Poster som er forfalt men ikke saldert
      *
     c                   if        rnbelØ = rnrest
      *
     c                   if        rnrdat < rnfdat
     c                   eval      w_rfda = rnfdat
     c                   else
     c                   eval      w_rfda = rnrdat
     c                   endif
      *
6.11  *
6.11 c     w_rfda        adddur    1 :*d         w_rfda
6.11  *
     c                   eval      w_rtda = w_kdat
     c                   eval      w_rgrl = rnrest
     c                   exsr      beregn
      *
      * Sjekk mot antall løpedager
      *
     c                   if        wdg <= ra6pld
     c                   goto      nypost
     c                   endif
      *
      * Sjekk mot rentegrense faktura
      *
     c                   if        rnrest > 0
     c                   if        w_rent < ra6pfg or
     c                             w_rent = 0
     c                   goto      nypost
     c                   endif
T5.60c                   else
T5.60c                   if        w_rent  * -1 < ra6pfg
T5.60c                   goto      nypost
T5.60c                   endif
     c                   endif
      *
      * Summer rente for senere kontroll
      *
     c                   eval      w_sumr = w_sumr + w_rent
      *
      * Legg ut transaksjon
      *
     c                   eval      rwrfda = w_rfda
     c                   eval      rwrtda = w_rtda
     c                   eval      rwrent = w_rent
     c                   eval      rwdagr = wdg
     c                   eval      rwrgrl = w_rgrl
      *
      * Teller/Timestamp
      *
     c                   eval      w_tell = w_tell + 1
     c                   move      w_tell        w_tell_alf
     c                   time                    rwtist
     c     *iso0         move      rwtist        w_tmst_20
     c                   move      w_tell_alf    w_tmst_20
     c     *iso0         move      w_tmst_20     rwtist
      *
     c                   write     rkwbpfr
     c                   goto      nypost
      *
     c                   endif
      *
      /eject
      *
      * Poster som er forfalt og delvis saldert uten referansesetting
      *
     c                   if        rnbelØ <> rnrest and
     c                             rnrefn = 0 and
     c                             rnrest <> 0
      *
     c                   if        rnrdat < rnfdat
     c                   eval      w_rfda = rnfdat
     c                   else
     c                   eval      w_rfda = rnrdat
     c                   endif
      *
     c                   eval      w_rtda = w_kdat
     c                   eval      w_rgrl = rnrest
     c                   exsr      beregn
      *
      * Sjekk mot antall løpedager
      *
     c                   if        wdg <= ra6pld
     c                   goto      nypost
     c                   endif
      *
      * Sjekk mot rentegrense faktura
      *
     c                   if        rnrest > 0
     c                   if        w_rent < ra6pfg or
     c                             w_rent = 0
     c                   goto      nypost
     c                   endif
T5.60c                   else
T5.60c                   if        w_rent  * -1 < ra6pfg
T5.60c                   exsr      oppdat_post
T5.60c                   goto      nypost
T5.60c                   endif
     c                   endif
      *
      * Summer rente for senere kontroll
      *
     c                   eval      w_sumr = w_sumr + w_rent
      *
      * Legg ut transaksjon
      *
     c                   eval      rwrfda = w_rfda
     c                   eval      rwrtda = w_rtda
     c                   eval      rwrent = w_rent
     c                   eval      rwdagr = wdg
     c                   eval      rwrgrl = w_rgrl
      *
      * Teller/Timestamp
      *
     c                   eval      w_tell = w_tell + 1
     c                   move      w_tell        w_tell_alf
     c                   time                    rwtist
     c     *iso0         move      rwtist        w_tmst_20
     c                   move      w_tell_alf    w_tmst_20
     c     *iso0         move      w_tmst_20     rwtist
      *
     c                   write     rkwbpfr
     c                   goto      nypost
      *
     c                   endif
      *
      * Poster som er forfalt og saldert helt eller delvis
      *
     c                   if        rnbelØ <> rnrest and
     c                             rnrefn <> 0
      *
     c                   exsr      les_motpost
      *
     c                   endif
      *
     c     nypost        tag
      *
     c     rktrl2_key    reade     rktrl2                                 61
     c                   enddo
      *
     c                   endsr
      *
      /eject
      *
     c     les_motpost   begsr
      *
     c                   eval      w_rgrl = rnbelØ
     c                   eval      w_fmot = 0
      *
      * Sett fra dato
      *
     c                   if        rnrdat > *loval
     c                   eval      w_rfda = rnrdat
     c                   eval      w_rtda = rnrdat
     c                   else
     c                   eval      w_rfda = rnfdat
     c                   eval      w_rtda = rnfdat
     c                   endif
      *
      * Finn motpost for salderingen
      *
      * Leser først for å sjekke om det finnes motposter med bilnr. som refnr.
      *
     c                   eval      rktrlc_kund = rkkund
     c                   eval      rktrlc_refn = rnbiln
     c     rktrlc_key    setll     rktrlc                                 91
     c     rktrlc_key    reade     rktrlc                                 91
      *
     c                   dow       *in91 = *off
      *
     c                   eval      w_fmot = 1
     c                   eval      w_refn = xnbiln
      *
      * Hvis dette er et samme bilag som er oppgitt som refnr
      * skal ikke posten leses på nytt
      *
     c                   if        xnbiln = rnrefn
     c                   eval      w_fmot = 2
     c                   endif
      *
     c                   exsr      beh_motpost
      *
     c     rktrlc_key    reade     rktrlc                                 91
     c                   enddo
      *
     c                   if        w_rgrl <> 0
      *
      * Dersom ikke ferdig saldert, sjekk om det finnes motpost med refnr som bilagsnr.
      *
     c                   eval      rktrl8_kund = rkkund
     c                   eval      rktrl8_biln = rnrefn
     c     rktrl8_key    setll     rktrl8                                 92
     c     rktrl8_key    reade     rktrl8                                 92
      *
     c                   dow       *in92 = *off
      *
      * Bilaget er lest tidligere og skal ikke behandles på nytt
      *
     c                   if        w_fmot = 2 and
     c                             xnbiln = rnrefn
     c                   goto      neste_mot
      *
     c                   else
      *
     c                   eval      w_fmot = 1
     c                   eval      w_refn = xnbiln
      *
     c                   exsr      beh_motpost
      *
     c                   endif
      *
     c     neste_mot     tag
      *
     c     rktrl8_key    reade     rktrl8                                 92
      *
     c                   enddo
      *
     c                   endif
      *
      * Ikke beregn rente dersom restbeløp = 0 og ingen motpost funnet
      *
     c                   if        w_fmot = 0 and
     c                             rnrest = 0
     c                   exsr      oppdat_post
     c                   goto      end_les_motp
     c                   endif
      *
      * Ikke beregn rente dersom rentegrunnlaget er 0 eller negativt
      *
     c                   if        w_rgrl <= 0
     c                   goto      end_les_motp
     c                   endif
      *
      * Ikke beregn rente dersom postert beløp < 0 og rentegrunnlag > 0
      *
     c                   if        rnbelØ <  0  and
     c                             w_rgrl >  0
     c                   goto      end_les_motp
     c                   endif
      *
      * Dersom rentegrl. <> restbeløp, sett rentegrl. = restbelø
      *
     c                   if        w_rgrl <> rnrest
     c***                eval      w_rgrl = rnrest
     c                   endif
      *
      * Beregn rente på restbeløp
      *
     c     w_rtda        adddur    1 :*d         w_rfda
     c                   eval      w_rtda = w_kdat
      *
      * Beregn rente
      *
     c                   exsr      beregn
      *
      * Sjekk mot antall løpedager og rentegrense
T5.60 *
     c                   if        wdg <= ra6pld or
     c                             w_rent < ra6pfg or
     c                             w_rent = 0
     c                   goto      end_les_motp
T5.60c                   endif
      *
      * Summer rente for senere kontroll
      *
     c                   eval      w_sumr = w_sumr + w_rent
      *
      * Legg ut postering
      *
     c                   eval      rwrfda = w_rfda
     c                   eval      rwrtda = w_rtda
     c                   eval      rwrent = w_rent
     c                   eval      rwdagr = wdg
     c                   eval      rwrgrl = w_rgrl
      *
     c                   eval      x_refn = rnrefn
     c                   eval      rnrefn = w_refn
      *
      * Teller/Timestamp
      *
     c                   eval      w_tell = w_tell + 1
     c                   move      w_tell        w_tell_alf
     c                   time                    rwtist
     c     *iso0         move      rwtist        w_tmst_20
     c                   move      w_tell_alf    w_tmst_20
     c     *iso0         move      w_tmst_20     rwtist
      *
     c                   write     rkwbpfr
      *
     c                   eval      rnrefn = x_refn
      *
     c     end_les_motp  endsr
      *
      /eject
      *
     c     beh_motpost   begsr
      *
      * Hvis bilagsdato > kjøredato skal ikke posten behandles
      *
     c                   if        xnbdat > w_kdat
     c                   goto      end_beh_motp
     c                   endif
      *
      *
      * Sjekk om posten er eldre enn bilagsdato/rentedato og beløp < 0
      * reduser rentegrunnlaget og les neste post dersom beløpet > 0
      * les neste post
      *
     c                   if        xnbdat < rnbdat or
KOB  c                             xnbdat <= rnrdat
      *
     c                   if        xnbelØ < 0
     c                   eval      w_rgrl = w_rgrl + xnbelØ
      *
      *  Hvis rentegrunnlaget blir < 0, sett det = 0
      *
     c                   if        w_rgrl < 0
     c                   eval      w_rgrl = 0
     c                   endif
      *
     c                   goto      end_beh_motp
     c                   else
     c                   goto      end_beh_motp
     c                   endif
      *
     c                   endif
      *
      * Ikke renteberegnet tidligere
      *
     c                   if        rnrdat = *loval
      *
      * Betalt før/på  forfall  - rentegrunnlag 0 eller negativt
      *  - Ingen renteberegning/Oppdater kundereskontro
      *
     c                   if        xnbdat <= rnfdat and
     c                             w_rgrl <= 0
     c                   exsr      oppdat_post
     c                   goto      end_beh_motp
     c                   endif
      *
      * Betalt før/på  forfall  - rentegrunnlag > 0
      *  - Ingen renteberegning - reduser rentegrunnlaget
      *
     c                   if        xnbdat <= rnfdat and
     c                             w_rgrl > 0
     c                   eval      w_rgrl = w_rgrl + xnbelØ
      *
      *  Hvis rentegrunnlaget blir < 0, sett det = 0
      *
     c                   if        w_rgrl < 0
     c                   eval      w_rgrl = 0
     c                   endif
      *
     c                   goto      end_beh_motp
     c                   endif
      *
     c                   endif
      *
      * Renteberegnet tidligere
      *
     c                   if        rnrdat > *loval
      *
      * Betalt før/på  rentedato - rentegrunnlag 0 eller negativt
      *  - Ingen renteberegning/Oppdater kundereskontro
      *
     c                   if        xnbdat <= rnrdat and
     c                             w_rgrl <= 0
     c                   exsr      oppdat_post
     c                   goto      end_beh_motp
     c                   endif
      *
     c                   endif
      *
      * Beregn rente
      *
     c     w_rfda        adddur    1 :*d         w_rfda
     c                   eval      w_rtda = xnbdat
      *
     c                   exsr      beregn
      *
      * Sjekk mot antall løpedager og rentegrense
      *
T5.60c                   if        w_rent >= 0
      *
     c                   if        wdg <= ra6pld or
     c                             w_rent < ra6pfg or
     c                             w_rent = 0
     c                   eval      w_rgrl = w_rgrl + xnbelØ
      *
      * Sett tilbake rente fra dato
      *
     c                   if        rnrdat > *loval
     c                   eval      w_rfda = rnrdat
     c                   else
     c                   eval      w_rfda = rnfdat
     c                   endif
      *
      *  Antall dager < grenseverdi => posten merkes slik at den ikke tas med ved neste kjøring
      *
     c                   exsr      oppdat_post
     c                   goto      end_beh_motp
     c                   endif
T5.60 *
T5.60c                   else
T5.60 *
T5.60c                   if        w_rent * -1 < ra6pfg
T5.60c                   eval      w_rgrl = w_rgrl + xnbelØ
T5.60 *
T5.60 * Sett tilbake rente fra dato
T5.60 *
T5.60c                   if        rnrdat > *loval
T5.60c                   eval      w_rfda = rnrdat
T5.60c                   else
T5.60c                   eval      w_rfda = rnfdat
T5.60c                   endif
T5.60 *
     c                   goto      end_beh_motp
T5.60c                   endif
      *
T5.60c                   endif
      *
E5.60c                   if        w_rent <> 0
      *
      * Summer rente for senere kontroll
      *
     c                   eval      w_sumr = w_sumr + w_rent
      *
      * Legg ut postering hvis rente er beregnet
      *
     c                   eval      rwrfda = w_rfda
     c                   eval      rwrtda = w_rtda
     c                   eval      rwrent = w_rent
     c                   eval      rwdagr = wdg
     c                   eval      rwrgrl = w_rgrl
      *
     c                   eval      x_refn = rnrefn
     c                   eval      rnrefn = w_refn
      *
      * Teller/Timestamp
      *
     c                   eval      w_tell = w_tell + 1
     c                   move      w_tell        w_tell_alf
     c                   time                    rwtist
     c     *iso0         move      rwtist        w_tmst_20
     c                   move      w_tell_alf    w_tmst_20
     c     *iso0         move      w_tmst_20     rwtist
      *
     c                   write     rkwbpfr
      *
     c                   eval      w_rgrl = w_rgrl + xnbelØ
     c                   eval      w_rfda = w_rtda
      *
     c                   eval      rnrefn = x_refn
      *
     c                   endif
      *
     c     end_beh_motp  tag
      *
     c                   endsr
      *
     c/eject
      *
      * Beregn løpedager og rente
      *
     c     beregn        begsr
      *
      * Beregn rentedager
      *
     c     w_rtda        subdur    w_rfda        wdg : *d
      *
      * Hvis antall rentedager negativ, sett dager = 0
      *
     c                   if        wdg < 0
     c                   eval      wdg = 0
     c                   endif
      *
      * Beregn rente
      *
     c                   if        w_rgrl / 100 * w_rpro / 360 * wdg > 9999999
     c                   eval      w_rent = 9999999
     c                   else
     c                   eval      w_rent = w_rgrl / 100 * w_rpro / 360 * wdg
     c                   endif
      *
     c                   endsr
     c/eject
      *
      * Sletting av poster dersom rentebeløp < rentegrense
      *
     c     slett_post    begsr
      *
     c                   eval      rkwblu_kund = rkkund
     c     rkwblu_key    setll     rkwblu                                 92
     c     rkwblu_key    reade     rkwblu                                 92
      *
     c                   dow       *in92 = *off
      *
     c                   delete    rkwblu
      *
     c     rkwblu_key    reade     rkwblu                                 92
      *
     c                   enddo
      *
     c                   endsr
      *
     c/eject
      *
      * Oppdatering av rentekode på poster som ikke skal renteberegnes
      *
     c     oppdat_post   begsr
      *
     c                   eval      rktrlu_kund = rnkund
     c                   eval      rktrlu_bdat = rnbdat
     c                   eval      rktrlu_tist = rntist
     c     rktrlu_key    chain     rktrlu
      *
     c                   if        %found and
     c                             rnrenk = 0 and
     c                             rnrest = 0
     c                   eval      rnrenk = 1
6.10 c                   time                    rnedat
6.10 c                   time                    rnetim
6.10 c                   eval      rneusr = l_user
     c                   update    rktrlur
     c                   endif
      *
     c                   endsr
      *
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c                   eval      w_firm = l_firm
     c                   eval      n = *zeros
     c                   eval      w_rpro= *zeros
      *
     c     *dmy          move      pkdat         w_kdat
      *
      * Key for les av kundeposter
      *
     c     rktrl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rktrl2_kund
      *
      * Key for lesing av motposter
      *
     c     rktrlc_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rktrlc_kund
     c                   kfld                    rktrlc_refn
      *
      * Key for lesing av motposter
      *
     c     rktrl8_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rktrl8_kund
     c                   kfld                    rktrl8_biln
      *
      * Key for oppdatering av rentekode
      *
     c     rktrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rktrlu_kund
     c                   kfld                    rktrlu_bdat
     c                   kfld                    rktrlu_tist
      *
      * Key for kunderegister
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for statuskode 6
      *
     c     raa6l1_key    klist
     c                   kfld                    w_firm
      *
      * Key for bilagskoder
      *
     c     ra01l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra01l1_akod
      *
      * Key for kundekategori
      *
     c     ra11l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra11l1_kkod
      *
      * Key for statuskode 3
      *
     c     raa3l1_key    klist
     c                   kfld                    w_firm
      *
      * Key for lesing av renteposter
      *
     c     rkwblu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkwblu_kund
      *
     c                   eval      w_firm = l_firm
      *
      * Sjekk om kontovalg utfylt
      *
     c                   eval      tstkt = %xfoot(kun)
      *
      * Hent verdier fra koderegister
      *
     c     raa6l1_key    chain     raa6l1                             99
      *
     c     raa3l1_key    chain     raa3l1                             99
      *
     c                   endsr
