      /title  ASLAGR Håndterminal ordre-hode, innlasting - PSI
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : HO702R
      * Beskrivelse . . : Håndterminal ordre-hode, innlasting - PSI
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       050600 HKO  Nytt program
      *       280900 HKO  Tilpasset nye filer
      *       230301 HKO  Fjernet merknad
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fhbtrpf    if   e           k disk
     fhohelu    uf a e           k disk    rename(hohepfr:hohelur)
     fhodtlu    uf a e           k disk    rename(hodtpfr:hodtlur)
      *
      * Local Data Area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Datastruktur - håndterminalfil
      *
     d                 ds
     d d_ht                          73
     d  d_ht_head                    25    overlay(d_ht)
     d  d_ht_dato                     6  0 overlay(d_ht:26)
     d  d_ht_sep1                     1    overlay(d_ht:32)
     d  d_ht_time                     4  0 overlay(d_ht:33)
     d  d_ht_sep2                     1    overlay(d_ht:37)
     d  d_ht_kund                     6  0 overlay(d_ht:38)
     d  d_ht_sep3                     1    overlay(d_ht:44)
     d  d_ht_refn                     6  0 overlay(d_ht:45)
     d  d_ht_sep4                     1    overlay(d_ht:51)
     d  d_ht_vare                     8    overlay(d_ht:52)
     d  d_ht_sep5                     1    overlay(d_ht:60)
     d  d_ht_enhe                     3    overlay(d_ht:61)
     d  d_ht_sep6                     1    overlay(d_ht:64)
     d  d_ht_anta                     9    overlay(d_ht:65)
      *
      * Definering av variable
      *
     d w_firm          s                   like(hofirm)
     d w_anta_alf      s              9
     d w_time          s              6  0
     d w_refn          s              6  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser alle rader i håndterminalfilen
      *
     c                   read      hbtrpf                                 90
     c                   dow       *in90 = *off
      *
     c                   exsr      behandling
      *
     c                   read      hbtrpf                                 90
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av innlest rad
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
     c                   eval      d_ht = hoinfo
      *
     c                   if        d_ht_vare = *blank
     c                   goto      end_behandl
     c                   endif
      *
      * Oppretter linje i håndterminal ordre-hode
      *
     c                   if        w_refn <> d_ht_refn
      *
     c                   eval      hofirm = w_firm
     c                   eval      horefn = d_ht_refn
     c                   eval      hokund = d_ht_kund
     c                   eval      hoselg = 0
     c                   eval      hostat = 0
      *
     c                   time                    hodato
     c                   time                    hotime
      *
     c                   write     hohelur
      *
     c                   eval      w_refn = d_ht_refn
      *
     c                   endif
      *
      * Oppretter linje i håndterminal ordre-linje
      *
     c                   eval      hdfirm = w_firm
     c                   eval      hdrefn = d_ht_refn
     c                   movel     d_ht_vare     hdvare
     c                   eval      hdenhe = d_ht_enhe
      *
     c                   eval      w_anta_alf = %subst(d_ht_anta:1:6) +
     c                                          %subst(d_ht_anta:8:2) + '0'
     c                   move      w_anta_alf    hdanta
      *
     c     *ymd          move      d_ht_dato     hddato
     c                   movel     d_ht_time     w_time
     c     *hms          move      w_time        hdtime
      *
     c                   write     hodtlur
      *
     c     end_behandl   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Henter inn firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
