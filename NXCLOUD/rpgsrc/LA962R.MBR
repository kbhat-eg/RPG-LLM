     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LA962R
      *  Beskrivelse . . : Oppdatering av lager-register utfra lagerbok
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      *  --------- ------  ------------------------------------------------
6.10  *  R6.10     110110  Lagt inn sporingsinformasjon            BSA
6.11  *  R6.10     260213  J-justering skal fungere som telling    bof
6.20  *  R6.20     210115  Telling skal ogås bruke antallet som    bsa
6.20  *                    beholdning.                             bsa
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     fvlagl2    uf   e           k disk    rename(vlagpfr:vlagl2r)
     fvhisl1    uf   e           k disk    rename(vhispfr:vhisl1r)
      *
      * Parameter-inn
      *
     d                 ds
     d d_prec                        64
     d  d_lage                        2  0 overlay(d_prec)
     d  d_dato                       10d   datfmt(*iso)
     d                                     overlay(d_prec:3)
     d  d_vare                        8  0 overlay(d_prec:13)
      *
      * Local Data Area
      *
     d                uds
6.10 d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variable i nøkler
      *
     d vlagl2_lage     s                   like(vllage)
     d vlagl2_vare     s                   like(vlvare)
     d vhisl1_lage     s                   like(vhlage)
     d vhisl1_vare     s                   like(vhvare)
     d vhisl1_dato     s                   like(vhdato)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vlfirm)
     d w_behl          s                   like(vlbehl)
     d w_vare          s                   like(vlvare)
     d w_parm          s             64
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sjekker om oppdatering av en vare eller hele lageret
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   if        d_vare <> 0
     c                   goto      en_vare
     c                   else
     c                   goto      hele_lager
     c                   endif
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Leser varer i lager register for oppdatering - bare en vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     en_vare       tag
      *
     c     vlagl2_key    setll     vlagl2
      *
     c     vlagl2_key    reade     vlagl2r
     c                   dow       not %eof
      *
     c                   if        w_vare <> *blank and
     c                             w_vare <> vlvare
     c                   leave
     c                   endif
      *
     c                   exsr      les_lagbok
      *
     c                   eval      vlbehl = w_behl
6.10 c                   time                    vledat
6.10 c                   time                    vletim
6.10 c                   eval      vleusr = l_user
     c                   update    vlagl2r
      *
     c     vlagl2_key    reade     vlagl2r
     c                   enddo
      *
     c                   goto      end_pgm
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Leser varer i lager register for oppdatering - hele lageret
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hele_lager    tag
      *
     c     vlagl2_ke2    setll     vlagl2
      *
     c     vlagl2_ke2    reade     vlagl2r
     c                   dow       not %eof
      *
     c                   exsr      les_lagbok
      *
     c                   eval      vlbehl = w_behl
6.10 c                   time                    vledat
6.10 c                   time                    vletim
6.10 c                   eval      vleusr = l_user
     c                   update    vlagl2r
      *
     c     vlagl2_ke2    reade     vlagl2r
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å håndtere poster i lagerboka
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_lagbok    begsr
      *
      * Finn riktig startpunkt og beholdning
      *
     c                   eval      w_behl = 0
     c                   eval      vhisl1_vare = vlvare
     c     vhisl1_key1   setll     vhisl1
     c     vhisl1_key1   reade     vhisl1
     c                   if        %eof
     c     vhisl1_key1   setll     vhisl1
     c     vhisl1_key2   readpe    vhisl1
     c                   if        %eof
     c     vhisl1_key1   setll     vhisl1
     c     vhisl1_key2   reade     vhisl1
     c                   endif
     c                   endif
      *
     c                   if        not %eof
     c                   eval      w_behl = vhbehl
      *
      * Les i gjennom varen og finn sluttsaldo
      *
     c     vhisl1_key2   reade     vhisl1r
     c                   dow       not %eof
      *
6.11 c                   if        vhkode = 'T ' or
6.11 c                             vhkode = 'J '
6.11 c*6.20              if        vhkode = 'J'
6.11 c                   eval      w_behl = vhanta
6.11 c*6.20              else
6.20 c*                  eval      w_behl = vhbehl
6.11 c*6.20              endif
     c                   else
     c                   if        vhtype = 'I'
     c                   eval      w_behl = w_behl + vhanta
     c                   else
     c                   eval      w_behl = w_behl - vhanta
     c                   endif
     c                   endif
      *
     c                   eval      vhbehl = w_behl
6.10 c                   time                    vhedat
6.10 c                   time                    vhetim
6.10 c                   eval      vheusr = l_user
     c                   update    vhisl1r
      *
     c     vhisl1_key2   reade     vhisl1r
     c                   enddo
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for LAGER  pr vare
      *
     c     vlagl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl2_lage
     c                   kfld                    vlagl2_vare
      *
      *  Key for LAGER  pr lager
      *
     c     vlagl2_ke2    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl2_lage
      *
      *  Key for les av HISTORISK-LAGERBOK
      *
     c     vhisl1_key1   klist
     c                   kfld                    w_firm
     c                   kfld                    vhisl1_lage
     c                   kfld                    vhisl1_vare
     c                   kfld                    vhisl1_dato
      *
     c     vhisl1_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vhisl1_lage
     c                   kfld                    vhisl1_vare
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    w_parm
     c                   eval      d_prec = w_parm
      *
      *  Legge inn startverdier
      *
     c                   eval      w_firm = l_firm
     c                   eval      vhisl1_lage = d_lage
     c                   eval      vhisl1_dato = d_dato
     c                   eval      vlagl2_lage = d_lage
     c                   if        d_vare <> 0
     c                   movel     d_vare        w_vare
     c                   eval      vlagl2_vare = w_vare
     c                   else
     c                   eval      vlagl2_vare = *blank
     c                   endif
      *
     c                   endsr
