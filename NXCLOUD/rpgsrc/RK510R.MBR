     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RK510R
      * Beskrivelse . . : Kundeposteringer, spørring bilag/beløp
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       050802 KOB  Nytt program
      *       250603 KOB  Lagt inn dato for purring/rente i bildet
      *       250603 KOB  Lagt tekst avdeling/selger i bildet
      * V5.30 121004 HFL  Beløp utvidet til 11.2.
      * V5.30 141004 KOB  Nullstilt nøkkel etter eof
      * V5.30 181004 KOB  Lagt inn dato som nytt posisjoneringsfelt
      * V5.40 220305 KOB  Lagt inn raar i logisk file rktrl4
      * V5.40 220305 KOB  Lagt inn ny logisk file rktrlb (raar/biln)
      * V5.60 030308 HFL  Tatt vekk raar i logisk file rktrl4
      * V5.60 120308 HFL  Logisk file rktrl3 for bilagnr. uansett år.
7.xx  * 7.xx  030516 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frktrlb    if   e           k disk    rename(rktrpfr:rktrlbr)
T5.60frktrl3    if   e           k disk    rename(rktrpfr:rktrl3r)
     frktrl4    if   e           k disk    rename(rktrpfr:rktrl4r)
     frktrla    if   e           k disk    rename(rktrpfr:rktrlar)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     frktrl2    if   e           k disk    rename(rktrpfr:rktrl2r)
      *
     frk510d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d rktrlb_raar                         like(rnraar)
     d rktrlb_biln                         like(rnbiln)
T5.60d rktrl3_biln                         like(rnbiln)
S5.60d*rktrl4_raar                         like(rnraar)
     d rktrl4_belØ     s                   like(rnbelØ)
     d rktrla_bdat     s                   like(rnbdat)
     d rkunl1_kund     s                   like(rnkund)
     d rktrl2_kund     s                   like(rnkund)
     d rktrl2_bdat     s                   like(rnbdat)
     d rktrl2_tist     s                   like(rntist)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_valg_ok       s              1    inz(*off)
      *
     d p_stat          s              1
     d p_gkod          s                   like(rkavde)
     d p_ikod          s                   like(rkslgr)
     d p_gtxt          s             20
     d p_itxt          s             20
      *
     d w_firm          s                   like(rnfirm)
     d w_biln          s                   like(rnbiln)                                      .
     d w_belØ          s                   like(rnbelØ)                                      .
     d w_bdat          s                   like(rnbdat)                                      .
     d w_raar          s                   like(rnraar)                                      .
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(13)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * d_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2biln <> 0 or
     c                             b2belØ <> 0 or
     c                             b2bdat <> 0 or
     c                             b2raar <> w_raar
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        d_fcrn <> 0
     c     d_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
S5.60c*                  eval      rktrl4_raar = b2raar
     c                   eval      rktrl4_belØ = b1belØ
     c     rktrl4_key    setll     rktrl4
     c                   when      w_seqe = 'N'
     c                   eval      rktrlb_raar = b2raar
     c                   eval      rktrlb_biln = b1biln
     c     rktrlb_key    setll     rktrlb
T5.60c                   when      w_seqe = 'M'
T5.60c                   eval      rktrl3_biln = b1biln
T5.60c     rktrl3_key    setll     rktrl3
     c                   other
     c                   eval      rktrla_bdat = b1bdat
     c     rktrla_key    setll     rktrla
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      *
      * Posisjoner startverdi, regnskapsår endret
      *
     c                   if        b2raar <> w_raar                                          .
     c                   eval      w_raar = b2raar                                           .
      *
     c                   if        b2biln = 0 and
     c                             b2belØ = 0 and
     c                             b2bdat = 0
     c                   eval      b2biln = 1
     c                   endif
      *                                                                 .
     c                   endif                                                           .....
      *
      * Bilagsnummer
      *
     c                   if        b2biln <> 0
     c                   eval      w_biln = b2biln
     c                   eval      w_belØ = 0
     c                   eval      w_bdat = *loval
T5.60c                   if        b2raar <> 0
     c                   eval      w_seqe = 'N'
     c                   eval      rktrlb_raar = b2raar
     c                   eval      rktrlb_biln = b2biln
     c     rktrlb_key    setll     rktrlb
T5.60c                   else
T5.60c                   eval      w_seqe = 'M'
T5.60c                   eval      rktrl3_biln = b2biln
T5.60c     rktrl3_key    setll     rktrl3
T5.60c                   endif
     c                   goto      end_pos
     c                   endif
      *
      * Beløp
      *
     c                   if        b2belØ <> 0
     c                   eval      w_seqe = 'B'
     c                   eval      w_belØ = b2belØ
     c                   eval      w_biln = 0
     c                   eval      w_bdat = *loval
S5.60c*                  eval      rktrl4_raar = b2raar
     c                   eval      rktrl4_belØ = b2belØ
     c     rktrl4_key    setll     rktrl4
     c                   goto      end_pos
     c                   endif
      *
      * Bilagsdato
      *
     c                   if        b2bdat <> 0
     c                   eval      w_seqe = 'D'
     c     *dmy          move      b2bdat        w_bdat
     c                   eval      w_biln = 0
     c                   eval      w_belØ = 0
     c     *dmy          move      b2bdat        rktrla_bdat
     c     rktrla_key    setll     rktrla
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2biln = 0
     c                   eval      b2belØ = 0
     c                   eval      b2bdat = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = d_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   exsr      xc2win                                                  . .
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      rktrl4
     c                   when      w_seqe = 'N'
     c                   read      rktrlb
T5.60c                   when      w_seqe = 'M'
T5.60c                   read      rktrl3
     c                   other
     c                   read      rktrla
     c                   endsl
      *
     c                   if        %eof or
     c                             rnfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *                                                                 .
     c                   if        b2raar <> *zero and                                 ..... .
     c                             b2raar <> rnraar
     c                   iter                                                              . .
     c                   endif                                                         ..... .
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      rktrl4_belØ = rnbelØ
     c                   when      w_seqe = 'N'
     c                   eval      rktrlb_biln = rnbiln
T5.60c                   when      w_seqe = 'M'
T5.60c                   eval      rktrl3_biln = rnbiln
     c                   other
     c                   eval      rktrla_bdat = rnbdat
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1bdat = rnbdat
     c                   eval      b1tist = rntist
     c                   eval      b1biln = rnbiln
     c                   eval      b1biln = rnbiln
     c     *dmy          move      rnbdat        b1bida
     c                   eval      b1bilk = rnbilk
     c                   eval      b1text = rntext
     c                   eval      b1belØ = rnbelØ
     c                   eval      b1kund = rnkund
      *                                                                 .
      * Hent kunde                                                      .
      *                                                                 .
     c                   eval      rkunl1_kund = rnkund                                      .
     c     rkunl1_key    chain     rkunl1                             61                     .
     c                   if        *in61 = *off                                              .
     c                   eval      b1navn = rknavn                                           .
     c                   endif                                                               .
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'B'
S5.30c*                  eval      rktrl4_raar = b2raar
     c     rktrl4_key    setgt     rktrl4
     c                   readp     rktrl4
     c                   when      w_seqe = 'N'
T5.30c                   eval      rktrlb_biln = 0
     c     rktrlb_key    setgt     rktrlb
     c                   readp     rktrlb
T5.60c                   when      w_seqe = 'M'
T5.60c                   eval      rktrl3_biln = 0
T5.60c     rktrl3_key    setgt     rktrl3
T5.60c                   readp     rktrl3
     c                   other
T5.30c                   eval      rktrla_bdat = *loval
     c     rktrla_key    setgt     rktrla
     c                   readp     rktrla
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   readp     rktrl4
     c                   when      w_seqe = 'N'
     c                   readp     rktrlb
T5.60c                   when      w_seqe = 'M'
T5.60c                   readp     rktrl3
     c                   other
     c                   readp     rktrla
     c                   endsl
      *
     c                   if        %eof or
     c                             rnfirm <> w_firm
     c                   leave
     c                   endif
      *                                                                 .
     c                   if        b2raar <> *zero and                                 ..... .
     c                             b2raar <> rnraar
     c                   iter                                                              . .
     c                   endif                                                         ..... .
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
S5.60c*                  eval      rktrl4_raar = b2raar
     c                   eval      rktrl4_belØ = rnbelØ
     c                   when      w_seqe = 'N'
     c                   eval      rktrlb_raar = b2raar
     c                   eval      rktrlb_biln = rnbiln
T5.60c                   when      w_seqe = 'M'
T5.60c                   eval      rktrl3_biln = rnbiln
     c                   other
     c                   eval      rktrla_bdat = rnbdat
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'B'
     c     rktrl4_key    setll     rktrl4
     c                   when      w_seqe = 'N'
     c     rktrlb_key    setll     rktrlb
T5.60c                   when      w_seqe = 'M'
T5.60c     rktrl3_key    setll     rktrl3
     c                   other
     c     rktrla_key    setll     rktrla
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * C2WIN Behandle bilde for vis                                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xc2win        begsr
      *
     c                   eval      rktrl2_kund = b1kund
     c                   eval      rktrl2_bdat = b1bdat
     c                   eval      rktrl2_tist = b1tist
     c     rktrl2_key    chain     rktrl2                             60
      *
     c                   move      rnbiln        c2biln                                      .
     c                   move      rnbilk        c2bilk                                      .
     c                   move      rntext        c2text                                      .
     c     *dmy          move      rnbdat        c2bdat                                      .
     c     *dmy          move      rnfdat        c2fdat                                      .
     c                   move      rnrper        c2rper                                      .
     c                   move      rnraar        c2raar                                      .
     c                   move      rnrekl        c2rekl                                      .
     c                   move      rnjour        c2jour                                      .
     c                   move      rnrefn        c2refn                                      .
     c                   move      rnbiln        c2biln                                      .
     c                   move      rnbelØ        c2belØ                                      .
     c                   move      rnrest        c2rest                                      .
     c                   move      rnvalk        c2valk                                      .
     c                   move      rnvalb        c2valb                                      .
     c                   move      rnvalr        c2valr                                      .
      *
     c                   move      rnantp        c2antp
      *
     c                   if        rnpdat = *loval
     c                   eval      c2pdat = 0
     c                   else
     c     *dmy          move      rnpdat        c2pdat
     c                   endif
      *
     c                   move      rnrenk        c2renk
      *
     c                   if        rnrdat = *loval
     c                   eval      c2rdat = 0
     c                   else
     c     *dmy          move      rnrdat        c2rdat
     c                   endif
      *
     c                   move      rninko        c2inko
      *
     c                   if        rnidat = *loval
     c                   eval      c2idat = 0
     c                   else
     c     *dmy          move      rnidat        c2idat
     c                   endif
      *
      * Hent avdelingstekst
      *
     c                   move      rnavde        c2avde                                      .
      *
     c                   if        rnavde <> *zero
     c                   move      rnavde        p_gkod
     c                   call      'RS707R'                                      avdelings-
     c                   parm                    p_stat                           kode
     c                   parm                    p_gkod                           kode
     c                   parm                    p_gtxt                           kode
      *
     c                   movel     p_gtxt        c2gtxt
     c                   endif
      *
     c                   move      rnslgr        c2slgr                                      .
      *
      * Hent selgertekst
      *
     c                   if        rnslgr <> *zero
     c                   move      rnslgr        p_ikod
     c                   call      'RS709R'
     c                   parm                    p_stat
     c                   parm                    p_ikod
     c                   parm                    p_itxt
      *
     c                   movel     p_itxt        c2itxt
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c2taga
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   endsr
      *
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på bilagsnummer
      *
     c     rktrlb_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rktrlb_raar
     c                   kfld                    rktrlb_biln
      *
T5.60 * Key for posisjonering på bilagsnummer
      *
T5.60c     rktrl3_key    klist
T5.60c                   kfld                    w_firm
T5.60c                   kfld                    rktrl3_biln
      *
      * Key for posisjonering på beløp
      *
     c     rktrl4_key    klist
     c                   kfld                    w_firm
S5.60c*                  kfld                    rktrl4_raar
     c                   kfld                    rktrl4_belØ
      *
      * Key for posisjonering på dato
      *
     c     rktrla_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rktrla_bdat
      *
      * Key for kunderegister via kundenummer
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for lesing av postering
      *
     c     rktrl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rktrl2_kund
     c                   kfld                    rktrl2_bdat
     c                   kfld                    rktrl2_tist
      *
      * Henter parametre, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_raar = *year
     c                   eval      b2raar = *year
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      w_seqe = 'N'
S5.60c*                  eval      rktrl4_raar = *year
     c                   eval      rktrlb_raar = *year
     c                   eval      rktrlb_biln = 0
     c     rktrlb_key    setll     rktrlb
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
