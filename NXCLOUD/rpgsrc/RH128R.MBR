      /TITLE  Omberegning av budsjett
     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RH128R                                              *
      * Beskrivelse . . : Omberegning av budsjett                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
6.10  * V6.10     210909  Lagt inn sporingsfelter                        BSA  *
6.31  * 6.30  040719 bof  utvidet tabell i hht. utvidelse i RHSAPF
      *                                                                       *
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frhsal1    if   e           k disk    rename(rhsapfr:rhsal1r)
     frhovlr    if   e           k disk    rename(rhovpfr:rhovlrr)
     fra13lr    if   e           k disk    rename(ra13pfr:ra13lrr)
     frhsalu    uf a e           k disk    rename(rhsapfr:rhsalur)
      *
      *+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      *               T   A   B   E   L   L   E   R                      +
      *+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      *
6.31 d sal             s             15  2 dim(14)                              Saldo
     d gsa             s             11  2 dim(14)                              Saldo forrige record
     d nsa             s             11  2 dim(14)                              Saldo forrige record
     d isa             s             11  2 dim(14)                               SALDOMATRISE
     d mfo             s              9  6 dim(13)                               FORDELINGSNØKLER
      *
      /eject
      *- - - - - - - - -*
      *  DATA STRUCTURE *
      *- - - - - - - - -*
      *
      *  Definisjon av bruddbegrep
      *
     d                 ds
     d nybrd                         13  0                                      Bruddb
     d  nyavd                         4  0 overlay(nybrd)                       Avdeli
     d  nykto                         5  0 overlay(nybrd:5)                     Konto
     d  nysps                         4  0 overlay(nybrd:10)                    Spesif
      *
     d                 ds
     d gmbrd                         13  0                                      Bruddb
     d  gmavd                         4  0 overlay(gmbrd)                       Avdeli
     d  gmkto                         5  0 overlay(gmbrd:5)                     Konto
     d  gmsps                         4  0 overlay(gmbrd:10)                    Spesif
      *
     d                uds
     d rhpaar                300    303  0
     d rhpavf                         4  0
     d rhpavt                         4  0
     d rhpspf                         4  0
     d rhpspt                         4  0
     d rhpktf                         5  0
     d rhpktt                         5  0
     d rhpnok                         2  0
     d rhptyp                         1
      *
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d w_firm          s                   like(rifirm)
     d w_raar          s                   like(riraar)
     d w_type          s                   like(ritype)
     d w_bnok          s                   like(ramkod)
     d w_tbud          s             13  2
      *
     d akkbel          s             13  0
     d akkpro          s             11  6
     d work2           s             11  0
     d n               s              2  0
      *
     d rhovlr_kont     s                   like(rikont)
     d rhovlr_spes     s                   like(rispes)
     d rhovlr_avde     s                   like(riavde)
     d rhsalu_kont     s                   like(rikont)
     d rhsalu_spes     s                   like(rispes)
     d rhsalu_avde     s                   like(riavde)
     d rhsalu_raar     s                   like(riraar)
     d rhsalu_type     s                   like(ritype)
     d ra13lr_mkod     s                   like(ramkod)
      *
     d p_skod          s              2
     d p_tkod          s              1
      *
      * Saldo-register - kuntorekkefølge
      *
     irhsal1r
     i              risa01                      sal(01)
     i              risa02                      sal(02)
     i              risa03                      sal(03)
     i              risa04                      sal(04)
     i              risa05                      sal(05)
     i              risa06                      sal(06)
     i              risa07                      sal(07)
     i              risa08                      sal(08)
     i              risa09                      sal(09)
     i              risa10                      sal(10)
     i              risa11                      sal(11)
     i              risa12                      sal(12)
     i              risa13                      sal(13)
     i              risa00                      sal(14)
      *
      /eject
      *
     c     dummy         tag
      *
      * Les saldo-records
      *
     c                   move      l_firm        w_firm
      *
     c     rhsal1_key    setll     rhsal1r                                60
     c     rhsal1_key    reade     rhsal1r                                60
      *
     c                   dow       *in60 = *off                                          .....
      *
     c                   move      riavde        nyavd
     c                   move      rikont        nykto
     c                   move      rispes        nysps
     c                   move      riraar        w_raar
     c                   move      ritype        w_type
      *
      * Sjekk kontonr. fra/til
      *
     c                   if        nykto < rhpktf or                                     .....
     c                             nykto > rhpktt                                        .....
     c                   goto      neste                                                     .
     c                   endif                                                           .....
      *
      * Sjekk kostnadsbærer fra/til
      *
     c                   if        nysps < rhpspf or                                     .....
     c                             nysps > rhpspt                                        .....
     c                   goto      neste                                                     .
     c                   endif                                                           .....
      *
      * Sjekk avdeling fra/til
      *
     c                   if        nyavd < rhpavf or                                     .....
     c                             nyavd > rhpavt                                        .....
     c                   goto      neste                                                     .
     c                   endif                                                           .....
      *
      * Sjekk om riktig år
      *
     c                   if        rhpaar <> w_raar                                      .....
     c                   goto      neste                                                     .
     c                   endif                                                           .....
      *
      * Sjekk hvilke record-type skal behandles
      *
     c                   if        rhptyp = 'B' and                             ALT.BUDSJ.....
     c                             w_type <> 3                                               .
     c                   goto      neste                                                     .
     c                   endif                                                           .....
      *
     c                   if        rhptyp = 'A' and                             ALT.BUDSJ.....
     c                             w_type <> 4                                               .
     c                   goto      neste                                                     .
     c                   endif                                                           .....
      *
      * Første gang - ikke test på brudd
      *
     c                   if        gmbrd = *zeros                                        .....
     c                   move      nybrd         gmbrd                                       .
     c                   move      sal           gsa                                         .
     c                   goto      neste                                                     .
     c                   endif                                                           .....
      *
      * Sjekk konto
      * Konto-spes-avd
      *
     c                   eval      rhovlr_kont = gmkto                                       .
     c                   eval      rhovlr_spes = gmsps                                       .
     c                   eval      rhovlr_avde = gmavd                                       .
     c     rhovlr_key    chain     rhovlrr                            61
      *
      * Konto-spes
      *
     c                   if        *in61 = *on                                           .....
     c                   eval      rhovlr_avde = *zeros                                      .
     c     rhovlr_key    chain     rhovlrr                            61                     .
     c                   endif                                                           .....
      *
      * Konto
      *
     c                   if        *in61 = *on                                           .....
     c                   eval      rhovlr_spes = *zeros                                      .
     c     rhovlr_key    chain     rhovlrr                            61                     .
     c                   endif                                                           .....
      *
     c                   if        *in61 = *on                                           .....
     c                   goto      neste                                                     .
     c                   endif                                                           .....
      *
      * Sjekk om riktig budsjettnøkkel
      *
     c                   if        rhpnok <> *zeros and                                  .....
     c                             rhbnØk <> rhpnok                                      .....
     c                   goto      neste                                                     .
     c                   endif                                                           .....
      *
      *  Behandle valgt record
      *
     c                   exsr      behand
      *
      * Flytt nytt brudd-begrep til gammelt
      *
     c                   move      nybrd         gmbrd
     c                   move      sal           gsa
      *
     c     neste         tag
      *
     c     rhsal1_key    reade     rhsal1r                                60
     c                   enddo
      *
     c     slutt         tag
      *
     c                   eval      *inlr = *on                                               .
     c                   move      *hival        nybrd                                       .
      *
     c                   exsr      behand                                                    .
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
      *   Subrutine for behandling av matrise-record                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
      *
     c     behand        begsr
      *
      * Hente inn budsjett-nøkkel og beregn
      *
      *
     c                   eval      mfo = *zero
     c                   eval      w_bnok = rhbnØk                                       .....
      *
     c                   if        w_bnok <> 99 and                                      .....
     c                             w_bnok <> *zero                                           .
     c                   move      w_bnok        ra13lr_mkod                                 .
     c     ra13lr_key    chain     ra13lr                             61         budsjettnøkkel-
      *
     c                   eval      mfo(01) = ramp01                                          .
     c                   eval      mfo(02) = ramp02                                          .
     c                   eval      mfo(03) = ramp03                                          .
     c                   eval      mfo(04) = ramp04                                          .
     c                   eval      mfo(05) = ramp05                                          .
     c                   eval      mfo(06) = ramp06                                          .
     c                   eval      mfo(07) = ramp07                                          .
     c                   eval      mfo(08) = ramp08                                          .
     c                   eval      mfo(09) = ramp09                                          .
     c                   eval      mfo(10) = ramp10                                          .
     c                   eval      mfo(11) = ramp11                                          .
     c                   eval      mfo(12) = ramp12                                          .
     c                   eval      mfo(13) = ramp13                                          .
     c                   endif                                                           .....
      *
     c                   if        w_bnok = 99                                           .....
     c     1             do        12            n                                           .
     c                   eval      mfo(n) = 8.333333                                         .
     c                   enddo                                                               .
     c                   endif                                                               .
      *
     c                   if        w_bnok <> *zero                                       .....
      *                                                                 .
     c                   eval      akkpro = *zero                                            .
     c                   eval      akkbel = *zero                                            .
      *
      * Summer budsjett
      *
     c                   xfoot     gsa           w_tbud                                      .
      *                                                                 .
      * Beregn ny fordeling
      *
     c     1             do        13            n                                       ... .
     c                   eval      akkpro = akkpro + mfo(n)                                . .
     c                   eval(h)   work2 = akkpro * w_tbud /100                            . .
     c                   eval      nsa(n) = work2 - akkbel                                 . .
     c                   eval      akkbel = work2                                          . .
     c                   enddo                                                           ... .
      *
      * Oppdater budsjett
      *
     c                   move      gmkto         rhsalu_kont                                 .
     c                   move      gmsps         rhsalu_spes                                 .
     c                   move      gmavd         rhsalu_avde                                 .
     c                   move      rhpaar        rhsalu_raar
     c                   move      '3'           rhsalu_type
     c     rhsalu_key    chain     rhsalur                            62
      *
     c                   if        *in62 = *off
     c                   move      nsa(1)        risa01
     c                   move      nsa(2)        risa02
     c                   move      nsa(3)        risa03
     c                   move      nsa(4)        risa04
     c                   move      nsa(5)        risa05
     c                   move      nsa(6)        risa06
     c                   move      nsa(7)        risa07
     c                   move      nsa(8)        risa08
     c                   move      nsa(9)        risa09
     c                   move      nsa(10)       risa10
     c                   move      nsa(11)       risa11
     c                   move      nsa(12)       risa12
     c                   move      nsa(13)       risa13
     c                   eval      risa00 = *zero                                     IB = 0
      *
6.10 c                   time                    riedat
6.10 c                   time                    rietim
6.10 c                   eval      rieusr = l_user
      *
     c                   update    rhsalur
     c                   endif
      *
     c                   endif
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Sett startverdier
      *
     c                   eval      nybrd = *zeros
     c                   eval      gmbrd = *zeros
      *
      * Key for saldoregister - konto
      *
     c     rhsal1_key    klist
     c                   kfld                    w_firm
      *
      * Key for saldoregister - konto
      *
     c     rhsalu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhsalu_kont
     c                   kfld                    rhsalu_spes
     c                   kfld                    rhsalu_avde
     c                   kfld                    rhsalu_raar
     c                   kfld                    rhsalu_type
      *
      * Key for kontoplan
      *
     c     rhovlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhovlr_kont
     c                   kfld                    rhovlr_spes
     c                   kfld                    rhovlr_avde
      *
      * Key for budsjettnøkler
      *
     c     ra13lr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra13lr_mkod
      *
     c                   endsr
