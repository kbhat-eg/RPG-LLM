      /TITLE  ASSTAT Vise detaljer solgt vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASSTAT
      * Program . . . . : SF510R
      * Beskrivelse . . : Vise detaljer solgt vare
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       210898 HKO  Nytt program
5.21  * 5.21  100504 ASK  Viser leveringstype og bestillingsnr
V5.50 * 5.50  260607 KOB  Endret til å vise solgt enhet og antall
8.01  *PRG2   220622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner - COMP
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsstal1    if   e           k disk    rename(sstapfr:sstal1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
      *
     fsf510d    cf   e             workstn
      *
      * Definering av parametre
      *
     d p_firm          s                   like(sffirm)
     d p_paar          s                   like(sfpaar)
     d p_binr          s                   like(sfbinr)
     d p_line          s                   like(sfline)
     d p_bida          s                   like(sfbida)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d sstal1_paar     s                   like(sfpaar)
     d sstal1_binr     s                   like(sfbinr)
     d sstal1_line     s                   like(sfline)
     d sstal1_bida     s                   like(sfbida)
     d rkunl1_kund     s                   like(rkkund)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
      *
      * Definering av variable
      *
     d w_firm          s                   like(sffirm)
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle bildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer skjermbildet
      *
     c                   eval      sstal1_paar = p_paar
     c                   eval      sstal1_binr = p_binr
     c                   eval      sstal1_line = p_line
     c                   eval      sstal1_bida = p_bida
     c     sstal1_key    chain     sstal1                             60
     c                   if        *in60 = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      b1vkun = sfvkun
     c                   eval      b1kpro = sfkpro
     c                   eval      b1binr = sfbinr
     c                   eval      b1ornr = sfornr
     c                   eval      b1otyp = sfotyp
     c                   eval      b1otyp = sfotyp
     c                   eval      b1bong = sfbong
     c                   eval      b1wsid = sfwsid
     c                   eval      b1vare = sfvare
     c                   eval      b1tek1 = sftek1
V5.50c                   eval      b1anto = sfanta
V5.50c                   eval      b1enho = sfenhe
     c                   eval      b1kpri = sfkpri
     c                   eval      b1sumk = sfsumk
     c                   eval      b1sumo = sfsumo
     c                   eval      b1sumb = sfsumb
     c                   eval      b1rab1 = sfrab1
     c                   eval      b1rab2 = sfrab2
     c                   eval      b1rabo = sfrabo
5.21 c                   eval      b1lety = sflety
5.21 c                   eval      b1benr = sfbenr
      *
     c                   if        sfbida <> *loval
     c     *dmy          move      sfbida        b1bida
     c                   endif
      *
     c                   if        sforda <> *loval
     c     *dmy          move      sforda        b1orda
     c                   endif
      *
     c     *hms          move      sftime        b1time
      *
     c                   eval(h)   b1sumn = sfsumb - sfrab1 - sfrab2 -
     c                                      sfrabo
      *
     c                   if        sfvkun <> 0
     c                   eval      rkunl1_kund = sfvkun
     c     rkunl1_key    chain     rkunl1                             60
     c                   if        *in60 = *off
     c                   eval      b1knav = rknavn
     c                   endif
     c                   endif
      *
     c                   if        sfkpro <> 0
     c                   eval      fkprl1_kund = sfvkun
     c                   eval      fkprl1_kpro = sfkpro
     c     fkprl1_key    chain     fkprl1                             60
     c                   if        *in60 = *off
     c                   eval      b1lnav = flnavn
     c                   endif
     c                   endif
      *
      * Behandler bildet
      *
     c     b1taga        tag
     c                   exfmt     b1bld
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   endsl
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_paar
     c                   parm                    p_binr
     c                   parm                    p_line
     c                   parm                    p_bida
      *
      * Key for oppslag på statistikk-register
      *
     c     sstal1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sstal1_paar
     c                   kfld                    sstal1_binr
     c                   kfld                    sstal1_line
     c                   kfld                    sstal1_bida
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på leveringsadresse
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      *
      * Henter firmakode fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
