     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASPGPL
      * Program . . . . : AO101R
      * Beskrivelse . . : Nettbutikk, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       150502 HKO  Nytt program
6.20  *       190813 bsa  Utvidet med leveringsmåte (hente/levere kode)
6.21  *       191113 nho  F4 på lagerkode
6.22  *       281113 nho  Hentetekst og lev.kode tekst ble ikke blanket
      *                   dersom ny skulle opprettes
6.31  *       080219 bof  Utvidet med kode for fors.måte fabrikk
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fanbll1    if   e           k disk    rename(anblpfr:anbll1r)
     fanbll2    if   e           k disk    rename(anblpfr:anbll2r)
     fanbllr    if   e           k disk    rename(anblpfr:anbllrr)
     fanbllu    uf a e           k disk    rename(anblpfr:anbllur)
     fanbhl1    if   e           k disk    rename(anbhpfr:anbhl1r)
6.20 fra17l1    if   e           k disk    rename(ra17pfr:ra17l1r)
6.21 fra10l1    if   e           k disk    rename(ra10pfr:ra10l1r)
      *
     fao101d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_eier          s                   like(aoleie)
     d p_firm          s                   like(raqfir)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
6.20 d l_firm                944    946  0
     d l_fnav                951    980
6.21 d w_ltek          s             30
6.21 d w_lage          s              2  0
6.21 d ra10l1_jkod     s                   like(rajkod)
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d anbll1_lbut     s                   like(aolbut)
     d anbll2_lnav     s                   like(aolnav)
     d anbllr_lbut     s                   like(aolbut)
     d anbllu_lbut     s                   like(aolbut)
     d anbhl1_heie     s                   like(aoheie)
6.20 d ra17l1_qkod     s                   like(raqkod)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_eier          s                   like(aoleie)
6.20 d w_qkod          s              2  0
6.20 d w_qtxt          s             20
      *
     d s_lbut          s                   like(aolbut)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(12)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * d_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meleingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
     c                   if        w_curn > 0
     c                   eval      w_sfrn = w_curn / c_sfil
     c                   eval      w_sfrn = w_sfrn * c_sfil + 1
     c                   endif
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
6.21 c                   eval      *in33 = '0'
6.21 c                   eval      *in34 = '0'
6.21 c                   eval      *in35 = '0'
6.31 c                   eval      *in36 = '0'
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2lbut <> *blank or
     c                             b2lnav <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        d_fcrn <> 0
     c     d_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'N'
     c                   eval      anbll2_lnav = b1lnav
     c     anbll2_key    setll     anbll2
     c                   other
     c                   eval      anbll1_lbut = b1lbut
     c     anbll1_key    setll     anbll1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på butikk
      *
     c                   if        b2lbut <> *blank
     c                   eval      w_seqe = *blank
     c                   eval      anbll1_lbut = b2lbut
     c     anbll1_key    setll     anbll1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på navn
      *
     c                   if        b2lnav <> *blank
     c                   eval      w_seqe = 'N'
     c                   eval      anbll2_lnav = b2lnav
     c     anbll2_key    setll     anbll2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2lbut = *blank
     c                   eval      b2lnav = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = d_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1lbut = b1lbut
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1lbut = b1lbut
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1lbut = b1lbut
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1lbut = b1lbut
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1leie = w_eier
     c                   eval      c1hnav = b2hnav
      *
     c                   eval      c1lbut = b2lbut
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
     c                   if        c1lbut = *blank
     c                   goto      end_c1bld
     c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      anbllr_lbut = c1lbut
     c     anbllr_key    chain     anbllr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
6.21 c                   eval      *in33 = '0'
6.21 c                   eval      *in34 = '0'
6.21 c                   eval      *in35 = '0'
6.31 c                   eval      *in36 = '0'
     c                   exsr      xc2bld
      *
     c                   eval      c1lbut = *blank
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2lbut = *blank
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      c1leie = w_eier
     c                   eval      c1hnav = b2hnav
      *
     c                   eval      anbllr_lbut = c1lbut
     c     anbllr_key    chain     anbllr
      *
     c                   if        %found
     c                   eval      c2lnav = aolnav
     c                   eval      c2lfgr = aolfgr
     c                   eval      c2lfir = aolfir
     c                   eval      c2lavd = aolavd
     c                   eval      c2llag = aollag
6.20 c                   eval      c2hkod = aohkod
6.20 c                   eval      w_qkod = aohkod
6.20 c                   eval      w_qtxt = *blanks
6.20 c                   exsr      sjk_kode
6.20 c                   eval      c2hftx = w_qtxt
6.20 c                   eval      c2lkod = aolkod
6.20 c                   eval      w_qkod = aolkod
6.20 c                   eval      w_qtxt = *blanks
6.20 c                   exsr      sjk_kode
6.20 c                   eval      c2lftx = w_qtxt
6.31 c                   eval      w_qkod = aofkod
6.31 c                   eval      w_qtxt = *blanks
6.31 c                   exsr      sjk_kode
6.31 c                   eval      c2fftx = w_qtxt
     c                   eval      c2leus = aoleus
     c     *dmy          move      aoloda        c2loda
     c     *dmy          move      aoleda        c2leda
     c                   else
     c                   eval      c2lnav = *blank
     c                   eval      c2lfgr = *blank
     c                   eval      c2lfir = 0
     c                   eval      c2lavd = 0
     c                   eval      c2llag = *blank
6.20 c                   eval      c2hkod = *zeros
6.20 c                   eval      c2lkod = *zeros
6.31 c                   eval      c2fkod = *zeros
6.22 c                   eval      c2hftx = *blanks
6.22 c                   eval      c2lftx = *blanks
6.31 c                   eval      c2fftx = *blanks
     c                   eval      c2leus = *blank
     c                   eval      c2loda = 0
     c                   eval      c2leda = 0
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1lbut = k1lbut
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
6.21  * Sett av feilindikator
6.21 c                   eval      *in34 = '0'
6.21 c                   eval      *in33 = '0'
6.21 c                   eval      *in35 = '0'
6.31 c                   eval      *in36 = '0'
6.21  *
6.20  *
6.20  *  F4=Spørring
6.20  *
6.20 c                   if        *inkd  = *on
6.20  *
6.20  * Call program for lager - hentes
6.21 c                   if        w_afld = 'C2LLAG'
6.21 c                   eval      c2llag = *blanks
6.21 c                   call      'RA510R'
6.21 c                   parm                    p_firm
6.21 c                   parm                    w_lage
6.21 c                   parm                    w_ltek
6.21 c                   move      w_lage        c2llag
6.21 c                   goto      c2taga
6.21 c                   endif
      *                                                                 .
6.20  * Call program for leveringsmåte - hentes
6.20  *
6.20 c                   if        w_afld = 'C2HKOD'
6.20 c                   eval      w_qkod = *zeros
6.20 c                   eval      w_qtxt = *blanks
6.20 c                   call      'RA517R'
6.20 c                   parm                    p_firm
6.20 c                   parm                    w_qkod
6.20 c                   parm                    w_qtxt
6.20 c                   if        w_qkod <> 0
6.20 c                   eval      c2hkod = w_qkod
6.20 c                   eval      c2hftx = w_qtxt
6.20 c                   endif
6.20 c                   goto      c2taga
6.20 c                   endif
6.20  *
6.20  * Call program for leveringsmåte - leveres
6.20  *
6.20 c                   if        w_afld = 'C2LKOD'
6.20 c                   eval      w_qkod = *zeros
6.20 c                   eval      w_qtxt = *blanks
6.20 c                   call      'RA517R'
6.20 c                   parm                    p_firm
6.20 c                   parm                    w_qkod
6.20 c                   parm                    w_qtxt
6.20 c                   if        w_qkod <> 0
6.20 c                   eval      c2lkod = w_qkod
6.20 c                   eval      c2lftx = w_qtxt
6.20 c                   endif
6.20 c                   goto      c2taga
6.20 c                   endif
6.31  *
6.31  * Call program for leveringsmåte - fabrikk
6.31  *
6.31 c                   if        w_afld = 'C2FKOD'
6.31 c                   eval      w_qkod = *zeros
6.31 c                   eval      w_qtxt = *blanks
6.31 c                   call      'RA517R'
6.31 c                   parm                    p_firm
6.31 c                   parm                    w_qkod
6.31 c                   parm                    w_qtxt
6.31 c                   if        w_qkod <> 0
6.31 c                   eval      c2fkod = w_qkod
6.31 c                   eval      c2fftx = w_qtxt
6.31 c                   endif
6.31 c                   goto      c2taga
6.31 c                   endif
6.31 c
6.20 c                   endif
      * Validering av input
      *
     c                   if        c2lnav = *blank
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
     c                   if        c2lfgr = *blank
     c                   eval      *in32 = *on
     c                   goto      c2taga
     c                   endif
6.21  * Sjekk at lager finnes
6.21 c                   if        c2llag <> *blanks
6.21 c                   move      c2llag        ra10l1_jkod
6.21 c                   exsr      sjk_lagk
6.21 c                   if        *in34 = *on
6.21 c                   goto      c2taga
6.20 c                   endif
6.20 c                   endif
6.20  *
6.20  * Sjekk forsendelsesmåte hentes
6.20  *
6.20 c                   if        c2hkod = *zeros
6.20 c                   eval      *in33 = *on
6.20 c                   goto      c2taga
6.20 c                   endif
6.20  * Må finnes
6.20 c                   eval      w_qtxt = *blanks
6.20 c                   eval      w_qkod = c2hkod
6.20 c                   exsr      sjk_kode
6.20 c                   if        w_qtxt = *blanks
6.21 c                   eval      c2hftx = *blanks
6.20 c                   eval      *in33 = *on
6.20 c                   goto      c2taga
6.20 c                   endif
6.20 c                   eval      c2hftx = w_qtxt
6.20  * Sjekk forsendelsesmåte leveres
6.20 c                   if        c2lkod = *zeros
6.20 c***                eval      *in33 = *on
6.21 c                   eval      *in35 = *on
6.20 c                   goto      c2taga
6.20 c                   endif
6.20  * Må finnes
6.20 c                   eval      w_qtxt = *blanks
6.20 c                   eval      w_qkod = c2lkod
6.20 c                   exsr      sjk_kode
6.20 c                   if        w_qtxt = *blanks
6.20 c**                 eval      *in33 = *on
6.20 c                   eval      *in35 = *on
6.21 c                   eval      c2lftx = *blanks
6.20 c                   goto      c2taga
6.20 c                   endif
6.20 c                   eval      c2lftx = w_qtxt
6.31  * Sjekk forsendelsesmåte fabrikk
6.31 c                   if        c2fkod = *zeros
6.31 c                   eval      *in36 = *on
6.31 c                   goto      c2taga
6.31 c                   endif
6.31  * Må finnes
6.31 c                   eval      w_qtxt = *blanks
6.31 c                   eval      w_qkod = c2fkod
6.31 c                   exsr      sjk_kode
6.31 c                   if        w_qtxt = *blanks
6.31 c                   eval      *in36 = *on
6.31 c                   eval      c2fftx = *blanks
6.31 c                   goto      c2taga
6.31 c                   endif
6.31 c                   eval      c2fftx = w_qtxt
6.31  *
      * Oppdatering
      *
     c                   clear                   anbllur
      *
     c                   eval      anbllu_lbut = c1lbut
     c     anbllu_key    chain     anbllur
      *
     c                   eval      aolnav = c2lnav
     c                   eval      aolfgr = c2lfgr
     c                   eval      aolfir = c2lfir
     c                   eval      aolavd = c2lavd
     c                   eval      aollag = c2llag
6.20 c                   eval      aohkod = c2hkod
6.20 c                   eval      aolkod = c2lkod
6.31 c                   eval      aofkod = c2fkod
      *
     c                   time                    aoleda
     c                   time                    aoleti
     c                   eval      aoleus = l_user
      *
     c                   if        %found
     c                   update    anbllur
     c                   else
     c                   time                    aoloda
     c                   eval      aoleie = w_eier
     c                   eval      aolbut = c1lbut
     c                   write     anbllur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1lnav = aolnav
     c                   eval      b1lfgr = aolfgr
     c                   eval      b1lfir = aolfir
     c                   eval      b1lavd = aolavd
     c                   eval      b1llag = aollag
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      d1leie = w_eier
      *
     c                   eval      anbllr_lbut = d1lbut
     c     anbllr_key    chain     anbllr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter nettbutikk
      *
     c                   eval      anbllu_lbut = d1lbut
     c     anbllu_key    chain     anbllur
     c                   delete    anbllur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_lbut = k1lbut
      *
      * Fyller kopierings-bildet
      *
     c                   eval      anbllr_lbut = k1lbut
     c     anbllr_key    chain     anbllr
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1lbut = *blank
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      anbllr_lbut = k1lbut
     c     anbllr_key    chain     anbllr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1lbut = s_lbut
6.21 c                   eval      *in33 = '0'
6.21 c                   eval      *in34 = '0'
6.21 c                   eval      *in35 = '0'
6.31 c                   eval      *in36 = '0'
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'N'
     c                   read      anbll2
     c                   other
     c                   read      anbll1
     c                   endsl
      *
     c                   if        %eof or
     c                             aoleie <> w_eier
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'N'
     c                   eval      anbll2_lnav = aolnav
     c                   other
     c                   eval      anbll1_lbut = aolbut
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1lbut = aolbut
     c                   eval      b1lnav = aolnav
     c                   eval      b1lfgr = aolfgr
     c                   eval      b1lfir = aolfir
     c                   eval      b1lavd = aolavd
     c                   eval      b1llag = aollag
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'N'
     c     anbll2_key    setgt     anbll2
     c                   readp     anbll2
     c                   other
     c     anbll1_key    setgt     anbll1
     c                   readp     anbll1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'N'
     c                   readp     anbll2
     c                   other
     c                   readp     anbll1
     c                   endsl
      *
     c                   if        %eof or
     c                             aoleie <> w_eier
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'N'
     c                   eval      anbll2_lnav = aolnav
     c                   other
     c                   eval      anbll1_lbut = aolbut
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'N'
     c     anbll2_key    setll     anbll2
     c                   other
     c     anbll1_key    setll     anbll1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  * Subrutine for sjekk av forsendelsesmåte
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  *
6.20 c     sjk_kode      begsr
6.20  *
6.20 c                   eval      ra17l1_qkod = w_qkod
6.20 c     ra17l1_key    chain     ra17l1
6.20 c                   if        %found
6.20 c                   eval      w_qtxt = raqtxt
6.20 c                   endif
6.20  *
6.20 c                   endsr
6.20  *
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  * Subrutine for sjekk av lager
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  *
6.21 c     sjk_lagk      begsr
6.21  *
6.21 c     ra10l1_key    chain     ra10l1                             91
6.21 c                   if        *in91 = '1'
6.21 c                   eval      *in34 = '1'
6.21 c                   endif
6.21  *
6.21 c                   endsr
6.21  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_eier
      *
      * Key for posisjonering på butikk
      *
     c     anbll1_key    klist
     c                   kfld                    w_eier
     c                   kfld                    anbll1_lbut
      *
      * Key for posisjonering på navn
      *
     c     anbll2_key    klist
     c                   kfld                    w_eier
     c                   kfld                    anbll2_lnav
      *
      * Key for oppslag på nettbutikk
      *
     c     anbllr_key    klist
     c                   kfld                    w_eier
     c                   kfld                    anbllr_lbut
      *
      * Key for oppdatering av nettbutikk
      *
     c     anbllu_key    klist
     c                   kfld                    w_eier
     c                   kfld                    anbllu_lbut
      *
      * Key for oppslag på nettbutikk-eier
      *
     c     anbhl1_key    klist
     c                   kfld                    anbhl1_heie
6.20  *
6.20  * Key for oppdatering av forsendelsmåte
6.20  *
6.20 c     ra17l1_key    klist
6.20 c                   kfld                    p_firm
6.20 c                   kfld                    ra17l1_qkod
6.21  * Key for posisjonering på lager
6.21  *
6.21 c     ra10l1_key    klist
6.21 c                   kfld                    l_firm
6.21 c                   kfld                    ra10l1_jkod
      *
      *
      *
      * Henter butikkeier fra parameter
      *
     c                   eval      w_eier = p_eier
6.20 c                   eval      p_firm = l_firm
      *
      * Initierer skjermbilde
      *
     c                   eval      *in22 = *on
      *
     c                   eval      b2leie = w_eier
      *
     c                   eval      anbhl1_heie = w_eier
     c     anbhl1_key    chain     anbhl1                             91
     c                   if        *in91 = *off
     c                   eval      b2hnav = aohnav
     c                   endif
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      anbll1_lbut = *blank
     c     anbll1_key    setll     anbll1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
