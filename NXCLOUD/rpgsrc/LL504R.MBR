     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LL504R
      *  Beskrivelse . . : Viser lagerinformasjon, full skjerm
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      *  --------- ------  -------------------------------------------------
5.70  *  PRGR     021208  Utvidet med prisgruppe                   bof
7.01  *   7.01    290812  Legger også ut alt. 1+2 for lagerplass   amd
8.01  *PRG2   150622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     flusrl1    if   e           k disk    rename(lusrpfr:lusrl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     fll504d    cf   e             workstn
      *
      * Definering av datastrukturer
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering parametre
      *
     d p_dato          s               d   datfmt(*iso)
     d p_time          s               t   timfmt(*iso)
     d p_inlr          s              1
     d p_lety          s              1  0
      *
      * Definering av variable i nøkler
      *
     d lusrl1_user     s                   like(lbuser)
     d vvarl1_vare     s                   like(vvvare)
     d vlagl1_lage     s                   like(vllage)
     d vlagl1_vare     s                   like(vlvare)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vvfirm)
     d w_lage          s                   like(vllage)
     d w_vare          s                   like(vvvare)
     d w_enhe          s                   like(vvenhl)
     d w_sapr          s              9  2
     d w_bupr          s              9  2
     d w_tipr          s              9  2
     d w_kopr          s              9  2
     d w_retk          s              1
     d w_latx          s             20
     d w_adrs          s             30
     d w_ponr          s              4  0
     d w_pste          s             30
     d w_avde          s              4  0
8.01 d w_prgr          s              2
     d b_feil          s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  B1 Behandling av vare/lager informasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   movel     w_vare        b1vare
     c                   eval      b1lage = w_lage
      *
     c                   exsr      finn_lager
5.70 c                   exsr      hent_prisgr
     c                   exsr      hent_vare
     c                   exsr      hent_lager
      *
      *  Send Alt
      *
     c     b1tagb        tag
     c                   exfmt     b1bld
      *
      *  F3=Avslutt program
      *
     c                   if        *inkc = *on
     c                   goto      end_pgm
     c                   endif
      *
      *  Hent opplysninger fra VARE-REGISTER
      *
     c                   exsr      hent_vare
     c                   if        b_feil = *on
     c                   eval      *in31 = *on
     c                   goto      b1tagb
     c                   endif
      *
      *  Sjekk,  Finnes lager i KODE-REGISTER
      *
     c                   eval      b1latx = *blank
      *
     c                   exsr      finn_lager
     c                   if        b_feil = *on
     c                   eval      *in34 = *on
     c                   goto      b1tagb
     c                   endif
      *
      *  Henter inn lagerinformasjon
      *
     c                   exsr      hent_lager
      *
      *  Gå tilbake til start
      *
     c                   goto      b1tagb
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Henter informasjon fra vare-register
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_vare     begsr
      *
     c                   movel     b1vare        vvarl1_vare
     c     vvarl1_key    chain     vvarl1r                            90
      *
     c                   movel     vvtek1        b1tek1
     c                   eval      b1enh1 = vvenhl
     c                   eval      b1ldor = vvldor
     c                   eval      b1ogrp = vvogrp
     c                   eval      b1hgrp = vvhgrp
     c                   eval      b1ugrp = vvugrp
     c                   eval      b1enhl = vvenhl
      *
      * Henter prisinformasjon -
      * Initiering av variable
      *
     c                   eval      w_sapr = 0
     c                   eval      w_bupr = 0
     c                   eval      w_tipr = 0
     c                   eval      w_kopr = 0
      *
     c                   if        vvenhl <> *blank
     c                   eval      w_enhe = vvenhl
     c                   else
     c                   eval      w_enhe = vvenhe
     c                   endif
      *
     c                   eval      p_lety = 0
     c                   eval      p_inlr = ' '
     c                   move      udate         p_dato
      *
      * Henter og beregner prisgrunnlag fra vareregisteret
      *
     c                   call      'VP700R'
     c                   parm                    w_firm
     c                   parm                    vvvare
5.70 c                   parm                    w_prgr
     c                   parm                    w_enhe
     c                   parm                    p_lety
     c                   parm                    p_dato
     c                   parm                    p_time
     c                   parm                    p_inlr
     c                   parm                    w_sapr
     c                   parm                    w_bupr
     c                   parm                    w_tipr
     c                   parm                    w_kopr
      *
     c                   eval      b1kpny  = w_kopr
     c                   eval      b1spny  = w_sapr
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Henter informasjon fra lager-register
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_lager    begsr
      *
     c                   eval      b1trda = *zero
     c                   eval      b1opda = *zero
      *
     c                   eval      b1behl = *zero
     c                   eval      b1pakk = *zero
     c                   eval      b1inng = *zero
     c                   eval      b1utta = *zero
     c                   eval      b1just = *zero
     c                   eval      b1oord = *zero
     c                   eval      b1tell = *zero
     c                   eval      b1salg = *zero
     c                   eval      b1bbes = *zero
     c                   eval      b1beve = *zero
      *
     c                   eval      b1totl = *zero
     c                   eval      b1disp = *zero
      *
     c                   movel     b1vare        vlagl1_vare
     c                   eval      vlagl1_lage = b1lage
     c     vlagl1_key    chain     vlagl1r                            90
      *
     c                   if        *in90 = *off
      *
     c                   if        vltrda <> *loval
     c     *dmy          move      vltrda        b1trda
     c                   endif
      *
     c                   if        vlopda <> *loval
     c     *dmy          move      vlopda        b1opda
     c                   endif
      *
     c                   eval      b1behl = vlbehl
     c                   eval      b1pakk = vlpakk
     c                   eval      b1inng = vlinng
     c                   eval      b1utta = vlutta
     c                   eval      b1just = vljust
     c                   eval      b1oord = vloord
     c                   eval      b1tell = vltell
     c                   eval      b1salg = vlsalg
     c                   eval      b1bbes = vlbbes
     c                   eval      b1beve = vlbeve
     c                   eval      b1plas = vlplas
7.01 c                   eval      b1pla1 = vlpla1
7.01 c                   eval      b1pla2 = vlpla2
     c                   eval      b1bpun = vlbpun
     c                   eval      b1bmen = vlbmen
     c                   eval      b1sikl = vlsikl
     c                   eval      b1b101 = vlb101
      *
      *  Beregner antall til utlevering
      *
     c                   eval      b1totl = vlbehl
     c                   eval      b1totl = b1totl - vlpakk
      *
      *  beregner antall disponiblet
      *
     c                   eval      b1disp = b1totl
     c                   eval      b1disp = b1disp - vloord
     c                   eval      b1disp = b1disp - vlores
     c                   eval      b1disp = b1disp + vlbbes
     c                   eval      b1disp = b1disp + vlbres
      *
     c                   endif
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sjekk/Hent tekst for lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_lager    begsr
      *
     c                   call      'FÅ720R'
     c                   parm                    w_firm
     c                   parm                    b1lage
     c                   parm                    w_latx
     c                   parm                    w_adrs
     c                   parm                    w_ponr
     c                   parm                    w_pste
     c                   parm                    w_avde
     c                   parm                    b_feil
     c                   movel     w_latx        b1latx
      *
     c                   endsr
5.70  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5 70  * Subrutine for å hente prisgruppe
5.70  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.70  *
5.70 c     hent_prisgr   begsr
5.70  *
5.70 c                   call      'VL712R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    w_lage
5.70 c                   parm                    w_avde
5.70 c                   parm                    w_prgr
5.70  *
5.70 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for les av GPL-BRUKER
      *
     c     lusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lusrl1_user
      *
      *  Key for les av VARE
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *  Key for les av LAGER
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl1_vare
     c                   kfld                    vlagl1_lage
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    w_vare
     c                   parm                    w_lage
      *
      *  Setter startverdier
      *
     c                   eval      w_firm = l_firm
      *
      *  Henter opplysninger fra BRUKER-REGISTER OFAK
      *
     c                   eval      lusrl1_user = l_user
     c     lusrl1_key    chain     lusrl1                             60
      *
      *  Sett indikator som styrer kostpris i henhold til kode på bruker
      *
     c                   if        lbko01 = 0
     c                   eval      *in49 = *off
     c                   else
     c                   eval      *in49 = *on
     c                   endif
      *
     c                   endsr
