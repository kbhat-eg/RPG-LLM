     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LI730R
      *  Beskrivelse . . : Oppdatering av faktura
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      *  --------- ------  -------------------------------------------------
5.40  *  BOF       290107  Tar vare på ldlety før slett. oppd. med denne
5.51  *  ASK       080108  Legger inn fast kode for lager oppdatert
6.10  *  6.10  170609 BSA  Oppdatert med sporingsinformasjon
6.11  *  6.11  041010 BSA  Sjekker mot GTIN register, hvis nobb nummer er 0
6.11  *                    på innlest register.
6.12  *  6.10  270213 BSA  Henter mvakode fra leverandøren
6.20  *  6.20  231110 NHO  Proj er nå alfa
6.21  *  6.10  280212 ASK  Bruker opprinnelig kostpris fra best.
6.nu  *  6.30  050614 bof  Utvidelser mtp. nummerutvidelse
6.30  *  6.30  220914 bsa  Legger over mvakoden fra EDI meldinga inn.
6.30  *                    På "nye" varer hentes mvakoden fra varereg.
6.31  *  6.30  130515 bkv  Endret LVAR pga økt fra 15 til 20 tegn
6.32  *  6.30  121115 bsa  Byttet program for sjekk av GTIN.
6.33  *  6.30  231116 bsa  Ny logikk for tildeling av mva kode
6.34  *  6.30  070317 bsa  Utvider antall desimerl i pris for å unngå for store
6.34  *                    øreavvik.
6.35  *  6.30  280617 bsa  Utvidet bruk av datastruktur.
6.36  *  6.30  100118 bsa  Feil ved mva kode beregning.
6.37  *  6.30  110118 bsa  Skal ikke akseptere alle mva koder fra mvak.
6.38  *  6.30  081018 bof  Utvidet med trelast sortiment
6.39  *  6.30  191218 bsa  Kan ikke sjekke mot logistikkvare hvis vi
6.39  *                    ikke opererer med NOBB nummer.
      *
7.01  *  6.20  270416 bsa  Kaller program for arkivering av utskrift EDI
7.01  *                    melding. Legger ved bilagsnummer for gjenfinning.
7.01  *                    NB Endres på 6.30 da versjonsnummer er lagt til
7.01  *                    på slutten.
7.02  *  6.20  240220 bsa  Hvis ikke GTIN eller nobbnr finnes, er det ingen vits
7.02  *                    å lete etter gyldig varenummer.
7.03  *  6.30  230317 bsa  GTIN nummer er 14 siffer
      *
8.01  *K0000  240621 BSA  Utvidet meldingsnummer fra 6-8 siffer
8.02  *K0000  100921 BSA  Switchstyring for arkivering av EDI faktura.
8.03  *K0000  141021 BSA  Legger EDI meldingsnummer ut på bestilling.
8.04  *K0000  141021 BSA  Kaller annet program for arkivering.
8.05  *K0000  020924 bsa  Prøver å beholde linjereferansene til ordre
8.05  *K0000              ved oppdatering.
8.06  *K0000  180924 bsa  Prøver å beholde linjereferansene til ordre
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *  Bruk av linjenummersekvenser (2 første siffer i linjenr LIMLIN)
      *
      *    10 : Hodeinformasjon
      *    20 : Partsinformasjon
      *    30 : Referanser
      *    40 : Transportinformasjon
      *    50 : Linjer
      *    55 : Linjer fra best. (ikke match i EDI-melding)
      *    60 : Tillegg/fradrag hodenivå
      *    65 : Fritkstlinjer på hodenivå
      *    90 : Totaler
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flohelu    uf a e           k disk    rename(lohepfr:lohelur)
     flodtl1    if   e           k disk    rename(lodtpfr:lodtl1r)
     flodtlu    uf a e           k disk    rename(lodtpfr:lodtlur)
     fledilr    if   e           k disk    rename(ledipfr:ledilrr)
     fledil1    if   e           k disk    rename(ledipfr:ledil1r)
7.01 fledilu    uf   e           k disk    rename(ledipfr:ledilur)
     frlevl5    if   e           k disk    rename(rlevpfr:rlevl5r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     fvvarl3    if   e           k disk    rename(vvarpfr:vvarl3r)
     fjvarl3    if   e           k disk    rename(jvarpfr:jvarl3r)
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
6.30 fvmval1    if   e           k disk    rename(vmvapfr:vmval1r)
8.05 fledsiu    uf a e           k disk    rename(ledsst:ledsiur)
8.05 ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
      *
      * Parameter-inn
      *
     d                 ds
8.01 d d_prec                        22
     d  d_firm                        3  0 overlay(d_prec)
     d  d_type                        4    overlay(d_prec:4)
8.01 d  d_mnum                        8  0 overlay(d_prec:8)
8.01 d  d_mlin                        6  0 overlay(d_prec:16)
8.01 d  d_kode                        1    overlay(d_prec:22)
7.01  *
7.01 d                 ds
8.01 d p_prec                        32
7.01 d  p_firm                        3  0 overlay(p_prec)
7.01 d  p_type                        4    overlay(p_prec:4)
8.01 d  p_mnum                        8  0 overlay(p_prec:8)
8.01 d  p_mlin                        6  0 overlay(p_prec:16)
8.01 d  p_biln                        8  0 overlay(p_prec:22)
8.01 d  p_kode                        1    overlay(p_prec:30)
      *
      *  Definering av datastrukturer for meldinger
      *
      * FAKTURA Hode
      *
     d d_fakh1         ds
     d  d_fh_dtyp                     3
     d  d_fh_fanr                    35
     d  d_fh_fako                     3
     d  d_fh_ldat                      d   datfmt(*iso)
     d  d_fh_best                    20
     d  d_fh_lord                    20
     d  d_fh_lfak                    20
     d  d_fh_sean                    13  0
     d  d_fh_snav                    30
     d  d_fh_fean                    13  0
     d  d_fh_fnav                    30
     d  d_fh_fad1                    30
     d  d_fh_fad2                    30
     d  d_fh_fste                    30
     d  d_fh_forg                    20
     d  d_fh_kref                    20
     d  d_fh_lref                    20
     d  d_fh_valk                     3
     d  d_fh_ffda                      d   datfmt(*iso)
     d  d_fh_levb                     3
     d  d_fh_forf                      d   datfmt(*iso)
6.35 d  d_fh_bean                    13  0
6.35 d  d_fh_mfor                     6
7.01 d  d_fh_bilna                    8
7.01 d    d_fh_biln                   8  0 overlay(d_fh_bilna:1)
      *
      * Tillegg/fradragslinjer
      *
     d d_fakh2         ds
     d  d_fh_tsig                     3
     d  d_fh_tkod                     3
     d  d_fh_ttek                    30
     d  d_fh_tpro                     4  2
     d  d_fh_tbel                     8  2
     d  d_fh_apro                     4  2
      *
      * Fritekst/hodelinje
      *
     d d_fakh3         ds
     d  d_fh_ftxq                     3
     d  d_fh_ftxt                    70
      *
      * Fakturareferanser/hode
      *
     d d_fakh4         ds
     d  d_fh_bstn                    35
     d  d_fh_ordr                    35
     d  d_fh_fakk                    35
     d  d_fh_paks                    35
     d  d_fh_kunr                    35
     d  d_fh_proj                    35
     d  d_fh_lvfa                    35
     d  d_fh_ofav                    35
     d  d_fh_plnr                    35
     d  d_fh_konr                    35
     d  d_fh_kidn                    35
      *
      * Faktura partsinfo./hode
      *
     d d_fakh5         ds
     d  d_fh_part                     3
     d  d_fh_pean                    13  0
     d  d_fh_pnav                    30
     d  d_fh_pad1                    30
     d  d_fh_pad2                    30
     d  d_fh_pste                    30
     d  d_fh_porg                    20
      *
      * Faktura transport/lev. - hode
      *
     d d_fakh6         ds
     d  d_fh_trmo                     3
     d  d_fh_trna                    35
     d  d_fh_lebe                     3
      *
      * FAKTURA Linje
      *
     d d_fakl          ds
     d  d_fl_llin                     6  0
7.03 d  d_fl_vean                    14  0
     d  d_fl_vnob                     8  0
     d  d_fl_vlev                    15
     d  d_fl_tek1                    35
     d  d_fl_tek2                    35
     d  d_fl_bkva                    15  3
     d  d_fl_benh                     3
     d  d_fl_lkva                    15  3
     d  d_fl_lenh                     3
     d  d_fl_fkva                    15  3
     d  d_fl_fenh                     3
     d  d_fl_bnet                    15  2
     d  d_fl_bbto                    15  2
     d  d_fl_prkv                     3
     d  d_fl_pris                    15  3
     d  d_fl_penh                     3
     d  d_fl_prpr                     9  0
     d  d_fl_bnum                    20
     d  d_fl_blin                     6  0
     d  d_fl_mvap                     4  2
     d  d_fl_sig1                     3
     d  d_fl_kod1                     3
     d  d_fl_bel1                     9  2
     d  d_fl_pro1                     5  2
     d  d_fl_sig2                     3
     d  d_fl_kod2                     3
     d  d_fl_bel2                     9  2
     d  d_fl_pro2                     5  2
     d  d_fl_sig3                     3
     d  d_fl_kod3                     3
     d  d_fl_bel3                     9  2
     d  d_fl_pro3                     5  2
     d  d_fl_sig4                     3
     d  d_fl_kod4                     3
     d  d_fl_bel4                     9  2
     d  d_fl_pro4                     5  2
     d  d_fl_sig5                     3
     d  d_fl_kod5                     3
     d  d_fl_bel5                     9  2
     d  d_fl_pro5                     5  2
     d  d_fl_sig6                     3
     d  d_fl_kod6                     3
     d  d_fl_bel6                     9  2
     d  d_fl_pro6                     5  2
6.21 d  d_fl_kopa                    15
6.21 d    d_fl_kopr                  15  2 overlay(d_fl_kopa)
6.30 d  d_fl_mvak                     1
      *
      * Fakturatotallinjer
      *
     d d_fakt          ds
     d  d_ft_ftot                    15  2
     d  d_ft_teks                    15  2
     d  d_ft_tmva                    15  2
     d  d_ft_mvgr                    15  2
     d  d_ft_veks                    15  2
     d  d_ft_vink                    15  2
     d  d_ft_stil                    15  2
     d  d_ft_sfra                    15  2
      *
      * Definering av LDA
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
8.02 d l_filg                931    933
     d l_firm                944    946  0
     d l_avde                947    950  0
     d l_lage               1001   1002  0
      *
      * Definering av variable i nøkler
      *
     d lohelu_numm     s                   like(lonumm)
     d lohelu_suff     s                   like(losuff)
     d lodtl1_numm     s                   like(ldnumm)
     d lodtl1_suff     s                   like(ldsuff)
     d lodtlu_numm     s                   like(ldnumm)
     d lodtlu_suff     s                   like(ldsuff)
     d lodtlu_line     s                   like(ldline)
     d ledilr_type     s                   like(litype)
     d ledilr_mnum     s                   like(limnum)
     d ledilr_mlin     s                   like(limlin)
     d ledil1_type     s                   like(litype)
     d ledil1_mnum     s                   like(limnum)
     d ledil1_mlin     s                   like(limlin)
     d rlevl5_eanl     s                   like(rleanl)
     d votyl1_otyp     s                   like(vaotyp)
     d vvarl3_nobb     s                   like(vvnobb)
     d jvarl3_nobb     s                   like(jvnobb)
6.30 d vmval1_mkod     s                   like(vamkod)
7.01 d ledilu_type     s                   like(litype)
7.01 d ledilu_mnum     s                   like(limnum)
7.01 d ledilu_mlin     s                   like(limlin)
8.05 d ledsiu_snum     s                   like(ldsnum)
8.05 d ledsiu_ssuf     s                   like(ldssuf)
8.05 d ledsiu_slin     s                   like(ldslin)
8.05 d fodtl1_numm     s                   like(fdnumm)
8.05 d fodtl1_suff     s                   like(fdsuff)
8.05 d fodtl1_line     s                   like(fdline)
      *
      * Definering av parametre
      *
     d p_parm          s             45
     d p_errk          s              2
6.11 d p_eann          s             14  0
6.11 d p_vare          s             15
6.11 d p_enhe          s              3
6.11 d p_nobb          s              8  0
6.11 d p_stat          s              1
      *
      * Definering av øvrige variable
      *
     d w_firm          s                   like(lifirm)
     d w_line          s                   like(liline)
6.34 d*w_pris          s                   like(ldipny)
6.34 d w_pris          s             11  3
     d w_psav          s                   like(ldipny)
6.34 d*w_netp          s                   like(ldipny)
6.34 d w_netp          s             11  3
     d w_ntpe          s                   like(ldipny)
     d w_nobp          s                   like(ldipny)
     d w_frad          s                   like(ldipny)
     d w_tfra          s                   like(ldipny)
     d w_ttil          s                   like(ldipny)
     d w_kpri          s             15  3
     d w_nobb          s                   like(vvnobb)
     d w_enob          s                   like(vvnobb)
     d w_otyp          s                   like(lootyp)
     d w_spos          s              2  0
     d w_epos          s              2  0
     d w_vare          s             15
     d w_lvar          s             15
     d w_tek1          s             35
     d w_tek2          s             35
     d w_ogrp          s              2  0
     d w_hgrp          s              2  0
     d w_ugrp          s              3  0
     d w_lvre          s              1  0
     d w_enor          s              1
     d w_kode          s              1
     d w_fell          s              6
     d w_type          s              2
     d w_fast          s             10
6.nu d w_numm          s              8  0
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_retu          s              1
     d w_valg          s              1  0
     d w_lety          s                   like(ldlety)
6.11 d w_wlen          s              5  0
6.30 d w_mkod          s              1
6.33 d w_lv_vare       s                   like(vvvare)
7.01 d w_biln          s              8  0
7.01 d w_arec          s             45
      *
8.05 d x               s              2  0
      *
     d b_meld          s              1    inz(*off)
     d b_pris          s              1    inz(*off)
     d b_lety          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
6.11 d c_digits        c                   ' 0123456789'
      *
8.02 d u_802           s              1    inz(*off)
      *
8.02   dcl-s co402_verdi1  char(1);
8.02   dcl-s co402_verdi2  char(2048);
8.02   DCL-PR CO402R EXTPGM('CO402R');
8.02     p_filgrp            char(3)     const;
8.02     p_firm              packed(3:0) const;
8.02     p_lager             packed(2:0) const;
8.02     p_nokkel            char(50)    const;
8.02     p_verdi1            char(1);
8.02     p_verdi2            char(2048);
8.02   END-PR;
      *
6.33 C/Exec SQL
6.33 C+  Set Option DatFmt=*ISO
6.33 C/End-Exec
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hoved program styrerutine - oppdatering
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   exsr      sjk_meld
      *
     c                   if        p_errk = *blank
     c                   exsr      oppd_hode
     c                   if        b_feil = *on
     c                   goto      end_pgm
     c                   endif
     c                   exsr      slett_linjer
     c                   exsr      nye_linjer
     c                   exsr      til_linjer
      *
     c                   exsr      endre_otyp
     c                   exsr      kalk_best
7.01  * Lag arkivkopi
7.01 c                   exsr      arkiv_fakt
      *
     c                   if        d_kode = '1'
     c                   exsr      skriv_fakt
     c                   endif
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å oppdatere bestillingshode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_hode     begsr
      *
     c     ny_les        tag
      *
     c     lohelu_key    chain     lohelur
     c                   if        not %found
     c*                  exsr      bygg_hode
     c                   eval      b_feil = *on
     c                   goto      end_oppd
     c                   endif
      *
      * Legg inn fakturainformasjon
      *
     c                   eval      lofkda = d_fh_ffda
     c                   eval      loffda = d_fh_forf
     c                   if        lovref = *blank
     c                   eval      lovref = d_fh_kref
     c                   endif
     c                   eval      lodref = d_fh_lref
     c                   if        d_fh_dtyp = '380'
     c                   eval      lobitx = 'EDI FAKTURA'
     c                   else
     c                   eval      lobitx = 'EDI KREDIT '
     c                   endif
      *
     c                   eval      w_epos = %scan(' ' : d_fh_fanr)
     c                   if        w_epos > 16
     c                   eval      w_spos = w_epos - 15
     c                   else
     c                   eval      w_spos = 1
     c                   endif
     c                   eval      losfak = %subst(d_fh_fanr : w_spos : 15)
      *
      * Finn fakturaleverandør
      *
     c                   if        d_fh_fean <> 0
     c                   eval      rlevl5_eanl = d_fh_fean
     c     rlevl5_key    chain     rlevl5
     c                   if        %found
     c                   eval      loflev = rllevr
     c                   else
     c                   eval      loflev = 0
     c                   endif
     c                   endif
      *
      * Finn Kid-felt
      *
     c                   eval      ledilr_mlin = 300001
     c     ledilr_key    chain     ledilr
     c                   if        %found
     c                   eval      d_fakh4 = limeld
     c                   eval      lokidi = d_fh_kidn
     c                   endif
      *
      * Finn fakturatotal
      *
     c                   eval      ledilr_mlin = 900001
     c     ledilr_key    chain     ledilr
     c                   if        %found
     c                   eval      d_fakt = limeld
     c                   eval      lototf = d_ft_ftot
     c                   endif
5.51  *
5.51 c                   eval      lolopp = 1
      *
6.10 c                   time                    loedat
6.10 c                   time                    loetim
6.10 c                   eval      loeusr = l_user
7.01  * Finn bilagsnummer og legg ut
7.01  *
7.01 c     ' ':'0'       xlate     d_fh_bilna    d_fh_bilna
7.01 c                   if        d_fh_biln > 0
7.01 c                   eval      w_biln = d_fh_biln
7.01 c                   else
7.01 c                   eval      w_fell = laabin
7.01 c                   eval      w_type = '  '
7.01 c                   eval      w_kode = '0'
7.01 c                   eval      w_fast = *blank
7.01  *
7.01 c                   call      'AS100R'
7.01 c                   parm                    w_kode
7.01 c                   parm                    w_firm
7.01 c                   parm                    w_fell
7.01 c                   parm                    w_type
7.01 c                   parm                    w_fast
7.01 c                   parm                    w_biln
7.01 c                   endif
7.01  *
7.01 c                   eval      lobinr = w_biln
8.03 c                   eval      lomnum = d_mnum
     c                   update    lohelur
7.01  *
7.01  * Legger bilagsnummer tilbake til 100001 record
7.01  *
7.01 c                   eval      ledilu_type = d_type
7.01 c                   eval      ledilu_mnum = d_mnum
7.01 c                   eval      ledilu_mlin = 100001
7.01 c     ledilu_key    chain     ledilu
7.01 c                   if        %found
7.01 c                   eval      d_fakh1 = limeld
7.01 c                   eval      d_fh_biln = w_biln
7.01 c                   eval      limeld = d_fakh1
7.01 c                   update    ledilur
7.01 c                   endif
7.01  *
      *
     c     end_oppd      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å danne bestillingshode hvor bestilling mangler
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bygg_hode     begsr
      *
      * Bestem ordretype
      *
     c                   if        d_fh_dtyp = '380'
     c                   eval      w_otyp = 'IF'
     c                   else
     c                   eval      w_otyp = 'IK'
     c                   endif
      *
      * Finn neste ledige ordrenummer
      *
     c                   eval      w_fell = laabrk
     c                   eval      w_type = w_otyp
     c                   eval      w_fast = *blank
     c                   eval      w_kode = '0'
      *
     c     nytt_nr       tag
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
     c                   parm                    w_numm
      *
     c                   eval      lohelu_numm = w_numm
     c                   eval      lohelu_suff = 0
     c     lohelu_key    chain     lohelu
     c                   if        %found
     c                   goto      nytt_nr
     c                   endif
      *
      * Finn vareleverandør
      *
     c                   eval      rlevl5_eanl = d_fh_sean
     c     rlevl5_key    chain     rlevl5
     c                   if        not %found
     c                   eval      rllevr = 0
     c                   endif
      *
      * Legg inn tilgjengelig informasjon og skriv hode
      *
     c                   eval      lofirm = w_firm
     c                   eval      lonumm = w_numm
     c                   eval      losuff = 0
     c                   eval      loline = 0
     c                   eval      lootyp = w_otyp
     c                   eval      lolage = l_lage
     c                   eval      lobinr = 0
     c                   eval      lobitx = *blank
     c                   eval      lokidi = *blank
     c                   eval      lokref = 0
     c                   eval      lobdat = w_date
     c                   eval      loldat = *loval
     c                   eval      lofkda = *loval
     c                   eval      loffda = *loval
     c                   eval      lomoda = *loval
     c                   eval      lovida = *loval
     c                   eval      lobetb = rlbetb
     c                   eval      loinnk = 0
     c                   eval      lodist = rldist
     c                   eval      lolmat = rlform
     c                   eval      lolbet = rlbetb
     c                   eval      lovalk = rlvalk
     c                   eval      lorkat = rlrabk
     c                   eval      losped = 0
     c                   eval      lokjor = 0
6.12 c**                 eval      lomkod = 0
6.12 c                   eval      lomkod = rlmkod
     c                   eval      lolele = 0
     c                   eval      loldor = rllevr
     c                   eval      lolena = rlnavn
     c                   eval      lonavn = *blank
     c                   eval      loadr1 = *blank
     c                   eval      loadr2 = *blank
     c                   eval      losted = *blank
     c                   eval      loavde = l_avde
     c                   eval      lobsda = *loval
     c                   eval      lovref = *blank
     c                   eval      lodref = *blank
     c                   eval      lorekv = *blank
     c                   eval      lolmtx = *blank
     c                   eval      lolbtx = *blank
     c                   eval      lorabp = 0
     c                   eval      loornr = 0
     c                   eval      loorsu = 0
     c                   eval      lonett = 0
     c                   eval      lobrut = 0
     c                   eval      lokoli = 0
     c                   eval      lokonr = 0
     c                   eval      lototx = 0
     c                   eval      lototf = 0
     c                   eval      lototi = 0
     c                   eval      lototr = 0
     c                   eval      lototk = 0
     c                   eval      lowsid = l_wsid
     c                   eval      louser = l_user
     c                   eval      lodate = w_date
     c                   eval      lotime = w_time
     c                   eval      loladr = 0
     c                   eval      loflev = 0
     c                   eval      losorn = *blank
     c                   eval      losfak = *blank
      *
6.10 c                   time                    lodate
6.10 c                   time                    lotime
6.10 c                   eval      louser = l_user
6.10 c                   time                    loedat
6.10 c                   time                    loetim
6.10 c                   eval      loeusr = l_user
     c                   write     lohelur
      *
      * Posisjoner på post for innlegg av tilleggsinformasjon
      *
     c                   eval      lohelu_numm = lonumm
     c                   eval      lohelu_suff = 0
     c     lohelu_key    chain     lohelur
      *
     c     end_bygg      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å slette opprinnelige bestillingslinjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     slett_linjer  begsr
      *
     c     lodtl1_key    setll     lodtl1
     c     lodtl1_key    reade     lodtl1
      *
     c                   dow       not %eof
     c                   if        b_lety = *off and
     c                             ldlety <> 0
     c                   eval      w_lety = ldlety
     c                   eval      b_lety = *on
     c                   endif
      *
     c                   eval      lodtlu_numm = ldnumm
     c                   eval      lodtlu_suff = ldsuff
     c                   eval      lodtlu_line = ldline
     c     lodtlu_key    chain     lodtlur
     c                   if        %found
     c                   delete    lodtlur
8.05  * Ta vare på referansen til ordren - hvis finnes
8.05 c                   if        ldornu > 0
8.05 c                   eval      ledsiu_snum = ldnumm
8.05 c                   eval      ledsiu_ssuf = ldsuff
8.05 c                   eval      ledsiu_slin = ldline
8.05 c     ledsiu_key    chain     ledsiur
8.05 c                   if        not %found
8.05 c                   eval      ldsnum = ledsiu_snum
8.05 c                   eval      ldssuf = ledsiu_ssuf
8.05 c                   eval      ldslin = ledsiu_slin
8.05 c                   eval      ldsonu = ldornu
8.05 c                   eval      ldsosu = ldorsu
8.05 c                   eval      ldsoli = ldorli
8.05 c                   eval      ldsgti = ldeann
8.05 c                   eval      ldsnob = ldnobb
8.05 c                   eval      ldsenh = ldenh1
8.05 c                   time                    ldsoda
8.05 c                   time                    ldsoti
8.05 c                   eval      ldsous = l_user
8.05  * Sjekk om vi får treff på ordrelinje, hvis ikke prøv å øke suffiks. Fort gjort
8.05  * at dette kommer ut av synk. NB maks 5 forsøk på å øke suffiks.
8.05 c                   eval      fodtl1_numm = ldornu
8.05 c                   eval      fodtl1_suff = ldorsu
8.05 c                   eval      fodtl1_line = ldorli
8.05 c     fodtl1_key    chain     fodtl1
8.05 c                   if        not %found
8.05 c                   eval      x = 1
8.05 c     ny_suffiks    tag
8.05 c                   eval      fodtl1_numm = ldornu
8.05 c                   eval      fodtl1_suff = fodtl1_suff + 1
8.05 c                   eval      fodtl1_line = ldorli
8.05 c     fodtl1_key    chain     fodtl1
8.05 c                   if        %found
8.05 c                   eval      ldsosu = fodtl1_suff
8.05 c                   else
8.05 c                   eval      x = x +1
8.05 c                   if        x < 5
8.05 c                   goto      ny_suffiks
8.05 c                   endif
8.05 c                   endif
8.05 c                   endif
8.05  *
8.05 c                   write     ledsiur
8.05 c                   endif
8.05 c                   endif
     c                   endif
      *
     c     lodtl1_key    reade     lodtl1
     c                   enddo
      *
     c     end_slett     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å bygge opp nye bestillingslinjer fra EDI
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     nye_linjer    begsr
      *
      * Leser meldingslinjer og bygger nye bestillingslinjer
      *
     c                   eval      w_line = 0
     c                   eval      ledil1_mlin = 500001
     c     ledil1_key    setll     ledil1
     c                   read      ledil1
      *
     c                   dow       not %eof and
     c                             limlin < 550000
      *
     c                   eval      d_fakl = limeld
      *
      * Legger inn data fra bestillingshode
      *
     c                   eval      ldfirm = lofirm
     c                   eval      ldnumm = lonumm
     c                   eval      ldsuff = losuff
     c                   eval      ldotyp = lootyp
     c                   eval      ldlage = lolage
     c                   eval      ldornu = loornr
     c                   eval      ldorsu = loorsu
     c                   eval      ldorli = 0
8.05  * Hent linjereferansen om den finnes
8.05 c                   eval      ledsiu_snum = lonumm
8.05 c                   eval      ledsiu_ssuf = losuff
8.05 c                   eval      ledsiu_slin = liline
8.05 c     ledsiu_key    chain     ledsiu
8.05 c                   if        %found
8.05 c                   eval      ldorli = ldsoli
8.05 c                   unlock    ledsiu
8.05 c                   endif
      *
      * Legger inn data fra vareregister
      *
     c                   eval      w_enob = d_fl_vnob
6.30 c                   eval      w_mkod = *blank
     c                   exsr      hent_vare
      *
     c                   eval      ldvare = w_vare
6.31 c*                  eval      ldlvar = w_lvar
6.31 c                   movel     w_lvar        ldlvar
     c                   eval      ldtek1 = w_tek1
     c                   eval      ldtek2 = w_tek2
     c                   eval      ldnobb = w_nobb
     c                   eval      ldogrp = w_ogrp
     c                   eval      ldhgrp = w_hgrp
     c                   eval      ldugrp = w_ugrp
     c                   eval      ldlvre = w_lvre
8.06 c                   eval      ldeann = d_fl_vean
      *
      * Legger inn data fra EDI melding
      *
     c                   eval      ldenh1 = d_fl_fenh
     c                   eval      ldanta = d_fl_fkva
     c                   exsr      finn_pris
     c                   eval      ldipny = w_pris
6.21 c     ' ':'0'       xlate     d_fl_kopa     d_fl_kopa
6.21 c                   if        d_fl_kopr > 0
6.21 c                   eval      ldkpny = d_fl_kopr
6.21 c                   else
6.21 c                   eval      ldkpny = w_pris
6.21 c                   endif
     c                   eval(h)   ldsumi = d_fl_fkva * w_pris
     c                   eval(h)   ldsumk = d_fl_fkva * w_pris
6.30 c                   eval      ldomva = d_fl_mvak
6.37  *
6.37 c                   if        ldomva <> 'I' and
6.37 c                             ldomva <> 'J' and
6.37 c                             ldomva <> '0' and
6.37 c                             ldomva <> '1'
6.37 c                   eval      ldomva = '1'
6.37 c                   endif
6.37  *
6.30 c                   if        lomkod = 1
6.30 c                   eval      ldomva = '0'
6.30 c                   endif
6.33  * Ny mva kode håndtering
6.33 c                   if        lomkod = 2
6.33 c                   eval      vmval1_mkod = vvkmva
6.33 c     vmval1_key    chain     vmval1
6.33 c                   if        %found
6.33 c                   eval      ldomva = vamkui
6.33 c                   endif
6.33 c                   endif
6.30  * Finn kode basert på vare, hvis feltet er blankt
6.30 c                   if        ldomva = *blank
6.30 c                   if        w_mkod = 'E'
6.30 c                   eval      vmval1_mkod = vvkmva
6.30 c     vmval1_key    chain     vmval1
6.30 c                   if        %found
6.30 c                   eval      ldomva = vamkla
6.30 c                   else
6.30 c                   eval      ldomva = '1'
6.30 c                   select
6.30 c                   when      vvkmva = 1
6.30 c                   eval      ldomva = '0'
6.30 c                   when      vvkmva = 2
6.30 c                   eval      ldomva = 'I'
6.30 c                   when      vvkmva = 3
6.30 c                   eval      ldomva = 'J'
6.30 c                   endsl
6.30 c                   endif
6.30 c                   else
6.30 c                   eval      ldomva = '1'
6.30 c                   endif
6.36 c**                 else
6.36 c**                 eval      ldomva = '1'
6.30 c                   endif
      *
      * Nullstiller øvrige verdier
      *
     c                   eval      ldlety = w_lety
     c                   eval      ldavde = 0
     c                   eval      ldkont = 0
     c                   eval      ldspes = 0
6.20 c**                 eval      ldproj = 0
6.20 c                   eval      ldproj = *blank
     c                   eval      ldakti = *blank
     c                   eval      ldrab1 = 0
     c                   eval      ldrab2 = 0
     c                   eval      ldsumr = 0
     c                   eval      ldkplj = 0
      *
      * Legger ut ny bestillingslinje
      *
     c                   eval      ldltyp = ' '
     c                   eval      w_line = w_line + 10
     c                   eval      lodtlu_line = w_line
     c                   eval      ldline = w_line
     c     lodtlu_key    chain     lodtlur
     c                   if        not %found
6.10 c                   time                    ldodat
6.10 c                   time                    ldotim
6.10 c                   eval      ldousr = l_user
6.10 c                   time                    ldedat
6.10 c                   time                    ldetim
6.10 c                   eval      ldeusr = l_user
     c                   write     lodtlur
     c                   endif
      *
     c                   read      ledil1
     c                   enddo
8.05  *
8.05  * Slett hjelpetabell
8.05  *
8.05 c                   eval      ledsiu_snum = lonumm
8.05 c                   eval      ledsiu_ssuf = losuff
8.05 c                   eval      ledsiu_slin = 0
8.05 c     ledsiu_key    setll     ledsiu
8.05 c     ledsiu_key2   reade     ledsiu
8.05 c                   dow       not %eof
8.05 c                   delete    ledsiur
8.05 c**                 unlock    ledsiu
8.05 c     ledsiu_key2   reade     ledsiu
8.05 c                   enddo
      *
     c     end_nye       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å bygge opp bestillingslinjer fra tillegg/fradrag
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     til_linjer    begsr
      *
      * Leser meldingslinjer og bygger nye bestillingslinjer
      *
     c                   eval      ledil1_mlin = 600001
     c     ledil1_key    setll     ledil1
     c                   read      ledil1
      *
     c                   dow       not %eof and
     c                             limlin < 650000
      *
     c                   eval      d_fakh2 = limeld
      *
      * Legger inn data fra bestillingshode
      *
     c                   eval      ldfirm = lofirm
     c                   eval      ldnumm = lonumm
     c                   eval      ldsuff = losuff
     c                   eval      ldotyp = lootyp
     c                   eval      ldlage = lolage
     c                   eval      ldornu = loornr
     c                   eval      ldorsu = loorsu
     c                   eval      ldorli = 0
      *
      * Legger inn data fra vareregister
      *
     c                   exsr      hent_nobb
     c                   exsr      hent_vare
      *
     c                   eval      ldvare = w_vare
6.31 c*                  eval      ldlvar = w_lvar
6.31 c                   movel     w_lvar        ldlvar
     c                   eval      ldtek1 = w_tek1
     c                   eval      ldtek2 = w_tek2
     c                   eval      ldnobb = w_nobb
     c                   eval      ldogrp = w_ogrp
     c                   eval      ldhgrp = w_hgrp
     c                   eval      ldugrp = w_ugrp
     c                   eval      ldlvre = w_lvre
6.30  * MVA kode håndtering
6.30 c                   if        lomkod = 1
6.30 c                   eval      ldomva = '0'
6.30 c                   endif
6.33  * Ny mva kode håndtering
6.33 c                   if        lomkod = 2
6.33 c                   eval      vmval1_mkod = vvkmva
6.33 c     vmval1_key    chain     vmval1
6.33 c                   if        %found
6.33 c                   eval      ldomva = vamkui
6.33 c                   endif
6.33 c                   endif
6.30  * Finn kode basert på vare, hvis feltet er blankt
6.30 c                   if        ldomva = *blank
6.30 c                   if        w_mkod = 'E'
6.30 c                   eval      vmval1_mkod = vvkmva
6.30 c     vmval1_key    chain     vmval1
6.30 c                   if        %found
6.30 c                   eval      ldomva = vamkla
6.30 c                   else
6.30 c                   eval      ldomva = '1'
6.30 c                   select
6.30 c                   when      vvkmva = 1
6.30 c                   eval      ldomva = '0'
6.30 c                   when      vvkmva = 2
6.30 c                   eval      ldomva = 'I'
6.30 c                   when      vvkmva = 3
6.30 c                   eval      ldomva = 'K'
6.30 c                   endsl
6.30 c                   endif
6.30 c                   else
6.30 c                   eval      ldomva = '1'
6.30 c                   endif
6.36 c**                 else
6.36 c**                 eval      ldomva = '1'
6.30 c                   endif
      *
      * Legger inn data fra EDI melding
      *
     c                   if        d_fh_ttek <> *blank
     c                   eval      ldtek1 = d_fh_ttek
     c                   eval      ldtek2 = *blank
     c                   endif
     c                   eval      ldenh1 = 'STK'
     c                   eval      ldanta = 1
     c                   eval      ldipny = d_fh_tbel
     c                   eval      ldkpny = d_fh_tbel
     c                   eval(h)   ldsumi = d_fh_tbel
     c                   eval(h)   ldsumk = d_fh_tbel
      *
      * Nullstiller øvrige verdier
      *
     c                   eval      ldlety = w_lety
     c                   eval      ldavde = 0
     c                   eval      ldkont = 0
     c                   eval      ldspes = 0
6.20 c**                 eval      ldproj = 0
6.20 c                   eval      ldproj = *blank
     c                   eval      ldakti = *blank
     c                   eval      ldrab1 = 0
     c                   eval      ldrab2 = 0
     c                   eval      ldsumr = 0
     c                   eval      ldkplj = 0
      *
      * Legger ut ny bestillingslinje
      *
     c                   if        d_fh_tsig = 'A  '
     c                   eval      ldltyp = '-'
     c                   else
     c                   eval      ldltyp = ' '
     c                   endif
      *
     c                   eval      w_line = w_line + 10
     c                   eval      lodtlu_line = w_line
     c                   eval      ldline = w_line
     c     lodtlu_key    chain     lodtlur
     c                   if        not %found
6.10 c                   time                    ldodat
6.10 c                   time                    ldotim
6.10 c                   eval      ldousr = l_user
6.10 c                   time                    ldedat
6.10 c                   time                    ldetim
6.10 c                   eval      ldeusr = l_user
     c                   write     lodtlur
     c                   endif
      *
     c                   read      ledil1
     c                   enddo
      *
     c     end_til       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å rekalkulere bestilling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kalk_best     begsr
      *
     c                   eval      w_enor = ' '
      *
     c                   call      'LO730R'
     c                   parm                    w_enor
     c                   parm                    w_firm
     c                   parm                    lohelu_numm
     c                   parm                    lohelu_suff
      *
     c     end_kalk      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å bytte ordretype til inngående faktura
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     endre_otyp    begsr
      *
      * Leser ordretyperegister og finner neste ordre type
      *
     c                   eval      votyl1_otyp = lootyp
     c     votyl1_key    chain     votyl1
     c                   if        not %found
     c                   goto      end_endre
     c                   endif
      *
      * Bytter til neste ordretype (hvis utfyllt; ellers hardkodet)
      *?? BM Skattum/TERRON ??
      *
     c     lohelu_key    chain     lohelur
     c                   if        %found
     c*                  if        vaonxt = *blank
     c                   if        d_fh_dtyp = '380'
     c                   eval      lootyp = 'IF'
     c                   else
     c                   eval      lootyp = 'IK'
     c                   endif
     c*                  else
     c*                  eval      lootyp = vaonxt
     c*                  endif
6.10 c                   time                    loedat
6.10 c                   time                    loetim
6.10 c                   eval      loeusr = l_user
     c                   update    lohelur
     c                   endif
      *
     c     end_endre     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å skrive ut bestilling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_fakt    begsr
      *
     c                   eval      w_valg = 0
      *
     c                   call      'LO600R'
     c                   parm                    w_retu
     c                   parm                    w_firm
     c                   parm                    lohelu_numm
     c                   parm                    lohelu_suff
     c                   parm                    w_valg
      *
     c     end_skriv     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å hente vareinformasjon fra eget vareregister
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_vare     begsr
      *
      * Nullstiller verdier
      *
     c                   eval      w_vare = *blank
     c                   eval      w_lvar = *blank
     c                   eval      w_tek1 = *blank
     c                   eval      w_tek2 = *blank
     c                   eval      w_nobb = 0
     c                   eval      w_ogrp = 0
     c                   eval      w_hgrp = 0
     c                   eval      w_ugrp = 0
     c                   eval      w_lvre = 0
      *
7.02 c                   if        w_enob = 0    and
7.02 c                             d_fl_vean = 0
7.02 c                   eval      w_lvar = d_fl_vlev
7.02 c                   eval      w_tek1 = d_fl_tek1
7.02 c                   eval      w_tek2 = d_fl_tek2
7.02 c                   eval      w_nobb = d_fl_vnob
7.02 c                   eval      w_mkod = 'U'
7.02 c                   goto      end_vare
7.02 c                   endif
      *
6.11  * Hvis nobbnummer er 0, prøv å hente nobbnummer via GTIN kode
6.11  * hvis denne finnes. Resten av rutine fortsetter som før.
6.11 c                   if        w_enob = 0 and
6.11 c                             d_fl_vean <> 0
6.11 c                   exsr      hent_gtin
6.11 c                   endif
      *
6.39 c                   if        w_enob <> 0
6.38  * sjekker om logistikk-vare
6.38 c                   eval      vvvare = *blank
6.38 c/exec sql
6.38 c+ select vvlvnr,
6.38 c+        vvllva,
6.38 c+        vvtek1,
6.38 c+        vvtek2,
6.38 c+        vvogrp,
6.38 c+        vvhgrp,
6.38 c+        vvugrp
6.38 c+ into   :vvvare,
6.38 c+        :vvlvar,
6.38 c+        :vvtek1,
6.38 c+        :vvtek2,
6.38 c+        :vvogrp,
6.38 c+        :vvhgrp,
6.38 c+        :vvugrp
6.38 c+ from   vvlepf
6.38 c+ inner join vvarpf on
6.38 c+               vvfirm = vvlfir and
6.38 c+               vvvare = vvlvnr
6.38 c+ where  vvlnob = :w_enob
6.38 c+ fetch first 1 rows only
6.38 c/end-exec
6.38 c                   if        vvvare = *blank
     c                   eval      vvarl3_nobb = w_enob
     c     vvarl3_key    chain     vvarl3r
     c                   if        not %found or
     c                             w_enob = *zero
     c                   exsr      hent_vadm
     c                   goto      end_vare
     c                   endif
6.38 c                   endif
6.39 c                   else
6.39 c                   exsr      hent_vadm
6.39 c                   goto      end_vare
6.39 c                   endif
      *
     c                   eval      w_vare = vvvare
6.31 c*                  eval      w_lvar = vvlvar
6.31 c                   movel     vvlvar        w_lvar
     c                   eval      w_tek1 = vvtek1
     c                   eval      w_tek2 = vvtek2
     c                   eval      w_nobb = vvnobb
     c                   eval      w_ogrp = vvogrp
     c                   eval      w_hgrp = vvhgrp
     c                   eval      w_ugrp = vvugrp
     c                   eval      w_lvre = 0
6.30 c                   eval      w_mkod = 'E'
8.06  * Hvis anne logikk over feiler, bruk verdier fra EDI fakturaen
8.06 c                   if        w_nobb = 0
8.06 c                   eval      w_nobb = w_enob
8.06 c                   endif
      *
     c     end_vare      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å hente vareinformasjon fra vareadministrasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_vadm     begsr
      *
     c                   eval      jvarl3_nobb = w_enob
     c     jvarl3_key    chain     jvarl3r
     c                   if        %found and
     c                             w_enob <> *zero
     c                   eval      w_vare = jvvare
6.31 c*                  eval      w_lvar = jvlvar
6.31 c                   movel     jvlvar        w_lvar
     c                   eval      w_tek1 = jvtek1
     c                   eval      w_tek2 = jvtek2
     c                   eval      w_nobb = jvnobb
     c                   eval      w_ogrp = jvogrp
     c                   eval      w_hgrp = jvhgrp
     c                   eval      w_ugrp = jvugrp
     c                   eval      w_lvre = 1
6.30 c                   eval      w_mkod = 'V'
     c                   else
      *
      * Varen finnes ikke i EVR/VA
      *
     c                   movel     d_fl_vnob     w_vare
     c                   eval      w_lvar = d_fl_vlev
     c                   eval      w_tek1 = d_fl_tek1
     c                   eval      w_tek2 = d_fl_tek2
     c                   eval      w_nobb = d_fl_vnob
6.30 c                   eval      w_mkod = 'U'
      *
     c                   endif
      *
     c     end_vadm      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å beregne nettopris for oppdatering av bestilling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_pris     begsr
      *
      * Nullstiller
      *
     c                   eval      w_tfra = 0
     c                   eval      w_ttil = 0
     c                   eval      w_kpri = 0
      *
      * Tillegg 1
      *
     c                   eval      w_frad = 0
     c                   if        d_fl_bel1  <> 0 and
     c                             d_fl_fkva  <> 0
     c                   eval(h)   w_frad = d_fl_bel1 / d_fl_fkva
     c                   else
     c                   if        d_fl_pro1 <> 0
     c                   eval      w_frad = (d_fl_pris * d_fl_pro1 ) / 100
     c                   endif
     c                   endif
      *
     c                   if        d_fl_sig1 = 'A  '
     c                   eval      w_tfra = w_tfra + w_frad
     c                   else
     c                   eval      w_ttil = w_ttil + w_frad
     c                   endif
      *
      * Tillegg 2
      *
     c                   eval      w_kpri = d_fl_pris + w_ttil - w_tfra
     c                   eval      w_frad = 0
     c                   if        d_fl_bel2  <> 0 and
     c                             d_fl_fkva  <> 0
     c                   eval(h)   w_frad = d_fl_bel2 / d_fl_fkva
     c                   else
     c                   if        d_fl_pro2 <> 0
     c                   eval      w_frad = (w_kpri * d_fl_pro2 ) / 100
     c                   endif
     c                   endif
      *
     c                   if        d_fl_sig2 = 'A  '
     c                   eval      w_tfra = w_tfra + w_frad
     c                   else
     c                   eval      w_ttil = w_ttil + w_frad
     c                   endif
      *
      * Tillegg 3
      *
     c                   eval      w_kpri = d_fl_pris + w_ttil - w_tfra
     c                   eval      w_frad = 0
     c                   if        d_fl_bel3  <> 0 and
     c                             d_fl_fkva  <> 0
     c                   eval(h)   w_frad = d_fl_bel3 / d_fl_fkva
     c                   else
     c                   if        d_fl_pro3 <> 0
     c                   eval      w_frad = (w_kpri * d_fl_pro3 ) / 100
     c                   endif
     c                   endif
      *
     c                   if        d_fl_sig3 = 'A  '
     c                   eval      w_tfra = w_tfra + w_frad
     c                   else
     c                   eval      w_ttil = w_ttil + w_frad
     c                   endif
      *
      * Tillegg 4
      *
     c                   eval      w_kpri = d_fl_pris + w_ttil - w_tfra
     c                   eval      w_frad = 0
     c                   if        d_fl_bel4  <> 0 and
     c                             d_fl_fkva  <> 0
     c                   eval(h)   w_frad = d_fl_bel4 / d_fl_fkva
     c                   else
     c                   if        d_fl_pro4 <> 0
     c                   eval      w_frad = (w_kpri * d_fl_pro4 ) / 100
     c                   endif
     c                   endif
      *
     c                   if        d_fl_sig4 = 'A  '
     c                   eval      w_tfra = w_tfra + w_frad
     c                   else
     c                   eval      w_ttil = w_ttil + w_frad
     c                   endif
      *
      * Tillegg 5
      *
     c                   eval      w_kpri = d_fl_pris + w_ttil - w_tfra
     c                   eval      w_frad = 0
     c                   if        d_fl_bel5  <> 0 and
     c                             d_fl_fkva  <> 0
     c                   eval(h)   w_frad = d_fl_bel5 / d_fl_fkva
     c                   else
     c                   if        d_fl_pro5 <> 0
     c                   eval      w_frad = (w_kpri * d_fl_pro5 ) / 100
     c                   endif
     c                   endif
      *
     c                   if        d_fl_sig5 = 'A  '
     c                   eval      w_tfra = w_tfra + w_frad
     c                   else
     c                   eval      w_ttil = w_ttil + w_frad
     c                   endif
      *
      * Tillegg 6
      *
     c                   eval      w_kpri = d_fl_pris + w_ttil - w_tfra
     c                   eval      w_frad = 0
     c                   if        d_fl_bel6  <> 0 and
     c                             d_fl_fkva  <> 0
     c                   eval(h)   w_frad = d_fl_bel6 / d_fl_fkva
     c                   else
     c                   if        d_fl_pro6 <> 0
     c                   eval      w_frad = (w_kpri * d_fl_pro6 ) / 100
     c                   endif
     c                   endif
      *
     c                   if        d_fl_sig6 = 'A  '
     c                   eval      w_tfra = w_tfra + w_frad
     c                   else
     c                   eval      w_ttil = w_ttil + w_frad
     c                   endif
      *
      * Nobb pris?
      *
     c                   eval      w_nobp = *zero
     c                   eval      w_ntpe = *zero
     c                   eval      w_netp = *zero
     c                   eval      w_pris = *zero
      *
     c                   if        d_fl_prkv = 'AAB'
6.34 c                   eval(h)   w_pris = d_fl_pris + w_ttil - w_tfra
     c                   eval      w_nobp = d_fl_pris + w_ttil - w_tfra
     c                   else
     c                   eval      w_pris = d_fl_pris
     c                   eval      w_ntpe = d_fl_pris
     c                   endif
      *
     c                   if        d_fl_fkva <> *zero
     c                   if        d_fl_bnet <> *zero
     c                   eval(h)   w_netp = d_fl_bnet / d_fl_fkva
     c                   endif
     c                   endif
      *
     c                   eval      w_psav = w_pris
      *
      * Korr. - Bruker nettopris uten kalk. av tlg./fradrag uansett...
      *
     c*                  if        w_pris = *zero
     c                   if        w_netp <> *zero
     c                   eval      w_pris = w_netp
     c                   endif
     c*                  endif
      *
     c     end_pris      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finne nobb-nummer på tillegg/fradrag
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_nobb     begsr
      *
     c                   select
     c                   when      d_fh_tkod = 'FRA'
     c                   eval      w_enob = 26924696
     c                   when      d_fh_tkod = 'FC '
     c                   eval      w_enob = 26924696
     c                   when      d_fh_tkod = 'IN '
     c                   eval      w_enob = 26924696
     c                   when      d_fh_tkod = 'HD '
     c                   eval      w_enob = 26924761
     c                   when      d_fh_tkod = 'PI '
     c                   eval      w_enob = 26924803
     c                   when      d_fh_tkod = 'PBE'
     c                   eval      w_enob = 26924829
     c                   when      d_fh_tkod = 'PC '
     c                   eval      w_enob = 26924837
     c                   when      d_fh_tkod = 'ZEE'
     c                   eval      w_enob = 26924985
     c                   when      d_fh_tkod = 'RCH'
     c                   eval      w_enob = 26925024
     c                   when      d_fh_tkod = 'IS '
     c                   eval      w_enob = 26925032
     c                   endsl
      *
     c     end_nobb      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Kontroll av meldingsdata
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_meld      begsr
      *
     c                   if        b_meld = *off
     c                   eval      p_errk = '99'
     c                   leavesr
     c                   endif
      *
     c                   eval      b_pris = *on
      *
      * Leser varelinjer og sjekker konsistens ift rabattbeløp
      *
     c                   eval      ledil1_mlin = 500001
     c     ledil1_key    setll     ledil1
     c                   read      ledil1
      *
     c                   dow       not %eof and
     c                             limnum = d_mnum and
     c                             limlin < 550000
      *
     c                   eval      d_fakl = limeld
     c                   exsr      finn_pris
      *
      * Hvis (NOBB-pris - rab.) <> (Nettobel.varelinje/antall) og
      * rabatt ikke er spesifisert; gi mld om mangelfull EDI-fakt.....
      *
     c*                  if        w_nobp <> *zero
     c*                  if        w_netp <> *zero
     c*                  if        w_nobp <> w_netp
     c*                  if        w_tfra = *zero
     c*                  eval      b_pris = *off
     c*                  leave
     c*                  endif
     c*                  endif
     c*                  endif
     c*                  endif
      *
     c                   read      ledil1
     c                   enddo
      *
     c                   if        b_pris = *off
     c                   eval      p_errk = '1'
     c                   leavesr
     c                   endif
      *
     c     end_sjk       endsr
      *
6.11  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.11  *  Subrutine for å hente nobbnummer via GTIN koden.
6.11  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.11 c     hent_gtin     begsr
6.11  *
6.11 c                   move      d_fl_vean     p_eann
6.11 c                   eval      p_stat = *blank
6.11 c                   eval      p_enhe = *blank
6.11 c                   eval      p_nobb = *zero
6.11 c                   eval      p_vare = *blank
6.11  *
6.32 c**                 call      'VE715R'
6.32 c                   call      'VE717R'
6.11 c                   parm                    w_firm
6.11 c                   parm                    p_eann
6.11 c                   parm                    p_vare
6.11 c                   parm                    p_enhe
6.11 c                   parm                    p_nobb
6.11 c                   parm                    p_stat
6.11  *
6.11 c                   if        p_stat = '1'
6.11  * Sjekk at det er nobbnummer som kommer i retur
6.11 c                   if        p_nobb <> 0
6.11 c                   eval      w_enob = p_nobb
6.11 c                   endif
6.11  *
6.11 c                   endif
6.11  *
6.11 c     end_gtin      endsr
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *  Subrutine for å arkivere EDI melding
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *
7.01 c     arkiv_fakt    begsr
7.01  *
7.01 c                   eval      p_firm = w_firm
7.01 c                   eval      p_type = 'FAKT'
7.01 c                   eval      p_mnum = d_mnum
7.01 c                   eval      p_mlin = d_mlin
7.01 c                   eval      p_kode = *blank
7.01 c                   eval      p_biln = w_biln
7.01 c                   eval      w_arec = p_prec
7.01  *
8.02 c                   if        u_802 = *on
8.04 c**                 call      'LI615C'
8.04 c                   call      'LI620C'
7.01 c                   parm                    w_arec
8.02 c                   endif
7.01  *
7.01 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Tar inn parameter fra forrige program
      *
     c     *entry        plist
     c                   parm                    p_parm
     c                   parm                    p_errk
      *
     c                   eval      d_prec = p_parm
      *
      *  Key for oppdatering av BESTILLING-HODE
      *
     c     lohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelu_numm
     c                   kfld                    lohelu_suff
      *
      *  Key for les av BESTILLING-LINJE
      *
     c     lodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtl1_numm
     c                   kfld                    lodtl1_suff
      *
      *  Key for oppdatering BESTILLING-LINJE
      *
     c     lodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlu_numm
     c                   kfld                    lodtlu_suff
     c                   kfld                    lodtlu_line
      *
      * Key for les av EDI-mottaksfil
      *
     c     ledilr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ledilr_type
     c                   kfld                    ledilr_mnum
     c                   kfld                    ledilr_mlin
      *
      * Key for posisjonering av EDI-mottaksfil
      *
     c     ledil1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ledil1_type
     c                   kfld                    ledil1_mnum
     c                   kfld                    ledil1_mlin
      *
      * Key for les av leverandør på EAN - lokasjonsnummer
      *
     c     rlevl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl5_eanl
      *
      * Key for oppslag på ordretype
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for les på nobb-nummer
      *
     c     vvarl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl3_nobb
      *
      * Key for les av vare på nobb-nummer (Vareadm.)
      *
     c     jvarl3_key    klist
     c                   kfld                    jvarl3_nobb
      *
      *  Key for les av LAGER-KODE
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
6.30  *
6.30  * Key for les av mva kode
6.30  *
6.30 c     vmval1_key    klist
6.30 c                   kfld                    w_firm
6.30 c                   kfld                    vmval1_mkod
7.01  *
7.01  * Key for les av EDI-mottaksfil
7.01  *
7.01 c     ledilu_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    ledilu_type
7.01 c                   kfld                    ledilu_mnum
7.01 c                   kfld                    ledilu_mlin
8.05  *
8.05  * Key for les av EDI-arbeidsregister
8.05  *
8.05 c     ledsiu_key    klist
8.05 c                   kfld                    w_firm
8.05 c                   kfld                    ledsiu_snum
8.05 c                   kfld                    ledsiu_ssuf
8.05 c                   kfld                    ledsiu_slin
8.05  *
8.05  * Key for les av EDI-arbeidsregister
8.05  *
8.05 c     ledsiu_key2   klist
8.05 c                   kfld                    w_firm
8.05 c                   kfld                    ledsiu_snum
8.05 c                   kfld                    ledsiu_ssuf
8.05  *
8.05  * Key for les av ordre
8.05  *
8.05 c     fodtl1_key    klist
8.05 c                   kfld                    w_firm
8.05 c                   kfld                    fodtl1_numm
8.05 c                   kfld                    fodtl1_suff
8.05 c                   kfld                    fodtl1_line
      *
      *  Legger inn firmanummer
      *
     c                   eval      w_firm = l_firm
      *
      *  Hent opplysninger fra LAGER-KODE
      *
     c     lstsl1_key    chain     lstsl1
      *
      *  Legger inn dato og klokkeslett (timestamp)
      *
     c                   time                    w_date
     c                   time                    w_time
      *
      * Legg inn nøkkelbegreper fra parametre
      *
     c                   eval      ledilr_type = d_type
     c                   eval      ledilr_mnum = d_mnum
     c                   eval      ledilr_mlin = d_mlin
     c                   eval      ledil1_type = d_type
     c                   eval      ledil1_mnum = d_mnum
     c                   eval      ledil1_mlin = d_mlin
      *
      * Les meldingshode
      *
     c                   eval      ledilr_mlin = 100001
     c     ledilr_key    chain     ledilr
     c                   if        %found
     c                   eval      lohelu_numm = linumm
     c                   eval      lohelu_suff = lisuff
     c                   eval      lodtl1_numm = linumm
     c                   eval      lodtl1_suff = lisuff
     c                   eval      lodtlu_numm = linumm
     c                   eval      lodtlu_suff = lisuff
     c                   eval      d_fakh1 = limeld
     c                   eval      b_meld = *on
     c                   else
     c                   eval      b_meld = *off
     c                   endif
      *
     c                   eval      b_lety = *off
     c                   eval      p_errk = *blank
8.02  *
8.02  * Skal vi arkivere EDI faktura.
8.02  *
8.02   CallP CO402R(l_filg:w_firm:0:'ARKIVER_EDI_FAKTURA':
8.02                    co402_verdi1:co402_verdi2);
8.02   if co402_verdi1 = '1';
8.02     u_802 = *on;
8.02   endif;
      *
     c                   endsr
