     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * System  . . . . : ASBANK                                        *
      * Program . . . . : DB811R                                        *
      * Beskrivelse . . : Konvertering av kontoinformasjon.             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign *
      * --------- ------  ----------------------------------------------*
      *           151199  Nytt program                             RH   *
      *                                                                 *
      * 15119     151199  Utvidet testen på om post er mottatt     RH   *
      *                   tidligere, d.v.s. om det er dobbel       RH   *
      *                   kontoinformasjon.  Endringen gjelder     RH   *
      *                   her kun for Fellesdata-FDI (STORM)       RH   *
      *                                                                 *
      * 25010     250100  Rettet ÅR2000-feil.                      RH   *
      *                                                                 *
      * 17063     170603  Lagt ny parameter i entry for å unngå    KOB  *
      *                   at teller nullstilles for hvert call     KOB  *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *    60-62 = Fileindikatorer chain etc.                           *
      *    63-69 = Varsle v/feilmeldinger.                              *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     fdfasl2    uf   e           k disk    rename(dfaspfr:dfasl2r)
     fdbnkpf    o  a e             disk    rename(dbnkpfr:dbnkpff)
     fddobpf    o  a e             disk    rename(ddobpfr:ddobpff)
     fddkil1    uf a e           k disk    rename(ddkipfr:ddkil1r)
     fdbnkl4    if   e           k disk    rename(dbnkpfr:dbnkl4r)
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Datastruktur for dato - DDMMÅÅ:
      *
     d                 ds
     d dsdat1                         6  0
     d  dsaar1                        2  0 overlay(dsdat1)
     d  dsmnd1                        2  0 overlay(dsdat1:3)
     d  dsdag1                        2  0 overlay(dsdat1:5)
      *
      * Datastruktur for redigering av BBS-refereanse
      *
     d                 ds
     d dsbbsr                         9
     d  dsbbs1                        3    overlay(dsbbsr)
     d  dsbbs2                        1    overlay(dsbbsr:4)
     d  dsbbs3                        5    overlay(dsbbsr:5)
      *
      * Datastruktur for redigering av Arkiv-referanse
      *
     d                 ds
     d dsarkr                        12
     d  dsark1                        1    overlay(dsarkr)
     d  dsark2                        5    overlay(dsarkr:2)
     d  dsark3                        6    overlay(dsarkr:7)
      *
     d wpalf3          s              3
     d wparf           s              9
     d wpbank          s              3
     d wpbdin          s              4  0
     d wpbdmn          s              2  0
     d wpbein          s             15  2
     d wpbe15          s             15  2
     d wpbkin          s              1
     d wpbkon          s             11  0
     d wpbri1          s              4
     d wpbri2          s              2
     d wpbrut          s              6
     d wpdato          s              8  0
     d wpdobb          s              1
     d wpfdat          s               d   datfmt(*iso)
     d wpfirm          s              3  0
     d wptist          s               z
     d wpnrf           s             11  0
     d wpsain          s             15  2
     d wpsa15          s             15  2
     d wpsdat          s               d    datfmt(*iso)
     d wpskin          s              1
     d wptime          s              6  0
     d wptrin          s              6
     d wptrut          s             12
     d wptxin          s              7
     d wpvdin          s              6  0
     d wpvdmn          s              2  0
     d wwbdin          s              4
     d wwbdmn          s              2
     d wwbein          s             15
     d wwsain          s             15
     d wwvdin          s              6
     d wwvdmn          s              2
      *
      * Definering av variable for timestamp
      *
     d w_tmst          s               z
     d w_tmst_20       s             20
     d w_tell          s              6  0
     d w_tell_alf      s              6
      *
     d dfasl2_bkon     s                   like(dabkon)
      *
      /eject
      *
     c                   exsr      xpost
      *
      /space 3
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
      *
      * Oppdaterer Faste data med eldste og nyeste post som er
      * lagt ut klar til avstemming:
      *
     c     wpbkon        chain     dfasl2                             60
      *
     c                   if        *in60 = *off and
     c                             wpfdat <> *hival
     c                   eval      dafdat = wpfdat
     c                   eval      dasdat = wpsdat
     c                   eval      datist = wptist
     c                   update    dfasl2r
     c                   endif
      *
     c                   eval      *inlr = *on
      *
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for utlegging av post på file DBNKPF:                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xpost         begsr
      *
      *  Overfører data til felter i DBNKPF:
      *
     c                   exsr      xoverf
      *
      * Tester på om post er mottat tidligere:
      *
      * IN61 er PÅ dersom den IKKE ER mottatt før.
      * IN61 er AV dersom den ER mottat før.
      *
      * Egen test for poster fra Fellesdata:
      *
     c                   if        wpbank <> 'FFI'
      *
     c     dbnkl4_key    chain     dbnkl4                             61
      *
     c     *in61         cabeq     *on           okpost
     c                   else
      *
      * FFI
      *
     c     ddkil1_key    chain     ddkil1                             61
      *
     c                   if        *in61 = *on
     c                   exsr      xdki
     c                   goto      okpost
     c                   endif
      *
     c                   endif
      *
     c                   move      '1'           wpdobb
      *
      * Skriver post ut på DDOBPF dersom posten er mottatt tidligere,
      * D.v.s.:  - Bilagsdato på posten er mindre enn sist innleste
      *            post på Faste data.
      *
     c                   if        dbbdat < dasdat
     c                   move      '1'           wpdobb
     c                   exsr      xdobb
     c                   goto      xover
     c                   endif
      *
      * - Bilagsdato på posten er lik 7Til dato" på faste data, OG
      *   samtidig ble forrige leste post avvist (lagt ut på DDOBPF)
      *   i denne batchen:
      *
     c                   if        dbbdat = dasdat and
     c                             wpdobb = '1'
     c                   exsr      xdobb
     c                   goto      xover
     c                   endif
      *
     c     okpost        tag
      *
      *  Overfører data til felter i DBNKPF.  Dette må gjøres på nytt
      *  for å unngå feil v/tilslag på feil post v/chain mot DDKIL1
      *
     c                   eval      wpsain = wpsa15
     c                   eval      wpbein = wpbe15
      *
     c                   exsr      xoverf
      *
      * Posten har ikke vært mottatt tidligere, og kan derfor
      * legges ut på file DBNKPF:
      *
      * Tar vare på eldste- og nyeste dato:
      *
     c                   if        dbbdat < wpfdat
     c                   eval      wpfdat = dbbdat
     c                   endif
      *
     c                   if        dbbdat > wpsdat
     c                   eval      wpsdat = dbbdat
     c                   endif
      *
     c                   eval      wpdobb = *blank
      *
     c                   write     dbnkpff
      *
     c     xover         tag
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for å overføre informasjon til DBNKPF:                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -  - *
      *
     c     xoverf        begsr
      *
     c                   eval      dbstat = *blanks
     c                   eval      dbavst = *blanks
     c                   eval      dbbkon = wpbkon
      *
      * Teller/Timestamp
      *
     c                   eval      w_tell = w_tell + 1
     c                   move      w_tell        w_tell_alf
     c                   time                    wptist
     c     *iso0         move      wptist        w_tmst_20
     c                   move      w_tell_alf    w_tmst_20
     c     *iso0         move      w_tmst_20     dbtist
      *
      * Valuteringsdato
      *
     c     *ymd          move      wpvdin        dbvdat
      *
      * Bilagsdato
      *
     c                   movel     wpvdin        dsdat1
     c                   movel     wpbdin        dsmnd1
     c                   move      wpbdin        dsdag1
      *
     c                   if        wpvdmn = 01 and
     c                             wpbdmn = 12
     c                   eval      dsaar1 = dsaar1 - 1
     c                   endif
      *
     c     *ymd          move      dsdat1        dbbdat
      *
     c                   eval      wpsa15 = wpsain
     c                   eval      wpbe15 = wpbein
      *
      * Snur fortegn på saldo dersom Kredit-post.  DSSKIN har verdien
      * "C" ved Debet-post:
      *
     c                   if        wpskin <> 'C'
     c                   eval      wpsain = wpsain * -1
     c                   endif
      *
     c                   eval      dbsald = wpsain
      *
      * Snur fortegn på beløp dersom Kredit-post.  WPBKIN har verdien
      * "C" ved Debet-post:
      *
     c                   if        wpbkin <> 'C'
     c                   eval      wpbein = wpbein * -1
     c                   endif
      *
     c                   eval      dbbelp = wpbein
      * Tekst:
     c                   eval      dbteks = *blanks
     c                   movel     wptxin        dbteks
      *
      * REDIGERER REFERANSEFELTER:
      *
      * BBS-ref:
      *
     c                   movel     wpbri1        wpbrut
     c                   move      wpbri2        wpbrut
      * Trans.ref:
     c                   eval      wptrut = *blanks
     c                   movel     wptrin        wptrut
     c                   movel     wptrin        wpalf3
      * Bygger DS for BBS:
     c                   movel     wpbrut        dsbbsr
     c                   move      wpalf3        dsbbsr
     c                   movel     dsbbs1        dbref1
     c                   movel     dsbbs2        dbref2
     c                   movel     dsbbs3        dbref3
      * Bygger DS for Arkivref:
     c                   movel     wptrut        dsarkr
     c                   movel     dsark1        dbref4
     c                   movel     dsark2        dbref5
     c                   movel     dsark3        dbref6
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for å legge ut kontoinformasjon på file DDOBPF, d v s *
      * kontoinformasjon som er mottatt tidligere.                      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -  - *
      *
     c     xdobb         begsr
      *
     c                   eval      dbtist = wptist
      *
     c                   eval      destat = dbstat
     c                   eval      deavst = dbavst
     c                   eval      debkon = dbbkon
     c                   eval      detist = dbtist
     c                   eval      debdat = dbbdat
     c                   eval      devdat = dbvdat
     c                   eval      deteks = dbteks
     c                   eval      deref1 = dbref1
     c                   eval      deref2 = dbref2
     c                   eval      deref3 = dbref3
     c                   eval      deref4 = dbref4
     c                   eval      deref5 = dbref5
     c                   eval      deref6 = dbref6
     c                   eval      debelp = dbbelp
     c                   eval      desald = dbsald
      *
     c                   time                    dedato
     c                   movel     wptime        deklok
      *
      * Skriver post:
      *
     c                   write     ddobpff
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for å legge ut kontoinformasjon på file DDKIL1        *
      * Filen brukes i test på om post er mottatt tidligere             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -  - *
      *
     c     xdki          begsr
      *
     c                   eval      dbtist = wptist
      *
     c                   eval      dcavst = dbavst
     c                   eval      dcbkon = dbbkon
     c                   eval      dctist = dbtist
     c                   eval      dcbdat = dbbdat
     c                   eval      dcvdat = dbvdat
     c                   eval      dcteks = dbteks
     c                   eval      dcref1 = dbref1
     c                   eval      dcref2 = dbref2
     c                   eval      dcref3 = dbref3
     c                   eval      dcref4 = dbref4
     c                   eval      dcref5 = dbref5
     c                   eval      dcref6 = dbref6
     c                   eval      dcbelp = dbbelp
     c                   eval      dcsald = dbsald
      *
     c                   eval      dcnref = wpnrf
     c                   movel     wparf         dcaref
      *
     c                   eval      dcmoÅr = *year
     c                   eval      dcmomn = *month
      *
      * Skriver post:
      *
     c                   write     ddkil1r
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Parameterliste:
      *
     c     *entry        plist
     c                   parm                    wpfirm
     c                   parm                    wpbkon
     c                   parm                    wpskin
     c                   parm                    wwsain
     c                   parm                    wwvdin
     c                   parm                    wwvdmn
     c                   parm                    wwbdin
     c                   parm                    wwbdmn
     c                   parm                    wpbkin
     c                   parm                    wwbein
     c                   parm                    wpbri1
     c                   parm                    wpbri2
     c                   parm                    wptxin
     c                   parm                    wptrin
     c                   parm                    wpdobb
     c                   parm                    wptime
     c                   parm                    wpnrf
     c                   parm                    wparf
     c                   parm                    w_tell
      *
      * Hjelpefelt:
      *
     c                   eval      wpsa15 = 0
     c                   eval      wpbe15 = 0
      *
      * Overfører fra alfa til numeriske felter:
      *
     c                   move      wwsain        wpsain
     c                   move      wwvdin        wpvdin
     c                   move      wwvdmn        wpvdmn
     c                   move      wwbdin        wpbdin
     c                   move      wwbdmn        wpbdmn
     c                   move      wwbein        wpbein
      *
      * Initierer hjelpefelt:
      *
     c                   eval      wptist = *loval
     c                   eval      wpfdat = *loval
     c                   eval      wpsdat = *loval
     c                   eval      wpdato = *loval
     c                   eval      wpbrut = *blanks
     c                   eval      wptrut = *blanks
     c                   eval      wpalf3 = *blanks
      *
      * KEY for å teste mot DBNKL4 om poste finnes fra før:
      *
     c     dbnkl4_key    klist
     c                   kfld                    dbbkon
     c                   kfld                    dbbdat
     c                   kfld                    dbvdat
     c                   kfld                    dbteks
     c                   kfld                    dbref1
     c                   kfld                    dbref2
     c                   kfld                    dbref3
     c                   kfld                    dbref4
     c                   kfld                    dbref5
     c                   kfld                    dbref6
     c                   kfld                    dbbelp
      *
      * KEY for å teste mot DDKIL1 om poste finnes fra før:
      *
     c     ddkil1_key    klist
     c                   kfld                    dbbkon
     c                   kfld                    dbbdat
     c                   kfld                    dbvdat
     c                   kfld                    dbteks
     c                   kfld                    dbref1
     c                   kfld                    dbref2
     c                   kfld                    dbref3
     c                   kfld                    dbref4
     c                   kfld                    dbref5
     c                   kfld                    dbref6
     c                   kfld                    dbbelp
     c                   kfld                    wpnrf
     c                   kfld                    wparf
      *
      * Key for les av DFASL2:
      *
     c     dfasl2_key    klist
     c                   kfld                    dfasl2_bkon
      *
      * Henter datoinformasjon fra faste data.  Rutinen avsluttes
      * dersom post ikke finnes:
      *
     c                   eval      dfasl2_bkon = wpbkon
      *
     c     dfasl2_key    chain     dfasl2                             60
      *
     c     *in60         cabeq     *on           xslutt
      *
     c                   if        *in60 = *off
     c                   eval      wpfdat = dafdat
     c                   eval      wpsdat = dasdat
     c                   eval      wptist = datist
     c                   movel     dabank        wpbank
     c                   unlock    dfasl2
     c                   endif
      *
      * Legger inn "*hival" i fra dato for "Første post" i
      * faste data, dersom dato er null:
      *
     c                   if        wpfdat = *loval
     c                   eval      wpfdat = *hival
     c                   endif
      *
     c                   endsr
