      /TITLE  ASOFAK Lev.vare, henter vareregister - Christ Engebretsen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FW710R
      * Beskrivelse . . : Danning av leverandør vareregister, Christ
      *                   Engebretsen
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       060699 HKO  Nytt program
      * 6.10  230609 BSA  Oppdatert med sporingsinformasjon
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fflvwpf    if   e           k disk
     ffenkl1    if   e           k disk    rename(fenkpfr:fenkl1r)
     fflvapf    o  a e             disk
      *
6.10  * Local data area
6.10  *
6.10 d                uds
6.10 d l_user                911    920
      *
      * Definering av datastruktur
      *
      *  Recordbeskrivelse - Christ Engebretsen
      *
     d                 ds
     d d_inpu                       160
     d  d_dato                        6  0 overlay(d_inpu:1)                    Dato fra grossist
     d  d_udat                        6  0 overlay(d_inpu:7)                    Utgått dato vare
     d  d_vare                       13    overlay(d_inpu:13)                   Artikkelnr
     d  d_evar                       13    overlay(d_inpu:26)                   Erstanings varenr
     d  d_prod                        5    overlay(d_inpu:39)                   Produsentnr
     d  d_pvar                       13    overlay(d_inpu:44)                   Prod. varenr
     d  d_eann                       13    overlay(d_inpu:57)                   EAN-nr
     d  d_hgrp                        2    overlay(d_inpu:70)                   Hovedgruppe
     d  d_ugrp                        3    overlay(d_inpu:72)                   Undergruppe
     d  d_agrp                        3    overlay(d_inpu:75)                   Artikkelgruppe
     d  d_tek1                       30    overlay(d_inpu:78)                   Benevnelse
     d  d_stor                        8    overlay(d_inpu:108)                  Størrelse
     d  d_enhe                        5    overlay(d_inpu:116)                  Enhet
     d  d_anta                        9  3 overlay(d_inpu:121)                  Antall i pakn.
     d  d_rkod                        4    overlay(d_inpu:130)                  Rabattkode
     d  d_inpr                        9  2 overlay(d_inpu:134)                  Innkjøpspris
     d  d_grpr                        9  2 overlay(d_inpu:143)                  Grunnpris
     d  d_sapr                        9  2 overlay(d_inpu:152)                  Salgspris
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_ldor          s              6  0
      *
      * Definering av variable i nøkler
      *
     d fenkl1_enhf     s                   like(feenhf)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_udat          s               d   datfmt(*iso)
     d w_vtxt          s             71
     d w_posi          s              2  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter dagens dato
      *
     c                   time                    w_udat
      *
      * Leser input-fil, og legger data ut på leverandør vareregister
      *
     c                   read      flvwpf                                 90
     c                   dow       *in90 = *off
      *
     c                   eval      d_inpu = fwirad
      *
      * Skriver til leverandør vareregister
      *
     c                   eval      fwfirm = p_firm
     c                   eval      fwldor = p_ldor
     c                   eval      fwekod = *blank
     c                   eval      fwtek1 = %trim(d_tek1)
     c                   eval      fwtek2 = %trim(d_stor)
     c                   eval      fwlvar = %subst(d_vare:8:6)
     c                   eval      fweann = 0
     c                   eval      fwnobb = 0
     c                   eval      fwenhe = d_enhe
     c                   eval      fwinpr = d_inpr
     c                   eval      fwsapr = d_sapr
     c                   eval      fwdato = *loval
     c                   eval      fwbmen = d_anta
     c                   eval      fwudat = *loval
     c                   eval      fwlvae = *blank
     c                   eval      fwhkod = 1
      *
     c                   eval      fwline = fwline + 1
      *
     c                   if        d_eann <> *blank
     c                   move      d_eann        fweann
     c                   endif
      *
      * Konkatinerer varetekst-1 og størrelse
      *
     c                   exsr      konkatiner
      *
      * Konverterer enhet
      *
     c                   eval      fenkl1_enhf = d_enhe
     c     fenkl1_key    chain     fenkl1                             91
     c                   if        *in91 = *off
     c                   eval      fwenhe = feenht
     c                   endif
      *
      * Dato
      *
     c                   if        d_dato <> 0
     c     *ymd          move      d_dato        fwdato
     c                   endif
      *
      * Utgått dato
      *
     c                   if        d_udat <> 0
     c     *ymd          move      d_udat        fwudat
     c                   if        fwudat < w_udat
     c                   eval      fwekod = 'S'
     c                   endif
     c                   endif
      *
      * Erstanings varenr
      *
     c                   if        d_evar <> '0000000000000'
     c                   eval      fwlvae = %subst(d_evar:8:6)
     c                   endif
      *
      * Skriver til fil
      *
6.10 c                   time                    fwodat
6.10 c                   time                    fwotim
6.10 c                   eval      fwousr = l_user
6.10 c                   time                    fwedat
6.10 c                   time                    fwetim
6.10 c                   eval      fweusr = l_user
     c                   write     flvapfr
      *
     c                   read      flvwpf                                 90
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for konkatinering av varetekst-1 og varetekst-2
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     konkatiner    begsr
      *
     c                   eval      w_vtxt = *blank
      *
      * Trimmer varetekst-1
      *
     c     ' '           scan      fwtek1:1      w_posi                   92
     c                   dow       w_posi <> 1
      *
     c                   if        *in92 = *off
     c                   eval      w_posi = 36
     c                   endif
      *
     c                   eval      w_vtxt = %trim(w_vtxt) + ' ' +
     c                                      %subst(fwtek1:1:w_posi-1)
      *
     c                   if        w_posi >= 35
     c                   leave
     c                   endif
      *
     c                   eval      fwtek1 = %trim(
     c                                      %subst(fwtek1:w_posi:36-w_posi))
      *
     c     ' '           scan      fwtek1:1      w_posi                   92
     c                   enddo
      *
      * Trimmer varetekst-2, og konkatinerer denne med varetekt-1
      *
     c                   if        fwtek2 <> *blank
      *
     c     ' '           scan      fwtek2:1      w_posi                   92
     c                   dow       w_posi <> 1
      *
     c                   if        *in92 = *off
     c                   eval      w_posi = 36
     c                   endif
      *
     c                   eval      w_vtxt = %trim(w_vtxt) + ' ' +
     c                                      %subst(fwtek2:1:w_posi-1)
      *
     c                   if        w_posi >= 35
     c                   leave
     c                   endif
      *
     c                   eval      fwtek2 = %trim(
     c                                      %subst(fwtek2:w_posi:36-w_posi))
      *
     c     ' '           scan      fwtek2:1      w_posi                   92
     c                   enddo
      *
     c                   endif
      *
      * Splitter konkatinert varetekst i varetekst-1 og varetekst-2
      *
     c                   eval      fwtek1 = %trim(%subst(w_vtxt:1:35))
     c                   eval      fwtek2 = %trim(%subst(w_vtxt:36:35))
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ldor
      *
      * Key for oppslag på enhet-konvertering
      *
     c     fenkl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fenkl1_enhf
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
