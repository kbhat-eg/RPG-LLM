     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RL112R
      * Beskrivelse . . : Leverandørposteringer, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       150802 KOB  Nytt program
      *       300603 KOB  Lagt inn F2=Notatark
      * R5.20 110903 KOB  Lagt inn nytt felt - lev.fakturanummer
      *                   Dersom feltet er utfylt vises dette i tekstfeltet
      * V5.20 110504 KOB  Lagt inn mulighet til vedlikeh. lev.fakturanummer
      * V5.30 241104 KOB  Korrigert kall til faktura-visning
      * V5.30 061204 HFL  Lagt på farger for å vise rekl./sendt utb.
      * V5.30 040205 HFL  Lagt inn merke når notatblokk på leverandøren
      * V5.40 190505 KOB  Lagt ut attestasjonsdato og bruker i bildet
T5.40 * V5.40 170605 HFL  Leser RKLAL2 i stedet for RKLALU.
T5.40 * V5.40 030805 HFL  Notatark på postnivå.
      * V5.40 160805 KOB  Lagt inn ny funksjon for å vise arkivpost
T5.40 * R5.60 200308 KOB  Endret slik at RK513R kalles med bilagsnr.
6.10  * R6.10 250909 BSA  Lagt in  sporingsinformasjon
pdf   * R6.10     041010  Henter fra ASP arkiv. Mulighet for valg mellom BSA  *
pdf   *                   ASP og Multiarkiv gjøres i AP622C. Det finnes  BSA  *
pdf   *                   to varianter av dette programmet.              BSA  *
6.20  *       26.05.11    Pros er nå alfa
6.21  * R6.20 070611 bsa  Starter arkiv hvis ikke faktura                     *
6.22  * R6.20     210711  Legger ut firmanummer i skjultfelt for         bsa  *
6.22  *                   spørring mot ASP-arkiv fra Jacada              bsa  *
6.23  * R6.20     061212  Endret metode for oppslag mot arkiv            bsa  *
6.24  * R6.20     040113  Endret oppbygging av refno                     bsa  *
6.25  * R6.20     270213  Egen kode for forskjellige bilag               bsa  *
6.26  * R6.20     260313  Henter dokumenttype basert på bilagskode       bsa  *
6.27  * R6.20     040313  Notatblokk ble ikke vist                       nho  *
6.28  * R6.20     110813  Må flytte blanke ut i RXPROJ og ikke '0'       nho  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frltrl2    if   e           k disk    rename(rltrpfr:rltrl2r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     frltrlu    uf   e           k disk    rename(rltrpfr:rltrlur)
E5.40frklal2    if   e           k disk    rename(rklapfr:rklal2r)
T5.40frklal3    if   e           k disk    rename(rklapfr:rklal3r)
     fraa1l1    if   e           k disk    rename(raa1pfr:raa1l1r)
     fraa3l1    if   e           k disk    rename(raa3pfr:raa3l1r)
      *
     frl112d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d rltrl2_levr     s                   like(rolevr)
     d rltrl2_bdat     s                   like(robdat)
     d rltrl2_tist     s                   like(rotist)
     d rlevl1_levr     s                   like(rolevr)
     d rltrlu_levr     s                   like(rolevr)
     d rltrlu_bdat     s                   like(robdat)
     d rltrlu_tist     s                   like(rotist)
E5.40d rklal2_syst     s                   like(rxsyst)
E5.40d rklal2_pros     s                   like(rxpros)
E5.40d rklal2_akti     s                   like(rxakti)
E5.40d rklal2_levr     s                   like(rxkont)
      *
T5.40d rklal3_syst     s                   like(rxsyst)
T5.40d rklal3_pros     s                   like(rxpros)
T5.40d rklal3_akti     s                   like(rxakti)
T5.40d rklal3_levr     s                   like(rxkont)
T5.40d rklal3_spes     s                   like(rxspes)
T5.40d rklal3_avde     s                   like(rxavde)
T5.40d rklal3_tist     s                   like(rxtist)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_valg_ok       s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *                                                               . .
     d w_firm          s                   like(rofirm)
     d w_biln          s                   like(robiln)
     d w_levr          s                   like(rolevr)
     d w_raar          s                   like(roraar)
     d w_rper          s                   like(rorper)
     d w_3la1          s                   like(ra3la1)
     d w_3la2          s                   like(ra3la2)
     d w_3la3          s                   like(ra3la3)
     d w_valg          s              1
     d w_valg_r        s              1    inz(*on)
     d w_valg_v        s              1    inz(*off)
      *
     d w_dato          s                   like(robdat)
     d w_bel0          s                   like(robelØ)
     d w_bel1          s                   like(robelØ)
     d w_bel2          s                   like(robelØ)
     d w_bel3          s                   like(robelØ)
     d w_bel4          s                   like(robelØ)
     d w_beln          s                   like(robelØ)
     d w_fodag         s              5  0
     d w_krdag         s              5  0
     d w_tran          s              7  0
     d w_dagr          s              7  0
     d w_sald          s                   like(robelØ)
     d w_inka          s                   like(robelØ)
6.23 d w_type          s             50
pdf  d w_modu          s             10    inz('PROA    ')
pdf  d*w_modu          s             10    inz('MSA     ')
     d w_disp          s              1    inz('1')
     d w_tilg          s              1
6.23 d w_refn          s             50
6.26 d w_dtyp          s             50
6.26 d w_year          s              1
6.26 d w_ok            s              1
      *
      * Definering av parametre MultiArcive
      *
     d w_pgrp          s              2
     d w_aara          s              4
     d w_bila          s              6
     d w_afir          s              3
      *
     d p_stat          s              1
     d p_akod          s                   like(robilk)
     d p_atxt          s             20
     d p_ckod          s                   like(robetb)
     d p_ctxt          s             20
     d p_gkod          s                   like(roavde)
     d p_gtxt          s             20
T5.40d p_levr          s                   like(rolevr)
      *
T510  * Parameter - program RF020R
6.20 i/COPY QCPYLESRC,RF020PAR
6.20 d*                ds
6.200d*rxulda                       175
6.20 d* rxstat                        1    overlay(rxulda)
6.20 d* rxfirm                        3  0 overlay(rxulda:2)
6.20 d* rxsyst                        1    overlay(rxulda:5)
6.20 d* rxpros                        6  0 overlay(rxulda:6)
6.20 d* rxakti                        3    overlay(rxulda:12)
6.20 d* rxf001                        3    overlay(rxulda:15)
6.20 d* rxkont                        6  0 overlay(rxulda:18)
6.20 d* rxspes                        4  0 overlay(rxulda:24)
6.20 d* rxavde                        4  0 overlay(rxulda:28)
6.20 d* rxlinj                        4  0 overlay(rxulda:32)
6.20 d* rxtgrp                        3    overlay(rxulda:36)
6.20 d* rxtext                       78    overlay(rxulda:39)
6.20 d* rxf002                       13    overlay(rxulda:117)
6.20 d* rxvalg                        1  0 overlay(rxulda:130)
6.20 d* rxtist                         z   overlay(rxulda:131)
6.20 d* rxbiln                        6  0 overlay(rxulda:158)
6.20 d* rxrper                        2  0 overlay(rxulda:164)
6.20 d* rxraar                        4  0 overlay(rxulda:166)
6.20 d* rxonr                         6  0 overlay(rxulda:170)
T610 d rxupar          s                   like(rxulda)
      *
      * Definering av parametre Multiarcive
      *
     d m_firm          s              3
     d m_kund          s              8    inz('00000000')
     d m_bilt          s              3    inz('000')
     d m_biln          s              7    inz('0      ')
     d m_bdat          s              8    inz('YYYY0101')
     d m_inte          s              1    inz('J')
     d m_syst          s              1    inz('0')
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(13)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * d_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
T5.30 * Finnes notatblokk på leverandøren?
      *
E5.40c                   eval      rklal2_syst = 'L'
6.27 c**                 eval      rklal2_pros = *zeros
6.27 c                   eval      rklal2_pros = *blanks
E5.40c                   eval      rklal2_akti = *blanks
E5.40c                   eval      rklal2_levr = b2levr
E5.40c     rklal2_key    setll     rklal2                                 94
      *
T5.30c                   if        *in94 = *on
T5.30c                   eval      b2notb = 'NOTATARK'
T5.30c                   endif
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
T510 c                   when      *inkb
T5.40c                   move      *loval        rxtist
T5.40c                   eval      rxbiln = *zero
T5.40c                   eval      rxrper = *zero
T5.40c                   eval      rxraar = *zero
T5.40c                   eval      rxonr  = *zero
T510 c                   exsr      notat
T510 c                   goto      b2tagb
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2raar <> w_raar or
     c                             b2rper <> w_rper
      *
     c                   if        b2raar <> w_raar
     c                   eval      w_raar = b2raar
     c                   endif
      *
     c                   if        b2rper <> w_rper
     c                   eval      w_rper= b2rper
     c                   endif
      *
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Åpne poster/Alle poster
      *
     c                   if        *inkf = *on
     c                   if        w_valg_r = *on
     c                   eval      w_valg_r = *off
     c                   eval      rltrl2_bdat = *loval
     c                   eval      rltrl2_tist = *loval
     c                   else
     c                   eval      w_valg_r = *on
     c                   eval      rltrl2_bdat = *loval
     c                   eval      rltrl2_tist = *loval
     c                   endif
      *                                                                 .
     c     rltrl2_key    setll     rltrl2
      *                                                                 .
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   endif
      *
      * NOK/Valuta
      *
     c                   if        *inkg = *on
     c                   if        w_valg_v = *on
     c                   eval      w_valg_v = *off
     c                   else
     c                   eval      w_valg_v = *on
     c                   endif
      *                                                                 .
     c                   eval      rltrl2_bdat = *loval
     c                   eval      rltrl2_tist = *loval
      *                                                                 .
     c     rltrl2_key    setll     rltrl2
      *                                                                 .
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   endif
      *
      * Statistikk
      *
     c                   if        *inkh = *on
     c                   eval      rltrl2_bdat = *loval
     c                   eval      rltrl2_tist = *loval
      *                                                                 .
     c     rltrl2_key    setll     rltrl2
      *                                                                 .
     c                   exsr      xe2win
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        d_fcrn <> 0
     c     d_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      rltrl2_levr = b2levr
     c     rltrl2_key    setll     rltrl2
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
     c                   eval      rltrl2_levr = b2levr
     c     rltrl2_key    setll     rltrl2
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = d_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
      * Filen er tom
      *
     c                   if        b1biln = 0
     c                   goto      avslutt
     c                   endif
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Oppdater
      *
     c                   if        b1valg = 2
     c                   eval      *in30  = *off
     c                   eval      c2nyen = ' Endring '
     c                   exsr      xc2win
     c                   eval      b1valg = 0
      *
T5.30 * Legg på farger
      *
T5.30c                   eval      b1rekl = rorekl
T5.30c                   eval      b1betk = robetk
      *
T5.30 * Reklamasjon - hvit
      *
T5.30c                   if        b1rekl <> ' '
T5.30c                   eval      *in70 = *on
T5.30c                   endif
      *
T5.30 * Sendt til utbetaling - Rød
      *
T5.30c                   if        b1betk = '1' and
T5.30c                             b1rest <> 0
T5.30c                   eval      *in71 = *on
T5.30c                   endif
      *
     c                   update    b1sfl
      *
T5.30c                   eval      *in70 = *off
T5.30c                   eval      *in71 = *off
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      *in30  = *on
     c                   eval      c2nyen = '   Vis   '
     c                   exsr      xc2win
     c                   eval      b1valg = 0
      *
T5.30 * Legg på farger
      *
T5.30c                   eval      b1rekl = rorekl
T5.30c                   eval      b1betk = robetk
      *
T5.30 * Reklamasjon - hvit
      *
T5.30c                   if        b1rekl <> ' '
T5.30c                   eval      *in70 = *on
T5.30c                   endif
      *
T5.30 * Sendt til utbetaling - Rød
      *
T5.30c                   if        b1betk = '1' and
T5.30c                             b1rest <> 0
T5.30c                   eval      *in71 = *on
T5.30c                   endif
     c                   update    b1sfl
      *
T5.30c                   eval      *in70 = *off
T5.30c                   eval      *in71 = *off
     c                   iter
     c                   endif
      *
      * Vis motpost
      *
     c                   if        b1valg = 6
      *
T5.40c                   eval      p_levr = b2levr
     c                   call      'RL513R'
E5.40c                   parm                    p_levr
     c                   parm                    b1biln
     c                   parm                    b1bdat
     c                   parm                    b1rper
     c                   parm                    b1raar
      *
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis bilag
      *
     c                   if        b1valg = 7
      *
T5.40c                   eval      p_levr = b2levr
     c                   call      'RF512R'
E5.40c                   parm                    p_levr
     c                   parm                    b1biln
     c                   parm                    b1bdat
     c                   parm                    b1rper
     c                   parm                    b1raar
      *
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vise faktura
      *
     c                   if        b1valg = 8
     c                   eval      *in30  = *on
     c                   eval      w_biln = b1biln
     c                   eval      w_raar = b1raar
     c                   call      'RL110C'
     c                   parm                    w_firm
     c                   parm                    w_raar
     c                   parm                    b2levr
     c                   parm                    w_biln
      *
     c                   eval      b1valg = *zero
      *
T5.30 * Legg på farger
      *
T5.30c                   eval      b1rekl = rorekl
T5.30c                   eval      b1betk = robetk
      *
T5.30 * Reklamasjon - hvit
      *
T5.30c                   if        b1rekl <> ' '
T5.30c                   eval      *in70 = *on
T5.30c                   endif
      *
T5.30 * Sendt til utbetaling - Rød
      *
T5.30c                   if        b1betk = '1' and
T5.30c                             b1rest <> 0
T5.30c                   eval      *in71 = *on
T5.30c                   endif
     c                   update    b1sfl
      *
T5.30c                   eval      *in70 = *off
T5.30c                   eval      *in71 = *off
     c                   iter
     c                   endif
      *
      * Vis arkiv
      *
     c                   if        b1valg = 9
      *
      * Kontrollerer gyldig tilgang
      *
     c                   call      'AX700R'
     c                   parm                    w_modu
     c                   parm                    w_disp
     c                   parm                    w_tilg
      *
     c                   if        w_tilg = '0'
6.26  *
6.26  * Hent dokumenttype basert på bilagskoden
6.26  *
6.26 c                   eval      w_dtyp = *blanks
6.26 c                   eval      w_year = *blanks
6.26 c                   eval      w_ok = '0'
6.26  *
6.26 c                   call      'AP632R'
6.26 c                   parm                    w_firm
6.26 c                   parm                    b1bilk
6.26 c                   parm                    w_dtyp
6.26 c                   parm                    w_year
6.26 c                   parm                    w_ok
6.26  *
6.26 c                   if        w_ok = '1'
6.26 c                   eval      w_type = w_dtyp
6.26 c                   if        w_year = 'J'
6.26 c                   eval      w_refn = %char(b1raar)+%trim(%char(b1biln))
6.26 c                   else
6.26 c                   eval      w_refn = %trim(%char(b1biln))
6.26 c                   endif
6.26 c                   else
6.26  * Gammel logikk hvis den ikke finnes
      *
6.25 c                   if        b1bilk = 1 or
6.25 c                             b1bilk = 3
pdf  c                   eval      w_type = 'FAKTURA'
6.24 c                   eval      w_refn = %char(b1raar)+%trim(%char(b1biln))
6.25 c                   elseif    b1bilk = 2 or
6.24 c                             b1bilk = 4
6.23 c                   eval      w_type = 'INNGÅENDE%20FAKTURA'
6.24 c                   eval      w_refn = %trim(%char(b1biln))
6.25 c                   else
6.25 c                   eval      w_type = 'DIVERSEBILAG'
6.25 c                   eval      w_refn = %char(b1raar)+%trim(%char(b1biln))
pdf  c                   endif
6.26 c                   endif
      *
     c                   move      w_firm        w_afir
     c                   eval      w_pgrp = '30'
     c                   eval      w_aara = %char(b1raar)
     c                   eval      w_bila = %char(b1biln)
      *
pdf  c                   call      'AP622C'
pdf  c                   parm                    w_type
6.23 c                   parm                    w_refn
6.23 c*                  parm                    w_aara
6.23 c*                  parm                    w_bila
6.23 c*                  parm                    w_pgrp
pdf  c**                 call      'AH720C'
pdf  c**                 parm                    w_pgrp
pdf  c**                 parm                    w_afir
pdf  c**                 parm                    w_aara
pdf  c**                 parm                    w_bila
      *
     c                   endif
      *
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      rltrl2
      *
     c                   if        %eof or
     c                             rofirm <> w_firm or
     c                             rolevr <> w_levr
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *                                                                 .
     c                   if        b2raar <> *zero and
     c                             b2raar <> roraar
     c                   iter
     c                   endif
      *                                                                 .
     c                   if        b2rper <> *zero and
     c                             b2rper <> rorper
     c                   iter
     c                   endif
      *                                                                 .
      * Ved valg "Åpne poster"                                          .
      * test om restbeløp = 0                                           .
      *                                                                 .
     c                   if        w_valg_r = *on and
     c                             rorest = *zero
     c                   iter
     c                   endif
      *                                                                 .
      * Ved valg "Valuta"                                               .
      * test om valutakode = blank                                      .
      *                                                                 .
     c                   if        w_valg_v = *on and
     c                             rovalk = *blank
     c                   iter
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1raar = roraar
     c                   eval      b1bdat = robdat
     c                   eval      b1tist = rotist
     c                   eval      b1biln = robiln
     c     *dmy          move      robdat        b1bida
     c                   eval      b1bilk = robilk
      *
     c                   if        rolfak = *blank
     c                   eval      b1text = rotext
     c                   else
     c                   eval      b1text = %trim(rolfak) + '/' +
     c                                      %trim(rotext)
     c                   endif
     c                   eval      b1refn = rorefn
     c     *dmy          move      rofdat        b1fdat
      *                                                                 .
      * Valuta/NOK                                                      .
      *                                                                 .
     c                   if        w_valg_v = *off
     c                   eval      b1belØ = robelØ
     c                   eval      b1rest = rorest
     c                   else
     c                   eval      b1belØ = rovalb
     c                   eval      b1rest = rovalr
     c                   move      '    '        b1text
     c                   move      rovalk        b1text
     c                   endif
      *
T5.30 * Legg på farger
      *
T5.30c                   eval      b1rekl = rorekl
T5.30c                   eval      b1betk = robetk
      *
T5.30 * Reklamasjon - hvit
      *
T5.30c                   if        b1rekl <> ' '
T5.30c                   eval      *in70 = *on
T5.30c                   endif
      *
T5.30 * Sendt til utbetaling - Rød
      *
T5.30c                   if        b1betk = '1' and
T5.30c                             b1rest <> 0
T5.30c                   eval      *in71 = *on
T5.30c                   endif
      *
T5.40 * Gult bilagsnr. hvis det finnes notatblokk på posten
      *
T5.40c                   eval      rklal3_syst = 'L'
6.27 c**                 eval      rklal3_pros = *zeros
6.27 c                   eval      rklal3_pros = *blanks
T5.40c                   eval      rklal3_akti = *blanks
T5.40c                   eval      rklal3_levr = b2levr
T5.40c                   eval      rklal3_spes = *zero
T5.40c                   eval      rklal3_avde = *zero
T5.40c                   eval      rklal3_tist = b1tist
T5.40c     rklal3_key    setll     rklal3                                 73
      *
     c                   write     b1sfl
      *
T5.30c                   eval      *in70 = *off
T5.30c                   eval      *in71 = *off
T5.40c                   eval      *in73 = *off
      *
     c                   enddo
      *
      * Send melding hvis ingen poster
      *
     c                   if        w_srrn = *zero
     c                   exsr      clr_subfile
      *
     c                   if        w_valg_r = *on
     c                   eval      b1text = 'Ingen åpne poster'
     c                   else
     c                   eval      b1text = 'Ingen poster'
     c                   endif
      *                                                                 .
     c                   if        w_valg_v = *on
     c                   eval      b1text = 'Ingen valutaposter'
     c                   endif
      *                                                                 .
     c                   eval      b1biln = *zeros
     c                   eval      b1bdat = *loval
     c                   eval      b1bida = *zeros
     c                   eval      b1bilk = *zeros
     c                   eval      b1refn = *zeros
     c                   eval      b1fdat = *zeros
     c                   eval      b1belØ = *zeros
     c                   eval      b1rest = *zeros
     c                   eval      w_srrn = 1
      *
T5.30 * Legg på farger
      *
T5.30c                   eval      b1rekl = rorekl
T5.30c                   eval      b1betk = robetk
      *
T5.30 * Reklamasjon - hvit
      *
T5.30c                   if        b1rekl <> ' '
T5.30c                   eval      *in70 = *on
T5.30c                   endif
      *
T5.30 * Sendt til utbetaling - Rød
      *
T5.30c                   if        b1betk = '1' and
T5.30c                             b1rest <> 0
T5.30c                   eval      *in71 = *on
T5.30c                   endif
     c                   write     b1sfl
      *
T5.30c                   eval      *in70 = *off
T5.30c                   eval      *in71 = *off
     c                   endif
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * C2WIN Behandle bilde for oppdatering                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xc2win        begsr
      *
     c                   eval      rltrlu_levr = b2levr
     c                   eval      rltrlu_bdat = b1bdat
     c                   eval      rltrlu_tist = b1tist
     c     rltrlu_key    chain     rltrlu                             60
      *
     c                   move      robiln        c2biln
     c                   move      robilk        c2bilk
     c                   move      rotext        c2text
     c     *dmy          move      robdat        c2bdat
     c     *dmy          move      rofdat        c2fdat
     c                   move      rorper        c2rper
     c                   move      roraar        c2raar
     c                   move      rorekl        c2rekl
     c                   move      rojour        c2jour
     c                   move      rorefn        c2refn
     c                   move      robelØ        c2belØ
     c                   move      rorest        c2rest
     c                   move      rovalk        c2valk
     c                   move      roavde        c2avde
     c                   move      rovalb        c2valb
     c                   move      rovalr        c2valr
      *
     c                   move      roatts        c2atts
     c                   if        roatts = '1'
     c                   move      'J'           c2atts
     c                   endif
T5.40c                   move      roattu        c2attu
      *
     c                   if        roadat = *loval
     c                   eval      c2adat = 0
     c                   else
T5.40c     *dmy          move      roadat        c2adat
     c                   endif
      *
     c                   move      robetk        c2betk
     c                   move      rosamb        c2samb
     c                   move      rorrf1        c2rrf1
     c                   move      rorrf2        c2rrf2
     c                   move      rodeck        c2deck
     c                   move      robfnr        c2bfnr
     c                   move      robfli        c2bfli
     c                   move      rokidi        c2kidi
     c                   move      rodelb        c2delb
     c                   move      robetb        c2betb
     c                   move      *blank        c2ctxt
     c                   move      *blank        c2gtxt
T5.20c                   move      rolfak        c2lfak
      *
      * Hent betalingsbetingelsetekst
      *
     c                   if        robetb <> *zero
     c                   move      robetb        p_ckod
     c                   call      'RS703R'
     c                   parm                    p_stat
     c                   parm                    p_ckod
     c                   parm                    p_ctxt
      *
     c                   movel     p_ctxt        c2ctxt
     c                   endif
      *
      * Hent avdelingstekst
      *
     c                   if        roavde <> *zero
     c                   move      roavde        p_gkod
     c                   call      'RS707R'
     c                   parm                    p_stat
     c                   parm                    p_gkod
     c                   parm                    p_gtxt
      *
     c                   movel     p_gtxt        c2gtxt
     c                   endif
      *
T5.40 * Gult bilagsnr. hvis det finnes notatblokk på posten
      *
T5.40c                   eval      rklal3_syst = 'L'
6.27 c**                 eval      rklal3_pros = *zeros
6.27 c                   eval      rklal3_pros = *blanks
T5.40c                   eval      rklal3_akti = *blanks
T5.40c                   eval      rklal3_levr = b2levr
T5.40c                   eval      rklal3_spes = *zero
T5.40c                   eval      rklal3_avde = *zero
T5.40c                   eval      rklal3_tist = b1tist
T5.40c     rklal3_key    setll     rklal3                                 73
T5.40c                   if        *in73 = *on
T5.40c                   eval      c2notb = 'NOTATARK'
T5.40c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2win
T5.40c                   eval      *in73 = *off
      *
T5.40 * Notatblokk bilag
T5.40c                   if        *inkb
T5.40c                   eval      rxtist = rotist
T5.40c                   eval      rxbiln = robiln
T5.40c                   eval      rxrper = rorper
T5.40c                   eval      rxraar = roraar
T5.40c                   eval      rxonr  = *zero
T5.40c                   exsr      notat
T5.40c                   goto      end_c2bld
T5.40c                   endif
      *
     c                   if        *inkc or *inkl
     c                   goto      end_c2bld
     c                   endif
      *
      * Call program for bilagskode
      *
     c                   if        *inkd = *on and
     c                             w_afld = 'C2BILK'
     c                   call      'RA501R'
     c                   parm                    w_firm
     c                   parm                    p_akod
     c                   parm                    p_atxt
      *                                                                 .
     c                   if        p_akod <> *zero
     c                   eval      c2bilk = p_akod
     c                   eval      c2text = p_atxt
     c                   endif
      *                                                                 .
     c                   move      *on           *in33
     c                   goto      c2taga
     c                   endif
      *
      * Call program for betalingsbetingelse
      *
     c                   if        *inkd = *on and
     c                             w_afld = 'C2BETB'
     c                   call      'RA503R'
     c                   parm                    w_firm
     c                   parm                    p_ckod
     c                   parm                    p_ctxt
      *                                                                 .
     c                   if        p_ckod <> *zero
     c                   eval      c2betb = p_ckod
     c                   eval      c2ctxt = p_ctxt
     c                   endif
      *                                                                 .
     c                   move      *on           *in34
     c                   goto      c2taga
     c                   endif
      *
      * Call program for avdeling
      *
     c                   if        *inkd = *on and
     c                             w_afld = 'C2AVDE'
     c                   call      'RA507R'
     c                   parm                    w_firm
     c                   parm                    p_gkod
     c                   parm                    p_gtxt
      *                                                                 .
     c                   if        p_gkod <> *zero
     c                   move      p_gkod        c2avde
     c                   movel     p_gtxt        c2gtxt
     c                   endif
      *                                                                 .
     c                   move      *on           *in35
     c                   goto      c2taga
     c                   endif
      *
      * Kontroll av skjermbildet
      *
     c                   exsr      sjekk_input
      *
     c                   if        b_feil = *on
     c                   goto      c2taga
     c                   endif
      *
      * Oppdater posteringsregister
      *
     c                   eval      rltrlu_bdat = b1bdat
     c                   eval      rltrlu_tist = b1tist
     c     rltrlu_key    chain     rltrlur                            60
      *
     c                   move      c2bilk        robilk
     c                   move      c2text        rotext
     c     *dmy          move      c2fdat        rofdat
     c                   move      c2refn        rorefn
     c                   move      c2avde        roavde
     c                   move      c2rest        rorest
     c                   move      c2valr        rovalr
      *
     c                   move      c2avde        roavde
T5.40 *
T5.40 * Hvis attestasjonskoden er endret legges bruker og dato ut
T5.40 *
T5.40c                   if        c2atts <> ' ' and
T5.40c                             roatts =  ' '
T5.40c                   eval      roattu = l_user
T5.40c                   time                    roadat
T5.40c                   endif
T5.40 *
     c                   move      c2atts        roatts
     c                   move      c2betb        robetb
     c                   move      c2betk        robetk
     c                   move      c2rrf1        rorrf1
     c                   move      c2rrf2        rorrf2
     c                   move      c2deck        rodeck
     c                   move      c2kidi        rokidi
     c                   move      c2rekl        rorekl
T5.20c                   move      c2lfak        rolfak
      *
     c                   if        *in60 = *off
6.10 c                   time                    roedat
6.10 c                   time                    roetim
6.10 c                   eval      roeusr = l_user
     c                   update    rltrlur
     c                   endif
      *
     c                   if        b1valg = 2
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * E2WIN Behandle bilde for saldo/kjøp/statistikk                  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xe2win        begsr
      *
     c                   eval      e2post = 0
     c                   eval      e2debe = 0
     c                   eval      e2kred = 0
     c                   eval      e2rest = 0
     c                   eval      e2rekl = 0
      *
     c     e2tagb        tag
      *
     c                   read      rltrl2                                 15
      *
     c     *in15         cabeq     *on           xe2d01
      *
     c                   if        rofirm <> w_firm or
     c                             rolevr <> w_levr
     c                   move      *on           *in15
     c                   endif
      *
     c     *in15         cabeq     *on           xe2d01
      *
      *  Restbeløp = 0
      *
     c                   if        rorest = *zero
     c                   goto      srut
     c                   endif
      *
      *  Summer til aldersfordeling
      *
     c                   eval      w_sald = w_sald + rorest
      *
      * Beregn antall dager fra forfallsdato
      *
     C     w_dato        subdur    rofdat        w_fodag : *d
      *
      * Beregn antall dager fra bilagsdato
      *
     C     w_dato        subdur    robdat        w_krdag : *d
      *
      * Ikke saldert
      *
     c                   if        rorest > *zeros
     c                   eval      w_beln = w_beln + rorest
      *
     c                   else
      *
      * Beregn grunnlag for gj.snitt kredittid
      *
     c                   eval      w_dagr = w_dagr + w_krdag
     c                   eval      w_tran = w_tran + 1
      *
      * Ikke forfalt
      *
     c                   select
     c                   when      w_fodag < 0
     c                   eval      w_bel0 = w_bel0 + rorest
      *
      * Aldersfordeling 0 - A
      *
     c                   when      w_fodag <= ra3la1
     c                   eval      w_bel1 = w_bel1 + rorest
      *
      * Aldersfordeling A - B
      *
     c                   when      w_fodag <= ra3la2
     c                   eval      w_bel2 = w_bel2 + rorest
      *
      * Aldersfordeling B - C
      *
     c                   when      w_fodag <= ra3la3
     c                   eval      w_bel3 = w_bel3 + rorest
      *
      * Aldersfordeling C - D
      *
     c                   other
     c                   eval      w_bel4 = w_bel4 + rorest
     c                   endsl
      *
     c                   endif
      *
     c     srut          tag
      *
      *  Antall poster
      *
     c                   eval      e2post = e2post + 1
      *
      *  Antall debet/kredit
      *
     c                   if        robelØ > *zero
     c                   eval      e2debe = e2debe + 1
     c                   else
     c                   eval      e2kred = e2kred + 1
     c                   endif
      *
      *  Antall restbeløp
      *
     c                   if        rorest <> *zero
     c                   eval      e2rest = e2rest + 1
     c                   endif
      *
      *  Antall reklamasjon
      *
     c                   if        rorekl <> *blank
     c                   eval      e2rekl = e2rekl + 1
     c                   endif
      *
     c                   goto      e2tagb
      *
     c     xe2d01        tag
      *
     c                   move      ra3la1        e23la1
     c                   move      ra3la1        e23ls2
     c                   move      ra3la2        e23la2
     c                   move      ra3la2        e23ls3
     c                   move      ra3la3        e23la3
     c                   move      ra3la3        e23la4
      *
     c                   move      rlsald        e2sald
     c                   move      rlsaif        e2saif
     c                   move      rlkjØp        e2kjØp
     c                   move      rlkjØf        e2kjØf
     c                   move      rlsald        e2salt
      *
      * Beregner gj.snitt kredittid pr. leverandør
      *
      * Flytt felt til skjerm
      *
     c                   eval      e2ald0 = w_bel0
     c                   eval      e2ald1 = w_bel1
     c                   eval      e2ald2 = w_bel2
     c                   eval      e2ald3 = w_bel3
     c                   eval      e2ald4 = w_bel4
     c                   eval      e2aldn = w_beln
      *
      * Nullstill
      *
     c                   eval      w_bel0 = *zeros
     c                   eval      w_bel1 = *zeros
     c                   eval      w_bel2 = *zeros
     c                   eval      w_bel3 = *zeros
     c                   eval      w_bel4 = *zeros
     c                   eval      w_beln = *zeros
      *
     c                   eval      w_sald = *zeros
     c                   eval      w_dagr = *zeros
     c                   eval      w_tran = *zeros
      *
     c                   exfmt     e2win
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Kontroll  C2 bilde (endringer)                                  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
     c                   eval      *in32  = *off
     c                   eval      *in33  = *off
     c                   eval      *in34  = *off
     c                   eval      *in35  = *off
      *
      * Sjekke om forfallsdato er gyldig
      *
     c     *dmy          test(d)                 c2fdat                 32
     c                   if        *in32 = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * Sjekk om bilagskode er gyldig
      *
     c                   move      c2bilk        p_akod
     c                   call      'RS701R'
     c                   parm                    p_stat
     c                   parm                    p_akod
     c                   parm                    p_atxt
      *
     c                   if        p_stat = 'N'
     c                   move      *on           *in33
     c                   move      *on           *in81
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * Sjekk om betalingsbetingelse er gyldig
      *
     c                   if        c2betb <> *zero
     c                   move      c2betb        p_ckod
     c                   call      'RS703R'
     c                   parm                    p_stat
     c                   parm                    p_ckod
     c                   parm                    p_ctxt
      *
     c                   if        p_stat = 'N'
     c                   move      *on           *in34
     c                   move      *on           *in81
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
      * Sjekk om avdeling er gyldig
      *
     c                   if        c2avde <> *zero
     c                   move      c2avde        p_gkod
     c                   call      'RS707R'
     c                   parm                    p_stat
     c                   parm                    p_gkod
     c                   parm                    p_gtxt
      *
     c                   if        p_stat = 'N'
     c                   move      *on           *in35
     c                   move      *on           *in81
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
     c     end_sjekk     endsr
      *
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
T510  * Subrutine for oppkall av notatark
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     notat         begsr
      *
     c                   eval      rxfirm = w_firm
     c                   eval      rxsyst = 'L'
6.27 c*                  eval      rxpros = *blank
6.28 c*                  eval      rxpros = '000000'
     c                   eval      rxpros = *blank
     c                   eval      rxakti = ' '
     c                   eval      rxkont = rllevr
     c                   eval      rxspes = 0
     c                   eval      rxavde = 0
     c                   eval      rxlinj = *zeros
     c                   eval      rxtext = *blank
6.20 c                   eval      rxvalg = *blank
     c                   move      rxulda        rxupar
      *
     c                   call      'RF020R'
     c                   parm                    rxupar
      *
     c                   move      rxupar        rxulda
      *
T510 c                   endsr
      *
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    w_valg
     c                   parm                    w_levr
      *
      * Key for posisjonering på leverandør
      *
     c     rltrl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rltrl2_levr
     c                   kfld                    rltrl2_bdat
     c                   kfld                    rltrl2_tist
      *
      * Key for oppdatering av postering
      *
     c     rltrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rltrlu_levr
     c                   kfld                    rltrlu_bdat
     c                   kfld                    rltrlu_tist
      *
      * Key for leverandørregister
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
T5.30 * Key for notatblokk kontonivå
      *
E5.40c     rklal2_key    klist
T5.30c                   kfld                    w_firm
E5.40c                   kfld                    rklal2_syst
E5.40c                   kfld                    rklal2_pros
E5.40c                   kfld                    rklal2_akti
E5.40c                   kfld                    rklal2_levr
      *
T5.40 * Key for notatblokk postnivå
      *
T5.40c     rklal3_key    klist
T5.30c                   kfld                    w_firm
T5.40c                   kfld                    rklal3_syst
T5.40c                   kfld                    rklal3_pros
T5.40c                   kfld                    rklal3_akti
T5.40c                   kfld                    rklal3_levr
T5.40c                   kfld                    rklal3_spes
T5.40c                   kfld                    rklal3_avde
T5.40c                   kfld                    rklal3_tist
      *
      * Key for statuskode 1
      *
     c     raa1l1_key    klist
     c                   kfld                    w_firm
      *
      * Key for statuskode 3
      *
     c     raa3l1_key    klist
     c                   kfld                    w_firm
      *
      * Henter parametre, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
6.22 c                   eval      b2firm = w_firm
      *
     c                   eval      *in22 = *on
     c                   move      *date         w_dato
      *
      * Hent leverandør
      *
     c                   eval      rlevl1_levr = w_levr
     c     rlevl1_key    chain     rlevl1                             61
      *
     c                   eval      b2levr = w_levr
     c                   eval      b2navn = rlnavn
     c                   eval      b2sald = rlsald
      *
      * Hent statuskode 1
      *
     c     raa1l1_key    chain     raa1l1                             61
      *
     c                   if        *in61 = *off
     c***                move      ra1bek        ww1bek
     c***                move      ra1bkk        ww1bkk
     c                   endif
      *
      * Hent statuskode 3
      *
     c     raa3l1_key    chain     raa3l1                             61
      *
     c                   if        *in61 = *off
     c                   move      ra3la1        w_3la1
     c                   move      ra3la2        w_3la2
     c                   move      ra3la3        w_3la3
     c                   else
     c                   move      30            w_3la1
     c                   move      60            w_3la2
     c                   move      90            w_3la3
     c                   endif
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      rltrl2_levr = w_levr
     c                   eval      rltrl2_bdat = *loval
     c                   eval      rltrl2_tist = *loval
      *
     c     rltrl2_key    setll     rltrl2
     c     rltrl2_key    setll     rltrl2
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
