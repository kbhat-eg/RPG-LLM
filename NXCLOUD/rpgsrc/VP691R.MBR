     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : VP691R
      * Beskrivelse . . : Vare-pris, prisfil til kunder - styreprogram
      *                   Kopi av VP681R uten mappehåndtering
      *                   Bygger parameterlisten i dette programmet
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       311013 ask  Nytt program
7.01  *       170719 bsa  Manglet oppslag mot kunde for å hente lager
7.02  *       290909 bof  Parm ta med tilbudspris skal være på.
7.03  *k00  030620 BOF NXS-1195  Utvidet med parm inkl skaffvare / tilbudspris
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffkdil1    if   e           k disk    rename(fkdipfr:fkdil1r)
      *
      * Definering av parametre inn
      *
     d p_firm          s              3
     d p_kund          s              6
     d p_kpro          s              6
     d p_rfmt          s             10
     d p_dumm          s              1
      *
      * Definering av variabler i nøkler
      *
7.01 d rkunl1_kund     s                   like(rkkund)
     d fkdil1_kund     s                   like(fhkund)
     d fkdil1_kpro     s                   like(fhkpro)
      *
      * Definering av variable
      *
     d w_vsor          s             10
     d w_ogrp          s              2  0
     d w_hgrp          s              2  0
     d w_ugrp          s              3  0
     d w_ldor          s              6  0
     d w_rfmt          s             10
     d w_sval          s              1  0
     d w_tval          s              1  0
     d w_2val          s              1  0
     d w_bval          s              1  0
     d w_dato          s               d   datfmt(*iso)
     d w_tmst          s               z
     d w_tmst_20       s             20
     d w_memb          s             10
      *
     d w_firm          s                   like(fhfirm)
     d w_kund          s                   like(fhkund)
     d w_kpro          s                   like(fhkpro)
     d w_lety          s                   like(fhlety)
     d w_ftis          s                   like(fhftis)
5.70 d w_lage          s              2  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Utplukk på kunde eller kundeprosjekt
      *
     c                   move      p_firm        w_firm
     c                   move      p_kund        w_kund
     c                   move      p_kpro        w_kpro
      *
     c                   eval      fkdil1_kund = w_kund
     c                   eval      fkdil1_kpro = w_kpro
     c     fkdil1_key1   chain     fkdil1
      *
     c                   if        %found
     c                   eval      w_vsor = fhvsor
     c                   eval      w_lety = fhlety
     c                   eval      w_rfmt = fhrfmt
     c                   eval      p_rfmt = fhrfmt
7.03 c                   eval      w_sval = fhsval
7.03 c                   eval      w_tval = fhtval
7.03 c                   else
     c                   goto      avslutt
     c                   endif
7.01  *
7.01  * Slå opp på kunde
7.01  *
7.01 c                   eval      rkunl1_kund = w_kund
7.01 c     rkunl1_key    chain     rkunl1
7.01 c                   if        not %found
7.01 c                   eval      rklagr = 0
7.01 c                   endif
      *
     c                   eval      w_ogrp = 0
     c                   eval      w_hgrp = 0
     c                   eval      w_ugrp = 0
     c                   eval      w_ldor = 0
7.03 c*                   eval      w_sval = 0
7.03 c*                   eval      w_tval = 1
7.03 c                   eval      w_2val = 0
     c                   eval      w_bval = 0
     c                   eval      w_lage = rklagr
      *
     c                   time                    w_tmst
     c                   time                    w_dato
     c     *iso0         move      w_tmst        w_tmst_20
      *
     c                   eval      w_memb = 'P' + %subst(w_tmst_20:9:9)
      *
     c                   call      'VP692C'
     c                   parm                    w_firm
     c                   parm                    w_kund
     c                   parm                    w_kpro
     c                   parm                    w_vsor
     c                   parm                    w_ogrp
     c                   parm                    w_hgrp
     c                   parm                    w_ugrp
     c                   parm                    w_ldor
     c                   parm                    w_rfmt
     c                   parm                    fhendr
     c                   parm                    fhdato
     c                   parm                    w_sval
     c                   parm                    w_tval
     c                   parm                    w_2val
     c                   parm                    w_bval
     c                   parm                    w_lety
     c                   parm                    w_dato
     c                   parm                    w_lage
     c                   parm                    w_ftis
     c                   parm                    w_memb
      *
      * Avslutt
      *
     c     avslutt       tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_kund
     c                   parm                    p_kpro
     c                   parm                    p_rfmt
     c                   parm                    p_dumm
      *
      * Key for les på kunde-register
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
7.01 c                   kfld                    rkunl1_kund
      *
      * Key for les på kunde distribusjonsregister
      *
     c     fkdil1_key1   klist
     c                   kfld                    w_firm
     c                   kfld                    fkdil1_kund
     c                   kfld                    fkdil1_kpro
      *
     c     fkdil1_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    fkdil1_kund
      *
     c     fkdil1_key3   klist
     c                   kfld                    w_firm
      *
      * Henter timestamp, for å benytte denne som unik nøkkel på
      * forsendelsen.
      *
     c                   time                    w_ftis
      *
     c                   endsr
