     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LO586R
      * Beskrivelse . . : Synkronisering mellom bestilling og ordre
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       030914 bsa  Nytt program
6.30  * R6.30 120914 bsa  Utvidet parameterliste til FO798R. Håndtering av
6.30  *                   flere mailfelter.
6.31  * R6.30 100616 bsa  Kaller cl for å overstyre printerfile LO581P
6.31  *                   Virker som den kommer ut på skriver.
8.01  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flodtlu    uf   e           k disk    rename(lodtpfr:lodtlur)
     flodtlr    if   e           k disk    rename(lodtpfr:lodtlrr)
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffohelu    uf   e           k disk    rename(fohepfr:fohelur)
     ffohell    if   e           k disk    rename(fohepfr:fohellr)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     ffodtlr    if   e           k disk    rename(fodtpfr:fodtlrr)
     flsbol1    if   e           k disk    rename(lsbopfr:lsbol1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     ffwbolu    uf a e           k disk    rename(fwbopfr:fwbolur)
     ffwbolr    if   e           k disk    rename(fwbopfr:fwbolrr)
     fra09lr    if   e           k disk    rename(ra09pfr:ra09lrr)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
      *
      * Definering av Local Data Arae (LDA)
      *
     dlda             uds
     d l_arch                636    636  0
     d l_skje                637    637  0
     d l_mail                640    640  0
     d l_user                911    920
     d l_fnav                951    980
      *
      * Definering av array
      *
     d a_meld          s             50    dim(1) ctdata perrcd(1)
      *
      * Definering av Information Data Structure - display feedback
      *
      * Datastrukturer
      *
      * Datastruktur for Ean128 - kode
      *
     d*                ds
     d*d_e128                        26
     d* d_fast                        2  0 overlay(d_e128)
      *
      * Parametre
      *
     d p_firm          s              3  0
     d p_numm          s                   like(ldnumm)
     d p_suff          s                   like(ldsuff)
     d p_emal          s             50
     d p_ema2          s             50
6.30 d p_ema3          s             50
6.30 d p_kopi          s             50
     d p_emne          s             50
     d p_txt1          s             50
     d p_txt2          s             50
     d p_txt3          s             50
     d p_txt4          s             50
     d p_pers          s             50
     d p_inif          s             40
     d p_in03          s              1
     d p_kule          s              1
     d p_kpro          s              6  0
     d p_kund          s              6  0
     d p_navn          s             30
     d p_selg          s              4  0
     d p_kode          s              1
     d p_line          s                   like(ldline)
      *
      * Definering av variabler i nøkler
      *
     d lodtlr_line     s                   like(ldline)
     d lodtlu_line     s                   like(ldline)
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d fohell_numm     s                   like(fonumm)
     d fodtl1_numm     s                   like(fdnumm)
     d fodtl1_suff     s                   like(fdsuff)
     d fodtl1_line     s                   like(fdline)
     d fodtlr_numm     s                   like(fdnumm)
     d fodtlr_suff     s                   like(fdsuff)
     d fodtlr_line     s                   like(fdline)
     d votyl1_otyp     s                   like(vaotyp)
     d fwbolu_numm     s                   like(wfnumm)
     d fwbolu_suff     s                   like(wfsuff)
     d fwbolu_line     s                   like(wfline)
     d fwbolu_type     s                   like(wftype)
     d ra09lr_ikod     s                   like(raikod)
     d fwbolr_numm     s                   like(wfnumm)
     d fwbolr_suff     s                   like(wfsuff)
      *
      * Definering av variable
      *
     d b_oppr          s              1    inz(*off)
     d b_logg          s              1    inz(*off)
     d b_newl          s              1    inz(*off)
     d b_prtl          s              1    inz(*off)
     d b_updo          s              1    inz(*off)
     d b_orde          s              1    inz(*off)
      *
     d w_firm          s                   like(ldfirm)
     d w_numm          s                   like(ldnumm)
     d w_suff          s                   like(ldsuff)
     d w_line          s                   like(ldline)
     d w_enhe          s                   like(ldenh1)
     d w_vare          s                   like(ldvare)
     d w_lety          s                   like(ldlety)
     d w_eann          s             14  0
     d w_eava          s                   like(ldvare)
     d w_eaen          s                   like(ldenh1)
     d w_ldor          s                   like(loldor)
     d b_inlr          s              1    inz(*off)
     d w_retk          s              1
     d w_anta          s                   like(ldanta)
     d w_inkp          s                   like(ldipny)
     d w_kopr          s                   like(ldipny)
     d w_tek1          s                   like(wftek1)
     d w_mld1          s             50
     d w_enor          s              1
     d w_csta          s              1
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(7)
      *
     d lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
     d digit           c                   '0123456789 '
     d null            c                   '0000000000000'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandler ordre/bestilling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Slå opp etter info på bestillingshodet
     c     lohel1_key    chain     lohel1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
      * Automatisk endring av linjer
      * Les alle bestillingslinjene, sjekk hvilke endringer og gjør disse
      *
     c                   eval      lodtlr_line = 0
     c     lodtlr_key    setll     lodtlr
     c     lodtlr_key2   reade     lodtlr
     c                   dow       not %eof
      *
     c                   eval      w_anta = 0
     c                   eval      w_inkp = 0
     c                   eval      w_kopr = 0
      * Bestillingslinje må være opprettet senere enn dato satt på LSBO
     c                   if        ldodat  < lafgfr
     c                   goto      les_next
     c                   endif
      * Må være varelinje
     c                   if        ldvare <> *blank
      * Ordrenummer/suffix må finnes
     c                   eval      fohel1_numm = loornr
     c                   eval      fohel1_suff = loorsu
     c     fohel1_key    chain     fohel1
     c                   if        %found
     c                   eval      fodtl1_numm = ldornu
     c                   eval      fodtl1_suff = ldorsu
     c                   eval      fodtl1_line = ldorli
     c     fodtl1_key    chain     fodtl1
     c                   if        %found
      * Hvis avvik enhet, kan ikke synkronisering kjøres
     c                   if        fdenh1 <> ldenh1
     c                   goto      les_next
     c                   endif
      * Hvis avvik på antall, og dette skal oppdateres, utfør dette
     c                   if        lafcha = 'J' and
     c                             ldanta <> fdanta
     c                   eval      w_anta = ldanta
     c                   endif
      * Hvis avvik på innkjøpspris, rekalkuler kost
     c                   if        lafchk = 'J' and
     c                             ldipny <> fdinpr
     c                   eval      w_inkp = ldipny
     c                   endif
      * Hvis avvik på kostpris, logg
     c                   if        lafchk = 'J' and
     c                             ldkpny <> fdkopr
     c                   eval      w_kopr = ldkpny
     c                   endif
      *
     c                   if        w_anta <> 0 or
     c                             w_inkp <> 0 or
     c                             w_kopr <> 0
     c                   exsr      opd_ordrel
     c                   endif
      *
     c                   else
      * Skal vi legge til nye linjer
     c                   if        lafchl = 'J'
     c                   exsr      add_nylinje
     c                   endif
     c                   endif
      *
     c                   endif
     c                   endif
      *
     c     les_next      tag
      *
     c     lodtlr_key2   reade     lodtlr
     c                   enddo
      *
      * Oppdater ordrehode med verdier fra varelinjer
      *
     c                   if        b_updo = *on
     c                   call      'FO730R'
     c                   parm                    w_enor
     c                   parm                    w_firm
     c                   parm                    loornr
     c                   parm                    loorsu
      *
      * Hvis ordren har kommet lenger enn behandlingskode 1, settes infokode i heading
      *
     c                   eval      votyl1_otyp = footyp
     c     votyl1_key    chain     votyl1r                            90
     c                   if        vaoakk > 1
     c     fohel1_key    chain     fohelu
     c                   if        %found
     c                   eval      foityp = lafity
     c                   update    fohelur
      * Skriv til kvitteringsfile for info til selger
     c                   eval      fwbolu_type = 'INF'
     c                   eval      w_tek1 = 'Ordreinfokode ' + lafity +
     c                             ' er satt på ordren.'
      *
     c                   exsr      logg_txt
     c                   endif
     c                   endif
     c                   endif
      *
      * Skal vi varsle mottaker om at det er gjort endringer
     c                   if        lafvar = 'J'
     c                   exsr      inf_selger
     c                   endif
      *
     c*                  endif
     c*                  else
      * Informer selger om eventuelle endringer. Skriv melding i
     c                   exsr      log_bestil
     c                   if        lafvar = 'J'
     c                   eval      p_txt1 = 'Endringer er gjort i bestilling '+
     c                             %char(lonumm)
     c                   eval      p_txt2 = 'som kan påvirke ordrenummer '+
     c                             %char(loornr)
     c                   eval      p_txt3 = 'Ordren er ikke oppdatert. Trolig å+
     c                                      rsak er '
     c                   eval      p_txt4 = 'manglende tilgang.'
     c                   exsr      inf_selger
     c                   endif
     c**                 endif
      *
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   if        b_orde = '1'
     c                   eval      w_mld1 = a_meld(1)
     c                   call      'AA005R'
     c                   parm                    w_mld1
     c                   endif
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av ordrelinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     opd_ordrel    begsr
      * Hvis det skal varsles, skal vi logge endringen før
     c                   if        lafvar = 'J'
     c                   eval      fwbolu_type = 'FRA'
     c                   exsr      oppd_logg
     c                   endif
      * Kall program for endring av antall
     c                   call      'FO701R'
     c                   parm                    w_firm
     c                   parm                    ldornu
     c                   parm                    ldorsu
     c                   parm                    ldorli
     c                   parm                    w_anta
     c                   parm                    w_inkp
     c                   parm                    w_kopr
     c                   parm                    w_csta
     c
      * Hvis det skal varsles, skal vi logge endringen etter
     c                   if        lafvar = 'J'
     c                   eval      fwbolu_type = 'TIL'
     c                   exsr      oppd_logg
     c                   endif
      * Vi forusetter at det er gjort endringer. Skrur på endringsindikator
     c                   eval      b_updo = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for skrive hele bestilling til loggfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     log_bestil    begsr
      * Gjøres hvis vi ikke har tilgang til ordre/sperret
      *
     c     lodtlr_key2   setll     lodtlr
     c     lodtlr_key2   reade     lodtlr
     c                   dow       not %eof
      * Må vare varelinje
     c                   if        ldvare <> *blank
      *
     c                   exsr      logg_bline
      *
     c                   endif
      *
     c     lodtlr_key2   reade     lodtlr
     c                   enddo
      * Legg ut tekstlinje i bunn
     c                   eval      fwbolu_type = 'INF'
     c                   eval      w_tek1 = 'Ordren var sperret. Sjekk manuelt'
      *
     c                   exsr      logg_txt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sende info til ansvarlig selger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     inf_selger    begsr
      * Sjekk først om det er noe å sende informasjon om.
     c                   eval      b_logg = *off
     c                   eval      fwbolr_numm = loornr
     c                   eval      fwbolr_suff = loorsu
     c     fwbolr_key    setll     fwbolrr
     c     fwbolr_key    reade     fwbolr                                 91
     c                   dow       *in91 = *off
     c                   eval      b_logg = *on
     c     fwbolr_key    reade     fwbolr                                 91
     c                   enddo
      * Hvis ikke finnes, sjekk også bestillingen
     c                   if        b_logg = *off
     c                   eval      fwbolr_numm = lonumm
     c                   eval      fwbolr_suff = losuff
     c     fwbolr_key    setll     fwbolrr
     c     fwbolr_key    reade     fwbolr                                 91
     c                   dow       *in91 = *off
     c                   eval      b_logg = *on
     c     fwbolr_key    reade     fwbolr                                 91
     c                   enddo
     c                   endif
      *
     c                   if        b_logg = *on
      * Finn emailadresse fra selger, som skal varsles
     c                   if        foselg <> 0
     c                   eval      ra09lr_ikod = foselg
     c     ra09lr_key    chain     ra09lr
     c                   if        %found and
     c                             raiema <> *blank
     c                   eval      p_emal = raiema
     c                   endif
     c                   endif
      *
      * Legg inn beskrivelser, og mottaker av mailadresse
      *
     c                   eval      p_in03 = '0'
     c                   eval      p_kund = fokund
     c                   eval      p_selg = foselg
     c                   eval      p_navn = raitxt
      * Setter p_kule til ' '. Da vil FO798 bruke email adressen i parameterlista
     c                   eval      p_kule = ' '
     c                   eval      p_emne = 'Endringer i best: '+ %char(lonumm)+
     c                             '. Ref. ordre: ' + %char(loornr)
      *
     c                   call      'FO798R'
     c                   parm                    p_kule
     c                   parm                    p_kund
     c                   parm                    p_kpro
     c                   parm                    p_numm
6.30 c                   parm                    p_suff
     c                   parm                    p_selg
     c                   parm                    p_navn
     c                   parm                    p_emal
6.30 c                   parm                    p_ema2
6.30 c                   parm                    p_ema3
     c                   parm                    p_emne
     c                   parm                    p_txt1
     c                   parm                    p_txt2
     c                   parm                    p_txt3
     c                   parm                    p_txt4
     c                   parm                    p_pers
6.30 c                   parm                    p_kopi
     c                   parm                    p_inif
     c                   parm                    p_in03
      * Merk LDA, slik at vi kan bruke PROA funksjonalitet
     c                   eval      l_arch = 0
     c                   eval      l_skje = 0
     c                   eval      l_mail = 1
      * Er F3 eller F12 benyttet fra mailbildet ?
     c                   if        p_in03 = '1'
     c                   eval      l_mail = 0
     c                   endif
      *
     c                   out       lda
      *
6.31 c**                 call      'LO581R'
6.31 c                   call      'LO581C'
     c                   parm                    loornr
     c                   parm                    loorsu
     c                   parm                    p_emal
     c                   parm                    p_emne
     c                   parm                    p_txt1
     c                   parm                    p_txt2
     c                   parm                    p_txt3
     c                   parm                    p_txt4
     c                   parm                    p_pers
6.30 c                   parm                    p_kopi
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegg av ny linje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     add_nylinje   begsr
      * Sjekk om hvilket linjenummer denne skal ha på FODT
     c                   eval      fodtlr_numm = loornr
     c                   eval      fodtlr_suff = loorsu
     c                   eval      fodtlr_line = *hival
     c     fodtlr_key    setgt     fodtlr
     c                   readp     fodtlr
     c                   if        %found
     c                   eval      p_line = fdline + 10
     c                   endif
      * Oppdater bestillingslinje med info om ordren
     c                   eval      lodtlu_line = ldline
     c     lodtlu_key    chain     lodtlur
     c                   if        %found
     c                   eval      ldornu = loornr
     c                   eval      ldorsu = loorsu
     c                   eval      ldorli = p_line
     c                   update    lodtlur
     c                   endif
      * Hvis det skal varsles, skal vi logge endringen før
     c                   if        lafvar = 'J'
     c                   eval      fwbolu_type = 'FRA'
     c                   exsr      oppd_logg
     c                   endif
      * Vanlig linje
     c                   call      'FD108R'
     c                   parm                    p_kode
     c                   parm                    w_firm
     c                   parm                    loornr
     c                   parm                    loorsu
     c                   parm                    p_line
     c                   parm                    faalty
     c                   parm                    folety
     c                   parm                    ldvare
     c                   parm                    ldanta
     c                   parm                    ldenh1
     c                   parm                    foldat
     c                   parm                    ldnumm
     c                   parm                    ldsuff
     c                   parm                    ldline
      *
      * Hvis det skal varsles, skal vi logge endringen etter
     c                   if        lafvar = 'J'
     c                   eval      fwbolu_type = 'TIL'
     c                   exsr      oppd_logg
     c                   endif
      * Vi forusetter at det er gjort endringer. Skrur på endringsindikator
     c                   eval      b_updo = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for logg av endringer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_logg     begsr
      * Sjekk om varelinjen finnes fra før
     c                   eval      b_newl = *off
      *
     c                   eval      fodtl1_numm = loornr
     c                   eval      fodtl1_suff = loorsu
     c                   eval      fodtl1_line = ldorli
     c     fodtl1_key    chain     fodtl1r
     c                   if        not %found
     c                   eval      b_newl = *on
     c                   endif
      *
     c                   eval      fwbolu_numm = loornr
     c                   eval      fwbolu_suff = loorsu
     c                   eval      fwbolu_line = ldorli
     c     fwbolu_key    chain     fwbolur
     c                   if        %found
     c                   if        fwbolu_type = 'TIL'
     c                   eval      wfvare = fdvare
     c                   eval      wftek1 = fdtek1
     c                   eval      wfanta = fdanta
      * Logger kost
     c                   eval      wfsapr = fdkopr
     c                   eval      wfsums = fdsumk
     c                   eval      wfbnum = ldnumm
     c                   eval      wfenhe = fdenh1
      *
     c                   time                    wfedat
     c                   time                    wfetim
     c                   eval      wfeusr = l_user
     c                   update    fwbolur
     c                   endif
     c                   else
      *
     c                   clear                   fwbolur
      *
     c                   eval      wffirm = w_firm
     c                   eval      wfnumm = fwbolu_numm
     c                   eval      wfsuff = fwbolu_suff
     c                   eval      wfline = fwbolu_line
     c                   eval      wftype = fwbolu_type
     c                   eval      wfbnum = ldnumm
     c                   if        b_newl = *on
     c                   eval      wftek1 = 'Linje finnes ikke !!!'
     c                   else
     c                   eval      wfvare = fdvare
     c                   eval      wftek1 = fdtek1
     c                   eval      wfanta = fdanta
      * Logger kost
     c                   eval      wfsapr = fdkopr
     c                   eval      wfsums = fdsumk
     c                   eval      wfenhe = fdenh1
     c                   endif
      *
     c                   time                    wfedat
     c                   time                    wfetim
     c                   eval      wfeusr = l_user
     c                   time                    wfodat
     c                   time                    wfotim
     c                   eval      wfousr = l_user
      *
     c                   write     fwbolur
      *
     c                   endif
      *
     c                   eval      b_prtl = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for logg av bestillingen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     logg_bline    begsr
      * Sjekk om varelinjen finnes fra før
     c                   eval      b_newl = *off
      *
     c                   eval      fodtl1_numm = loornr
     c                   eval      fodtl1_suff = loorsu
     c                   eval      fodtl1_line = ldorli
     c     fodtl1_key    chain     fodtl1r
     c                   if        not %found
     c                   eval      b_newl = *on
     c                   endif
      * Legg ut bestillingsinformasjon
     c                   eval      fwbolu_numm = loornr
     c                   eval      fwbolu_suff = loorsu
     c                   eval      fwbolu_line = ldline
     c                   eval      fwbolu_type = 'BES'
     c     fwbolu_key    chain     fwbolur
     c                   if        not %found
     c                   clear                   fwbolur
      *
     c                   eval      wffirm = w_firm
     c                   eval      wfnumm = fwbolu_numm
     c                   eval      wfsuff = fwbolu_suff
     c                   eval      wfline = fwbolu_line
     c                   eval      wftype = fwbolu_type
     c                   eval      wfbnum = ldnumm
      *
     c                   eval      wfvare = ldvare
     c                   eval      wftek1 = ldtek1
     c                   eval      wfanta = ldanta
     c                   eval      wfsapr = ldipny
     c                   eval      wfsums = ldsumi
     c                   eval      wfenhe = ldenh1
      *
     c                   time                    wfedat
     c                   time                    wfetim
     c                   eval      wfeusr = l_user
     c                   time                    wfodat
     c                   time                    wfotim
     c                   eval      wfousr = l_user
      *
     c                   write     fwbolur
     c                   endif
      *
      * Legg ut ordrelinje
     c                   eval      fwbolu_numm = loornr
     c                   eval      fwbolu_suff = loorsu
     c                   eval      fwbolu_line = ldline
     c                   eval      fwbolu_type = 'ONR'
     c     fwbolu_key    chain     fwbolur
     c                   if        not %found
     c                   clear                   fwbolur
      *
     c                   eval      wffirm = w_firm
     c                   eval      wfnumm = fwbolu_numm
     c                   eval      wfsuff = fwbolu_suff
     c                   eval      wfline = fwbolu_line
     c                   eval      wftype = fwbolu_type
     c                   eval      wfbnum = ldnumm
     c                   if        b_newl = *on
     c                   eval      wftek1 = 'Linje finnes ikke !!!'
     c                   else
     c                   eval      wfvare = fdvare
     c                   eval      wftek1 = fdtek1
     c                   eval      wfanta = fdanta
      * Logger kost
     c                   eval      wfsapr = fdkopr
     c                   eval      wfsums = fdsumk
     c                   eval      wfenhe = fdenh1
     c                   endif
      *
     c                   time                    wfedat
     c                   time                    wfetim
     c                   eval      wfeusr = l_user
     c                   time                    wfodat
     c                   time                    wfotim
     c                   eval      wfousr = l_user
      *
     c                   write     fwbolur
     c                   endif
      *
     c                   eval      b_prtl = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for logg av tekstinfomasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     logg_txt      begsr
      * Sjekk om varelinjen finnes fra før
      *
     c                   eval      fwbolu_numm = loornr
     c                   eval      fwbolu_suff = loorsu
     c                   eval      fwbolu_line = 9999
     c     fwbolu_key    chain     fwbolur
     c                   if        %found
      * Tekstinformasjon er allerede initiert
     c                   time                    wfedat
     c                   time                    wfetim
     c                   eval      wfeusr = l_user
     c                   update    fwbolur
     c                   else
      *
     c                   clear                   fwbolur
      *
     c                   eval      wffirm = w_firm
     c                   eval      wfnumm = fwbolu_numm
     c                   eval      wfsuff = fwbolu_suff
     c                   eval      wfline = fwbolu_line
     c                   eval      wftype = fwbolu_type
     c                   eval      wftek1 = w_tek1
     c                   time                    wfedat
     c                   time                    wfetim
     c                   eval      wfeusr = l_user
     c                   time                    wfodat
     c                   time                    wfotim
     c                   eval      wfousr = l_user
      *
     c                   write     fwbolur
      *
     c                   endif
      *
     c                   eval      b_prtl = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *dtaara       define    *lda          lda
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_numm
     c                   parm                    p_suff
      *
      * Key for oppslag på bestilling hode
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
      *
      * Key for les/oppslag på bestilling-linje
      *
     c     lodtlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
     c                   kfld                    lodtlr_line
      *
     c     lodtlr_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
      *
      * Key for oppdatering av bestilling-linje
      *
     c     lodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
     c                   kfld                    lodtlu_line
      *
      * Key for oppslag på synkeregister
      *
     c     lsbol1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på statusregister
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på odre-hode
      *
     c     fohell_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohell_numm
      *
      * Key for oppslag på odre-hode
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      * Key for oppslag på odre-linjer
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
     c                   kfld                    fodtl1_line
      *
      * Key for oppslag på odre-linjer
      *
     c     fodtlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtlr_numm
     c                   kfld                    fodtlr_suff
     c                   kfld                    fodtlr_line
      *
      * Key for arbeidsregister for endringer
      *
     c     fwbolu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fwbolu_numm
     c                   kfld                    fwbolu_suff
     c                   kfld                    fwbolu_line
     c                   kfld                    fwbolu_type
      *
      * Key for les av arbeidsregister for endringer
      *
     c     fwbolr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fwbolr_numm
     c                   kfld                    fwbolr_suff
      *
      * Key for oppslag på selger-register
      *
     c     ra09lr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra09lr_ikod
      *
      * Key for oppslag på ordretype
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Henter verdier fra parmametre
      *
     c                   move      p_firm        w_firm
     c                   move      p_numm        w_numm
     c                   move      p_suff        w_suff
      * Slå opp etter synkroniseringsparametre
     c     lsbol1_key    chain     lsbol1
      * Slå opp på statusregister
     c     fstsl1_key    chain     fstsl1r
      *
     c                   endsr
**     M e l d i n g e r
Kostpris er endret på salgsordren                 <---
