     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : MD100R
      * Beskrivelse . . : Lagerlokasjon, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       131005 ASK  Nytt program
      * 6.10  180609 BSA  Oppdatert med sporingsinformasjon
6.31  * 6.30  041018 bof  Krav til at beskrivelse fylles ut.
7.xx  * 7.00  150316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fmdlolr    if   e           k disk    rename(mdlopfr:mdlolrr)
     fmdlol1    if   e           k disk    rename(mdlopfr:mdlol1r)
     fmdlolu    uf a e           k disk    rename(mdlopfr:mdlolur)
     fmdlol2    if   e           k disk    rename(mdlopfr:mdlol2r)
      *
     fmd100d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d mdlol1_lage     s                   like(mdlage)
     d mdlol1_loka     s                   like(mdloka)
     d mdlolr_lage     s                   like(mdlage)
     d mdlolr_loka     s                   like(mdloka)
     d mdlolu_lage     s                   like(mdlage)
     d mdlolu_loka     s                   like(mdloka)
     d mdlol2_lage     s                   like(mdlage)
     d mdlol2_loka     s                   like(mdloka)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_stat          s              1
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(mdfirm)
     d w_lage          s                   like(mdlage)
     d w_loka          s                   like(mdloka)
     d w_lagn          s             20
     d w_lokn          s             30
     d w_latx          s             30
     d w_lotx          s             30
      *
5.20 d s_lage          s                   like(mdlage)
     d s_loka          s                   like(mdloka)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2taga
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2lage <> 0  or
     c                             b2loka <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'L'
     c                   eval      mdlol1_lage = b1lage
     c                   eval      mdlol1_loka = b1loka
     c     mdlol1_key    setll     mdlol1
     c                   when      w_seqe = 'O'
     c                   eval      mdlol2_lage = b1lage
     c                   eval      mdlol2_loka = b1loka
     c     mdlol2_key    setll     mdlol2
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på lager
      *
     c                   if        b2lage <> 0
     c                   eval      w_seqe = 'L'
     c                   eval      mdlol1_lage = b2lage
     c                   eval      mdlol1_loka = *blank
     c     mdlol1_key    setll     mdlol1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på lokasjon
      *
     c                   if        b2loka <> *blank
     c                   eval      w_seqe = 'O'
     c                   eval      mdlol2_lage = 0
     c                   eval      mdlol2_loka = b2loka
     c     mdlol2_key    setll     mdlol2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2lage = 0
     c                   eval      b2loka = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
5.20 c                   eval      c1lage = b1lage
     c                   eval      c1loka = b1loka
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
5.20 c                   eval      k1lage = b1lage
     c                   eval      k1loka = b1loka
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
5.20 c                   eval      d1lage = b1lage
     c                   eval      d1loka = b1loka
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
5.20 c                   eval      c1lage = b1lage
     c                   eval      c1loka = b1loka
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
5.20 c                   eval      c1lage = 0
     c                   eval      c1loka = *blank
     c                   eval      c1lagn = *blank
     c                   eval      c1lokn = *blank
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      c1taga
     c                   endsl
      *
     c                   if        c1lage = 0 and
     c                             c1loka = *blank
     c                   goto      end_c1bld
     c                   endif
      *
      * Sjekker gyldig lager
      *
     c                   eval      w_lage = c1lage
     c                   exsr      sjekk_lager
     c                   if        b_feil = *on
     c                   eval      *in31 = *on
     c                   goto      c1taga
     c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
5.20 c                   eval      mdlolr_lage = c1lage
     c                   eval      mdlolr_loka = c1loka
     c     mdlolr_key    chain     mdlolr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
5.20 c                   eval      c1lage = 0
     c                   eval      c1loka = *blank
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
5.20 c                   eval      mdlolr_lage = c1lage
     c                   eval      mdlolr_loka = c1loka
     c     mdlolr_key    chain     mdlolr
      *
     c                   if        %found
     c                   eval      c2lbes = mdlbes
     c                   eval      c2eusr = mdeusr
     c     *dmy          move      mdedat        c2edat
     c                   else
     c                   eval      c2lbes = *blank                                            .
     c                   eval      c2eusr = *blank
     c                   eval      c2edat = 0
     c                   endif
      *
     c                   if        b1valg = 3
5.20 c                   eval      c1lage = k1lage
     c                   eval      c1loka = k1loka
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c                   eval      w_lage = c1lage
     c                   exsr      sjekk_lager
     c                   eval      c1lagn = w_latx
      *
     c                   eval      c1lokn = w_lotx
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Validering av input
      *
6.31 c                   if        c2lbes = *blank
6.31 c                   eval      *in31  = *on
6.31 c                   goto      c2taga
6.31 c                   endif
      *
      * Oppdatering
      *
     c                   clear                   mdlolur
      *
5.20 c                   eval      mdlolu_lage = c1lage
     c                   eval      mdlolu_loka = c1loka
     c     mdlolu_key    chain     mdlolur
      *
     c                   eval      mdlbes = c2lbes                                            .
      *
     c                   time                    mdedat
     c                   time                    mdetim
     c                   eval      mdeusr = l_user
      *
     c                   if        %found
6.10 c                   time                    mdedat
6.10 c                   time                    mdetim
6.10 c                   eval      mdeusr = l_user
     c                   update    mdlolur
     c                   else
     c                   eval      mdfirm = w_firm
     c                   eval      mdlage = c1lage
     c                   eval      mdloka = c1loka
     c                   time                    mdodat
6.10 c                   time                    mdotim
6.10 c                   eval      mdousr = l_user
6.10 c                   time                    mdedat
6.10 c                   time                    mdetim
6.10 c                   eval      mdeusr = l_user
     c                   write     mdlolur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b_forn = *on
     c                   eval      b1lbes = mdlbes
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
      * Fyller slette-bildet
      *
5.20 c                   eval      mdlolr_lage = d1lage
     c                   eval      mdlolr_loka = d1loka
     c     mdlolr_key    chain     mdlolr
      *
     c                   eval      d1lage = mdlage
     c                   eval      d1loka = mdloka
     c                   eval      d1lbes = mdlbes
      *
     c                   eval      w_lage = d1lage
     c                   exsr      sjekk_lager
     c                   eval      d1lagn = w_latx
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
5.20 c                   eval      mdlolu_lage = d1lage
     c                   eval      mdlolu_loka = d1loka
     c     mdlolu_key    chain     mdlolur
      *
     c                   delete    mdlolur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
5.20 c                   eval      s_lage = k1lage
     c                   eval      s_loka = k1loka
      *
      * Fyller kopierings-bildet
      *
5.20 c                   eval      mdlolr_lage = k1lage
     c                   eval      mdlolr_loka = k1loka
     c     mdlolr_key    chain     mdlolr
      *
     c                   eval      k2lage = mdlage
     c                   eval      k2loka = mdloka
      *
     c                   eval      w_lage = k2lage
     c                   exsr      sjekk_lager
     c                   eval      k2lagn = w_latx
      *
     c                   eval      k2lokn = mdlbes
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_k1win
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      k1taga
     c                   endsl
      *
      * Sjekker gyldig lager
      *
     c                   eval      w_lage = k1lage
     c                   exsr      sjekk_lager
     c                   if        b_feil = *on
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
5.20 c                   eval      mdlolr_lage = k1lage
     c                   eval      mdlolr_loka = k1loka
     c     mdlolr_key    chain     mdlolr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
5.20 c                   eval      c1lage = s_lage
     c                   eval      c1loka = s_loka
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'L'
     c                   read      mdlol1
     c                   when      w_seqe = 'O'
     c                   read      mdlol2
     c                   endsl
      *
     c                   if        %eof or
     c                             mdfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
5.20 c                   eval      b1lage = mdlage
     c                   eval      b1loka = mdloka
     c                   eval      b1lbes = mdlbes
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'L'
     c     mdlol1_key    setgt     mdlol1
     c                   readp     mdlol1
     c                   when      w_seqe = 'O'
     c     mdlol2_key    setgt     mdlol2
     c                   readp     mdlol2
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'L'
     c                   readp     mdlol1
     c                   when      w_seqe = 'O'
     c                   readp     mdlol2
     c                   endsl
      *
     c                   if        %eof or
     c                             mdfirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'L'
     c     mdlol1_key    setll     mdlol1
     c                   when      w_seqe = 'O'
     c     mdlol2_key    setll     mdlol2
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Lager
      *
     c                   if        w_actf = 'B2LAGE'
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    b2lage
     c                   parm                    w_lagn
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_actf = 'C1LAGE'
     c                   eval      c1lagn = *blank
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    c1lage
     c                   parm                    c1lagn
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_actf = 'K1LAGE'
     c                   eval      k1lagn = *blank
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    k1lage
     c                   parm                    k1lagn
     c                   goto      end_spØrr
     c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekk/hente lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_lager   begsr
      *
     c                   eval      b_feil = *off
     c                   call      'RS710R'
     c                   parm                    w_stat
     c                   parm                    w_lage
     c                   parm                    w_latx
      *
     c                   if        w_stat <> *blank
     c                   eval      b_feil = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering lager, lagerlokasjon
      *
     c     mdlol1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mdlol1_lage
     c                   kfld                    mdlol1_loka
      *
      * Key for oppslag på lagerlokasjon
      *
     c     mdlolr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mdlolr_lage
     c                   kfld                    mdlolr_loka
      *
      * Key for oppdatering av lagerlokasjon
      *
     c     mdlolu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mdlolu_lage
     c                   kfld                    mdlolu_loka
      *
      * Key for posisjonering lagerlokasjon, lager
      *
     c     mdlol2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mdlol2_loka
     c                   kfld                    mdlol2_lage
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
      * Initierer skjermbildet
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      w_seqe = 'L'
     c                   eval      mdlol1_lage = 0
     c                   eval      mdlol1_loka = *blank
     c     mdlol1_key    setll     mdlol1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
