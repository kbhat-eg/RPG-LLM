     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : AsistT
      * Program . . . . : SI528R
      * Beskrivelse . . : Endring av innkjøpsisttistikk - oppdateringen
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       260305 KOB  Nytt program
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     fsistl1    uf   e           k disk    rename(sistpfr:sistl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvasl1    if   e           k disk    rename(vvaspfr:vvasl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
      *
      * Datastruktur - parameter
      *
     d                 ds
     d d_parm                        96
     d  d_firm                       03  0 overlay(d_parm:01)
     d  d_paar                       04  0 overlay(d_parm:04)
     d  d_fper                       02  0 overlay(d_parm:08)
     d  d_tper                       02  0 overlay(d_parm:10)
     d  d_lkat                       01  0 overlay(d_parm:12)
     d  d_dist                       01  0 overlay(d_parm:13)
     d  d_selg                       01  0 overlay(d_parm:14)
     d  d_avde                       01  0 overlay(d_parm:15)
     d  d_lagr                       01  0 overlay(d_parm:16)
     d  d_nobb                       01  0 overlay(d_parm:17)
     d  d_nmod                       01  0 overlay(d_parm:18)
     d  d_ldor                       01  0 overlay(d_parm:19)
     d  d_lvar                       01  0 overlay(d_parm:20)
     d  d_vgrp                       01  0 overlay(d_parm:21)
     d  d_agrp                       01  0 overlay(d_parm:22)
      *
      * Definering av variabler i nøkler
      *
     d sistl1_paar     s                   like(sipaar)
     d sistl1_binr     s                   like(sibinr)
     d sistl1_line     s                   like(siline)
     d sistl1_bida     s                   like(sibida)
     d vvarl1_vare     s                   like(vvvare)
     d rlevl1_levr     s                   like(rllevr)
      *
      * Definering av variable
      *
     d w_firm          s                   like(sifirm)
      *
     d w_oppd          s              1  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Arbeide med handlinger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Posisjonering
      *
     c                   exsr      posisjoner
      *
      * Les statistikkregisteret og oppdater
      *
     c                   exsr      oppdat
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å lese og oppdatere
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdat        begsr
      *
     c                   dou       *in15  = *on
      *
     c                   read      sistl1                                 15
      *
     c                   if        *in15                                        EOF
     c                   goto      end_oppdat
     c                   endif
      *
     c                   if        sifirm <> w_firm or
     c                             sipaar <> d_paar
     c                   eval      *in15 = *on
     c                   goto      end_oppdat
     c                   endif
      *
     c                   eval      w_oppd  = 0
      *
      * Test om valg kunde
      *
     c                   if        d_lkat <> 0 or
     c                             d_dist <> 0 or
     c                             d_selg <> 0 or
     c                             d_avde <> 0 or
     c                             d_lagr <> 0
      *
     c                   eval      rlevl1_levr = sivaln
     c     rlevl1_key    chain     rlevl1                             90
      *
     c                   if        *in90 =*off
      *
     c                   if        d_lkat <> 0 and
     c                             silkat <> rlkatg
     c                   eval      silkat  = rlkatg
     c                   eval      w_oppd  = 1
     c                   endif
      *
     c                   if        d_dist <> 0 and
     c                             sidist <> rldist
     c                   eval      sidist  = rldist
     c                   eval      w_oppd  = 1
     c                   endif
      *
     c                   if        d_selg <> 0 and
     c                             siselg <> rlsaks
     c                   eval      siselg  = rlsaks
     c                   eval      w_oppd  = 1
     c                   endif
      *
     c                   if        d_avde <> 0 and
     c                             sikavd <> rlavde
     c                   eval      sikavd  = rlavde
     c                   eval      w_oppd  = 1
     c                   endif
      *
     c                   if        d_lagr <> 0 and
     c                             silage <> rllagr
     c                   eval      silage  = rllagr
     c                   eval      w_oppd  = 1
     c                   endif
      *
     c                   endif
      *
     c                   endif
      *
      * Test om valg vare
      *
     c                   if        d_nobb <> 0 or
     c                             d_nmod <> 0 or
     c                             d_ldor <> 0 or
     c                             d_lvar <> 0 or
     c                             d_vgrp <> 0 or
     c                             d_agrp <> 0
      *
     c                   eval      vvarl1_vare = sivare
     c     vvarl1_key    chain     vvarl1                             90
     c                   if        *in90 = *on
     c     vvarl1_key    chain     vvasl1                             90
     c                   endif
      *
     c                   if        *in90 =*off
      *
     c                   if        d_nobb <> 0 and
     c                             sinobb <> vvnobb
     c                   eval      sinobb  = vvnobb
     c                   eval      w_oppd  = 1
     c                   endif
      *
     c                   if        d_nmod <> 0 and
     c                             simodn <> vvnmod
     c                   eval      simodn  = vvnmod
     c                   eval      w_oppd  = 1
     c                   endif
      *
     c                   if        d_ldor <> 0 and
     c                             sildor <> vvldor
     c                   eval      sildor  = vvldor
     c                   eval      w_oppd  = 1
     c                   endif
      *
     c                   if        d_lvar <> 0 and
     c                             silvar <> vvlvar
     c                   eval      silvar  = vvlvar
     c                   eval      w_oppd  = 1
     c                   endif
      *
     c                   if        d_vgrp <> 0 and
     c                             siogrp <> vvogrp or
     c                             sihgrp <> vvhgrp or
     c                             siugrp <> vvugrp
     c                   eval      siogrp  = vvogrp
     c                   eval      sihgrp  = vvhgrp
     c                   eval      siugrp  = vvugrp
     c                   eval      w_oppd  = 1
     c                   endif
      *
     c                   if        d_agrp <> 0 and
     c                             siahgr <> vvahgr or
     c                             siaugr <> vvaugr
     c                   eval      siahgr  = vvahgr
     c                   eval      siaugr  = vvaugr
     c                   eval      w_oppd  = 1
     c                   endif
      *
     c                   endif
      *
     c                   endif
      *
      * Oppdater hvis endring
      *
     c                   if        w_oppd = 1
      *
     c                   update    sistl1r
      *
     c                   endif
      *
     c                   enddo
      *
     c     end_oppdat    tag
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på år/periode
      *
     c                   eval      sistl1_paar = d_paar
     c                   eval      sistl1_binr = *zeros
     c                   eval      sistl1_line = *zeros
     c                   eval      sistl1_bida = *loval
     c     sistl1_key    setll     sistl1
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på år
      *
     c     sistl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sistl1_paar
     c                   kfld                    sistl1_binr
     c                   kfld                    sistl1_line
     c                   kfld                    sistl1_bida
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    wwparm           96
     c                   move      wwparm        d_parm
      *
      * Henter firmakode fra local
      *
     c                   eval      w_firm = d_firm
      *
     c                   endsr
