     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VP730R
      * Beskrivelse . . : Vare-pris, henter lagerenhet m/priser
      *
      *                   Henter priser på lagerenhet
      *
6.30  *                   Input:  Firma, vare, prisgr., lev.type, dato og lager
      *                   Output: Enhet, ny salgspris, gammel salgspris,
      *                           kostpris og innkjøpspris
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       051099 HKO  Nytt program
5.70  * PRGR  241008 BOF  Utvidet med prisgruppe
6.20  *       220311 BOF  Utvidet med prisgiver på vare-pris
6.30  *       030117 bsa  Utvidet med lager
      *
8.01  *PRG2   150622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvprl1    if   e           k disk    rename(vvprpfr:vvprl1r)
      *
      * Definering av pamametre
      *
     d p_firm          s                   like(vpfirm)
     d p_vare          s                   like(vpvare)
5.70 d p_prgr          s                   like(vpprgr)
     d p_lety          s                   like(vplety)
     d p_dato          s                   like(vppdat)
     d p_enhe          s                   like(vpenhe)
     d p_sapr          s                   like(vpsapr)
     d p_gmpr          s                   like(vpsapr)
     d p_kopr          s                   like(vpkopr)
     d p_inpr          s                   like(vpinpr)
6.30 d p_lage          s              2  0
      *
      * Definering av variable i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vvprl1_vare     s                   like(vpvare)
6.20 d vvprl1_ldor     s                   like(vpldor)
5.70 d vvprl1_prgr     s                   like(vpprgr)
     d vvprl1_enhe     s                   like(vpenhe)
     d vvprl1_lety     s                   like(vplety)
      *
      * Definering av variable
      *
     d b_pris          s              1    inz(*off)
     d b_gmpr          s              1    inz(*off)
6.20 d b_inlr          s              1    inz(*off)
      *
     d w_firm          s                   like(vpfirm)
     d w_dato          s                   like(vppdat)
6.20 d w_ldor          s                   like(vvldor)
6.20 d w_lage          s              2  0
      *
      *
6.20 d                uds
6.20 d l_lage               1001   1002  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30 c**                 eval      w_lage = l_lage
6.30 c                   eval      w_lage = p_lage
6.20  *
6.20  * Henter leverandør for vare og lager
6.20  *
6.20 c                   call      'VL721R  '
6.20 c                   parm                    w_firm
6.20 c                   parm                    p_vare
6.20 c                   parm                    w_lage
6.20 c                   parm                    w_ldor
6.20 c                   parm                    b_inlr
      *
      *
      * Henter lagerenhet fra vare
      *
     c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1                             90
     c                   if        *in90 = *on or
     c                             vvenhl = *blank
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      p_enhe = vvenhl
      *
      * Henter priser på lagerenhet
      *
6.20  * 1. leverandør for lageret
6.20 c                   eval      vvprl1_ldor = w_ldor
     c                   exsr      hent_pris
6.20 c                   if        b_pris = *off  and
6.20 c                             w_ldor <> vvldor
6.20  * 2. hovedleverandør
6.20 c                   eval      vvprl1_ldor = vvldor
6.20 c                   exsr      hent_pris
6.20 c                   if        b_pris = *off
6.20  * siste mulighet...
6.20 c                   eval      vvprl1_ldor = 0
6.20 c                   exsr      hent_pris
6.20 c                   endif
6.20 c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av prisinformasjon for lagerenhet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_pris     begsr
      *
     c                   eval      b_pris = *off
     c                   eval      b_gmpr = *off
      *
     c                   eval      vvprl1_vare = p_vare
5.70 c                   eval      vvprl1_prgr = p_prgr
     c                   eval      vvprl1_enhe = vvenhl
     c                   eval      vvprl1_lety = p_lety
     c     vvprl1_key    setll     vvprl1
     c     vvprl1_key    reade     vvprl1                                 91
     c                   dow       *in91 = *off
      *
     c                   eval      b_pris = *on
      *
     c                   if        b_gmpr = *off
     c                   eval      p_sapr = vpsapr
     c                   eval      p_kopr = vpkopr
     c                   eval      p_inpr = vpinpr
     c                   endif
      *
     c                   if        b_gmpr = *on
     c                   eval      p_gmpr = vpsapr
     c                   leave
     c                   endif
      *
     c                   if        w_dato >= vppdat
     c                   eval      b_gmpr = *on
     c                   endif
      *
     c     vvprl1_key    reade     vvprl1                                 91
     c                   enddo
      *
      * Dersom pris ikke finnes for oppgitt leveringstype, hentes pris fra
      * leveringstype 0
      *
     c                   if        b_pris = *off and
     c                             p_lety <> 0
      *
     c                   eval      vvprl1_vare = p_vare
5.70 c                   eval      vvprl1_prgr = p_prgr
     c                   eval      vvprl1_enhe = vvenhl
     c                   eval      vvprl1_lety = 0
     c     vvprl1_key    setll     vvprl1
     c     vvprl1_key    reade     vvprl1                                 91
     c                   dow       *in91 = *off
      *
     c                   eval      b_pris = *on
      *
     c                   if        b_gmpr = *off
     c                   eval      p_sapr = vpsapr
     c                   eval      p_kopr = vpkopr
     c                   eval      p_inpr = vpinpr
     c                   endif
      *
     c                   if        b_gmpr = *on
     c                   eval      p_gmpr = vpsapr
     c                   leave
     c                   endif
      *
     c                   if        w_dato >= vppdat
     c                   eval      b_gmpr = *on
     c                   endif
      *
     c     vvprl1_key    reade     vvprl1                                 91
     c                   enddo
      *
     c                   endif
5.70  *
5.70  * Dersom pris ikke finnes for oppgitt prisgruppe, hentes pris fra
5.70  * prisgruppe = blank
5.70  *
5.70 c                   if        b_pris = *off and
5.70 c                             p_prgr <> *blank
5.70 c                   eval      vvprl1_vare = p_vare
5.70 c                   eval      vvprl1_prgr = *blank
5.70 c                   eval      vvprl1_enhe = vvenhl
5.70 c                   eval      vvprl1_lety = p_lety
5.70 c     vvprl1_key    setll     vvprl1
5.70 c     vvprl1_key    reade     vvprl1                                 91
5.70 c                   dow       *in91 = *off
5.70  *
5.70 c                   eval      b_pris = *on
5.70  *
5.70 c                   if        b_gmpr = *off
5.70 c                   eval      p_sapr = vpsapr
5.70 c                   eval      p_kopr = vpkopr
5.70 c                   eval      p_inpr = vpinpr
5.70 c                   endif
5.70  *
5.70 c                   if        b_gmpr = *on
5.70 c                   eval      p_gmpr = vpsapr
5.70 c                   leave
5.70 c                   endif
5.70  *
5.70 c                   if        w_dato >= vppdat
5.70 c                   eval      b_gmpr = *on
5.70 c                   endif
5.70  *
5.70 c     vvprl1_key    reade     vvprl1                                 91
5.70 c                   enddo
5.70  *
5.70  * Dersom pris ikke finnes for oppgitt leveringstype, hentes pris fra
5.70  * leveringstype 0
5.70  *
5.70 c                   if        b_pris = *off and
5.70 c                             p_lety <> 0
5.70  *
5.70 c                   eval      vvprl1_vare = p_vare
5.70 c                   eval      vvprl1_prgr = *blank
5.70 c                   eval      vvprl1_enhe = vvenhl
5.70 c                   eval      vvprl1_lety = 0
5.70 c     vvprl1_key    setll     vvprl1
5.70 c     vvprl1_key    reade     vvprl1                                 91
5.70 c                   dow       *in91 = *off
5.70  *
5.70 c                   eval      b_pris = *on
5.70  *
5.70 c                   if        b_gmpr = *off
5.70 c                   eval      p_sapr = vpsapr
5.70 c                   eval      p_kopr = vpkopr
5.70 c                   eval      p_inpr = vpinpr
5.70 c                   endif
5.70  *
5.70 c                   if        b_gmpr = *on
5.70 c                   eval      p_gmpr = vpsapr
5.70 c                   leave
5.70 c                   endif
5.70  *
5.70 c                   if        w_dato >= vppdat
5.70 c                   eval      b_gmpr = *on
5.70 c                   endif
5.70  *
5.70 c     vvprl1_key    reade     vvprl1                                 91
5.70 c                   enddo
5.70  *
5.70 c                   endif
5.70  *
5.70 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
5.70 c                   parm                    p_prgr
     c                   parm                    p_lety
     c                   parm                    p_dato
     c                   parm                    p_enhe
     c                   parm                    p_sapr
     c                   parm                    p_gmpr
     c                   parm                    p_kopr
     c                   parm                    p_inpr
6.30 c                   parm                    p_lage
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for les på vare-pris
      *
     c     vvprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvprl1_vare
6.20 c                   kfld                    vvprl1_ldor
5.70 c                   kfld                    vvprl1_prgr
     c                   kfld                    vvprl1_enhe
     c                   kfld                    vvprl1_lety
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
      * Initierer dato
      *
     c                   eval      w_dato = p_dato
      *
     c                   if        w_dato = *loval
     c                   time                    w_dato
     c                   endif
      *
      * Initierer output-parametre
      *
     c                   eval      p_enhe = *blank
     c                   eval      p_sapr = 0
     c                   eval      p_gmpr = 0
     c                   eval      p_kopr = 0
     c                   eval      p_inpr = 0
      *
     c                   endsr
