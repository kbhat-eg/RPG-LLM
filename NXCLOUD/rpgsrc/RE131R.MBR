     h/TITLE    Program : RE131R   ASOKON
     h decedit(',') datedit(*dmy.)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RE131R                                              *
      * Beskrivelse . . : Integrasjon Huldt&Lillevik lønn ==> ASOKON          *
      * Beskrivelse . . : Danner rengskapsposteringer                         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      * R3.40     290103  Nytt program                                   HFL  *
      * R5.20     260104  Tilpasset v5.20                                HFL  *
6.10  * R6.10     250211  Flytter riktig år til oppdatering av bilagsnr  NHO  *
6.11  * R6.10     120511  Legger ut mva kode                             NHO  *
6.20  *  sst      190312  Tilpasset nytt prosjekt system                      *
6.21  *  nho      130511  Legger ut momskode til rtrapf                       *
6.22  *  nho      010313  Feil i nøkkel, stigende buntnr feilet               *
6.22  *  nho      040313  Feilet også dersom LONN ikke lå inne fra før
6.23  *  sst      280513  Tilpasset numerisk prosjekt fra H&L
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     freutpf    upe  e           k disk
     fafirl1    if   e           k disk
     f                                     rename(afirpfr:afirl1r)
E5.20fraa4l1    if   e           k disk
T5.20f                                     rename(raa4pfr:raa4l1r)
6.20 frp4ol1    if   e           k disk    rename(rp4opfr:rp4ol1r)
     fraa4lu    uf   e           k disk
     f                                     rename(raa4pfr:raa4lur)
     frbunlu    uf a e           k disk
     f                                     rename(rbunpfr:rbunlur)
     frtralu    o  a e           k disk
      *
     d                uds
     d l_feil                211    211
     d lcuser                911    920
     d lcfgrp                931    933
     d lcfirm                944    946  0
     d lcnavn                951    980
      *---------------------------------------------------------------------
      * Stand Alone Fields - TOP
      *---------------------------------------------------------------------
     d Åfem0           s              5  0
     d Åtte0           s              8  0
     d pdatx           s                   like(Åtte0)
     d wlinje          s                   like(Åtte0)
6.NU d wnota           s              8  0
     d wnxbun          s                   like(rtbunt)
     d wtpre           s             10  2
     d w_aa            s              2  0
     d w_aaaa          s              4  0
610  d rtralu_nivo     s                   like(rtnivo)
610  d rtralu_memb     s                   like(rtmemb)
610  d rbunlu_nivo     s                   like(rsnivo)
610  d rbunlu_memb     s                   like(rsmemb)
610  d w_memb          s                   like(rsmemb)
6.20 d rp4ol1_frm      s                   like(rp4frm)
6.20 d rp4ol1_aor      s                   like(rp4ord)
      *---------------------------------------------------------------------
      * Stand Alone Fields - BOTTOM
      *---------------------------------------------------------------------
      *
     ireutpfr       01
      *  Lønnsposteringer - H&L LØNN
     i                                          refirm        l6
T5.30i                                          reraar        l5
E5.30i                                          rerper        l4
     i                                          rebdat        l3
      *
     iafirl1r
      *  Firmaopplysninger - firmaregister
      *
E5.20iraa4l1r
      *-----------------
      *    BRUDD FIRMA
      *-----------------
     c                   if        *inl6 = *on
     c                   move      *on           *in50
     c                   endif
      *
     c   l6              if        refirm = lcfirm and
     c                             reflgr = lcfgrp
     c                   move      *off          *in50
     c                   endif
     c                   if        *inl6 = *on
     c                   move      refirm        aaffir
     c     keyfir        chain     afirl1                             10
     c                   endif
     c                   if        *inl6 = *on
     c                   move      refirm        ra4fir
T5.30c                   move      reraar        ra4aar
     c                   endif
      *
     c                   if        *inl3 = *on
E5.20c     keyaa4        chain     raa4l1                             10
     c                   endif
     c                   if        *inl3 = *on
     c                             and *in10 = *off
     c                   z-add     ra4sgb        wnota
     c                   endif
     c                   if        *inl3 = *on
     c                             and *in10 = *on
     c                   z-add     0             wnota
     c                   endif
     c                   if        *inl3 = *on
     c                   add       1             wnota
     c                   endif
      *-------------------------------------
      *  Finn ledig buntnr. for LØNNSposter
      *-------------------------------------
     c                   if        *inl3 = *on
610  c                   eval      rbunlu_nivo = *blank
610  c                   eval      rbunlu_memb = w_memb
     c                   move      refirm        rsfirm
     c                   eval      rsbunt = 99999
     c     keybun        setgt     rbunlu
     c                   readp     rbunlu                                 10
     c                   endif
      *
6.22 c                   if        *inl3 = *on and
6.22 c                             w_memb <> rsmemb
6.22 c                   move      '1'           *in10
6.22 c                   endif
6.22  *
     c                   if        *inl3 = *on
     c                             and *in10 = *on
     c                   z-add     1             wnxbun
     c                   endif
     c                   if        *inl3 = *on
     c                             and *in10 = *off
     c     rsbunt        add       1             wnxbun
     c                   endif
      *
     c                   if        *in50 = *on
     c                   move      '1'           l_feil
     c                   goto      slutt
     c                   endif
      *
6.11 c                   eval      rtdmva = *blank                              Debet-mva.
6.11 c                   eval      rtcmva = *blank                              Debet-mva.
      *
     c                   if        rebelØ > *zero
     c                   add       rebelØ        wtpre
     c                   z-add     rebelØ        rtbelØ
6.11 c                   eval      rtdmva = remvak                              Bilag-nr.
     c                   else
     c                   sub       rebelØ        wtpre
6.11 c                   eval      rtcmva = remvak                              Bilag-nr.
     c                   z-sub     rebelØ        rtbelØ
     c                   endif
     c                   add       1             wlinje
     c                   eval      rtreid = 'T'
     c                   move      refirm        rtfirm
     c                   eval      rtbunt = wnxbun
     c                   move      wlinje        rtlinj
     c                   eval      rtstat = *blank
     c                   move      rebdat        rtbdat
T5.30c                   move      reraar        rtraar
     c                   move      rerper        rtrper
     c                   eval      rtbiln = wnota
     c                   eval      rtdavd = *zeros
     c                   eval      rtdkto = *zeros
     c                   eval      rtdsps = *zeros
6.11 c*                  eval      rtdmva = *blank
     c                   if        rebelØ > *zeros
     c                   eval      rtdavd = reavde
     c                   eval      rtdkto = rekont
     c                   eval      rtdsps = respes
     c                   endif
     c                   eval      rtcavd = *zeros
     c                   eval      rtckto = *zeros
     c                   eval      rtcsps = *zeros
6.11 c*                  eval      rtcmva = *blank
     c                   if        rebelØ < *zeros
     c                   eval      rtcavd = reavde
     c                   eval      rtckto = rekont
     c                   eval      rtcsps = respes
     c                   endif
6.20 c*                  move      repros        rtpros
6.20 c*                  move      reakti        rtakti
6.20 c                   if        repros = '000000'
6.20 c                   eval      repros = *blank
6.20 c                   endif
6.20 c                   eval      rp4ol1_frm = refirm
6.20 c                   eval      rp4ol1_aor = repros
6.20 c     rp4ol1_key    chain     rp4ol1
6.22 c                   if        not %found(rp4ol1)
6.22 c                   dow       %subst(repros:1:1) = '0'
6.22 c                   eval      %subst(repros:1:5) =
6.22 c                             %subst(repros:2:5)
6.22 c                   eval      %subst(repros:6:1) = *blank
6.22 c                   enddo
6.22 c                   eval      rp4ol1_aor = repros
6.22 c     rp4ol1_key    chain     rp4ol1
6.22 c                   endif
6.20 c                   if        %found(rp4ol1)
6.20 c                   eval      rtpros = rp4pro
6.20 c                   eval      rtupro = rp4upr
6.20 c                   eval      rtakti = rp4akt
6.20 c                   eval      rtarbo = repros
6.20 c                   else
6.20 c                   eval      rtpros = repros
6.20 c                   eval      rtupro = *blank
6.20 c                   eval      rtakti = *blank
6.20 c                   eval      rtarbo = repros
6.20 c                   endif
6.20  *
     c                   eval      rtmngd = remngd
     c                   eval      rtrefn = *zeros
E5.20c                   eval      rtfdat = *loval
     c                   eval      rtkrab = *zeros
     c                   eval      rtvalk = *blank
     c                   eval      rtvalb = *zeros
     c                   eval      rtresn = *zeros
     c                   eval      rtvirk = *blank
     c                   move      *blank        rtatte
S5.20c*                  eval      rtemrk = *blank
     c                   eval      rtkrys = *blank
     c                   eval      rtkidi = *blank
OBS  c                   eval      rtbilk = 40
     c                   eval      rttext = *blank
E5.20c                   eval      rtbetb = *zeros
     c                   eval      rtbref = *blank
S5.20c*                  eval      rtf001 = *blank
610  c                   eval      rtnivo = *blank
610  c                   eval      rtmemb = w_memb
610  c                   time                    rtedat
610  c                   time                    rtetim
610  c                   eval      rteusr = lcuser
     c                   write     rtrapfr
      *
     c                   delete    reutpfr
      *                                                     i H&L Lønn
      *-------------------------
     c     slutt         tag
      *-------------------------
      *   ------------------------------------------------------
      *    LES STATUSREC FOR OPPDATERING AV GENERERT BILAG
      *   ------------------------------------------------------
     cl3                 if        *in50 = *on
     cl3                 goto      felfir
     cl3                 endif
     cl3                 move      refirm        ra4fir
6.10 c***l3                 move      w_aaaa        ra4aar
6.10 cl3                 move      reraar        ra4aar                         ÅR
     cl3   keyaa4        chain     raa4lu                             10
     cl3                 eval      ra4sgb = wnota
6.10 c***l3                 add       1             ra4sgb
     cl3                 if        *in10 = *off
     cl3                 update    raa4lur
     cl3                 endif
      *
     cl3                 z-add     wtpre         rtbelØ
     cl3                 eval      rsfirm = rtfirm
     cl3                 eval      rsbunt = rtbunt
     cl3                 eval      rsstat = *blank
T5.30cl3                 eval      rsraar = rtraar
     cl3                 eval      rsrper = rtrper
     cl3                 z-add     1             rsfØrs
     cl3                 move      wlinje        rssist
E5.20cl3                 time                    rspdat
     cl3                 eval      rsbsum = wtpre
     cl3                 eval      rspsum = wtpre
     cl3                 eval      rspdeb = wtpre
     cl3                 eval      rspkre = wtpre
610  cl3                 eval      rsnivo = *blank
610  cl3                 eval      rsmemb = w_memb
610  cl3                 time                    rsedat
610  cl3                 time                    rsetim
610  cl3                 eval      rseusr = lcuser
     cl3                 write     rbunlur
      *-------------------------
     cl6   felfir        tag
      *-------------------------
     cl6                 move      *on           *in50
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     c     *inzsr        begsr
     c     *entry        plist
610  c                   parm                    w_memb
      *
      *  DEFINISJON AV ARBEIDSFELT
     c                   eval      Åtte0 = *zeros
     c                   eval      Åfem0 = *zeros
     c                   eval      wtpre = *zeros
      **
     c     keyfir        klist
     c                   kfld                    aaffir
     c     keyaa4        klist
     c                   kfld                    ra4fir
     c                   kfld                    ra4aar
     c     keybun        klist
610  c**** 6.22          kfld                    rtralu_nivo
610  c**** 6.22          kfld                    rtralu_memb
6.22 c                   kfld                    rbunlu_nivo
6.22 c                   kfld                    rbunlu_memb
     c                   kfld                    rsfirm
     c                   kfld                    rsbunt
     c     rp4ol1_key    klist
     c                   kfld                    rp4ol1_frm
     c                   kfld                    rp4ol1_aor
      *
     c                   endsr
      * ----------------------------------------
