     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RR630R
      * Beskrivelse . . : Utlisting av resultatrapporter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frr630d    cf   e             workstn
     frrf0l1    if   e           k disk    rename(rrf0pfr:rrf0l1r)
      *
      * Definering av LDA
      *
     d                uds
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
     d w_firm          s              3  0
     d p_0rap          s                   like(rr0rap)
      *
     d rrf0l1_0rap     s                   like(rr0rap)
      *
     d w_mld1          c                   'Vennligst vent .........     '
     d w_mld2          c                   'Utlisting pågår.........     '
     d wactli          s              2  0
     d wactpo          s              3  0
     d wmld01          s             30
     d wmld02          s             30
      *
     d w_ruti          s             10
     d w_prtf          s             10
     d w_valg          s              1
     d w_tvbt          s              1
     d w_in03          s              1
     d w_btch          s              1
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Behandling av skjermbilde
      *
     c     b1taga        tag
      *
     c                   exfmt     b1bld
      *
      * F3=Avslutt
      *
     c                   if        *inkc or *inkl
     c                   goto      avslutt
     c                   endif
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1taga
     c                   endif
      *
     c                   exsr      flytt_par
      *
      * Send meldingsbildet
      *
     c                   eval      wactli = 4
     c                   eval      wactpo = 44
     c                   eval      wmld01 = w_mld1
     c                   eval      wmld02 = w_mld2
      *
     c                   call      'AA006C'
     c                   parm                    wactli
     c                   parm                    wactpo
     c                   parm                    wmld01
     c                   parm                    wmld02
      *
      * Kaller program for rutineregister
      *
     c                   eval      w_ruti = 'RR631P    '
     c                   eval      w_prtf = 'RR631P    '
     c                   eval      w_valg = '1'
     c                   eval      w_tvbt = '0'
     c                   eval      w_in03 = ' '
     c                   eval      w_btch = ' '
      *
     c                   call      'AP600R'
     c                   parm                    w_ruti
     c                   parm                    w_valg
     c                   parm                    w_tvbt
     c                   parm                    w_in03
     c                   parm                    w_btch
      *
     c                   call      'AP601C'
     c                   parm                    w_prtf
     c                   parm                    w_btch
      *
      * Kaller utskriftsprogram
      *
     c                   call      'RR631R'
     c                   parm                    p_0rap
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
     c                   eval      *in31 = *off
      *
      * Rapport
      *
     c                   if        b10rap <> 0
     c                   eval      rrf0l1_0rap = b10rap
     c     rrf0l1_key    chain     rrf0l1r                            90
     c                   if        *in90 = *on
     c                   eval      *in31 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
     c     end_sjekk     tag
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for flytting av felter til DS-parametre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     flytt_par     begsr
      *
     c                   eval      p_0rap = b10rap
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_0rap
      *
      * Initiering av skjermbilde
      *
     c                   eval      w_firm = l_firm
     c                   eval      wactli = 3
     c                   eval      wactpo = 2
     c                   eval      b10rap = p_0rap
      *
      * Key for rapportregister
      *
     c     rrf0l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rrf0l1_0rap
      *
     c                   endsr
