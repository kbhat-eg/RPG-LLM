     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLOGI
      * Program . . . . : MM401R
      * Beskrivelse . . : Sletter prognoseresultat pr. parameter
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       010305 bø   Nytt program
5.50  *       220906 bø   Rettet slik at den sletter
      *       310507 bø   Utvidet b.punkt, m.meng,mini til 3 desimaler
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fmprul4    uf   e           k disk    rename(mprupfr:mprul4r)
      *
      * Definering av variable i nøkler
      *
     d mprul4_vsor     s                   like(mrvsor)
     d mprul4_ogrp     s                   like(mrogrp)
     d mprul4_hgrp     s                   like(mrhgrp)
     d mprul4_ugrp     s                   like(mrugrp)
     d mprul4_ldor     s                   like(mrldor)
     d mprul4_vark     s                   like(mrvark)
     d mprul4_crdo     s                   like(mrcrdo)
      *
      * Parameter-inn
     d p_dato          s               d   datfmt(*iso)
     d p_antu          s              3  0
      *
      * Local Data Area
      *
     d                uds
      *
     d l_firm                944    946  0
      *
     d                 ds
     d d_list                       128
     d  d_vsor                       10    overlay(d_list)
     d  d_ogrp                        2  0 overlay(d_list:11)
     d  d_hgrp                        2  0 overlay(d_list:13)
     d  d_ugrp                        3  0 overlay(d_list:15)
     d  d_ldor                        6  0 overlay(d_list:18)
     d  d_vare                        8  0 overlay(d_list:24)
     d  d_crdo                        1  0 overlay(d_list:32)
     d  d_dato                        6  0 overlay(d_list:33)
     d  d_antu                        2  0 overlay(d_list:39)
      *
      * Definering av variable
      *
     d w_firm          s                   like(mrfirm)
     d w_list          s            128
      *
     d b_ok            s              1    inz(*off)
     d b_matr          s              1    inz(*off)
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   exsr      slett_res
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
      *
     c                   return
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for kontroll mot parameter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     slett_res     begsr
      *
     c                   eval      mprul4_vsor = d_vsor
     c                   eval      mprul4_ogrp = d_ogrp
     c                   eval      mprul4_hgrp = d_hgrp
     c                   eval      mprul4_ugrp = d_ugrp
     c                   eval      mprul4_ldor = d_ldor
5.50 c                   if        d_vare = 0
5.50 c                   eval      mprul4_vark = *blank
5.50 c                   else
     c                   movel     d_Vare        mprul4_vark
5.50 c                   endif
     c                   eval      mprul4_crdo = d_crdo
5.50 c     mprul4_key    setll     mprul4
5.50 c     mprul4_key    reade     mprul4
5.50 c                   dow       not %eof(mprul4)
     c                   delete    mprul4r
5.50 c     mprul4_key    reade     mprul4
     c                   enddo
5.50 *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *
      * Key for å slette prognose for et spesielt parameter
      *
     c     mprul4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mprul4_vsor
     c                   kfld                    mprul4_ogrp
     c                   kfld                    mprul4_hgrp
     c                   kfld                    mprul4_ugrp
     c                   kfld                    mprul4_ldor
     c                   kfld                    mprul4_vark
     c                   kfld                    mprul4_crdo
      *
     c     *entry        plist
     c                   parm                    w_list
     c                   eval      d_list = w_list
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
