     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LH700R
      *  Beskrivelse . . : Generering av forslag - parameter inntasting
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign
      * --------- ------  --------------------------------------------------
      *           140200  Nytt program                             ASK
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flh700d    cf   e             workstn
      *
      *  Definering av datastrukturer
      *
      * Parameter-inn
     d                 ds
     d d_prec                       256
     d  d_flev                        6  0 overlay(d_prec)
     d  d_tlev                        6  0 overlay(d_prec:7)
     d  d_fogr                        2  0 overlay(d_prec:13)
     d  d_fhgr                        2  0 overlay(d_prec:15)
     d  d_fugr                        3  0 overlay(d_prec:17)
     d  d_fvar                       15    overlay(d_prec:20)
     d  d_togr                        2  0 overlay(d_prec:35)
     d  d_thgr                        2  0 overlay(d_prec:37)
     d  d_tugr                        3  0 overlay(d_prec:39)
     d  d_tvar                       15    overlay(d_prec:42)
     d  d_sakb                        4    overlay(d_prec:57)
     d  d_lage                        2  0 overlay(d_prec:61)
     d  d_vval                        1  0 overlay(d_prec:63)
     d  d_sats                        3  0 overlay(d_prec:64)
     d  d_aval                        1  0 overlay(d_prec:67)
     d  d_tdat                        6  0 overlay(d_prec:68)
      *
      * Local Data Area
      *
     d                uds
     d l_prtf                800    809
     d l_chgv                844    844
     d l_firm                944    946  0
     d l_fnav                951    980
     d l_lage               1001   1002  0
      *
      *
      *   Definering av variable
      *
     d w_firm          s              3  0
     d w_latx          s             20
     d w_letx          s             30
     d w_satx          s             30
     d w_vare          s             15
     d w_vtxt          s             35
     d w_otxt          s             35
     d w_htxt          s             35
     d w_utxt          s             35
     d w_adrs          s             30
     d w_ponr          s              4  0
     d w_pste          s             30
     d w_avde          s              4  0
      *
     d b_feil          s              1    inz(*off)
     d b_retu          s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     start_bld     tag
      *
     c                   exsr      beh_b1bld
     c                   if        *inkc = *on
     c                   goto      end_pgm
     c                   endif
      *
     c                   exsr      beh_r1win
     c                   if        *inkc = *on
     c                   eval      b_retu = *on
     c                   goto      start_bld
     c                   endif
      *
      *  Kaller opp neste program
      *
     c                   call      'LH701C'
     c                   parm                    d_prec
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  B1 Behandle bilde for utvalgsparametre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     beh_b1bld     begsr
      *
      *  Nullstiller variable som skal vises i skjermbildet
      *
     c                   if        b_retu = *off
     c                   eval      b1flev = 0
     c                   eval      b1fogr = 0
     c                   eval      b1fhgr = 0
     c                   eval      b1fugr = 0
     c                   eval      b1fvar = *blank
     c                   eval      b1tlev = 0
     c                   eval      b1togr = 0
     c                   eval      b1thgr = 0
     c                   eval      b1tugr = 0
     c                   eval      b1tvar = *blank
      *
     c                   eval      b1tdat = 0
     c                   eval      b1lage = l_lage
     c                   eval      b1valg = 0
     c                   eval      b1sats = 0
     c                   endif
      *
     c     b1bld_ut      tag
     c                   exfmt     b1bld
      *
      *  Behandle funksjonstaster
      *
     c                   select
     c                   when      *inkc = *on
     c                   goto      b1bld_end
     c                   when      *inkd = *on
     c                   exsr      sp_input
     c                   goto      b1bld_ut
     c                   endsl
      *
      *  Sjekk, er riktig valg tastet
      *
     c                   if        b1valg > 4
     c                   eval      *in32 = *on
     c                   goto      b1bld_ut
     c                   endif
      *
      *  Sjekk, Finnes lager
      *
     c                   call      'FÅ720R'
     c                   parm                    w_firm
     c                   parm                    b1lage
     c                   parm                    w_latx
     c                   parm                    w_adrs
     c                   parm                    w_ponr
     c                   parm                    w_pste
     c                   parm                    w_avde
     c                   parm                    b_feil
      *
     c                   if        b_feil = *on
     c                   eval      *in31 = *on
     c                   goto      b1bld_ut
     c                   endif
      *
      *  Sjekk, Er dato lagt inn og er gyldig
      *
     c                   if        b1tdat <> 0
     c     *dmy          test(d)                 b1tdat                 33
     c                   if        *in33 = *on
     c                   goto      b1bld_ut
     c                   endif
     c                   endif
      *
      *  Legger inntastede opplysninger i Hjelpe-felter for Parameter
      *
     c                   eval      d_flev = b1flev
     c                   eval      d_fogr = b1fogr
     c                   eval      d_fhgr = b1fhgr
     c                   eval      d_fugr = b1fugr
     c                   eval      d_fvar = b1fvar
     c                   eval      d_tlev = b1tlev
     c                   eval      d_togr = b1togr
     c                   eval      d_thgr = b1thgr
     c                   eval      d_tugr = b1tugr
     c                   eval      d_tvar = b1tvar
     c                   eval      d_sakb = b1inkj
      *
     c                   eval      d_tdat = b1tdat
     c                   eval      d_lage = b1lage
     c                   eval      d_vval = b1valg
     c                   eval      d_sats = b1sats
      *
      *  Dersom t.o.m. er tomt legges inn 9-tall
      *
     c                   if        d_tlev = 0
     c                   eval      d_tlev = 999999
     c                   endif
     c                   if        d_togr = 0
     c                   eval      d_togr = 99
     c                   endif
     c                   if        d_thgr = 0
     c                   eval      d_thgr = 99
     c                   endif
     c                   if        d_tugr = 0
     c                   eval      d_tugr = 999
     c                   endif
     c                   if        d_tvar = *blank
     c                   eval      d_tvar = '99999999'
     c                   endif
      *
     c     b1bld_end     endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  R1 Behandle bilde for å bestemme antall
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     beh_r1win     begsr
      *
     c                   eval      r1valg = 0
      *
     c     r1tagb        tag
     c                   exfmt     r1win
      *
      *  F3=Gå tilbake
      *
     c                   if        *inkc = *on
     c                   goto      r1win_end
     c                   endif
      *
      *  Sjekk, er riktig valg tastet
      *
     c                   if        r1valg <> 1 and
     c                             r1valg <> 2
     c                   eval      *in33 = *on
     c                   goto      r1tagb
     c                   endif
      *
      *  Legger inntastede opplysninger i Hjelpe-felter for Parameter
      *
     c                   eval      d_aval = r1valg
      *
     c     r1win_end     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for spørring på input B1BLD
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sp_input      begsr
      *
      *  Spørring på leverandør
      *
     c                   if        wactfe = 'B1FLEV'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    b1flev
     c                   parm                    w_letx
     c                   goto      sp_end
     c                   endif
      *
     c                   if        wactfe = 'B1TLEV'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    b1tlev
     c                   parm                    w_letx
     c                   goto      sp_end
     c                   endif
      *
      *  Spørring i overgrupperegister
      *
     c                   if        wactfe = 'B1FOGR'
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b1fogr
     c                   parm                    w_otxt
     c                   goto      sp_end
     c                   endif
      *
     c                   if        wactfe = 'B1TOGR'
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b1togr
     c                   parm                    w_otxt
     c                   goto      sp_end
     c                   endif
      *
      *  Spørring i hovedgrupperegister
      *
     c                   if        wactfe = 'B1FHGR'
 001 C                   call      'VG511R'
 001 C                   parm                    w_firm
 001 C                   parm                    b1fogr
 001 C                   parm                    b1fhgr
 001 C                   parm                    w_htxt
     c                   goto      sp_end
     c                   endif
      *
     c                   if        wactfe = 'B1THGR'
     c                   if        b1togr = 99
     c                   eval      b1togr = 0
     c                   eval      b1thgr = 0
     c                   endif
 001 c                   call      'VG511R'
 001 c                   parm                    w_firm
 001 c                   parm                    b1togr
 001 c                   parm                    b1thgr
 001 c                   parm                    w_htxt
     c                   goto      sp_end
     c                   endif
      *
      *  Spørring i undergrupperegister
      *
     c                   if        wactfe = 'B1FUGR'
 001 c                   call      'VG512R'
 001 c                   parm                    w_firm
 001 c                   parm                    b1fogr
 001 c                   parm                    b1fhgr
 001 c                   parm                    b1fugr
 001 c                   parm                    w_utxt
     c                   goto      sp_end
     c                   endif
      *
     c                   if        wactfe = 'B1TUGR'
     c                   if        b1togr = 99
     c                   eval      b1togr = 0
     c                   eval      b1thgr = 0
     c                   eval      b1tugr = 0
     c                   endif
     c                   if        b1thgr = 99
     c                   eval      b1thgr = 0
     c                   eval      b1tugr = 0
     c                   endif
 001 c                   call      'VG512R'
 001 c                   parm                    w_firm
 001 c                   parm                    b1togr
 001 c                   parm                    b1thgr
 001 c                   parm                    b1tugr
 001 c                   parm                    w_utxt
     c                   goto      sp_end
     c                   endif
      *
      *  Spørring i vareregister
      *
     c                   if        wactfe = 'B1FVAR'
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_vtxt
     c                   movel     w_vare        b1fvar
     c                   goto      sp_end
     c                   endif
      *
     c                   if        wactfe = 'B1TVAR'
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_vtxt
     c                   movel     w_vare        b1tvar
     c                   goto      sp_end
     c                   endif
      *
      *  Spørring lager
      *
     c                   if        wactfe = 'B1LAGE'
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    b1lage
     c                   parm                    w_latx
     c                   goto      sp_end
     c                   endif
      *
      *  Spørring saksbehandler/innkjøper
      *
     c                   if        wactfe = 'B1INKJ'
     c                   call      'RA526R'
     c                   parm                    w_firm
     c                   parm                    b1inkj
     c                   parm                    w_satx
     c                   goto      sp_end
     c                   endif
      *
     c     sp_end        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     *inzsr        begsr
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
