     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : QD101R
      * Beskrivelse . . : Velge bestillingsnummer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       010419 bsa  Nytt program
8.01  *PRG2   220622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffodtlr    if   e           k disk    rename(fodtpfr:fodtlrr)
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
      *
     fqd101d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(fdfirm)
     d p_numm          s                   like(fdnumm)
     d p_suff          s                   like(fdsuff)
     d p_line          s                   like(fdline)
     d p_benr          s                   like(fdnumm)
     d p_besu          s                   like(fdsuff)
      *
      * Definering av variabler i nøkler
      *
     d fodtlr_numm     s                   like(fdnumm)
     d fodtlr_suff     s                   like(fdsuff)
     d fodtlr_line     s                   like(fdline)
     d lohel1_numm     s                   like(lonumm)
     d lohel1_suff     s                   like(losuff)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_eof           s              1    inz(*off)
     d b_valg_ok       s              1    inz(*off)
      *
     d s_benr          s                   like(lonumm)
     d s_bes0          s                   like(lonumm)
     d s_bes1          s                   like(lonumm)
     d s_bes2          s                   like(lonumm)
     d s_bes3          s                   like(lonumm)
     d s_bes4          s                   like(lonumm)
     d s_bes5          s                   like(lonumm)
     d s_bes6          s                   like(lonumm)
     d s_bes7          s                   like(lonumm)
     d s_bes8          s                   like(lonumm)
     d s_bes9          s                   like(lonumm)
     d s_besu          s                   like(losuff)
     d s_bsu0          s                   like(losuff)
     d s_bsu1          s                   like(losuff)
     d s_bsu2          s                   like(losuff)
     d s_bsu3          s                   like(losuff)
     d s_bsu4          s                   like(losuff)
     d s_bsu5          s                   like(losuff)
     d s_bsu6          s                   like(losuff)
     d s_bsu7          s                   like(losuff)
     d s_bsu8          s                   like(losuff)
     d s_bsu9          s                   like(losuff)
      *
     d w_firm          s                   like(fdfirm)
     d w_ddat          s               d   datfmt(*iso)
     d w_daar          s              4  0
     d w_faar          s              4  0
     d w_tell          s              2  0
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(10)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * w_curn - Current recordnummer på siden
      * w_virn - Vis den side som inneholder denne record.
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   if        w_tell < 2
     c                   eval      p_benr = s_benr
     c                   eval      p_besu = s_besu
     c                   goto      avslutt
     c                   endif
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
     c                   if        b_valg_ok = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_sfrn <> 0
     c     w_sfrn        chain     b1sfl
     c                   endif
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Velg
      *
     c                   if        b1valg = 1
     c                   eval      p_benr = b1numm
     c                   eval      p_besu = b1suff
     c                   eval      b_valg_ok = *on
     c                   leave
     c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      s_benr = 0
     c                   eval      s_besu = 0
     c                   eval      s_bes0 = 0
     c                   eval      s_bes1 = 0
     c                   eval      s_bes2 = 0
     c                   eval      s_bes3 = 0
     c                   eval      s_bes4 = 0
     c                   eval      s_bes5 = 0
     c                   eval      s_bes6 = 0
     c                   eval      s_bes7 = 0
     c                   eval      s_bes8 = 0
     c                   eval      s_bes9 = 0
     c                   eval      s_bsu0 = 0
     c                   eval      s_bsu1 = 0
     c                   eval      s_bsu2 = 0
     c                   eval      s_bsu3 = 0
     c                   eval      s_bsu4 = 0
     c                   eval      s_bsu5 = 0
     c                   eval      s_bsu6 = 0
     c                   eval      s_bsu7 = 0
     c                   eval      s_bsu8 = 0
     c                   eval      s_bsu9 = 0
     c                   eval      w_tell = 0
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c     fodtlr_key    setll     fodtlr
     c     fodtlr_key2   reade     fodtlr
      *
     c                   dow       not %eof
      *
     c                   if        s_benr <> fdbenr  and
     c                             fdbenr <> 0
      *
     c                   if        s_bes0 = fdbenr
     c                   goto      neste
     c                   elseif    s_bes1 = fdbenr
     c                   goto      neste
     c                   elseif    s_bes2 = fdbenr
     c                   goto      neste
     c                   elseif    s_bes3 = fdbenr
     c                   goto      neste
     c                   elseif    s_bes4 = fdbenr
     c                   goto      neste
     c                   elseif    s_bes5 = fdbenr
     c                   goto      neste
     c                   elseif    s_bes6 = fdbenr
     c                   goto      neste
     c                   elseif    s_bes7 = fdbenr
     c                   goto      neste
     c                   elseif    s_bes8 = fdbenr
     c                   goto      neste
     c                   elseif    s_bes9 = fdbenr
     c                   goto      neste
     c                   endif
      *
      * Ta vare på inntil 10 bestillingsnummer.
     c                   if        s_bes0 = 0
     c                   eval      s_bes0 = fdbenr
     c                   eval      s_bsu0 = fdbsuf
     c                   elseif    s_bes1 = 0
     c                   eval      s_bes1 = fdbenr
     c                   eval      s_bsu1 = fdbsuf
     c                   elseif    s_bes2 = 0
     c                   eval      s_bes2 = fdbenr
     c                   eval      s_bsu2 = fdbsuf
     c                   elseif    s_bes3 = 0
     c                   eval      s_bes3 = fdbenr
     c                   eval      s_bsu3 = fdbsuf
     c                   elseif    s_bes4 = 0
     c                   eval      s_bes4 = fdbenr
     c                   eval      s_bsu4 = fdbsuf
     c                   elseif    s_bes5 = 0
     c                   eval      s_bes5 = fdbenr
     c                   eval      s_bsu5 = fdbsuf
     c                   elseif    s_bes6 = 0
     c                   eval      s_bes6 = fdbenr
     c                   eval      s_bsu6 = fdbsuf
     c                   elseif    s_bes7 = 0
     c                   eval      s_bes7 = fdbenr
     c                   eval      s_bsu7 = fdbsuf
     c                   elseif    s_bes8 = 0
     c                   eval      s_bes8 = fdbenr
     c                   eval      s_bsu8 = fdbsuf
     c                   elseif    s_bes9 = 0
     c                   eval      s_bes9 = fdbenr
     c                   eval      s_bsu9 = fdbsuf
     c                   endif
      *
     c                   eval      w_tell = w_tell + 1
     c                   eval      s_benr = fdbenr
     c                   eval      s_besu = fdbsuf
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1numm = fdbenr
     c                   eval      b1suff = fdbsuf
     c                   eval      lohel1_numm = fdbenr
     c                   eval      lohel1_suff = fdbsuf
     c     lohel1_key    chain     lohel1
      *
     c     *dmy          move      lobdat        b1dato
     c                   movel     lolena        b1levn
      *
     c                   write     b1sfl
      *
     c                   endif
      *
     c     neste         tag
      *
     c     fodtlr_key2   reade     fodtlrr
     c                   enddo
      *
     c                   eval      *in15 = *on
     c                   leave
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_line
     c                   parm                    p_benr
     c                   parm                    p_besu
      *
      *  Key les av bestillingslinjer
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
     c                   kfld                    lohel1_suff
      *
      *  Key les av ordrelinjer
      *
     c     fodtlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtlr_numm
     c                   kfld                    fodtlr_suff
     c                   kfld                    fodtlr_line
      *
      *  Key les av ordrelinjer
      *
     c     fodtlr_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    fodtlr_numm
     c                   kfld                    fodtlr_suff
      *
      * Henter parametre, og initierer skjermbildet
      *
     c                   eval      w_firm = p_firm
     c                   eval      *in22 = *on
      *
     c                   eval      fodtlr_numm = p_numm
     c                   eval      fodtlr_suff = p_suff
     c                   eval      fodtlr_line = 0
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
