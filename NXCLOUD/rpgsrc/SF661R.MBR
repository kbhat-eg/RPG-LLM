      /TITLE  Danner statistikk-PC-register
     H datedit(*dmy.) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  System. . . . . : ASSTAT                                       *
      *  Program . . . . : SF661R                                       *
      *  Beskrivelse . . : Danner Statistikk-PC-register                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  Spesialversjon. :                                              *
      *  Tilpasset for . :                                              *
      *                                                                 *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.*
      *  --------- ------  ---------------------------------------------*
      *  V5.30     080304  Henting av navn kundekategori var feil       *
      *  V5.30     080304  Henting av navn rabattkategori var feil      *
      *  V5.30     200904  Fjernet rabattgruppe                    KOB  *
      *  V5.60     210109  Endret til bruk av kopi av parameter    KOB  *
      *  V5.60     230109  Endret lenge på en del arbeidsfelt      KOB  *
      *  V5.60     210409  Problemer med antallet som benyttes     BSA  *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     fskwwpf    ip   e           k disk
     fskpwpf    if   f  512    15aidisk    keyloc(1)
     frkunl1    if   e           k disk
     frlevl1    if   e           k disk
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     Fvvasl1    if   e           k disk    rename(vvaspfr:vvasl1r)
     fvahgl1    if   e           k disk
     fvaugl1    if   e           k disk
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fvotyl1    if   e           k disk
     fra06pf    if   e           k disk
     fra07pf    if   e           k disk
     fra08pf    if   e           k disk
     fra09l1    if   e           k disk    rename(ra09pfr:ra09l1r)
     fra10pf    if   e           k disk
     fra11pf    if   e           k disk
     fra12pf    if   e           k disk
     ffkprl1    if   e           k disk
     fvltpl1    if   e           k disk
     fskpcpf    o  a e           k disk
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tabeller                                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     D rt              s             25    dim(60) ctdata perrcd(1)
      *
     D w_uhgrp         s              5
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Data Strukturer                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     D                 ds
     D skpwpf_key                    15
     D  w1firm                        3  0 overlay(skpwpf_key:1)
     D  wwtype                       10    overlay(skpwpf_key:4)
     D  wwkode                        2    overlay(skpwpf_key:14)
      *
     D                 ds
     D beg                           15
     D  begnum                       15  0 overlay(beg)
     D   k6                           6  0 overlay(begnum)
      *
     d                 ds
     d wwxgrp                         7  0
     d  wwxogr                        2  0 overlay(wwxgrp:1)
     d  wwxhgr                        2  0 overlay(wwxgrp:3)
     d  wwxugr                        3  0 overlay(wwxgrp:5)
      *
     d                uds
     d lcwsid                901    910
     d lcuser                911    920
     d lcfirm                944    946  0
     d lcnavn                951    980
      *
     d ant2            s              9  2
     d ant4            s              9  2
     d ant4u           s              8  0
     d ant5            s                   like(ant4)
     d ant5u           s                   like(ant4u)
     d ant6            s                   like(ant4)
     d ant6u           s                   like(ant4u)
      *
     d bru2            s             13  2
     d bru4            s             11  0
     d bru5            s                   like(bru4)
     d bru6            s                   like(bru4)
      *
     d rab2            s              9  2
     d rab4            s              8  0
     d rab5            s                   like(rab4)
     d rab6            s                   like(rab4)
      *
     d pra4            s              9  2
     d pra5            s                   like(pra4)
     d pra6            s                   like(pra4)
      *
     d net4            s             11  0
     d net5            s                   like(net4)
     d net6            s                   like(net4)
      *
     d kst2            s             13  2
     d kst4            s             11  0
     d kst5            s                   like(kst4)
     d kst6            s                   like(kst4)
      *
     d dek4            s             11  0
     d dek5            s                   like(dek4)
     d dek6            s                   like(dek4)
      *
     d pde4            s              9  2
     d pde5            s                   like(pde4)
     d pde6            s                   like(pde4)
      *
     d rap             s              2  0
     d rptek           s             30
     d brtt1           s             35
     d brtt2           s             35
     d brtt3           s             35
      *
     d wwfkod          s                   like(rafkod)
     d wwgkod          s                   like(ragkod)
     d wwhkod          s                   like(rahkod)
     d wwikod          s                   like(raikod)
     d wwjkod          s                   like(rajkod)
     d wwkkod          s                   like(rakkod)
     d wwkund          s                   like(rkkund)
     d wwlevr          s                   like(rllevr)
     d wwlkod          s                   like(ralkod)
     d wwotyp          s                   like(vaotyp)
     d wwoogr          s                   like(vgoogr)
     d wwhogr          s                   like(vghogr)
     d wwhhgr          s                   like(vghhgr)
     d wwuogr          s                   like(vguogr)
     d wwuhgr          s                   like(vguhgr)
     d wwuugr          s                   like(vguugr)
     d wwvare          s                   like(vvvare)
     d wwfirm          s                   like(spfirm)
     d wwkpro          s                   like(flkpro)
     d wwlety          s                   like(vylety)
     d w_timestamp     s             12  0
     d w_date          s              6  0
      *
     d level           s              1  0
      *
     i/eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Input section                                                  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     Iskwwpfr       01
     I                                          sffirm        l7
     I                                          swsrt1        l6
     I                                          swsrt2        l5
     I                                          swsrt3        l4
      *
     Iskpwpf    ns  01   14 c0   15 c0
     I                                  1    3 0spfirm
     I                                  4   13  sptype
     I                                 14   15 0spkode
     I                                 31   60  sptext
     I                                 61   62 0rp1
     I                                 63   64 0rp2
     I                                 65   66 0rp3
     I                                 67   67  spes1
     I                                 68   68  spes2
     I                                 69   69  spes3
     I                                 70   70  spes4
     I                                 71   71  spes5
     I                                 73   74 0spmnd
     I                                 75   78 0spaar
     I                                 79   79  spsmli
     I                                 80   80  spsps7
     I                                 81   81  spsss1
     I                                 82   82  spsss2
     I                                 83   83  spsss3
     I                                 84   84  spslet
     *
     I          ns  01   14 c4   15 c1
     I                                  1    3 0spfirm
     I                                  4   13  sptype
     I                                 14   15 0spkode
     I                                 31   60  sptext
     I                                 61   64 0spfaar
     I                                 65   66 0spfmnd
     I                                 67   70 0sptaar
     I                                 71   72 0sptmnd
      *
     C/eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Calculationn                                                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C   l6              do
     C                   eval      ant6 = 0
     C                   eval      bru6 = 0
     C                   eval      rab6 = 0
     C                   eval      kst6 = 0
      *                                                                  .
     C                   move      swsrt1        beg
     C                   eval      rap = rp1
     C                   exsr      raprut
     C                   movel     rptek         brtt1
      *                                                                  .
     C                   enddo
      *
     C   l5              do
     C                   eval      ant5 = 0
     C                   eval      bru5 = 0
     C                   eval      rab5 = 0
     C                   eval      kst5 = 0
     C                   eval      brtt2 = *blank
      *                                                                  .
     C                   if        rp2 <> *zero
     C                   move      swsrt2        beg
     C                   eval      rap = rp2
     C                   exsr      raprut
     C                   movel     rptek         brtt2
     C                   endif
      *                                                                  .
     C                   enddo
      *
     C   l4              do
     C                   eval      ant4 = 0
     C                   eval      bru4 = 0
     C                   eval      rab4 = 0
     C                   eval      kst4 = 0
      *                                                                  .
     C                   eval      ant2 = 0
     C                   eval      bru2 = 0
     C                   eval      rab2 = 0
     C                   eval      kst2 = 0
      *                                                                  .
     C                   eval      brtt3 = *blank
      *                                                                  .
     C                   if        rp3 <> *zero
     C                   move      swsrt3        beg
     C                   eval      rap = rp3
     C                   exsr      raprut
     C                   movel     rptek         brtt3
     C                   endif
      *                                                                  .
     C                   enddo
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Summering av enkeltposter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C                   eval      ant2 = ant2 + sfanta
     C                   eval      bru2 = bru2 + sfsumb
     C                   eval      rab2 = rab2 + sfrab1 + sfrab2 + sfrabo
     C                   eval      kst2 = kst2 + sfsumk
      *
      *
     C     xslutt        tag
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Bruddnivåer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Brudd på L4 (SRT3)
      *
     CL4                 exsr      brdl4
     CL4                 if        swsrt3 <> *blank
     C                             and spes3 = ' '
     CL4                 if        level = 3
     CL4                 add       1             ssline
     CL4                 move      SWSRT1        ssbeg1
     CL4                 move      SWSRT2        ssbeg2
     CL4                 move      SWSRT3        ssbeg3
     CL4                 movel     brtt1         sstxt1
     CL4                 movel     brtt2         sstxt2
     CL4                 movel     brtt3         sstxt3
     C**                 z-add     ant4u         ssanta
     CL4                 z-add     ant4          ssanta
     CL4                 z-add     bru4          ssbrub
     CL4                 z-add     rab4          ssrabb
     CL4                 z-add     pra4          ssrabp
     CL4                 z-add     net4          ssnetb
     CL4                 z-add     kst4          sskstb
     CL4                 z-add     dek4          ssdekb
     CL4                 z-add     pde4          ssdekp
     CL4                 write     skpcpfr
     CL4                 endif
     CL4                 endif
      *
      * BRUDD PÅ L5  (SRT2)
      *
     Cl5                 exsr      brdl5
     Cl5                 if        swsrt2 <> *blank
     C                             and spes2 = ' '
     CL5                 if        level = 2
     CL5                 add       1             ssline
     CL5                 move      SWSRT1        ssbeg1
     CL5                 move      SWSRT2        ssbeg2
     CL5                 move      SWSRT3        ssbeg3
     CL5                 movel     brtt1         sstxt1
     CL5                 movel     brtt2         sstxt2
     CL5                 movel     brtt3         sstxt3
     C**                 z-add     ant5u         ssanta
     CL5                 z-add     ant5          ssanta
     CL5                 z-add     bru5          ssbrub
     CL5                 z-add     rab5          ssrabb
     CL5                 z-add     pra5          ssrabp
     CL5                 z-add     net5          ssnetb
     CL5                 z-add     kst5          sskstb
     CL5                 z-add     dek5          ssdekb
     CL5                 z-add     pde5          ssdekp
     CL5                 write     skpcpfr
     Cl5                 endif
     Cl5                 endif
      *
      * BRUDD PÅ L6  (SRT1)
      *
     Cl6                 exsr      brdl6
      *
     Cl6                 if        swsrt1 <> *blank
     C                             and swsrt2 <> *blank and spes1 = ' '
     cl6                 else
     CL4                 if        level = 1
     CL6                 add       1             ssline
     CL6                 move      SWSRT1        ssbeg1
     CL6                 move      SWSRT2        ssbeg2
     CL6                 move      SWSRT3        ssbeg3
     CL6                 movel     brtt1         sstxt1
     CL6                 movel     brtt2         sstxt2
     CL6                 movel     brtt3         sstxt3
     C**                 z-add     ant6u         ssanta
     CL6                 z-add     ant6          ssanta
     CL6                 z-add     bru6          ssbrub
     CL6                 z-add     rab6          ssrabb
     CL6                 z-add     pra6          ssrabp
     CL6                 z-add     net6          ssnetb
     CL6                 z-add     kst6          sskstb
     CL6                 z-add     dek6          ssdekb
     CL6                 z-add     pde6          ssdekp
     CL6                 write     skpcpfr
     cl6                 endif
     cl6                 endif
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for henting av rett tekst
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     raprut        begsr
      *
     C                   eval      rptek = *blank
      *
      * -----------------
      * Alt. hovedgruppe
      * -----------------
      *
     C                   if        rap = 11
     C                   movel     beg           wwhhgr
     C     hgrkey        chain     vahgl1                             50
     C                   if        *in50 = *off
     C                   movel     vfhtxt        rptek
     C                   endif
     C                   goto      endrut
     C                   endif
      *
      * -----------------
      * Alt. undergruppe
      * -----------------
      *
     C                   if        rap = 12
     C                   movel     beg           w_uhgrp
     C                   movel     w_uhgrp       wwuhgr
     C                   move      w_uhgrp       wwuugr
     C     ugrkey        chain     vaugl1                             50
     C                   if        *in50 = *off
     C                   movel     vfutxt        rptek
     C                   endif
     C                   goto      endrut
     C                   endif
      *
      * ------------------
      * Overgruppe
      * ------------------
      *
     C                   if        rap = 14
     C                   movel     beg           wwxgrp
     C                   eval      wwoogr = wwxogr
     C     vogrl1_key    chain     vogrl1                             50
     C                   if        *in50 = *off
     C                   movel     vgotxt        rptek
     C                   endif
     C                   goto      endrut
     C                   endif
      *
      * ------------------
      * Hovedgruppe
      * ------------------
      *
     C                   if        rap = 15
     C                   movel     beg           wwxgrp
     C                   eval      wwhogr = wwxogr
     C                   eval      wwhhgr = wwxhgr
     C     vhgrl1_key    chain     vhgrl1                             50
     C                   if        *in50 = *off
     C                   movel     vghtxt        rptek
     C                   endif
     C                   goto      endrut
     C                   endif
     *
     * ------------------
     * Undergruppe
     * ------------------
     *
     C                   if        rap = 16
     C                   movel     beg           wwxgrp
     C                   eval      wwuogr = wwxogr
     C                   eval      wwuhgr = wwxhgr
     C                   eval      wwuugr = wwxugr
     C     vugrl1_key    chain     vugrl1                             50
     C                   if        *in50 = *off
     C                   movel     vgutxt        rptek
     C                   endif
     C                   goto      endrut
     C                   endif
     *
     * ---------------
     * Kundekategori
     * ---------------
     *
     C                   if        rap = 21
     C                   movel     beg           wwkkod
     C     ra11pf_key    chain     ra11pf                             50
     C                   if        *in50 = *off
     C                   movel     raktxt        rptek
     C                   endif
     C                   goto      endrut
     C                   endif
     *
     * ---------------
     * Rabattkategori
     * ---------------
     *
     C                   if        rap = 22
     C                   movel     beg           wwfkod
     C     ra06pf_key    chain     ra06pf                             50
     C                   if        *in50 = *off
     C                   movel     raftxt        rptek
     C                   endif
     C                   goto      endrut
     C                   endif
     *
     * ---------------
     * Distrikt
     * ---------------
     *
     C                   if        rap = 23
     C                   movel     beg           wwhkod
     C     ra08pf_key    chain     ra08pf                             50
     C                   if        *in50 = *off
     C                   movel     rahtxt        rptek
     C                   endif
     C                   goto      endrut
     C                   endif
     *
     * ---------------
     * Selger
     * ---------------
     *
     C                   if        rap = 24
     C                   movel     beg           wwikod
     C     ra09l1_key    chain     ra09l1                             50
     C                   if        *in50 = *off
     C                   movel     raitxt        rptek
     C                   endif
     C                   goto      endrut
     C                   endif
     *
     * ---------------
     * Land
     * ---------------
     *
     C                   if        rap = 25
     C                   move      beg           wwlkod
     C     ra12pf_key    chain     ra12pf                             50
     C                   if        *in50 = *off
     C                   movel     raltxt        rptek
     C                   endif
     C                   goto      endrut
     C                   endif
     *
     * ---------------
     * Varenummer
     * ---------------
     *
     C                   if        rap = 31
     C                   movel     beg           wwvare
     C     vvarl1_key    chain     vvarl1                             50
     C                   if        *in50 = *off
     C                   movel     vvtek1        rptek
     C                   endif
B002 C                   if        *in50 = *on
 001 C     vvarl1_key    chain     vvasl1                             50
B002 C                   if        *in50 = *off
 002 C                   movel     vvtek1        rptek
E002 C                   else
 002 C                   movel     sftek1        rptek
E002 C                   endif
E002 C                   endif
     C                   goto      endrut
     C                   endif
     *
     * ---------------
     * Kundenummer
     * ---------------
     *
     C                   if        rap = 32
     C                   movel     k6            wwkund
     C     rkunl1_key    chain     rkunl1                             50
     C                   if        *in50 = *off
     C                   movel     rknavn        rptek
     C                   endif
     C                   goto      endrut
     C                   endif
     *
     * ---------------
     * Kundeprosjekt
     * ---------------
     *
     C                   if        rap = 33
     C                   if        wwkund <> *zeros
     C                   movel     k6            wwkpro
     C     fkprl1_key    chain     fkprl1                             50
     C                   if        *in50 = *off
     C                   movel     flnavn        rptek
     C                   endif
     C                   endif
     C                   goto      endrut
     C                   endif
     *
     * ---------------
     * Periode
     * ---------------
     *
     C                   if        rap = 41
     C                   movel     rt(41)        rptek
     C                   endif
     *
     * ---------------
     * Fakturanr/
     * Kreditnotanr.
     * ---------------
     *
     C                   if        rap = 42
     C                   movel     rt(42)        rptek
     C                   endif
     *
     * ---------------
     * Fakturadato
     * ---------------
     *
     C                   if        rap = 43
     C                   movel     rt(43)        rptek
     C                   endif
     *
     * ---------------
     * Ordretype
     * ---------------
     *
     C                   if        rap = 51
     C                   movel     beg           wwotyp
     C     votyl1_key    chain     votyl1                             50
     C                   if        *in50 = *off
     C                   movel     vaotxt        rptek
     C                   endif
     C                   goto      endrut
     C                   endif
     *
     * ---------------
     * Avdeling
     * ---------------
     *
     C                   if        rap = 52
     C                   movel     beg           wwgkod
     C     ra07pf_key    chain     ra07pf                             50
     C                   if        *in50 = *off
     C                   movel     ragtxt        rptek
     C                   endif
     C                   goto      endrut
     C                   endif
     *
     * ---------------
     * Lager
     * ---------------
     *
     C                   if        rap = 53
     C                   movel     beg           wwjkod
     C     ra10pf_key    chain     ra10pf                             50
     C                   if        *in50 = *off
     C                   movel     rajtxt        rptek
     C                   endif
     C                   goto      endrut
     C                   endif
     *
     * ---------------
     * 1. siffer
     * Kundekategori
     * ---------------
     *
     C                   if        rap = 26
     C                   movel     rt(26)        rptek
     C                   movel     beg           wwkkod
     C     ra11pf_key    chain     ra11pf                             50
     C                   if        *in50 = *off
     C                   movel     raktxt        rptek
     C                   endif
     C                   goto      endrut
     C                   endif
     *
     * ---------------
     * Leveringstype
     * ---------------
     *
     C                   if        rap = 55
     C                   movel     rt(55)        rptek
     C                   movel     beg           wwlety
     C     vltpl1_key    chain     vltpl1                             50
     C                   if        *in50 = *off
     C                   movel     vybesk        rptek
     C                   endif
     C                   goto      endrut
     C                   endif
     *
     * ---------------
     * Leverandørnummer
     * ---------------
     *
     C                   if        rap = 56
     C                   movel     k6            wwlevr
     C     rlevl1_key    chain     rlevl1                             50
     C                   if        *in50 = *off
     C                   movel     rlnavn        rptek
     C                   endif
     C                   goto      endrut
     C                   endif
      *
     C     endrut        endsr
     *
     /EJECT
     * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     * Subrutine for brudd på L4 (SRT3)                                *
     * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     *
     C     brdl4         begsr
     *
     C                   eval(h)   bru4 = bru2
     C                   eval(h)   rab4 = rab2
     C                   eval(h)   kst4 = kst2
     C                   eval(h)   ant4 = ant2
     *
     C                   eval      net4 = bru4 - rab4
     C                   eval      dek4 = net4 - kst4
     *
     C                   if        net4 <> *zero
     c                   select
     C                   when      (dek4 / net4 * 100) < -999,99
     c                   eval      pde4 = -999,99
     C                   when      (dek4 / net4 * 100) >  99,99
     c                   eval      pde4 = 100
     c                   other
     C                   eval(h)   pde4 = dek4 / net4 * 100
     c                   endsl
     C                   endif
     *
     C                   if        bru4 <> *zero
     c                   select
     C                   when      (rab4 / bru4 * 100) < -99,99
     c                   eval      pra4 = -100
     C                   when      (rab4 / bru4 * 100) >  99,99
     c                   eval      pra4 = 100
     c                   other
     C                   eval(h)   pra4 = rab4 / bru4 * 100
     c                   endsl
     C                   endif
     *
     C                   eval      ant5 = ant5 + ant4
     C                   eval      bru5 = bru5 + bru4
     C                   eval      rab5 = rab5 + rab4
     C                   eval      kst5 = kst5 + kst4
     C                   eval      ant4u = ant4
     *
     C                   endsr
     /EJECT
     * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     * Subrutine for brudd på L5 (SRT2)                                *
     * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     *
     C     brdl5         begsr
     *
     C                   eval      net5 = bru5 - rab5
     C                   eval      dek5 = net5 - kst5
     *
     C                   if        net5 <> *zero
     c                   select
     C                   when      (dek5 / net5 * 100) < -999,99
     c                   eval      pde5 = -999,99
     C                   when      (dek5 / net5 * 100) >  99,99
     c                   eval      pde5 = 100
     c                   other
     C                   eval(h)   pde5 = dek5 / net5 * 100
     c                   endsl
     C                   endif
     *
     C                   if        bru5 <> *zero
     c                   select
     C                   when      (rab5 / bru5 * 100) < -99,99
     c                   eval      pra5 = -100
     C                   when      (rab5 / bru5 * 100) >  99,99
     c                   eval      pra5 = 100
     c                   other
     C                   eval(h)   pra5 = rab5 / bru5 * 100
     c                   endsl
     C                   endif
     *
     C                   eval      ant6 = ant6 + ant5
     C                   eval      bru6 = bru6 + bru5
     C                   eval      rab6 = rab6 + rab5
     C                   eval      kst6 = kst6 + kst5
     C                   eval      ant5u = ant5
     *
     C                   endsr
     /EJECT
     * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     * Subrutine for brudd på L6 (SRT1)                                *
     * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     *
     C     brdl6         begsr
     *
     C                   eval      net6 = bru6 - rab6
     C                   eval      dek6 = net6 - kst6
     *
     C                   if        net6 <> *zero
     c                   select
     C                   when      (dek6 / net6 * 100) < -999,99
     c                   eval      pde6 = -999,99
     C                   when      (dek6 / net6 * 100) >  99,99
     c                   eval      pde6 = 100
     c                   other
     C                   eval(h)   pde6 = dek6 / net6 * 100
     c                   endsl
     C                   endif
     *
     C                   if        bru6 <> *zero
     C                   eval      *in36 = bru6 <> 0
     c                   select
     C                   when      (rab6 / bru6 * 100) < -99,99
     c                   eval      pra6 = -100
     C                   when      (rab6 / bru6 * 100) >  99,99
     c                   eval      pra6 = 100
     c                   other
     C                   eval(h)   pra6 = rab6 / bru6 * 100
     c                   endsl
     C                   endif
     *
     C                   eval      ant6u = ant6
     *
     C                   endsr
     /EJECT
     * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     * Subrutine for initiering av program                             *
     * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     *
     C     *inzsr        begsr
     *
     *
     *
     C                   eval      rptek = *blank
     C                   eval      brtt1 = *blank
     C                   eval      brtt2 = *blank
     C                   eval      brtt3 = *blank
     C                   eval      rap = *zero
     *
     C                   eval      net4 = 0
     C                   eval      dek4 = 0
     C                   eval      rab4 = 0
     C                   eval      pde4 = 0
     C                   eval      pra4 = 0
     C                   eval      ant4u = 0
     *
     C                   eval      ant2 = 0
     C                   eval      bru2 = 0
     C                   eval      rab2 = 0
     C                   eval      kst2 = 0
     *
     C                   eval      *in20 = *off
     C                   eval      *in96 = *off
     *
     * Behandle kriterier for valg
     *
     C     *entry        plist
     C                   parm                    dstype           10
     *
     C                   time                    w_timestamp
     C                   move      w_timestamp   w_date
      *
     C                   eval      skpwpf_key = *blank
     *
     C                   move      dstype        sstype
     *
     C                   move      lcfirm        w1firm
     C                   move      lcfirm        wwfirm
     C                   movel     dstype        wwtype
     C                   move      '00'          wwkode
     C     skpwpf_key    chain     skpwpf                             50
      *
      *  Hvilket er laveste nivå
      *
     C                   if        rp1 > 0
     C                   eval      level = 1
     C                   endif
     C                   if        rp2 > 0
     C                   eval      level = 2
     C                   endif
     C                   if        rp3 > 0
     C                   eval      level = 3
     C                   endif
     *
     * Hent periode fra - til
     *
     C                   move      '41'          wwkode
     C     skpwpf_key    chain     skpwpf                             50
     *
     * Key for vareregister
     *
     C     vvarl1_key    klist
     C                   kfld                    wwfirm
     C                   kfld                    wwvare
     *
     * Key for kunderegister
     *
     C     rkunl1_key    klist
     C                   kfld                    wwfirm
     C                   kfld                    wwkund
     *
     * Key for leverandørregister
     *
     C     rlevl1_key    klist
     C                   kfld                    wwfirm
     C                   kfld                    wwlevr
     *
     * Key for kundeprosjekt
     *
     C     fkprl1_key    klist
     C                   kfld                    wwfirm
     C                   kfld                    wwkund
     C                   kfld                    wwkpro
     *
     * Key for alt. hovedgruppe
     *
     C     hgrkey        klist
     C                   kfld                    wwfirm
     C                   kfld                    wwhhgr
     *
     * Key for alt. undergruppe
     *
     C     ugrkey        klist
     C                   kfld                    wwfirm
     C                   kfld                    wwuhgr
     C                   kfld                    wwuugr
     *
     * Key for overgruppe
     *
     C     vogrl1_key    klist
     C                   kfld                    wwfirm
     C                   kfld                    wwoogr
     *
     * Key for hovedgruppe
     *
     C     vhgrl1_key    klist
     C                   kfld                    wwfirm
     C                   kfld                    wwhogr
     C                   kfld                    wwhhgr
     *
     * Key for undergruppe
     *
     C     vugrl1_key    klist
     C                   kfld                    wwfirm
     C                   kfld                    wwuogr
     C                   kfld                    wwuhgr
     C                   kfld                    wwuugr
     *
     * Key for ORDRETYPE
     *
     C     votyl1_key    klist
     C                   kfld                    wwfirm
     C                   kfld                    wwotyp
     *
     * Key for RABATTKATEGORI
     *
     C     ra06pf_key    klist
     C                   kfld                    wwfirm
     C                   kfld                    wwfkod
     *
     * Key for AVDELING
     *
     C     ra07pf_key    klist
     C                   kfld                    wwfirm
     C                   kfld                    wwgkod
     *
     * Key for DISTRIKT
     *
     C     ra08pf_key    klist
     C                   kfld                    wwfirm
     C                   kfld                    wwhkod
     *
     * Key for SELGER
     *
     C     ra09l1_key    klist
     C                   kfld                    wwfirm
     C                   kfld                    wwikod
     *
     * Key for LAGER
     *
     C     ra10pf_key    klist
     C                   kfld                    wwfirm
     C                   kfld                    wwjkod
     *
     * Key for KUNDEKATEGORI
     *
     C     ra11pf_key    klist
     C                   kfld                    wwfirm
     C                   kfld                    wwkkod
     *
     * Key for LAND
     *
     C     ra12pf_key    klist
     C                   kfld                    wwfirm
     C                   kfld                    wwlkod
     *
     * Key for leveringstype
     *
     C     vltpl1_key    klist
     C                   kfld                    wwfirm
     C                   kfld                    wwlety
     *
     C                   endsr
      /EJECT
      **********************  T A B E L L E R  ***************************
**       RAPPORT  TEKSTER
 Ledig                      01
 Ledig                      02
 Ledig                      03
 Ledig                      04
 Ledig                      05
 Ledig                      06
 Ledig                      07
 Ledig                      08
 Ledig                      09
 Ledig                      10
 Alt. hovedgruppe           11
 Alt. undergruppe           12
 Rabattgrp.                 13
 Overgruppe                 14
 Hovedgruppe                15
 Undergruppe                16
 Ledig                      17
 Ledig                      18
 Ledig                      19
 Ledig                      20
 Kundekategori              21
 Rabattkateg.               22
 Distrikt                   23
 Selger                     24
 Land                       25
 Hgr.-kundekat.             26
 Ledig                      27
 Ledig                      28
 Ledig                      29
 Ledig                      30
 Varenummer                 31
 Kunde                      32
 Kundeprosjekt              33
 Ledig                      34
 Ledig                      35
 Ledig                      36
 Ledig                      37
 Ledig                      38
 Ledig                      39
 Ledig                      40
 Periode                    41
 Faktnr/Krednr              42
 Fakturadato                43
 Ledig                      44
 Ledig                      45
 Ledig                      46
 Ledig                      47
 Ledig                      48
 Ledig                      49
 Ledig                      50
 Ordretype                  51
 Avdeling                   52
 Lager                      53
 Produktansvarlig           54
 Leveringstype              55
 Leverandør                 56
 Time                       57
 Ledig                      58
 Ledig                      59
 Ledig                      60
