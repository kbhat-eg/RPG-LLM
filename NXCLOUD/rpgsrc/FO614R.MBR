     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO614R
      * Beskrivelse . . : Behandling av ordre, resting og sortering mm
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
3.11  * R3.11 190199 AMD  Åpnet mulighet for å kunne plukke fra tilbud til
3.11  *                   en ordretype med behandlingskode større enn 0.
3.11  *                   I så fall skapes det en egen ordre med ordrenummer
3.11  *                   som ligger i den serie som benyttes på ordretypen.
3.12  * R3.12 211299 AMD  Liten justering for å løse muligheten for å skrive
3.12  *                   10 CPI. (Foreløpig hardkodet)
3.13  * R3.13 220300 AMD  Leveringstype er tatt med i datastruktur hlrec
3.13  *                   I tillegg legges det ut referanse på 10 posisjoner
3.14  * R3.14 191000 AMD  Lagt inn oppdatering av planlagt leveringsdato
      *       140201 AMD  Hele programmet er lagt om til std. ILE
3.31  * R3.31 140201 AMD  Hardkoding av hvilke program som skal kalles ved
      *                   alternativer på ordre (plukkliste) og tilbud er
      *                   fjernet.  Nå styres alt fra rutineregister.
      *                   Se notat i AAINFO330.
      * R3.32 280201 AMD  Ved oppdatering av historisk lagerbok, i forbind-
      *                   else med fakturering, benyttes nå fakturadato og
      *                   ikke dagens dato.
3.33  * R3.33 050301 AMD  Bygget inn styringen fra info-register, dersom
      *                   ordre skal stoppes før fakturering.
3.34  * R3.34 070301 AMD  Begrensning på hvem som kan kjøre faktureringm
3.35  * R3.35 030401 AMD  Splitter på separate faktura dersom fakturadato
      *                   eller forfallsdato er overstyrt.
3.36  * R3.36 261001 AMD  Firmanummer ble ikke lagt inn i nøkkel for
3.36  *                   bruker-register (FUSRPF)
5.01  * R5.01 080502 AMD  Kontroll på om pakkseddel allerede er skrevet.
5.01  *                   Dersom ja, sjekkes det om bruker har lov til å
5.01  *                   foreta gjenutskrift av pakkseddelen
5.01  * R5.01 080502 AMD  Oppdatering av kode som sier at pakkseddel er
5.01  *                   skrevet dersom pakkseddel kjøres
5.02  * R5.02 241002 AMD  Lagt om rutiner for oppdatering av lager
5.03  * R5.03 300403 AMD  Lagt inn ny logikk for styring av hvordan
5.03  *                   ordre skal legges ut ved fakturering:
5.03  *                    0 - Faktura m/flere ordre men brudd på
5.03  *                        leveringsadresse teksten  (som idag)
5.03  *                    1 - En faktura pr. ordre      (som idag)
5.03  *                    2 - En faktura dersom kundenr.      (ny)
5.03  *                        og prosjektnummer er likt
5.03  *                    3 - En faktura dersom kundenr.      (ny)
5.03  *                        og prosjektnummer er likt, men
5.03  *                        egen faktura v/leveringsadresse
5.03  *                    4 - En faktura på kunden uansett    (ny)
5.03  *                        om forskjellig prosjekt/lev.adr.
5.04  * R5.04 230503 AMD  Lagt inn egne rutinenavn for e-mail, da det vil
5.04  *                   være behov for å få med firmanavn ut på mailen.
5.04  *                   (se beskrivelse under AAINFO510)
5.21  * R5.21 200803 AMD  Lagt om til AP600R i stedet for AP100R
5.22  * R5.22 270803 AMD  Få med overstyrt utkø fra OFAK inn i
5.22  *                   bilde for valg av utskrift (AP600R)
5.23  * R5.23 101003 AMD  Oppdatering av bruker/skjerm/dato/klokkeslett
5.23  *                   lagt inn på nyskapt ordrehode
5.24  * R5.24 131003 AMD  Lagt inn automatisk brudd slik at forskjellig
5.24  *                   betalingsbetingelse ikke havner på samme faktura
5.31  * R5.31 100104 ASK  Lagt inn endringer for lager på linjenivå.
      *                   Dette er en midlertidig løsning da
      *                   overstyring ved utplukk er sperret.
5.32  * R5.32 080904 AMD  Lagt inn ordretype for nummerserie (se dok)
5.33  * R5.33 250804 GRO  Endring av sorteringsbegrep (kortnr)
5.41  * R5.41 160605 AMD  Endring av kontroll på skrevet pakkseddel
5.42  * R5.42 050705 AMD  Tar ikke med ordren dersom det ikke finnes
5.42  *                   noen varelinjer på den. (Skapte problemer i
5.42  *                   faktureringen.)
5.43  * R5.43 250905 AMD  Ved utplukk av ordre skal bruker fra opp-
5.43  *                   rinnelig ordre beholdes (endret tilbake)
5.44  * R5.44 190106 AMD  Ved flere firma i samme fil, ble alle poster
      *                   tatt med dersom man sto inne på firmanummer
      *                   med lavest verdi.
5.51  * R5.51 261005 AMD  Ved resting skal restordre merkes med en
5.51  *                   kode som sier at den er restet
5.52  * R5.52 100307 KOB  Lagt inn nytt valg ved utskrift av tilbud
5.53  * R5.53 230307 AMD  Utvidet sorteringsbegrep slik at både Navn og
      *                   Adresse 1 kan skape brudd
5.54  * R5.54 151107 BOF  Hvis plukkliste så slette evt. RT.plukk.reg.
5.55  * R5.55 161107 BOF  Lagt inn oppd. av logg-koder ved resting
5.56  * R5.56 211107 BOF  Justert oppd.logg-koder generelt
5.60  * R5.60 091107 AMD  Lagt inn løsning for kontroll av grense for
      *                   almenningsrabatter
5.61  * R5.61 080108 BOF  Utvidet logg med år
5.63  * R5.63 150208 AMD  Lagt inn logging av slettede ordrelinjer
5.64  * R5.64 220508 BOF  Justert oppd.av loggkode = 2
5.65  * R5.65 230508 AMD  Lagt inn løsning for depositum-behandling
5.66  * R5.66 270508 BOF  Endr.i forb. med "avrop" på ordre (valg 16)
5.67  * R5.67 200608 BOF  Tester om kjørekontor er valgt før oppd.kode
5.68  * R5.68 230608 AMD  Tilbud kun summer hadde ikke rutine for epost
      * 6.10  240609 BSA  Oppdatert med sporingsinformasjon
      * 6.10  220909 ASK  Nye parametre til VL001R
6.20  * 6.20  260511 bof  Oppdater pakks.dato i stedet for å endre lev.dato
6.21  * 6.20  250412 AMD  Dersom direkte utkjøring er valgt, skal ikke
6.21  *                   faktura over til e-print
6.22  * R6.20 140112 bof  Mulighet til å legge ut ByggDok innsamling
6.23  * R6.20 040113 bof  Henter byggdok - kode fra FKPRPF
6.24  * R6.20 041114 bof  Ekstra test før DELETE
6.25  * R6.20 270415 bof  oprettet dato skal ikke oppdateres
6.26  * R6.20 280915 BOF  Lagt inn ryddeprogram ved oppr.av ordre
6.27  * R6.20 060116 bsa  Leveringsdato skal overleve også på 00 suffiks.
6.nu  * R6.20 050614 bof  Utvidelse vedr. nummerutvidelse
6.31  * 6.30  120914 BKV  Henter overstyrt info fra kundeprosjekt
6.32  * 6.30  221014 BKV  Søtte for cobuilder
6.33  * 6.30  040615 bkv  Ordre økt fra 6 til 8
6.34  * 6.30  290616 bof  Rettelse vedr. Cobuilder
6.35  * 6.30  050716 bof  Byggdok/Cob. skal evt. ta mailadr. fra kundeprosjekt
6.36  * 6.30  041218 ask  Kostprisføring på utgående pakkseddel
      *
7.01  * K000  240220 bof  Cobuilder, men ikke krav til nobbnummer
7.02  * K000  061218 sst  Hente ut tidligere fakt. v/sluttfakt fra HO
7.03  *       260521 ask  Fjernet test på o
      *
8.00  * K00   150323 sst  Beregne Memosaldo m/FO763R
8.01  * K000  170723 bsa  Fakturere både kreditnota og faktura sammen.
8.02  * K000  280524 bsa  Hvis kunde ikke skal splittes, endrer vi i
8.02  * K000              sorteringsbegrep.
8.03  *       140524 ask  Fjernet skriv til ByggDok
8.10  *K000   240924 bsa  Hvis nytt sufiks, nullstill eksternref hvis kode AOR.
8.11  *K000   070425 mst  NXS-21039 Hvis valgt F3('Gå tilbake'), ingen utskrift
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffpm2lu    ip   e           k disk    rename(fpm2pfr:fpm2lur)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     ffohelu    uf a e           k disk    rename(fohepfr:fohelur)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffodtlu    uf a e           k disk    rename(fodtpfr:fodtlur)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
5.42 ffodtlr    if   e           k disk    rename(fodtpfr:fodtlrr)
     ffotxl1    if   e           k disk    rename(fotxpfr:fotxl1r)
     ffplol1    if   e           k disk    rename(fplopfr:fplol1r)
     ffplolu    uf   e           k disk    rename(fplopfr:fplolur)
     ffplllu    uf   e           k disk    rename(fpllpfr:fplllur)
     ffpsolu    uf a e           k disk    rename(fpsopfr:fpsolur)
3.33 fvinfl1    if   e           k disk    rename(vinfpfr:vinfl1r)
3.34 ffusrl1    if   e           k disk    rename(fusrpfr:fusrl1r)
5.33 ffntrl1    if   e           k disk    rename(fntrpfr:fntrl1r)
5.33 ffmkal1    if   e           k disk    rename(fmkapfr:fmkal1r)
5.54 flwpllu    uf   e           k disk    rename(lwplpfr:lwpllur)
5.65 fsdepl1    if   e           k disk    rename(sdeppfr:sdepl1r)
6.22 fafpslr    if   e           k disk    rename(afpspfr:afpslrr)
6.23 ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
6.22 frukpl6    if   e           k disk    rename(rukppfr:rukpl6r)
6.22 famodl1    if   e           k disk    rename(amodpfr:amodl1r)
8.02 frku2l1    if   e           k disk    rename(rku2pfr:rku2l1r)
7.02 fsodtpf    if   e           k disk    rename(sodtpfr:sodtpfr)
6.22 ffbdopf    o  a e             disk
      *
      * Definering av data-struktur
      * for lageroppdatering
      *
     d                 ds
6.nu d hlrec                        128
     d  hlvare                       15    overlay(hlrec)
     d  hllage                        2  0 overlay(hlrec:16)
     d  hldato                         d   overlay(hlrec:18) datfmt(*iso)
     d  hltime                         t   overlay(hlrec:28) timfmt(*iso)
     d  hlotyp                        2    overlay(hlrec:36)
     d  hlltyp                        2    overlay(hlrec:38)
     d  hlanta                       11  3 overlay(hlrec:40)
     d  hlkopr                        9  2 overlay(hlrec:51)
     d  hlrefr                       20    overlay(hlrec:60)
     d  hlsyst                        1    overlay(hlrec:80)
     d  hlaltk                        2    overlay(hlrec:81)
     d  hlrest                        1    overlay(hlrec:83)
     d  hltype                        1    overlay(hlrec:84)
     d  hlenhe                        3    overlay(hlrec:85)
     d  hlinlr                        1    overlay(hlrec:88)
     d  hllety                        1    overlay(hlrec:89)
6.nu d  hlonum                        8  0 overlay(hlrec:90)
6.nu d  hlolin                        4  0 overlay(hlrec:98)
      *
      * Definering av data-struktur
      * for sorteringsbegrep
      *
     d                 ds
6.nu d sorter                       114
     d  sor001                        8  0 overlay(sorter)
5.53 d  sor002                       60    overlay(sorter:9)
5.53 d  sor003                       16    overlay(sorter:69)
5.53 d  sor004                        3    overlay(sorter:85)
5.53 d  sor005                       17    overlay(sorter:88)
6.nu d  sor006                        8  0 overlay(sorter:105)
6.nu d  sor007                        2  0 overlay(sorter:113)
5.55  *
5.55 d                 ds
5.55 d d_urec                        80
5.55 d  d_ufir                        3  0 overlay(d_urec)
5.61 d  d_aarr                        4  0 overlay(d_urec:4)
6.nu d  d_unum                        8  0 overlay(d_urec:8)
6.nu d  d_usuf                        2  0 overlay(d_urec:16)
6.nu d  d_ukod                        1    overlay(d_urec:18)
6.nu d  d_date                         d   overlay(d_urec:19) datfmt(*iso)
6.nu d  d_time                         t   overlay(d_urec:29) timfmt(*iso)
6.nu d  d_user                       10    overlay(d_urec:37)
6.nu d  d_wsid                       10    overlay(d_urec:47)
      *
      *
      * Definering av local data area
      *
     d lda            uds
     d l_alt1                540    540  0
     d l_alt2                541    541  0
     d l_peri                561    566  0
     d l_dato                567    576d   datfmt(*iso)
      *
     d l_ruti                800    809
     d l_outq                810    819
     d l_acpi                830    833  0
     d l_chgv                844    844
     d l_prog                871    880
5.22 d l_outm                899    899
     d l_wsid                901    910
3.34 d l_user                911    920
6.22 d l_filg                931    933
3.36 d l_firm                944    946  0
8.01 d l_otyp               1023   1024
      *
      * Definering av variable i nøkler
      *
     d w_firm          s                   like(fofirm)
     d rkunl1_kund     s                   like(rkkund)
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d fodtl1_numm     s                   like(fdnumm)
     d fodtl1_suff     s                   like(fdsuff)
     d fodtl1_line     s                   like(fdline)
5.42 d fodtlr_numm     s                   like(fdnumm)
5.42 d fodtlr_suff     s                   like(fdsuff)
5.42 d fodtlr_line     s                   like(fdline)
     d votyl1_otyp     s                   like(vaotyp)
3.33 d vinfl1_ityp     s                   like(vaityp)
3.34 d fusrl1_user     s                   like(fbuser)
     d fplllu_lonr     s                   like(fklonr)
     d fplllu_lsuf     s                   like(fklsuf)
     d fplllu_llin     s                   like(fkllin)
     d fplolu_ponr     s                   like(fkponr)
     d fplolu_psuf     s                   like(fkpsuf)
     d fpsolu_slin     s                   like(fkslin)
     d fpsolu_sonr     s                   like(fksonr)
     d fpsolu_ssuf     s                   like(fkssuf)
     d fpsolu_stll     s                   like(fkstll)
     d rohel1_numm     s                   like(fonumm)
     d rohel1_suff     s                   like(fosuff)
     d fotxl1_numm     s                   like(ftnumm)
     d fotxl1_suff     s                   like(ftsuff)
     d fotxl1_line     s                   like(ftline)
5.33 d fntrl1_numm     s                   like(fnnumm)
5.33 d fntrl1_suff     s                   like(fnsuff)
5.33 d fmkal1_kund     s                   like(fmkund)
5.33 d fmkal1_kpro     s                   like(fmkpro)
5.33 d fmkal1_kftp     s                   like(fmkftp)
5.33 d fmkal1_ktsq     s                   like(fmktsq)
5.54 d lwpllu_numm     s                   like(mknumm)
5.54 d lwpllu_suff     s                   like(mksuff)
5.65 d sdepl1_numm     s                   like(sknumm)
5.65 d sdepl1_kund     s                   like(skkund)
6.23 d fkprl1_kund     s                   like(flkund)
6.23 d fkprl1_kpro     s                   like(flkpro)
6.22 d amodl1_modu     s                   like(axmodu)
6.22 d afpslr_ufil     s                   like(l_filg)
6.22 d afpslr_uide     s                   like(afuide)
6.22 d rukpl6_kund     s                   like(rukund)
      *
      *
3.11  * Definering av variable
3.11  *
3.11 d w_kode          s              1
3.11 d w_fell          s              6
3.11 d w_type          s              2
3.11 d w_fast          s             10
3.11 d w_numm          s                   like(fonumm)
3.11  *
6.nu d w_lrec          s            128
     d w_tonr          s                   like(fkponr)
     d w_tsuf          s                   like(fkpsuf)
     d w_otyp          s                   like(footyp)
     d w_otyp_sav      s                   like(footyp)
     d w_lage          s                   like(folage)
     d w_lage_sav      s                   like(folage)
     d w_date          s                   like(foldat)
     d w_oakk          s                   like(vaoakk)
     d w_rest          s                   like(fdanta)
     d w_test          s              1  0
     d w_enor          s              1
5.21 d w_in03          s              1
5.21 d w_tvbt          s              1
5.21 d w_btch          s              1
     d w_ruti          s             10
     d w_sald          s             11  2
5.60 d w_sal2          s             11  2
     d w_stat          s              1
3.35 d w_fkda          s              6  0
3.35 d w_ffda          s              6  0
5.02 d w_olag          s                   like(foolag)
5.32 d w_onum          s                   like(vaonum)
5.63 d w_spgm          s             10
5.67 d w_aarr          s              4  0
5.67 d w_kod2          s              1
5.67 d w_sva2          s              1
5.67 d w_lnum          s                   like(fonumm)
5.67 d w_lsuf          s                   like(fosuff)
6.33 d w_numa          s              8
5.02 d w_lina          s              4
6.22 d w_btms          s               Z
6.22 d w_ti26          s             26
6.22 d w_bnum          s              6
6.31 d w_samf          s                   like(rksamf)
6.36 d w_disp          s              1
6.36 d w_tilg          s              1
6.36 d w_modu          s             10
      *
5.66 d b_dell          s              1    inz(*off)
5.67 d b_kjko          s              1    inz(*off)
6.22 d b_bdo1          s              1    inz(*off)
6.22 d b_bdo2          s              1    inz(*off)
6.22 d b_bdo3          s              1    inz(*off)
6.22 d b_bdo4          s              1    inz(*off)
6.22 d b_kper          s              1    inz(*off)
8.01 d b_fakt          s              1    inz(*off)
      *
5.55 d p_urec          s             80
5.55 d p_stat          s              1
8.00 d p_firm          s              3
8.00 d p_kund          s              6
6.22  *
6.22  * Definering av konstanter
6.22  *
6.22 d lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
6.22 d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
      *
7.01 d u_s701          s              1    inz(*off)                            Klient
7.01  *
7.01  * Definering av eksterne prg
7.01  *
7.01   dcl-s co402_verdi1  char(1);
7.01   dcl-s co402_verdi2  char(2048);
7.01   DCL-PR CO402R EXTPGM('CO402R');
7.01     p_filgrp            char(3)     const;
7.01     p_firm              packed(3:0) const;
7.01     p_lager             packed(2:0) const;
7.01     p_nokkel            char(50)    const;
7.01     p_verdi1            char(1);
7.01     p_verdi2            char(2048);
7.01   END-PR;
7.01  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Beskrivelse av spesielle felter som er benyttet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  w_test  Benyttes til og sjekkke om det er linjer igjen
      *  (0,1)   på ordren etter at den er restet. Hvis det ikke
      *          er varelinjer igjen på ordren slettes heading-record
      *
      *
      *  Hent opplysninger fra OFAK-KODE-REGISTER
      *
     c                   eval      w_firm = fk2fir
     c     fstsl1_key    chain     fstsl1r                            90
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Utplukk av ordre til utskrift og oppdatering
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Legger inn verdier fra PLUKK-2-LINJE-REGISTER (Parametre)
      * Dersom ett ordrenummer er valgt vil fra/til være like
      *
     c                   eval      w_firm      = fk2fir
     c                   eval      fplolu_ponr = fk2onr
     c                   eval      fplolu_psuf = fk2suf
     c                   eval      w_tonr      = fk2onr
     c                   eval      w_tsuf      = fk2suf
      *
6.nu  * Legger inn 99999999/99 som sluttverdi
      * dersom sluttverdi ikke er utfylt
      *
     c                   if        w_tonr = *zero
6.nu c                   eval      w_tonr = 99999999
     c                   eval      w_tsuf = 99
     c                   endif
      *
      * Setter start for lesing i UTPLUKK-PARAM-REGISTER
      *
     c     fplol1_key    setll     fplol1r
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av utplukk
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      * Leser record fra UTPLUKK-PARAM-REGISTER
      *
     c     xstart        tag
     c                   read      fplol1r                                90
     c     *in90         cabeq     *on           xslutt
      *
      * Avslutter dersom ordrenummer lest
      * er større en testverdi
      *
     c     fkponr        cabgt     w_tonr        xslutt
      *
      * Avslutter dersom ordrenummer
      * og suffix er for stort
      *
     c                   if        fkponr = w_tonr
     c     fkpsuf        cabgt     w_tsuf        xslutt
     c                   endif
5.44  *
5.44  * Sjekk, Dersom uaktuelt firma
5.44  *
5.44 c                   if        fkpfir <> l_firm
5.44 c                   goto      xstart
5.44 c                   endif
      *
      * Sjekk, ordretype plukket er lik
      * ordretype fra parameter
      *
8.01  * Hvis ordretype 99, sjekk om lest ordretype er av kode 3. Da skal den
8.01  * godkjennes.
      *
8.01 c                   if        fk2oty = '99'
8.01 c                   eval      b_fakt = *on
8.01 c                   eval      votyl1_otyp = fkpoty
8.01 c     votyl1_key    chain     votyl1r
8.01 c                   if        %found and
8.01 c                             vaoakk <> 3
8.01 c                   goto      xstart
8.01 c                   endif
8.01 c                   else
     c     fkpoty        cabne     fk2oty        xstart
8.01 c                   endif
      *
      * Sjekk, Dersom ordre bare fra denne skjerm
      *
     c                   if        fk2vg1 = 1
     c     fkpwsd        cabne     fk2wsd        xstart
     c                   endif
      *
      * Sjekk, Dersom ordre bare for denne bruker
      *
     c                   if        fk2vg2 = 1
     c     fkpusr        cabne     fk2usr        xstart
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Ett ordrenummer er funnet som skal plukkes ut
      * Dette ordrenummer skal behandles helt ferdig før neste
      * ordrenummer hentes fra UTPLUKK-PARAM-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Legger inn firmanummer i nøkklene
      *
     c                   eval      w_firm = fkpfir
      *
      * Legger inn ordrenummer og suffix i nøkklene
      *
3.11 c                   eval      rohel1_numm = fkponr
     c                   eval      fohel1_numm = fkponr
     c                   eval      fohel1_suff = fkpsuf
      *
     c                   eval      fodtl1_numm = fkponr
     c                   eval      fodtl1_suff = fkpsuf
     c                   eval      fodtl1_line = *zero
      *
     c                   eval      fotxl1_numm = fkponr
     c                   eval      fotxl1_suff = fkpsuf
     c                   eval      fotxl1_line = *zero
      *
     c                   eval      fplolu_ponr = fkponr
     c                   eval      fplolu_psuf = fkpsuf
      *
     c                   eval      fplllu_lonr = fkponr
     c                   eval      fplllu_lsuf = fkpsuf
     c                   eval      fplllu_llin = *zero
      *
     c                   eval      fpsolu_sonr = fkponr
     c                   eval      fpsolu_ssuf = fkpsuf
      *
      * Legger inn ordretype og alternativ i PLUKK-S-LINJE-felter
      *
     c                   eval      fksoty = fkpoty
     c                   eval      fksalt = fkpalt
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Bygger opp standardverdier for denne utskriften
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      * Hent opplysninger fra ORDRETYPE-REGISTER
      *
8.01 c                   if        fk2oty <> '99'
     c                   eval      votyl1_otyp = fk2oty
     c     votyl1_key    chain     votyl1r                            90
     c                   eval      w_ruti = vaorut
     c                   eval      w_oakk = vaoakk
8.01 c                   else
8.01 c                   eval      w_ruti = 'FFAKT'
8.01 c                   eval      w_oakk = 3
8.01 c                   endif
      *
      * Legg inn verdier for henting av utskrifts-opplysninger
      *
     c                   eval      l_chgv = *blank
     c                   eval      l_ruti = w_ruti
3.31  *
3.31  * Er det valgt alternativt tilbud, overstyr rutine
3.31  *
3.31 c                   if        vaoakk = 0
3.31  *
3.31 c                   if        fkpalt = 1
3.31 c                   eval      l_ruti = %trim(w_ruti) + '1'
3.31 c                   endif
3.31  *
3.31 c                   if        fkpalt = 2
3.31 c                   eval      l_ruti = %trim(w_ruti) + '2'
3.31 c                   endif
3.31  *
3.31 c                   if        fkpalt = 3
3.31 c                   eval      l_ruti = %trim(w_ruti) + '3'
3.31 c                   endif
3.31  *
5.52 c                   if        fkpalt = 4
5.52 c                   eval      l_ruti = %trim(w_ruti) + '4'
5.52 c                   endif
5.52  *
3.31 c                   endif
3.31  *
3.31  * Er det valgt plukkliste fra ordre, overstyr rutine
3.31  *
3.31 c                   if        vaoakk = 1
3.31 c                   if        fkpalt = 1
3.31 c                   eval      l_ruti = %trim(w_ruti) + '1'
3.31 c                   endif
3.31 c                   endif
3.31  *
3.31  * Er det valgt plukkliste fra ordre, overstyr rutine
3.31  *
3.31 c                   if        vaoakk = 1
3.31 c                   if        fkpalt = 1
3.31 c                   eval      l_ruti = %trim(w_ruti) + '1'
3.31 c                   endif
3.31 c                   endif
      *
5.04  *
5.04  * Er det valgt mail, overstyr rutine  (Tilbud)
5.04  *
5.04 c                   if        vaoakk = 0
5.04 c                   if        fk2utq = 'E_MAIL_ASP'
5.04  *
5.04 c                   if        fkpalt = 0
5.04 c                   eval      l_ruti = %trim(w_ruti) + '5'
5.04 c                   endif
5.04  *
5.04 c                   if        fkpalt = 1
5.04 c                   eval      l_ruti = %trim(w_ruti) + '6'
5.04 c                   endif
5.04  *
5.04 c                   if        fkpalt = 2
5.04 c                   eval      l_ruti = %trim(w_ruti) + '7'
5.04 c                   endif
5.04  *
5.04 c                   if        fkpalt = 3
5.04 c                   eval      l_ruti = %trim(w_ruti) + '8'
5.04 c                   endif
5.68  *
5.68 c                   if        fkpalt = 4
5.68 c                   eval      l_ruti = %trim(w_ruti) + '9'
5 68 c                   endif
5.04  *
5.04 c                   endif
5.04 c                   endif
5.04  *
5.04  * Er det valgt mail, overstyr rutine  (Ordrebekr/plukkliste)
5.04  *
5.04 c                   if        vaoakk = 1
5.04 c                   if        fk2utq = 'E_MAIL_ASP'
5.04  *
5.04 c                   if        fkpalt = 0
5.04 c                   eval      l_ruti = %trim(w_ruti) + '5'
5.04 c                   endif
5.04  *
5.04 c                   if        fkpalt = 1
5.04 c                   eval      l_ruti = %trim(w_ruti) + '6'
5.04 c                   endif
5.04  *
5.04 c                   endif
5.04 c                   endif
5.22  *
5.22  * Dersom OUTQ er overstyrt manuelt,
5.22  * sett kode som varsler dette i AP600r
5.22  *
5.22 c                   if        fk2utq <> *blank
5.22 c                   eval      l_outq = fk2utq
5.22 c                   eval      l_outm = '1'
5.22 c                   endif
      *
      * Oppdaterer LDA
      *
     c                   out       *dtaara
      *
      * Kaller opp program før utskrift
      *
5.21 c                   call      'AP600R'
5.21 c                   parm                    l_ruti
5.21 c                   parm                    l_chgv
5.21 c                   parm                    w_tvbt
5.21 c                   parm                    w_in03
5.21 c                   parm                    w_btch
      *
8.11 c                   if        w_in03 = '1'
8.11 c                   goto      xslutt
8.11 c                   endif
      *
      * Henter inn igjen LDA
      *
     c                   in        *dtaara
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av de enkelte utskrifter/formularer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Legger inn verdi for at ordre/ordrelinjer er valgt
      *
     c                   eval      l_alt1 = 1
      *
      * Faktura/Kreditnota :
      *
     c                   if        vaoakk = 3
     c                   eval      l_alt1 = 2
     c                   move      fk2per        l_peri
     c                   move      fk2dat        l_dato
     c                   endif
5.22  *
5.22  * Tilbakestill kode for manuelt overstyrt OUTQ
5.22  *
5.22 c                   eval      l_outm = ' '
8.01  *
8.01  * Samle fakturering faktura/kreditnota
8.01  *
8.01 c                   if        b_fakt = *on
8.01 c                   eval      l_otyp = '99'
8.01 c                   endif
      *
      * Oppdaterer LDA
      *
     c                   out       *dtaara
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Arbeider med opplysninger i tilknyttning til denne ordren
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      * Henter inn opplysninger fra ORDRE-HODE-REGISTER
      *
     c     fohel1_key    chain     fohel1r                            90
     c                   eval      w_otyp_sav = footyp
     c                   eval      w_lage_sav = folage
5.02 c                   eval      w_olag     = foolag
6.22  *
6.22  * Sjekker om kundeprosjekt skal legge ut ByggDok
6.22  *
6.22 c                   eval      b_bdo2 = *off
6.22 c                   eval      b_bdo4 = *off
6.22 c                   if        b_bdo1 = *on
6.23 c                   eval      fkprl1_kund = fokund
6.23 c                   eval      fkprl1_kpro = fokpro
6.23 c     fkprl1_key    chain     fkprl1r
6.34 c                   if        %found(fkprl1)
6.34 c                   if        flbdok = 1 or
6.34 c                             flcobu = 1
6.22 c                   time                    w_btms
6.22 c                   eval      b_bdo2 = *on
6.34 c                   endif
6.22 c                   endif
6.22 c                   endif
6.22 c                   eval      b_bdo3 = *off
6.22 c                   if        vaoakk = 2 and
6.22 c                             vaokre = *blank
6.22 c                   eval      b_bdo3 = *on
6.22 c                   endif
      *
      * Hopper forbi denne ordre dersom OBEK ikke skal skrives ut
      *
     c                   if        vaoakk = 1
     c     foksob        cabeq     1             tag300
     c                   endif
      *
      * Hopper forbi denne faktura dersom fakturadato på ordre
      * er større enn periodens fakturadato (oppgitt i bildet)
      *
     c                   if        vaoakk = 3
     c                   if        fofkda <> *loval
     c                   if        fofkda > l_dato
     c                   goto      xstart
     c                   endif
     c                   endif
     c                   endif
5.42  *
5.42  * Hopper forbi denne ordren dersom det ikke finnes noen
5.42  * varelinjer på den.
5.42  *
5.42 c                   if        vaoakk = 3
5.42 c                   eval      fodtlr_numm = fonumm
5.42 c                   eval      fodtlr_suff = fosuff
5.42 c                   eval      fodtlr_line = 0
5.42 c     fodtlr_key    setll     fodtlrr
5.42 c                   read      fodtlrr                                90
5.42 c                   if        %eof
5.42 c                   goto      xstart
5.42 c                   endif
5.42 c                   if        fonumm = fdnumm
5.42 c                   if        fosuff = fdsuff
5.42 c                   else
5.42 c                   goto      xstart
5.42 c                   endif
5.42 c                   endif
5.42 c                   endif
3.33  *
3.33  * Hent inn ordre-info-kode i inforegister
3.33  *
3.33 c                   eval      vinfl1_ityp = foityp
3.33 c     vinfl1_key    chain     vinfl1r                            98
3.33 c                   if        *in98  = *on
3.33 c                   eval      vaifak = *blank
3.33 c                   endif
      *
3.33  * Dersom ordre har en infokode, og dette er fakturering,
3.33  * sjekkes det om fakturering skal stoppes.
      *
     c                   if        foityp <> *blank
     c                   if        vaoakk = 3
3.33 c                   if        vaifak = '1'
     c                   goto      xstart
     c                   endif
     c                   endif
3.33 c                   endif
3.34  *
3.34  *  Kontroll om bruker har lov til å sette igang fakturering.
3.34  *
3.34 c                   if        fbko08 <> 1
3.34 c                   if        vaoakk =  3
3.34 c                   goto      xstart
3.34 c                   endif
3.34 c                   endif
5.01  *
5.01  *  Kontroll på om pakkseddel allerede er skrevet
5.41  *  + sjekk mot bruker om gjenutskrift er tillatt
5.01  *
5.41 c                   if        fbko09 <> 1
5.01 c                   if        fopaks =  1
5.01 c                   if        vaoakk =  2
5.01 c                   goto      xstart
5.01 c                   endif
5.01 c                   endif
5.41 c                   endif
      *
      * Legger inn sorteringsbegrep fra ordre-register
      *
     c                   eval      sorter = *blank
     c                   eval      sor001 = *zero
5.33 c                   eval      sor006 = *zero
5.33 c                   eval      sor007 = *zero
      *
     c                   move      fokund        sor001
5.33 c                   move      fkponr        sor006
5.33 c                   move      fkpsuf        sor007
5.53 c                   eval      sor002 = fonavn + foadr1
      *
      * Oppsamling av flere ordre på samme blankett
      *
     c                   if        vaosam = 1
      *
5.33 c                   eval      sor006 = *zero
5.33 c                   eval      sor007 = *zero
      *
      * Overstyring av samlefaktura
      *
     c                   if        vaoakk = 3
      *
      * Hent opplysninger fra KUNDE-REGISTER
      *
     c                   eval      rkunl1_kund = fokund
     c     rkunl1_key    chain     rkunl1                             90
      *
8.02 c     rkunl1_key    chain     rku2l1
8.02 c                   if        not %found
8.02 c                   eval      rk2fas = 0
8.02 c                   endif
      *
6.31 c                   eval      w_samf = rksamf
      *
6.31 c                   eval      fkprl1_kund = fokund
6.31 c                   eval      fkprl1_kpro = fokpro
6.31 c     fkprl1_key    chain     fkprl1r
6.31 c                   if        %found(fkprl1)
6.31 c                   eval      w_samf = flsamf
6.31 c                   endif

5.65  *
5.65  * Dersom dette er en ordre som har depositum,
5.65  * skal det bare være en faktura pr. ordre
5.65  *
5.65 c                   eval      sdepl1_numm = fonumm
5.65 c                   eval      sdepl1_kund = fokund
5.65 c     sdepl1_key    chain     sdepl1r
5.65 c                   if        %found
6.31 c                   eval      w_samf = 1
5.65 c                   endif
      *
      * En faktura pr. ordre
      *
6.31 c                   if        w_samf = 1
5.33 c                   move      fkponr        sor006
5.33 c                   move      fkpsuf        sor007
     c                   endif
5.03  *
5.03  * Flere ordre pr. faktura dersom
5.03  * prosjektnummer er det samme
5.03  *
6.31 c                   if        w_samf = 2
5.03 c                   move      *blank        sor002
5.03 c                   movel     fokpro        sor002
5.03 c                   endif
5.03  *
5.03  * Flere ordre pr. faktura dersom
5.03  * prosjektnummer er det samme, men ...
5.03  * egen faktura dersom oppgitt leveringsadresse.
5.03  *
6.31 c                   if        w_samf = 3
5.03 c                   move      *blank        sor002
5.03 c                   movel     fokpro        sor002
5.03 c                   if        fokpro = 0
5.53 c                   eval      sor002 = fonavn + foadr1
5.03 c                   endif
5.03 c                   endif
5.03  *
5.03  * Flere ordre pr. faktura uansett om
5.03  * leveringsadresse/prosjekt er forskjellig
5.03  *
6.31 c                   if        w_samf = 4
5.03 c                   eval      sor002 = *blank
5.03 c                   endif
5.03  *
     c                   endif
     c                   endif
5.33  *
5.33  *  Kontroll om korttrans. - kortnr. inn i sorteringsbegrep
5.33  *
5.33 c                   eval      fntrl1_numm = fonumm
5.33 c                   eval      fntrl1_suff = fosuff
5.33 c     fntrl1_key    chain     fntrl1r
5.33 c                   if        %found
5.33 c                   eval      fmkal1_kund = fnkund
5.33 c                   eval      fmkal1_kpro = fnkpro
5.33 c                   eval      fmkal1_kftp = fnkftp
5.33 c                   eval      fmkal1_ktsq = fnktsq
5.33 c     fmkal1_key    chain     fmkal1r
5.33 c                   if        %found
5.33 c                   move      fmkrnr        sor003
5.33 c                   endif
5.33 c                   endif
      *
5.24  * Legger inn forskjellig bet.bet på forskjellig faktura
      *
5.33 c                   move      fobetb        sor004
3.35  *
3.35  * Dersom fakturadato eller forfallsdato er overstyrt,
3.35  * skal ikke ordre slås sammen på en faktura,
3.35  * men komme på hver sin.
3.35  * (Samme plassering benyttes for fakturadato og
3.35  *  forfallsdato, da jeg anser at det sjelden vil
3.35  *  bli noe sammenfall her.)
3.35  * Fakturadato legges ut først (dersom oppgitt),
3.35  * og forfallsdato legges over (dersom oppgitt)
3.35  * Dette for å få brudd på sorteringsbegrepet.
3.35  *
3.35 c                   eval      w_fkda = 0
3.35 c                   eval      w_ffda = 0
3.35  *
3.35 c                   if        fofkda <> *loval
3.35 c     *ymd          move      fofkda        w_fkda
3.35 c                   endif
3.35  *
3.35 c                   if        foffda <> *loval
3.35 c     *ymd          move      foffda        w_ffda
3.35 c                   endif
3.35  *
3.35 c                   if        w_fkda <> 0
5.33 c                   movel     w_fkda        sor005
3.35 c                   endif
3.35  *
3.35 c                   if        w_ffda <> 0
5.33 c                   movel     w_ffda        sor005
3.35 c                   endif
8.02  * Hvsi vi ikke skal samle ordretyper, legg typen i samlebegrep.
8.02 c                   if        rk2fas = 1
8.02 c                   move      fkpoty        sor005
8.02 c                   endif
      *
      * Legger over hele sorteringsbegrepet til register-feltet
      *
     c                   movel     sorter        fkssrt
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Arbeider med henting av heading/linjer/tekster og resting
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      * Hent opplysninger fra ORDRETYPE-REGISTER
      *
     c                   eval      votyl1_otyp = footyp
     c     votyl1_key    chain     votyl1r                            90
      *
      * Hopp til plukk linjer dersom dette bare er utskrift
      *
8.01 c                   if        fk2oty <> '99'
     c     footyp        cabeq     fk2oty        tag200
8.01 c                   endif
      *
      * Legger inn stanard suffix dersom ikke resting av linjer
      *
     c                   eval      rohel1_suff = fohel1_suff
      *
      * Nullstiller hjelpevariable for test på om restordre benyttes
      *
     c                   eval      w_test = *zero
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av resting    Danner nye linjer og heading
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      * Sjekk, Resting av ordre, utfør dette først
      *
     c                   if        fkpres <> *zero
      *
      * Kaller opp subrutine for henting av neste suffix
      *
     c                   exsr      hntsuf
      *
      * Kaller opp subrutiner for behandling av resting
      *
     c                   exsr      olrest
      *
      * Danner ny ordreheading/sletter eventuellt gammel
      *
     c     fohel1_key    chain     fohel1r                            90
      *
      * Det finnes ikke linjer som er restet igjen så slett ordre
      *
     c                   if        w_test = *zero
     c     fohel1_key    chain     fohelur                            90
6.24 c                   if        %found(fohelu)
     c                   delete    fohelur
6.24 c                   endif
     c                   endif
      *
      * Danner ny ordre med ny suffix
      *
     c                   call      'FO709R'
3.11 c                   parm                    rohel1_numm
     c                   parm                    rohel1_suff
      *
     c     rohel1_key    chain     fohelur                            90
     c                   if        *in90 = *on
3.11 c                   eval      fonumm = rohel1_numm
     c                   eval      fosuff = rohel1_suff
     c                   eval      fokode = 3
5.51 c                   eval      forest = 0
8.10 c                   if        fofsys = 'AOR'
8.10 c                   eval      fofsys = *blanks
8.10 c                   eval      fonref = *blanks
8.10 c                   endif
6.25 c*                  time                    fodate
6.25 c*                  time                    fotime
6.10 c                   eval      fooous = l_user
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = l_user
     c                   write     fohelur
     c                   endif
      *
5.56 c*                  if        vaoakk = 1
5.64 c                   if        w_oakk = 1
5.67 c                   eval      w_lnum = fohel1_numm
5.67 c                   eval      w_lsuf = fohel1_suff
5.67 c                   exsr      sjekk_lkode
5.67 c                   if        b_kjko = *on
5.67  * oppdat. status hvis plukkstyring er valg
     c                   eval      d_ufir = fofirm
     c                   eval      d_unum = fonumm
     c                   eval      d_usuf = fosuff
5.56 c                   eval      d_ukod = '2'
5.56 c                   exsr      upd_logg
5.67 c                   endif
      *
5.56 c                   endif
      *
     c                   call      'FO190R'
     c                   parm                    w_stat
     c                   parm                    w_firm
     c                   parm                    rohel1_numm
     c                   parm                    rohel1_suff
     c                   parm                    fokund
     c                   parm                    fokpro
      *
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * SPESIAL FOR LAGER :  behandling av ordre som ikke restes
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c                   if        faalag <> *zero
     c                   if        fkpres = *zero
5.02 c                   if        w_olag = *zero
     c                   exsr      laglag
     c                   endif
     c                   endif
5.02 c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Etterbehandling av "gammel" ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Hopper til etterbehandling ny ordre dersom ikke
      * Heading finnes på forrige /fra) ordre
      *
      * Trekker ut hele beløpet fra fra ordre derosm ikke restlinjer
      *
     c                   if        w_test = *zero
      *
      * Beregner justeringsbeløp
      *
     c                   eval      w_sald = fototr - fotots
5.60 c                   eval      w_sal2 = fobrra * -1
      *
      * Kaller opp subrutine for oppdatering av memosaldo
      *
8.00 c                   move      w_firm        p_firm
8.00 c                   move      fokund        p_kund
8.00 c                   call      'FO763R'
8.00 c                   parm                    p_firm
8.00 c                   parm                    p_kund
      *
8.00 c*                  call      'FA922R'
8.00 c*                  parm                    w_firm
8.00 c*                  parm                    footyp
8.00 c*                  parm                    fokund
8.00 c*                  parm                    w_sald
8.00 c*                  parm                    w_sal2
      *
     c                   goto      tag100
     c                   endif
      *
      * Beregner nye summer for "GAMMEL" ordre
      *
     c                   eval      w_enor = *blank
     c                   call      'FO730R'
     c                   parm                    w_enor
     c                   parm                    w_firm
     c                   parm                    fohel1_numm
     c                   parm                    fohel1_suff
      *
      * Oppdaterer ordren slik at den kan endres etc.etc.
      *
     c     fohel1_key    chain     fohelur                            90
5.02  *
5.02  * Legger inn kode for at beholdning er oppdatert
5.02  *
5.02 c                   if        w_oakk = 2 or
5.02 c                             w_oakk = 3
5.02 c                   if        w_test = 0
5.02 c                   eval      foolag = 1
5.02 c                   endif
5.02 c                   endif
5.51  *
5.51  * Legger inn kode for restnotering
5.51  *
5.51 c                   if        w_oakk = 2
5.51 c                   eval      forest = 1
5.56 c                   eval      d_ufir = w_firm
5.56 c                   eval      d_unum = fohel1_numm
5.56 c                   eval      d_usuf = fohel1_suff
5.56 c                   eval      d_ukod = '8'
5.55 c                   exsr      upd_logg
5.51 c                   endif
5.56 c                   if        w_oakk = 1
5.56 c                   eval      d_ufir = w_firm
5.56 c                   eval      d_unum = fohel1_numm
5.56 c                   eval      d_usuf = fohel1_suff
5.56 c                   eval      d_ukod = '9'
5.56 c                   exsr      upd_logg
5.66 c                   eval      b_dell = *on
5.56 c                   endif
5.56  *
     c                   if        *in90 = *off
5.66 c                   if        b_dell = *on
5.66  * det er suff. som skal ha lev.dato og info. suff lik 00 har hatt
5.66  * info midlertidig.
6.27 c**                 eval      foldat = *loval
6.20 c                   eval      fopada = *loval
5.66 c                   eval      foinf1 = *blank
5.66 c                   eval      foinf2 = *blank
5.66 c                   endif
     c                   eval      fokode = *zero
     c                   move      *loval        fokoda
     c                   move      *loval        fokoti
     c                   eval      fokous = *blank
     c                   eval      fokows = *blank
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = l_user
     c                   update    fohelur
     c                   endif
5.54  *
5.54  * Sletter evt. arbeidsregister for plukking for denne ordre/suff
5.54  *
5.54 c                   eval      lwpllu_numm = fohel1_numm
5.54 c                   eval      lwpllu_suff = fohel1_suff
5.54 c                   exsr      slett_plukk
5.51  *
5.51  * Tilbakestill kode for restnotering
5.51  *
5.51 c                   eval      forest = 0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Etterbehandling av "ny" ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     tag100        tag
3.11  *
3.11  * Legger inn rett ordrenummer etter plukking
3.11  *
3.11 c                   eval      fkponr      = rohel1_numm
3.11 c                   eval      fohel1_numm = rohel1_numm
3.11 c                   eval      fodtl1_numm = rohel1_numm
3.11 c                   eval      fotxl1_numm = rohel1_numm
3.11 c                   eval      fpsolu_sonr = rohel1_numm
      *
      * Legger inn rett suffix etter plukking
      *
     c                   eval      fkpsuf      = rohel1_suff
     c                   eval      fohel1_suff = rohel1_suff
     c                   eval      fodtl1_suff = rohel1_suff
     c                   eval      fotxl1_suff = rohel1_suff
     c                   eval      fpsolu_ssuf = rohel1_suff
      *
      * Oppdaterer ordren med nye verdier fra utplukk
      *
     c     fohel1_key    chain     fohelur                            90
     c                   if        *in90 = *off
     c                   eval      footyp = fkpoty
     c                   eval      folage = fkplag
     c                   eval      fotots = *zero
     c                   eval      fototr = *zero
5.60 c                   eval      fobrra = *zero
5.51 c                   eval      forest = *zero
5.02 c                   if        w_oakk = 2 or
5.02 c                             w_oakk = 3
5.02 c***>               if        w_test = 0
5.02 c                   eval      foolag = 1
5.02 c***>               endif
5.02 c                   endif
      *
      * Dersom leveringsdato ikke er registert og dette er en
      * pakkseddel, legges dagens dato ut i leveringsdato.
      *
3.14  *    w_oakk        ifeq      2
3.14  *    foldat        ifeq      *loval
3.14  *                  move      w_date        foldat
3.14  *                  end
3.14  *                  end
3.14  *
6.20  * Når en ordre plukkes til pakkseddel, oppdateres pakks.dato
6.20  * med dag.dato. Leveringsdato blir liggende som før.
6.20  * Den dato som lå i feltet fra før,
3.14  * tas vare på i planlagt levering, slik at man i ettertid
3.14  * kan se planlagt levering opp mot virkelig levering.
3.14  *
3.14 c                   if        w_oakk = 2
6.20 c                   if        foldat = *loval
6.20 c                   eval      foldat = w_date
6.20 c                   endif
3.14 c                   if        fopdat = *loval
3.14 c                   eval      fopdat = foldat
3.14 c                   endif
6.20 c                   eval      fopada = w_date
3.14 c                   endif
      *
5.23  * Oppdatering av bruker/skjerm/dato/klokkeslett
5.23  *
6.25 c*                  time                    fodate
6.25 c*                  time                    fotime
5.23 c                   eval      fowsid = l_wsid
5.43 c***                eval      fouser = l_user
5.23  *
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = l_user
     c                   update    fohelur
     c                   endif
      *
      * Beregner nye summer for "NY" ordre
      *
     c                   eval      w_enor = *blank
     c                   call      'FO730R'
     c                   parm                    w_enor
     c                   parm                    w_firm
3.11 c                   parm                    rohel1_numm
     c                   parm                    rohel1_suff
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Etterbehandling av ordre som skal skrives ut  (henting m.m.)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter ordre/-linjer fra valgt ordre til PLUKK-S-LINJE-REGISTER
      *
     c     tag200        tag
5.01  *
5.01  * Oppdater kode som sier at pakkseddel er skrevet,
5.01  * dersom dette er en pakkseddel.
5.01  *
5.01 c     fohel1_key    chain     fohelur                            90
5.01 c                   if        *in90 = *off
5.01 c                   if        w_oakk = 2
5.01 c                   eval      fopaks = 1
5.01 c                   endif
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = l_user
5.01 c                   update    fohelur
5.01 c                   endif
6.21  *
6.21  * Dersom det er direkte utskrift, skal ordre ikke
6.21  * kjøres via logic
6.21  *
6.21 c                   if        w_oakk = 3
6.21 c                   if        fk2vg0 = 1
6.21 c     fohel1_key    chain     fohelur
6.21 c                   if        %found(fohelu)
6.21 c                   if        fofaty <> 0
6.21 c                   eval      fofaty =  0
6.21 c                   endif
6.21 c                   update    fohelur
6.21 c                   endif
6.21 c                   endif
6.21 c                   endif
      *
     c                   exsr      ohead
     c                   if        fkptxt = *zero
     c                   exsr      foran
     c                   endif
     c                   exsr      linje
     c                   if        fkptxt = *zero
     c                   exsr      etter
     c                   endif
      *
      * Sletter record fra UTPLUKK-PARAM-REGISTER
      *
     c     tag300        tag
      *
     c     fplol1_key    chain     fplolur                            90
     c                   if        *in90 = *off
     c                   delete    fplolur
     c                   endif
      *
6.36  *  Sjekker om kostprisbokføring skal gjøres
6.36  *
7.03 c**                 if        w_oakk = 2
6.36 c                   eval      w_modu = 'GJKOST'
6.36 c                   call      'AX700R'
6.36 c                   parm                    w_modu
6.36 c                   parm                    w_disp
6.36 c                   parm                    w_tilg
6.36  *
6.36 c                   if        w_tilg = '0'
6.36 c                   call      'VG802R'
6.36 c                   parm                    rohel1_numm
6.36 c                   parm                    rohel1_suff
6.36 c                   endif
7.03 c**                 endif
6.36  *
     c                   goto      xstart
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xslutt        tag
      *
      *
      *
     clr                 return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter neste ledige suffix
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hntsuf        begsr
      *
     c                   eval      rohel1_suff = 49
     c     rohel1_key    setll     fohel1r
      *
     c                   eval      *in90 = *off
     c                   readp     fohel1r                                90
      *
     c                   eval      rohel1_suff = fosuff + 1
      *
3.11  * Dersom dette er plukking fra et tilbud (behandlingskode = 0)
3.11  * og til en ordretype som ikke er tilbud (behandlingskode > 0)
3.11  * skal det skapes en helt ny ordre i den nummerserie som
3.11  * tilhører ordretypen det plukkes til.
3.11  * På dette tidspunkt i programmet skal behandlingskode for
3.11  * gammel ordretype være = VAOAKK, og ny ordretype = w_oakk.
3.11  *
3.11 c                   if        vaoakk = 0 and w_oakk > 0
3.11 c                   exsr      hntnum
3.11 c                   eval      rohel1_numm = w_numm
3.11 c                   eval      rohel1_suff = 0
3.11 c                   endif
3.11  *
     c                   endsr
3.11  *
3.11  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
3.11  * Henter/Oppdatering av NUMMER-REGISTER
3.11  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
3.11  *
3.11 c     hntnum        begsr
3.11  *
3.11 c                   eval      w_fell = faaork
3.11 c                   eval      w_type = fkpoty
3.11 c                   eval      w_fast = l_wsid
3.11 c                   eval      w_kode = '0'
5.32  *
5.32  * Hente inn eventuell overstyrt
5.32  * ordretype for nummerserie
5.32  *
5.32 c                   call      'VA752R'
5.32 c                   parm                    w_firm
5.32 c                   parm                    fkpoty
5.32 c                   parm                    w_onum
5.32  *
5.32 c                   if        w_onum <> *blank
5.32 c                   eval      w_type = w_onum
5.32 c                   endif
3.11  *
3.11  * Kaller opp subprogram for henting av nummer fra NUMMER-REGISTER
3.11  *
3.11 c                   call      'AS100R'
3.11 c                   parm                    w_kode
3.11 c                   parm                    w_firm
3.11 c                   parm                    w_fell
3.11 c                   parm                    w_type
3.11 c                   parm                    w_fast
3.11 c                   parm                    w_numm
6.26  *
6.26  * sjekker at ordrenr ikke har tidligere relasjoner liggende
6.26  *
6.26 c                   call      'FO415R '
6.26 c                   parm                    w_firm
6.26 c                   parm                    w_numm
3.11  *
3.11 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Legger ut ordre-linjer til resting
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Forklaring på hva som skjer i denne subrutine:
      * ----------------------------------------------
      *
      * I skjermbildet for utplukk finnes tre alternativer:
      *
      *    0 = Ingen spesifisering av linjer
      *    1 = Plukk bare linjer som oppgis
      *    2 = Plukk alle linjer, men unntak
      *        (Feltet for denne koden heter FKPRES)
      *
      * Kode 0 kommer ikke innom denne rutine, bare kode 1 eller 2.
      * Det settes lower limit på varelinjeregister (som inneholder varer
      * og tekstlinjer) og det leses så lenge man befinner seg på samme
      * firmanummer / ordrenummer / suffix.
      * Samtidig gjøres det oppslag mot utplukksregister for å se om det
      * skal gjøres endringer i forhold til opprinnelig varelinje.
      *
      * Disse koder er tilgjengelige:
      *    1 = Plukkes
      *    3 = Kopieres                 (Kun tekstlinjer)
      *    4 = Holdes tilbake
      *    5 = Ingen resting av antall  (Kun varelinjer)
      *        (Feltet for denne koden heter FKLKOD)
      *
      * Utplukks(linje)register inneholder derfor poster bare der hvor
      * det er registrert enten et antall eller en kode for utplukk.
      * Vær også oppmerksom på at selv om det ikke blir funnet en
      * post i utplukksregisteret, kan det legges inn verdier i feltene
      * FKLANT og FKLKOD
      * F. eks. settes FKLKOD til 1 når hele ordren skal plukkes, men
      * unntak (FKPRES=2)
      *
      * Rutinen tar seg av restnotering, oppdatering av lager, kopiering/
      * plukking av tekslinjer etc.  En ordre som kommer innom her vil
      * alltid få et nytt suffix, mens resten blir liggende igjen på
      * gammel ordre eller slettes dersom den plukkes tom.
      *
     c     olrest        begsr
      *
     c                   eval      fodtl1_line = *zero
     c     fodtl1_key    setll     fodtl1r
      *
      * Leser inn en record fra ORDRE-LINJE-REGISTER
      *
     c     olres1        tag
     c                   eval      *in90 = *off
     c                   read      fodtl1r                                90
     c                   if        *in90 = *off
     c                   if        fdfirm = w_firm
     c                   if        fdnumm = fodtl1_numm
     c                   if        fdsuff = fodtl1_suff
      *
      * Hent opplysninger fra UTPLUKK-LINJE-REGISTER (rest/Tekst-linje)
      *
     c                   eval      fplllu_llin = fdline
     c     fplllu_key    chain     fplllur                            92
     c                   if        *in92 = *off
     c                   delete    fplllur
     c                   endif
      *
      * Dersom alle linjer skal plukkes, men unntak kan gjøres
      * Legges hele antallet inn som antall plukket
      * dersom linjen ikke finnes i ORDRE-PLUKK-LINJE-REGISTER
      *
     c                   if        *in92 = *on
     c                   eval      fklant = *zero
     c                   eval      fklkod = *blank
      *
      * Plukke alt, men unntak
      *
     c                   if        fkpres = 2
     c                   eval      fklant = fdanta
     c                   eval      fklkod = '1'
     c                   endif
      *
     c                   endif
      *
      * Dersom det er oppgitt kode 1 for utplukk, og antall
      * er lik 0 i plukkfilen, legges hele antallet over.
      *
     c                   if        *in92 = *off
     c                   if        fklkod = '1'
     c                   if        fklant = *zero
     c                   eval      fklant = fdanta
     c                   endif
     c                   endif
     c                   endif
      *
      * Dersom tekstlinjer, gå til eget avsnitt
      *
     c     fdvare        cabeq     *blank        xtxlin
      *
      * Dersom antall levert=0 skal ikke ORDRE-LINJE-REGISTER oppdat.
      *
     c                   if        fklkod = *blank
     c                   if        fklant = *zero
     c                   eval      w_test = 1
     c                   goto      olres1
     c                   endif
     c                   endif
      *
      * Dersom kode 4 er oppgitt, skal varelinje vente,
      * og ordrelinjeregister skal ikke oppdateres.
      *
     c                   if        fklkod = '4'
     c                   eval      w_test = 1
     c                   goto      olres1
     c                   endif
      *
      * Danner ny linjer for RESTORDRE
      * Legger ut nye linjer for "NY" ordre  ): NY SUFFIX
      *
3.11 c                   eval      fodtl1_numm = fdnumm
     c                   eval      fodtl1_line = fdline
     c     rodtlu_key    chain     fodtlur                            94
     c                   if        *in94 = *on
3.11 c                   eval      fdnumm = rohel1_numm
     c                   eval      fdsuff = rohel1_suff
     c                   eval      fdanta = fklant
6.10 c                   time                    fdodat
6.10 c                   time                    fdotim
6.10 c                   eval      fdoous = l_user
6.10 c                   time                    fdedat
6.10 c                   time                    fdetim
6.10 c                   eval      fdeusr = l_user
     c                   write     fodtlur
7.02  *
7.02 c                   if        footyp = 'HO'
7.02 c                             and (%subst(fdtek1:1:9) = 'Sluttfakt'
7.02 c                              or %subst(fdtek1:1:9) = 'SLUTTFAKT'
7.02 c                              or %subst(fdtek1:1:9) = 'SluttFakt')
7.02 c                   call      'FO615R'
7.02 c                   parm                    fdnumm
7.02 c                   parm                    fdsuff
7.02 c                   parm                    fdline
7.02 c                   parm                    foproj
7.02 c                   parm                    foupro
7.02 c                   parm                    foakti
7.02 c                   endif
      *
      * Kaller opp subrutine for oppdatering av lager
      *
     c                   if        faalag <> *zero
5.02 c                   if        w_olag =  *zero
     c                   eval      w_lage = fkplag
     c                   eval      w_otyp = fkpoty
     c                   exsr      opplag
     c                   endif
5.02 c                   endif
      *
     c                   endif
      *
      * Oppdaterer linjer for "GAMMEL" ordre
      *
     c     fodtl1_key    chain     fodtlur                            94
     c                   if        *in94 = *off
      *
      * Kaller opp subrutine for oppdatering av lager
      *
     c                   if        faalag <> *zero
5.02 c                   if        w_olag =  *zero
     c                   eval      w_lage = w_lage_sav
     c                   eval      w_otyp = w_otyp_sav
     c                   eval      fdanta = fdanta * -1
     c                   exsr      opplag
     c                   eval      fdanta = fdanta * -1
     c                   endif
5.02 c                   endif
      *
      * Beregn rest    (REST=BESTILT-LEVERT)
      * Kode for og bevare tidligere linjeantall =0
      *
     c                   if        vaoboo = 0
     c                   eval      w_rest = fdanta - fklant
     c                   endif
      *
      * Det skal ikke restnoteres
      *
     c                   if        vaoboo = 1
     c                   eval      w_rest = 0
     c                   endif
      *
      * Resten skal ikke beholdes, den skal slettes
      *
     c                   if        fklkod = '5'
     c                   eval      w_rest = 0
     c                   endif
      *
      * Behold alltid hele det opprinnelige antallet
      *
     c                   if        vaoboo = 2
     c                   eval      w_rest = fdanta
     c                   endif
      *
      * Behandler oppdatering av ordren utfra resultatet av restingen
      *
     c                   if        w_rest > 0
     c                   eval      w_test = 1
     c                   eval      fdanta = w_rest
6.10 c                   time                    fdedat
6.10 c                   time                    fdetim
6.10 c                   eval      fdeusr = l_user
     c                   update    fodtlur
     c                   else
5.63  *
5.63  * Logg ordrelinje før den blir slettet
5.63  *
5.63 c                   eval      w_spgm      = 'FO614R'
5.63 c                   call      'FS710R'
5.63 c                   parm                    w_firm
5.63 c                   parm                    fdnumm
5.63 c                   parm                    fdsuff
5.63 c                   parm                    fdline
5.63 c                   parm                    w_spgm
5.63  *
     c                   eval      fdanta = 0
     c                   delete    fodtlur
     c                   endif
      *
      * Kaller opp subrutine for oppdatering av lager
      *
     c                   if        faalag <> *zero
5.02 c                   if        w_olag =  *zero
     c                   eval      w_lage = w_lage_sav
     c                   eval      w_otyp = w_otyp_sav
     c                   exsr      opplag
     c                   endif
5.02 c                   endif
      *
     c                   endif
      *
      * Ferdig med behandling av varelinjer
      *
     c                   goto      xneste
      *
      * -----------------------------------
      * Behandling av tekstlinjer
      *
     c     xtxlin        tag
      *
      * Gå ut med linjer som ikke skal behandles
      *
     c                   if        fklkod = *blank
     c                   eval      w_test = 1
     c                   goto      xneste
     c                   endif
      *
     c                   if        fklkod = '4'
     c                   eval      w_test = 1
     c                   goto      xneste
     c                   endif
      *
      * Legge ut tekstlinje på ny suffix
      *
3.11 c                   eval      fodtl1_numm = fdnumm
     c                   eval      fodtl1_line = fdline
     c     rodtlu_key    chain     fodtlur                            94
     c                   if        *in94 = *on
3.11 c                   eval      fdnumm = rohel1_numm
     c                   eval      fdsuff = rohel1_suff
     c                   eval      fdanta = 0
6.10 c                   time                    fdodat
6.10 c                   time                    fdotim
6.10 c                   eval      fdoous = l_user
6.10 c                   time                    fdedat
6.10 c                   time                    fdetim
6.10 c                   eval      fdeusr = l_user
     c                   write     fodtlur
     c                   endif
      *
      * Dersom det er igjen linjer på "gammel ordre"
      * kan ikke ordrehode slettes (kopiering)
      *
     c                   if        fklkod = '3'
     c                   eval      w_test = 1
     c                   goto      xneste
     c                   endif
      *
      * Slette eventuelle tekstlinjer fra "gammel ordre"
      *
     c     fodtl1_key    chain     fodtlur                            94
     c                   if        *in94 = *off
     c                   delete    fodtlur
     c                   endif
      *
     c     xneste        tag
      *
     c                   goto      olres1
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer PLUKK-S-LINJE-REGISTER med sorterings-felter m.m.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ohead         begsr
      *
     c                   eval      fpsolu_stll = *zero
     c                   eval      fpsolu_slin = *zero
     c     fpsolu_key    chain     fpsolur                            92
      *
     c                   if        *in92 = *off
     c                   update    fpsolur
     c                   else
     c                   eval      fksfir = fkpfir
     c                   eval      fksonr = fkponr
     c                   eval      fkssuf = fkpsuf
     c                   eval      fkslin = *zero
     c                   eval      fkstll = *zero
     c                   eval      fkskod = 'H'
     c                   write     fpsolur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter ordre-linjer og legger ut til PLUKK-S-LINJE-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     linje         begsr
      *
     c                   eval      fodtl1_line = *zero
      *
     c     fodtl1_key    setll     fodtl1r
      *
     c     linje1        tag
     c                   eval      *in90 = *off
     c                   read      fodtl1r                                90
     c                   if        *in90 = *off
     c                   if        fdfirm = w_firm
     c                   if        fdnumm = fodtl1_numm
     c                   if        fdsuff = fodtl1_suff
      *
      * Kaller opp subrutine for oppdatering av lager
      *
     c                   if        faalag <> *zero
5.02 c                   if        w_olag =  *zero
     c                   if        vaoakk = 3
8.01 c                   if        fk2oty = '99' or
8.01 c                             footyp = fk2oty
     c                   eval      w_lage = w_lage_sav
     c                   eval      w_otyp = w_otyp_sav
     c                   exsr      opplag
     c                   endif
     c                   endif
     c                   endif
5.02 c                   endif
6.22  *
6.22  * legger evt. linjer til BygDok
6-2x  *
6.22 c                   if        b_bdo1 = *on and
6.22 c                             b_bdo2 = *on and
6.22 c                             b_bdo3 = *on and
7.01 c                             ((fdnobb <> 0) or
7.01 c                             (u_s701 = *on and
7.01 c                              fdvare <> *blank))
6.22 c                   eval      rkunl1_kund = fokund
6.22 c     rkunl1_key    chain     rkunl1
6.22  * finner kontaktperson for kunde som har rolle BYGGDOK
6.22 c                   eval      b_kper  = *off
6.22 c                   eval      rukpl6_kund = fokund
6.22 c     rukpl6_key    setll     rukpl6
6.22 c     rukpl6_key    reade     rukpl6
6.22 c                   dow       not %eof
6.22 c     lo:up         xlate     rukrol        rukrol
6.32 c                   if        rukrol  = 'BYGGDOK' or rukrol = 'COBUILDER'
6.22 c                   eval      b_kper  = *on
6.22 c                   goto      fbdo_tag
6.22 c                   endif
6.22 c     rukpl6_key    reade     rukpl6
6.22 c                   enddo
6.22 c     fbdo_tag      tag
6.22 c                   eval      fwbfir = w_firm
6.22 c                   eval      fwbkun = fokund
6.22 c                   eval      fwbkpr = fokpro
6.22 c                   movel     fonumm        fwbnum
6.22 c                   eval      fwbnav = rknavn
6.22 c                   if        b_kper  = *on and
6.22 c                             ruenav <> *blank and
6.22 c                             rumail <> *blank
6.22 c                   eval      fwbper = %trim(ruenav) + ',' +
6.22 c                                      %trim(rufnav)
6.22 c                   eval      fwbmai = rumail
6.22 c                   else
6.22 c                   eval      fwbper = rknavn
6.22 c                   eval      fwbmai = rkemal
6.22 c                   endif
6.35  * hvis mail på kunde-prosjekt overstyrer det alt
6.35 c                   if        flmail <> *blank
6.35 c                   eval      fwbmai = flmail
6.35 c                   if        flkprs <> *blank
6.35 c                   eval      fwbper = flkprs
6.35 c                   endif
6.35 c                   endif
6.22 c                   eval      fwbftn = rkftnr
6.22 c                   move      w_btms        w_ti26
6.22 c                   eval      fwbtms = w_ti26
7.01 c                   if        u_s701 = *on
7.01 c                   movel     fdvare        fwbnob
7.01 c                   else
6.22 c                   eval      fwbnob = fdnobb
7.01 c                   endif
6.22 c                   time                    fwboda
6.22 c                   time                    fwboti
6.22 c                   time                    fwbeda
6.22 c                   time                    fwbeti
6.22 c                   eval      fwbous = l_user
6.22 c                   eval      fwbeus = l_user
6.22 c                   write     fbdopfr
6.2c c                   move      fwbnum        w_bnum
6.22 c                   eval      b_bdo4 = *on
6.22 c                   endif
      *
     c                   eval      fpsolu_stll = 1
     c                   eval      fpsolu_slin = fdline
     c     fpsolu_key    chain     fpsolur                            92
      *
     c                   if        *in92 = *off
     c                   update    fpsolur
     c                   else
     c                   eval      fkstll = 3
     c                   eval      fkslin = fdline
     c                   eval      fkskod = 'V'
     c                   write     fpsolur
     c                   endif
      *
     c                   goto      linje1
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter ordre-tekst (FORAN) og legger ut til PLUKK-S-LINJE-REG.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     foran         begsr
      *
     c                   eval      fotxl1_line = *zero
      *
     c     fotxl1_key    setll     fotxl1r
      *
     c     foran1        tag
     c                   eval      *in90 = *off
     c                   read      fotxl1r                                90
     c                   if        *in90 = *off
     c                   if        ftfirm = w_firm
     c                   if        ftnumm = fotxl1_numm
     c                   if        ftsuff = fotxl1_suff
     c                   if        ftline <= 4999
      *
     c                   eval      fpsolu_stll = 2
     c                   eval      fpsolu_slin = ftline
     c     fpsolu_key    chain     fpsolur                            92
      *
     c                   if        *in92 = *off
     c                   update    fpsolur
     c                   else
     c                   eval      fkstll = 2
     c                   eval      fkslin = ftline
     c                   eval      fkskod = 'K'
     c                   write     fpsolur
     c                   endif
      *
     c                   goto      foran1
     c                   endif
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter ordre-tekst (ETTER) og legger ut til PLUKK-S-LINJE-REG.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     etter         begsr
      *
     c                   eval      fotxl1_line = 5000
      *
     c     fotxl1_key    setll     fotxl1r
      *
     c     etter1        tag
     c                   eval      *in90 = *off
     c                   read      fotxl1r                                90
     c                   if        *in90 = *off
     c                   if        ftfirm = w_firm
     c                   if        ftnumm = fotxl1_numm
     c                   if        ftsuff = fotxl1_suff
     c                   if        ftline <= 9999
      *
     c                   eval      fpsolu_stll = 4
     c                   eval      fpsolu_slin = ftline
     c     fpsolu_key    chain     fpsolur                            92
      *
     c                   if        *in92 = *off
     c                   update    fpsolur
     c                   else
     c                   eval      fkstll = 4
     c                   eval      fkslin = ftline
     c                   eval      fkskod = 'K'
     c                   write     fpsolur
     c                   endif
      *
     c                   goto      etter1
     c                   endif
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Leser igjenom ordrelinjer for oppdatering av lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     laglag        begsr
      *
     c                   eval      fodtl1_line  = *zero
     c     fodtl1_key    setll     fodtl1r
      *
     c     lanje1        tag
     c                   eval      *in90 = *off
     c                   read      fodtl1r                                90
     c                   if        *in90 = *off
     c                   if        fdfirm = w_firm
     c                   if        fdnumm = fodtl1_numm
     c                   if        fdsuff = fodtl1_suff
      *
      * Behandler fra antall på ordretype FRA
      *
     c                   eval      w_lage = w_lage_sav
     c                   eval      w_otyp = w_otyp_sav
     c                   eval      fdanta = fdanta * -1
     c                   exsr      opplag
     c                   eval      fdanta = fdanta * -1
      *
      * Behandler til antall på ordretype TIL
      *
     c                   eval      w_lage = fkplag
     c                   eval      w_otyp = fkpoty
     c                   exsr      opplag
      *
     c                   goto      lanje1
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdatering av Lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     opplag        begsr
      *
      * Legger over verdier i overførings-felter
      *
     c                   eval      hlvare = fdvare
5.31 c                   eval      hllage = fdlage
     c                   eval      hlenhe = fdenh1
     c                   eval      hldato = *loval
     c                   eval      hltime = *loval
     c                   eval      hlotyp = w_otyp
     c                   eval      hlltyp = fdltyp
      *
     c                   eval      hlanta = fdanta
     c                   eval      hlkopr = fdkopr
     c                   eval      hlonum = fdnumm
     c                   eval      hlolin = fdline
     c                   eval      hlsyst = 'F'
     c                   eval      hlaltk = *blank
     c                   eval      hlrest = *blank
     c                   move      fdnumm        w_numa
     c                   move      fdline        w_lina
     c                   eval      hlrefr = 'O:' + w_numa +
     c                                      ' L:' + w_lina
3.13 c                   move      fdlety        hllety
3.32  *
3.32  * Ved oppdatering av fakturering,
3.32  * skal fakturadato benyttes
3.32  * (ikke dagens dato)
3.32  *
3.32 c                   if        vaoakk = 3
3.32 c                   eval      hldato = fk2dat
3.32 c                   endif
      *
      * Kaller opp lageroppdaterings-program
      *
     c                   eval      w_lrec = hlrec
      *
     c                   call      'VL001R'
     c                   parm                    w_stat
     c                   parm                    w_lrec
      *
     c                   endsr
5.54  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.54  * Subrutine for slette plukk-reg. på RadioTerm.
5.54  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.54  *
5.54 c     slett_plukk   begsr
5.54  *
5.54 c     lwpllu_key    setll     lwpllu
5.54 c     lwpllu_key    reade     lwpllu
5.54 c                   dow       not %eof(lwpllu)
5.54  *
5.54 c                   delete    lwpllur
5.54  *
5.54 c     lwpllu_key    reade     lwpllu
5.54 c                   enddo
5.54  *
5.54 c                   endsr
      *
5.55  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.55  * Subrutine for slette plukk-reg. på RadioTerm.
5.55  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.55  *
5.55 c     upd_logg      begsr
5.55  *
5.55  *
5.55 c                   time                    d_date
5.55 c                   time                    d_time
5.55 c                   eval      d_user = l_user
5.55 c                   eval      d_wsid = l_wsid
5.61 c                   eval      d_aarr = %subdt(fobdat:*years)
5.55 c                   eval      p_urec = d_urec
5.55 c                   call      'FU900R  '
5.55 c                   parm                    p_stat
5.55 c                   parm                    p_urec
5.55  *
5.55  *
5.55 c                   endsr
5.67  *
5.67  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.67  * Subrutine for sjekke om kjørekontor er valgt (loggkode = 0)
5.67  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.67  *
5.67 c     sjekk_lkode   begsr
5.67 c                   eval      b_kjko = *off
5.67 c                   eval      w_kod2 = '0'
5.67  *
5.67 c                   eval      w_aarr = %subdt(fobdat:*years)
5.67 c                   call      'FU702R'
5.67 c                   parm                    w_aarr
5.67 c                   parm                    w_lnum
5.67 c                   parm                    w_lsuf
5.67 c                   parm                    w_kod2
5.67 c                   parm                    w_sva2
5.67  *
5.67 c                   if        w_sva2 = 'J'
5.67 c                   eval      b_kjko= *on
5.67 c                   endif
5.67 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Definisjon av local data area for output
      *
     c     *dtaara       define    *lda          lda
      *
5.21 c                   eval      w_in03 = *blank
      *
      * Nullstilling av felter som ikke kan redefineres
      *
     c                   eval      w_test = *zero
      *
     c                   eval      w_sald = *zero
5.60 c                   eval      w_sal2 = *zero
      *
     c                   eval      w_enor = *blank
     c                   eval      w_ruti = *blank
      *
      * Key for file : KUNDE-REGISTER
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for file : ORDRETYPE-REGISTER
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for file : OFAK-KODE-REGISTER
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for file : ORDRE-HODE-REGISTER
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      * Key for file : ORDRE-LINJE-REGISTER
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
     c                   kfld                    fodtl1_line
5.42  *
5.42  * Key for file : ORDRE-LINJE-REGISTER (les)
5.42  *
5.42 c     fodtlr_key    klist
5.42 c                   kfld                    w_firm
5.42 c                   kfld                    fodtlr_numm
5.42 c                   kfld                    fodtlr_suff
5.42 c                   kfld                    fodtlr_line
      *
      * Key for file : ORDRE-TEKST-REGISTER
      *
     c     fotxl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fotxl1_numm
     c                   kfld                    fotxl1_suff
     c                   kfld                    fotxl1_line
      *
      * Key for file : ORDRE-HODE-REGISTER, for resting
      *
     c     rohel1_key    klist
     c                   kfld                    w_firm
3.11 c                   kfld                    rohel1_numm
     c                   kfld                    rohel1_suff
      *
      * Key for file : ORDRE-LINJE-REGISTER, for resting
      *
     c     rodtlu_key    klist
     c                   kfld                    w_firm
3.11 c                   kfld                    rohel1_numm
     c                   kfld                    rohel1_suff
     c                   kfld                    fodtl1_line
      *
      * Key for file : PLUKK-S-LINJE-REGISTER
      *
     c     fpsolu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fpsolu_sonr
     c                   kfld                    fpsolu_ssuf
     c                   kfld                    fpsolu_stll
     c                   kfld                    fpsolu_slin
      *
      * Key for file : UTPLUKK-PARAM-REGISTER
      *
     c     fplol1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fplolu_ponr
     c                   kfld                    fplolu_psuf
      *
      * Key for file : UTPLUKK-LINJE-REGISTER
      *
     c     fplllu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fplllu_lonr
     c                   kfld                    fplllu_lsuf
     c                   kfld                    fplllu_llin
3.33  *
3.33  * Key for oppslag på ordre-info-type
3.33  *
3.33 c     vinfl1_key    klist
3.33 c                   kfld                    w_firm
3.33 c                   kfld                    vinfl1_ityp
3.34  *
3.34  * Key for brukerregister
3.34  *
3.34 c     fusrl1_key    klist
3.34 c                   kfld                    w_firm
3.34 c                   kfld                    fusrl1_user
5.33  *
5.33  * Key for kort/fakt trans/numm
5.33  *
5.33 c     fntrl1_key    klist
5.33 c                   kfld                    w_firm
5.33 c                   kfld                    fntrl1_numm
5.33 c                   kfld                    fntrl1_suff
5.33  *
5.33  * Key for oppslag på kort/fakt.avtale-register
5.33  *
5.33 c     fmkal1_key    klist
5.33 c                   kfld                    w_firm
5.33 c                   kfld                    fmkal1_kund
5.33 c                   kfld                    fmkal1_kpro
5.33 c                   kfld                    fmkal1_kftp
5.33 c                   kfld                    fmkal1_ktsq
5.54  *
5.54  * Key for file : Arbeidsregister utplukk
5.54  *
5.54 c     lwpllu_key    klist
5.54 c                   kfld                    w_firm
5.54 c                   kfld                    lwpllu_numm
5.54 c                   kfld                    lwpllu_suff
5.65  *
5.65  * Key for oppslag på depositum-fil
5.65  *
5.65 c     sdepl1_key    klist
5.65 c                   kfld                    w_firm
5.65 c                   kfld                    sdepl1_numm
5.65 c                   kfld                    sdepl1_kund
6.22  *
6.22  * Key for les av moduler Nexstep
6.22  *
6.22 c     amodl1_key    klist
6.22 c                   kfld                    amodl1_modu
6.22  *
6.22  * Key for les av kundeprosjekt
6.22  *
6.23 c     fkprl1_key    klist
6.23 c                   kfld                    w_firm
6.23 c                   kfld                    fkprl1_kund
6.23 c                   kfld                    fkprl1_kpro
pdf   *
pdf   * Key for propertiesfil
pdf   *
pdf  c     afpslr_key    klist
pdf  c                   kfld                    afpslr_ufil
pdf  c                   kfld                    afpslr_uide
6.22  *
6.22  * Key for kontakpersonregister
6.22  *
6.22 c     rukpl6_key    klist
6.22 c                   kfld                    w_firm
6.22 c                   kfld                    rukpl6_kund
      *
      * Blanker hjelpefelter som benyttes i Local Data Area
      *
     c                   eval      l_alt1 = *zero
     c                   eval      l_alt2 = *zero
     c                   eval      l_peri = *zero
     c                   eval      l_dato = *loval
     c                   eval      l_prog = *blank
      *
      * Legger inn type for oppdatering av lager  R=Registrering
      *                                             =Utskrift/Utplukk
      *
     c                   eval      hltype = *blank
3.34  *
3.34  * Hente inn brukerregister
3.34  *
3.36 c                   eval      w_firm      = l_firm
3.34 c                   eval      fusrl1_user = l_user
3.34 c     fusrl1_key    chain     fusrl1r                            90
      *
      * Legger inn dagens dato
      *
     c                   time                    w_date
6.22  *
6.22  * Sjekker om Byggdok skal kjøres forutsetter at
6.22  * modul er på plass og at
6.22  *
6.22 c                   eval      b_bdo1 = *off
6.22 c                   eval      amodl1_modu = 'BYGGDOK'
6.22 c     amodl1_key    chain     amodl1
6.22 c                   if        %found(amodl1)
6.22 c                   eval      afpslr_ufil = l_filg
6.22 c                   eval      afpslr_uide = 'BYGGDOK   '
6.22  *
6.32  * Sjekk om byggdok eller cobuilder aktivering er på/av
6.22  *
6.22 c     afpslr_key    chain     afpslrr
6.22 c                   if        %found(afpslr)
6.22 c                   eval      b_bdo1 = *on
6.32 c                   else
6.32 c                   eval      afpslr_uide = 'COBUILDER '
6.32 c     afpslr_key    chain     afpslrr
6.32 c                   if        %found(afpslr)
6.32 c                   eval      b_bdo1 = *on
6.32 c                   endif
6.22 c                   endif
6.22 c                   endif
      *
7.01  *
7.01  * Sjekker om det ikke er krav til nobbnummer
7.01  *
7.01   CallP CO402R(l_filg:w_firm:0:'COBU_IKKENOBB':
7.01   co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01     u_s701 = *on;
7.01   endif;
7.01  *
     c                   endsr
