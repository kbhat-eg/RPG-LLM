      /TITLE  Fordeling utfra saldobalanse - parameter
     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
      * System  . . . . : ASOKON
      * Program . . . . : RH190R                                        *
      * Beskrivelse . . : Fordeling utfra saldobalanse  - parameter     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
      * R5.30     241104  Kostnadsbærer til ble ikke initialisert       *
      *                                                                 *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KA = F1  = Valg av firma                                  *
      *       KC = F3  = Exit                                           *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     fausrl1    if   e           k disk
     frh190d    cf   e             workstn
      *
     d                uds
     d rhpkda                300    305
     d rhprÅr                         4
     d rhpper                         2
     d rhppef                         2
     d rhppet                         2
     d rhpavf                         4
     d rhpavt                         4
     d rhpktf                         5
     d rhpktt                         5
     d rhpsrt                         1
     d rhpsml                         1
     d rhpavv                         1
     d rhpkgs                         1
     d rhpkks                         1
      *
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d waar            s              4  0
     d wmnd            s              2  0
     d wpakod          s              2  0
     d wpdat1          s              6  0
     d p_in03          s              1
     d wwaar           s              2  0
     d w_firm          s                   like(l_firm)
     d w_user          s                   like(l_user)
      *
     c     dummy         tag
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av felter                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      *
      * A2BLD  - A2 Bilde for valg av parametre
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * A2BLD Behandle bilde for valg av utskriftskriteria              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     a2taga        tag
      *
      * Bare data
      *
     c     a2tagb        tag
      *
     c                   exfmt     a2bld
      *
      * Gå ut
      *
     c                   if        *inkc = *on                                  .....
     c                   eval      p_in03 = '1'                                     .
     c                   goto      xslutt                                           .
     c                   endif                                                  .....
      *
      *  Kontroll avdeling
      *
     c                   if        a2pavf > a2pavt                                       .....
     c                   eval      *in31 = *on                                               .
     c                   goto      a2taga                                                    .
     c                   endif                                                           .....
      *
      *  Kontroll konto
      *
     c                   if        a2pktf > a2pktt                                       .....
     c                   eval      *in32 = *on                                               .
     c                   goto      a2taga                                                    .
     c                   endif                                                           .....
      *
      * Sjekke om kjøredato er gyldig
      *
     c                   if        a2pkda = *zero                                        .....
     c                   move      udate         a2pkda                                      .
     c                   endif                                                           .....
      *
     c                   move      a2pkda        wwaar
      *
     c     *dmy          test(d)                 a2pkda                 33
     c                   if        *in33 = *on                                           .....
     c                   goto      a2taga                                                    .
     c                   endif                                                           .....
      *
      *  Kontroll av regnskapsår
      *
     c                   if        a2prÅr > *year                                        .....
     c                   eval      *in34 = *on                                               .
     c                   goto      a2taga                                                    .
     c                   endif                                                           .....
      *
      *  Kontroll av periode fra - til
      *
     c                   if        a2ppef > a2ppet                                       .....
     c                   eval      *in35 = *on                                               .
     c                   goto      a2taga                                                    .
     c                   endif                                                           .....
      *
      * Skriv parameter
      *
     c                   move      a2pkda        rhpkda
     c                   move      a2prÅr        rhprÅr
     c                   move      a2pper        rhpper
     c                   move      a2ppef        rhppef
     c                   move      a2ppet        rhppet
     c                   move      a2pavf        rhpavf
     c                   move      a2pavt        rhpavt
     c                   move      a2pktf        rhpktf
     c                   move      a2pktt        rhpktt
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c                   eval      wpakod = *zero
     c                   eval      wpdat1 = 0
      *
      * Legge inn firmanummer fra parameter
      *
     c                   move      l_user        w_user
     c                   move      l_firm        w_firm
     c                   eval      a2pavf = 0
     c                   eval      a2pavt = 9999
     c                   eval      a2pktf = 0
     c                   eval      a2pktt = 99999
     c                   eval      a2spsf = 0
     c                   eval      a2spst = 9999
      *
     c                   move      udate         a2pkda
      *
      * Beregner periode
      *
     c                   move      *year         waar
      *
     c                   eval      wmnd = *month - 1
      *
     c                   if        wmnd = *zero                                          .....
     c                   movel     12            wmnd                                        .
     c                   eval      waar = waar - 1                                           .
     c                   move      waar          a2prÅr                                      .
     c                   else                                                            .....
     c                   move      waar          a2prÅr                                      .
     c                   endif                                                           .....
      *
     c                   move      wmnd          a2pper
     c                   move      wmnd          a2ppef
     c                   move      wmnd          a2ppet
      *
     c                   eval      p_in03 = ' '
      *
      * Hent bruker-record
      *
     c     key03         chain     ausrl1                             63
      *
      *  Sjekk om begrenset til avdeling
      *
     c                   if        abbavd = '1'                                          .....
     c                   eval      *in30 = *on                                               .
     c                   move      absavd        a2pavf                                      .
     c                   move      absavd        a2pavt                                      .
     c                   endif                                                           .....
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_in03
      *
      * Key for bruker-file
      *
     c     key03         klist
     c                   kfld                    w_user
      *
     c                   endsr
