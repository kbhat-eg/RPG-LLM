     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : LL814R
      * Beskrivelse . . : Oppdater telle-register, Oppdateringen
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       261299 ASK  Endret oppdatering av varegruppe
      *       130100 HKO  Skrevet om programmet
      *       180100 ASK  Legger til enhet fra vareregister hvis blank
5.70  *       021208 bof  Utvidet med prisgruppe
      * 6.10  180609 BSA  Oppdatert med sporingsinformasjon
8.01  *PRG2   150622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fltell1    if   e           k disk    rename(ltelpfr:ltell1r)
     fltellu    uf   e           k disk    rename(ltelpfr:ltellur)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     flbchl1    if   e           k disk    rename(lbchpfr:lbchl1r)
     fvhisl3    if   e           k disk    rename(vhispfr:vhisl3r)
      *
      * Definering av Local data area
      *
     d                uds
6.10 d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av parametre
      *
     d p_list          s             64
      *
      * Definering av parameter som datastruktur
      *
     d                 ds
     d d_list                        64
     d  d_batc                       10    overlay(d_list)
     d  d_val1                        1  0 overlay(d_list:11)
     d  d_val2                        1  0 overlay(d_list:12)
     d  d_val3                        1  0 overlay(d_list:13)
     d  d_val4                        1  0 overlay(d_list:14)
      *
      * Definering av variabler i nøkler
      *
     d ltell1_batc     s                   like(lebatc)
     d ltellu_batc     s                   like(lebatc)
     d ltellu_numm     s                   like(lenumm)
     d ltellu_suff     s                   like(lesuff)
     d ltellu_line     s                   like(leline)
     d vvarl1_vare     s                   like(vvvare)
     d lbchl1_batc     s                   like(lcbatc)
     d vhisl3_vare     s                   like(vhvare)
     d vhisl3_lage     s                   like(vhlage)
     d vhisl3_dato     s                   like(vhdato)
     d vhisl3_time     s                   like(vhtime)
      *
      * Definering av variable
      *
     d w_lety          s              1  0
     d w_inlr          s              1
     d w_bupr          s              9  2
     d w_inpr          s              9  2
5.70 d w_avde          s              4  0
8.01 d w_prgr          s              2
      *
     d w_firm          s                   like(lefirm)
     d w_enh1          s                   like(leenh1)
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser telleregiser på batch-nummer
      *
     c                   eval      ltell1_batc = d_batc
     c     ltell1_key    setll     ltell1
     c     ltell1_key    reade     ltell1                                 90
     c                   dow       *in90 = *off
      *
     c                   if        lesuff <> 0
     c                   goto      neste_rad
     c                   endif
      *
      * Henter vareinformasjon
      *
     c                   eval      vvarl1_vare = levare
     c     vvarl1_key    chain     vvarl1                             91
     c                   if        *in91 = *on
     c                   goto      neste_rad
     c                   endif
      *
      * Oppdatering av telle-register
      *
     c                   eval      ltellu_batc = lebatc
     c                   eval      ltellu_numm = lenumm
     c                   eval      ltellu_suff = lesuff
     c                   eval      ltellu_line = leline
     c     ltellu_key    chain     ltellur                            91
     c                   if        *in91 = *on
     c                   goto      neste_rad
     c                   endif
      *
      * Henter enhet
      *
     c                   eval      w_enh1 = leenh1
      *
     c                   if        d_val2 = 1 and
     c                             ledlop = 0
     c                   eval      leenh1 = vvenhl
     c                   if        letek1 = *blank
     c                   eval      letek1 = vvtek1
     c                   endif
     c                   endif
      *
      * Henter salgspris/kostpris. Prisene hentes dersom dette er valg,
      * eller dersom enhet er endret
      *
     c                   if        ledlop = 0
     c                   if        d_val1 = 1 or
     c                             w_enh1 <> *blank and
     c                             w_enh1 <> leenh1
      *
     c                   eval      w_lety = 0
     c                   eval      w_inlr = *off
5.70 c                   exsr      hent_prisgr
      *
     c                   call      'VP701R'
     c                   parm                    w_firm
     c                   parm                    levare
5.70 c                   parm                    w_prgr
     c                   parm                    leenh1
     c                   parm                    w_lety
     c                   parm                    lcbdat
     c                   parm                    w_inlr
     c                   parm                    lespny
     c                   parm                    w_bupr
     c                   parm                    lekpny
     c                   parm                    w_inpr
      *
     c                   endif
     c                   endif
      *
      * Henter varegruppe
      *
     c                   if        d_val3 = 1
     c                   eval      leogrp = vvogrp
     c                   eval      lehgrp = vvhgrp
     c                   eval      leugrp = vvugrp
     c                   endif
      *
      * Henter lagersaldo
      *
     c                   if        d_val4 = 1
     c                   exsr      hent_saldo
     c                   endif
      *
      * Oppdaterer
      *
6.10 c                   time                    leedat
6.10 c                   time                    leetim
6.10 c                   eval      leeusr = l_user
     c                   update    ltellur
      *
     c     neste_rad     tag
      *
     c     ltell1_key    reade     ltell1                                 90
     c                   enddo
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å hente siste lagersaldo
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_saldo    begsr
      *
      * Posisjonerer databasen, og leser siste post
      *
     c                   eval      vhisl3_vare = levare
     c                   eval      vhisl3_lage = lelage
     c                   eval      vhisl3_dato = lcbdat
     c                   eval      vhisl3_time = lcbtim
      *
     c     vhisl3_key    setll     vhisl3
      *
     c                   readp     vhisl3
      *
     c                   if        not %eof and
     c                             vhvare = levare
     c                   eval      leantl = vhbehl
     c                   else
     c                   eval      leantl = 0
     c                   endif
      *
     c                   endsr
5 70  *
5.70  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5 70  * Subrutine for å hente prisgruppe
5.70  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.70  *
5.70 c     hent_prisgr   begsr
5.70  *
5.70 c                   eval      w_avde = 0
5.70 c                   call      'VL712R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    lelage
5.70 c                   parm                    w_avde
5.70 c                   parm                    w_prgr
5.70  *
5.70 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_list
      *
      * Key for les på lager-telle-regsiter
      *
     c     ltell1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ltell1_batc
      *
      * Key for oppdatering av lager-telle-regsiter
      *
     c     ltellu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ltellu_batc
     c                   kfld                    ltellu_numm
     c                   kfld                    ltellu_suff
     c                   kfld                    ltellu_line
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på batch-register
      *
     c     lbchl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbchl1_batc
      *
      *  Key for les av historisk lagerbok
      *
     c     vhisl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhisl3_vare
     c                   kfld                    vhisl3_lage
     c                   kfld                    vhisl3_dato
     c                   kfld                    vhisl3_time
      *
      *
      * Kopierer parametre til datastruktur
      *
     c                   eval      d_list = p_list
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
      * Hent opplysninger fra batch-regsiter
      *
     c                   eval      lbchl1_batc = d_batc
     c     lbchl1_key    chain     lbchl1                             90
      *
     c                   endsr
