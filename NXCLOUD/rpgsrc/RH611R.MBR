      /TITLE  Utskrift av kontoplan
     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RH611R                                              *
      * Beskrivelse . . : Utskrift av kontoplan                               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      *                                                                       *
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KA = F1  = Valg av firma                                  *
      *       KC = F3  = Exit                                           *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     frhovl1    if   e           k disk    rename(rhovpfr:rhovl1r)
     frhovl3    if   e           k disk    rename(rhovpfr:rhovl3r)
     frhovl4    if   e           k disk    rename(rhovpfr:rhovl4r)
     frh611p    o    e             printer oflind(*in30)
      *
     d avd             s              4  0 dim(25)
     d txt             s             30    dim(3) ctdata perrcd(1)
      *
      /eject
      *
     d                 ds
      *
     d nybrd                         13  0                                      Bruddb
     d  nyavd                         4  0 overlay(nybrd)                       Avdeli
     d  nykto                         5  0 overlay(nybrd:5)                     Konto
     d   nygrp                        2  0 overlay(nykto:2)                     Gruppe
     d    nykla                       1  0 overlay(nygrp)                       Klasse
     d  nysps                         4  0 overlay(nybrd:10)                    Spesif
      *
     d                 ds
     d gmbrd                         13  0                                      Bruddb
     d  gmavd                         4  0 overlay(gmbrd)                       Avdeli
     d  gmkto                         5  0 overlay(gmbrd:5)                     Konto
     d   gmgrp                        2  0 overlay(gmkto:2)                     Gruppe
     d    gmkla                       1  0 overlay(gmgrp)                       Klasse
     d  gmsps                         4  0 overlay(gmbrd:10)                    Spesif
      *
     d                 ds
     d wkonto                         5  0
     d  rhkkla                        2  0 overlay(wkonto)
     d  rhkgrp                        2  0 overlay(wkonto:2)
      *
     d                uds
     d rhpavf                300    303  0
     d rhpavt                         4  0
     d rhpbØf                         4  0
     d rhpbØt                         4  0
     d rhpktf                         5  0
     d rhpktt                         5  0
     d rhpksp                         1
     d rhpkgs                         1
     d rhpkks                         1
     d rhpsrt                         1
      *
     d  l_wsid               901    910
     d  l_user               911    920
     d  l_fgrp               931    933
     d  l_firm               944    946  0
     d  l_fnav               951    980
      *
     d antkto          s              5  0
     d nyside          s              1
     d wspmrk          s              1
      *
     d w_firm          s                   like(rhfirm)
     d p_stat          s              1
     d p_tx30          s             30
     d p_gkod          s                   like(rhavde)
     d p_bkod          s                   like(rhspes)
     d p_skod          s              2  0
     d p_tkod          s              2  0
      *
      /eject
      *
      * Les kontoplan
      *
     c                   select
      *
     c                   when      rhpsrt = 'K'                                          .....
     c     rhovl1_key    setll     rhovl1r                                60
     c     rhovl1_key    reade     rhovl1r                                60
      *
     c                   when      rhpsrt = 'A'                                          .....
     c     rhovl3_key    setll     rhovl3r                                60
     c     rhovl3_key    reade     rhovl3r                                60
      *
     c                   when      rhpsrt = 'B'                                          .....
     c     rhovl4_key    setll     rhovl4r                                60
     c     rhovl4_key    reade     rhovl4r                                60
      *
     c                   endsl                                                           .....
      *
     c                   dow       *in60 = *off
      *
     c                   move      rhkont        wkonto
      *
      * Sjekk om sperrede konti skal skrives
      *
     c                   if        rhpksp = 'N' and                                      .....
     c                             rhsprk = 'J'                                              .
     c                   goto      neste                                                     .
     c                   endif                                                           .....
      *
      *  Skriv '*' foran på linjen på sperrede konti
      *
     c                   if        rhpksp = 'J' and                                      .....
     c                             rhsprk = 'J'                                              .
     c                   move      '*'           wspmrk                                      .
     c                   else                                                                .
     c                   move      ' '           wspmrk                                      .
     c                   endif                                                           .....
      *
      * Sjekk kontonr. fra/til
      *
     c                   if        rhkont < rhpktf or                                    .....
     c                             rhkont > rhpktt                                       .....
     c                   goto      neste                                                     .
     c                   endif                                                           .....
      *
      * Sjekk avdeling fra/til
      *
     c                   if        rhavde < rhpavf or                                    .....
     c                             rhavde > rhpavt                                       .....
     c                   goto      neste                                                     .
     c                   endif                                                           .....
      *
      * Sjekk kostnadsbærer fra/til
      *
     c                   if        rhspes < rhpbØf or                                    .....
     c                             rhspes > rhpbØt                                       .....
     c                   goto      neste                                                     .
     c                   endif                                                           .....
      /eject
      *
      * Sjekk om brudd i kontrollbegrepet
      *
     c     brudd         tag
      *                                                                 .
      * Lag nytt bruddbegrep                                            .
      *                                                                 .
     c                   if        rhavde <> *zero                                    .....  .
     c                   move      rhavde        nyavd                                    .  .
     c                   endif                                                        .....  .
      *                                                                 .
     c                   move      rhkont        nykto                                       .
     c                   move      rhspes        nysps                                       .
      *                                                                 .
      * Dersom kontorekkefølge, nullstill avd i bruddbegrep            .
      *                                                                 .
     c                   if        rhpsrt = 'K'                                       .....  .
     c                   eval      nyavd = *zeros                                         .  .
     c                   endif                                                        .....  .
      *                                                                 .
      * Test hele bruddbegrepet - hvis like, gå til neste record        .
      *                                                                 .
     c                   if        nybrd = gmbrd                                      .....  .
     c                   goto      neste                                                  .  .
     c                   endif                                                        .....  .
      *                                                                 .
      /eject
      *
      * Brudd på avdeling - (kun hvis sortert på avdeling)
      *  - Skap brudd på klasse/gruppe
      *
     c                   if        rhpsrt = 'A'                                          .....
      *
     c                   if        nyavd <> gmavd                                     .....  .
     c                   exsr      brdavd                                                 .  .
     c                   eval      gmbrd = *zero                                          .  .
     c                   eval      nyside = *blank                                        .  .
     c                   endif                                                        .....  .
      *                                                                 .
     c                   if        nyavd = *zero                                      .....  .
     c                   exsr      brdavd                                                 .  .
     c                   endif                                                        .....  .
      *                                                                 .
     c                   endif                                                           .....
      *
      * Brudd på kostnadsbærer - (kun hvis sortert på kostnadsbærer)
      *  - Skap brudd på klasse/gruppe
      *
     c                   if        rhpsrt = 'B'                                          .....
      *
     c                   if        nysps <> gmsps                                     .....  .
     c                   exsr      brdsps                                                 .  .
     c                   eval      gmbrd = *zero                                          .  .
     c                   eval      nyside = *blank                                        .  .
     c                   endif                                                        .....  .
      *                                                                 .
     c                   if        nysps = *zero                                      .....  .
     c                   exsr      brdsps                                                 .  .
     c                   endif                                                        .....  .
      *                                                                 .
     c                   endif                                                           .....
      *
      * Hvis første godkjente konto, skriv heading
      *
     c                   if        nyside = *blank                                       .....
     c                   move      'N'           nyside                                      .
     c                   write     a1hdr                                                     .
     c                   endif                                                           .....
      *
     c                   eval      antkto = antkto + 1
      /space 3
      *
      * Brudd på klasse - skrives ?
      *
     c                   if        rhpkks = 'J' and                                      .....
     c                             nykla <> gmkla                                     .....  .
     c                   exsr      brdkla                                                 .  .
      *
     c                   if        *in30 = *on
     c                   write     a1hdr                                                  .  .
     c                   eval      *in30 = *off                                           .  .
     c                   endif
      *
     c                   write     t1kla                                                  .  .
     c                   endif                                                        .....  .
      /space 3
      *
      * Brudd på gruppe - skrives ?
      *
     c                   if        rhpkgs = 'J' and                                      .....
     c                             nygrp <> gmgrp                                     .....  .
     c                   exsr      brdgrp                                                 .  .
      *
     c                   if        *in30 = *on
     c                   write     a1hdr                                                  .  .
     c                   eval      *in30 = *off                                           .  .
     c                   endif
      *
     c                   write     t1grp                                                  .  .
     c                   endif                                                           .....
      /eject
      *
      * Skriv linje
      *
     c                   if        *in30 = *on
     c                   write     a1hdr                                                     .
     c                   eval      *in30 = *off                                           .  .
     c                   endif
      *                                                                 .
     c                   eval      d1kont = rhkont                                        .  .
     c                   eval      d1spes = rhspes                                        .  .
     c                   eval      d1text = rhtext                                        .  .
     c                   eval      d1txt2 = rhtxt2                                        .  .
     c                   eval      d1sprk = rhsprk                                        .  .
     c                   eval      d1nØkk = rhnØkk                                        .  .
     c                   eval      d1mvak = rhmvak                                        .  .
     c                   eval      d1mvao = rhmvao                                        .  .
     c                   eval      d1mvag = rhmvag                                        .  .
     c                   eval      d1bnØk = rhbnØk                                        .  .
     c                   eval      d1proj = rhproj                                        .  .
     c                   eval      d1avdk = rhavdk                                        .  .
     c                   write     d1det                                                     .
      *                                                                 .
      * Flytt nytt bruddbegrep til gammelt                              .
      *                                                                 .
     c                   move      nybrd         gmbrd                                       .
      *
      /space 3
      *
     c     neste         tag
      *
     c                   select
      *
     c                   when      rhpsrt = 'K'                                          .....
     c     rhovl1_key    reade     rhovl1r                                60
      *
     c                   when      rhpsrt = 'A'                                          .....
     c     rhovl3_key    reade     rhovl3r                                60
      *
     c                   when      rhpsrt = 'B'                                          .....
     c     rhovl4_key    reade     rhovl4r                                60
      *
     c                   endsl                                                           .....
      *
     c                   enddo                                                           .....
      *
      /space 3
      *
     c     slutt         tag
      *
     c                   eval      t1anta = antkto
     c                   write     t1tot
     c                   eval      *inlr = *on
      /eject
      *
      * Subrutine for brudd på avdeling
      *
     c     brdavd        begsr
      *
     c                   move      rhavde        p_gkod
     c                   call      'RS707R'                                      avdelings-
     c                   parm                    p_stat
     c                   parm                    p_gkod
     c                   parm                    p_tx30
      *
     c                   eval      a1gkod = p_gkod
     c                   eval      a1gtxt = p_tx30
      *
     c                   endsr
      /space 3
      *
      * Subrutine for brudd på kostnadsbærer
      *
     c     brdsps        begsr
      *
     c                   move      rhspes        p_bkod
     c                   call      'RS728R'                                      avdelings-
     c                   parm                    p_stat
     c                   parm                    p_bkod
     c                   parm                    p_tx30
      *
     c                   eval      a1bkod = p_bkod
     c                   eval      a1btxt = p_tx30
      *
     c                   endsr
      /space 3
      *
      * Subrutine for brudd på kontoklasse
      *
     c     brdkla        begsr
      *
     c                   move      rhkkla        p_skod
     c                   call      'RS719R'                                      klasse-
     c                   parm                    p_stat
     c                   parm                    p_skod
     c                   parm                    p_tx30
      *
     c                   eval      t1skod = p_skod
     c                   eval      t1stxt = p_tx30
      *
     c                   endsr
      /space 3
      *
      * Subrutine for brudd på kontogruppe
      *
     c     brdgrp        begsr
      *
     c                   move      rhkgrp        p_tkod
     c                   call      'RS720R'                                      gruppe-
     c                   parm                    p_stat
     c                   parm                    p_tkod
     c                   parm                    p_tx30
      *
     c                   eval      t1tkod = p_tkod
     c                   eval      t1ttxt = p_tx30
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Sett startverdier
      *
     c                   eval      nybrd = *zero
     c                   eval      gmbrd = *zero
     c                   eval      w_firm = l_firm                                           .
      *
     c                   select
      *
     c                   when      rhpsrt = 'A'                                          .....
     c                   eval      *in15 = *on                                               .
      *
     c                   when      rhpsrt = 'B'                                          .....
     c                   eval      *in16 = *on                                               .
      *
     c                   endsl                                                           .....
      *
     c                   eval      antkto = *zeros
     c                   eval      nyside = *blank
      *
     c***                write     a1hdr                                                  .  .
      *
      * Key for konto-file
      *
     c     rhovl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for konto-fil pr. avdeling
      *
     c     rhovl3_key    klist
     c                   kfld                    w_firm
      *
      * Key for konto-fil pr. kostnadsbærer
      *
     c     rhovl4_key    klist
     c                   kfld                    w_firm
      *
     c                   endsr
**
K L A S S E S U M
G R U P P E S U M
A V D E L I N G
