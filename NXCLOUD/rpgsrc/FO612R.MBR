      /TITLE  ASOFAK: Flytter parameter til ny member i nytt register
     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO612R
      * Beskrivelse . . : Flytter parameter til member i nytt register
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * R3.30 140201 AMD  Programmet er skrevet om til std. ILE
6.nu  * R6.30 230614 bof  Sjekket mtp. nummerutvidelse
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffpm1l1    ip   e           k disk    rename(fpm1pfr:fpm1l1r)
     ffpm1lu    uf   e           k disk    rename(fpm1pfr:fpm1lur)
     ffpm2lu    uf a e           k disk    rename(fpm2pfr:fpm2lur)
      *
      * Definering av data-struktur
      *
     d                 ds
     d d_memb                        10
     d  d_boks                        1    overlay(d_memb)
     d  d_firm                        3  0 overlay(d_memb:2)
     d  d_list                        6  0 overlay(d_memb:5)
      *
      * Definering av local data area
      *
     d                uds
     d l_vlg1                500    500  0
     d l_vlg2                         1  0
     d l_memb                        10
     d l_vlg3                599    599  0
     d l_wsid                901    910
     d l_user                        10
      *
      * Definering av variable i nøkler
      *
     d w_firm          s                   like(fk1fir)
     d fpm1lu_1wsd     s                   like(fk1wsd)
     d fpm1lu_1usr     s                   like(fk1usr)
     d fpm1lu_1lst     s                   like(fk1lst)
      *
      * Definering av variable
      *
     d w_numm          s              6  0
     d w_stat          s              1
      *
      * Internt definert file
      *
     ifpm1l1r       01
     i                                          fk1fir
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Behandler oppstart av utskrift, en pr. gang
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Blanker verdier hver gang, verdier som også benyttes i CL-pros.
      *
     c                   eval      l_vlg1 = *zero
     c                   eval      l_vlg2 = *zero
     c                   eval      l_memb = *blank
      *
      *  Hopper ut dersom record ikke funnet
      *
     c     *in01         cabeq     *off          xslutt
      *
      *  Sjekk,  At kun parameter for denne SKJERM taes med
      *
     c     fk1wsd        cabne     l_wsid        xslutt
      *
      *  Sjekk,  At kun parameter for denne BRUKER taes med
      *
     c     fk1usr        cabne     l_user        xslutt
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Behandler flytting av PARAM1 til PARAM2    (Record funnet)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Sett inn 1, som sier at rutinen skal starte FO610C
      *
     c                   eval      l_vlg1 = 1
      *
      *  Legger inn 2 som sier at jobben  skal kjøres fra jobbkø
      *
     c                   if        fk1vg0 = 2
     c                   eval      l_vlg1 = 2
     c                   endif
      *
      *  Legger over record fra PARAM1 til PARAM2 (Like registre)
      *
     c                   eval      w_firm = fk1fir
     c     fpm2lu_key    chain     fpm2lur                            90
     c                   if        *in90 = *on
     c                   eval      fk2fir = fk1fir
     c                   eval      fk2lst = fk1lst
     c                   eval      fk2oty = fk1oty
     c                   eval      fk2wsd = fk1wsd
     c                   eval      fk2usr = fk1usr
     c                   eval      fk2utq = fk1utq
     c                   eval      fk2onr = fk1onr
     c                   eval      fk2suf = fk1suf
     c                   eval      fk2vg0 = fk1vg0
     c                   eval      fk2vg1 = fk1vg1
     c                   eval      fk2vg2 = fk1vg2
     c                   eval      fk2per = fk1per
     c                   eval      fk2dat = fk1dat
     c                   write     fpm2lur
     c                   endif
      *
      *  Sletter recorden i PLUKK-1-LINJE-REGISTER
      *
     c                   eval      w_firm = fk1fir
     c                   eval      fpm1lu_1wsd = fk1wsd
     c                   eval      fpm1lu_1usr = fk1usr
     c                   eval      fpm1lu_1lst = fk1lst
     c     fpm1lu_key    chain     fpm1lur                            90
     c                   if        *in90 = *off
     c                   delete    fpm1lur
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sjekker om rutinen skal kjøres flere ganger,
      *  den skal kun kjøres flere ganger dersom ordrenummer er null
      *  Ordrenummer lik null, er utskrift av mange ordrenummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   if        fk1onr = *zero
      *
      *  Henter siste nummer for File-member
      *
     c                   call      'FA920R'
     c                   parm                    w_stat
     c                   parm                    w_numm
      *
      *  Danner navn på Filemembers for NESTE (NESTE) utskrift
      *
     c                   eval      d_boks = 'A'
     c                   eval      d_firm = fk1fir
     c                   eval      d_list = w_numm
      *
     c                   eval      l_memb = d_memb
      *
      *  Sett inn 1, som sier at rutinen skal starte pånytt igjen
      *
     c                   eval      l_vlg2 = 1
     c                   eval      l_vlg3 = 1
     c                   endif
      *
      *  Sett på LR for at record er funnet og programmet skal avsluttes
      *
     c                   eval      *inlr = *on
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xslutt        tag
      *
     clr                 return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     c                   eval      w_stat = *zero
     c                   eval      w_numm = *zero
      *
      *  Key for file : PLUKK-1-LINJE-REGISTER
      *
     c     fpm1lu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fpm1lu_1wsd
     c                   kfld                    fpm1lu_1usr
     c                   kfld                    fpm1lu_1lst
      *
      *  Key for file : PLUKK-2-LINJE-REGISTER
      *
     c     fpm2lu_key    klist
     c                   kfld                    w_firm
      *
     c                   endsr
