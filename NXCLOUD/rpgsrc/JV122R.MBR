     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JV122R
      * Beskrivelse . . : Overføring tjeneste varer til EVR, parameterprog
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       270814 BKV  Nytt program
6.31  *       170914 BKV  Advarsel om ugyldige enheter
6.32  *       081014 BKV  Byttet søke prg JV501
      *                   Feilmelding hvis ogr <> 1
6.33  *       180515 BKV  Endret LVAR pga økt fra 15 til 20 tegn
7.xx  * 7.00  040316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fvltpl1    if   e           k disk    rename(vltppfr:vltpl1r)
     fra30l1    if   e           k disk    rename(ra30pfr:ra30l1r)
     fjsorl1    if   e           k disk    rename(jsorpfr:jsorl1r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
6.32 fjsokl3    if   e           k disk    rename(jsokpfr:jsokl3r)
      *
     fjv122d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variable i nøkler
      *
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d vltpl1_lety     s                   like(vylety)
     d ra30l1_dkod     s                   like(redkod)
     d jsorl1_osor     s                   like(jsosor)
     d jvarl1_vare     s                   like(jvvare)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s              3  0
     d d_antw          s              5  0
     d d_antu          s              5  0
6.31 d d_enhm          s             15

     d w_dato          s               d   datfmt(*iso)

6.32 d w_tek2          s             30
6.32 d w_ldor          s              6  0
6.33 d w_lvar          s                   like(jvlvar)
6.32 d w_eann          s             14  0
6.32 d w_nobb          s              8  0
6.32 d w_var2          s                   like(jvvare)
6.32 d w_ldo2          s                   like(jvldor)
6.32 d w_stek_pos      s                   like(jsstek)
6.32 d w_tek1_pos      s                   like(jvtek1)
6.32 d w_vare_pos      s                   like(jvvare)
6.32 d w_nobb_pos      s                   like(jvnobb)
6.32 d w_lvar_pos      s                   like(jvlvar)


      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     b1tag         tag
     c                   eval      b1ogrp = 1
      *
      * Behandler bildet
      *
     c     b1taga        tag
     c                   exfmt     b1bld
      *
     c                   eval      b1ofer = ''
     c                   eval      b1anto = 0
6.31 c                   eval      b1enhm = ''
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
6.32 c                   exsr      sjekk_input
     c                   goto      b1taga
     c                   endsl
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1taga
     c                   endif
      *
     c                   eval      w_dato = *loval


      *
      * F20=Start overføring
      *
     c                   if        *inku = *on
      *
      * Kaller overføringsprogram
      *
     c                   call      'JV622R'
     c                   parm                    w_firm
     c                   parm                    b1ogrp
     c                   parm                    b1hgrp
     c                   parm                    b1ugrp
     c                   parm                    b1vare
     c                   parm                    d_antw
     c                   parm                    d_antu
6.31 c                   parm                    d_enhm
      *
     c                   eval      b1anto = d_antw + d_antu
     c                   eval      b1ofer = 'Overføring ferdig'
6.31 c                   if        d_enhm <> ''
6.31 c                   eval      b1enhm = 'Du har overført varer med følgende'
6.31 c                             +' ugylidge enheter: ' +d_enhm
6.31 c                   else
6.31 c                   eval      b1enhm = ''
6.31 c                   endif
     c                   endif

     c                   goto      b1taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørringer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Overgruppe
      *
     c                   if        w_afld = 'B1OGRP'
     c                   eval      b1hgrp = 0
     c                   eval      b1ugrp = 0
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b1ogrp
     c                   parm                    b1utxt
     c                   leavesr
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        w_afld = 'B1HGRP'
     c                   eval      b1ugrp = 0
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    b1ogrp
     c                   parm                    b1hgrp
     c                   parm                    b1utxt
     c                   leavesr
     c                   endif
      *
      * Undergruppe
      *
     c                   if        w_afld = 'B1UGRP'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    b1ogrp
     c                   parm                    b1hgrp
     c                   parm                    b1ugrp
     c                   parm                    b1utxt
     c                   leavesr
     c                   endif
      *
      * Vare
      *
     c                   if        w_afld = 'B1VARE'
6.32 c                   call      'JV501R'
6.32 c                   parm                    w_firm
6.32 c                   parm                    b1vtxt
6.32 c                   parm                    w_tek2
6.32 c                   parm                    w_ldor
6.32 c                   parm                    w_lvar
6.32 c                   parm                    w_eann
6.32 c                   parm                    w_nobb
6.32 c                   parm                    b1vare
6.32 c                   parm                    w_ldo2
      *
6.32 c                   parm                    b1ogrp
6.32 c                   parm                    b1hgrp
6.32 c                   parm                    b1ugrp
6.32 c                   parm                    w_stek_pos
6.32 c                   parm                    w_tek1_pos
6.32 c                   parm                    w_vare_pos
6.32 c                   parm                    w_nobb_pos
6.32 c                   parm                    w_lvar_pos
6.32 c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
     c                   eval      b1utxt = *blank
      *
      * Overgruppe
      *
6.32 c                   if        b1ogrp <> 1
6.32 c                   eval      b1ogrp = 1
6.32 c                   eval      *in38 = *on
6.32 c                   eval      b_feil = *on
6.32 c                   leavesr
6.32 c                   endif

     c                   if        b1ogrp <> 0 and
     c                             b1hgrp = 0 and
     c                             b1ugrp = 0
     c                   eval      vogrl1_oogr = b1ogrp
     c     vogrl1_key    chain     vogrl1                             33
     c                   if        *in33 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      b1utxt = vgotxt
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        b1hgrp <> 0 and
     c                             b1ugrp = 0
     c                   eval      vhgrl1_hogr = b1ogrp
     c                   eval      vhgrl1_hhgr = b1hgrp
     c     vhgrl1_key    chain     vhgrl1                             34
     c                   if        *in34 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      b1utxt = vghtxt
     c                   endif
      *
      * Undergruppe
      *
     c                   if        b1ugrp <> 0
     c                   eval      vugrl1_uogr = b1ogrp
     c                   eval      vugrl1_uhgr = b1hgrp
     c                   eval      vugrl1_uugr = b1ugrp
     c     vugrl1_key    chain     vugrl1                             35
     c                   if        *in35 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      b1utxt = vgutxt
     c                   endif
      *
      *
      * vare-nummer
      *
     c                   if        b1vare <> ''
     c                   eval      jvarl1_vare = b1vare
     c     jvarl1_key    chain     jvarl1                             36
     c                   if        *in36 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   if        jvogrp <> b1ogrp
     c                   eval      *in37  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   if        (b1hgrp > 0) and (jvhgrp <> b1hgrp)
     c                   eval      *in37  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   if        (b1ugrp > 0) and (jvugrp <> b1ugrp)
     c                   eval      *in37  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      b1vtxt = jvtek1
     c                   else
     c                   eval      b1vtxt = ''
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for les av vare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Henter firma fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
      *
     c                   endsr
