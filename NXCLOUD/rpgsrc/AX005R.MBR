     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . :
      *  Program . . . . : AX005R
      *  Beskrivelse . . : KUNDER    Legger ut XML til SAF-T
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
Versj *Uts   Dato Usr Referanse Beskrivelse
      *____________________________________________________________________________________________
      *K00-200420               START FOR VERSJON K00
7.00  *K00 200420 SST NXS-4426  Utrulling av SAFT-Rapporter til alle kundene
7.01  *K00 261020 SST NXS-6029  Justering av feil tegn i poststed (Skattum)
      *
      *
      *
      *____________________________________________________________________________________________
      *  Dette programmet henter data fra ADBxxx/rkunPF m/RKFIRM = Firma
      *                                og ADBxxx/rukpPF m/RSFIRM = Firma
      *
      *
     fra04l1    if   e           k disk    rename(ra04pfr:ra04l1r)
     fra11l1    if   e           k disk    rename(ra11pfr:ra11l1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     frktrl1    if   e           k disk    rename(rktrpfr:rktrl1r)
     frukpl1    if   e           k disk    rename(rukppfr:rukpl1r)
     frxmll1    if   e           k disk    rename(rxmlpfr:rxmll1r)
      *
     faxmlut    o    e             disk
      *
     d dig1            c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅ'
     d dig2            c                   'abcdefghijklmnopqrstuvwxyzæøå'
     d dig3            c                   '-_+=\/%&() 0123456789.,*@?:;!'
     d w_digj          s              1
      *
     d w_firm          s                       like(radfir)
      *
     d ra11l1_kod      s                       like(rakkod)
      *
     d rkunl1_kund     s                       like(rkkund)
      *
     d rktrl1_kund     s                       like(rnkund)
     d rktrl1_raar     s                       like(rnraar)
     d rktrl1_rper     s                       like(rnrper)
     d rktrl1_bdat     s                       like(rnbdat)
      *
     d rukpl1_kund     s                       like(rukund)
     d rukpl1_levr     s                       like(rulevr)
     d rukpl1_kpnr     s                       like(rukpnr)
      *
     d rxmll1_head     s                       like(axhead)
     d rxmll1_linj     s                       like(axlinj)
      *
     d w_kpnr          s                   like(rukpnr)
     d w_ponr          s              4
     d text            s             50
     d txt2            s             50
     d stmt            s            500A
     d xmlut           s            500A
     d i               s              9S 0
     d j               s              9S 0
     d f               s              3
     d t               s              1
     d tx              s              1
     d fn              s              3S 0
     d tn              s              3S 0
     d s_head          s                   like(axhead)
     d s_linj          s                   like(axlinj)
     d s_kontx         s              5
     d w_iso_dat       s               d   datfmt(*iso)
     d w_x_dat         s             10A
     d w_isalx         s             14
     d w_usalx         s             14
     d w_02            s              2S 2
     d w_s             s             11S 2
     d w_110           s             11S 0
     d w_110x          s             11
     d w_02x           s              2
     d w_isald         s             11S 2
     d w_usald         s             11S 2
     d w_ant_T         s              9S 0
7.01 d w_sted          s             30
7.01 d w_konv          s              1
7.01 d w_po1           s              3  0
7.01 d w_po2           s              3  0
7.01 d w_po3           s              3  0
      * *
     d w_str_i         s            100
     d w_str_u         s            100
     d w_posi          s              2  0
     d w_posu          s              2  0
     d w_str_x         s              1
     d c_apos          c                   x'7D'
      *
     d p_perfm         s              2
     d p_perfa         s              4
     d p_pertm         s              2
     d p_perta         s              4
     d p_pfm           s              2S 0
     d p_pfa           s              4S 0
     d p_ptm           s              2S 0
     d p_pta           s              4S 0
     d p_hcom          s             50
      *
     d                uds
     d l_fgr                 931    933
     d l_firm                944    946  0
     d l_user                911    920
      *
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Start
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     start         tag
      *
     c                   read      rkunl1
     c                   if        not %eof(rkunl1) and
     c                             rkfirm = l_firm
      *
     c                   if        rkpass <> '1'
      *
     c                   eval      rxmll1_head  = 'KUNDE'
     c                   eval      rxmll1_linj  = 10
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1 )
     c                   eval      xmlrec = axxmlt
     c                   write     axmlutr
     c                   endif
      *
      *   Foretaksnummer
      *
     c                   eval      rxmll1_linj = 20
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1 )
     c                   movel     rkkund        text
     c                   if        rkpenr <> *zero
     c                   movel     rkpenr        text
     c                   endif
     c                   if        rkftnr <> *zero
     c                   movel     rkftnr        text
     c                   endif
     c                   exsr      redut
     c                   endif
      *
      *   Navn
      *
     c                   eval      rxmll1_linj = 30
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1 )
     c                   movel     rknavn        text
     c                   exsr      redut
     c                   endif
      *
      *   Adresse
      *
     c                   eval      rxmll1_linj = 110
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1 )
     c                   eval      xmlrec = axxmlt
     c                   write     axmlutr
     c                   endif
      *
      *   Adresse
      *
     c                   eval      rxmll1_linj = 120
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1 )
     c                   movel     rkgate        text
     c                   exsr      redut
     c                   endif
      *
      *   Adresse 2
      *
     c                   eval      rxmll1_linj = 130
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1 )
     c                   movel     rkadr2        text
     c                   exsr      redut
     c                   endif
      *
      *   Poststed
      *
     c                   eval      rxmll1_linj = 140
     c                   eval      w_ponr = *blank
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1 )
     c                   if        %subst(rksted:1:1) >= '0' and
     c                             %subst(rksted:1:1) <= '9' and
     c                             %subst(rksted:4:1) >= '0' and
     c                             %subst(rksted:4:1) <= '9' and
     c                             %subst(rksted:5:1) = ' '
     c                   movel     rksted        w_ponr
     c                   eval      rksted = %subst(rksted:5:25)
     c                   endif
     c                   movel     rksted        text
     c                   exsr      redut
     c                   endif
      *
      *   Postnummer
      *
     c                   eval      rxmll1_linj  = 150
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1 )
     c                   if        rkponr > 0
     c                   movel     rkponr        text
     c                   else
     c                   movel     w_ponr        text
     c                   endif
     c                   exsr      redut
     c                   endif
      *
      *   Landkode
      *
     c                   eval      rxmll1_linj  = 160
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1 )
     c                   if        rkland = '  '
     c                   move      'NO'          rkland
     c                   endif
     c                   movel     rkland        text
     c                   exsr      redut
     c                   endif
      *
      *   Adressetype
      *
     c                   eval      rxmll1_linj  = 170
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1 )
     c                   exsr      redut
     c                   endif
      *
      *   /Adresse
      *
     c                   eval      rxmll1_linj  = 180
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1 )
     c                   eval      xmlrec = axxmlt
     c                   write     axmlutr
     c                   endif
      *
      *   Contact
      *
     c                   eval      rxmll1_linj  = 190
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1 )
     c                   eval      xmlrec = axxmlt
     c                   write     axmlutr
     c                   endif
      *
      *   Contact
      *
     c                   eval      rukpl1_kund = rkkund
     c                   eval      rukpl1_levr = *zero
     c                   eval      rukpl1_kpnr = *zero
     c                   eval      w_kpnr      = *zero
     c     rukpl1_key    setll     rukpl1
     c                   read      rukpl1
     c                   dow       not %eof(rukpl1) and
     c                             rukund = rkkund
      *
      *  Dersom en kont.pers har status X skal denne rapporteres
      *
     c                   if        rustat = 'X'
     c                   leave
     c                   endif
      *
      *  Hvis ikke rapporteres den første som finnes
      *
     c                   if        w_kpnr = *zero
     c                   eval      w_kpnr = rukpnr
     c                   endif
     c                   read      rukpl1
     c                   enddo
      *
     c                   if        rustat <> 'X'
     c                   eval      rukpl1_kpnr = w_kpnr
     c     rukpl1_key    chain     rukpl1
     c                   if        not %found(rukpl1)
     c                   eval      rufnav = *blank
     c                   eval      ruenav = *blank
     c                   endif
     c                   endif
      *
      *   ContactPerson
      *
     c                   eval      rxmll1_linj  = 200
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1 )
     c                   eval      xmlrec = axxmlt
     c                   write     axmlutr
     c                   endif
      *
      *     First Name
      *
     c                   eval      rxmll1_linj  = 210
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1 )
     c                   movel     rufnav        text
     c                   exsr      redut
     c                   endif
      *
      *     Last Name
      *
     c                   eval      rxmll1_linj  = 220
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1 )
     c                   movel     ruenav        text
     c                   exsr      redut
     c                   endif
      *
      *   /ContactPerson
      *
     c                   eval      rxmll1_linj  = 230
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1 )
     c                   eval      xmlrec = axxmlt
     c                   write     axmlutr
     c                   endif
      *
     c     TNoKpnr       tag
      *
      *     Telephone
      *
     c                   eval      rxmll1_linj  = 240
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1 )
     c                   eval      text = *blank
     c                   select
     c                   when      ruktlf <> *blank
     c                   movel     ruktlf        text
     c                   when      rumtlf <> *blank
     c                   movel     rumtlf        text
     c                   other
     c                   movel     rktlfn        text
     c                   endsl
     c                   if        text <> *blank
     c                   exsr      redut
     c                   endif
     c                   endif
      *
      *     Fax
      *
     c                   eval      rxmll1_linj  = 250
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1 )
     c                   eval      text = *blank
     c                   select
     c                   when      rukfax <> *blank
     c                   movel     rukfax        text
     c                   other
     c                   movel     rktlfx        text
     c                   endsl
     c                   if        text <> *blank
     c                   exsr      redut
     c                   endif
     c                   endif
      *
      *     Email
      *
     c                   eval      rxmll1_linj  = 260
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1 )
     c                   eval      text = *blank
     c                   select
     c                   when      rumail <> *blank
     c                   movel     rumail        text
     c                   other
     c                   movel     rkemal        text
     c                   endsl
     c                   if        text <> *blank
     c                   exsr      redut
     c                   endif
     c                   endif
      *
      *     Web-adresse
      *
     c                   eval      rxmll1_linj  = 270
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1 ) and
     c                             rkweba <> *blank
     c                   movel     rkweba        text
     c                   exsr      redut
     c                   endif
      *
      *   /Contact
      *
     c                   eval      rxmll1_linj  = 280
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1 )
     c                   eval      xmlrec = axxmlt
     c                   write     axmlutr
     c                   endif
      *
      *     CustomerID
      *
     c                   eval      rxmll1_linj  = 290
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1 )
     c                   movel     rkkund        text
     c                   exsr      redut
     c                   endif
      *
      *     AccountID
      *
     c                   eval      ra11l1_kod = rkkatg
     c     ra11l1_key    chain     ra11l1
     c                   if        %found(ra11l1) and
     c                             rakkto > 0
      *
      *    Konto fra kundekat.
      *
     c                   movel     rakkto        text
     c                   else
      *
      *    Konto fra faste kontonummer
      *
     c                   movel     radk05        text
     c                   endif
     c                   eval      rxmll1_linj  = 300
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1 )
     c                   exsr      redut
     c                   endif
      *
      *    OpeningBalance
      *
     c                   eval      w_isald = *zero
     c                   eval      w_usald = *zero
     c                   eval      rktrl1_kund = rkkund
     c                   eval      rktrl1_raar = *zero
     c                   eval      rktrl1_rper = *zero
     c                   eval      rktrl1_bdat = *loval
     c     rktrl1_key    setll     rktrl1
     c                   read      rktrl1
     c                   dow       not %eof(rktrl1) and
     c                             rnkund = rkkund  and
     c                             (rnraar < p_pfa  or
     c                             (rnraar = p_pfa  and
     c                              rnrper < p_pfm))
     c                   eval      w_isald = w_isald + rnbelØ
     c                   read      rktrl1
     c                   enddo
      *
      *    Legge ut inng.saldo
      *
     c                   eval      w_s = w_isald
     c                   if        w_s < 0
     c                   eval      w_s = w_s * -1
     c                   endif
     c                   eval      w_110 = w_s
     c                   eval      w_02  = w_s - w_110
     c                   move      w_110         w_110x
     c                   move      w_02          w_02x
     c                   eval      w_isalx = w_110x + '.' + w_02x
     c                   dow       %subst(w_isalx:1:1) = '0'
     c                   eval      %subst(w_isalx:1:13) = %subst(w_isalx:2:13)
     c                   eval      %subst(w_isalx:14:1) = ' '
     c                   enddo
     c                   if        w_isald >= *zero
     c                   eval      rxmll1_linj  = 310
     c                   else
     c                   eval      rxmll1_linj  = 320
     c                   endif
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1 )
     c                   movel     w_isalx       text
     c                   exsr      redut
     c                   endif
      *
      *    ClosingBalance
      *
     c                   eval      w_usald = w_isald
     c                   dow       not %eof(rktrl1) and
     c                             rnkund = rkkund  and
     c                             (rnraar < p_pta   or
     c                             (rnraar = p_pta   and
     c                              rnrper <= p_ptm))
     c                   eval      w_usald = w_usald + rnbelØ
     c                   read      rktrl1
     c                   enddo
      *
      *    Legge ut utg.saldo
      *
     c                   eval      w_s = w_usald
     c                   if        w_s < 0
     c                   eval      w_s = w_s * -1
     c                   endif
     c                   eval      w_110 = w_s
     c                   eval      w_02  = w_s - w_110
     c                   move      w_110         w_110x
     c                   move      w_02          w_02x
     c                   eval      w_usalx = w_110x + '.' + w_02x
     c                   dow       %subst(w_usalx:1:1) = '0'
     c                   eval      %subst(w_usalx:1:13) = %subst(w_usalx:2:13)
     c                   eval      %subst(w_usalx:14:1) = ' '
     c                   enddo
     c                   if        w_usald >= *zero
     c                   eval      rxmll1_linj  = 340
     c                   else
     c                   eval      rxmll1_linj  = 350
     c                   endif
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1 )
     c                   movel     w_usalx       text
     c                   exsr      redut
     c                   endif
      *
      *   /Customer
      *
     c                   eval      rxmll1_linj  = 370
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1 )
     c                   eval      xmlrec = axxmlt
     c                   write     axmlutr
     c                   endif
      *
      *   End_Passiv?
      *
     c                   endif
     c                   goto      start
      *
     c                   endif
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *   End_EOF
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     slutt         tag
      *
      *   /Customers
      *
     c                   eval      rxmll1_linj  = 900
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1 )
     c                   eval      xmlrec = axxmlt
     c                   write     axmlutr
     c                   endif
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Rediger og skriv xml-melding
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     redut         begsr
      *
     c                   exsr      spstgn
      *
     c                   if        axmaop = 'M' or
     c                             text  <> *blank
      *
     c                   eval      i = %scan('?V?':axxmlt)
     c                   if        i > 0
     c                   eval      xmlrec = %subst(axxmlt:1:i-1) +
     c                             %trim(text) +
     c                             %subst(axxmlt:i+3:50)
     c                   goto      TWrite
     c                   endif
      *
     c                   eval      i = %scan('?F?':axxmlt)
     c                   if        i > 0
     c                   eval      xmlrec = %subst(axxmlt:1:i-1) +
     c                             %trim(axfast) +
     c                             %subst(axxmlt:i+3:50)
     c                   goto      TWrite
     c                   endif
      *
     c     TWrite        tag
     c                   write     axmlutr
     c                   endif
      *
     c                   eval      text = *blank
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Spesial-tegn
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     spstgn        begsr
      *
     c                   eval      w_konv = *blank
     c                   eval      w_posi = 0
     c                   eval      w_posu = 0
     c                   eval      w_str_u = *blank
     c                   eval      w_str_i = text
      *
     c                   dow       w_posi < 70
     c                   eval      w_posi = w_posi + 1
     c                   eval      w_str_x = %subst(w_str_i:w_posi:1)
     c                   if        w_posu < 90
     c                   select
     c                   when      w_str_x = '&'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:5) = '&amp;'
     c                   eval      w_konv = 'J'
     c                   eval      w_posu = w_posu + 4
      *
     c                   when      w_str_x = '<'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:4) = '&lt;'
     c                   eval      w_konv = 'J'
     c                   eval      w_posu = w_posu + 3
      *
     c                   when      w_str_x = '>'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:4) = '&gt;'
     c                   eval      w_konv = 'J'
     c                   eval      w_posu = w_posu + 3
      *
     c                   when      w_str_x = c_apos
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:6) = '&apos;'
     c                   eval      w_konv = 'J'
     c                   eval      w_posu = w_posu + 5
      *
     c                   when      w_str_x = '"'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:6) = '&quot;'
     c                   eval      w_konv = 'J'
     c                   eval      w_posu = w_posu + 5
      *
     c                   other
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:1) = w_str_x
      *
     c                   endsl
     c                   endif
     c                   enddo
      *
     c                   eval      text   =  w_str_u
7.01  * Andre tegn
7.01 c     x'20':'A'     xlate     text          text
7.01 c     x'75':'I'     xlate     text          text
7.01 c     x'DB':'Y'     xlate     text          text
7.01 c     x'7B':'U'     xlate     text          text
7.01 c     x'ED':'O'     xlate     text          text
7.01 c     x'a1':'U'     xlate     text          text
7.01 c     x'CF':'O'     xlate     text          text
7.01 c     x'44':'A'     xlate     text          text
7.01 c     x'43':'A'     xlate     text          text
7.01 c     x'45':'A'     xlate     text          text
7.01 c     x'52':'E'     xlate     text          text
7.01 c     x'72':'E'     xlate     text          text
7.01 c     x'36':' '     xlate     text          text
7.01 c     x'BE':x'7D'   xlate     text          text
7.01 c     x'00':' '     xlate     text          text
7.01 c     x'DE':'U'     xlate     text          text
7.01 c     x'63':'E'     xlate     text          text
      *  À
7.01 c     x'64':'A'     xlate     text          text
      *
7.ss  *
7.ss  *  Dersom det fortsatt er et ukjent tegn så erstattes det med $
7.ss  *
7.ss  *
     c                   eval      i = 1
     c                   dow       i < 50
     c                             and %subst(text:i:51-i) <> ' '
7.ss c                   eval      t = %subst(text:i:1)
7.ss c     dig1          check     t             w_po1
7.ss c                   if        w_po1 > 0
7.ss c     dig2          check     t             w_po2
7.ss c                   if        w_po2 > 0
7.ss c     dig3          check     t             w_po3
7.ss c                   if        w_po3 > 0
7.ss c                   eval      %subst(text:i:1) = '%'
7.ss c                   endif
7.ss c                   endif
7.ss c                   endif
     c                   eval      i = i + 1
     c                   enddo
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     *inzsr        begsr
      *
     c     ra04l1_key    klist
     c                   kfld                    w_firm
      *
     c     ra11l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra11l1_kod
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
     c     rktrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rktrl1_kund
     c                   kfld                    rktrl1_raar
     c                   kfld                    rktrl1_rper
     c                   kfld                    rktrl1_bdat
      *
     c     rukpl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rukpl1_kund
     c                   kfld                    rukpl1_levr
     c                   kfld                    rukpl1_kpnr
      *
     c     rxmll1_key    klist
     c                   kfld                    rxmll1_head
     c                   kfld                    rxmll1_linj
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *                           Legger inn verdier i felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     *dtaara       define                  ax000par        100
     c     *lock         in        *dtaara
     c                   eval      p_perfm = %subst(ax000par:1:2)
     c                   eval      p_perfa = %subst(ax000par:3:4)
     c                   eval      p_pertm = %subst(ax000par:7:2)
     c                   eval      p_perta = %subst(ax000par:9:4)
     c                   eval      p_hcom  = %subst(ax000par:13:50)
      *
     c                   move      p_perfm       p_pfm
     c                   move      p_perfa       p_pfa
     c                   move      p_pertm       p_ptm
     c                   move      p_perta       p_pta
      *
     c                   movel     'KUNDE'       rxmll1_head
     c                   move      *zero         rxmll1_linj
      *
     c                   eval      w_firm      = l_firm
     c     ra04l1_key    chain     ra04l1
      *
     c                   eval      rkunl1_kund = *zero
     c     rkunl1_key    setll     rkunl1
      *
     c                   eval      rxmll1_head  = 'KUNDE'
     c                   eval      rxmll1_linj  = 05
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1 )
     c                   eval      xmlrec = axxmlt
     c                   write     axmlutr
     c                   endif
      *
     c                   endsr
