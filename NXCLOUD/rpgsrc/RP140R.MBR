     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RP140R
      * Beskrivelse . . : Prosjekt, Budsjett pr konto
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign  Ref.     Endringsbeskrivelse
      * ----- ------ ----  -------- -------------------------------------------------
      *       230819 sst            Nytt program
7.01  *       070420 bsa            Fjernet kode med til eksternordre (NO100)
7.02  *K00    061020 sst  NXS-12347 Aut. forny etter behanlding av varelinjer
7.03  *K00    061020 sst  NXS-12347 Enklere registrering av budsjett
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frpval3    if   e           k disk    rename(rpvapfr:rpval3r)
     frpval2    if   e           k disk    rename(rpvapfr:rpval2r)
7.03 frpvalu    uf a e           k disk    rename(rpvapfr:rpvalur)
     frp1pl1    if   e           k disk    rename(rp1ppfr:rp1pl1r)
     frp2ul1    if   e           k disk    rename(rp2upfr:rp2ul1r)
     frp3al1    if   e           k disk    rename(rp3apfr:rp3al1r)
     frhovl1    if   e           k disk    rename(rhovpfr:rhovl1r)
     frhtrlc    if   e           k disk    rename(rhtrpfr:rhtrlcr)
7.03 fsloglu    uf a e           k disk    rename(slogpfr:sloglur)
      *
     frp140d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av Local data area
      *
     d                uds
     d l_pros                101    106
     d l_upro                107    112
     d l_akti                113    118
     d l_kpro                119    124
      *
     d l_user                911    920
     d l_filg                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av parametre
      *
     d p_firm          s                   like(rpvfir)
     d p_pros          s                   like(rpvpro)
     d p_upro          s                   like(rpvupr)
     d p_akti          s                   like(rpvakt)
     d p_kpro          s                   like(rpvprk)
     d p_kost          s              9  0
     d p_kont          s              5  0
     d p_spes          s              4  0
     d p_avde          s              4  0
     d p_text          s                   like(b1text)
      *
      * Definering av variabler i nøkler
      *
7.03 d rpvalu_pros     s                   like(rpvpro)
7.03 d rpvalu_upro     s                   like(rpvupr)
7.03 d rpvalu_akti     s                   like(rpvakt)
7.03 d rpvalu_vare     s                   like(rpvvar)
7.03 d rpvalu_lass     s                   like(rpvlas)
     d rpval3_pros     s                   like(rpvpro)
     d rpval3_kont     s                   like(rpvkto)
     d*rpval2_pros     s                   like(rpvpro)
     d*rpval2_kont     s                   like(rpvkto)
     d rp1pl1_pros     s                   like(rp1pro)
     d rp2ul1_pros     s                   like(rp2pro)
     d rp2ul1_upro     s                   like(rp2upr)
     d rp3al1_pros     s                   like(rp3pro)
     d rp3al1_upro     s                   like(rp3upr)
     d rp3al1_akti     s                   like(rp3akt)
     d rhovl1_kont     s                   like(rhkont)
     d rhovl1_spes     s                   like(rhspes)
     d rhovl1_avde     s                   like(rhavde)
     d rhtrlc_hpro     s                   like(rbhpro)
     d rhtrlc_kont     s                   like(rbkont)
7.03 d sloglu_regi     s                   like(elregi)
7.03 d sloglu_fore     s                   like(elfore)
7.03 d sloglu_fort     s                   like(elfort)
7.03 d sloglu_felt     s                   like(elfelt)
7.03 d sloglu_dato     s                   like(elrdat)
7.03 d sloglu_time     s                   like(elrtim)
      *
      * Definering av variable
      *
7.03  *w_buds_alt  T = Totalnivå,  K = Kontonivå,  V = Varelinjenivå
7.03 d w_buds_alt      s              1    inz('T')
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_valg_ok       s              1    inz(*off)
      *
     d w_firm          s                   like(rpvfir)
     d w_konto         s                   like(rpvkto)
     d w_type          s              3
     d w_kosto         s              9  0
     d w_ms1           s             50
     d w_ms2           s             50
     d w_svar          s              1
     d w_in12          s              1  0
s.ss d w_forny         s              1
9.03   dcl-s co402_verdi1  char(1);
9.03   dcl-s co402_verdi2  char(2048);
9.03   DCL-PR CO402R EXTPGM('CO402R');
9.03     p_filgrp            char(3)     const;
9.03     p_firm              packed(3:0) const;
9.03     p_lager             packed(2:0) const;
9.03     p_nokkel            char(50)    const;
9.03     p_verdi1            char(1);
9.03     p_verdi2            char(2048);
9.03   END-PR;
      *
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(8)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
     c                   if        w_curn > 0
     c                   eval      w_sfrn = w_curn / c_sfil
     c                   eval      w_sfrn = w_sfrn * c_sfil + 1
     c                   endif
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inki
7.01 c**                 eval      w_type = 'SMK'
7.01 c                   eval      w_type = '   '
     c                   call      'NO100R'
     c                   parm                    w_type
     c                   parm                    p_pros
     c                   parm                    p_upro
     c                   parm                    p_akti
     c                   parm                    p_kpro
     c                   exsr      forny
     c                   goto      b2tagb
      *
     c                   when      *inkj
     c                   exsr      Hentkost
     c                   goto      avslutt
     c                   when      *inkp
     c                             or *inkn
     c                             or *inkm
     c                   eval      p_kont = *zero
     c                   eval      p_text = *blank
     c                   call      'RP145R'
     c                   parm                    p_pros
     c                   parm                    p_upro
     c                   parm                    p_akti
     c                   parm                    p_kont
     c                   parm                    p_text
7.02 c                   exsr      forny
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
      *sven
     c                   when      *inkw
     c                   eval      w_buds_alt = 'T'
     c                   eval      %subst(b2pros:58:3) = 'TOT'
     c                   eval      *in41 = *on
     c                   when      *inkx
     c                   eval      w_buds_alt = 'K'
     c                   eval      %subst(b2pros:58:3) = 'KON'
     c                   eval      *in41 = *off
     c                   when      *inky
     c                   eval      w_buds_alt = 'V'
     c                   eval      %subst(b2pros:58:3) = 'VAR'
     c                   eval      *in41 = *on
     c                   endsl
      *
      * Posisjonering
      *
     c****               if        b2pros <> *blank or
     c                   if        b2ftxt <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
     c                   if        b_valg_ok = *on
     c                   goto      avslutt
     c                   endif
      *
s.ss c                   if        w_forny <> 'N'
     c                   exsr      forny
s.ss c                   endif
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
      *    Spørsmål om oppdatering dersom kost er endret uten at oppdatering er valgt
      *
     c                   exsr      hentkost
      *
     c                   if        p_kost <> w_kosto
7.03 c                             and  w_buds_alt <> 'T'
     c                   eval      w_ms1 = 'Kost er endret utan at du har +
     c                                      valg oppdatering.'
     c                   eval      w_ms2 = 'Ønsker du å oppdatere kost nå, +
     c                                      svar <J>'
     c                   eval      w_svar = 'N'
     c                   call      'AA007R'
     c                   parm                    w_ms1
     c                   parm                    w_ms2
     c                   parm                    w_svar
     c                   parm                    w_in12
     c                   if        w_svar <> 'J'
     c                   eval      p_kost = 0
     c                   endif
     c                   endif
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_sfrn <> 0
     c     w_sfrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
     c*                  eval      rpval2_kont = b1kont
     c*    rpval2_key    setll     rpval2
     c                   other
     c                   eval      rpval3_pros = p_pros
     c                   eval      rpval3_kont = *zero
     c     rpval3_key    setll     rpval3
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Konto
      *
     c                   if        b2fkto <> 0
     c                   eval      w_seqe = *blank
     c                   eval      rpval3_kont = b2fkto
     c     rpval3_key    setll     rpval3
     c                   goto      end_pos
     c                   endif
      *
      * Beskrivelse
      *
     c*                  if        b2text <> *blank
     c*                  eval      w_seqe = 'B'
     c*                  eval      rpval2_text = b2text
     c*    rpval2_key    setll     rpval2
     c*                  goto      end_pos
     c*                  endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2pros = *blank
     c                   eval      b2fkto = *zero
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_sfrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Velg
      *
7.03  * 4 = Slette
7.03 c                   if        b1valg = 4
7.03 c                   eval      rpvalu_pros = p_pros
7.03 c                   eval      rpvalu_upro = p_upro
7.03 c                   eval      rpvalu_akti = p_akti
7.03 c                   eval      rpvalu_vare = %char(b1kont)
7.03 c                   eval      rpvalu_lass = *zero
7.03 c     rpvalu_key    chain     rpvalu
7.03 c                   if        %found(rpvalu)
7.03  *  Logg hendelse
7.03 c                   eval      elregi = 'RPVAPF'
7.03 c                   eval      elfore = p_pros
7.03 c                   eval      elfort = %char(bhkont)
7.03 c                   time                    elrdat
7.03 c                   time                    elrtim
7.03 c                   eval      eluser = l_user
7.03 c                   time                    eldato
7.03 c                   eval      elfrav = %char(bhbuds)
7.03 c                   eval      eltilv = '0 - Slettet'
7.03 c                   write     sloglur
7.03  *
7.03 c                   delete    rpvalur
7.03 c                   eval      b1valg = *zero
7.03 c                   update    b1sfl
7.03 c                   endif
7.03 c                   iter
7.03 c                   endif
      *
      * 5 = Vise
     c                   if        b1valg = 5
     c*                  eval      p_pros = *blank
     c*                  eval      p_upro = *blank
     c*                  eval      p_akti = *blank
     c                   eval      p_kont = b1kont
     c                   eval      p_text = b1text
     c                   call      'RP145R'
     c                   parm                    p_pros
     c                   parm                    p_upro
     c                   parm                    p_akti
     c                   parm                    p_kont
     c                   parm                    p_text
     c                   eval      b1valg = *zero
     c                   update    b1sfl
     c                   iter
     c                   endif
s.ss  *
s.ss  *  Spørring på konto
s.ss  *
s.ss c                   if        b1valg = 9
s.ss c                   eval      p_firm = l_firm
s.ss c                   eval      p_kont = *zero
s.ss c                   eval      p_spes = *zero
s.ss c                   eval      p_avde = *zero
s.ss c                   eval      p_text = '*Prosjekt'
s.ss c                   call      'RH500R'
s.ss c                   parm                    p_firm
s.ss c                   parm                    p_kont
s.ss c                   parm                    p_spes
s.ss c                   parm                    p_avde
s.ss c                   parm                    p_text
s.ss c                   eval      b1kont = p_kont
s.ss c                   eval      b1text = p_text
s.ss c                   eval      b1valg = *zero
s.ss c                   update    b1sfl
s.ss c                   eval      w_forny = 'N'
s.ss c                   iter
s.ss c                   endif
      *
7.03  *  Registerer budsjett
7.03  *
7.03 c                   if        b1kont <> *zero
7.03 c                             and bhbuds <> b1buds
     c                   eval      rpvalu_pros = p_pros
     c                   eval      rpvalu_upro = p_upro
     c                   eval      rpvalu_akti = p_akti
     c                   eval      rpvalu_vare = %char(b1kont)
     c                   eval      rpvalu_lass = *zero
     c     rpvalu_key    chain     rpvalu
7.03 c                   eval      rpvfir = l_firm
7.03 c                   eval      rpvpro = p_pros
7.03 c                   eval      rpvupr = p_upro
7.03 c                   eval      rpvakt = p_akti
7.03 c                   eval      rpvvar = %char(b1kont)
7.03 c                   eval      rpvkto = b1kont
7.03 c                   eval      rpvlas = *zero
7.03 c                   eval      rpvabj = 1
7.03 c                   eval      rpvkpr = b1buds
7.03 c                   eval      rpvsts = 'B'
     c                   if        %found(rpvalu)
     c                   time                    rpveda
     c                   time                    rpveti
     c                   eval      rpveus = l_user
7.03 c                   update    rpvalur
     c                   else
7.03 c                   write     rpvalur
     c                   endif
7.03  *  Logg hendelse
7.03 c                   eval      elregi = 'RPVAPF'
7.03 c                   eval      elfore = p_pros
7.03 c                   eval      elfort = %char(b1kont)
7.03 c                   time                    elrdat
7.03 c                   time                    elrtim
7.03 c                   eval      eluser = l_user
7.03 c                   time                    eldato
7.03 c                   eval      elfrav = %char(bhbuds)
7.03 c                   eval      eltilv = %char(b1buds)
7.03 c                   write     sloglur
7.03  *
7.03 c                   iter
7.03 c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
     c                   eval      w_konto = *zero
      *
      *  Start side
     c                   dou       w_srrn = w_spge
      *
     c                   if        w_konto = *zero
     c                   select
     c                   when      w_seqe = 'B'
     c*                  read      rpval2
     c                   other
     c                   read      rpval3
     c                   endsl
     c                   endif
     c                   if        %eof(rpval3)
     c                   eval      p_pros = p_pros
     c                   endif
      *
     c                   if        %eof(rpval3)
     c                             or rpvfir <> w_firm
     c                             or rpvpro <> p_pros
     c**                           or rpvupr <> p_upro
     c**                           or rpvakt <> p_akti
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_konto = rpvkto
     c                   eval      b1buds  = *zero
      * _______________________________________________________________________________
      *   Lese alle forekomster på dette kontonr
      *  Start kontonr
     c                   dow       not %eof(rpval3)
     c                             and rpvkto = w_konto
     c                             and rpvpro = p_pros
     c**                           and rpvupr = p_upro
     c**                           and rpvakt = p_akti
      *
     c                   if        p_upro <> *blank
     c                             and rpvupr <> p_upro
     c                   goto      IkkeMed
     c                   endif
      *
     c                   if        p_akti <> *blank
     c                             and rpvakt <> p_akti
     c                   goto      IkkeMed
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
     c*                  eval      rpval2_kont = rpvkto
     c                   other
     c                   eval      rpval3_kont = rpvkto
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1kont = w_konto
7.03 c                   eval      bhkont = w_konto
     c                   eval      b1text = rpvtxt
7.03 c                   if           w_buds_alt = 'V'
7.03 c                                and  rpvsts <> 'B'
7.03 c                             or w_buds_alt = 'K'
7.03 c                             and  rpvsts  = 'B'
     c                   eval(h)   b1buds = b1buds + (rpvabj * rpvkpr)
7.03 c                   endif
     c                   eval      b1regn = *zero
      *
     c     IkkeMed       tag
      *
      *
     c                   select
     c                   when      w_seqe = 'B'
     c*                  read      rpval2
     c                   other
     c                   read      rpval3
     c                   endsl
     c                   if        %eof(rpval3)
     c                   eval      p_pros = p_pros
     c                   endif
      *
      *  Slutt kontonr
     c                   enddo
      *
      * ______________________________________________________________________________
      *
      *  Hent Postert i regnskap på denne konto/prosjekt
      *
     c                   eval      b1regn = *zero
     c                   eval      rhtrlc_hpro = p_pros
     c                   eval      rhtrlc_kont = w_konto
     c     rhtrlc_key    setll     rhtrlc
     c     rhtrlc_key    reade     rhtrlc
      *  Start virkelig
     c                   dow       not %eof(rhtrlc)
     c                   eval      b1regn = b1regn + rbneto
     c     rhtrlc_key    reade     rhtrlc
      *  Slutt virkelig
     c                   enddo
     c
      * ______________________________________________________________________________
      *
      *  Hent kontonavn fra kontoplan
     c                   if        b1buds <> 0
     c                   eval      w_srrn = w_srrn + 1
     c                   eval      rhovl1_kont = w_konto
     c                   eval      rhovl1_spes = *zero
     c                   eval      rhovl1_avde = *zero
     c     rhovl1_key    chain     rhovl1
     c                   if        %found(rhovl1)
     c                   eval      b1text = rhtext
     c                   else
     c                   eval      b1text = 'Ukjent konto'
     c                   endif
7.03 c                   eval      bhbuds = b1buds
      *
     c                   write     b1sfl
      *
     c                   endif
      *
     c                   if        %eof(rpval3)
     c                             or rpvfir <> w_firm
     c                             or rpvpro  <> p_pros
     c**                           or rpvupr  <> p_upro
     c**                           or rpvakt  <> p_akti
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
      *  Slutt konto
     c                   enddo
7.03  *
7.03  *  Fyll på med blanke linjer
7.03  *
7.03 c                   dow       w_srrn < w_spge
7.03 c                   eval      w_srrn = w_srrn + 1
7.03 c                   eval      b1valg = *zero
7.03 c                   eval      b1kont = *zero
7.03 c                   eval      b1text = *blank
7.03 c                   eval      b1buds = *zero
7.03 c                   eval      b1regn = *zero
7.03 c                   eval      bhbuds = *zero
7.03 c                   eval      bhkont = *zero
7.03 c                   write     b1sfl
7.03 c                   enddo
      *   __________________________________________________________
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å hente all kost på dette prosjektet.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     HentKost      begsr
      *
     c                   eval      rpval3_pros = p_pros
     c                   eval      rpval3_kont = *zero
     c     rpval3_key    setll     rpval3
     c                   read      rpval3
     c                   eval      p_kost = *zero
     c                   dow       not %eof(rpval3)
     c                             and rpvfir = w_firm
     c                             and rpvpro = p_pros
      *
     c                   if        p_upro = *blank
     c                             or rpvupr = p_upro
     c                   if        p_akti = *blank
     c                             or rpvakt = p_akti
7.03 c                   if             w_buds_alt = 'V'
7.03 c                             and  rpvsts <> 'B'
7.03 c                             or   w_buds_alt = 'K'
7.03 c                             and  rpvsts = 'B'
7.03 c                   eval(h)   p_kost = p_kost + (rpvabj * rpvkpr)
7.03 c                   endif
     c                   endif
     c                   endif
      *
     c                   read      rpval3
      *
     c                   enddo
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'B'
     c*    rpval2_key    setgt     rpval2
     c*                  readp     rpval2
     c                   other
     c     rpval3_key    setgt     rpval3
     c                   readp     rpval3
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'B'
     c*                  readp     rpval2
     c                   other
     c                   readp     rpval3
     c                   endsl
      *
     c                   if        %eof or
     c                             rpvfir <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c*                  eval      rpval2_kont = rpvkto
     c                   other
     c                   eval      rpval3_kont = rpvkto
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'B'
     c*    rpval2_key    setll     rpval2
     c                   other
     c     rpval3_key    setll     rpval3
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_pros
     c                   parm                    p_upro
     c                   parm                    p_akti
     c                   parm                    p_kpro
     c                   parm                    p_kost
      *
      * Key for posisjonering på avdeling
      *
     c     rpval3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rpval3_pros
     c                   kfld                    rpval3_kont
      *
7.03 c     rpvalu_key    klist
7.03 c                   kfld                    w_firm
7.03 c                   kfld                    rpvalu_pros
7.03 c                   kfld                    rpvalu_upro
7.03 c                   kfld                    rpvalu_akti
7.03 c                   kfld                    rpvalu_vare
7.03 c                   kfld                    rpvalu_lass
      *
      * Key for posisjonering på beskrivelse
      *
     c*    rpval2_key    klist
     c*                  kfld                    w_firm
     c*                  kfld                    rpval2_text
      *
      * Key for les av prosjekt
      *
     c     rp1pl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rp1pl1_pros
      *
      * Key for les av underprosjekt
      *
     c     rp2ul1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rp2ul1_pros
     c                   kfld                    rp2ul1_upro
      *
      * Key for les av prosjekt
      *
     c     rp3al1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rp3al1_pros
     c                   kfld                    rp3al1_upro
     c                   kfld                    rp3al1_akti
      *
      * Key for les av kontotekst
      *
     c     rhovl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhovl1_kont
     c                   kfld                    rhovl1_spes
     c                   kfld                    rhovl1_avde
      *
      * Key for les av hovedboksposter
      *
     c     rhtrlc_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhtrlc_hpro
     c                   kfld                    rhtrlc_kont
      *
      *
      * Henter parametre, og initierer skjermbildet
      *
     c                   eval      w_firm = p_firm
      *
     c                   eval      *in22 = *on
      *
     c                   eval      b2pros = p_pros
     c                   eval      rp1pl1_pros = p_pros
     c     rp1pl1_key    chain     rp1pl1
     c                   if        %found(rp1pl1)
     c                   eval      b2ptxt = rp1txt
     c                   endif
     c                   if        p_upro <> *blank
     c                   eval      b2pros = %trim(b2pros) + '\' + p_upro
     c                   eval      rp2ul1_pros = p_pros
     c                   eval      rp2ul1_upro = p_upro
     c     rp2ul1_key    chain     rp2ul1
     c                   if        %found(rp2ul1)
     c                   eval      b2ptxt = %trim(b2ptxt) + '\' + rp2txt
     c                   endif
     c                   endif
     c                   if        p_akti <> *blank
     c                   eval      b2pros = %trim(b2pros) + '\' + p_akti
     c                   eval      rp3al1_pros = p_pros
     c                   eval      rp3al1_upro = p_upro
     c                   eval      rp3al1_akti = p_akti
     c     rp3al1_key    chain     rp3al1
     c                   if        %found(rp3al1)
     c                   eval      b2ptxt = %trim(b2ptxt) + '\' + rp3txt
     c                   endif
     c                   endif
      *
      *    Ta vare på kost fra tidligere
      *
     c                   eval      w_kosto = p_kost
9.03  *
9.03  * Hvordan skal budsjett håndtereskategori
9.03  *
9.03   CallP CO402R(l_filg:w_firm:0:'PROSJEKT_BUDSJETT_ALTERNATIV':
9.03                    co402_verdi1:co402_verdi2);
9.03   if co402_verdi1 = '1';
9.03     w_buds_alt = %subst(co402_verdi2:1:1);
9.03   endif;
      *
     c                   select
     c                   when      w_buds_alt = 'T'
     c                   eval      %subst(b2pros:58:3) = 'TOT'
     c                   when      w_buds_alt = 'K'
     c                   eval      %subst(b2pros:58:3) = 'KON'
     c                   when      w_buds_alt = 'V'
     c                   eval      %subst(b2pros:58:3) = 'VAR'
     c                   endsl
      *
7.03  *    Skal det være mulig å registrere budsjett
7.03 c                   if        w_buds_alt <> 'K'
7.03 c                   eval      *in41 = *on
7.03 c                   else
7.03 c                   eval      *in41 = *off
7.03 c                   endif
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      rpval3_pros = p_pros
     c                   eval      rpval3_kont = *zero
     c     rpval3_key    setll     rpval3
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
