     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  system. . . . . : aslagr                                       *
      *  program . . . . : lo737r                                       *
      *  beskrivelse . . : splitte ip til ip                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  spesialversjon. :                                              *
      *  tilpasset for . :                                              *
      *                                                                 *
      *  referanse  dato   endringsbeskrivelse                     sign.*
      *  --------- ------  ---------------------------------------------*
      *  v6.30     140819  nytt program                                 *
      *  6.30      140819  splitting av ip i to(til ny ip)              *
7.00  *  K000  020620 bsa  Takler brede skjermbilder                    *
      *
8.01  *  800   271022 bsa  Legg også ut referanse på tilleggsregister.
8.01  *  800               Brukes ved utsendelse av EDI bestilling fra
8.01  *  800               ny EDI modul (NGN).
8.02  *K0000   170924 bsa  Skal ikke gjenbruke suffikser                *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
      *
     flohelu    uf a e           k disk    rename(lohepfr:lohelur)
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flodtl1    uf   e           k disk    rename(lodtpfr:lodtl1r)
     flodtlu    uf a e           k disk    rename(lodtpfr:lodtlur)
     flloglu    uf a e           k disk    rename(llogpfr:lloglur)
      *
     fldell3    if   e           k disk    rename(ldelpfr:ldell3r)
     flusrl1    if   e           k disk    rename(lusrpfr:lusrl1r)
8.01 flotii2    uf a e           k disk    rename(lotist:lotii2r)
8.01 ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
      *
     flo737d    cf   e             workstn sfile(d1sfl:srrn01)
      *
      *
      * definering av variable i parametre
      *
     d p_firm          s              3  0
     d p_kode          s              2  0
     d p_text          s             20
     d p_adrs          s             30
     d p_ponr          s              4  0
     d p_pste          s             30
     d p_avde          s              4  0
     d p_feil          s              1
      *
      * definering av variabler i nøkler
      *
     d lohel1_numm     s                   like(lonumm)
     d lohel1_suff     s                   like(losuff)
     d lusrl1_user     s                   like(lbuser)
     d lusrl1_firm     s                   like(lbfirm)
     d ldell3_numm     s                   like(lynumm)
     d ldell3_suff     s                   like(lysuff)
     d lloglu_numm     s                   like(lmnumm)
     d lloglu_suff     s                   like(lmsuff)
     d lloglu_kode     s                   like(lmkode)
8.01 d lotii2_tnum     s                   like(lotnum)
8.01 d lotii2_tsuf     s                   like(lotsuf)
8.01 d fohel1_numm     s                   like(fonumm)
8.01 d fohel1_suff     s                   like(fosuff)
      *
      * definering av øvrige variable
      *
     d w_on01          s              2
     d w_on02          s              2
     d w_on03          s              2
     d w_on04          s              2
     d w_on05          s              2
     d w_on06          s              2
     d w_datf          s                   like(lofkda)
     d w_datt          s                   like(lofkda)
     d w_utda          s                   like(loedat)
     d w_ldor          s                   like(loldor)
     d w_mld1          s             50
     d w_mld2          s             50
     d w_svar          s              1
     d w_in12          s              1  0
     d w_debug         s              1
     d w_numm          s                   like(lynumm)
     d w_aarr          s                   like(lyaarr)
     d w_nysu          s                   like(lysuff)
     d w_suff          s                   like(lysuff)
     d w_firm          s                   like(lyfirm)
     d w_linje_ut      s              1      inz(*off)
     d w_enor          s              1
     d w_logk          s                   like(lmkode)
      *
      *
     d                uds
     d  l_wsid               901    910
     d  l_user               911    920
     d  l_firm               944    946  0
     d  l_fnav               951    980
     d  l_syst               999    999
      *
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  a1 behandler plukking av bestillingsnummer                     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      *  hent opplysninger fra bestilling-hode-register
      *
     c                   move      wpnumm        hednum
     c                   move      wpsuff        hedsuf
     c     hedkey        chain     lohel1r                            90
      *
      *  hent opplysninger fra ordretype-register
      *
     c                   move      lootyp        otytyp
     c     otykey        chain     votyl1r                            90
      *
      *   hent ny suffix  se(fy500/505r)
      *   legg ut ny bestilling m/ny suffix   w_nysu
     c                   exsr      finn_nxtsuff
      *
      *
      *  oppdater bestilling-hode-register, bestillingen er til behandling
      *   ???????????????????
     c                   call      'LO709R'
     c                   parm                    hednum
     c                   parm                    hedsuf
      *
     c     hedkey        chain     lohelur                            90
     c     *in90         ifeq      *off
     c                   move      3             lokode
     c                   update    lohelur
     c                   end
      *
      *    behandling av linjer
      *
     c                   exsr      xd1bld
      *
     c                   if        w_linje_ut = *on
      *   Legg ut ny ordre på ny suffix
     c                   eval      losuff = w_nysu
     c                   time                    loedat
     c                   time                    loetim
     c                   eval      loeusr = l_user
     c                   eval      lokode = *zero
     c                   write     lohelur
8.01  *
8.01  * Legg ut post på tilleggsregister
8.01  *
8.01 c                   eval      lotii2_tnum = lonumm
8.01 c                   eval      lotii2_tsuf = losuff
8.01 c     lotii2_key    chain     lotii2r
8.01 c                   if        not %found
8.01  *
8.01 c                   if        loornr <> 0
8.01 c                   eval      fohel1_numm = loornr
8.01 c                   eval      fohel1_suff = loorsu
8.01 c     fohel1_key    chain     fohel1
8.01 c                   if        %found(fohel1)
8.01 c                   if        folety = 1
8.01 c                   eval      lotko2 = 'B'
8.01 c                   else
8.01 c                   eval      lotko2 = 'L'
8.01 c                   endif
8.01 c                   else
8.01 c                   eval      lotko2 = 'L'
8.01 c                   endif
8.01 c                   else
8.01 c                   eval      lotko2 = 'L'
8.01 c                   endif
8.01  *
8.01 c                   eval      lotfir = wpfirm
8.01 c                   eval      lotnum = lonumm
8.01 c                   eval      lotsuf = losuff
8.01  *
8.01 c                   time                    loteda
8.01 c                   time                    loteti
8.01 c                   eval      loteus = l_user
8.01  *
8.01 c                   write     lotii2r
8.01 c                   endif
      *
      *   Beregne ny ordre
     c                   eval      w_enor = *blank
     c                   call      'LO730R'
     c                   parm                    w_enor
     c                   parm                    w_firm
     c                   parm                    hednum
     c                   parm                    w_nysu
      *
      *   Oppdatere logg på ny suffix
     c                   eval      lloglu_numm = hednum
     c                   eval      lloglu_suff = hedsuf
     c                   eval      lloglu_kode = '9'
     c     lloglu_key    setgt     lloglu
     c                   readp     lloglu
     c                   if        not %eof(lloglu)
     c                             and lmnumm = hednum
     c                             and lmsuff = hedsuf
     c                   eval      w_logk = lmkode
     c                   eval      lloglu_suff = w_nysu
     c     lloglu_key    chain     lloglu
     c                   eval      lmkode = w_logk
     c                   time                    lmdate
     c                   time                    lmtime
     c                   eval      lmuser = l_user
     c                   if        %found(lloglu)
     c                   update    lloglur
     c                   else
     c                   eval      lmnumm = hednum
     c                   eval      lmsuff = w_nysu
     c                   write     lloglur
     c                   endif
     c                   endif
      *
      *   Oppdater gammel ordre med ferdig behandlet og endret dato
     c     hedkey        chain     lohelur
     c                   if        %found(lohelu)
     c                   eval      lokode = *zero
     c                   time                    loedat
     c                   time                    loetim
     c                   eval      loeusr = l_user
     c                   update    lohelur
      *   Beregne gammel ordre
     c                   call      'LO730R'
     c                   parm                    w_enor
     c                   parm                    w_firm
     c                   parm                    hednum
     c                   parm                    hedsuf
     c                   end
     c                   else
      *   Oppdater gammel ordre med ferdig behandlet (ikke behandlet)
     c     hedkey        chain     lohelur
     c                   if        %found(lohelu)
     c                   eval      lokode = *zero
     c                   update    lohelur
     c                   endif
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     xslutt        tag
      *
     c                   seton                                        lr
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  d1 behandle bilde for plukking av linjer                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     xd1bld        begsr
      *  debug8
     c                   eval      w_debug = w_debug
      *
     c                   eval      *in21 = *on
      *
      *  legger inn bestillingsnummer og suffix i nøkklene
      *
     c                   eval      lodtl1_num = hednum
     c                   eval      lodtl1_suf = hedsuf
      *
      *  hent opplysninger fra bestilling-hode-register
      *
     c     hedkey        chain     lohel1r                            90
     c                   move      lonumm        d2numm
     c                   move      losuff        d2suff
     c                   move      loldor        d2ldor
     c                   move      lolena        d2lena
      *
      *  hent opplysninger fra bestilling-linje-register
      *
     c                   move      *zero         lodtl1_lin
     c     lodtl1_key    setll     lodtl1
     c                   exsr      xclr01
     c                   exsr      xcrt01
      *
      *  send alt
      *
     c     d2taga        tag
     c                   write     d2cmd
      *
      *  bare data
      *
     c     d2tagb        tag
     c                   exsr      xdsp01
      *
      *  f3=avslutt
      *
     c     *inkc         cabeq     *on           xd1slt
      *
      *  f5=forny
      *
     c     *inke         ifeq      *on
     c                   exsr      xlsp01
     c                   goto      d2tagb
     c                   endif
      *
      *  roll
      *
     c     *in10         ifeq      *on
     c                   exsr      xcrt01
     c                   goto      d2tagb
     c                   endif
     c/eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  behandling av startverdier for start f.o.m.                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *  posisjoner
      *  startverdi
      *  på linjenummer
      *
     c     d2line        ifne      *zero
     c                   move      d2line        lodtl1_lin
     c     lodtl1_key    setll     lodtl1r
     c                   exsr      xclr01
     c                   exsr      xcrt01
     c                   move      *zero         d2line
     c                   goto      d2tagb
     c                   end
      *
     c/eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  behandling av subfile, foretar valgene                         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *  sjekk om valg
      *  er foretatt
      *
      * debug 10
     c                   eval      w_debug = w_debug
     c                   do        ssrn01
     c                   readc     d1sfl                                  16
     c     *in16         ifeq      *on
     c                   leave
     c                   endif
      *
      *  sjekk alle linjer
      *
      *        her skal linjer ev legges ut på ny ip
      *
      * sstdebug   hvis splitting av ip til ny ip kjøres egen subrutine
     c                   if        d1kode = '1'
     c                             or d1leve > *zero
     c                   exsr      nyip
     c                   eval      d1kode = *blank
     c                   eval      d1leve = *zero
     c                   update    d1sfl
     c                   endif
      *
     c                   enddo
      *
      *        dersom ip - ip beregnes begge bestillinger vedr summer
      *                       på gammel og ny suffix
      *
      *
      *  nullstiller/blanker feltet for start f.o.m.
      *
     c                   move      *zero         d2line
      *
     c                   exsr      xlsp01
     c                   goto      d2taga
     c     xd1slt        tag
     c                   unlock    lodtl1
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  subrutine for å splitte ip til ny ip m/sufix                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     nyip          begsr
      *  les linje fra lodtxx
     c                   eval      lodtl1_lin = d1line
     c     lodtl1_key    chain     lodtl1
     c                   if        %found(lodtl1)
      *  hvis d1kode = 1 og d1leve = d1anta oppdateres linje med ny suff
     c                   if        d1leve = d1anta
     c                             or d1leve = *zero
     c                   eval      ldsuff = w_nysu
     c                   write     lodtlur
     c                   delete    lodtl1r
     c                   else
      *  hvis d1kode = 1 og d1leve <> d1anta oppdateres eks linje m/
      *                  antall = d1anta - deleve
     c                   eval      ldanta = d1anta - d1leve
     c                   update    lodtl1r
      *                  det legges ut en ny linje med ny suffix og
      *                  antall = d1leve   linje = d1line
     c                   eval      ldsuff = w_nysu
     c                   eval      ldanta = d1leve
     c                   write     lodtlur
     c                   endif
     c                   eval      w_linje_ut = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * subrutine for å finne neste ledige suffix på denne ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_nxtsuff  begsr
8.02  *
8.02  * Hent høyeste brukte suffiks på ordrenummer
8.02  *
8.02 c                   eval      w_nysu = 0
8.02 c                   call      'AS115R'
8.02 c                   parm                    hednum
8.02 c                   parm                    w_nysu
      *
      *       - sjekk om ordre med høyere suffix i ldelpf
      *
8.02 c*                  eval      ldell3_numm = wpnumm
8.02 c*                  eval      ldell3_suff = 99
8.02 c*    ldell3_key    setgt     ldell3
8.02 c*                  readp     ldell3
8.02 c*                  if        not %eof(ldell3)
8.02 c*                            and lynumm = wpnumm
8.02 c*                  eval          w_nysu = lysuff + 1
8.02 c*                  endif
      *
      *       - sjekk om ordre med høyere suffix i lohepf
8.02 c*                  eval      lohel1_numm = wpnumm
8.02 c*                  eval      lohel1_suff = 99
8.02 c*    lohel1_key    setgt     lohel1
8.02 c*                  readp     lohel1
8.02 c*                  if        not %eof(lohel1)
8.02 c*                            and lonumm = wpnumm
8.02 c*                            and losuff + 1 > w_nysu
8.02 c*                  eval          w_nysu = losuff + 1
8.02 c*                  endif
      *
      *       - sjekk om ordre med høyere suffix i sohepf
     c*                  eval      sohel3_aarr = w_aarr
     c*                  eval      sohel3_numm = w_numm
     c*    sohel3_key    setll     sohel3
     c*                  read      sohel3
     c*                  dow       not %eof(sohel3)
     c*                            and sonumm = w_numm
     c*                  if        sosuff + 1 > w_nysu
     c*                  eval      w_nysu = sosuff + 1
     c*                  endif
     c*                  read      sohel3
     c*                  enddo
      *
     c                   endsr
      *
     c/eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  fornyer subfilen (dvs. loader subfilen pånytt)                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xlsp01        begsr
      *
     c     sfrn01        ifne      *zero
     c     sfrn01        chain     d1sfl                              90
     c                   end
      *
     c                   move      d1line        lodtl1_lin
      *
     c     lodtl1_key    setll     lodtl1
      *
     c                   exsr      xclr01
     c                   exsr      xcrt01
      *
     c                   endsr
      *
     c/eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  display subfile                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xdsp01        begsr
      *
     c     srrn01        ifne      0
     c                   move      *on           *in12
     c                   endif
      *
     c                   move      *on           *in13
     c                   exfmt     d2ctl
     c                   move      *off          *in12
     c                   move      *off          *in13
      *
     c                   endsr
      *
     c/eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  tøm subfile                                                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xclr01        begsr
     c                   move      *on           *in14
     c                   write     d2ctl
     c                   move      *off          *in14
      *
     c                   move      *off          *in15
     c                   z-add     0             srrn01
     c                   z-add     0             sfrn01
     c                   z-add     0             ssrn01
     c                   z-add     0             spge01
      *
     c                   endsr
      *
     c/eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  fyll opp subfile                                               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xcrt01        begsr
     c                   add       14            spge01
     c                   z-add     ssrn01        srrn01
      *
     c     srrn01        doueq     spge01
      *
     c                   read      lodtl1                                 15
     c     *in15         ifeq      *off
     c     ldnumm        ifne      lodtl1_num
     c                   move      *on           *in15
     c                   end
     c                   end
     c     *in15         ifeq      *off
     c     ldsuff        ifne      lodtl1_suf
     c                   move      *on           *in15
     c                   end
     c                   end
      *
     c     *in15         cabeq     '1'           xend01
      *
     c                   add       1             srrn01
      *
     c                   move      ldline        d1line
     c                   move      ldanta        d1anta
      *
     c                   if        ldnobb <> 0
     c                   movel     ldnobb        d1vare
     c                   else
     c                   movel     ldlvar        d1vare
     c                   endif
      *
     c                   movel     ldvare        d1evar
     c                   movel     ldtek1        d1tek1
     c                   move      ldenh1        d1enh1
      *
      * setter farge
     c                   eval      *in55 = *off
     c                   eval      *in56 = *off
     c                   eval      *in57 = *off
     c                   eval      *in58 = *off
     c                   eval      *in59 = *on
     c                   select
     c                   when      d1kode = '1'
     c                   eval      *in57 = *on
     c                   eval      *in59 = *off
     c                   when      d1kode = '3'
     c                   eval      *in56 = *on
     c                   eval      *in59 = *off
     c                   when      d1kode = '4'
     c                   eval      *in55 = *on
     c                   eval      *in59 = *off
     c                   when      d1kode = '5'
     c                   eval      *in58 = *on
     c                   eval      *in59 = *off
     c                   endsl
      *
     c                   write     d1sfl
     c                   enddo
      *
     c     xend01        tag
      *
     c     srrn01        ifgt      ssrn01
     c     ssrn01        add       1             sfrn01
     c                   endif
      *
     c                   z-add     srrn01        ssrn01
     c                   z-add     sfrn01        svrn01
      *
     c                   endsr
      *
     c/eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      *  nullstilling av felter som ikke kan redefineres
      *
     c                   z-add     0             spge01            3 0
     c                   z-add     0             srrn01            4 0
     c                   move      *blank        wseqen            1
     c                   move      *blank        wpretk            1
     c                   move      *blank        wpplor            1
     c                   move      *blank        wp110d            1
      *
     c                   z-add     0             spge01            3 0
     c                   z-add     0             srrn01            4 0
      *
     c                   move      *blank        wwoutq           10
      *
     c                   move      *blank        wpipor            1
     c                   move      *blank        wpftor            1
     c                   move      *zero         wwvalg            1 0
     c                   move      *zero         wxpdir            1 0
      *
      *  redefinering av felter
      *
     c     *like         define    srrn01        sfrn01
     c     *like         define    srrn01        ssrn01
      *
      *  redefinering av parameterfeltene
      *
     c     *like         define    lofirm        wpfirm
     c     *like         define    lonumm        wpnumm
     c     *like         define    losuff        wpsuff
      *
      *  redefinering av nøkler
      *
     c     *like         define    vaofir        otyfir
     c     *like         define    vaotyp        otytyp
      *
     c     *like         define    lofirm        hedfir
     c     *like         define    lonumm        hednum
     c     *like         define    losuff        hedsuf
      *
     c     *like         define    ldfirm        lodtl1_fir
     c     *like         define    ldnumm        lodtl1_num
     c     *like         define    ldsuff        lodtl1_suf
     c     *like         define    ldline        lodtl1_lin
      *
     c     *like         define    lofirm        potfir
     c     *like         define    lootyp        pototy
     c     *like         define    lonumm        potnum
     c     *like         define    losuff        potsuf
      *
      *  key for les av bruker bestilling/lager
      *
     c     lusrl1_key    klist
     c                   kfld                    lusrl1_firm
     c                   kfld                    lusrl1_user
      *
      *  key for file : ordretype-register
      *
     c     otykey        klist
     c                   kfld                    otyfir
     c                   kfld                    otytyp
      *
      *  key for file : bestilling-hode-register
      *
     c     hedkey        klist
     c                   kfld                    hedfir
     c                   kfld                    hednum
     c                   kfld                    hedsuf
      *
      *  key for file : bestilling-linje-register
      *
     c     lodtl1_key    klist
     c                   kfld                    lodtl1_fir
     c                   kfld                    lodtl1_num
     c                   kfld                    lodtl1_suf
     c                   kfld                    lodtl1_lin
      *
      * key for les av bestillingshode
      *
     c     lohel1_key    klist
     c                   kfld                    wpfirm
     c                   kfld                    lohel1_numm
     c                   kfld                    lohel1_suff
8.01  *
8.01  * key for les av ordrehode
8.01  *
8.01 c     fohel1_key    klist
8.01 c                   kfld                    wpfirm
8.01 c                   kfld                    fohel1_numm
8.01 c                   kfld                    fohel1_suff
8.01  *
8.01  * key for les av ordrehode
8.01  *
8.01 c     lotii2_key    klist
8.01 c                   kfld                    wpfirm
8.01 c                   kfld                    lotii2_tnum
8.01 c                   kfld                    lotii2_tsuf
      *
      * key for oppslag på ordre-hode-register
      *
     c     ldell3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
      *
      * key for oppslag på loggkoder
      *
     c     lloglu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lloglu_numm
     c                   kfld                    lloglu_suff
      *
     c/eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     *entry        plist
     c                   parm                    wpplor
     c                   parm                    wpfirm
     c                   parm                    wpnumm
     c                   parm                    wpsuff
      *
      *  legger inn firma i nøkklene
      *
     c                   move      wpfirm        otyfir
     c                   move      wpfirm        hedfir
     c                   move      wpfirm        lodtl1_fir
     c                   eval      p_firm = wpfirm
     c                   eval      lusrl1_user = l_user
     c                   eval      lusrl1_firm = l_firm
      *
      *  nullstiller felter som alltid skal være blanke/null
      *
     c                   endsr
