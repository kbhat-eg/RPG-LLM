     H DATEDIT(*DMY)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  System. . . . . : ASLAGR                                       *
      *  Program . . . . : LO526R                                       *
      *  Beskrivelse . . : Vise leverandør-opplysninger                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  Spesialversjon. :                                              *
      *  Tilpasset for . :                                              *
      *                                                                 *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.*
      *  --------- ------  ---------------------------------------------*
7.00  *  K00       220420  Utvidet skjermbile                      bsa  *
      *                                                                 *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     FLO526D    CF   E             WORKSTN
      *
     Frlevl1    IF   E           K DISK
     F                                     RENAME(RLEVPFR:rlevl1R)
      *
      *
     D                UDS
     D  l_firm               944    946  0
     D  l_fnav               951    980
      *
      *
      *  Forklaring til statuskoder
      *
      *       WRETUR  Benyttes ved retur fra ett annet program
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  B1 Behandler registrering av leverandør-nummer                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     B1TAG         TAG
      *
      *  Nullstiller/Blanker felter som skal benyttes
      *
     C     B1TAGA        TAG
      *
     C                   MOVE      *OFF          *IN41
     C                   MOVE      *ZERO         B1LDOR
     C                   MOVE      *ZERO         WWLDOR
      *
     C                   MOVE      *BLANK        B2NAVN
     C                   MOVE      *BLANK        B2GATE
     C                   MOVE      *BLANK        B2ADR2
     C                   MOVE      *BLANK        B2STED
      *
      *  Send Alt
      *
     C     B1TAGB        TAG
     C                   MOVE      *ZERO         B1VALG
     C                   MOVE      *BLANK        WRETUR
      *
     C                   EXFMT     B1BLD
      *
      *  F3=Avslutt program
      *
     C     *INKC         IFEQ      *ON
     C                   GOTO      XSLUTT
     C                   END
      *
      *  F4=Spørring i Leverandør-register
      *
     C     *INKD         IFEQ      *ON
     C                   CALL      'RL500R'
     C                   PARM                    levfir
     C                   PARM                    b1ldor
     C                   parm                    p_navn
      *
     C     WRETUR        IFNE      *BLANK
     C                   GOTO      B1TAG
     C                   END
     C                   END
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Sjekker opplysninger om leverandøren                           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     B1TAG0        TAG
      *
      *  Sjekk, Finnes leverandør i LEVERANDØR-REGISTER
      *
     C     B1LDOR        IFNE      WWLDOR
     C                   MOVE      B1LDOR        LEVNUM
     C     LEVKEY        CHAIN     rlevl1R                            90
     C     *IN90         IFEQ      *ON
     C                   MOVE      *ON           *IN31
     C                   MOVE      *OFF          *IN41
     C                   GOTO      B1TAGB
     C                   END
      *
     C                   MOVE      *ON           *IN41
     C                   MOVE      B1LDOR        WWLDOR
     C                   MOVE      RLNAVN        B2NAVN
     C                   MOVE      RLGATE        B2GATE
     C                   MOVE      RLADR2        B2ADR2
     C                   MOVEL     RLSTED        B2STED
      *
     C                   GOTO      B1TAGB
     C                   ENDIF
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Sjekker hvilket valg som er benyttet                           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      *  Kaller opp SUB-program for å vise   (LEV.DØR/ORDRE/LINJE)
      *
     C     B1VALG        IFEQ      01
     C                   MOVE      '1'           WRETUR
     C                   CALL      'VA556R'
     C                   PARM                    WRETUR
     C                   PARM                    LEVFIR
     C                   PARM                    OTYNUM
      *
     C     WRETUR        IFEQ      *BLANK
     C                   CALL      'LO510R'
     C                   PARM                    WRETUR
     C                   PARM                    OTYNUM
     C                   PARM                    B1VALG
     C                   PARM                    LEVFIR
     C                   PARM                    B1LDOR
     C                   PARM                    B2NAVN
     C                   GOTO      B1TAGB
     C                   ENDIF
     C                   ENDIF
      *
      *  Kaller opp SUB-program for å vise   (LEV.DØR/VARE)
      *
     C     B1VALG        IFEQ      02
     C                   MOVE      '1'           WRETUR
     C                   CALL      'VA556R'
     C                   PARM                    WRETUR
     C                   PARM                    LEVFIR
     C                   PARM                    OTYNUM
      *
     C     WRETUR        IFEQ      *BLANK
     C                   CALL      'LO510R'
     C                   PARM                    WRETUR
     C                   PARM                    OTYNUM
     C                   PARM                    B1VALG
     C                   PARM                    LEVFIR
     C                   PARM                    B1LDOR
     C                   PARM                    B2NAVN
     C                   GOTO      B1TAGB
     C                   ENDIF
     C                   ENDIF
      *
      *  Gå tilbake til start
      *
     C                   GOTO      B1TAGB
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     XSLUTT        TAG
      *
     C                   SETON                                        LR
     C                   RETURN
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     *INZSR        BEGSR
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     C                   MOVE      *BLANK        WRETUR            1
     C                   MOVE      *BLANK        OTYNUM            2
      *
      *  Redefinering av felter
      *
     C     *LIKE         DEFINE    B1LDOR        WWLDOR
     c     *like         define    rlnavn        p_navn
      *
      *  Redefinering av nøkler
      *
     C     *LIKE         DEFINE    RLFIRM        LEVFIR
     C     *LIKE         DEFINE    RLLEVR        LEVNUM
      *
      *  Key for file : LEVERANDØR-REGISTER
      *
     C     LEVKEY        KLIST
     C                   KFLD                    LEVFIR
     C                   KFLD                    LEVNUM
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     *ENTRY        PLIST
     C                   PARM                    WPPLOR            1
     C                   PARM                    WPFIRM            3 0
      *
      *  Legger inn firmanummer i nøkklene
      *
     C                   MOVE      l_firm        LEVFIR
      *
     C                   ENDSR
