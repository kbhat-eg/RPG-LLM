     H DFTACTGRP(*NO)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JV860R_XML
      * Beskrivelse . . : Leser NOBB XML Vare 8.0. Legger data i vare variable.
      *                   Kaller standard rpg program, som gjør sjekk av data
      *                   og oppdatere basen
      *
      *                   Dette programmet er skrevet i free format RPG
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       040414 BKV  Nytt program
6.30  * R6.30 230414 BKV  Støtte for NIB nr
6.31  * R6.30 240414 BKV  Støtte for NIB nr
6.32  * R6.30 190614 BKV  Leverandør.VareNummer økt fra 8 til 15 tegn
6.33  * R6.30 300614 BKV  Angi xpath i basen
6.34  * R6.30 160714 BKV  Lagre VareLeverandør i egen tabell
6.35  * R6.30 160714 BKV  Leverandør sin utgår dato settes ikke på pakning og pris
6.36  * R6.30 130814 BKV  Init av variabler
6.37  * R6.30 040914 BKV  Stopper programmet hvis ikke JSTSPF kan leses
6.38  * R6.30 040914 BKV  Oppdaterer dato i JSTSPF
6.39  * R6.30 061014 BKV  Ok hvis ingen moduler, dvs retur dagens dato
6.3A  * R6.30 150515 BKV  d_lvar og d_pvar økt til 20
6.3B  * R6.30 181215 BKV  unntak hvis nrfNr len > 8
6.3C  * R6.30 050216 BKV  Send med Nobb/Nib nummer til vare-leverandør
6.3D  * R6.30 070416 BKV  Logge utgåtte vare-leverandør
6.3E  * R6.30 011017 BKV  Håndtere for stort tall
6.3f  * 6.30  250918 bkv  Utvidet med trelast sortiment
6.3g  * R6.30 110119 BKV  Leverandør.VareNummer økt fra 15 til 20 tegn
8.01  *       120824 bof  NXS-19864 Vare-lev må ryddes hver gang, nobb legger
8.01  *                   ikke alltid ut alle leverandører.
8.03  *       251124 bof  NXS-20481 Bare nobb.lev må ryddes på jvlepf
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjvpkl1    if   e           k disk    rename(jvpkpfr:jvpkl1r)
6.30 fjstsl1    if   e           k disk    rename(jstspfr:jstsl1r)
6.33 fafpslr    if   e           k disk    rename(afpspfr:afpslrr)
6.38 fjstslu    uf   e           k disk    rename(jstspfr:jstslur)
6.3f fjvkhl1    if   e           k disk    rename(jvkhpfr:jvkhl1r)
8.01 fjvlelu    uf   e           k disk    rename(jvlepfr:jvlelur)
      *
6.30  * Definering av Local Data Area
6.30  *
6.30 d                uds
6.30 d l_user                911    920
6.30 d l_firm                944    946  0
6.33 d l_filg                931    933
      *
      * Definering av variabler i nøkler
      *
6.33 d afpslr_ufil     s                   like(l_filg)
6.33 d afpslr_uide     s                   like(afuide)
6.3f d jvkhl1_kvnr     s                   like(jvkvnr)
8.01 d jvlelu_lvnr     s                   like(jvlvnr)
      *
6.30 d w_firm          s                   like(jaafir)
     d p_stat          s              1
      * Brukes som parametre til jobblogging
     d p_jnav          s             15
     d p_jbid          s             41
     d p_jtxt          s             35
     d p_jsts          s              2  0
     d p_jmld          s            200
      * Definering av tabell med meldinger
6.3D d a_meld          s            100    dim(11) ctdata perrcd(1)
      *
     d p_sistDato      s             10a
     d d_blank         s              8a   Inz(*blank)
6.39 d w_lest          s              1  0 Inz(0)
      *
      *  Konstanter
      *
     d c_ofak_max      c                   999999,999999
     d c_bred_max      c                   999999
     d c_leng_max      c                   999999
     d c_hoyd_max      c                   999999
     d c_vekt_max      c                   999999999
     d c_volu_max      c                   9999,99999
     d c_antp_max      c                   999999,999999
     d c_10_4          c                   '10_4'
     d c_13_4          c                   '13_4'
     d c_10_5          c                   '10_5'
      *
      * START NOBB 8.0 Vare XML definisjon
      *
      * Path til vare taggen
     D vare_path       s             25a   Inz(
     D                                       'VareResult/VareListe/Vare')

      * Taggen <b:Vare>
     D Vare_t          DS                  Qualified
     D   Status                            LikeDS(Status_t)
     D   NOBBNr                       8a   varying
6.30 D   NIBNr                        8a   varying
     D   Sortimentskode...
     D                                2a   varying
     D   Sortimentsleverandor...
     D                                6a   varying
     D   Kategoriansvarlig...
     D                                3a   varying
     D   Kategoriomrade...
     D                               40a   varying
     D   Oppsalg...
     D                              100a   varying
     D   Mersalg...
     D                              100a   varying
     D   Brand...
     D                               35a   varying
     D   Utstillingskode...
     D                                1a   varying
     D   NOBBModulNr...
     D                                8a   varying
     D   VareType                    15a   varying
     D   VareTekst1                  35a   varying
     D   VareTekst2                  35a   varying
     D   LangVaretekst...
     D                              100a   varying
     D   ProdusentVareNr...
     D                               20a   varying
     D   UnderArbeid...
     D                                5a   varying
     D   BestarAvVareListe...
     D                                     LikeDS(BestarAvVareListe_t)
     D   Lagerfort                    5a   varying
     D   HarHoldbarhetsdato...
     D                                5a   varying
     D   TalerFrost                   5a   varying
     D   TUNKoblinger...
     D                                     LikeDS(TUNKoblinger_t)
     D   FINFOKoblinger...
     D                                     LikeDS(FINFOKoblinger_t)
     D   PrisEnhet                    5a   varying
     D*  PSEVaregruppe...
     D*                               5a   varying
     D   HarVarePSE                   5a   varying
     D   Statistikkenhet...
     D                                5a   varying
     D   ErstattesAvNOBBnr...
     D                                8a   varying
     D*  SorteringsIndex...
     D*                               8a   varying
     D   Varebeskrivelse...
     D                             4000a   varying
     D   Leverandorer...
     D                                     LikeDS(Leverandorer_t)
     D   Avgifter                          LikeDS(Avgifter_t)
     D   NRFData                           LikeDS(NRFData_t)
     D   FarligGods                        LikeDS(FarligGods_t)
     D   TollKoder                         LikeDS(TollKoder_t)
     D   GodkjenningsStatus...
     D                               30a   varying

      * Taggen: <b:TUNKoblinger>
     D TUNKoblinger_t...
     D                 DS                  Qualified
     D                                     based(Template)
     D   TUN                               LikeDS(TUN_t) dim(5)
     D   countTUN                     5i 0

      * Taggen: <b:TUN>
     D TUN_t           DS                  Qualified
     D                                     based(Template)
     D   Nummer                      20a   varying
     D   Navn                        50a   varying

      * Taggen: <b:FINFOKoblinger>
     D  FINFOKoblinger_t...
     D                 DS                  Qualified
     D                                     based(Template)
     D   FINFO                             LikeDS(FINFO_t) dim(5)
     D   countFINFO                   5i 0

     d c_ant_togf      c                   5

      * Taggen: <b:FINFO>
     D FINFO_t         DS                  Qualified
     D                                     based(Template)
     D   Nummer                      20a   varying
     D   Navn                        50a   varying

      * Taggen: <b:BestarAvVareListe>
     D BestarAvVareListe_t...
     D                 DS                  Qualified
     D                                     based(Template)
     D   BestarAvVare...
     D                                     LikeDS(BestarAvVare_t) Dim(99)
     D   countBestarAvVare...
     D                                5i 0

     d c_ant_bavv      c                   99

      * Taggen: <b:BestarAvVare>
     D BestarAvVare_t...
     D                 DS                  Qualified
     D                                     based(Template)
     D   NOBBNr                      10a   varying
     D   GTIN                        15a   varying
     D   Enhet                        5a   varying
     D   Antall                      15a   varying

      * Taggen <b:Leverandorer>
     D Leverandorer_t...
     D                 DS                  Qualified
     D                                     based(Template)
     D   Leverandor...
     D                                     LikeDS(Leverandor_t) Dim(50)
     D   countLeverandor...
     D                                5i 0

     d c_ant_lev       c                   50
     d d_levLest       s                   like(JWLDOR) dim(50)


      * Taggen <b:Leverandor>
     D Leverandor_t...
     D                 DS                  Qualified
     D                                     based(Template)
     D   DeltakerNummer...
     D                                6a   varying
     D   Navn                       100a   varying
     D   ErVareeier...
     D                                5a   varying
6.32 D   VareNummer                  20a   varying
     D   Pakninger                         LikeDS(Pakninger_t)
     D   Merkinger                         LikeDS(Merkinger_t)
     D   Medier                            LikeDS(Medier_t)
     D   UtgarDato                   11a   varying

      * Taggen <b:Pakninger>
     D Pakninger_t     DS                  Qualified
     D                                     based(Template)
     D   Pakning                           LikeDS(Pakning_t) Dim(50)
     D   countPakning...
     D                                5i 0

     d c_ant_pak       c                   50

      * Taggen <b:Pakning>
     D Pakning_t       DS                  Qualified
     D                                     based(Template)
     D   Status                            LikeDS(Status_t)
     D   PakningNr                    2a   varying
     D   PakningKlasse...
     D                                     LikeDS(PakningKlasse_t) Dim(3)
     D   countPakningKlasse...
     D                                5i 0
     D   PakningsEnhet...
     D                                3a
     D   ErPrisEnhet...
     D                                5a
     D   ErPSE                        5a
     D   KalkulertInnhold...
     D                               15a
     D   BestarAv                          LikeDS(BestarAv_t)
     D   GTIN                        20a
     D   GTINType                    10a
     D   PakningsMal...
     D                                     LikeDS(PakningsMal_t) Dim(2)
     D   countPakningsMal...
     D                                5i 0
     D   Lagerfort                    3a
     D   Bestillbar                   3a
     D   PalleInfo                         LikeDS(PalleInfo_t)
     D   FraDato                     11a
     D   TilDato                     11a

      * Taggen b:PakningKlasse
     D PakningKlasse_t...
     D                 DS                  Qualified
     D                                     based(Template)
     D   text                         1a   varying

      * Taggen: <b:BestarAv>
     D BestarAv_t      DS                  Qualified
     D                                     based(Template)
     D   Antall                      15a
     D   Enhet                        5a

      * Taggen: b:PakningsMal>
     D PakningsMal_t...
     D                 DS                  Qualified
     D                                     based(Template)
     D   Type                        10a
     D   Bredde                      15a
     D   Lengde                      15a
     D   Hoyde                       15a
     D   Vekt                        15a
     D   Volum                       15a

      * Taggen <b:PalleInfo>
     D PalleInfo_t     DS                  Qualified
     D                                     based(Template)
     D   DetaljistPakPrLag...
     D                               10a
     D   MaksStablevekt...
     D                               15a

      * Taggen <b:Merkinger>
     D Merkinger_t     DS                  Qualified
     D                                     based(Template)
6.34 D   Merking                           likeds(Merking_t) Dim(3)
     D   countMerking...
     D                                5i 0

      * Taggen: <ns0:Merking>
     D Merking_t       DS                  Qualified
     D                                     based(Template)
     D   text                        10a   varying

      * Taggen <b:Medier>
     D Medier_t        DS                  Qualified
     D                                     based(Template)
6.34 D   Media                             likeds(Media_t) Dim(25)
     D   countMedia...
     D                                5i 0

6.34 d c_ant_leme      c                   25

      * Taggen: <b:Media>
     D Media_t         DS                  Qualified
     D                                     based(Template)
     D   MediaType...
     D                               10a   varying
     D   URL                        100a   varying

      * Taggen: <b:Avgifter>
     D Avgifter_t      DS                  Qualified
     D                                     based(Template)
     D   Avgift                            likeds(Avgift_t) Dim(5)
     D   countAvgift...
     D                                5i 0

     d c_ant_avgift    c                   5

      *Taggen: <ns0:Avgift>
     D Avgift_t        DS                  Qualified
     D                                     based(Template)
     D   AvgiftsKode...
     D                                3a   varying
     D   AvgiftsTekst...
     D                               50a   varying

      * Taggen: <b:NRFData>
     D NRFData_t       DS                  Qualified
     D                                     based(Template)
     D   NRFNr                       10a   varying
     D   NRFGruppeNr...
     D                               10a   varying
     D   NRFLeverandorNr...
     D                                8a   varying
     D   Dimensjon...
     D                               60a   varying
     D   Navn                        60a   varying
     D   Tilleggsopplysninger...
     D                               40a   varying

      * Taggen: <b:FarligGods>
     D FarligGods_t    DS                  Qualified
     D                                     based(Template)
     D   TransportMerking...
     D                                     LikeDS(TransportMerking_t)
     D   EmballasjeMerking...
     D                                     LikeDS(EmballasjeMerking_t)
     D                                     dim(3)
     D   countEmballasjeMerking...
     D                                5i 0

      * Taggen: <ns0:TransportMerking>
     D TransportMerking_t...
     D                 DS                  Qualified
     D                                     based(Template)
     D   UNNr                         4a   varying
     D   FareNr                       6a   varying
     D   Beskrivelse...
     D                              100a   varying
     D   EmballasjeGruppe...
     D                               30a   varying

      * Taggen: <ns0:EmballasjeMerking>
     D EmballasjeMerking_t...
     D                 DS                  Qualified
     D                                     based(Template)
     D   text                        30a   varying

      * Taggen: <b:TollKoder>
     D TollKoder_t     DS                  Qualified
     D   NoTollKode...
     D                               15a   varying
     D   EuTollKode...
     D                               15a   varying

      * Taggen: <b:Status>
     D Status_t        DS                  Template
     D   Created                     25a
     D   Updated                     25a
      *
      * STOPP NOBB 8.0 XML definisjon
      *

      * info og commArea må være likt def. De brukes ikke i programmet, men må
      * være definert
     D info            s             10a
     D options         s            200a   varying

     d                 ds
6.3D d d_job_log                    150
     d  w_vare                        8    overlay(d_job_log:2)
     d  w_ojva                        8    overlay(d_job_log:10)
     d   w_ojva_num                   8  0 overlay(w_ojva:1)
     d  w_jvar                        8    overlay(d_job_log:18)
     d   w_jvar_num                   8  0 overlay(w_jvar:1)
     d  w_jvdt                        8    overlay(d_job_log:26)
     d   w_jvdt_num                   8  0 overlay(w_jvdt:1)
     d  w_jvpk                        8    overlay(d_job_log:34)
     d   w_jvpk_num                   8  0 overlay(w_jvpk:1)
     d  w_jvpg                        8    overlay(d_job_log:42)
     d   w_jvpg_num                   8  0 overlay(w_jvpg:1)
     d  w_jvpke                       8    overlay(d_job_log:50)
     d   w_jvpke_num                  8  0 overlay(w_jvpke:1)
     d  w_jvpku                       8    overlay(d_job_log:58)
     d   w_jvpku_num                  8  0 overlay(w_jvpku:1)
     d  w_jvpru                       8    overlay(d_job_log:66)
     d   w_jvpru_num                  8  0 overlay(w_jvpru:1)
6.34 d  w_jvlevn                      8    overlay(d_job_log:74)
6.34 d   w_jvlevn_num                 8  0 overlay(w_jvlevn:1)
6.34 d  w_jvleve                      8    overlay(d_job_log:82)
6.34 d   w_jvleve_num                 8  0 overlay(w_jvleve:1)
6.3D d  w_jvlevu                      8    overlay(d_job_log:90)
6.3D d   w_jvlevu_num                 8  0 overlay(w_jvlevu:1)
6.3D d  w_lnr                         5  0 overlay(d_job_log:98)
6.3D d  w_jbid                       41    overlay(d_job_log:103)

     d b_inlr          s              1

      *
      *  Dataelementer vare
      *
     d                 ds
     d d_vrec                      5900
     d  d_stat                        1    overlay(d_vrec:2)
     d   d_stat_num                   1  0 overlay(d_stat:1)
     d  d_vare                        8    overlay(d_vrec:3)
6.30 d   d_vare_num                   8  0 overlay(d_vare:1)
     d  d_nobb                        8    overlay(d_vrec:11)
     d   d_nobb_num                   8  0 overlay(d_nobb:1)
     d  d_vtx1                       35    overlay(d_vrec:19)
     d  d_vtx2                       35    overlay(d_vrec:54)
     d  d_modn                        8    overlay(d_vrec:89)
     d   d_modn_num                   8  0 overlay(d_modn:1)
6.3A d  d_lvar                       20    overlay(d_vrec:97)
6.3A d  d_pvar                       20    overlay(d_vrec:117)
6.3A d  d_eann                       13    overlay(d_vrec:137)
6.3A d   d_eann_num                  13  0 overlay(d_eann:1)
6.3A d  d_stre                       15    overlay(d_vrec:150)
6.3A d  d_leve                        1    overlay(d_vrec:165)
6.3A d   d_leve_num                   1  0 overlay(d_leve:1)
6.3A d  d_doki                       20    overlay(d_vrec:166)
6.3A d  d_preh                        3    overlay(d_vrec:186)
6.3A d  d_ete1                       35    overlay(d_vrec:189)
6.3A d  d_mmk1                        5    overlay(d_vrec:224)
6.3A d  d_mmk2                        5    overlay(d_vrec:229)
6.3A d  d_mlk1                        3    overlay(d_vrec:234)
6.3A d  d_mlk2                        3    overlay(d_vrec:237)
6.3A d  d_ftxt                      256    overlay(d_vrec:240)
6.3A d  d_crea                       10    overlay(d_vrec:496)
6.3A d   d_crea_iso                    d   overlay(d_crea:1) datfmt(*iso)
6.3A d  d_upda                       10    overlay(d_vrec:506)
6.3A d   d_upda_iso                    d   overlay(d_upda:1) datfmt(*iso)
6.3A d  d_dele                       10    overlay(d_vrec:516)
6.3A d   d_dele_iso                    d   overlay(d_dele:1) datfmt(*iso)
6.3A d  d_gsts                        1    overlay(d_vrec:526)
6.3A d   d_gsts_num                   1  0 overlay(d_gsts:1)
6.3A d  d_enob                        8    overlay(d_vrec:527)
6.3A d   d_enob_num                   8  0 overlay(d_enob:1)
6.3A d  d_utgd                       10    overlay(d_vrec:535)
6.3A d   d_utgd_iso                    d   overlay(d_utgd:1) datfmt(*iso)
6.3A d  d_vtype                       2    overlay(d_vrec:545)
6.3A d  d_vtxlang                   100    overlay(d_vrec:547)
6.3A d  d_uarb                        5    overlay(d_vrec:647)
6.3A d   d_uarb_num                   1  0 overlay(d_uarb:1)
6.3A d  d_lfor                        5    overlay(d_vrec:652)
6.3A d   d_lfor_num                   1  0 overlay(d_lfor:1)
6.3A d  d_hdat                        5    overlay(d_vrec:657)
6.3A d   d_hdat_num                   1  0 overlay(d_hdat:1)
6.3A d  d_tfro                        5    overlay(d_vrec:662)
6.3A d   d_tfro_num                   1  0 overlay(d_tfro:1)
6.3A d  d_fjernes                     5    overlay(d_vrec:667)
6.3A d  d_hpse                        5    overlay(d_vrec:672)
6.3A d   d_hpse_num                   1  0 overlay(d_hpse:1)
6.3A d  d_senh                        5    overlay(d_vrec:677)
6.3A d  d_unnr                        4    overlay(d_vrec:682)
6.3A d  d_fanr                        6    overlay(d_vrec:686)
6.3A d  d_fabs                      100    overlay(d_vrec:692)
6.3A d  d_fgru                       30    overlay(d_vrec:792)
6.3A d  d_embm1                      30    overlay(d_vrec:822)
6.3A d  d_embm2                      30    overlay(d_vrec:852)
6.3A d  d_embm3                      30    overlay(d_vrec:882)
6.3A d  d_tono                       15    overlay(d_vrec:912)
6.3A d  d_toeu                       15    overlay(d_vrec:927)
6.3A d  d_sort                       10    overlay(d_vrec:942)
6.3A d  d_sldo                        6    overlay(d_vrec:952)
6.3A d   d_sldo_num                   6  0 overlay(d_sldo:1)
6.3A d  d_kate                        3    overlay(d_vrec:958)
6.3A d   d_kate_num                   3  0 overlay(d_kate:1)
6.3A d  d_bran                       35    overlay(d_vrec:961)
6.3A d  d_utko                        1    overlay(d_vrec:996)
6.3A d  d_opps                      100    overlay(d_vrec:997)
6.3A d  d_mers                      100    overlay(d_vrec:1097)
6.3A d  d_kaom                       40    overlay(d_vrec:1197)
6.3A d  d_besk                     4000    overlay(d_vrec:1237)


      *
      *  Dataelementer består av vare
      *
     d                 ds
     d d_brec                       100    dim(100)
     d  d_uvar                        8    overlay(d_brec:2)
     d   d_uvar_num                   8  0 overlay(d_uvar:1)
6.31 d  d_ubnob                       8    overlay(d_brec:10)
6.31 d   d_ubnob_num                  8  0 overlay(d_ubnob:1)
     d  d_ugti                       14    overlay(d_brec:18)
     d   d_ugti_num                  14  0 overlay(d_ugti:1)
     d  d_uenh                        3    overlay(d_brec:32)
     d  d_uant                       15    overlay(d_brec:35)
     d   d_uant_num                  12  6 overlay(d_uant:1)

      *
      *  Dataelementer TUNKoblinger / FINFOKoblinger
      *
     d                 DS
     d d_tfrec                      100    dim(5)
     d  d_bnob                        8    overlay(d_tfrec:2)
     d   d_bnob_num                   8  0 overlay(d_bnob:1)
     d  d_btyp                        1    overlay(d_tfrec:10)
     d  d_bvar                       15    overlay(d_tfrec:11)
     d  d_btxt                       50    overlay(d_tfrec:26)
      *  Dataelementer TUNKoblinger / FINFOKoblinger
     d d_trec          s            100    dim(5)
     d d_frec          s            100    dim(5)
      *
      * Avgifter
     d                 DS
     d d_arec                       100    dim(5)
     d  d_anob                        8    overlay(d_arec:2)
     d   d_anob_num                   8  0 overlay(d_anob:1)
     d  d_akod                        3    overlay(d_arec:10)
     d  d_atxt                       50    overlay(d_arec:13)
      *
      * NRF
     d                 DS
     d d_nrec                       200
     d  d_nnob                        8a   overlay(d_nrec:2)
     d   d_nnob_num                   8  0 overlay(d_nnob:1)
     d  d_nnrf                       10a   overlay(d_nrec:10)
     d   d_nnrf_num                   8  0 overlay(d_nnrf:1)
     d  d_ngrp                       10a   overlay(d_nrec:20)
     d  d_nldo                       10a   overlay(d_nrec:30)
     d  d_ntx1                       60a   overlay(d_nrec:40)                   Dim
     d  d_ntx2                       60a   overlay(d_nrec:100)                  Navn
     d  d_ntx3                       40a   overlay(d_nrec:160)                  Tillegg
      *
      * Kategori
     d                 DS
     d d_krec                        50    dim(20)
     d  d_katc                        2    overlay(d_krec:2)
     d   d_katc_num                   2  0 overlay(d_katc:1)
     d  d_kata                        3    overlay(d_krec:4)
     d   d_kata_num                   3  0 overlay(d_kata:1)
     d  d_kato                       40    overlay(d_krec:7)
      * Kategori look up
     d d_katoLU        s             40    dim(20)
     d w_kate          s              5i 0

      *
      *  Dataelementer leverandør
      *
     d                 ds
6.34 d d_lrec                       200
     d  d_deltakerNummer...
     d                                6a   overlay(d_lrec)
     d   d_del_num                    6  0 overlay(d_deltakerNummer:1)
     d  d_navn                      100a   overlay(d_lrec:7)
     d  d_erVareeier...
     d                                1    overlay(d_lrec:107)
     d    d_er_eier                   1  0 overlay(d_erVareeier:1)
     d  d_vareNummer                 20a   overlay(d_lrec:108)
     d  d_UtgarDato                  10a   overlay(d_lrec:128)
     d   d_UtgarD_iso                  d   overlay(d_UtgarDato:1) datfmt(*iso)
6.34 d  d_merking1                   15a   overlay(d_lrec:138)
6.34 d  d_merking2                   15a   overlay(d_lrec:153)
6.34 d  d_merking3                   15a   overlay(d_lrec:168)
6.34 d  d_vareL                       8    overlay(d_lrec:183)
6.34 d   d_vare_numL                  8  0 overlay(d_vareL:1)
6.34 d  d_nobbL                       8    overlay(d_lrec:191)
6.34 d   d_nobb_numL                  8  0 overlay(d_nobbL:1)
6.34  *
6.34  *  Dataelementer Leverandør Media
6.34  *
6.34 d                 ds
6.34 d d_lmrec                      120    dim(25)
6.34 d  d_mediaType                  10a   overlay(d_lmrec)
6.34 d  d_mediaUrl                  100a   overlay(d_lmrec:11)

      *
      *  Dataelementer pakning
      *
     d                 ds
     d d_prec                       500    dim(50)
     d  d_pakn                        3    overlay(d_prec:2)
     d  d_pakn_num                    3  0 overlay(d_pakn:1)
     d  d_pkla1                       1    overlay(d_prec:5)
     d  d_pkla2                       1    overlay(d_prec:6)
     d  d_pkla3                       1    overlay(d_prec:7)
     d  d_enhe                        3    overlay(d_prec:8)
     d  d_epeh                        1    overlay(d_prec:11)
     d   d_erpeh_num                  1  0 overlay(d_epeh:1)
     d  d_ePSE                        1    overlay(d_prec:12)
     d   d_pse_num                    1  0 overlay(d_ePSE:1)
     d  d_ofak                       15    overlay(d_prec:13)
     d   d_ofak_num                  14  6 overlay(d_ofak:1)
     d  d_antp                       15    overlay(d_prec:28)
     d   d_betAnt_num                14  6 overlay(d_antp:1)
     d  d_bestarAvEht                 5    overlay(d_prec:43)
     d  d_GTIN                       20    overlay(d_prec:48)
     d   d_GTIN_num                  14  0 overlay(d_GTIN:1)
     d  d_GTINType                   10    overlay(d_prec:68)
     d  d_lfop                        5    overlay(d_prec:78)
     d   d_lagp_num                   1  0 overlay(d_lfop:1)
     d  d_best                        5    overlay(d_prec:83)
     d   d_best_num                   1  0 overlay(d_best:1)
     d  d_fdat                       15    overlay(d_prec:88)
     d   d_fraD_iso                    d   overlay(d_fdat:1) datfmt(*iso)
     d  d_tdat                       15    overlay(d_prec:103)
     d   d_tilD_iso                    d   overlay(d_tdat:1) datfmt(*iso)
     d  d_nbre                       15    overlay(d_prec:118)
     d   d_BredN_num                 10  4 overlay(d_nbre:1)
     d  d_nlen                       15    overlay(d_prec:133)
     d   d_LengN_num                 10  4 overlay(d_nlen:1)
     d  d_nhoy                       15    overlay(d_prec:148)
     d   d_HoyN_num                  10  4 overlay(d_nhoy:1)
     d  d_nvek                       15    overlay(d_prec:163)
     d   d_VektN_num                 13  4 overlay(d_nvek:1)
     d  d_nvol                       15    overlay(d_prec:178)
     d   d_VolN_num                   9  5 overlay(d_nvol:1)
     d  d_bred                       15    overlay(d_prec:193)
     d   d_BredB_num                 10  4 overlay(d_bred:1)
     d  d_leng                       15    overlay(d_prec:208)
     d   d_LengB_num                 10  4 overlay(d_leng:1)
     d  d_hoyd                       15    overlay(d_prec:223)
     d   d_HoyB_num                  10  4 overlay(d_hoyd:1)
     d  d_vekt                       15    overlay(d_prec:238)
     d   d_VektB_num                 13  4 overlay(d_vekt:1)
     d  d_volu                       15    overlay(d_prec:253)
     d   d_VolB_num                   9  5 overlay(d_volu:1)
     d  d_papp                        6    overlay(d_prec:268)
     d   d_papp_num                   6  0 overlay(d_papp:1)
     d  d_pmsb                       15    overlay(d_prec:274)
     d   d_pmsb_num                  14  4 overlay(d_pmsb:1)
     d  d_varp                        8    overlay(d_prec:289)
     d   d_vare_numP                  8  0 overlay(d_varp:1)
     d  d_delp                      100    overlay(d_prec:297)
     d   d_del_numP                   6  0 overlay(d_delp:1)
     d  d_prehP                       3    overlay(d_prec:397)
     d  d_noda                       25    overlay(d_prec:400)
     d   d_creD_iso                    d   overlay(d_noda:1) datfmt(*iso)
     d  d_neda                       25    overlay(d_prec:425)
     d   d_updD_iso                    d   overlay(d_neda:1) datfmt(*iso)
     d  d_udat                       10    overlay(d_prec:450)
     d   d_udat_iso                    d   overlay(d_udat:1) datfmt(*iso)

     d  w_kontroll     s             20  6

      * Prototype for externt program sjekker vare og lagrer i basen
      *  Overfører vara datastruktur som parameter
     D P_BehVare       PR                  ExtPgm('JV861R_XML')
     d  p_vrec                             like(d_vrec)
     d  p_brec                             like(d_brec) dim(100)
     d  p_trec                             like(d_trec) dim(5)
     d  p_frec                             like(d_frec) dim(5)
     d  p_arec                             like(d_arec) dim(5)
     d  p_nrec                      200
     d  p_krec                             like(d_krec) dim(20)
6.3D d  p_job_log                   150
     d  b_inlr                        1

      * Prototype for externt program sjekker vare og lagrer i basen
      *  Overfører vara datastruktur som parameter
     D P_BehPakn       PR                  ExtPgm('JV862R_XML')
     d  p_prec                      500    Dim(50)
     d  p_sist_nob                    8
     d  p_levLest                          like(jwldor) Dim(50)
6.3D d  p_job_log                   150
     d  b_inlr                        1

6.34  * Prototype for externt program sjekker vare og lagrer i basen
6.34  *  Overfører vara datastruktur som parameter
6.34 D P_BehLeve       PR                  ExtPgm('JV870R_XML')
6.34 d  p_lrec                      200
6.34 d  p_lmrec                     120    dim(25)
6.3D d  p_job_log                   150
6.34 d  b_inlr                        1

6.3f  * Prototype for externt program oppdatere logistikk vare
6.3f D P_OppLogi       PR                  ExtPgm('JK101R')

     D P_AB705R        PR                  ExtPgm('AB705R')
     d  p_jbid                       41
     d  p_jsts                        2  0
     d  p_jmld                      200

     D P_AB710R        PR                  ExtPgm('AB710R')
     d  p_jnav                       15
     d  p_jbid                       41

     D P_AB700R        PR                  ExtPgm('AB700R')
     d  p_jnav                       15
     d  p_jbid                       41
     d  p_jtxt                       35

      * Prototype for procedure LesVarer
      * Leser inn 5 varer, som blir behandlet av procduren LesVarer
     D LesVarer        PR            10i 0
     D   commArea                    10a
     D   Vare                              likeds(Vare_t)
     D                                     dim(5) const
     D   count                       10u 0 value

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

       // ------------------------------------- Prototypes
     D JV860R_XML      PR                  ExtPgm('JV860R_XML')
     D   filNavn                    255a   options(*varsize)
     D   lengde                       3p 0 const
     D   sisteDato                   10a
6.38 D   oppdatSts                    1p 0 const

       // ----------------------- Main procedure interface
     D JV860R_XML      PI
     D   filNavn                    255a   options(*varsize)
     D   lengde                       3p 0 const
     D   sisteDato                   10a
6.38 D   oppdatSts                    1p 0 const

     D   navnet        s            100a   varying
6.33 D   len           s              3i 0

6.30  //
6.30  // Key for oppslag på status
6.30  //
6.30 c     jstsl1_key    klist
6.30 c                   kfld                    w_firm

6.33  *
6.33  * Key for propertiesfil
6.33  *
6.33 c     afpslr_key    klist
6.33 c                   kfld                    afpslr_ufil
6.33 c                   kfld                    afpslr_uide
6.38  *
6.38  * Key for oppdatering av kode/status
6.38  *
6.38 c     jstslu_key    klist
6.38 c                   kfld                    w_firm
      *
6.3f  *
6.3f  * Key for les av logistikkvare
6.3f  *
6.3f c     jvkhl1_key    klist
6.3f c                   kfld                    w_firm
6.3f c                   kfld                    jvkhl1_kvnr
      *
8.01  *
8.01  * Key for les av vare-leverandør
8.01  *
8.01 c     jvlelu_key    klist
8.01 c                   kfld                    jvlelu_lvnr

      *

      /free

6.3f   Exec SQL
6.3f     Set Option DatFmt=*ISO;

         p_sistDato = '';
6.30   w_firm = l_firm;
6.30   chain jstsl1_key jstsl1;
6.37   if %found();

         len = lengde;
         navnet = %trim(%subst(filNavn:1:len));

         w_ojva = *zeros;
         w_jvar = *zeros;
         w_jvdt = *zeros;
         w_jvpk = *zeros;
         w_jvpg = *zeros;
         w_jvpke = *zeros;
         w_jvpku = *zeros;
         w_jvpru = *zeros;
6.36     w_jvlevn = *zeros;
6.36     w_jvleve = *zeros;
6.3D     w_jvlevu = *zeros;
         w_kate = *zero;
         w_lnr = *zero;
6.39     w_lest = 0;

         KategoriInit();

         // Vi starter jobblogging
         p_jnav = 'VARERXML';
         p_jtxt = *blank;
         CallP P_AB700R (p_jnav : p_jbid : p_jtxt);

         w_jbid = p_jbid;


6.33     //  Hent inn NOBB 80 XML path som skal brukes
6.33     afpslr_ufil = l_filg;
6.33     afpslr_uide = 'NOBB80VAR';
6.33     chain afpslr_key afpslrr;
6.33     if %found;
6.33       vare_path = *blanks;
6.33       vare_path = %trim(afudss);
6.33     else;
6.33       p_stat = '1';
6.33     endif;


6.33     if (p_stat <> '1');

6.36       b_inlr = *on;
           options = 'doc=file +
                      case=any +
                      countprefix=count +
                      datasubf=text +
                      ns=remove +
                      path='+vare_path+' '+
                     'allowmissing=yes +
                      allowextra=yes';

            // Bruker RPG xml-info til å lese xml filen.
            // Behandling av innleste varer gjøres av LesVarer
            xml-into %handler(LesVarer: info) %xml(navnet: options);

           // lagrer kategori
           VareInit();
           d_katc_num(1) = w_kate;
           CallP P_BehVare (d_vrec : d_brec : d_trec : d_frec : d_arec : d_nrec
            : d_krec : d_job_log : b_inlr);

           b_inlr = *off;

           CallP P_BehVare (d_vrec : d_brec : d_trec : d_frec : d_arec : d_nrec
             : d_krec : d_job_log : b_inlr);

           CallP P_BehPakn (d_prec : d_blank : d_levLest : d_job_log : b_inlr );

6.34       CallP P_BehLeve (d_lrec : d_lmrec : d_job_log : b_inlr );

6.3f       CallP P_OppLogi();

6.33     endif;


         sisteDato = p_sistDato;
6.39   // hvis w_lest er 0, så var det ingen elementer i XML, dvs tom liste, men det er ok
6.39   if (sisteDato = '' and w_lest = 0);
6.39     sisteDato = %char(%date(): *iso);
6.3f     p_sistDato = %char(%date(): *iso);
6.39   endif;


6.33   if (p_sistDato = '');
6.33     p_stat = '1';
6.33   endif;
6.37   else;
6.37     sisteDato = '';
6.37     p_stat = '1';
6.37     p_jsts = 40;
6.37     p_jmld = *blanks;
6.37     p_jmld = 'Fikk ikke lest jstspf';
6.37     CallP P_AB705R (p_jbid : p_jsts : p_jmld);
6.37   endif;

       // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
       // Avslutt program
       // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
       // Vi oppsummerer

       if (p_stat = '1');
         p_jsts = 50;
         p_jmld = 'Alvorlig feil oppstod i JV860R_XML';

         CallP P_AB705R (p_jbid : p_jsts : p_jmld);

       else;

6.38     // Oppdaterer statusregister med dato for innlesing
6.38     if oppdatSts = 1;
6.38       chain jstslu_key   jstslu;
6.38       jaaopd = %date;
6.38       update  jstslur;
6.38     endif;

         p_jsts = 10;
         p_jmld = *blanks;
         p_jmld = %trim(a_meld(1)) + ' ' + w_jvar;
         CallP P_AB705R (p_jbid : p_jsts : p_jmld);

         p_jsts = 10;
         p_jmld = *blanks;
         p_jmld = %trim(a_meld(5)) + ' ' + w_ojva;
         CallP P_AB705R (p_jbid : p_jsts : p_jmld);

         p_jsts = 10;
         p_jmld = *blanks;
         p_jmld = %trim(a_meld(2)) + ' ' + w_jvdt;
         CallP P_AB705R (p_jbid : p_jsts : p_jmld);

         p_jsts = 10;
         p_jmld = *blanks;
         p_jmld = %trim(a_meld(3)) + ' ' + w_jvpk;
         CallP P_AB705R (p_jbid : p_jsts : p_jmld);

         p_jsts = 10;
         p_jmld = *blanks;
         p_jmld = %trim(a_meld(4)) + ' ' + w_jvpg;
         CallP P_AB705R (p_jbid : p_jsts : p_jmld);

         p_jsts = 10;
         p_jmld = *blanks;
         p_jmld = %trim(a_meld(6)) + ' ' + w_jvpke;
         CallP P_AB705R (p_jbid : p_jsts : p_jmld);

         p_jsts = 10;
         p_jmld = *blanks;
         p_jmld = %trim(a_meld(7)) + ' ' + w_jvpku;
         CallP P_AB705R (p_jbid : p_jsts : p_jmld);

         p_jsts = 10;
         p_jmld = *blanks;
         p_jmld = %trim(a_meld(8)) + ' ' + w_jvpru;
         CallP P_AB705R (p_jbid : p_jsts : p_jmld);

6.34     p_jsts = 10;
6.34     p_jmld = *blanks;
6.34     p_jmld = %trim(a_meld(9)) + ' ' + w_jvlevn;
6.34     CallP P_AB705R (p_jbid : p_jsts : p_jmld);

         p_jsts = 10;
         p_jmld = *blanks;
         p_jmld = %trim(a_meld(10)) + ' ' + w_jvleve;
         CallP P_AB705R (p_jbid : p_jsts : p_jmld);

6.3D     p_jsts = 11;
6.3D     p_jmld = *blanks;
6.3D     p_jmld = %trim(a_meld(11)) + ' ' + w_jvlevu;
6.3D     CallP P_AB705R (p_jbid : p_jsts : p_jmld);

       endif;

        //* Vi kvitterer ut jobben fra jobbstatusfil
       CallP P_AB710R (p_jnav : p_jbid );

       *inlr = *on;

       /end-free

      *
      * Denne proseduren mottar x antall varer fra XML filen
      *
      *
     P LesVarer        B
     D LesVarer        pi            10i 0
     D   commArea                    10a
     D   Vare                              likeds(Vare_t)
     D                                     dim(5) const
     D   count                       10u 0 value

     D t_varer         s             10u 0
     D d               s              5i 0
     D e               s              5i 0
     D
      /free

         for t_varer = 1 to count;

           BehVarer(Vare(t_varer));
6.3f   //    w_lest = 1;


           if %error;
             dsply ('Lesing av XML fil feilet');
              // Error writing record: Abort XML parser
              //  FIXME: Add code here to notify user.
              return -1;
           endif;

         endfor;

         return 0;

      /end-free
     P LesVarer        E

      *
      * Behandler 1 vare. Overføre vare data til data struktur
      * og kaller programmet P_BehVare
     P BehVarer        B
     D BehVarer        pi            10i 0
     D   Vare                              likeds(Vare_t) const
     D
     D t               s              5i 0
     D t2              s              5i 0
     c
     c
      /free

       w_vare = '';
       VareInit();

         if (%Trim(Vare.Status.Created) <> '');
           d_crea = %subst(Vare.Status.Created:1:10);
         endif;

         if (%Trim(Vare.Status.Updated) <> '');
           d_upda = %subst(Vare.Status.Updated:1:10);
         endif;

       d_nobb = Vare.NOBBNr;

6.3f  // sjekk om det er logistikkvare
6.3f   jvkhl1_kvnr = d_nobb;
6.3f   chain jvkhl1_key jvkhl1;
6.3f   if (%found(jvkhl1) and (jvknup = 0));
6.3f     return 0;  // skal ikke oppdateres fra NOBB
6.3f   endif;

6.3f   w_lest = 1;

6.30   if (jaanob = 1);
6.30     d_vare = Vare.NOBBNr;
6.30   else;
6.30     if (%Trim(Vare.NIBNr) <> '');
6.30       d_vare = Vare.NIBNr;
6.30     else;
6.30       d_vare = Vare.NOBBNr;
6.30     endif;
6.30   endif;
       d_sort = Vare.Sortimentskode;
       if (%Trim(Vare.Sortimentsleverandor) <> '');
         d_sldo_num = %Int(Vare.Sortimentsleverandor);
       endif;
       if (%Trim(Vare.Kategoriansvarlig) <> '');
         d_kate_num = %Int(Vare.Kategoriansvarlig);
       endif;
       d_bran = Vare.Brand;
       d_utko = Vare.Utstillingskode;
       d_opps = Vare.Oppsalg;
       d_mers = Vare.Mersalg;
       d_kaom = Vare.Kategoriomrade;

       if (%Trim(Vare.Kategoriomrade) <> '' and
          %Trim(Vare.Kategoriansvarlig) <> '' );
         if (%lookup(%Trim(Vare.Kategoriomrade):d_katoLU)) = *zero;
           w_kate = w_kate + 1;
           d_kata_num(w_kate) = %Int(%Trim(Vare.Kategoriansvarlig));
           d_kato(w_kate) = %Trim(Vare.Kategoriomrade);
           d_katoLU(w_kate) = %Trim(Vare.Kategoriomrade);
         endif;
       endif;

       d_modn = Vare.NOBBModulNr;
       Select;
       When %Trim(Vare.VareType) = 'Standard';
         d_vtype = 'ST';
       When %Trim(Vare.VareType) = 'Spesialvare';
         d_vtype = 'SP';
       When %Trim(Vare.VareType) = 'Sammensatt';
         d_vtype = 'SS';
       When %Trim(Vare.VareType) = 'Display';
         d_vtype = 'DS';
       endsl;
       d_vtx1  = Vare.VareTekst1;
       d_vtx2 = Vare.VareTekst2;
       d_vtxlang = Vare.LangVaretekst;
       d_pvar = Vare.ProdusentVareNr;
       if (Vare.UnderArbeid = 'Ja');
        d_uarb_num = 1;
       else;
        d_uarb_num = 0;
       endif;
       if (Vare.Lagerfort = 'Ja');
        d_lfor_num = 1;
       else;
        d_lfor_num = 0;
       endif;
       if (Vare.HarHoldbarhetsdato = 'Ja');
        d_hdat_num = 1;
       else;
        d_hdat_num = 0;
       endif;
       if (Vare.TalerFrost = 'Ja');
        d_tfro_num = 1;
       else;
        d_tfro_num = 0;
       endif;
       d_preh = Vare.PrisEnhet;
       if (Vare.HarVarePSE = 'Ja');
        d_hpse_num = 1;
       else;
        d_hpse_num = 0;
       endif;
       d_senh = Vare.Statistikkenhet;
       if (%Trim(Vare.ErstattesAvNOBBnr) <> '');
         d_enob_num = %Int(Vare.ErstattesAvNOBBnr);
       endif;
       d_besk = Vare.Varebeskrivelse;

       select ;
       when %Trim(Vare.GodkjenningsStatus) = 'KontrollertGodkjent';
         d_gsts_num =  1;
       when %Trim(Vare.GodkjenningsStatus) = 'KontrollertUnderkjent';
         d_gsts_num =  4;
       when %Trim(Vare.GodkjenningsStatus) = 'AvventerGodkjenning';
         d_gsts_num =  4;
       when %Trim(Vare.GodkjenningsStatus) = 'IkkeGodkjentst';
         d_gsts_num =  4;
       endsl;

       BehFarlig(Vare.FarligGods);
       BehTollKoder(Vare.TollKoder);
       BehBestarAv(Vare.BestarAvVareListe);
       TUN_FINFOKoblingerInit();
       BehTUNKoblinger(Vare.TUNKoblinger);
       TUN_FINFOKoblingerInit();
       BehFINFOKoblinger(Vare.FINFOKoblinger);
       BehAvgift(Vare.Avgifter);
       BehNrf(Vare.NRFData);

       BehLever(Vare.Leverandorer);

       CallP P_BehVare (d_vrec : d_brec : d_trec : d_frec : d_arec : d_nrec
              : d_krec : d_job_log : b_inlr);

       p_sistDato = Vare.Status.Updated;

       return 0;

      /end-free

     P BehVarer        E

      *
      * Behandler Består av
      *
     P BehBestarAv     B
     D BehBestarAv     pi            10i 0
     D  BestarAvVareListe...
     D                                     likeds(BestarAvVareListe_t) const
     D
     D
     D t               s              5i 0
6.3E D   desimal       s             20  6
      /free

       BestarAvInit();

       for t = 1 to BestarAvVareListe.countBestarAvVare;
6.31     d_uvar_num(t) = %Int(d_vare);

6.31     d_ubnob_num(t) = %Int(BestarAvVareListe.BestarAvVare(t).NOBBNr);
         d_ugti_num(t) = %Int(BestarAvVareListe.BestarAvVare(t).GTIN);
         d_uenh(t) = BestarAvVareListe.BestarAvVare(t).Enhet;
6.3E     desimal = %Dech(BestarAvVareListe.BestarAvVare(t).Antall:20:6);
6.3E     if (desimal > 10000);
6.3E       d_uant_num(t) = 9999;
6.3E     else;
6.3E       d_uant_num(t) =
6.3E          %Dech(BestarAvVareListe.BestarAvVare(t).Antall:12:6);
6.3E     endif;
       endfor;

       return 0;

      /end-free

     P BehBestarAv     E

      *
      * Behandler TUNKoblinger
      *
     P BehTUNKoblinger...
     P                 B
     D BehTUNKoblinger...
     D                 pi            10i 0
     D  TUNKoblinger...
     D                                     likeds(TUNKoblinger_t) const
     D
     D
     D t               s              5i 0
      /free

       for t = 1 to TUNKoblinger.countTUN;

         d_bnob_num(t) = d_nobb_num;
         d_btyp(t) = 'T';
         d_bvar(t) = TUNKoblinger.TUN(t).Nummer;
         d_btxt(t) = TUNKoblinger.TUN(t).Navn;

       endfor;

       d_trec = d_tfrec;

       return 0;

      /end-free

     P BehTUNKoblinger...
     P                 E

      *
      * Behandler FINFOKoblinger
      *
     P BehFINFOKoblinger...
     P                 B
     D BehFINFOKoblinger...
     D                 pi            10i 0
     D  FINFOKoblinger...
     D                                     likeds(FINFOKoblinger_t) const
     D
     D
     D t               s              5i 0
      /free

       for t = 1 to FINFOKoblinger.countFINFO;
         d_bnob_num(t) = d_nobb_num;
         d_btyp(t) = 'F';
         d_bvar(t) = FINFOKoblinger.FINFO(t).Nummer;
         d_btxt(t) = FINFOKoblinger.FINFO(t).Navn;
       endfor;

       d_frec = d_tfrec;

       return 0;

      /end-free

     P BehFINFOKoblinger...
     P                 E

      *
      * Behandler Avgifter
      *
     P BehAvgift       B
     D BehAvgift       pi            10i 0
     D  Avgifter                           likeds(Avgifter_t) const
     D
     D
     D t               s              5i 0
      /free

       AvgiftInit();

       for t = 1 to Avgifter.countAvgift;
         d_anob_num(t) = d_nobb_num;
         d_akod(t) = Avgifter.Avgift(t).AvgiftsKode;
         d_atxt(t) = Avgifter.Avgift(t).AvgiftsTekst;
       endfor;

       return 0;

      /end-free

     P BehAvgift       E


      *
      * Behandler NRF Data
      *
     P BehNrf          B
     D BehNrf          pi            10i 0
     D  NRFData                            likeds(NRFData_t) const
     D
      /free

       NrfInit();

       if (%Trim(NRFData.NRFNr)) = '';
         return 0;
       endif;

       d_nnob_num = d_nobb_num;
6.3B   if (%len(%Trim(NRFData.NRFNr))) > 8;
6.3B     d_nnrf_num = 99999999;
6.3B   else;
6.3B     d_nnrf_num = %Int(NRFData.NRFNr);
6.3B   endif;
       d_ngrp = NRFData.NRFGruppeNr;
       d_nldo = NRFData.NRFLeverandorNr;
       d_ntx1 = NRFData.Dimensjon;
       d_ntx2 = NRFData.Navn;
       d_ntx3 = NRFData.Tilleggsopplysninger;

       return 0;

      /end-free

     P BehNrf          E

      *
      * Behandler Farlig gods
      *
     P BehFarlig       B
     D BehFarlig       pi            10i 0
     D  FarligGods                         likeds(FarligGods_t) const
     D
     D t               s              5i 0
      /free

       FarligInit();

       d_unnr = FarligGods.TransportMerking.UNNr;
       d_fanr = FarligGods.TransportMerking.FareNr;
       d_fabs = FarligGods.TransportMerking.Beskrivelse;
       d_fgru = FarligGods
         .TransportMerking.EmballasjeGruppe;

       for t = 1 to FarligGods.countEmballasjeMerking;
         Select;
         When t = 1;
           d_embm1
           = FarligGods.EmballasjeMerking(t).text;
         When t = 2;
           d_embm2
           = FarligGods.EmballasjeMerking(t).text;
         When t = 3;
           d_embm3
           = FarligGods.EmballasjeMerking(t).text;

         EndSL;
       endfor;

       return 0;

      /end-free

     P BehFarlig       E

      *
      * Behandler Tollkoder
      *
     P BehTollKoder    B
     D BehTollKoder    pi            10i 0
     D  TollKoder                          likeds(TollKoder_t) const
     D
      /free

       TollkoderInit();

       d_tono = TollKoder.NoTollKode;
       d_toeu = TollKoder.EuTollKode;

       return 0;

      /end-free

     P BehTollKoder    E

      *
      * Behandler leverandører
     P BehLever        B
     D BehLever        pi            10i 0
     D   Leverandorer...
     D                                     likeds(Leverandorer_t) const
     D
     D t               s              5i 0
      /free

       for t = 1 to c_ant_lev;
         d_levLest(t) = *zero;
       endfor;

8.01  * slette vare-leverandør før nye kommer.
8.01 c                   eval      jvlelu_lvnr = d_vare
8.01 c     jvlelu_key    setll     jvlelur
8.01 c     jvlelu_key    reade     jvlelu
8.01 c                   dow       not %eof(jvlelu)
8.03 c                   if        jvlldo < 900000
8.01 c                   delete    jvlelur
8.03 c                   else
8.03 c                   unlock    jvlelu
8.03 c                   endif
8.01 c     jvlelu_key    reade     jvlelu
8.01 c                   enddo
8.01    //

       for t = 1 to Leverandorer.countLeverandor;
         BehLev(Leverandorer.Leverandor(t) );
         d_levLest(t) = %Int(Leverandorer.Leverandor(t).DeltakerNummer);
       endfor;


6.30   CallP P_BehPakn (d_prec : d_vare : d_levLest : d_job_log : b_inlr );

       return 0;

      /end-free

     P BehLever        E

      *
      * Behandler leverandør
     P BehLev          B
     D BehLev          pi            10i 0
     D   Leverandor...
     D                                     likeds(Leverandor_t) const
     D
     D t               s              5i 0
      /free

       LevInit();
6.34   LeveMediaInit();

6.34   d_nobb_numL = d_nobb_num;

       if (Leverandor.ErVareeier = 'Ja');
         d_lvar =  Leverandor.VareNummer;
       endif;

       d_del_num = %Int(Leverandor.DeltakerNummer);
       d_navn = Leverandor.Navn;
6.34   if (Leverandor.ErVareeier = 'Ja');
6.34    d_er_eier = 1;
6.34   else;
6.34    d_er_eier = 0;
6.34   endif;
       d_vareNummer  = Leverandor.VareNummer;
       if (%Trim(Leverandor.UtgarDato) <> '');
           d_UtgarDato = %subst(Leverandor.UtgarDato:1:10);
       endif;


       for t = 1 to Leverandor.Merkinger.countMerking;
         Select;
         When t = 1;
           d_merking1 = Leverandor.Merkinger.Merking(t).text;
         When t = 2;
           d_merking2 = Leverandor.Merkinger.Merking(t).text;
6.34     When t = 3;
6.34       d_merking2 = Leverandor.Merkinger.Merking(t).text;

         EndSL;
       endfor;

       for t = 1 to Leverandor.Medier.countMedia;
6.34     d_mediaType(t) = Leverandor.Medier.Media(t).MediaType;
6.34     d_mediaUrl(t) = Leverandor.Medier.Media(t).URL;
       endfor;

       BehPakr(Leverandor.Pakninger);

6.3C   d_vareL = d_vare; //enten NIB eller NOBB nr

6.34   CallP P_BehLeve (d_lrec : d_lmrec : d_job_log : b_inlr );

       return 0;

      /end-free

     P BehLev          E

      *
      * Behandler Pakninger
     P BehPakr         B
     D BehPakr         pi            10i 0
     D   Pakninger...
     D                                     likeds(Pakninger_t) const
     D
     D t               s              5i 0
     D t1              s              5i 0
     D typen           s             20a   varying
     D pakning         DS                  likeds(Pakning_t)
     D pakningsMal     DS                  likeds(PakningsMal_t)

      /free

       PakInit();

       for t = 1 to Pakninger.countPakning;
         pakning = Pakninger.Pakning(t);

6.30     d_vare_numP(t) = d_vare_num;
         d_del_numP(t) = d_del_num;
         d_prehP(t) = d_preh;
         d_pakn_num(t) = %Int(pakning.PakningNr);

         for t1
             = 1 to pakning.countPakningKlasse;
           Select;
           When t1 = 1;
              d_pkla1(t)
                = pakning.PakningKlasse(t1).text;
           When t1 = 2;
              d_pkla2(t)
                = pakning.PakningKlasse(t1).text;
           When t1 = 3;
              d_pkla3(t)
                = pakning.PakningKlasse(t1).text;
           EndSL;
         endfor;

         d_enhe(t) = pakning.PakningsEnhet;

         if (pakning.ErPrisEnhet = 'Ja');
          d_erpeh_num(t) = 1;
         else;
          d_erpeh_num(t) = 0;
         endif;

         //d_erPSE(t) = pakning.ErPSE;
         if (pakning.ErPSE = 'Ja');
          d_pse_num(t) = 1;
         else;
          d_pse_num(t) = 0;
         endif;
         d_ofak_num(t) = %Dech(pakning.KalkulertInnhold:14:6);
         d_betAnt_num(t) = %Dech(pakning.BestarAv.Antall:14:6);
         d_bestarAvEht(t) = pakning.BestarAv.Enhet;
         if (%Trim(pakning.GTIN) <> '');
          d_GTIN_num(t) = %Int(pakning.GTIN);
         endif;
         d_GTINType(t) = pakning.GTINType;
         if (pakning.Lagerfort = 'Ja');
          d_lagp_num(t) = 1;
         else;
          d_lagp_num(t) = 0;
         endif;
         if (pakning.Bestillbar = 'Ja');
          d_best_num(t) = 1;
         else;
          d_best_num(t) = 0;
         endif;
         if (%Trim(pakning.FraDato) <> '');
           d_fdat(t) = pakning.FraDato;
         endif;
         if (%Trim(pakning.TilDato) <> '');
           d_tdat(t) = pakning.TilDato;
         endif;
         if (%Trim(pakning.PalleInfo.DetaljistPakPrLag) <> '');
          d_papp_num(t)
             = %Int(pakning.PalleInfo.DetaljistPakPrLag);
         endif;
         if (%Trim(pakning.PalleInfo.MaksStablevekt) <> '');
          d_pmsb_num(t)
             = %Dech(pakning.PalleInfo.MaksStablevekt:14:6);
         endif;

         if (%Trim(pakning.Status.Created) <> '');
           d_noda(t) = %subst(pakning.Status.Created:1:10);
         endif;

         if (%Trim(pakning.Status.Updated) <> '');
           d_neda(t) = %subst(pakning.Status.Updated:1:10);
         endif;

         for t1
             = 1 to pakning.countPakningsMal;
           typen = pakning.PakningsMal(t1).Type;
           pakningsMal = pakning.PakningsMal(t1);

           Select;
           When typen = 'Netto';
            d_BredN_num(t) = PakMal(pakningsMal.Bredde:c_bred_max:1000:c_10_4);
            d_LengN_num(t) = PakMal(pakningsMal.Lengde:c_leng_max:1000:c_10_4);
            d_HoyN_num(t) = PakMal(pakningsMal.Hoyde:c_hoyd_max:1000:c_10_4);
            d_VektN_num(t) = PakMal(pakningsMal.Vekt:c_vekt_max:1000:c_13_4);
            d_VolN_num(t) = PakMal(pakningsMal.Volum:c_volu_max:1:c_10_5);

           When typen = 'Brutto';

            d_BredB_num(t) = PakMal(pakningsMal.Bredde:c_bred_max:1000:c_10_4);
            d_LengB_num(t) = PakMal(pakningsMal.Lengde:c_leng_max:1000:c_10_4);
            d_HoyB_num(t) = PakMal(pakningsMal.Hoyde:c_hoyd_max:1000:c_10_4);
            d_VektB_num(t) = PakMal(pakningsMal.Vekt:c_vekt_max:1000:c_13_4);
            d_VolB_num(t) = PakMal(pakningsMal.Volum:c_volu_max:1:c_10_5);
           EndSL;
         endfor;

       endfor;

6.35   //d_udat_iso = d_UtgarD_iso;  // leverandør utgår dato
6.35   d_udat_iso = *loval;

       CallP P_BehPakn (d_prec : d_blank : d_levLest : d_job_log : b_inlr );


       return 0;

      /end-free

     P BehPakr         E

      *
      * Behandler Pakninger
     P SjekkTall       B
     D SjekkTall       pi            10i 0
     D   tall                        20a   const
     D   hva                          3a   const
     D

      /free

         w_kontroll = %Dech(tall:20:6);

       Select;
       When hva = 'LEN';
         d_vtype = 'ST';
       When hva = 'VEK';
         d_vtype = 'SP';
       When hva = 'VOL';
         if (w_kontroll > 9999);
         return 1;
         endif;
       endsl;

       return 0;

      /end-free
     P SjekkTall       E

      * Behandler Pakningsmal, Lengde, Bredde
     P PakMal          B
     D PakMal          pi            16  6
     D   tall                        15a   const
     D   max                          9  0 const
     D   mulitip                      6  0 const
     D   hva                          4a   const
     D
     D   desimal       s             20  6
      /free

         desimal = *zero;

            if (%Trim(tall) <> '') ;

             desimal = %Dech(tall:20:6);
             if ((desimal * mulitip) > max);
               desimal = *zero;
               return desimal;
             endif;

             select;
             when hva = c_10_4;
               desimal = %Dech(tall:10:4);
             when hva = c_13_4;
               desimal = %Dech(tall:13:4);
             when hva = c_10_5;
               desimal = %Dech(tall:10:5);
             endsl;

             eval(h) desimal = desimal * mulitip;
            endif;

       return desimal;

      /end-free
     P PakMal          E

      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av vareinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     P VareInit        B
      /free
       d_vrec = *blank;

       d_stat_num = *zero;
6.30   d_vare_num = *zero;
       d_nobb_num = *zero;
       d_sort = *blank;
       d_sldo_num = *zero;
       d_kate_num = *zero;
       d_bran = *blank;
       d_utko = *blank;
       d_opps = *blank;
       d_mers = *blank;
       d_kaom = *blank;
       d_enob_num = *zero;
       d_modn_num = *zero;
       d_leve_num = *zero;
       d_crea_iso = *loval;
       d_upda_iso = *loval;
       d_dele_iso = *loval;
       d_utgd_iso = *loval;

       d_vtype = *blank;
       d_vtx1  = *blank;
       d_vtx2 = *blank;
       d_vtxlang = *blank;
       d_pvar = *blank;
       d_uarb = *blank;
       d_uarb_num = *zero;
       d_lfor = *blank;
       d_lfor_num = *zero;
       d_hdat = *blank;
       d_hdat_num = *zero;
       d_tfro = *blank;
       d_tfro_num = *zero;
       d_hpse = *blank;
       d_hpse_num = *zero;
       d_senh = *blank;
       d_lvar = *blank;
       d_besk = *blank;
       d_gsts = *zero;

      /end-free
     P VareInit        E
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av Består av
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     P BestarAvInit    B
     D t               s              5i 0
      /free

       for t = 1 to c_ant_bavv;
         d_brec(t) = *blank;
         d_uvar_num(t) = *zero;
6.31     d_ubnob_num(t) = *zero;
         d_ugti_num(t) = *zero;
         d_uenh(t)     = *blank;
         d_uant_num(t) = *zero;
       endfor;

      /end-free
     P BestarAvInit    E

      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av Kategori
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     P KategoriInit    B
     D t               s              5i 0
      /free

       d_krec = *blanks;
       d_katoLU = *blanks;

       for t = 1 to 20;
         d_katc_num(t) = *zero;
         d_kata_num(t) = *zero;
         d_kato(t)     = *blank;
       endfor;

      /end-free
     P KategoriInit    E

      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av TUNKoblinger / FINFOKoblinger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     P TUN_FINFOKoblingerInit...
     P                 B
     D t               s              5i 0
      /free

       for t = 1 to c_ant_togf;
         d_tfrec(t)    = *blank;

         d_bnob_num(t) = *zero;
         d_btyp(t)     = *blank;
         d_bvar(t)     = *blank;
         d_btxt(t)     = *blank;
       endfor;

      /end-free
     P TUN_FINFOKoblingerInit...
     P                 E

      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av leverandørinfo
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     P LevInit         B
      /free
       d_lrec = *blank;

       d_deltakerNummer = *blank;
       d_navn = *blank;
       d_erVareeier = *blank;
       d_vareNummer  = *blank;
       d_merking1 = *blank;
       d_merking2 = *blank;
6.34   d_merking3 = *blank;
6.34   d_UtgarD_iso = *loval;

      /end-free
     P LevInit         E

      *
6.34  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.34  * Subrutine for initiering av Leverandør Media
6.34  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.34  *
6.34 P LeveMediaInit   B
6.34 D t               s              5i 0
6.34  /free
6.34
6.34   for t = 1 to c_ant_leme;
6.34     d_mediaType(t) = *blank;
6.34     d_mediaUrl(t) = *blank;
6.34   endfor;
6.34
6.34  /end-free
6.34 P LeveMediaInit   E

      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av pakning
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     P PakInit         B
     D
     D t               s              5i 0
      /free
       d_prec = *blank;

       for t = 1 to c_ant_pak;

       d_pakn(t) = *blank;
       d_pkla1(t) = *blank;
       d_pkla3(t) = *blank;
       d_pkla3(t) = *blank;
       d_enhe(t) = *blank;
       d_erpeh_num(t) = *zero;
       d_pse_num(t) = *zero;
       d_ofak_num(t) = *zero;
       d_betAnt_num(t) = *zero;
       d_bestarAvEht(t) = *blank;
       d_GTIN_num(t) = *zero;
       d_GTINType(t) = *blank;
       d_lagp_num(t) = *zero;
       d_best_num(t) = *zero;
       d_fraD_iso(t) = *loval;
       d_tilD_iso(t) = *loval;
       d_nlen(t) = *blank;
       d_nlen(t) = *blank;
       d_nhoy(t) = *blank;
       d_nvek(t) = *blank;
       d_nvol(t) = *blank;
       d_bred(t) = *blank;
       d_leng(t) = *blank;
       d_hoyd(t) = *blank;
       d_vekt(t) = *blank;
       d_volu(t) = *blank;
       d_BredN_num(t) = *zero;
       d_BredN_num(t) = *zero;
       d_LengN_num(t) = *zero;
       d_HoyN_num(t) = *zero;
       d_VektN_num(t)  = *zero;
       d_VolN_num(t)  = *zero;
       d_BredB_num(t) = *zero;
       d_LengB_num(t) = *zero;
       d_HoyB_num(t) = *zero;
       d_VektB_num(t) = *zero;
       d_VolB_num(t) = *zero;
6.30   d_vare_numP(t) = *zero;
       d_del_numP(t) = *zero;
       d_prehP(t) = *blank;
       d_papp_num(t) = *zero;
       d_pmsb_num(t) = *zero;
       d_creD_iso(t) = *loval;
       d_updD_iso(t) = *loval;
       d_udat_iso(t) = *loval;

       endfor;

      /end-free
     P PakInit         E

      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av Avgiftskode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     P AvgiftInit      B
     D t               s              5i 0
      /free

       for t = 1 to c_ant_avgift;
         d_arec(t) = *blank;
         d_anob_num(t) = *zero;
         d_akod(t) = *blank;
         d_atxt(t) = *blank;

       endfor;

      /end-free
     P AvgiftInit      E

      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av NRF
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     P NrfInit         B
     D t               s              5i 0
      /free

         d_nrec = *blanks;
         d_nnob_num = *zero;
         d_nnrf_num = *zero;
         d_ngrp = *blank;
         d_nldo = *blank;
         d_ntx1 = *blank;
         d_ntx2 = *blank;
         d_ntx3 = *blank;

      /end-free
     P NrfInit         E

      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av Farlig gods
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     P FarligInit      B
      /free

       d_unnr = *blank;
       d_fanr = *blank;
       d_fabs = *blank;
       d_fgru = *blank;
       d_embm1 = *blank;
       d_embm2 = *blank;
       d_embm3 = *blank;

      /end-free
     P FarligInit      E

      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av Tollkoder
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     P TollkoderInit   B
      /free

       d_tono = *blank;
       d_toeu = *blank;

      /end-free
     P TollkoderInit   E


**
Antall nye varer er:
Antall nye vare detaljer er:
Antall nye pakninger er:
Antall nye pakning historikk er:
Antall oppdaterte varer er:
Antall oppdaterte pakninger er:
Antall utgåtte pakninger er:
Antall utgåtte priser:
Antall nye vare leverandør:
Antall oppdatere vare leverandør:
Antall utgåtte vare leverandør:
