     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : MO701R
      * Beskrivelse . . : Overvåking - danner grunndata for type 2 & 3
      *                  -Salg av skaffevare uten tilknyttet bestilling
      *                  -Ordre uten leveringsdato
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       141206 bø   Nytt program
      *       110607 bø   Rettelse vedr. utlegging av logg
5.70  *       151208 bø   Varetype fra lager
6.21  * 6.20  190511 bø   Utvidet vl710r med div. info
6.nu  * 6.30  230614 bof  sjekket mtp. nummerutvidelse
6.31  * 6.30  030118 bof  Gen. lwo1pf,lwo2pf,lwo3pf,lwo4pf,lwo7pf (num=8)
6.32  * 6.30  170918 bkv  Utvidet med trelast sortiment
8.01  *PRG2   220622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsstal1    if   e           k disk    rename(sstapfr:sstal1r)
     ffoheld    if   e           k disk    rename(fohepfr:foheldr)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvotylr    if   e           k disk    rename(votypfr:votylrr)
     fra09l1    if   e           k disk    rename(ra09pfr:ra09l1r)
     flwo2lu    uf a e           k disk    rename(lwo2pfr:lwo2lur)
     flwo3lu    uf a e           k disk    rename(lwo3pfr:lwo3lur)
6.32 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
      *
     d sstal1_paar     s                   like(sfpaar)
     d foheld_bdat     s                   like(fobdat)
     d fodtl1_numm     s                   like(fdnumm)
     d fodtl1_suff     s                   like(fdsuff)
     d vvarl1_vare     s                   like(vvvare)
     d votylr_otyp     s                   like(vaotyp)
     d ra09l1_ikod     s                   like(raikod)
     d lwo2lu_odat     s                   like(mwodat)
     d lwo2lu_osys     s                   like(mwosys)
     d lwo2lu_oonr     s                   like(mwoonr)
     d lwo2lu_osuf     s                   like(mwosuf)
     d lwo2lu_olin     s                   like(mwolin)
     d lwo2lu_ovnr     s                   like(mwovnr)
     d lwo2lx_odat     s                   like(mwodat)
     d lwo3lu_odat     s                   like(mwodat)
     d lwo3lu_osys     s                   like(mwosys)
     d lwo3lu_oonr     s                   like(mwoonr)
     d lwo3lu_osuf     s                   like(mwosuf)
     d lwo3lu_olin     s                   like(mwolin)
     d lwo3lu_ovnr     s                   like(mwovnr)
     d lwo3lx_odat     s                   like(mwodat)
      *
     d w_firm          s                   like(vvfirm)
     d w_vare          s                   like(vvvare)
     d w_dato          s                   like(vvedat)
     d w_aar           s              4  0
     d p_dato          s                   like(vvedat)
     d w_usr           s                   like(fouser)
     d w_oda           s                   like(fodate)
     d w_tim           s                   like(fotime)
     d w_avd           s                   like(foavde)
     d w_slg           s                   like(mwoslg)
     d w_sln           s                   like(mwosln)
     d w_net           s                   like(mwonet)
     d w_lih           s                   like(mwolih)
     d w_tek1          s                   like(vvtek1)
     d w_ogrp          s                   like(vvogrp)
     d w_hgrp          s                   like(vvhgrp)
     d w_ugrp          s                   like(vvugrp)
     d w_vtyp          s                   like(vvvtyp)
6.21 d w_udat          s                   like(vvudat)
6.21 d w_ldor          s                   like(vvldor)
6.32 d w_lv_nobb       s                   like(vvlnob)
6.32 d w_lv_modn       s                   like(vvlmod)
6.32 d w_lv_lldo       s                   like(vvlnle)
6.32 d w_lv_llva       s                   like(vvllva)
      *
6.21 d b_inlr          s              1    inz(*off)
     d b_endr          s              1    inz(*off)
     d b_ldhe          s              1    inz(*off)
     d b_lddt          s              1    inz(*off)
      *
      * Local Data Area
      *
     d                uds
      *
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
      *
      * Definering av variable
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * sletter først for dato som det skal kjøres for.
      *
     c                   eval      lwo2lx_odat = w_dato
readec     lwo2lx_key    setll     lwo2lu
readec     lwo2lx_key    reade     lwo2lu
readec                   dow       not %eof(lwo2lu)
     c                   delete    lwo2lur
readec     lwo2lx_key    reade     lwo2lu
     c                   enddo
     c                   eval      lwo3lx_odat = w_dato
readec     lwo3lx_key    setll     lwo3lu
readec     lwo3lx_key    reade     lwo3lu
readec                   dow       not %eof(lwo3lu)
     c                   delete    lwo3lur
readec     lwo3lx_key    reade     lwo3lu
     c                   enddo
      *
     c                   eval      w_aar = %subdt(w_dato:*years)
     c                   exsr      les_sstat
      *
     c                   exsr      les_ordre
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     avslutt       tag
     c                   eval      *inlr = *on
      *
     c                   return
      *
      * --------------------------------------------------------------
      *  Leser salgstatistikk - overvåking 2 - salg av skaffevarer
      * --------------------------------------------------------------
      *
     c     les_sstat     begsr
      *
     c                   eval      sstal1_paar = w_aar
readec     sstal1_key    setll     sstal1
readec     sstal1_key    reade     sstal1
readec                   dow       not %eof(sstal1)
     c                   if        sforda = w_dato
      *
     c                   if        sfvtyp = 'S' and
     c                             sfbenr = 0
     c                   eval      lwo2lu_odat = sforda
     c                   eval      lwo2lu_osys = 'S'
     c                   eval      lwo2lu_oonr = sfornr
     c                   eval      lwo2lu_osuf = sforsu
     c                   eval      lwo2lu_olin = sforli
     c                   eval      lwo2lu_ovnr = sfvare
     c                   eval      w_slg       = sfselg
     c                   eval      w_avd       = sfsavd
     c                   eval      w_usr       = sfuser
     c                   eval      w_oda       = sfdate
     c                   eval      w_tim       = sftime
     c                   eval      w_net       = sfsumb -
     c                                           sfrab1 -
     c                                           sfrab2 -
     c                                           sfbrk2 -
     c                                           sfrabo
     c                   exsr      les_selg
     c                   exsr      skriv_logg
     c                   endif
     c                   endif
      *
readec     sstal1_key    reade     sstal1
readec                   enddo
      *
     c                   endsr
      * --------------------------------------------------------------
      *  Leser ordre - overvåking 2 og 3
      * --------------------------------------------------------------
      *
     c     les_ordre     begsr
      *
     c                   eval      foheld_bdat = w_dato
readec     foheld_key    setll     foheld
readec     foheld_key    reade     foheld
readec                   dow       not %eof(foheld)
      * sjekk lev.dato på heading
      * det gjelder ikke behandlingskode 0
     c                   eval      votylr_otyp = footyp
     c     votylr_key    chain     votylr
     c                   if        %found(votylr) and
     c                             vaoakk <> 0
      * ikke tilbud
     c                   if        foldat = *loval
     c                   eval      b_ldhe = *off
     c                   else
     c                   eval      b_ldhe = *on
     c                   endif
     c
     c                   eval      b_lddt = *off
      *
     c                   eval      fodtl1_numm = fonumm
     c                   eval      fodtl1_suff = fosuff
readec     fodtl1_key    setll     fodtl1
readec     fodtl1_key    reade     fodtl1
readec                   dow       not %eof(fodtl1)
     c
      * henter varetype
      *
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    fdvare
5.70 c                   parm                    fdlage
5.70 c                   parm                    w_vtyp
6.21 c                   parm                    w_udat
6.21 c                   parm                    w_ldor
6.21 c                   parm                    b_inlr
6.32 c                   parm                    w_lv_nobb
6.32 c                   parm                    w_lv_modn
6.32 c                   parm                    w_lv_lldo
6.32 c                   parm                    w_lv_llva
      * rapport 2
     c                   eval      vvarl1_vare = fdvare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found(vvarl1) and
     c                             w_vtyp = 'S'   and
     c                             fdbenr = 0
     c                   eval      lwo2lu_odat = fobdat
     c                   eval      lwo2lu_osys = 'O'
     c                   eval      lwo2lu_oonr = fdnumm
     c                   eval      lwo2lu_osuf = fdsuff
     c                   eval      lwo2lu_olin = fdline
     c                   eval      lwo2lu_ovnr = fdvare
     c                   eval      w_avd       = foavde
     c                   eval      w_slg       = foselg
     c                   eval      w_usr       = fouser
     c                   eval      w_oda       = fodate
     c                   eval      w_net       = fdsums -
     c                                           fdsumr
     c                   exsr      les_selg
     c                   exsr      skriv_logg
     c                   endif
      * rapport 3
     c                   if        fdldat = *loval  and
     c                             foldat = *loval  and
     c                             fdltyp <> 'TX'
      * lev.dato heading = 0 og detaljer = 0
     c                   eval      lwo3lu_odat = fobdat
     c                   eval      lwo3lu_osys = 'O'
     c                   eval      lwo3lu_oonr = fonumm
     c                   eval      lwo3lu_osuf = fosuff
     c                   eval      lwo3lu_olin = fdline
     c                   eval      lwo3lu_ovnr = fdvare
     c                   eval      w_usr       = fouser
     c                   eval      w_avd       = foavde
     c                   eval      w_slg       = foselg
     c                   eval      w_oda       = fodate
     c                   eval      w_tim       = fotime
     c                   eval      w_lih       = 'D'
     c                   eval      w_tek1      = vvtek1
     c                   eval      w_ogrp      = vvogrp
     c                   eval      w_hgrp      = vvhgrp
     c                   eval      w_ugrp      = vvugrp
     c                   exsr      les_selg
     c                   exsr      skriv_logg3
     c                   endif
     c                   if        fdldat <> *loval
     c                   eval      b_lddt = *on
     c                   endif
readec     fodtl1_key    reade     fodtl1
readec                   enddo
      *
     c                   if        b_ldhe = *off  and
     c                             b_lddt = *off
     c                   eval      lwo3lu_odat = fobdat
     c                   eval      lwo3lu_osys = 'O'
     c                   eval      lwo3lu_oonr = fonumm
     c                   eval      lwo3lu_osuf = fosuff
     c                   eval      lwo3lu_olin = 0
     c                   eval      lwo3lu_ovnr = *blank
     c                   eval      w_usr       = fouser
     c                   eval      w_avd       = foavde
     c                   eval      w_slg       = foselg
     c                   eval      w_oda       = fodate
     c                   eval      w_tim       = fotime
     c                   eval      w_lih       = 'H'
     c                   eval      w_tek1      = *blank
     c                   eval      w_ogrp      = 0
     c                   eval      w_hgrp      = 0
     c                   eval      w_ugrp      = 0
     c                   exsr      les_selg
     c                   exsr      skriv_logg3
     c                   endif
      *
      * ikke tilbud
     c                   endif
      *
readec     foheld_key    reade     foheld
readec                   enddo
      *
     c                   endsr
      *
      * --------------------------------------------------------------
      *  Skriver logg ved feil-situasjon
      * --------------------------------------------------------------
      *
     c     skriv_logg    begsr
      *
     c                   clear                   lwo2lur
     c     lwo2lu_key    chain     lwo2lu
     c                   eval      mwoavd = w_avd
     c                   eval      mwoslg = w_slg
     c                   eval      mwosln = w_sln
     c                   eval      mwonet = w_net
     c                   eval      mwousr = w_usr
     c                   eval      mwooda = w_oda
     c                   eval      mwotim = w_tim
     c                   eval      mwovtx = vvtek1
     c                   eval      mwoogr = vvogrp
     c                   eval      mwohgr = vvhgrp
     c                   eval      mwougr = vvugrp
     c                   if        %found(lwo2lu)
     c                   update    lwo2lur
     c                   else
     c                   eval      mwofir = w_firm
     c                   eval      mwodat = lwo2lu_odat
     c                   eval      mwosys = lwo2lu_osys
     c                   eval      mwoonr = lwo2lu_oonr
     c                   eval      mwosuf = lwo2lu_osuf
     c                   eval      mwovnr = lwo2lu_ovnr
     c                   eval      mwolin = lwo2lu_olin
     c                   write     lwo2lur
     c                   endif
      *
     c                   endsr
      *
      *
      * --------------------------------------------------------------
      *  Skriver logg ved feil-situasjon type 3
      * --------------------------------------------------------------
      *
     c     skriv_logg3   begsr
      *
     c                   clear                   lwo3lur
     c     lwo3lu_key    chain     lwo3lu
     c                   eval      mwoavd = w_avd
     c                   eval      mwoslg = w_slg
     c                   eval      mwosln = w_sln
     c                   eval      mwousr = w_usr
     c                   eval      mwooda = w_oda
     c                   eval      mwotim = w_tim
     c                   eval      mwolih = w_lih
     c                   eval      mwovtx = w_tek1
     c                   eval      mwoogr = w_ogrp
     c                   eval      mwohgr = w_hgrp
     c                   eval      mwougr = w_ugrp
     c                   if        %found(lwo3lu)
     c                   update    lwo3lur
     c                   else
     c                   eval      mwofir = w_firm
     c                   eval      mwodat = lwo3lu_odat
     c                   eval      mwosys = lwo3lu_osys
     c                   eval      mwoonr = lwo3lu_oonr
     c                   eval      mwosuf = lwo3lu_osuf
     c                   eval      mwovnr = lwo3lu_ovnr
     c                   eval      mwolin = lwo3lu_olin
     c                   write     lwo3lur
     c                   endif
      *
     c                   endsr
      *
      * --------------------------------------------------------------
      *  leser selger
      * --------------------------------------------------------------
      *
     c     les_selg      begsr
      *
     c                   eval      ra09l1_ikod = w_slg
     c     ra09l1_key    chain     ra09l1r
     c                   if        %found(ra09l1)
     c                   eval      w_sln = raitxt
     c                   else
     c                   eval      w_sln = *blank
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for les av salgsstatistikk
      *
     c     sstal1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sstal1_paar
      *
      *  Key for les av vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *  Key for les av ordretype
      *
     c     votylr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votylr_otyp
      *
      *  Key for les av ordre
      *
     c     foheld_key    klist
     c                   kfld                    w_firm
     c                   kfld                    foheld_bdat
      *
      *  Key for les av ordre
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
      *
      * Key for oppdatering av overvåking
      *
     c     lwo2lu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lwo2lu_odat
     c                   kfld                    lwo2lu_osys
     c                   kfld                    lwo2lu_oonr
     c                   kfld                    lwo2lu_osuf
     c                   kfld                    lwo2lu_olin
     c                   kfld                    lwo2lu_ovnr
      *
      * Key for oppdatering av overvåking  3
      *
     c     lwo3lu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lwo3lu_odat
     c                   kfld                    lwo3lu_osys
     c                   kfld                    lwo3lu_oonr
     c                   kfld                    lwo3lu_osuf
     c                   kfld                    lwo3lu_olin
     c                   kfld                    lwo3lu_ovnr
      *
      * Key for slett av overvåking på den dato man vil kjøre på.
      *
     c     lwo2lx_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lwo2lx_odat
      *
      * Key for slett av overvåking på den dato man vil kjøre på.
      *
     c     lwo3lx_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lwo3lx_odat
      *
      * les av selger
      *
     c     ra09l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra09l1_ikod
      *
     c     *entry        plist
     c                   parm                    p_dato
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_dato = p_dato
      *
     c                   endsr
