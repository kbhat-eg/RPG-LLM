      /TITLE  Tilrettelegger trans fra scanning
     h decedit(',') datedit(*dmy.)
     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * System. . . . . : ASOKON
      * Program . . . . : RFP15R
      * Beskrivelse . . : Tilrettelegger trans fra scanning Nexstep Arkiv
      * Div. Dokumentasjon:
      *      Bilagskode, Settes i properties-file på server
      *                  (Kan bli borte ved innl. av ny versjon)(Espen/Arild)
      *                  RFP15R transporterer denne ukritisk videre.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * V5.41 161005 KOB  Nytt program basert på RFA15R
      * V5.50 240607 KOB  Lagt ut reksontronummer og lev.fakturanr
      * V5.50 240607 KOB  Leser logisk fil i perioderekkefølge
      * V5.50 227087 KOB  Endret logikk knyttet til beløp/val.beløp
1.00  * V5.50 100210 Sst  Ev. flytte postering til 1. periode ikke sperret.
6.10  * 6.10  121010 NHO  Leser kun for inneværende firma
6.11  *       011110 Sst  Rettet logikk vedr firmanr/periode i bunt
6.12  *       270611 sst  Konv. av bilagskoder i.h.h.t compregler
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.10 fremsl1    uf   e           k disk    rename(remspfr:remsl1r)
     f                                     prefix(x:1)
      *
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     fraa3pf    if   e           k disk
1.0  fraa1pf    if   e           k disk
     frbunlu    uf a e           k disk    rename(rbunpfr:rbunlur)
     frtrapf    o    e           k disk
      *
     d                uds
1.00 d l_perfradat           100    100
1.00 d l_perettspr           101    101
1.00 d l_v_f_per             110    110
6.12 d l_blk                 111    126
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av array's
      *
     d per             s              2  0 dim(13)                              Perioder
      *
      * Definering av variable i nøkler
      *
     d rbunlu_bunt     s                   like(rsbunt)
      *
      * Definering av variable
      *
610  d rbunlu_nivo     s                   like(rsnivo)
610  d rbunlu_memb     s                   like(rsmemb)
610  d w_memb          s                   like(rsmemb)
     d w_firm          s                   like(rtfirm)
     d w_bunt          s                   like(rtbunt)
     d w_xper          s              8  0
     d w_linj          s              8  0
     d w_arec          s              9  0
     d w_Årm1          s              2  0
     d wpe             s              2  0
     d w_rper          s                   like(rsrper)
     d w_raar          s                   like(rsraar)
1.00 d w_sp_aar        s                   like(rsraar)
1.00 d w_sp_mnd        s                   like(rsrper)
1.00 d w_sp_per        s              6  0
1.00 d w_p_f_comp      s              1
1.00 d w_kv_bilnr      s                   like(xtbiln)
1.00 d w_m1            s             50    inz('Bilagsnr 123456 er postert på -
1.00 d                                     en avsluttet periode')
1.00 d w_m2            s             50    inz('Skal periode 2010-01 posteres -
1.00 d                                     på?                 ')
1.00 d w_sv            s              1
1.00 d w_in12          s              1  0
1.00 d w_bnr_x         s              6
1.00 d w_aar_x         s              4
1.00 d w_per_x         s              2
      *
     d w_bsum          s                   like(rsbsum)
     d w_psum          s                   like(rspsum)
     d w_pdeb          s                   like(rspdeb)
     d w_pkre          s                   like(rspkre)
     d w_belp          s                   like(rtbelØ)
      *
     d w_peri          s              6  0
1.00 d w_amd           s              6  0
1.00 d w_20            s              2  0
1.00 d w_40            s              4  0
      *
6.12 d w_bilk          s              2
      *
      * Definering av save-variable
      *
     d s_firm          s                   like(rtfirm)
     d s_peri          s              6  0
      *
      *  Statusopplysninger - avvikende perioder
      *
     iraa3pfr
     i              ra3p01                      per(01)
     i              ra3p02                      per(02)
     i              ra3p03                      per(03)
     i              ra3p04                      per(04)
     i              ra3p05                      per(05)
     i              ra3p06                      per(06)
     i              ra3p07                      per(07)
     i              ra3p08                      per(08)
     i              ra3p09                      per(09)
     i              ra3p10                      per(10)
     i              ra3p11                      per(11)
     i              ra3p12                      per(12)
     i              ra3p13                      per(13)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedrutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      * Les transaksjonsfile til e.o.f.
      *
     c                   read      remsl1                                 90
      *
     c                   dow       *in90 = *off
6.10  *
6.10 c                   if        xtfirm <> l_firm
6.10 c                   goto      nyles
6.10 c                   endif
6.10  *
      *
     c                   eval      w_firm = xtfirm
1.00 c                   eval      w_p_f_comp = 'J'
1.00  *   Dette må gjøres uavhengig av l_perfradat hvis periode = 0
1.00 c                   if        xtrper = *zero
1.00 c                   eval      w_p_f_comp = 'N'
1.00 c     *ymd          move      xtbdat        w_amd
1.00 c                   eval      w_40   = w_amd/100
1.00 c                   eval      w_20   = w_amd/10000
1.00 c                   eval      xtrper = w_40 - w_20 * 100
1.00 c                   eval      xtraar = w_20 + 2000
1.00 c                   endif
      *
     c                   eval      w_raar = xtraar
     c                   eval      w_rper = xtrper
      *
     c                   movel     xtraar        w_peri
     c                   move      xtrper        w_peri
1.00  *  Ev. Sett periode til første periode etter sperret periode.
1.00 c                   if        l_perettspr = 'J'   and
1.00 c                             xtrper <> 13        and
1.00 c                             w_peri <= w_sp_per  and
1.00 c                             w_p_f_comp = 'N'
1.00 c                   eval      xtrper = w_sp_mnd + 1
1.00 c                   if        xtrper > 12
1.00 c                   eval      xtrper = 1
1.00 c                   eval      xtraar = xtraar + 1
1.00 c                   endif
1.00 c                   eval      w_raar = xtraar
1.00 c                   eval      w_rper = xtrper
1.00 c                   movel     xtraar        w_peri
1.00 c                   move      xtrper        w_peri
1.00 c                   endif
1.00  *  Gi melding dersom det fra Compello er satt inn en sperret periode
1.00 c                   if        w_p_f_comp  = 'J'   and
1.00 c                             w_peri <= w_sp_per
1.00  *    Ikke dersom samme bilagsnr er kvittert før
1.00 c                   if        xtbiln <> w_kv_bilnr
1.00 c                   eval      w_kv_bilnr =  xtbiln
1.00 c                   move      xtbiln        w_bnr_x
1.00 c                   move      xtraar        w_aar_x
1.00 c                   move      xtrper        w_per_x
1.00 c                   eval      %subst(w_m1:10:6) = w_bnr_x
1.00 c                   eval      %subst(w_m2:14:4) = w_aar_x
1.00 c                   eval      %subst(w_m2:19:2) = w_per_x
1.00 c                   eval      w_sv = *blank
1.00 c                   call      'AA007R'
1.00 c                   parm                    w_m1
1.00 c                   parm                    w_m2
1.00 c                   parm                    w_sv
1.00 c                   parm                    w_in12
1.00 c                   endif
1.00  *  Dersom sperret periode ikke godkjennes settes neste gyldige periode
1.00 c                   if        w_sv <> 'J'
1.00 c                   eval      xtrper = w_sp_mnd + 1
1.00 c                   if        xtrper > 12
1.00 c                   eval      xtrper = 1
1.00 c                   eval      xtraar = xtraar + 1
1.00 c                   endif
1.00 c                   eval      w_raar = xtraar
1.00 c                   eval      w_rper = xtrper
1.00 c                   movel     xtraar        w_peri
1.00 c                   move      xtrper        w_peri
1.00 c                   else
1.00 c                   eval      l_v_f_per = 'J'
1.00 c                   endif
1.00 c                   endif
      *
      * Dersom brudd,
      * oppdater RBUNPF
      *
     c                   if        w_firm <> s_firm or
1.00 c                             w_peri <> s_peri or
1.0  c                             w_peri = 0
     c                   if        w_arec <> 0
     c                   exsr      opdat_rbun
     c                   endif
     c                   endif
      *
      * Nytt firma ?
      *
     c                   if        w_firm <> s_firm  or
     c                             w_arec =  0
     c                   exsr      nytt_firma
     c                   endif
      *
      * Ny periode ?
      *
     c                   if        w_firm <> s_firm or
     c                             w_peri <> s_peri
     c                   exsr      ny_periode
     c                   endif
      *
      * Ta vare på gamle verdier
      *
     c                   eval      s_firm = w_firm
     c                   eval      s_peri = w_peri
      *
      * Redigerer regnskapsperiode og år
      *
     c                   exsr      opdat_rtra
     c                   eval      w_arec = w_arec + 1
6.10  *
6.10 c                   delete    remsl1r
      *
6.10  *
6.10 c     nyles         tag
6.10  *
     c                   read      remsl1                                 90
      *
     c                   enddo
      *
      * Siste buntsum,
      * oppdater RBUNPF
      *
     c                   if        w_arec <> 0
     c                   exsr      opdat_rbun
     c                   endif
      *
      * Avslutt program
      *
     c                   eval      *inlr = *on
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for brudd ved nytt firmanr.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     nytt_firma    begsr
      *
      * Hent inn firmaregister
      *
     c     afirl1_key    chain     afirl1                             91
      *
      * Hent inn statuskoder (avvikende perioder)
      *
     c     raa3pf_key    chain     raa3pf                             91         Avvikende perioder
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for brudd periode / år
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ny_periode    begsr
      *
      * Brudd periode/år
      * Finn neste ledige buntnr
      *
610  c                   eval      rbunlu_nivo = *blank
610  c                   eval      rbunlu_memb = w_memb
     c                   eval      rbunlu_bunt = 99999
     c     rbunlu_key    setgt     rbunlu
     c                   readp     rbunlu                                 91
      *
     c                   if        *in91 = *on
     c                   eval      w_bunt = 1
     c                   else
     c                   eval      w_bunt = rsbunt + 1
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av RTRAPF
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     opdat_rtra    begsr
V5.50 *
     c                   if        xtbelØ < 0
     c                   eval      xtbelØ = xtbelØ * -1                         Snu fortegn
     c                   endif
V5.50 *
     c                   if        xtvalb < 0
     c                   eval      xtvalb = xtvalb * -1                         Snu fortegn
     c                   endif
V5.50 *
V5.50 * Dersom beløp = 0 og valutakode er blank, settes
V5.50 * beløp = valutabeløp og valutabeløp = 0
V5.50 *
     c                   if        xtbelØ = 0 and
     c                             xtvalk = ' '
     c                   eval      xtbelØ = xtvalb                              Snu fortegn
     c                   eval      xtvalb = 0                                   Snu fortegn
     c                   endif
      *
      * Beregn buntsummer
      *
     c                   if        xtdkto <> 0
     c                   eval      w_psum = w_psum + xtbelØ
     c                   eval      w_bsum = w_bsum + xtbelØ
     c                   eval      w_pdeb = w_pdeb + xtbelØ
     c                   endif
      *
     c                   if        xtckto <> 0
     c                   eval      w_psum = w_psum + xtbelØ
     c                   eval      w_pkre = w_pkre + xtbelØ
     c                   endif
      *
     c                   eval      w_linj = w_linj + 1                          Linjenr.
      *
     c                   eval      rtreid = 'T'                                 Rec.-id
6.11 c                   eval      rtfirm = s_firm                              Firma
     c                   eval      rtbunt = w_bunt                              Bunt-nr.
     c                   eval      rtlinj = w_linj                              Linjenr.
     c                   eval      rtstat = *blank                              Buntstatus
     c                   eval      rtraar = xtraar                              Regn.år
     c                   eval      rtrper = xtrper                              Regn.per
     c                   eval      rtbiln = xtbiln                              Bilag-nr.
     c                   eval      rtbill = xtbill                              Bilag-linje
      *
     c                   eval      rtdavd = xtdavd                              Debet-avd.
     c                   eval      rtdkto = xtdkto                              Debet-kto.
     c                   eval      rtdsps = xtdsps                              Debet-spes.
     c                   eval      rtdmva = xtdmva                              Debet-mva.
      *
     c                   eval      rtcavd = xtcavd                              Kredit-avd.
     c                   eval      rtckto = xtckto                              Kredit-kto.
     c                   eval      rtcsps = xtcsps                              Kredit-spes.
     c                   eval      rtcmva = xtcmva                              Kredit-mva.
      *
V5.50c                   eval      rtbelØ = xtbelØ                              Beløp
V5.50c                   eval      rtvalb = xtvalb                              Valutabeløp
V5.50c                   eval      rtvalk = xtvalk                              Valutakode
V5.50 *
     c                   eval      rtgmva = 0                                   Mva-grunnlag
     c                   eval      rtpros = xtpros                              Prosjekt
     c                   eval      rtakti = xtakti                              Aktivitet
     c                   eval      rtrefn = 0                                   Referanse
     c                   eval      rtkrab = 0                                   Rabatt
     c                   eval      rtmngd = 0                                   Mengde
     c                   eval      rtmngk = 0                                   Mengde-kode
     c                   eval      rtresn = xtresn                              Reskontronr.
     c                   eval      rtlfak = xtlfak                              Lev.fakturanr.
     c                   eval      rtvirk = ' '                                 Virksomhet
     c                   eval      rtautx = ' '                                 Avkryssing
     c                   eval      rtatte = ' '                                 Attestert
      *
     c                   eval      rtbdat = xtbdat                              Dilagsdato
     c                   eval      rtfdat = xtfdat                              Forfall
     c                   eval      rtkidi = xtkidi                              KID-referanse
6.12  *
6.12  *  Ev. konv av bilagskoder
6.12  *
6.12 c                   move      xtbilk        w_bilk
6.12 c                   select
6.12 c                   when      %subst(l_blk:01:4) <> *blank and
6.12 c                             %subst(l_blk:01:2) = w_bilk
6.12 c                   eval      w_bilk = %subst(l_blk:03:2)
6.12 c                   move      w_bilk        xtbilk
6.12  *
6.12 c                   when      %subst(l_blk:05:4) <> *blank and
6.12 c                             %subst(l_blk:05:2) = w_bilk
6.12 c                   eval      w_bilk = %subst(l_blk:07:2)
6.12 c                   move      w_bilk        xtbilk
6.12  *
6.12 c                   when      %subst(l_blk:09:4) <> *blank and
6.12 c                             %subst(l_blk:09:2) = w_bilk
6.12 c                   eval      w_bilk = %subst(l_blk:11:2)
6.12 c                   move      w_bilk        xtbilk
6.12  *
6.12 c                   when      %subst(l_blk:13:4) <> *blank and
6.12 c                             %subst(l_blk:13:2) = w_bilk
6.12 c                   eval      w_bilk = %subst(l_blk:15:2)
6.12 c                   move      w_bilk        xtbilk
6.12 c                   other
6.12 c                   endsl
     c                   eval      rtbilk = xtbilk                              Bilagskode
     c                   eval      rttext = xttext                              Bilagstekst
     c                   eval      rtbetb = 0                                   Betalingbet.
     c                   eval      rtbref = ' '                                 Bank-referanse
610  c                   eval      rtnivo = *blank
610  c                   eval      rtmemb = w_memb
610  c                   time                    rtedat
610  c                   time                    rtetim
610  c                   eval      rteusr = l_user
      *
     c                   write     rtrapfr                                      Skriv post
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av RBUNPF
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     opdat_rbun    begsr
      *
     c                   xfoot     per           w_xper                         Tabell finne
     c                   movel     rtrper        wpe                            Per på trans
      *
     c                   if        ra3stp > 01 and                              Start r.år
     c                             w_xper > *zero                                tab.utfyllt
     c                   movel     per(wpe)      rtrper                           bruk p.fra tab.
     c                   if        wpe    < ra3stp                              og fjorår
     c                   move      w_Årm1        rtrper                          bruk fjorår
     c                   endif
     c                   endif
      *
6.11 c                   eval      rsfirm = s_firm                              Firma
     c                   eval      rsbunt = w_bunt                              Bunt-nr.
     c                   eval      rsstat = *blank                              Buntstatus
     c                   eval      rsraar = rtraar                              Periode/År
     c                   eval      rsrper = rtrper                              Periode/Mnd
     c                   eval      rsfØrs = 1                                   Første linje
     c                   eval      rssist = w_linj                              Siste linje
     c                   eval      rsbsum = w_bsum                              Beløp
     c                   eval      rspsum = w_psum                              Beløp
     c                   eval      rspdeb = w_pdeb                              Beløp
     c                   eval      rspkre = w_pkre                              Beløp
     c                   time                    rspdat                         Postert dato
      *
610  c                   eval      rsnivo = *blank
610  c                   eval      rsmemb = w_memb
610  c                   time                    rsedat
610  c                   time                    rsetim
610  c                   eval      rseusr = l_user
     c                   write     rbunlur                                      Skriv post
      *
      * Nullstill buntsum
      *
     c                   eval      w_bsum = 0
     c                   eval      w_psum = 0
     c                   eval      w_pdeb = 0
     c                   eval      w_pkre = 0
     c                   eval      w_arec = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
610  c     *entry        plist
610  c                   parm                    w_memb                         user
      *
     c                   eval      wpe = 0
     c                   eval      w_bsum = 0
     c                   eval      w_Årm1 = uyear - 1
     c                   eval      w_rper = *month
      *
     c                   eval      w_firm = l_firm
     c                   eval      s_firm = l_firm
      *
      * Key for les av firmaregister
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for les av statuskoder 3 (avvikende perioder)
      *
     c     raa3pf_key    klist
     c                   kfld                    w_firm
1.00  *
1.00  * Key for les av statuskoder 1 (Sperret periode)
1.00  *
1.00 c     raa1pf_key    klist
1.00 c                   kfld                    w_firm
      *
      * Key for oppdatering av buntfile
      *
     c     rbunlu_key    klist
610  c                   kfld                    rbunlu_nivo
610  c                   kfld                    rbunlu_memb
     c                   kfld                    w_firm
     c                   kfld                    rbunlu_bunt
1.00  *
1.00  * Les RAA1PF for p finne sists sperret periode
1.00  *
1.00 c     raa1pf_key    chain     raa1pf
1.00 c                   if        %found(raa1pf)
1.00 c                   eval      W_sp_mnd = ra1per
1.00 c                   eval      w_sp_aar = ra1aar
1.00 c                   eval      w_sp_per = w_sp_aar * 100 + w_sp_mnd
1.00 c                   else
1.00 c                   eval      w_sp_per = *zero
1.00 c                   endif
1.00 c                   eval      l_v_f_per = *blank
6.12  *
6.12  * Hent ev konv bilagskoder fra l_blkegler   pos 28(4) * 4 (fra-til)
6.12  *
6.12  *    Bilagskoder måtte hentes i cl da dta-område varierer m/firma
6.12  *                lagt i l_blk  (lda(111-126))
     c                   endsr
