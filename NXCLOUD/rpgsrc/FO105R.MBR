     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO105R
      * Beskrivelse . . : Ordre-hode, vedlikehold av topp- og bunntekst
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * R4.00 230102 AMD  Skrevet om til ny standard
      *       301005 HKO  Skrevet om programmet
      * 6.10  250609 BSA  Oppdatert med sporingsinformasjon
6.NU  * R6.30 230514 bsa  Programmet gjennomgått mtp nummerutvidelser.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffotxl1    if   e           k disk    rename(fotxpfr:fotxl1r)
     ffotxlu    uf a e           k disk    rename(fotxpfr:fotxlur)
     ffftxl1    if   e           k disk    rename(fftxpfr:fftxl1r)
     ffo105d    cf   e             workstn
      *
      * Definering av array
      *
     d a_totx          s             70    dim(16)                              TEKST
     d a_butx          s             70    dim(16)                              TEKST
      *
      * Definering av parametere
      *
     d p_firm          s                   like(ftfirm)
     d p_numm          s                   like(ftnumm)
     d p_suff          s                   like(ftsuff)
      *
      * Definering av local data area
      *
     d                uds
6.10 d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variable i nøkler
      *
     d fotxl1_line     s                   like(ftline)
     d fotxlu_line     s                   like(ftline)
     d fftxl1_numm     s                   like(fjnumm)
      *
      * Definering av variable
      *
     d b_text          s              1    inz(*off)
      *
     d w_tell          s              2  0
      *
     d w_firm          s                   like(ftfirm)
     d w_numm          s                   like(ftnumm)
     d w_suff          s                   like(ftsuff)
      *
     d w_line          s                   like(ftline)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter topptekst og bunntekst fra ordre-tekst
      *
     c                   exsr      hent_tekst
      *
      * Behandler bildet
      *
     c     t1tag         tag
      *
     c                   if        *in80 = *off
     c                   eval      t1type = 'Topptekst:'
     c                   eval      t1bytt = 'F23=Vis bunntekst'
     c                   else
     c                   eval      t1type = 'Bunntekst:'
     c                   eval      t1bytt = 'F23=Vis topptekst'
     c                   endif
      *
      * Henter tekst fra array
      *
     c                   if        *in80 = *off
     c                   eval      t1tx01 = a_totx(01)
     c                   eval      t1tx02 = a_totx(02)
     c                   eval      t1tx03 = a_totx(03)
     c                   eval      t1tx04 = a_totx(04)
     c                   eval      t1tx05 = a_totx(05)
     c                   eval      t1tx06 = a_totx(06)
     c                   eval      t1tx07 = a_totx(07)
     c                   eval      t1tx08 = a_totx(08)
     c                   eval      t1tx09 = a_totx(09)
     c                   eval      t1tx10 = a_totx(10)
     c                   eval      t1tx11 = a_totx(11)
     c                   eval      t1tx12 = a_totx(12)
     c                   eval      t1tx13 = a_totx(13)
     c                   eval      t1tx14 = a_totx(14)
     c                   eval      t1tx15 = a_totx(15)
     c                   eval      t1tx16 = a_totx(16)
     c                   else
     c                   eval      t1tx01 = a_butx(01)
     c                   eval      t1tx02 = a_butx(02)
     c                   eval      t1tx03 = a_butx(03)
     c                   eval      t1tx04 = a_butx(04)
     c                   eval      t1tx05 = a_butx(05)
     c                   eval      t1tx06 = a_butx(06)
     c                   eval      t1tx07 = a_butx(07)
     c                   eval      t1tx08 = a_butx(08)
     c                   eval      t1tx09 = a_butx(09)
     c                   eval      t1tx10 = a_butx(10)
     c                   eval      t1tx11 = a_butx(11)
     c                   eval      t1tx12 = a_butx(12)
     c                   eval      t1tx13 = a_butx(13)
     c                   eval      t1tx14 = a_butx(14)
     c                   eval      t1tx15 = a_butx(15)
     c                   eval      t1tx16 = a_butx(16)
     c                   endif
      *
      * Send alt
      *
     c                   exfmt     t1bld
      *
      * F3=Gå tilbake
      *
     c                   if        *inkc or *inkl
     c                   goto      avslutt
     c                   endif
      *
      * F6=Hent ny tekst
      *
     c                   if        *inkf = *on
     c                   exsr      xt2win
     c                   goto      t1tag
     c                   endif
      *
      * Legger tekst tilbake til array
      *
     c                   if        *in80 = *off
     c                   eval      a_totx(01) = t1tx01
     c                   eval      a_totx(02) = t1tx02
     c                   eval      a_totx(03) = t1tx03
     c                   eval      a_totx(04) = t1tx04
     c                   eval      a_totx(05) = t1tx05
     c                   eval      a_totx(06) = t1tx06
     c                   eval      a_totx(07) = t1tx07
     c                   eval      a_totx(08) = t1tx08
     c                   eval      a_totx(09) = t1tx09
     c                   eval      a_totx(10) = t1tx10
     c                   eval      a_totx(11) = t1tx11
     c                   eval      a_totx(12) = t1tx12
     c                   eval      a_totx(13) = t1tx13
     c                   eval      a_totx(14) = t1tx14
     c                   eval      a_totx(15) = t1tx15
     c                   eval      a_totx(16) = t1tx16
     c                   else
     c                   eval      a_butx(01) = t1tx01
     c                   eval      a_butx(02) = t1tx02
     c                   eval      a_butx(03) = t1tx03
     c                   eval      a_butx(04) = t1tx04
     c                   eval      a_butx(05) = t1tx05
     c                   eval      a_butx(06) = t1tx06
     c                   eval      a_butx(07) = t1tx07
     c                   eval      a_butx(08) = t1tx08
     c                   eval      a_butx(09) = t1tx09
     c                   eval      a_butx(10) = t1tx10
     c                   eval      a_butx(11) = t1tx11
     c                   eval      a_butx(12) = t1tx12
     c                   eval      a_butx(13) = t1tx13
     c                   eval      a_butx(14) = t1tx14
     c                   eval      a_butx(15) = t1tx15
     c                   eval      a_butx(16) = t1tx16
     c                   endif
      *
      * F23=Bytt mellom å vise topptekst og bunntekst
      *
     c                   if        *inkx
     c                   if        *in80 = *off
     c                   eval      *in80 = *on
     c                   else
     c                   eval      *in80 = *off
     c                   endif
     c                   goto      t1tag
     c                   endif
      *
      * Oppdaterer ordre-tekst
      *
     c                   exsr      oppd_tekst
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter inn tekster fra ordre-tekst
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_tekst    begsr
      *
      * Henter inn topptekst og bunntekst fra ordre-tekst
      * Topptekst er linjenr 1000-1160 og bunntekst er linjenr 5000-5160
      * 16 tekstlinjer pr. type
      *
      * Topptekst
      *
     c                   eval      a_totx = *blank
     c                   eval      w_tell = 0
      *
     c                   eval      fotxl1_line = 1000
     c     fotxl1_key    setll     fotxl1
     c     fotxl1_key2   reade     fotxl1
     c                   dow       not %eof and
     c                             ftline < 5000 and
     c                             w_tell < 16
     c
     c                   eval      w_tell = w_tell + 1
     c                   eval      a_totx(w_tell) = fttext
      *
     c     fotxl1_key2   reade     fotxl1
     c                   enddo
      *
      * Bunntekst
      *
     c                   eval      a_butx = *blank
     c                   eval      w_tell = 0
      *
     c                   eval      fotxl1_line = 5000
     c     fotxl1_key    setll     fotxl1
     c     fotxl1_key2   reade     fotxl1
     c                   dow       not %eof and
     c                             w_tell < 16
     c
     c                   eval      w_tell = w_tell + 1
     c                   eval      a_butx(w_tell) = fttext
      *
     c     fotxl1_key2   reade     fotxl1
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandle bilde for å hente inn ny tekst
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xt2win        begsr
      *
      * Blanker felter som benyttes
      *
     c                   eval      t2numm = *blank
      *
      * Send alt
      *
     c     t2taga        tag
      *
     c                   exfmt     t2win
      *
      * F3=Gå tilbake
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * F4=Spørring
      *
     c                   if        *inkd = *on
     c                   call      'FJ510R'
     c                   parm                    w_firm
     c                   parm                    t2numm
     c                   goto      t2taga
     c                   endif
      *
      * Validering
      *
     c                   if        t2numm = *blank
     c                   leavesr
     c                   endif
      *
     c                   eval      fftxl1_numm = t2numm
     c     fftxl1_key    chain     fftxl1
     c                   if        not %found
     c                   eval      *in31 = *on
     c                   goto      t2taga
     c                   endif
      *
      * Henter inn ny tekst
      *
     c                   if        *in80 = *off
     c                   eval      a_totx(01) = fjsl01
     c                   eval      a_totx(02) = fjsl02
     c                   eval      a_totx(03) = fjsl03
     c                   eval      a_totx(04) = fjsl04
     c                   eval      a_totx(05) = fjsl05
     c                   eval      a_totx(06) = fjsl06
     c                   eval      a_totx(07) = fjsl07
     c                   eval      a_totx(08) = fjsl08
     c                   eval      a_totx(09) = fjsl09
     c                   eval      a_totx(10) = fjsl10
     c                   eval      a_totx(11) = fjsl11
     c                   eval      a_totx(12) = fjsl12
     c                   eval      a_totx(13) = fjsl13
     c                   eval      a_totx(14) = fjsl14
     c                   eval      a_totx(15) = fjsl15
     c                   eval      a_totx(16) = fjsl16
     c                   else
     c                   eval      a_butx(01) = fjsl01
     c                   eval      a_butx(02) = fjsl02
     c                   eval      a_butx(03) = fjsl03
     c                   eval      a_butx(04) = fjsl04
     c                   eval      a_butx(05) = fjsl05
     c                   eval      a_butx(06) = fjsl06
     c                   eval      a_butx(07) = fjsl07
     c                   eval      a_butx(08) = fjsl08
     c                   eval      a_butx(09) = fjsl09
     c                   eval      a_butx(10) = fjsl10
     c                   eval      a_butx(11) = fjsl11
     c                   eval      a_butx(12) = fjsl12
     c                   eval      a_butx(13) = fjsl13
     c                   eval      a_butx(14) = fjsl14
     c                   eval      a_butx(15) = fjsl15
     c                   eval      a_butx(16) = fjsl16
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer ordre-tekst med nye tekster
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_tekst    begsr
      *
      * Topptekst
      *
     c                   eval      b_text = *off
     c                   eval      w_tell = 16
     c                   eval      w_line = 1160
      *
     c                   do        16
      *
     c                   if        a_totx(w_tell) <> *blank
     c                   eval      b_text = *on
     c                   endif
      *
     c                   eval      fotxlu_line = w_line
     c     fotxlu_key    chain     fotxlur
      *
     c                   if        b_text = *off
     c                   if        %found(fotxlu)
     c                   delete    fotxlur
     c                   endif
     c                   else
     c                   eval      fttext = a_totx(w_tell)
     c                   if        %found(fotxlu)
6.10 c                   time                    ftedat
6.10 c                   time                    ftetim
6.10 c                   eval      fteusr = l_user
     c                   update    fotxlur
     c                   else
     c                   eval      ftfirm = w_firm
     c                   eval      ftnumm = w_numm
     c                   eval      ftsuff = w_suff
     c                   eval      ftline = w_line
6.10 c                   time                    ftodat
6.10 c                   time                    ftotim
6.10 c                   eval      ftousr = l_user
6.10 c                   time                    ftedat
6.10 c                   time                    ftetim
6.10 c                   eval      fteusr = l_user
     c                   write     fotxlur
     c                   endif
     c                   endif
      *
     c                   eval      w_tell = w_tell - 1
     c                   eval      w_line = w_line - 10
      *
     c                   enddo
      *
      * Bunntekst
      *
     c                   eval      b_text = *off
     c                   eval      w_tell = 16
     c                   eval      w_line = 5160
      *
     c                   do        16
      *
     c                   if        a_butx(w_tell) <> *blank
     c                   eval      b_text = *on
     c                   endif
      *
     c                   eval      fotxlu_line = w_line
     c     fotxlu_key    chain     fotxlur
      *
     c                   if        b_text = *off
     c                   if        %found(fotxlu)
     c                   delete    fotxlur
     c                   endif
     c                   else
     c                   eval      fttext = a_butx(w_tell)
     c                   if        %found(fotxlu)
6.10 c                   time                    ftedat
6.10 c                   time                    ftetim
6.10 c                   eval      fteusr = l_user
     c                   update    fotxlur
     c                   else
     c                   eval      ftfirm = w_firm
     c                   eval      ftnumm = w_numm
     c                   eval      ftsuff = w_suff
     c                   eval      ftline = w_line
6.10 c                   time                    ftodat
6.10 c                   time                    ftotim
6.10 c                   eval      ftousr = l_user
6.10 c                   time                    ftedat
6.10 c                   time                    ftetim
6.10 c                   eval      fteusr = l_user
     c                   write     fotxlur
     c                   endif
     c                   endif
      *
     c                   eval      w_tell = w_tell - 1
     c                   eval      w_line = w_line - 10
      *
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_numm
     c                   parm                    p_suff
      *
      * Key les på ordre-tekst
      *
     c     fotxl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
     c                   kfld                    fotxl1_line
      *
     c     fotxl1_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
      *
      * Key for oppdatering av ordre-tekst
      *
     c     fotxlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
     c                   kfld                    fotxlu_line
      *
      * Key for oppslag på fasttekst
      *
     c     fftxl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fftxl1_numm
      *
      *
      * Henter firma, ordrenr og suffiks fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_numm = p_numm
     c                   eval      w_suff = p_suff
      *
      * Initierer skjermbilde
      *
     c                   eval      t1numm = p_numm
      *
      * Viser først topptekst
      *
     c                   eval      *in80 = *off
      *
     c                   endsr
