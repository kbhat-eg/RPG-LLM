     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : NN831R
      * Beskrivelse . . : Nettbutikk, hent reskontroposter
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       161002 ASK  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frktrl2    if   e           k disk    rename(rktrpfr:rktrl2r)
      *
      * Definering av parametre
      *
     d p_uniq          s             15
     d p_dtqi          s             10
     d p_dtqo          s             10
     d p_bibl          s             10
     d p_feil          s              1
     d p_firm          s              3  0
     d p_lage          s              2
     d p_inpl          s              5p 0
     d p_outl          s              5p 0
     d p_wait          s              5p 0
     d p_keyo          s              2
     d p_keyl          s              3p 0
     d p_sndl          s              3p 0
     d p_sndi          s             36
      *
      * Definering av datastrukturer
      *
      * Datakø - inn
      *
     d                 ds
     d d_inpu                        51
     d  d_kund                        6  0 overlay(d_inpu: 1)
     d  d_dato                        8    overlay(d_inpu: 7)
     d  d_tbla                         z   overlay(d_inpu:15)
     d  d_dbla                        8    overlay(d_inpu:41)
     d  d_anta                        3    overlay(d_inpu:49)
      *
      * Datakøer - ut
      *
     d                 ds
     d d_out1                         6
     d  d_rec1                        1  0 overlay(d_out1: 1) inz(1)
     d  d_tota                        5  0 overlay(d_out1: 2)
      *
     d                 ds
6.NU d d_out2                        97
     d  d_rec2                        1  0 overlay(d_out2: 1) inz(2)
6.NU d  d_biln                        8  0 overlay(d_out2: 2)
6.NU d  d_bdat                        8    overlay(d_out2:10)
6.NU d  d_btxt                       20    overlay(d_out2:18)
6.NU d  d_fdat                        8    overlay(d_out2:38)
6.NU d  d_belo                       13    overlay(d_out2:46)
6.NU d  d_rbel                       13    overlay(d_out2:59)
6.NU d  d_tist                         z   overlay(d_out2:72)
      *
      * Definering av variabler i nøkler
      *
     d rktrl2_kund     s                   like(rnkund)
     d rktrl2_bdat     s                   like(rnbdat)
     d rktrl2_tist     s                   like(rntist)
      *
      * Definering av variable
      *
     d w_firm          s                   like(rnfirm)
     d w_anta          s              2  0
     d w_tell          s              5  0
     d w_retn          s              1
     d w_ant1          s              1
     d w_ant2          s              2
     d w_belp          s             11  2
     d w_dato          s               d   datfmt(*iso)
     d w_dbla          s               d   datfmt(*iso)
     d w_dato_8        s              8  0
     d w_data_8        s              8
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser og behandler datakø (input)
      *
     c                   dou       d_inpu = *blank
      *
     c                   eval      d_inpu = *blank
      *
     c                   call      'QRCVDTAQ'
     c                   parm                    p_dtqi
     c                   parm                    p_bibl
     c                   parm                    p_inpl
     c                   parm                    d_inpu
     c                   parm                    p_wait
     c                   parm                    p_keyo
     c                   parm                    p_keyl
     c                   parm                    p_uniq
     c                   parm                    p_sndl
     c                   parm                    p_sndi
      *
     c                   if        d_inpu <> *blank
     c                   exsr      behandling
     c                   endif
      *
     c                   enddo
      *
      * Avslutt
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedrutine for behandling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
      * Sjekker om dato er fyllt ut
      * Dersom dato ikke er utfylt, settes denne til 01.01 dette år
      *
     c                   if        d_dato <> *blank
     c                   move      d_dato        w_dato_8
     c                   else
     c                   time                    w_dato
     c     *iso0         move      w_dato        w_data_8
     c                   move      0101          w_data_8
     c                   move      w_data_8      w_dato_8
     c                   endif
      *
     c     *iso          move      w_dato_8      w_dato
      *
      * Utleder bla-dato fra nettbutikk
      *
     c                   eval      w_dbla = *loval
      *
     c                   if        d_dbla <> *blank
     c                   move      d_dbla        w_dato_8
     c     *iso          move      w_dato_8      w_dbla
     c                   endif
      *
      * Leser i gjennom og teller opp totalt antall poster
      *
     c                   exsr      tell_total
      *
      * Bestemmer blaretning og sidestørrelse
      *
     c                   select
     c                   when      %subst(d_anta:2:1) = ' '
     c                   eval      w_ant1 = %subst(d_anta:3:1)
     c                   move      w_ant1        w_anta
     c                   eval      w_retn = 'F'
     c                   when      %subst(d_anta:2:1) = '-'
     c                   eval      w_ant1 = %subst(d_anta:3:1)
     c                   move      w_ant1        w_anta
     c                   eval      w_retn = 'B'
     c                   when      %subst(d_anta:1:1) = ' '
     c                   eval      w_ant2 = %subst(d_anta:2:2)
     c                   move      w_ant2        w_anta
     c                   eval      w_retn = 'F'
     c                   when      %subst(d_anta:1:1) = '-'
     c                   eval      w_ant2 = %subst(d_anta:2:2)
     c                   move      w_ant2        w_anta
     c                   eval      w_retn = 'B'
     c                   endsl
      *
      * Henter ut riktig antall poster
      *
     c                   exsr      hent_poster
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne reskontroposter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_poster   begsr
      *
      * Posisjonerer på riktig startpunkt før fylling av side
      * Dersom bla-dato er utfyllt fra nettbutikk, benyttes denne
      *
     c                   eval      w_tell = 0
     c                   eval      rktrl2_kund = d_kund
     c                   eval      rktrl2_bdat = w_dato
     c                   eval      rktrl2_tist = d_tbla
      *
     c                   if        w_dbla <> *loval
     c                   eval      rktrl2_bdat = w_dbla
     c                   endif
      *
     c                   if        w_anta > 0
     c     rktrl2_key    setll     rktrl2                                                    .
     c                   else
     c     rktrl2_key    setgt     rktrl2                                                    .
     c                   endif
      *
      * Hopper over inpu-rad dersom bla-dato er utfylt
      *
     c                   if        w_dbla <> *loval
     c                   if        w_retn = 'F'
     c                   read      rktrl2
     c                   else
     c                   readp     rktrl2
     c                   endif
     c                   endif
      *
      * Leser poster i valgt blaretning
      *
     c                   if        w_retn = 'F'
     c                   read      rktrl2
     c                   else
     c                   readp     rktrl2
     c                   endif
      *
     c                   dow       not %eof and
     c                             rnfirm = w_firm and
     c                             rnkund = rktrl2_kund and
     c                             rnbdat >= w_dato and
     c                             w_tell < w_anta
      *
      * Redigerer ut-data og skriver til datakø
      *
     c                   eval      d_biln = rnbiln
     c     *iso0         move      rnbdat        d_bdat
     c                   eval      d_btxt = rntext
     c     *iso0         move      rnfdat        d_fdat
     c                   eval      d_belo = %editc(rnbelØ : 'P')
     c                   eval      d_rbel = %editc(rnrest : 'P')
     c                   eval      d_tist = rntist
      *
     c     '.':','       xlate     d_belo        d_belo
     c     '.':','       xlate     d_rbel        d_rbel
      *
     c                   call      'QSNDDTAQ'
     c                   parm                    p_dtqo
     c                   parm                    p_bibl
     c                   parm                    p_outl
     c                   parm                    d_out2
     c                   parm                    p_keyl
     c                   parm                    p_uniq
      *
     c                   eval      w_tell = w_tell + 1
      *
     c                   if        w_retn = 'F'
     c                   read      rktrl2
     c                   else
     c                   readp     rktrl2
     c                   endif
     c                   enddo
      *
     c     end_saldo     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å telle totalt antall poster i utalg
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     tell_total    begsr
      *
      * Posisjonerer på riktig startpunkt
      *
     c                   eval      w_tell = 0
     c                   eval      rktrl2_kund = d_kund
     c                   eval      rktrl2_bdat = w_dato
     c     rktrl2_key    setll     rktrl2                                                    .
      *
      * Leser poster og teller opp
      *
     c                   read      rktrl2
      *
     c                   dow       not %eof and
     c                             rnfirm = w_firm and
     c                             rnkund = rktrl2_kund
      *
     c                   eval      w_tell = w_tell + 1
      *
     c                   read      rktrl2
     c                   enddo
      *
     c                   eval      d_tota = w_tell
      *
     c                   call      'QSNDDTAQ'
     c                   parm                    p_dtqo
     c                   parm                    p_bibl
     c                   parm                    p_outl
     c                   parm                    d_out1
     c                   parm                    p_keyl
     c                   parm                    p_uniq
      *
     c     end_total     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ERROR-rutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *pssr         begsr
      *
     c                   eval      p_feil = *on
     c                   goto      avslutt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_uniq
     c                   parm                    p_dtqi
     c                   parm                    p_dtqo
     c                   parm                    p_bibl
     c                   parm                    p_feil
     c                   parm                    p_firm
     c                   parm                    p_lage
     c                   parm                    p_inpl
     c                   parm                    p_outl
     c                   parm                    p_wait
     c                   parm                    p_keyo
     c                   parm                    p_keyl
     c                   parm                    p_sndl
     c                   parm                    p_sndi
      *
      * Key for transaksjoner på kunde
      *
     c     rktrl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rktrl2_kund
     c                   kfld                    rktrl2_bdat
     c                   kfld                    rktrl2_tist
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
