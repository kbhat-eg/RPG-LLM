     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FO710R
      * Beskrivelse . . : Vise informasjon om hvem som holder ordre
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       150399 AMD  Nytt program
      *       030206 HKO  Utvidet informasjon
5.54  * R5.54 290307 AMD  Lagt om kjørekontorløsning til logg-filen
5.60  * R5.60 010708 BOF  Utvidet logg-fil med år
6.NU  * R6.30 260514 bsa  Programmet gjennomgått mtp nummerutvidelser.
7.00  * R7.00 060320 bkv  132 bilde
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     fausrl1    if   e           k disk    rename(ausrpfr:ausrl1r)
     fra09l1    if   e           k disk    rename(ra09pfr:ra09l1r)
      *
     ffo710d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av parametre
      *
     d p_firm          s                   like(fofirm)
6.nu d p_numm          s                   like(fonumm)
     d p_suff          s                   like(fosuff)
      *
      * Definering av variabler i nøkler
      *
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d ausrl1_user     s                   like(abuser)
     d ra09l1_ikod     s                   like(raikod)
      *
      * Definering av variable
      *
     d w_firm          s                   like(fofirm)
5.54 d w_kode          s              1
5.54 d w_svar          s              1
5.54 d w_date          s               d   datfmt(*iso)
5.54 d w_time          s               t   timfmt(*iso)
5.54 d w_user          s             10
5.54 d w_wsid          s             10
5.60 d w_aarr          s              4  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * X1 Behandler opplysning om hvem som holder ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter inn opplysninger fra Ordre-Hode-Tabell
      *
     c                   eval      fohel1_numm = p_numm
     c                   eval      fohel1_suff = p_suff
     c     fohel1_key    chain     fohel1r
     c                   if        %found
     c                   eval      x1kous = fokous
     c                   eval      x1kows = fokows
     c                   if        fokoda <> *loval
     c     *dmy          move      fokoda        x1koda
     c                   endif
     c                   if        fokoti <> *loval
     c     *hms          move      fokoti        x1koti
     c                   endif
     c                   if        fodate <> *loval
     c     *dmy          move      fodate        x1date
     c                   endif
     c                   if        fotime <> *loval
     c     *hms          move      fotime        x1time
     c                   endif
5.54  *
5.54  * Henter ut dato og kl. for
5.54  * når ordren er sendt til plukking
5.54  *
5.54 c                   eval      w_kode = '2'
5.60 c                   eval      w_aarr = %subdt(fobdat:*years)
5.54 c                   call      'FU704R'
5.60 c                   parm                    w_aarr
5.54 c                   parm                    fonumm
5.54 c                   parm                    fosuff
5.54 c                   parm                    w_kode
5.54 c                   parm                    w_svar
5.54 c                   parm                    w_date
5.54 c                   parm                    w_time
5.54 c                   parm                    w_user
5.54 c                   parm                    w_wsid
5.54  *
5.54 c                   if        w_svar = 'J'
5.54  *
5.54 c                   if        w_date <> *loval
5.54 c     *dmy          move      w_date        x1plda
5.54 c                   endif
5.54 c                   if        w_time <> *loval
5.54 c     *hms          move      w_time        x1plti
5.54 c                   endif
5.54 c                   endif
      *
      * Henter inn opplysninger fra Bruker-Tabell
      *
     c                   if        fokous <> *blank
     c                   eval      ausrl1_user = fokous
     c     ausrl1_key    chain     ausrl1r
     c                   if        %found
     c                   eval      x1knav = abnavn
     c                   endif
     c                   endif
      *
     c                   eval      ausrl1_user = fouser
     c     ausrl1_key    chain     ausrl1r
     c                   if        %found
     c                   eval      x1onav = abnavn
     c                   endif
      *
      * Henter inn opplysninger fra selger
      *
     c                   if        foplse <> 0
     c                   eval      ra09l1_ikod = foplse
     c     ra09l1_key    chain     ra09l1r
     c                   if        %found
     c                   eval      x1plna = raitxt
     c                   endif
     c                   endif
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *    Behandler skjembilde om hvem som holder ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send bilde
      *
     c     e1tabg        tag
     c                   exfmt     x1bld
      *
      * Gå tilbake til forrige bilde
      *
     c                   if        *inkc
     c                   goto      xslutt
     c                   endif
      *
      * Avslutt program
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
6.nu c                   parm                    p_numm
     c                   parm                    p_suff
      *
      * Key for Ordre-hode-tabell
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      * Key for brukerregister
      *
     c     ausrl1_key    klist
     c                   kfld                    ausrl1_user
      *
      * Key for selger-register
      *
     c     ra09l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra09l1_ikod
      *
      *
      * Henter firmanummer fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
