     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RP600R
      * Beskrivelse . . : Prosjekt, spørring
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
6.20  *       111210 NHO  Nytt program
7.01  * MGR   031017 sst  Mulighet til å opprette prosjekt
7.02  *       210819 sst  Prosjektregnskap Fase 2,  August 2019
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frp1pl1    if   e           k disk    rename(rp1ppfr:rp1pl1r)
     frp1pl2    if   e           k disk    rename(rp1ppfr:rp1pl2r)
     Frkunl1    if   e           k disk
     frp600d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av parametre
      *
T5.00d p_firm          s                   like(rp1frm)
     d p_pros          s                   like(rp1pro)
     d p_txt1          s                   like(rp1txt)
     d p_uprs          s                   like(rp1rup)
p.02 d p_fdat          s              6  0
p.02 d p_tdat          s              6  0
p.02 d p_knav          s             30
p.02 d p_selg          s              4  0
p.02 d p_kunf          s              6  0
p.02 d p_kpro          s              6  0
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
T5.00d rp1pl1_firm     s                   like(rp1frm)
     d rp1pl1_pros     s                   like(rp1pro)
     d rp1pl2_alfa     s                   like(rp1alf)
     d rkunl1_kund     s                   like(rkkund)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_eof           s              1    inz(*off)
     d b_valg_ok       s              1    inz(*off)
      *
     d w_firm          s                   like(rp1frm)
      *
      * Definering av konstanter
      *
E5.50d c_sfil          s              2  0 inz(8)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * w_curn - Current recordnummer på siden
      * w_virn - Vis den side som inneholder denne record.
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
7.01 c                   when      *inkf
7.01 c                   exsr      regny
7.01 c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2pros <> *blanks or
     c                             b2alfa <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
     c                   if        b_valg_ok = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_sfrn <> 0
     c     w_sfrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
     c     rp1pl2_key    setll     rp1pl2
     c                   other
     c     rp1pl1_key    setll     rp1pl1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  * Subrutine for registrering av nye Prosjekt/underprosjekt/aktivitet
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *
7.01 c     regny         begsr
7.01  *
7.01 c                   call      'RP100R'
7.02 c                   parm                    p_fdat
7.02 c                   parm                    p_tdat
7.02 c                   parm                    p_txt1
7.02 c                   parm                    p_selg
7.02 c                   parm                    p_kunf
7.02 c                   parm                    p_pros
7.02 c                   parm                    p_kpro
7.02 c                   eval      rp1pl1_pros = p_pros
7.01 c                   exsr      forny
7.01 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på nøkkel
      *
     c                   if        b2pros <> *blanks
     c                   eval      w_seqe = ' '
     c                   eval      rp1pl1_pros = b2pros
     c     rp1pl1_key    setll     rp1pl1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på beskrivelse
      *
     c                   if        b2alfa <> *blank
     c                   eval      w_seqe = 'B'
     c                   eval      rp1pl2_alfa = b2alfa
     c     rp1pl2_key    setll     rp1pl2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2pros = *blank
     c                   eval      b2alfa = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Velg
      *
     c                   if        b1valg = 1
     c                   eval      p_pros = b1pros
     c                   eval      p_txt1 = b1txt1
     c                   eval      p_uprs = b1uprs
     c                   eval      b_valg_ok = *on
     c                   leave
     c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      rp1pl2                                 15
     c                   other
     c                   read      rp1pl1                                 15
     c                   endsl
      *
     c                   if        %eof or
     c                             rp1frm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
      * Ikke ta med prosjekt som er avsluttet
     c                   if        rp1sts = 'A'
     c                   goto      dd100
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1pros = rp1pro
      *Finn kundenavn
     c                   eval      rkunl1_kund = rp1knr
     c     rkunl1_key    chain     rkunl1                             61
     c                   if        *in61 = '0'
     c                   eval      b1navn = rknavn
     c                   else
     c                   move      *blank        b1navn
     c                   endif
      *
     c                   eval      b1txt1 = rp1txt
     c                   eval      b1uprs = rp1rup
      *
     c                   write     b1sfl
      *
     c     dd100         tag
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   select
     c                   when      w_seqe = 'B'
     c     rp1pl2_key    setll     rp1pl2
     c                   other
     c     rp1pl1_key    setll     rp1pl1
     c                   endsl
      *
      * Leser tilbake en side + posisjoneringsrad
      *
     c                   eval      b_eof  = *off
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = c_sfil + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   readp     rp1pl2
     c                   other
     c                   readp     rp1pl1
     c                   endsl
      *
     c                   if        %eof or
     c                             rp1frm <> w_firm
     c                   eval      b_eof = *on
     c                   leave
     c                   endif
      *
      *
      * Ikke ta med prosjekt som er avsluttet
     c                   if        rp1sts = 'A'
     c                   goto      dd200
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   if        w_stel <= c_sfil
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      rp1pl2_alfa = rp1alf
     c                   other
     c                   eval      rp1pl1_pros = rp1pro
     c                   endsl
     c                   endif
      *
     c     dd200         tag
      *
     c                   enddo
      *
     c                   if        b_eof = *on
     c                   select
     c                   when      w_seqe = 'B'
     c     rp1pl2_key    setll     rp1pl2
     c                   other
     c     rp1pl1_key    setll     rp1pl1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
T5.00c                   parm                    p_firm
     c                   parm                    p_pros
     c                   parm                    p_txt1
     c                   parm                    p_uprs
7.02 c                   parm                    p_fdat
7.02 c                   parm                    p_tdat
7.02 c                   parm                    p_knav
7.02 c                   parm                    p_selg
7.02 c                   parm                    p_kunf
7.02 c                   parm                    p_kpro
      *
      * Key for posisjonering på prosjekt
      *
     c     rp1pl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rp1pl1_pros
      *
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for posisjonering på beskrivelse
      *
     c     rp1pl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rp1pl2_alfa
      *
      *
      * Henter parametre, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      rp1pl1_pros = *blanks
     c     rp1pl1_key    setll     rp1pl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
