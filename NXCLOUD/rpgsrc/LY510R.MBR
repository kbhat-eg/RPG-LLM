     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : LY510R
      * Beskrivelse . . : Slettede bestillinger, vise
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       010307 ASK  Nytt program
6.10  * R6.10 250809 BSA  Utvidet ref. felter fra 20 til 30 posisjoner
6.nu  * R6.30 270514 bsa  Programmet gjennomgått mtp nummerutvidelser.
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fldell1    if   e           k disk    rename(ldelpfr:ldell1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
      *
     fly510d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av parametre
      *
     d p_firm          s                   like(lyfirm)
     d p_aarr          s                   like(lyaarr)
6.nu d p_numm          s                   like(lynumm)
     d p_suff          s                   like(lysuff)
      *
      * Definering av variabler i nøkler
      *
     d ldell1_aarr     s                   like(lyaarr)
     d ldell1_numm     s                   like(lynumm)
     d ldell1_suff     s                   like(lysuff)
     d rlevl1_levr     s                   like(rllevr)
      *
      * Definering av variable
      *
     d w_firm          s                   like(lyfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Vise detaljer for slettede ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      a1ldor = 0
     c                   eval      a1dumy = *blank
     c                   eval      a1navn = *blank
     c                   eval      a1adr1 = *blank
     c                   eval      a1adr2 = *blank
     c                   eval      a1sted = *blank
     c                   eval      a1bdat = 0
      *
      *  Henter inn opplysninger fra Ordre-Slettet-tabell
      *
     c                   eval      ldell1_aarr = p_aarr
     c                   eval      ldell1_numm = p_numm
     c                   eval      ldell1_suff = p_suff
     c     ldell1_key    chain     ldell1r                            90
      *
     c                   if        *in90  = *off
     c                   eval      a1date = 0
     c                   eval      a1time = 0
     c                   eval      a1ldor = lyldor
     c                   eval      a1kjop = lytoti - lytotr
      *
     c                   if        lydate <> *loval
     c     *dmy          move      lydate        a1date
     c                   endif
      *
     c                   if        lytime <> *loval
     c     *hms          move      lytime        a1time
     c                   endif
      *
     c                   if        lybdat <> *loval
     c     *dmy          move      lybdat        a1bdat
     c                   endif
     c                   endif
      *
      *  Henter inn opplysninger fra Leverandør-tabell
      *
     c                   eval      rlevl1_levr = lyldor
     c     rlevl1_key    chain     rlevl1r                            91
     c                   if        *in91  = *off
     c                   eval      a1navn = rlnavn
     c                   eval      a1adr1 = rlgate
     c                   eval      a1adr2 = rladr2
     c                   eval      a1sted = rlsted
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *    Behandler skjembilde om hvem som holder ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send bilde
      *
     c     e1tabg        tag
     c                   exfmt     a1bld
      *
      * Gå tilbake til forrige bilde
      *
     c                   if        *inkc
     c                   goto      xslutt
     c                   endif
      *
      * Avslutt program
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for Ordre-Slettet-tabell
      *
     c     ldell1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ldell1_aarr
     c                   kfld                    ldell1_numm
     c                   kfld                    ldell1_suff
      *
      * Key for Levr.-register
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_aarr
6.nu c                   parm                    p_numm
     c                   parm                    p_suff
      *
      * Henter firmanummer fra LDA
      *
     c                   eval      w_firm      = l_firm
      *
     c                   endsr
      *
