     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLOGI
      * Program . . . . : MC100R
      * Beskrivelse . . : Parameter for leveringstid
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       081204 bø   Nytt program
      * 6.10  170609 BSA  Oppdatert med sporingsinformasjon
6.30  * 6.30  260614 BKV  Krav at leverandør må være angitt
6.31  * 6.30  050615 Bsa  Krav at leverandør må være gyldig ved kopiering
7.xx  * 7.00  160316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fmltil1    if   e           k disk    rename(mltipfr:mltil1r)
     fmltil2    if   e           k disk    rename(mltipfr:mltil2r)
     fmltil3    if   e           k disk    rename(mltipfr:mltil3r)
     fmltil4    if   e           k disk    rename(mltipfr:mltil4r)
     fmltil5    if   e           k disk    rename(mltipfr:mltil5r)
     fmltilr    if   e           k disk    rename(mltipfr:mltilrr)
     fmltilu    uf a e           k disk    rename(mltipfr:mltilur)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
6.31 frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
      *
     fMC100d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
     d                 ds
     d p_prec                       128
     d  p_fvar                        8  0 overlay(p_prec)
     d  p_tvar                        8  0 overlay(p_prec:9)
     d  p_flag                        2  0 overlay(p_prec:17)
     d  p_tlag                        2  0 overlay(p_prec:19)
     d  p_fogr                        2  0 overlay(p_prec:21)
     d  p_togr                        2  0 overlay(p_prec:23)
     d  p_fhgr                        2  0 overlay(p_prec:25)
     d  p_thgr                        2  0 overlay(p_prec:27)
     d  p_fugr                        3  0 overlay(p_prec:29)
     d  p_tugr                        3  0 overlay(p_prec:32)
     d  p_levn                        6  0 overlay(p_prec:35)
     d  p_sakb                        4    overlay(p_prec:41)
     d  p_sort                        1  0 overlay(p_prec:45)
     d  p_brud                        1  0 overlay(p_prec:46)
     d  p_dage                        3  0 overlay(p_prec:47)
     d  p_crdo                        1  0 overlay(p_prec:50)
      * Definering av variabler i nøkler
      *
     d mltil1_ogrp     s                   like(mcogrp)
     d mltil1_hgrp     s                   like(mchgrp)
     d mltil1_ugrp     s                   like(mcugrp)
     d mltil2_vare     s                   like(mcvare)
     d mltil3_ldor     s                   like(mcldor)
     d mltil4_lage     s                   like(mclage)
     d mltil5_crdo     s                   like(mccrdo)
     d mltilr_ogrp     s                   like(mcogrp)
     d mltilr_hgrp     s                   like(mchgrp)
     d mltilr_ugrp     s                   like(mcugrp)
     d mltilr_ldor     s                   like(mcldor)
     d mltilr_vare     s                   like(mcvare)
     d mltilr_lage     s                   like(mclage)
     d mltilr_crdo     s                   like(mccrdo)
     d mltilu_ogrp     s                   like(mcogrp)
     d mltilu_hgrp     s                   like(mchgrp)
     d mltilu_ugrp     s                   like(mcugrp)
     d mltilu_ldor     s                   like(mcldor)
     d mltilu_vare     s                   like(mcvare)
     d mltilu_lage     s                   like(mclage)
     d mltilu_crdo     s                   like(mccrdo)
     d vvarl1_vare     s                   like(vvvare)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hhgr     s                   like(vghogr)
     d vugrl1_uugr     s                   like(vguugr)
6.31 d rlevl1_levr     s                   like(rllevr)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_parm          s            128
     d w_firm          s                   like(mcfirm)
     d w_ogrp          s                   like(mcogrp)
     d w_hgrp          s                   like(mchgrp)
     d w_ugrp          s                   like(mcugrp)
     d w_vare          s                   like(mcvare)
     d w_lage          s                   like(mclage)
     d w_ldor          s                   like(mcldor)
     d w_stat          s              1
     d w_jkod          s                   like(mclage)
     d w_jtxt          s             30
     d w_ltxt          s             30
     d w_vtxt          s             35
     d w_ogna          s             30
     d w_hgna          s             30
     d w_ugna          s             30
     d w_lena          s             30
     d w_vana          s             30
     d w_lana          s             30
     d w_navn          s             30
     d w_dag           s              2  0
     d w_mnd           s              2  0
     d w_pogrp         s                   like(mcogrp)
     d w_phgrp         s                   like(mchgrp)
     d w_pugrp         s                   like(mcugrp)
     d w_pldor         s                   like(mcldor)
     d w_pvare         s                   like(mcvare)
     d w_plage         s              2
      *
     d s_ogrp          s                   like(mcogrp)
     d s_hgrp          s                   like(mchgrp)
     d s_ugrp          s                   like(mcugrp)
     d s_ldor          s                   like(mcldor)
     d s_vare          s                   like(mcvare)
     d s_lage          s                   like(mclage)
     d s_crdo          s                   like(mccrdo)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
     d                 ds
     d d_list                       128
     d  d_vare                        8  0 overlay(d_list)
     d  d_lage                        2  0 overlay(d_list:8)
     d  d_ogrp                        2  0 overlay(d_list:10)
     d  d_hgrp                        2  0 overlay(d_list:12)
     d  d_ugrp                        3  0 overlay(d_list:14)
     d  d_ldor                        6  0 overlay(d_list:17)
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnumcer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnumcer i subfile
      * w_sfrn - Første recordnumcer på siste side
      * w_ssrn - Siste recordnumcer på siste side
      * w_curn - Current recordnumcer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2tagb
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *inki
     c                   exsr      xn1win
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2ogrp <> 0 or
     c                             b2hgrp <> 0 or
     c                             b2ugrp <> 0 or
     c                             b2ldor <> 0 or
     c                             b2vare <> *blank or
     c                             b2lage <> 0 or
     c                             b2crdo <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      mltil1_ogrp = b1ogrp
     c                   eval      mltil1_ugrp = b1hgrp
     c                   eval      mltil1_ugrp = 0
     c     mltil1_key    setll     mltil1
     c                   when      w_seqe = 'C'
     c                   eval      mltil1_ogrp = b1ogrp
     c                   eval      mltil1_ugrp = b1hgrp
     c                   eval      mltil1_ugrp = b1ugrp
     c     mltil1_key    setll     mltil1
     c                   when      w_seqe = 'D'
     c                   eval      mltil2_vare = b1vare
     c     mltil2_key    setll     mltil2
     c                   when      w_seqe = 'E'
     c                   eval      mltil3_ldor = b1ldor
     c     mltil3_key    setll     mltil3
     c                   when      w_seqe = 'F'
     c                   eval      mltil4_lage = b1lage
     c     mltil4_key    setll     mltil4
     c                   when      w_seqe = 'G'
     c                   eval      mltil5_crdo = b1crdo
     c     mltil5_key    setll     mltil5
     c                   other
     c                   eval      mltil1_ogrp = b1ogrp
     c                   eval      mltil1_hgrp = 0
     c                   eval      mltil1_ugrp = 0
     c     mltil1_key    setll     mltil1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Undergruppe
      *
     c                   if        b2ugrp <> 0
     c                   eval      w_seqe = 'C'
     c                   eval      mltil1_hgrp = b2hgrp
     c                   eval      mltil1_ogrp = b2ogrp
     c                   eval      mltil1_ugrp = b2ugrp
     c     mltil1_key    setll     mltil1
     c                   goto      end_pos
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        b2hgrp <> 0
     c                   eval      w_seqe = 'B'
     c                   eval      mltil1_hgrp = b2hgrp
     c                   eval      mltil1_ogrp = b2ogrp
     c                   eval      mltil1_ugrp = 0
     c     mltil1_key    setll     mltil1
     c                   goto      end_pos
     c                   endif
      *
      * Overgruppe
      *
     c                   if        b2ogrp <> 0
     c                   eval      w_seqe = ' '
     c                   eval      mltil1_ogrp = b2ogrp
     c                   eval      mltil1_hgrp = 0
     c                   eval      mltil1_ugrp = 0
     c     mltil1_key    setll     mltil1
     c                   goto      end_pos
     c                   endif
      *
      * Vare
      *
     c                   if        b2vare <> *blank
     c                   eval      w_seqe = 'D'
     c                   movel     b2vare        mltil2_vare
     c     mltil2_key    setll     mltil2
     c                   goto      end_pos
     c                   endif
      *
      * Leverandør
      *
     c                   if        b2ldor <> 0
     c                   eval      w_seqe = 'E'
     c                   eval      mltil3_ldor = b2ldor
     c     mltil3_key    setll     mltil3
     c                   goto      end_pos
     c                   endif
      *
      * Lager
      *
     c                   if        b2lage <> 0
     c                   eval      w_seqe = 'F'
     c                   eval      mltil4_lage = b2lage
     c     mltil4_key    setll     mltil4
     c                   goto      end_pos
     c                   endif
      *
      * Cross docking
      *
     c                   if        b2crdo <> 0
     c                   eval      w_seqe = 'G'
     c                   eval      mltil5_crdo = b2crdo
     c     mltil5_key    setll     mltil5
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2ogrp = 0
     c                   eval      b2hgrp = 0
     c                   eval      b2ugrp = 0
     c                   eval      b2ldor = 0
     c                   eval      b2lage = 0
     c                   eval      b2crdo = 0
     c                   eval      b2vare = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1ogrp = b1ogrp
     c                   eval      c1hgrp = b1hgrp
     c                   eval      c1ugrp = b1ugrp
     c                   eval      c1vare = b1vare
     c                   eval      c1ldor = b1ldor
     c                   eval      c1lage = b1lage
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1ogrp = b1ogrp
     c                   eval      k1hgrp = b1hgrp
     c                   eval      k1ugrp = b1ugrp
     c                   eval      k1ldor = b1ldor
     c                   eval      k1vare = b1vare
     c                   eval      k1lage = b1lage
     c                   eval      k1crdo = b1crdo
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1ogrp = b1ogrp
     c                   eval      d1hgrp = b1hgrp
     c                   eval      d1ugrp = b1ugrp
     c                   eval      d1ldor = b1ldor
     c                   eval      d1vare = b1vare
     c                   eval      d1lage = b1lage
     c                   eval      d1crdo = b1crdo
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1ogrp = b1ogrp
     c                   eval      c1hgrp = b1hgrp
     c                   eval      c1ugrp = b1ugrp
     c                   eval      c1vare = b1vare
     c                   eval      c1ldor = b1ldor
     c                   eval      c1lage = b1lage
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Oppdater lager
      *
     c                   if        b1valg = 9
     c                   eval      m1ogrp = b1ogrp
     c                   eval      m1hgrp = b1hgrp
     c                   eval      m1ugrp = b1ugrp
     c                   eval      m1ldor = b1ldor
     c                   eval      m1vare = b1vare
     c                   eval      m1lage = b1lage
     c                   exsr      xm1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
     c                   eval      c1ogrp = 0
     c                   eval      c1hgrp = 0
     c                   eval      c1ugrp = 0
     c                   eval      c1ldor = 0
     c                   eval      c1lage = 0
     c                   eval      c1crdo = 0
     c                   eval      c1vare = *blank
     c                   eval      c1ogna = *blank
     c                   eval      c1hgna = *blank
     c                   eval      c1ugna = *blank
     c                   eval      c1lena = *blank
     c                   eval      c1vana = *blank
     c                   eval      c1lana = *blank
      *
     c                   eval      c1ogrp = b2ogrp
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Spørring
      *
     c                   if        *inkd
     c                   exsr      spØrring
     c                   goto      c1taga
     c                   endif
      *
      * Validering
      *
     c                   if        c1ogrp = 0 and
     c                             c1hgrp = 0 and
     c                             c1ugrp = 0 and
     c                             c1ldor = 0 and
     c                             c1vare = *blank
     c                   eval      *in37  = *on
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1ogrp <> 0
     c                   eval      vogrl1_oogr = c1ogrp
     c     vogrl1_key    chain     vogrl1
     c                   if        not %found(vogrl1)
     c                   eval      *in31 = *on
     c                   goto      c1taga
     c                   endif
     c                   eval      c1ogna = vgotxt
     c                   endif
      *
     c                   if        c1hgrp <> 0
     c                   eval      vogrl1_oogr = c1ogrp
     c                   eval      vhgrl1_hhgr = c1hgrp
     c     vhgrl1_key    chain     vhgrl1
     c                   if        not %found(vhgrl1)
     c                   eval      *in32 = *on
     c                   goto      c1taga
     c                   endif
     c                   eval      c1hgna = vghtxt
     c                   endif
      *
     c                   if        c1ugrp <> 0
     c                   eval      vogrl1_oogr = c1ogrp
     c                   eval      vhgrl1_hhgr = c1hgrp
     c                   eval      vugrl1_uugr = c1ugrp
     c     vugrl1_key    chain     vugrl1
     c                   if        not %found(vugrl1)
     c                   eval      *in33 = *on
     c                   goto      c1taga
     c                   endif
     c                   eval      c1ugna = vgutxt
     c                   endif
      * leverandør
     c                   if        c1ldor <> 0
     c                   eval      w_stat = *blank
     c                   eval      w_ldor = c1ldor
     c                   call      'RS760R  '
     c                   parm                    w_stat
     c                   parm                    w_ldor
     c                   parm                    w_ltxt
     c                   if        w_stat <> *blank
     c                   eval      *in34 = *on
     c                   goto      c1taga
     c                   endif
     c                   eval      c1vana = w_ltxt
     c                   else
     c                   eval      *in39 = *on
     c                   goto      c1taga
     c                   endif
      * vare
     c                   if        c1vare <> *blank
     c                   movel     c1vare        vvarl1_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found(vvarl1)
     c                   eval      *in35 = *on
     c                   goto      c1taga
     c                   endif
     c                   movel     vvtek1        c1vana
     c                   endif
      * lager
     c                   if        c1lage <> 0
     c                   eval      w_stat = *blank
     c                   eval      w_jkod = c1lage
     c                   call      'RS710R  '
     c                   parm                    w_stat
     c                   parm                    w_jkod
     c                   parm                    w_ltxt
     c                   if        w_stat <> *blank
     c                   eval      *in36 = *on
     c                   goto      c1taga
     c                   endif
     c                   movel     w_ltxt        w_lana
     c                   endif
     c                   if        c1vare <> *blank
     c                   movel     c1vare        vvarl1_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found(vvarl1)
     c                   eval      *in35 = *on
     c                   goto      c1taga
     c                   endif
     c                   movel     vvtek1        c1vana
     c                   endif
      * cross docking kode
     c                   if        c1crdo <> 0 and
     c                             c1crdo <> 1 and
     c                             c1crdo <> 1
     c                   eval      *in38 = *on
     c                   goto      c1taga
     c                   endif
      *
      * Sender opp informcsjonsbilde dersom rad finnes fra før
      *
     c                   eval      mltilr_ogrp = c1ogrp
     c                   eval      mltilr_hgrp = c1hgrp
     c                   eval      mltilr_ugrp = c1ugrp
     c                   eval      mltilr_ldor = c1ldor
     c                   eval      mltilr_vare = c1vare
     c                   eval      mltilr_lage = c1lage
     c                   eval      mltilr_crdo = c1crdo
     c     mltilr_key    chain     mltilr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1ogrp = 0
     c                   eval      c1hgrp = 0
     c                   eval      c1ugrp = 0
     c                   eval      c1ldor = 0
     c                   eval      c1lage = 0
     c                   eval      c1vare = *blank
     c                   eval      c1ogna = *blank
     c                   eval      c1hgna = *blank
     c                   eval      c1ugna = *blank
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2ogrp = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      mltilr_ogrp = c1ogrp
     c                   eval      mltilr_hgrp = c1hgrp
     c                   eval      mltilr_ugrp = c1ugrp
     c                   eval      mltilr_ldor = c1ldor
     c                   eval      mltilr_vare = c1vare
     c                   eval      mltilr_lage = c1lage
     c                   eval      mltilr_crdo = c1crdo
     c     mltilr_key    chain     mltilr
      *
     c                   if        %found
     c                   eval      c2ltid = mcltid
     c                   if        mckdat <> *loval
     c     *dmy          move      mckdat        c2kdat
     c                   else
     c                   eval      c2kdat = 0
     c                   endif
     c                   eval      c2eusr = mcuser
     c     *dmy          move      mcodat        c2odat
     c     *dmy          move      mcedat        c2edat
     c                   else
     c                   eval      c2ltid = 0
     c                   eval      c2kdat = 0
     c                   eval      c2eusr = *blank
     c                   eval      c2odat = 0
     c                   eval      c2edat = 0
     c                   endif
     c                   eval      w_ogrp = c1ogrp
     c                   eval      w_hgrp = c1hgrp
     c                   eval      w_ugrp = c1ugrp
     c                   eval      w_ldor = c1ldor
     c                   eval      w_lage = c1lage
     c                   eval      w_vare = c1vare
     c                   exsr      hent_txt
     c                   eval      c1ogna = w_ogna
     c                   eval      c1hgna = w_hgna
     c                   eval      c1ugna = w_ugna
     c                   eval      c1vana = w_vana
     c                   eval      c1lena = w_lena
     c                   eval      c1lana = w_lana
      *
     c                   if        b1valg = 3
     c                   eval      c1ogrp = k1ogrp
     c                   eval      c1hgrp = k1hgrp
     c                   eval      c1ugrp = k1ugrp
     c                   eval      c1ldor = k1ldor
     c                   eval      c1vare = k1vare
     c                   eval      c1lage = k1lage
     c                   eval      c1crdo = k1crdo
     c                   eval      w_ogrp = k1ogrp
     c                   eval      w_hgrp = k1hgrp
     c                   eval      w_ugrp = k1ugrp
     c                   eval      w_ldor = k1ldor
     c                   eval      w_lage = k1lage
     c                   eval      w_vare = k1vare
     c                   exsr      hent_txt
     c                   eval      c1ogna = w_ogna
     c                   eval      c1hgna = w_hgna
     c                   eval      c1ugna = w_ugna
     c                   eval      c1vana = w_vana
     c                   eval      c1lena = w_lena
     c                   eval      c1lana = w_lana
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   exsr      spØrring
     c                   goto      c2taga
     c                   endif
      *
      * Validering av input
      *
      *
      * Oppdatering
      *
     c                   clear                   mltilur
      *
     c                   eval      mltilu_ogrp = c1ogrp
     c                   eval      mltilu_hgrp = c1hgrp
     c                   eval      mltilu_ugrp = c1ugrp
     c                   eval      mltilu_ldor = c1ldor
     c                   eval      mltilu_vare = c1vare
     c                   eval      mltilu_lage = c1lage
     c                   eval      mltilu_crdo = c1crdo
     c     mltilu_key    chain     mltilur
      *
     c                   eval      mcltid = c2ltid
      *
     c                   time                    mcedat
     c                   time                    mcetim
6.10 c                   eval      mceusr = l_user
      *
     c                   if        %found
     c                   update    mltilur
     c                   else
     c                   eval      mcfirm = w_firm
     c                   eval      mcogrp = c1ogrp
     c                   eval      mchgrp = c1hgrp
     c                   eval      mcugrp = c1ugrp
     c                   eval      mcldor = c1ldor
     c                   eval      mcvare = c1vare
     c                   eval      mclage = c1lage
     c                   eval      mccrdo = c1crdo
     c                   time                    mcodat
6.10 c                   time                    mcotim
6.10 c                   eval      mcuser = l_user
     c                   write     mltilur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1ltid = mcltid
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      c1vare = *blank
     c                   eval      c1ogna = *blank
     c                   eval      c1hgna = *blank
     c                   eval      c1ugna = *blank
     c                   eval      c1lena = *blank
     c                   eval      c1vana = *blank
     c                   eval      c1lana = *blank
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      mltilr_ogrp = d1ogrp
     c                   eval      mltilr_hgrp = d1hgrp
     c                   eval      mltilr_ugrp = d1ugrp
     c                   eval      mltilr_ldor = d1ldor
     c                   eval      mltilr_vare = d1vare
     c                   eval      mltilr_lage = d1lage
     c                   eval      mltilr_crdo = d1crdo
     c     mltilr_key    chain     mltilr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      mltilu_ogrp = d1ogrp
     c                   eval      mltilu_hgrp = d1hgrp
     c                   eval      mltilu_ugrp = d1ugrp
     c                   eval      mltilu_ldor = d1ldor
     c                   eval      mltilu_vare = d1vare
     c                   eval      mltilu_lage = d1lage
     c                   eval      mltilu_crdo = d1crdo
     c     mltilu_key    chain     mltilur
      *
     c                   delete    mltilur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_ogrp = k1ogrp
     c                   eval      s_hgrp = k1hgrp
     c                   eval      s_ugrp = k1ugrp
     c                   eval      s_ldor = k1ldor
     c                   eval      s_vare = k1vare
     c                   eval      s_lage = k1lage
     c                   eval      s_crdo = k1crdo
      *
      * Fyller kopierings-bildet
      *
     c                   eval      mltilr_ogrp = k1ogrp
     c                   eval      mltilr_hgrp = k1hgrp
     c                   eval      mltilr_ugrp = k1ugrp
     c                   eval      mltilr_ldor = k1ldor
     c                   eval      mltilr_vare = k1vare
     c                   eval      mltilr_lage = k1lage
     c                   eval      mltilr_crdo = k1crdo
     c     mltilr_key    chain     mltilr
     c                   eval      k2vare = mcvare
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
     c                   if        *inkd
     c                   exsr      spØrring
     c                   goto      k1taga
     c                   endif
6.31  *
6.31  * Angitt leverandør må være gyldig
6.31  *
6.31 c                   if        k1ldor <> 0
6.31 c                   eval      rlevl1_levr = k1ldor
6.31 c     rlevl1_key    chain     rlevl1
6.31 c                   if        not %found
6.31 c                   eval      *in40 = *on
6.31 c                   goto      k1taga
6.31 c                   endif
6.31 c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      mltilr_ogrp = k1ogrp
     c                   eval      mltilr_hgrp = k1hgrp
     c                   eval      mltilr_ugrp = k1ugrp
     c                   eval      mltilr_ldor = k1ldor
     c                   eval      mltilr_vare = k1vare
     c                   eval      mltilr_lage = k1lage
     c                   eval      mltilr_crdo = k1crdo
     c     mltilr_key    chain     mltilr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1ogrp = s_ogrp
     c                   eval      c1hgrp = s_hgrp
     c                   eval      c1ugrp = s_ugrp
     c                   eval      c1ldor = s_ldor
     c                   eval      c1vare = s_vare
     c                   eval      c1lage = s_lage
     c                   eval      c1crdo = s_crdo
     c                   exsr      xc2bld
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å Oppdatere lager (M1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xm1win        begsr
      *
     c                   eval      mltilr_ogrp = m1ogrp
     c                   eval      mltilr_hgrp = m1hgrp
     c                   eval      mltilr_ugrp = m1ugrp
     c                   eval      mltilr_vare = m1vare
     c                   eval      mltilr_lage = m1lage
     c                   eval      mltilr_ldor = m1ldor
     c                   eval      mltilr_crdo = m1crdo
     c     mltilr_key    chain     mltilr
      *
     c                   eval      w_ogrp = m1ogrp
     c                   eval      w_hgrp = m1hgrp
     c                   eval      w_ugrp = m1ugrp
     c                   eval      w_ldor = m1ldor
     c                   eval      w_lage = m1lage
     c                   eval      w_vare = m1vare
     c                   exsr      hent_txt
     c                   eval      m1ogna = w_ogna
     c                   eval      m1hgna = w_hgna
     c                   eval      m1ugna = w_ugna
     c                   eval      m1vana = w_vana
     c                   eval      m1lena = w_lena
     c                   eval      m1lana = w_lana
      *
     c                   exfmt     m1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Oppdatere lager
      *
     c                   movel     mcvare        p_fvar
     c                   movel     mcvare        p_tvar
     c                   eval      p_flag = mclage
     c                   eval      p_tlag = mclage
     c                   eval      p_fogr = mcogrp
     c                   eval      p_togr = mcogrp
     c                   eval      p_fogr = mcogrp
     c                   eval      p_togr = mcogrp
     c                   eval      p_fhgr = mchgrp
     c                   eval      p_thgr = mchgrp
     c                   eval      p_fugr = mcugrp
     c                   eval      p_tugr = mcugrp
     c                   eval      p_levn = mcldor
     c                   eval      p_crdo = mccrdo
     c                   eval      p_dage = mcltid
     c                   eval      w_parm = p_prec
      *
     c                   call      'MC103R  '
     c                   parm                    w_parm
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å Oppdatere lager (N1WIN)- alle parameter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xn1win        begsr
      *
     c                   exfmt     n1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   eval      b_forn = *on
      *
readec     mltil1a_key   setll     mltil1
readec                   if        %found
readec                   dou       %eof
readec     mltil1a_key   reade     mltil1
readec                   if        not %eof
      *
      * Oppdatere lager
      *
     c                   movel     mcvare        p_fvar
     c                   movel     mcvare        p_tvar
     c                   eval      p_flag = mclage
     c                   eval      p_tlag = mclage
     c                   eval      p_fogr = mcogrp
     c                   eval      p_togr = mcogrp
     c                   eval      p_fogr = mcogrp
     c                   eval      p_togr = mcogrp
     c                   eval      p_fhgr = mchgrp
     c                   eval      p_thgr = mchgrp
     c                   eval      p_fugr = mcugrp
     c                   eval      p_tugr = mcugrp
     c                   eval      p_levn = mcldor
     c                   eval      p_crdo = mccrdo
     c                   eval      p_dage = mcltid
     c                   eval      w_parm = p_prec
      *
     c                   call      'MC103R  '
     c                   parm                    w_parm
      *
     c                   endif
     c                   enddo
     c                   endif
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømcing av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      mltil1
     c                   when      w_seqe = 'C'
     c                   read      mltil1
     c                   when      w_seqe = 'D'
     c                   read      mltil2
     c                   when      w_seqe = 'E'
     c                   read      mltil3
     c                   when      w_seqe = 'F'
     c                   read      mltil4
     c                   when      w_seqe = 'G'
     c                   read      mltil5
     c                   other
     c                   read      mltil1
     c                   endsl
      *
     c                   if        %eof or
     c                             mcfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      mltil1_ogrp = mcogrp
     c                   eval      mltil1_hgrp = mchgrp
     c                   when      w_seqe = 'C'
     c                   eval      mltil1_ogrp = mcogrp
     c                   eval      mltil1_hgrp = mchgrp
     c                   eval      mltil1_ugrp = mcugrp
     c                   when      w_seqe = 'D'
     c                   eval      mltil2_vare = mcvare
     c                   when      w_seqe = 'E'
     c                   eval      mltil3_ldor = mcldor
     c                   when      w_seqe = 'F'
     c                   eval      mltil4_lage = mclage
     c                   when      w_seqe = 'G'
     c                   eval      mltil5_crdo = mccrdo
     c                   other
     c                   eval      mltil1_ogrp = mcogrp
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1ogrp = mcogrp
     c                   eval      b1hgrp = mchgrp
     c                   eval      b1ugrp = mcugrp
     c                   eval      b1vare = mcvare
     c                   eval      b1lage = mclage
     c                   eval      b1crdo = mccrdo
     c                   eval      b1ldor = mcldor
     c                   eval      b1ltid = mcltid
     c                   eval      b1navn = *blank
     c                   if        mckdat <> *loval
     c     *dmy          move      mckdat        b1kdat
     c                   else
     c                   eval      b1kdat = 0
     c                   endif
     c                   if        mcldor <> 0
     c                   eval      w_stat = *blank
     c                   eval      w_ldor = mcldor
     c                   call      'RS760R  '
     c                   parm                    w_stat
     c                   parm                    w_ldor
     c                   parm                    w_ltxt
     c                   if        w_stat <> *blank
     c                   eval      b1navn = *blank
     c                   else
     c                   movel     w_ltxt        b1navn
     c                   endif
     c                   else
     c                   if        mcvare <> *blank
     c                   movel     mcvare        vvarl1_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found(vvarl1)
     c                   eval      b1navn = *blank
     c                   endif
     c                   movel     vvtek1        b1navn
     c                   endif
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'B'
     c     mltil1_key    setgt     mltil1
     c                   readp     mltil1
     c                   when      w_seqe = 'C'
     c     mltil1_key    setgt     mltil1
     c                   readp     mltil1
     c                   when      w_seqe = 'D'
     c     mltil2_key    setgt     mltil2
     c                   readp     mltil2
     c                   when      w_seqe = 'E'
     c     mltil3_key    setgt     mltil3
     c                   readp     mltil3
     c                   when      w_seqe = 'F'
     c     mltil4_key    setgt     mltil4
     c                   readp     mltil4
     c                   when      w_seqe = 'G'
     c     mltil5_key    setgt     mltil5
     c                   readp     mltil5
     c                   other
     c     mltil1_key    setgt     mltil1
     c                   readp     mltil1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   readp     mltil1
     c                   when      w_seqe = 'C'
     c                   readp     mltil1
     c                   when      w_seqe = 'D'
     c                   readp     mltil2
     c                   when      w_seqe = 'E'
     c                   readp     mltil3
     c                   when      w_seqe = 'F'
     c                   readp     mltil4
     c                   when      w_seqe = 'G'
     c                   readp     mltil5
     c                   other
     c                   readp     mltil1
     c                   endsl
      *
     c                   if        %eof or
     c                             mcfirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      mltil1_ogrp = mcogrp
     c                   eval      mltil1_hgrp = mchgrp
     c                   when      w_seqe = 'C'
     c                   eval      mltil1_ogrp = mcogrp
     c                   eval      mltil1_hgrp = mchgrp
     c                   eval      mltil1_ugrp = mcugrp
     c                   when      w_seqe = 'D'
     c                   eval      mltil2_vare = mcvare
     c                   when      w_seqe = 'E'
     c                   eval      mltil3_ldor = mcldor
     c                   when      w_seqe = 'F'
     c                   eval      mltil4_lage = mclage
     c                   when      w_seqe = 'G'
     c                   eval      mltil5_crdo = mccrdo
     c                   other
     c                   eval      mltil1_ogrp = mcogrp
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'B'
     c     mltil1_key    setll     mltil1
     c                   when      w_seqe = 'C'
     c     mltil1_key    setll     mltil1
     c                   when      w_seqe = 'D'
     c     mltil2_key    setll     mltil2
     c                   when      w_seqe = 'E'
     c     mltil3_key    setll     mltil3
     c                   when      w_seqe = 'F'
     c     mltil4_key    setll     mltil4
     c                   when      w_seqe = 'G'
     c     mltil5_key    setll     mltil5
     c                   other
     c     mltil1_key    setll     mltil1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * overgruppe
     c                   if        w_afld = 'C1OGRP'
     c                   eval      w_firm      = l_firm
     c                   eval      w_ogrp      = c1ogrp
     c                   eval      w_navn      = *blank
4.86 c                   CALL      'VG510R  '
     c                   PARM                    w_firm
     c                   PARM                    w_ogrp
     c                   PARM                    w_navn
     c                   eval      c1ogrp      = w_ogrp
     c                   goto      end_spØrr
     c                   endif
     c                   if        w_afld = 'K1OGRP'
     c                   eval      w_firm      = l_firm
     c                   eval      w_ogrp      = k1ogrp
     c                   eval      w_navn      = *blank
4.86 c                   CALL      'VG510R  '
     c                   PARM                    w_firm
     c                   PARM                    w_ogrp
     c                   PARM                    w_navn
     c                   eval      k1ogrp      = w_ogrp
     c                   goto      end_spØrr
     c                   endif
     c                   if        w_afld = 'B2OGRP'
     c                   eval      w_firm      = l_firm
     c                   eval      w_ogrp      = b2ogrp
     c                   eval      w_navn      = *blank
4.86 c                   CALL      'VG510R  '
     c                   PARM                    w_firm
     c                   PARM                    w_ogrp
     c                   PARM                    w_navn
     c                   eval      b2ogrp      = w_ogrp
     c                   goto      end_spØrr
     c                   endif
      *
      * hovedgruppe
     c                   if        w_afld = 'C1HGRP'
     c                   eval      w_firm      = l_firm
     c                   eval      w_ogrp      = c1ogrp
     c                   eval      w_hgrp      = c1hgrp
     c                   eval      w_navn      = *blank
4.86 c                   CALL      'VG511R  '
     c                   PARM                    w_firm
     c                   PARM                    w_ogrp
     c                   PARM                    w_hgrp
     c                   PARM                    w_navn
     c                   eval      c1ogrp      = w_ogrp
     c                   eval      c1hgrP      = w_hgrp
     c                   goto      end_spØrr
     c                   endif
     c                   if        w_afld = 'K1HGRP'
     c                   eval      w_firm      = l_firm
     c                   eval      w_ogrp      = k1ogrp
     c                   eval      w_hgrp      = k1hgrp
     c                   eval      w_navn      = *blank
4.86 c                   CALL      'VG511R  '
     c                   PARM                    w_firm
     c                   PARM                    w_ogrp
     c                   PARM                    w_hgrp
     c                   PARM                    w_navn
     c                   eval      k1ogrp      = w_ogrp
     c                   eval      k1hgrP      = w_hgrp
     c                   goto      end_spØrr
     c                   endif
     c                   if        w_afld = 'B2HGRP'
     c                   eval      w_firm      = l_firm
     c                   eval      w_ogrp      = b2ogrp
     c                   eval      w_hgrp      = b2hgrp
     c                   eval      w_navn      = *blank
4.86 c                   CALL      'VG511R  '
     c                   PARM                    w_firm
     c                   PARM                    w_ogrp
     c                   PARM                    w_hgrp
     c                   PARM                    w_navn
     c                   eval      b2ogrp      = w_ogrp
     c                   eval      b2hgrP      = w_hgrp
     c                   goto      end_spØrr
     c                   endif
      *
      * undergruppe
     c                   if        w_afld = 'C1UGRP'
     c                   eval      w_firm      = l_firm
     c                   eval      w_ogrp      = c1ogrp
     c                   eval      w_hgrp      = c1hgrp
     c                   eval      w_ugrp      = c1ugrp
     c                   eval      w_navn      = *blank
4.86 c                   CALL      'VG512R  '
     c                   PARM                    w_firm
     c                   PARM                    w_ogrp
     c                   PARM                    w_hgrp
     c                   PARM                    w_ugrp
     c                   PARM                    w_navn
     c                   eval      c1ogrp      = w_ogrp
     c                   eval      c1hgrp      = w_hgrp
     c                   eval      c1ugrp      = w_ugrp
     c                   goto      end_spØrr
     c                   endif
     c                   if        w_afld = 'K1UGRP'
     c                   eval      w_firm      = l_firm
     c                   eval      w_ogrp      = k1ogrp
     c                   eval      w_hgrp      = k1hgrp
     c                   eval      w_ugrp      = k1ugrp
     c                   eval      w_navn      = *blank
4.86 c                   CALL      'VG512R  '
     c                   PARM                    w_firm
     c                   PARM                    w_ogrp
     c                   PARM                    w_hgrp
     c                   PARM                    w_ugrp
     c                   PARM                    w_navn
     c                   eval      k1ogrp      = w_ogrp
     c                   eval      k1hgrp      = w_hgrp
     c                   eval      k1ugrp      = w_ugrp
     c                   goto      end_spØrr
     c                   endif
     c                   if        w_afld = 'B2UGRP'
     c                   eval      w_firm      = l_firm
     c                   eval      w_ogrp      = b2ogrp
     c                   eval      w_hgrp      = b2hgrp
     c                   eval      w_ugrp      = b2ugrp
     c                   eval      w_navn      = *blank
4.86 c                   CALL      'VG512R  '
     c                   PARM                    w_firm
     c                   PARM                    w_ogrp
     c                   PARM                    w_hgrp
     c                   PARM                    w_ugrp
     c                   PARM                    w_navn
     c                   eval      b2ogrp      = w_ogrp
     c                   eval      b2hgrp      = w_hgrp
     c                   eval      b2ugrp      = w_ugrp
     c                   goto      end_spØrr
     c                   endif
      *
      * Fra lager
     c                   if        w_afld = 'C1LAGE'
     c                   move      c1lage        w_jkod
     c                   call      'RA510R  '
     c                   parm                    w_firm
     c                   parm                    w_jkod
     c                   parm                    w_jtxt
     c                   move      w_jkod        c1lage
     c                   goto      end_spØrr
     c                   endif
     c                   if        w_afld = 'K1LAGE'
     c                   move      k1lage        w_jkod
     c                   call      'RA510R  '
     c                   parm                    w_firm
     c                   parm                    w_jkod
     c                   parm                    w_jtxt
     c                   move      w_jkod        k1lage
     c                   goto      end_spØrr
     c                   endif
     c                   if        w_afld = 'B2LAGE'
     c                   move      k1lage        w_jkod
     c                   call      'RA510R  '
     c                   parm                    w_firm
     c                   parm                    w_jkod
     c                   parm                    w_jtxt
     c                   move      w_jkod        b2lage
     c                   goto      end_spØrr
     c                   endif
      * Vare
     c                   if        w_afld = 'C1VARE'
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_vtxt
     c                   movel     w_vare        c1vare
     c                   goto      end_spØrr
     c                   endif
     c                   if        w_afld = 'K1VARE'
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_vtxt
     c                   movel     w_vare        k1vare
     c                   goto      end_spØrr
     c                   endif
     c                   if        w_afld = 'B2VARE'
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_vtxt
     c                   movel     w_vare        b2vare
     c                   goto      end_spØrr
     c                   endif
      * Leverandør
     c                   if        w_afld = 'C1LDOR'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    w_ldor
     c                   parm                    w_navn
     c                   eval      c1ldor = w_ldor
     c                   goto      end_spØrr
     c                   endif
     c                   if        w_afld = 'K1LDOR'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    w_ldor
     c                   parm                    w_navn
     c                   eval      k1ldor = w_ldor
     c                   goto      end_spØrr
     c                   endif
     c                   if        w_afld = 'B2LDOR'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    w_ldor
     c                   parm                    w_navn
     c                   eval      b2ldor = w_ldor
     c                   goto      end_spØrr
     c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å hente tekster til diverse felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_txt      begsr
      *
     c                   eval      w_ogna = *blank
     c                   eval      w_hgna = *blank
     c                   eval      w_ugna = *blank
     c                   eval      w_lena = *blank
     c                   eval      w_vana = *blank
     c                   eval      w_lana = *blank
      *
     c                   eval      vogrl1_oogr = w_ogrp
     c                   eval      vhgrl1_hhgr = w_hgrp
     c                   eval      vugrl1_uugr = w_ugrp
     c                   eval      vvarl1_vare = w_vare
     c     vogrl1_key    chain     vogrl1
     c                   if        %found(vogrl1)
     c                   eval      w_ogna = vgotxt
     c                   else
     c                   eval      w_ogna = *blank
     c                   endif
     c     vhgrl1_key    chain     vhgrl1
     c                   if        %found(vhgrl1)
     c                   eval      w_hgna = vghtxt
     c                   else
     c                   eval      w_hgna = *blank
     c                   endif
     c     vugrl1_key    chain     vugrl1
     c                   if        %found(vugrl1)
     c                   eval      w_ugna = vgutxt
     c                   else
     c                   eval      w_ugna = *blank
     c                   endif
     c     vvarl1_key    chain     vvarl1
     c                   if        %found(vvarl1)
     c                   eval      w_vana = vvtek1
     c                   else
     c                   eval      w_vana = *blank
     c                   endif
     c                   if        w_lage <> 0
     c                   eval      w_stat = *blank
     c                   eval      w_jkod = w_lage
     c                   call      'RS710R  '
     c                   parm                    w_stat
     c                   parm                    w_jkod
     c                   parm                    w_jtxt
     c                   movel     w_jtxt        w_lana
     c                   endif
     c                   if        w_ldor <> 0
     c                   eval      w_stat = *blank
     c                   eval      w_ldor = w_ldor
     c                   call      'RS760R  '
     c                   parm                    w_stat
     c                   parm                    w_ldor
     c                   parm                    w_ltxt
     c                   movel     w_ltxt        w_lena
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for les av alle parameter
      *
     c     mltil1a_key   klist
     c                   kfld                    w_firm
      *
      * Key for posisjonering på overgruppe
      *
     c     mltil1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mltil1_ogrp
     c                   kfld                    mltil1_hgrp
     c                   kfld                    mltil1_ugrp
      *
      * Key for posisjonering på vare
      *
     c     mltil2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mltil2_vare
      *
      * Key for posisjonering på ldor
      *
     c     mltil3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mltil3_ldor
      *
      * Key for posisjonering på lager
      *
     c     mltil4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mltil4_lage
      *
      * Key for posisjonering på cross docking
      *
     c     mltil5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mltil5_crdo
      *
      * Key for les på abc parameter
      *
     c     mltilr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mltilr_ogrp
     c                   kfld                    mltilr_hgrp
     c                   kfld                    mltilr_ugrp
     c                   kfld                    mltilr_ldor
     c                   kfld                    mltilr_vare
     c                   kfld                    mltilr_lage
     c                   kfld                    mltilr_crdo
      *
      * Key for oppdatering av abc parameter
      *
     c     mltilu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mltilu_ogrp
     c                   kfld                    mltilu_hgrp
     c                   kfld                    mltilu_ugrp
     c                   kfld                    mltilu_ldor
     c                   kfld                    mltilu_vare
     c                   kfld                    mltilu_lage
     c                   kfld                    mltilu_crdo
      *
      * Key for les av vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for les av overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for les av hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for les av undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
     c                   kfld                    vhgrl1_hhgr
     c                   kfld                    vugrl1_uugr
6.31  *
6.31  * Key for posisjonering på leverandør
6.31  *
6.31 c     rlevl1_key    klist
6.31 c                   kfld                    w_firm
6.31 c                   kfld                    rlevl1_levr
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      mltil1_ogrp = 0
     c                   eval      mltil1_hgrp = 0
     c                   eval      mltil1_ugrp = 0
     c     mltil1_key    setll     mltil1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
