      /TITLE  Oppslag mot bruker-register
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : AA000R
      * Beskrivelse . . : Syst. Adm. Oppslag mot bruker-register
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       140300 AMD  Nytt versjon av programmet
5.31  * R5.31 070704 AMD  Lagt inn variabel for miljø (A01 - A02 osv.)
      *
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer
      *
      *       LR = Avslutt program
      *    60-69 = Fileindikatorer chain etc.
      *    70-79 = Arbeidsindikatorer i sub-rutiner
      *    80-89 = Arbeidsindikatorer ordinære
      *    90-99 = Benyttes ved std. sub-rutiner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fausrl1    if   e           k disk
     f                                     rename(ausrpfr:ausrl1r)
      *
      * Definering av Local data area
      *
     d                uds
     d dsokok                900    900  0
     d dswsid                901    910
     d dsuser                911    920
     d dsmeny                921    930
     d dssfil                931    933
     d dssbib                934    943
     d dssfir                944    946  0
     d dssavd                947    950  0
     d dsfnav                951    980
     d dsbfil                981    981
     d dsbfir                982    982
     d dsbavd                983    983
     d dssw12                984    984
     d dslevl                996    997  0
     d dslagr               1001   1002  0
5.31 d dsmilj               1003   1005
      *
      * Definering av parametre
      *
     d p_jobb          s                   like(abuser)
     d p_okok          s              1  0
     d p_user          s                   like(abuser)
      *
      * Definering av variabler i nøkler
      *
     d ausrl1_user     s                   like(abuser)
      *
      * Definering av variable
      *
     d w_alfa          s              1
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hent inn bruker-record
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      ausrl1_user = p_user
     c     ausrl1_key    chain     ausrl1r                            60
      *
     c                   if        *in60 = *off
     c                   eval      p_okok = 0
     c                   eval      dsokok = 0
T001 c                   eval      dswsid = p_jobb
     c                   eval      dsuser = abuser
     c                   eval      dsmeny = abmeny
     c                   eval      dssfil = absfil
     c                   eval      dssbib = absbib
     c                   eval      dssfir = absfir
     c                   eval      dssavd = absavd
     c                   eval      dsfnav = abfnav
     c                   eval      dsbfil = abbfil
     c                   eval      dsbfir = abbfir
     c                   eval      dsbavd = abbavd
     c                   eval      dslevl = ablevl
     c                   eval      dslagr = ablagr
5.31 c                   eval      dsmilj = abmilj
     c                   else
     c                   eval      p_okok = 1
     c                   eval      dsokok = 1
     c                   endif
      *
      * Ved bruker fra nettbutikk
      * er det ikke krav om at
      * bruker skal være definert
      *
     c                   eval      w_alfa = %subst(p_user:1:1)
      *
     c                   if        p_user = 'ASPKASSE  ' or
     c                             p_user = 'NORGROS   ' or
     c                             p_user = 'ASPRMI    ' or
     c                             w_alfa = 'Q'
     c                   eval      p_okok = 0
     c                   eval      dsokok = 0
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c                   eval      p_okok = 0
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_okok
     c                   parm                    p_user
     c                   parm                    p_jobb
      *
      * Key for file nummerisk
      *
     c     ausrl1_key    klist
     c                   kfld                    ausrl1_user
      *
     c                   endsr
      *
