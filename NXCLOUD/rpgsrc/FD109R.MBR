     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FD109R
      * Description . . : Order-line, quick create item line
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Date  Sign Description
      * ----- ------ ---- -------------------------------------------------
      *       250425 bsa  New program
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     ffodtlu    uf a e           k disk    rename(fodtpfr:fodtlur)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     fvot1l1    if   e           k disk    rename(vot1pfr:vot1l1r)
      *
      * Definering av parametre
      *
     d p_kode          s              1
     d p_firm          s                   like(fdfirm)
     d p_numm          s                   like(fdnumm)
     d p_suff          s                   like(fdsuff)
     d p_line          s                   like(fdline)
     d p_ltyp          s                   like(fdltyp)
     d p_lety          s                   like(fdlety)
     d p_vare          s                   like(fdvare)
     d p_anta          s                   like(fdanta)
     d p_enhe          s                   like(fdenh1)
     d p_ldat          s                   like(fdldat)
     d p_vtyp          s                   like(vvvtyp)
     d p_rab1          s              5  2
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_filg                931    933
      *
      * Definering av datastrukturer
      *
      * Parametre for henting av pris -inn
      *
     d                 ds
     d hirec                        120
     d  hifirm                        3  0 overlay(hirec)
     d  hirkat                        3  0 overlay(hirec:4)
     d  hikund                        6  0 overlay(hirec:7)
     d  hikpro                        6  0 overlay(hirec:13)
     d  hivare                       15    overlay(hirec:19)
     d  hilety                        1  0 overlay(hirec:34)
     d  hienhe                        3    overlay(hirec:35)
     d  hidato                         d   overlay(hirec:38) datfmt(*iso)
     d  hitime                         t   overlay(hirec:48) timfmt(*iso)
     d  hinumm                        8  0 overlay(hirec:56)
     d  hikode                        1    overlay(hirec:64)
     d  hidekn                        5  2 overlay(hirec:65)
     d  hiavgf                        1  0 overlay(hirec:70)
     d  hipriv                        1  0 overlay(hirec:71)
     d  hitilb                        1    overlay(hirec:72)
     d  hiskaf                        1    overlay(hirec:73)
     d  hildor                        6  0 overlay(hirec:74)
     d  hisapr                        9  2 overlay(hirec:80)
     d  hikopr                        9  2 overlay(hirec:89)
     d  hiprgr                        2    overlay(hirec:98)
     d  hiinlr                        1    overlay(hirec:120)
      *
      * Parametre for henting av pris -ut
      *
     d                 ds
     d horec                         80
     d  holety                        1  0 overlay(horec)
     d  hokpri                        1    overlay(horec:2)
     d  hooppr                        9  2 overlay(horec:3)
     d  hosapr                        9  2 overlay(horec:12)
     d  hokopr                        9  2 overlay(horec:21)
     d  horab1                        5  2 overlay(horec:30)
     d  horab2                        5  2 overlay(horec:35)
     d  hodekn                        5  2 overlay(horec:40)
     d  hopros                        5  2 overlay(horec:45)
     d  hokamp                        5    overlay(horec:50)
     d  hoalme                        4  0 overlay(horec:55)
     d  hobrty                        1  0 overlay(horec:59)
     d  hobrr1                        5  2 overlay(horec:60)
     d  hobrr2                        5  2 overlay(horec:65)
     d  hoomva                        1    overlay(horec:70)
     d  hoprgr                        2    overlay(horec:71)
      *
      * Lageroppdatering
      *
     d                 ds
     d hlrec                        128
     d  hlvare                       15    overlay(hlrec)
     d  hllage                        2  0 overlay(hlrec:16)
     d  hldato                         d   overlay(hlrec:18) datfmt(*iso)
     d  hltime                         t   overlay(hlrec:28) timfmt(*iso)
     d  hlotyp                        2    overlay(hlrec:36)
     d  hlltyp                        2    overlay(hlrec:38)
     d  hlanta                       11  3 overlay(hlrec:40)
     d  hlkopr                        9  2 overlay(hlrec:51)
     d  hlrefr                       20    overlay(hlrec:60)
     d  hlsyst                        1    overlay(hlrec:80)
     d  hlaltk                        2    overlay(hlrec:81)
     d  hlrest                        1    overlay(hlrec:83)
     d  hltype                        1    overlay(hlrec:84)
     d  hlenhe                        3    overlay(hlrec:85)
     d  hlinlr                        1    overlay(hlrec:88)
     d  hllety                        1    overlay(hlrec:89)
     d  hlonum                        8  0 overlay(hlrec:90)
     d  hlolin                        4  0 overlay(hlrec:98)
      *
      * Definering av variabler i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vvenl1_vare     s                   like(vevare)
     d vvenl1_enhe     s                   like(veenhe)
     d rkunl1_kund     s                   like(rkkund)
     d vlagl1_vare     s                   like(vlvare)
     d vlagl1_lage     s                   like(vllage)
     d vot1l1_otyp     s                   like(va1typ)
      *
      * Definering av variable
      *
     d w_stat          s              1
      *
     d w_firm          s                   like(vvfirm)
     d w_numm          s                   like(fonumm)
     d w_suff          s                   like(fosuff)
     d w_line          s                   like(fdline)
      *
     d w_raba          s                   like(fdsums)
     d w_sumr          s                   like(fdsums)
     d w_sums          s                   like(fdsums)
     d w_prgr          s                   like(fdprgr)
     d w_udat          s               d   datfmt(*iso)
     d w_ldor          s                   like(vvldor)
     d w_lv_nobb       s                   like(vvlnob)
     d w_lv_modn       s                   like(vvlmod)
     d w_lv_lldo       s                   like(vvlnle)
     d w_lv_llva       s                   like(vvllva)
     d w_numa          s              8
     d w_lina          s              4
     d w_sura          s                   like(fdsums)
     d w_rabw          s             11  4
     d w_enh1          s                   like(fdenh1)
     d w_kopr          s                   like(fdkopr)
     d w_inpr          s                   like(fdinpr)
     d w_dato          s                   like(fdedat)
     d w_lety          s                   like(fdlety)
     d w_vare          s                   like(fdvare)
     d w_kamp          s              5
     d w_gtin          s                   like(fdeann)
     d w_levr          s              6  0
      *
     d b_inlr          s              1    inz(*off)
     d b_kont          s              1    inz(*off)
     d w_tilb          s                   like(fdsapr)
     d w_tra1          s                   like(fdrab1)
     d w_tra2          s                   like(fdrab2)
     d w_enhe          s                   like(fdenh1)
      *
      * Definering av konstanter
      *
     d lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
      *
      * Eksternt program
      *
       dcl-s co402_verdi1  char(1);
       dcl-s co402_verdi2  char(2048);
       DCL-PR CO402R EXTPGM('CO402R');
         p_filgrp            char(3)     const;
         p_firm              packed(3:0) const;
         p_lager             packed(2:0) const;
         p_nokkel            char(50)    const;
         p_verdi1            char(1);
         p_verdi2            char(2048);
       END-PR;
     d ukjent_vare_ok  s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Avbryter dersom vare eller antall ikke er utfylt
      *
     c                   if        p_vare = *blank or
     c                             p_anta = 0
     c                   goto      avslutt
     c                   endif
      *
      * Avbryter dersom enhet ikke finnes på vare
      *
     c                   eval      vvenl1_vare = p_vare
     c                   eval      vvenl1_enhe = p_enhe
     c     vvenl1_key    chain     vvenl1
     c                   if        not %found
     c                             and ukjent_vare_ok = *off
     c                   goto      avslutt
     c                   endif
      *
      * Avbryter dersom ordre-linje finnes fra før
      *
     c     fodtl1_key    chain     fodtl1
     c                   if        %found
     c                   goto      avslutt
     c                   endif
      *
      * Henter vareinformasjon
      *
     c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
      *
      *     Logikk fra HENTFAPR vedr ukjente varer
      *
     c                   if        ukjent_vare_ok = *on
     c                   call      'FD120R'
     c                   parm                    p_kode
     c                   parm                    w_firm
     c                   parm                    w_numm
     c                   parm                    w_suff
     c                   parm                    w_line
     c                   parm                    p_ltyp
     c                   parm                    p_lety
     c                   parm                    p_vare
     c                   parm                    p_anta
     c                   endif
      *
     c                   goto      avslutt
     c                   endif
      *
      * Kaller program for henting av Varetype fra lager/vare
      *
     c                   call      'VL710R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   parm                    folage
     c                   parm                    p_vtyp
     c                   parm                    w_udat
     c                   parm                    w_ldor
     c                   parm                    b_inlr
     c                   parm                    w_lv_nobb
     c                   parm                    w_lv_modn
     c                   parm                    w_lv_lldo
     c                   parm                    w_lv_llva
      *
     c                   if        b_kont = *off
     c                   eval      w_tilb = 0
     c                   eval      w_tra1 = 0
     c                   eval      w_tra2 = 0
     c                   eval      w_enhe = *blank
     c                   if        faak05 = 0
     c                   call      'FO751R  '
     c                   parm                    w_firm
     c                   parm                    fonumm
     c                   parm                    fokund
     c                   parm                    p_vare
     c                   parm                    w_tilb
     c                   parm                    w_tra1
     c                   parm                    w_tra2
     c                   parm                    w_enhe
     c                   endif
     c                   endif
      *
      * Kaller program for henting av prisgruppe fra lager/avdeling
      *
     c                   call      'VL712R'
     c                   parm                    w_firm
     c                   parm                    folage
     c                   parm                    foavde
     c                   parm                    w_prgr
      *
      * Initierer ordre-linje
      *
     c                   clear                   fodtlur
      *
     c                   eval      fdfirm = w_firm
     c                   eval      fdnumm = w_numm
     c                   eval      fdsuff = w_suff
     c                   eval      fdline = w_line
     c                   eval      fdltyp = p_ltyp
     c                   eval      fdvare = p_vare
     c                   eval      fdanta = p_anta
     c                   eval      fdenh1 = p_enhe
     c                   eval      fdtek1 = vvtek1
     c                   eval      fdtek2 = vvtek2
     c                   eval      fdlety = p_lety
     c                   eval      fdpasl = fopasl
     c                   eval      fdldat = p_ldat
      *
     c                   exsr      hent_GTIN
      *
      * Henter inn priser og rabatter
      *
     c                   eval      hienhe = fdenh1
     c                   eval      hilety = fdlety
     c                   eval      hidekn = fdpasl
      *
     c                   exsr      hent_pris
      *
     c                   eval      fdkopr = hokopr
      *
     c                   eval      fdprgr = hoprgr
     c                   exsr      hent_inpr
      *
     c                   if        fdinpr = 0
     c                   eval      fdinpr = w_inpr
     c                   endif
     c                   if        w_tilb <> 0
     c                   eval      fdsapr = w_tilb
     c                   eval      fdrab1 = w_tra1
     c                   eval      fdrab2 = w_tra2
     c                   eval      fdkpri = 'A'
     c                   else
     c                   eval      fdsapr = hosapr
     c                   if        va1int = 1
     c                   eval      fdkopr = w_inpr
     c                   else
     c                   eval      fdkopr = hokopr
     c                   endif
     c                   eval      fdkpri = hokpri
     c                   if        va1int = 1
     c                   eval      w_sura = fdsapr - fdkopr
     c                   if        fdsapr <> 0
     c                   eval      w_rabw = w_sura /fdsapr
     c                   else
     c                   eval      w_rabw = 0
     c                   endif
     c                   eval(h)   fdrab1 = w_rabw * 100
     c                   eval      fdrab2 = 0
     c                   else
     c                   eval      fdrab1 = horab1
     c                   eval      fdrab2 = horab2
     c                   endif
     c                   endif
     c                   if        p_rab1 <> 0
     c                   eval      fdrab1 = p_rab1
     c                   eval      fdrab2 = 0
     c                   endif
     c                   eval      fdkopr = hokopr
     c                   eval      fdpasl = hodekn
     c                   eval      fdkamp = hokamp
     c                   eval      fdalme = hoalme
     c                   eval      fdomva = hoomva
     c                   eval      fdbrty = hobrty
     c                   eval      fdbrr1 = hobrr1
     c                   eval      fdbrr2 = hobrr2
     c                   eval      fdpriv = 0
     c                   eval      fdprgr = hoprgr
      *
      * Beregning av totaler
      *
     c                   eval(h)   w_sums = fdsapr * fdanta
     c                   eval      w_sumr = 0
      *
      * Rabatt
      *
     c                   if        vvkord = 0
     c                   eval(h)   w_raba = (w_sums * forabp) / 100
     c                   eval      w_sumr = w_raba
     c                   endif
      *
     c                   if        fdrab1 <> 0
     c                   eval(h)   w_raba = (w_sums - w_sumr) * fdrab1 / 100
     c                   eval      w_sumr = w_sumr + w_raba
     c                   endif
      *
     c                   if        fdrab2 <> 0
     c                   eval(h)   w_raba = (w_sums - w_sumr) * fdrab2 / 100
     c                   eval      w_sumr = w_sumr + w_raba
     c                   endif
      *
     c                   if        fdbrr1 <> 0
     c                   eval(h)   w_raba = (w_sums - w_sumr) * fdbrr1 / 100
     c                   eval      w_sumr = w_sumr + w_raba
     c                   endif
      *
     c                   if        fdbrr2 <> 0
     c                   eval(h)   w_raba = (w_sums - w_sumr) * fdbrr2 / 100
     c                   eval      w_sumr = w_sumr + w_raba
     c                   endif
      *
      * Kostpris
      *
     c                   if        fdkopr = 0
     c                   eval      fdkopr = fdsapr * hopros
     c                   endif
      *
      * Legger inn totaler
      *
     c                   eval      fdsums = w_sums
     c                   eval      fdsumr = w_sumr
     c                   eval      fdsumk = fdkopr * fdanta
      *
      * Legger inn faste opplysninger fra vare og ordre
      *
     c                   eval      vlagl1_vare = fdvare
     c                   eval      vlagl1_lage = folage
     c     vlagl1_key    chain     vlagl1
     c                   eval      fdotyp = footyp
     c                   eval      fdlage = folage
     c                   eval      fdkakk = 0
     c                   eval      fdkgeb = 0
     c                   eval      fdlass = 0
      *
     c                   eval      fdkund = fokund
     c                   if        folkun <> 0
     c                   eval      fdkund = folkun
     c                   endif
      *
     c                   eval      fdbenr = 0
     c                   eval      fdbsuf = 0
     c                   eval      fdblin = 0
     c                   eval      fdavde = 0
     c                   eval      fdkont = 0
     c                   eval      fdspes = 0
     c                   eval      fdproj = *blank
     c                   eval      fdakti = *blank
     c                   if        w_lv_nobb > 0
     c                   eval      fdnobb = w_lv_nobb
     c                   eval      fdlvar = w_lv_llva
     c                   else
     c                   eval      fdnobb = vvnobb
     c                   eval      fdlvar = vvlvar
     c                   endif
     c                   eval      fdldor = vvldor
     c                   eval      fdogrp = vvogrp
     c                   eval      fdhgrp = vvhgrp
     c                   eval      fdugrp = vvugrp
     c                   if        vlbldo <> 0
     c                   eval      fdldor = vlbldo
     c                   endif
      *
      * Oversetter varetekst til store bokstaver dersom dette er valgt
      *
     c                   if        faahoy = 1
     c     lo:up         xlate     fdtek1        fdtek1
     c                   endif
      *
      * Skriver til ordre-linje
      *
     c                   time                    fdodat
     c                   time                    fdotim
     c                   eval      fdoous = l_user
     c                   time                    fdedat
     c                   time                    fdetim
     c                   eval      fdeusr = l_user
     c                   write     fodtlur
      *
      * Oppdatering av lager
      *
     c                   exsr      oppdat_lager
      *
      * Legger inn kode som sier at minst en linje er registrert
      *
     c                   eval      p_kode = *on
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter opplysninger om varen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_pris     begsr
      *
     c                   eval      hifirm = w_firm
     c                   eval      hirkat = 0
      *
     c                   eval      hikund = fokund
     c                   if        folkun <> 0
     c                   eval      hikund = folkun
     c                   endif
      *
     c                   eval      hikpro = fokpro
     c                   eval      hivare = vvvare
     c                   eval      hidato = fobdat
     c                   eval      hitime = *loval
     c                   eval      hinumm = 0
     c                   eval      hikode = fopask
     c                   eval      hiavgf = fomkod
     c                   eval      hipriv = 0
     c                   eval      hiskaf = *off
     c                   eval      hildor = 0
     c                   eval      hisapr = 0
     c                   eval      hikopr = 0
     c                   eval      hiprgr = w_prgr
      *
     c                   call      'VP900R'
     c                   parm                    hirec
     c                   parm                    horec
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdat_lager  begsr
      *
     c                   if        faalag = 1 and
     c                             p_vtyp = 'L'
     c                   else
     c                   goto      end_lager
     c                   endif
      *
      * Legger over verdier i overførings-felter
      *
     c                   eval      hlvare = fdvare
     c                   eval      hllage = folage
     c                   eval      hlenhe = fdenh1
     c                   eval      hldato = *loval
     c                   eval      hltime = *loval
     c                   eval      hlotyp = footyp
     c                   eval      hlltyp = fdltyp
      *
     c                   eval      hlanta = fdanta
     c                   eval      hlkopr = fdkopr
      *
     c                   eval      hlrefr = *blank
     c                   eval      hlsyst = 'F'
     c                   eval      hlaltk = *blank
     c                   eval      hlrest = *blank
     c                   move      fdnumm        w_numa
     c                   move      fdline        w_lina
     c                   eval      hlrefr = 'O:' + w_numa +
     c                                      ' L:' + w_lina
     c                   eval      hlonum = fdnumm
     c                   eval      hlolin = fdline
     c                   move      fdlety        hllety
      *
      * Kaller lageroppdaterings-program
      *
     c                   eval      w_stat = *blank
      *
     c                   call      'VL001R'
     c                   parm                    w_stat
     c                   parm                    hlrec
      *
     c     end_lager     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdat. av utg.fakt. logg
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_inpr     begsr
      *
     c                   eval      w_vare = fdvare
     c                   eval      w_enh1 = fdenh1
     c                   eval      w_lety = fdlety
     c                   call      'VP753R   '
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_prgr
     c                   parm                    w_enh1
     c                   parm                    w_lety
     c                   parm                    w_dato
     c                   parm                    w_kopr
     c                   parm                    w_inpr
     c                   parm                    w_kamp
     c                   if        w_inpr = 0
     c                   eval      w_inpr = fdkopr
     c                   endif
      *
     csr                 endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter oGTIN informasjon om vare/enhet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_GTIN     begsr
      *
     c                   eval      w_gtin = 0
     c                   eval      w_levr = 0
     c                   eval      w_stat = *blanks
      *
     c                   call      'VE713R'
     c                   parm                    w_firm
     c                   parm                    w_levr
     c                   parm                    p_vare
     c                   parm                    p_enhe
     c                   parm                    w_gtin
     c                   parm                    w_stat
      *
     c                   eval      fdeann = w_gtin
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_kode
     c                   parm                    p_firm
     c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_line
     c                   parm                    p_ltyp
     c                   parm                    p_lety
     c                   parm                    p_vare
     c                   parm                    p_anta
     c                   parm                    p_enhe
     c                   parm                    p_ldat
     c                   parm                    p_rab1
      *
      * Key for oppslag på status
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
     c                   kfld                    vvenl1_enhe
      *
      * Key for oppslag på ordre-hode
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
      *
      * Key for oppdatering av ordre-linje
      *
     c     fodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
     c                   kfld                    w_line
      *
      * Key for oppslag på ordre-linje
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
     c                   kfld                    w_line
      *
      * Key for oppslag på kunde - sideregister
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      *
      * Key for oppslag på lager
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl1_vare
     c                   kfld                    vlagl1_lage
      *
      * Key for oppslag på ordretype sideregister
      *
     c     vot1l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vot1l1_otyp
      *
      * Henter parametre
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_numm = p_numm
     c                   eval      w_suff = p_suff
     c                   eval      w_line = p_line
      *
      * Endring 7.00 Ukjent vare fra ekst.ordre ok
      *
       CallP CO402R(l_filg:w_firm:0:'EKSTORDRE_TILORDRE_UKJENT_VARE_OK':
                        co402_verdi1:co402_verdi2);
       if co402_verdi1 = '1';
         ukjent_vare_ok = *on;
       endif;
      *
      * Legger inn type for oppdatering av lager (R=Registrering)
      *
     c                   eval      hltype = ' '
      *
      * Hent opplysninger fra ofak koderegister
      *
     c     fstsl1_key    chain     fstsl1r                            90
      *
      * Hent opplysninger fra ordre-hode
      *
     c     fohel1_key    chain     fohel1r                            90
      *
      * sjekker om kunde har kontant-kunde satt på
      *
     c                   eval      b_kont = *off
     c                   eval      rkunl1_kund = fokund
     c     rkunl1_key    chain     rkunl1
     c                   if        %found(rkunl1) and
     c                             rkkoti = 1
     c                   eval      b_kont = *on
     c                   endif
      *
      * gjør oppslag i side-register av ordretype - sys inn i 6.30
      *
     c                   eval      vot1l1_otyp = footyp
     c     vot1l1_key    chain     vot1l1
     c                   if        not %found
     c                   eval      va1int = 0
     c                   endif
      *
      *
     c                   endsr
