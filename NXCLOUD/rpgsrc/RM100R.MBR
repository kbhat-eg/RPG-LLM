     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RM100R
      * Beskrivelse . . : Almenning, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       091203 HKO  Nytt program
      * V5.50 090607 KOB  Lagt inn vedlikehold av beløpsgrense almenning
7.xx  * 7.xx  120516 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fralml1    if   e           k disk    rename(ralmpfr:ralml1r)
     fralml2    if   e           k disk    rename(ralmpfr:ralml2r)
     fralmlr    if   e           k disk    rename(ralmpfr:ralmlrr)
     fralmlu    uf a e           k disk    rename(ralmpfr:ralmlur)
     fapospf    if   e           k disk    rename(apospfr:apospfr)
      *
     frm100d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d ralml1_alme     s                   like(rmalme)
     d ralml2_navn     s                   like(rmnavn)
     d ralmlr_alme     s                   like(rmalme)
     d ralmlu_alme     s                   like(rmalme)
     d apospf_ponr     s                   like(apponr)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(rmfirm)
      *
     d s_alme          s                   like(rmalme)
      *
      * Definering av konstanter
      *
7.xx d c_sfil          s              2  0 inz(13)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2alme <> 0 or
     c                             b2navn <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'N'
     c                   eval      ralml2_navn = b1navn
     c     ralml2_key    setll     ralml2
     c                   other
     c                   eval      ralml1_alme = b1alme
     c     ralml1_key    setll     ralml1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Almenning
      *
     c                   if        b2alme <> 0
     c                   eval      w_seqe = *blank
     c                   eval      ralml1_alme = b2alme
     c     ralml1_key    setll     ralml1
     c                   goto      end_pos
     c                   endif
      *
      * Navn
      *
     c                   if        b2navn <> *blank
     c                   eval      w_seqe = 'N'
     c                   eval      ralml2_navn = b2navn
     c     ralml2_key    setll     ralml2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2alme = 0
     c                   eval      b2navn = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1alme = b1alme
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1alme = b1alme
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1alme = b1alme
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1alme = b1alme
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Eiendommer
      *
     c                   if        b1valg = 6
     c                   call      'RM105R'
     c                   parm                    b1alme                                      .
     c                   parm                    b1navn                                      .
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Eiendomsknytning
      *
     c                   if        b1valg = 7
     c                   call      'RM110R'
     c                   parm                    b1alme
     c                   parm                    b1navn
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vare-knytning
      *
     c                   if        b1valg = 8
     c                   call      'VB100R'
     c                   parm                    w_firm                                      .
     c                   parm                    b1alme                                      .
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Bruksrett-typer
      *
     c                   if        b1valg = 9
     c                   call      'RM103R'
     c                   parm                    w_firm                                      .
     c                   parm                    b1alme                                      .
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1alme = b2alme
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1alme = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      ralmlr_alme = c1alme
     c     ralmlr_key    chain     ralmlr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1alme = 0
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2alme = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      ralmlr_alme = c1alme
     c     ralmlr_key    chain     ralmlr
      *
     c                   if        %found
     c                   eval      c2navn = rmnavn
     c                   eval      c2adr1 = rmadr1
     c                   eval      c2adr2 = rmadr2
     c                   eval      c2ponr = rmponr
     c                   eval      c2tlfn = rmtlfn
     c                   eval      c2faks = rmfaks
     c                   eval      c2mail = rmmail
     c                   eval      c2urla = rmurla
     c                   eval      c2kprs = rmkprs
     c                   eval      c2ktlf = rmktlf
     c                   eval      c2kmob = rmkmob
     c                   eval      c2kmai = rmkmai
     c                   eval      c2bgrn = rmbgrn
     c                   eval      c2eusr = rmeusr
     c     *dmy          move      rmodat        c2odat
     c     *dmy          move      rmedat        c2edat
     c                   else
     c                   eval      c2navn = *blank
     c                   eval      c2adr1 = *blank
     c                   eval      c2adr2 = *blank
     c                   eval      c2ponr = 0
     c                   eval      c2tlfn = *blank
     c                   eval      c2faks = *blank
     c                   eval      c2mail = *blank
     c                   eval      c2urla = *blank
     c                   eval      c2kprs = *blank
     c                   eval      c2ktlf = *blank
     c                   eval      c2kmob = *blank
     c                   eval      c2kmai = *blank
     c                   eval      c2bgrn = 0
     c                   eval      c2eusr = *blank
     c                   eval      c2odat = 0
     c                   eval      c2edat = 0
     c                   endif
      *
      * Henter poststed
      *
     c                   if        c2ponr <> 0
     c                   eval      apospf_ponr = c2ponr
     c     apospf_key    chain     apospf
     c                   if        %found
     c                   eval      c2sted = apsted
     c                   endif
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1alme = k1alme
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
      *
     c                   if        w_afld = 'C2PONR'
     c                   call      'AP500R'
     c                   parm                    c2ponr
     c                   parm                    c2sted
     c                   endif
      *
     c                   goto      c2taga
      *
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2navn = *blank
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
     c                   if        c2ponr <> 0
     c                   eval      apospf_ponr = c2ponr
     c     apospf_key    chain     apospf
     c                   if        not %found
     c                   eval      *in32 = *on
     c                   goto      c2taga
     c                   endif
     c                   eval      c2sted = apsted
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   ralmlur
      *
     c                   eval      ralmlu_alme = c1alme
     c     ralmlu_key    chain     ralmlur
      *
     c                   eval      rmnavn = c2navn
     c                   eval      rmadr1 = c2adr1
     c                   eval      rmadr2 = c2adr2
     c                   eval      rmponr = c2ponr
     c                   eval      rmtlfn = c2tlfn
     c                   eval      rmfaks = c2faks
     c                   eval      rmmail = c2mail
     c                   eval      rmurla = c2urla
     c                   eval      rmkprs = c2kprs
     c                   eval      rmktlf = c2ktlf
     c                   eval      rmkmob = c2kmob
     c                   eval      rmkmai = c2kmai
     c                   eval      rmbgrn = c2bgrn
      *
     c                   time                    rmedat
     c                   time                    rmetim
     c                   eval      rmeusr = l_user
      *
     c                   if        %found
     c                   update    ralmlur
     c                   else
     c                   eval      rmfirm = w_firm
     c                   eval      rmalme = c1alme
     c                   time                    rmodat
     c                   write     ralmlur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1navn = rmnavn
     c                   eval      b1kprs = rmkprs
     c                   eval      b1kmob = rmkmob
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      ralmlr_alme = d1alme
     c     ralmlr_key    chain     ralmlr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      ralmlu_alme = d1alme
     c     ralmlu_key    chain     ralmlur
      *
     c                   delete    ralmlur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_alme = k1alme
      *
      * Fyller kopierings-bildet
      *
     c                   eval      ralmlr_alme = k1alme
     c     ralmlr_key    chain     ralmlr
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1alme = 0
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      ralmlr_alme = k1alme
     c     ralmlr_key    chain     ralmlr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1alme = s_alme
     c                   exsr      xc2bld
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'N'
     c                   read      ralml2
     c                   other
     c                   read      ralml1
     c                   endsl
      *
     c                   if        %eof or
     c                             rmfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'N'
     c                   eval      ralml2_navn = rmnavn
     c                   other
     c                   eval      ralml1_alme = rmalme
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1alme = rmalme
     c                   eval      b1navn = rmnavn
     c                   eval      b1kprs = rmkprs
     c                   eval      b1kmob = rmkmob
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'N'
     c     ralml2_key    setgt     ralml2
     c                   readp     ralml2
     c                   other
     c     ralml1_key    setgt     ralml1
     c                   readp     ralml1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'N'
     c                   readp     ralml2
     c                   other
     c                   readp     ralml1
     c                   endsl
      *
     c                   if        %eof or
     c                             rmfirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'N'
     c                   eval      ralml2_navn = rmnavn
     c                   other
     c                   eval      ralml1_alme = rmalme
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'N'
     c     ralml2_key    setll     ralml2
     c                   other
     c     ralml1_key    setll     ralml1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på almenning
      *
     c     ralml1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ralml1_alme
      *
      * Key for posisjonering på navn
      *
     c     ralml2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ralml2_navn
      *
      * Key for les på almenning
      *
     c     ralmlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ralmlr_alme
      *
      * Key for oppdatering av almenning
      *
     c     ralmlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ralmlu_alme
      *
      * Key for oppslag på poststed
      *
     c     apospf_key    klist
     c                   kfld                    apospf_ponr
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      ralml1_alme = 0
     c     ralml1_key    setll     ralml1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
