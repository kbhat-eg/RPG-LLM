     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FM500R
      * Beskrivelse . . : Kort/fakt.avtaler, spørring
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       030704 GRO  Nytt program
6.10  * 6.10  021210 bsa  Flere mulige kortvarianter. Fjernet muligheten for
6.10  *                   å opprette flere. Må skje via kunderegister og F13.
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffmkalr    if   e           k disk    rename(fmkapfr:fmkalrr)
     ffmkal1    if   e           k disk    rename(fmkapfr:fmkal1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
      *
     ffm500d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(fmfirm)
     d p_kund          s                   like(fmkund)
     d p_kpro          s                   like(fmkpro)
     d p_kftp          s                   like(fmkftp)
     d p_ktsq          s                   like(fmktsq)
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d fmkalr_kund     s                   like(fmkund)
     d fmkal1_kund     s                   like(fmkund)
     d rkunl1_kund     s                   like(rkkund)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_valg_ok       s              1    inz(*off)
      *
     d w_firm          s                   like(fmfirm)
     d w_kund          s                   like(fmkund)
     d w_kpro          s                   like(fmkpro)
     d w_kort          s             16
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(8)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
6.10  * F6 er gjort ugyldig i skjermbildet. Lar koden ligge så lenge. Kommer til
6.10  * å finnes andre vedlikeholdsprogrammer.
     c                   when      *inkf
     c                   call      'FM100R'
     c                   parm                    w_firm
     c                   parm                    w_kund
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
     c                   if        b_valg_ok = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      fmkal1_kund = b1kund
     c     fmkal1_key    setll     fmkal1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Velg
      *
     c                   if        b1valg = 1
      *
     c                   eval      p_kftp = b1kftp
     c                   eval      p_ktsq = b1ktsq
     c                   eval      p_kpro = b1kpro
     c                   eval      b_valg_ok = *on
      *
     c                   leave
     c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
      * Fyller opp subfile
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      fmkal1
      *
     c                   if        %eof
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   if        fmfirm <> w_firm or
     c                             fmkund <> w_kund
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
      * Skriver til subfile
      *
     c                   if        fmkpro = *zero or
     c                             w_kpro = fmkpro
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      fmkal1_kund = fmkund
      *
     c                   eval      b1valg = 0
     c                   eval      b1kund = fmkund
     c                   eval      b1kpro = fmkpro
     c                   eval      b1kftp = fmkftp
     c                   eval      b1ktsq = fmktsq
      *
     c                   exsr      avt_type
      *
     c                   if        fmkrnr <> 0
     c                   move      fmkrnr        w_kort
     c                   eval      b1kort = '*' +
     c                             %subst(w_kort:11:6)
     c                   else
     c                   eval      b1kort = *blank
     c                   endif
      *
     c                   movel     fmhldr        b1navn
     c                   move      fmoblg        b1obli
      *
     c                   write     b1sfl
     c                   endif
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sette kort/avtaletype i klartekst
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avt_type      begsr
      *
     c                   if        fmkftp = 1
     c                   eval      b1type = 'VISA Purchas.'
6.10 c                   elseif    fmkftp = 2
     c                   eval      b1type = 'Europay Purc.'
6.10 c                   elseif    fmkftp = 3
6.10 c                   eval      b1type = 'MAXBO Kort   '
6.10 c                   elseif    fmkftp = 4
6.10 c                   eval      b1type = 'XL Bygg (GE) '
6.10 c                   elseif    fmkftp = 5
6.10 c                   eval      b1type = 'BM Kort (DNB)'
6.10 c                   elseif    fmkftp = 6
6.10 c                   eval      b1type = 'NBBL Fordelk.'
     c                   else
     c                   eval      b1type = 'Ukjent'
     c                   endif
      *
     c                   endsr
      *
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     fmkal1_key    setgt     fmkal1
     c                   readp     fmkal1
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     fmkal1
      *
     c                   if        %eof or
     c                             fmfirm <> w_firm or
     c                             fmkund <> w_kund
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   eval      fmkal1_kund = fmkund
      *
     c                   enddo
      *
     c                   if        %eof
     c     fmkal1_key    setll     fmkal1
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_bck       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_kund
     c                   parm                    p_kpro
     c                   parm                    p_kftp
     c                   parm                    p_ktsq
      *
      * Key for oppslag på kort/fakt.avtale-register
      *
     c     fmkalr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fmkalr_kund
      *
      * Key for posisjonering på kunde
      *
     c     fmkal1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fmkal1_kund
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      *
      * Henter parameter, og initierer skjermbildet
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_kund = p_kund
     c                   eval      w_kpro = p_kpro
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      fmkal1_kund = w_kund
     c     fmkal1_key    setll     fmkal1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
