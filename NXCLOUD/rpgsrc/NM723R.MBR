     H decedit('0.') datedit(*dmy-) option(*nodebugio)
     h DftActgrp(*No) Actgrp(*NEW)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : NM723R
      * Beskrivelse . . : Eksport av gårsdagens salg til Blueridge datahub
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       131221 bsa  Nytt program
      *
8.01  *       211221 bsa  Skriver til DB2, ikke json
8.02  *       130122 bsa  Bruker ikke gårsdagens dato, da denne kjøres
8.02  *                   på kvelden. Salget skal være fra samme dag.
8.03  * K000  010222 bsa  Lokal leverandør på lager, skal overstyre
8.03  * K000              evnt hovedleverandør fra vare.
8.04  * K000  020222 bsa  Rettet omregningsfaktor.
8.05  * K000  070222 bsa  Justert innhenting av leverandør. mangler overstyring
8.05  * K000              på lager. Fortsetter rettelse 8.03
8.06  * K000  070222 bsa  Feil håndtering av leverandør/prisgiver.
8.07  * K000  080222 bsa  Ikke alle prisgivere har reskontronummer. Endrer logikk.
8.08  * K000  210222 bsa  Endring av logikk rundt det å finne leverandør.
8.09  * K000  230222 bsa  Fortsetter med logikken, da det er endel "hull" i
8.09  * K000              grunndata rundt leverandører.
8.10  * K000  250222 bsa  Feil i hvordan antall på pakkseddel ble kalkulert. Opp-
8.10  * K000              rinnelig bestilt må fange opp det som er lagt tilbake
8.10  * K000              som restordre (dvs akkumulatorkode 1).
8.11  * K000  070322 bsa  Nullstiller navnet på lagertekst.
8.12  * K000  190522 bsa  Omregning av bonger går feil.
8.13  * K000  220622 bsa  Feil bruk av felter.
8.14  * K000  181022 bsa  Innfører ny tabell for overstyring av leverandør
8.14  * K000              via RDS lager.
8.15  * K000  250624 bsa  Tar med salg fra Malproff. Hentes fra VHISPF, og
8.15  * K000              identifiseres med kode MP.
      *
9.xx  * 9.xx  131221 bsa  Div test
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffodtlr    if   e           k disk    rename(fodtpfr:fodtlrr)
8.10 ffodtlv    if   e           k disk    rename(fodtpfr:fodtlvr)
8.10 f                                     prefix(xy:2)
     fsohel1    if   e           k disk    rename(sohepfr:sohel1r)
     fsodtlr    if   e           k disk    rename(sodtpfr:sodtlrr)
     fbohel1    if   e           k disk    rename(bohepfr:bohel1r)
     fbodtl1    if   e           k disk    rename(bodtpfr:bodtl1r)
     fvvenlr    if   e           k disk    rename(vvenpfr:vvenlrr)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     fbridiu    uf a e           k disk    rename(bridst:bridiur)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
8.03 fjlevl3    if   e           k disk    rename(jlevpfr:jlevl3r)
8.06 frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     fra10l1    if   e           k disk    rename(ra10pfr:ra10l1r)
8.08 fjvkhl1    if   e           k disk    rename(jvkhpfr:jvkhl1r)
     fafpspf    if   e           k disk    rename(afpspfr:afpspfr)
8.14 fvla2i1    if   e           k disk    rename(vla2st:vla2i1r)
8.15 fvhisl1    if   e           k disk    rename(vhispfr:vhisl1r)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_figr                934    939
     d l_filg                937    939
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d sodtlr_aarr     s                   like(sdaarr)
     d sodtlr_binr     s                   like(sdbinr)
     d sodtlr_numm     s                   like(sdnumm)
     d sodtlr_suff     s                   like(sdsuff)
     d sodtlr_ktxt     s                   like(sdktxt)
     d sodtlr_line     s                   like(sdline)
     d fodtlr_numm     s                   like(fdnumm)
     d fodtlr_suff     s                   like(fdsuff)
     d fodtlr_line     s                   like(fdline)
     d bridiu_dmem     s                   like(bidmem)
     d bridiu_dvar     s                   like(bidvar)
     d bridiu_dlag     s                   like(bidlag)
     d bridiu_divd     s                   like(bidivd)
     d vvenlr_vare     s                   like(vevare)
     d vvenlr_enhe     s                   like(veenhe)
     d votyl1_otyp     s                   like(vaotyp)
     d jlevl1_ldor     s                   like(jlldor)
8.03 d jlevl3_enum     s                   like(jlenum)
8.06 d rlevl1_levr     s                   like(rllevr)
     d vvarl1_vare     s                   like(vvvare)
     d ra10l1_jkod     s                   like(rajkod)
     d bodtl1_aarr     s                   like(bdaarr)
     d bodtl1_wsid     s                   like(bdwsid)
     d bodtl1_bong     s                   like(bdbong)
     d bodtl1_line     s                   like(bdline)
     d vlagl1_lage     s                   like(vllage)
     d vlagl1_vare     s                   like(vlvare)
8.08 d jvkhl1_kvnr     s                   like(jvkvnr)
8.10 d fodtlv_vare     s                   like(fdvare)
8.10 d fodtlv_numm     s                   like(fdnumm)
8.10 d fodtlv_suff     s                   like(fdsuff)
8.10 d fodtlv_enh1     s                   like(fdenh1)
8.14 d vla2i1_2var     s                   like(vl2var)
8.14 d vla2i1_2lag     s                   like(vl2lag)
      *
      * Definering av datastrukturer
      *
      *
      * Definering av parametere
      *
     d p_memb          s             10
      *
      * Definering av variable
      *
     d w_kode          s              1
     d w_fell          s              6
     d w_type          s              2
     d w_fast          s             10
     d w_numm          s              8  0
     d w_size          s              8  0
     d w_mal           s            200
     d w_path          s            200
     d w_firm          s                   like(fofirm)
     d w_inlr          s              1
     d w_memb          s             10
     d w_omrl          s                   like(veomre)
     d w_omrs          s                   like(veomre)
     d w_senh          s                   like(veenhe)
     d w_anta          s             11  3
8.10 d w_rest          s             11  3
     d w_lety          s              1  0
     d w_ornr          s              8  0
8.08 d w_valg          s              1  0
8.08 d w_jdor          s              6  0
8.08 d w_ldor          s              6  0
8.08 d w_ltxt          s             30
8.15 d w_mkod          s              2
      *
8.03 d s_ldor          s              6  0
8.10 d s_numm          s                   like(fonumm)
8.10 d s_suff          s                   like(fosuff)
8.10 d s_otyp          s                   like(footyp)
      *
      * Felter som legges ut i json rapporten
      *
     d w_date          s               d   datfmt(*iso)
      *
     d b_ordr          s              1    inz(*off)
      *
     c/Exec SQL
     c+  Set Option DatFmt=*ISO
     c/End-Exec
      *
      *-----------------------------------------------------------------*
      * Hovedprogram:                                                   *
      *   Plukker salg fra i går, basert utleverte pakksedler. Det gjør *
      *   av vi må sjekke flere tabeller for å finne resultatet.        *
      *   1 - Hent alle ordre med pakkseddeldato i går (FOPADA).        *
      *   2 - Bonger som er opprettet i går. Kunne jobbet mot           *
      *       statistikk, men ikke sikkert at dagsoppgjøret er kjørt.   *
      *   3 - Finn alle nye fakurerte ordre med pakkseddeldato i går.   *
      *                                                                 *
      *   Resultatet legges ut i temporær arbeidsfile, og selve json    *
      *   fila lages i etterfølgende program.                           *
      *                                                                 *
      *-----------------------------------------------------------------*
      *
      * Henter inn løpenummer teller
      *
8.01 c                   if        p_memb = *blanks
     c                   eval      w_kode = '0'
     c                   eval      w_fell = 'NXTKLI'
     c                   eval      w_type = *blank
     c                   eval      w_fast = *blank
     c                   eval      w_numm = *zero
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
     c                   parm                    w_numm
      *
     c                   eval      w_memb = 'AA' + %char(w_numm)
     c                   eval      bridiu_dmem = w_memb
8.02 c                   eval      p_memb = w_memb
8.01 c                   else
8.01 c                   eval      bridiu_dmem = p_memb
8.01 c                   endif
      *
9.xx c***                if        1=2
      *
      * Hent gårsdagens pakksedler basert på ordre som fortsatt ligger inne
      *
     c                   exsr      pakk_ordre
      *
      * Hent gårsdagens bonger basert på gårsdagens bonger.
      *
     c                   exsr      bong_ordre
      *
      * Hent gårsdagens salg basert på gårsdagens fakturaer.
      *
     c                   exsr      fakt_ordre
      *
9.xx c***                endif
8.15  *
8.15  * Hent gårsdagens salg fra Malproff
8.15  *
8.15 c                   exsr      fakt_malproff
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Finner paksedler fra gårsdagen.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     pakk_ordre    begsr
      *
      * Nullstiller teller for antall varer funnet
      *
     c                   eval      w_size = 0
     c                   eval      w_lety = 0
      *
      * Så finner vi alle ordre med pakkseddeldato fra i går.
      *
     c                   eval      *in15 = *off
      *
     c/exec sql
     c+                  declare sok cursor for
     c+                  select fofirm,
     c+                         fonumm,
     c+                         fosuff,
     c+                         folety,
     c+                         fopada,
     c+                         fokund,
     c+                         footyp
     c+                  from   fohepf
     c+                  where  fofirm = :w_firm and
     c+                         fopada = :w_date and
     c+                         folety = :w_lety
     c+                  order by fofirm, fonumm, fosuff
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok
     c/end-exec
     c/exec sql
     c+                  open sok
     c/end-exec
      *
     c                   dow       *in15 = *off
      *
     c/exec sql
     c+                  fetch next
     c+                  from  sok
     c+                  into  :fofirm,
     c+                        :fonumm,
     c+                        :fosuff,
     c+                        :folety,
     c+                        :fopada,
     c+                        :fokund,
     c+                        :footyp
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   if        *in15
     c                   leave
     c                   endif
      *
      * Ikke kreditordre, ref Gisle Åsberg
      *
     c                   eval      b_ordr = *off
     c                   eval      votyl1_otyp = footyp
     c     votyl1_key    chain     votyl1
     c                   if        %found
     c                   if        vaokre <> '-'
     c                   eval      b_ordr = *on
     c                   endif
     c                   endif
      *
     c                   if        b_ordr = *on
      *
      * Les ordrelinjer
      *
8.10 c                   eval      s_numm = fonumm
8.10 c                   eval      s_suff = fosuff
8.10 c                   eval      s_otyp = footyp
8.10  * Leser kun pakkseddel som er levert
     c                   eval      fodtlr_numm = fonumm
8.10 c**                 eval      fodtlr_suff = 0
8.10 c                   eval      fodtlr_suff = fosuff
     c                   eval      fodtlr_line = 0
     c     fodtlr_key    setll     fodtlr
     c     fodtlr_key2   reade     fodtlr
     c                   dow       not %eof
      * Ikke tekstlinjer
     c                   if        %trim(fdvare) <> *blanks and
     c                             fdanta <> 0
      *
      * Sjekk om det er EVR vare
      *
     c                   eval      vvarl1_vare = fdvare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   goto      ny_olinje
     c                   endif
9.xx  *
9.xx  *
9.xx c*                  if        fdlage = 15 and
9.xx c*                            %trim(fdvare) = '26925164'
9.xx c*                  goto      nest_xx
9.xx c*                  endif
9.xx c*    nest_xx       tag
9.xx  *
      *
      * Sjekk om det er lagervare
      *
     c                   eval      vlagl1_lage = fdlage
     c                   eval      vlagl1_vare = fdvare
     c     vlagl1_key    chain     vlagl1
     c                   if        not %found or
     c                             vlvtyp <> 'L'
     c                   goto      ny_olinje
     c                   endif
      *
      * Finner salgsenhet 1, og sjekker om samme. Hvis ikke må det regnes om.
      *
     c                   eval      w_senh = *blanks
     c                   eval      w_omrs = 0
     c                   eval      w_omrl = 0
     c                   eval      w_anta = 0
     c                   eval      vvenlr_vare = fdvare
     c                   eval      vvenlr_enhe = *blanks
     c     vvenlr_key    setll     vvenlr
     c     vvenlr_key2   reade     vvenlr
     c                   dow       not %eof
     c                   if        vesaen = 1
     c                   eval      w_omrs = veomre
     c                   eval      w_senh = veenhe
     c                   endif
     c                   if        fdenh1 = veenhe
     c                   eval      w_omrl = veomre
     c                   endif
     c     vvenlr_key2   reade     vvenlr
     c                   enddo
      *
     c                   if        w_omrs = 0
     c                   eval      w_omrs = 1
     c                   endif
     c                   if        w_omrl = 0
     c                   eval      w_omrl = 1
     c                   endif
      *
     c                   eval      w_anta = fdanta
     c                   if        fdenh1 <> w_senh
8.04 c**                 eval      w_anta = w_anta * w_omrs / w_omrl
8.04 c                   eval      w_anta = w_anta * w_omrl / w_omrs
     c                   endif
8.08  *
8.08  * 1. Hvis VVNLEV er 0, bruk VVLDOR. Vi forusetter at dette ikke gjelder
8.08  *    logistikkvarer.
8.08  * 2. Hvis logistikkvare, i JVKHPF, hent JVKLDO og slå opp i JLEVPF. Hvis
8.08  *    reskontronummer er 0, eller ikke treff i RLEV, bruk VVLDOR.
8.08  * 3. Hent vareeier fra JVARPF, hent reskontronummer fra JLEVPF. Hvis
8.08  *    reskontronummer er 0 eller ikke treff i RLEV, bruk VVLDOR.
8.08  * Ref dialog med Jørn
8.08  *
8.08 c                   eval      w_valg = 0
8.08  * Alt 1
8.08 c                   if        vvnlev =  0 and
8.08 c                             vvldor <> 0
8.08 c                   eval      w_valg = 1
8.08 c                   endif
8.08  * Alt 2
8.09 c                   if        w_valg = 0
8.08 c                   eval      jvkhl1_kvnr = vvvare
8.08 c     jvkhl1_key    chain     jvkhl1
8.08 c                   if        %found
8.08 c                   eval      w_valg = 2
8.08 c                   endif
8.09 c                   endif
8.08  * Alt 3
8.09 c                   if        w_valg = 0
8.08 c                   if        vvnlev <> 0
8.09 c                   eval      jlevl1_ldor = vvnlev
8.09 c     jlevl1_key    chain     jlevl1
8.09 c                   if        %found and
8.09 c                             jlenum <> 0
8.08 c                   eval      w_valg = 3
8.09 c                   else
8.09 c                   eval      w_valg = 1
8.09 c                   endif
8.08 c                   endif
8.09 c                   endif
8.08  *
8.08 c                   if        w_valg = 1
8.08 c                   eval      w_ldor = vvldor
8.08 c                   exsr      hent_rlev
8.08 c                   eval      s_ldor = vvldor
8.08 c                   elseif    w_valg = 2
8.08 c                   eval      w_jdor = jvkldo
8.08 c                   exsr      hent_jlev
8.08 c                   elseif    w_valg = 3
8.08 c                   eval      w_jdor = vvnlev
8.08 c                   exsr      hent_jlev
8.08 c                   else
8.08 c                   endif
8.08  *
8.08  * Overstyrt på lager ?
8.08 c                   if        vlbldo <> 0 and
8.08 c                             s_ldor <> vvldor
8.08 c                   eval      w_ldor = vlbldo
8.08 c                   exsr      hent_rlev
8.08 c                   endif
8.14  *
8.14  * For ikke ødelegge øvrig logikk, legges siste sjekk mot RDS
8.14  * leverandør her.
8.14  *
8.14 c                   eval      vla2i1_2var = fdvare
8.14 c                   eval      vla2i1_2lag = fdlage
8.14 c     vla2i1_key    chain     vla2i1
8.14 c                   if        %found and
8.14 c                             vl2gin = 1
8.14 c                   eval      rlevl1_levr = vl2rle
8.14 c     rlevl1_key    chain     rlevl1
8.14 c                   if        %found
8.14 c                   eval      w_ldor = vl2rle
8.14 c                   eval      w_ltxt = rlnavn
8.14 c                   endif
8.14 c                   endif
      *
      * Kan være samme vare er plukket med to ulike prefiks. Denne akkumuleres
      * inn på samme transaksjon, da linjenummer er en del av nøkkel.
      *
     c                   eval      bridiu_dlag = fdlage
     c                   eval      bridiu_dvar = fdvare
8.10 c                   eval      bridiu_divd = %char(fdnumm)+'-'+%char(fdline)
     c     bridiu_key    chain     bridiu
     c                   if        not %found
     c                   clear                   bridiur
     c                   eval      bidmem = bridiu_dmem
     c                   eval      bidvar = bridiu_dvar
     c                   eval      bidlag = bridiu_dlag
     c                   eval      bidivd = bridiu_divd
8.01 c                   eval      bidlid = l_filg + %char(bidlag)
8.11  * Flyttet
      * Hent info fra lager
      *
     c                   eval      ra10l1_jkod = fdlage
     c     ra10l1_key    chain     ra10l1
     c                   if        %found
     c                   eval      bidltx = rajtxt
     c                   endif
      *
     c                   endif
      *
8.01 c                   eval      w_size = w_size + 1
     c                   eval      bidlin = w_size
     c                   time                    bidoda
     c                   time                    bidoti
     c                   eval      bidous = l_user
8.08  *
8.08 c                   eval      bidldo = w_ldor
8.08 c                   eval      bidldt = w_ltxt
      *
     c                   eval      bidtda = w_date
     c                   eval      bidvtx = %trim(fdtek1 + fdtek2)
     c                   eval      bidvar = fdvare
     c                   eval      bidenh = w_senh
     c                   eval      bidkun = fokund
      *
     c                   eval      bidlqt = bidlqt + w_anta
8.10 c**                 eval      bidoqt = bidoqt + w_anta
8.10  *
8.10  * Finn kvantum på samme ordrenummer/vare som ikke er levert
8.10  *
8.10 c                   eval      w_rest = 0
8.10 c                   exsr      calc_rest
8.10 c                   eval      bidoqt = bidlqt + w_rest
      *
     c                   if        not %found(bridiu)
     c                   write     bridiur
     c                   else
     c                   update    bridiur
     c                   endif
     c                   endif
      *
     c     ny_olinje     tag
      *
     c     fodtlr_key2   reade     fodtlr
     c                   enddo
      *
     c                   endif
      *
     c                   enddo
      *
     c     pakk_slutt    endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Finner gårsdagens pakksedler fra bonger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bong_ordre    begsr
      *
      * Nullstiller teller for antall varer funnet
      *
     c                   eval      w_lety = 0
     c                   eval      w_ornr = 0
      *
      * Så finner vi alle pakksedler fra i går via bonger.
      *
     c                   eval      *in15 = *off
      *
     c/exec sql
     c+                  declare bong cursor for
     c+                  select bofirm,
     c+                         boaarr,
     c+                         bobong,
     c+                         bowsid,
     c+                         bodate,
     c+                         bolage,
     c+                         boornr,
     c+                         boorsu,
     c+                         bokund,
     c+                         bolety,
     c+                         bootyp
     c+                  from   bohepf
     c+                  where  bofirm = :w_firm and
     c+                         bodate = :w_date and
     c+                         bolety = :w_lety and
     c+                         boornr = :w_ornr
     c+                  order by bofirm, bobong
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close bong
     c/end-exec
     c/exec sql
     c+                  open bong
     c/end-exec
      *
     c                   dow       *in15 = *off
      *
     c/exec sql
     c+                  fetch next
     c+                  from  bong
     c+                  into  :bofirm,
     c+                        :boaarr,
     c+                        :bobong,
     c+                        :bowsid,
     c+                        :bodate,
     c+                        :bolage,
     c+                        :boornr,
     c+                        :boorsu,
     c+                        :bokund,
     c+                        :bolety,
     c+                        :bootyp
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   if        *in15
     c                   leave
     c                   endif
      *
      * Ikke kreditordre, ref Gisle Åsberg
      *
     c                   eval      b_ordr = *off
     c                   eval      votyl1_otyp = bootyp
     c     votyl1_key    chain     votyl1
     c                   if        %found
     c                   if        vaokre <> '-'
     c                   eval      b_ordr = *on
     c                   endif
     c                   endif
      *
     c                   if        b_ordr = *on
      *
      * Les bonglinjer
      *
     c                   eval      bodtl1_aarr = boaarr
     c                   eval      bodtl1_wsid = bowsid
     c                   eval      bodtl1_bong = bobong
     c                   eval      bodtl1_line = 0
     c     bodtl1_key    setll     bodtl1
     c     bodtl1_key2   reade     bodtl1
     c                   dow       not %eof
      *
      * Ikke tekstlinjer eller bonger med ordrenummer,(disse fanges opp
      * i FOHE/FODT), eller linjer med kvantum 0.
      *
     c                   if        %trim(bdvare) <> *blanks and
     c                             bdornr = 0 and
     c                             bdanta <> 0
      *
      * Sjekk om det er EVR vare
      *
     c                   eval      vvarl1_vare = bdvare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   goto      ny_blinje
     c                   endif
      *
      * Sjekk om det er lagervare
      *
     c                   eval      vlagl1_lage = bdlage
     c                   eval      vlagl1_vare = bdvare
     c     vlagl1_key    chain     vlagl1
     c                   if        not %found or
     c                             vlvtyp <> 'L'
     c                   goto      ny_blinje
     c                   endif
      *
      * Finner salgsenhet 1, og sjekker om samme. Hvis ikke må det regnes om.
      *
     c                   eval      w_senh = *blanks
     c                   eval      w_omrs = 0
     c                   eval      w_omrl = 0
     c                   eval      w_anta = 0
     c                   eval      vvenlr_vare = bdvare
     c                   eval      vvenlr_enhe = *blanks
     c     vvenlr_key    setll     vvenlr
     c     vvenlr_key2   reade     vvenlr
     c                   dow       not %eof
     c                   if        vesaen = 1
     c                   eval      w_omrs = veomre
     c                   eval      w_senh = veenhe
     c                   endif
     c                   if        bdenh1 = veenhe
     c                   eval      w_omrl = veomre
     c                   endif
     c     vvenlr_key2   reade     vvenlr
     c                   enddo
      *
     c                   if        w_omrs = 0
     c                   eval      w_omrs = 1
     c                   endif
     c                   if        w_omrl = 0
     c                   eval      w_omrl = 1
     c                   endif
      *
     c                   eval      w_anta = bdanta
     c                   if        bdenh1 <> w_senh
8.12 c**                 eval      w_anta = w_anta * w_omrs / w_omrl
8.12 c                   eval      w_anta = w_anta * w_omrl / w_omrs
     c                   endif
      *
      *
      * Så legger vi ut salgsinfo, som brukes i neste program (NM724R)
      *
     c                   eval      bridiu_dlag = bdlage
     c                   eval      bridiu_dvar = bdvare
     c                   eval      bridiu_divd = %char(bobong)
     c     bridiu_key    chain     bridiu
     c                   if        not %found
     c                   clear                   bridiur
     c                   eval      bidmem = bridiu_dmem
8.01 c                   eval      w_size = w_size + 1
8.01 c                   eval      bidlin = w_size
     c                   eval      bidlag = bridiu_dlag
8.01 c                   eval      bidlid = l_filg + %char(bidlag)
     c                   eval      bidvar = bridiu_dvar
     c                   eval      bidivd = bridiu_divd
     c                   time                    bidoda
     c                   time                    bidoti
     c                   eval      bidous = l_user
      *
      * Hent info fra lager
      *
     c                   eval      ra10l1_jkod = bdlage
     c     ra10l1_key    chain     ra10l1
     c                   if        %found
     c                   eval      bidltx = rajtxt
     c                   endif
      *
      * Hent info fra leverandør
      *
8.07  * Endret logikk til å bruke reskontronummer på leverandøren uansett.
8.07 c                   if        vvldor <> 0
8.06 c                   eval      rlevl1_levr = vvldor
8.06 c     rlevl1_key    chain     rlevl1
8.06 c                   if        %found
8.06 c                   eval      bidldo = vvldor
8.06 c                   eval      bidldt = rlnavn
8.06 c                   endif
      *
8.06 c                   if        vlbldo <> 0 and
8.06 c                             vlbldo <> vvldor
8.06 c                   eval      rlevl1_levr = vlbldo
8.06 c     rlevl1_key    chain     rlevl1
8.06 c                   if        %found
8.06 c                   eval      bidldo = vlbldo
8.06 c                   eval      bidldt = rlnavn
8.06 c                   endif
8.06 c                   endif
8.14  *
8.14  * For ikke ødelegge øvrig logikk, legges siste sjekk mot RDS
8.14  * leverandør her.
8.14  *
8.14 c                   eval      vla2i1_2var = bdvare
8.14 c                   eval      vla2i1_2lag = bdlage
8.14 c     vla2i1_key    chain     vla2i1
8.14 c                   if        %found and
8.14 c                             vl2gin = 1
8.14 c                   eval      rlevl1_levr = vl2rle
8.14 c     rlevl1_key    chain     rlevl1
8.14 c                   if        %found
8.14 c                   eval      bidldo = vl2rle
8.14 c                   eval      bidldt = rlnavn
8.14 c                   endif
8.14 c                   endif
8.07  * Leser primært reskontroleverandør.
8.07 c                   else
     c                   if        vvnlev <> 0
     c                   eval      jlevl1_ldor = vvnlev
     c     jlevl1_key    chain     jlevl1
     c                   if        %found
8.06 c**                 eval      bidldo = vvnlev
8.06 c                   eval      bidldo = jlenum
     c                   eval      bidldt = jlnavn
     c                   endif
      *
8.05 c                   if        vlbldo <> 0 and
8.05 c                             vlbldo <> jlenum
8.05 c                   eval      jlevl3_enum = vlbldo
8.05 c     jlevl3_key    chain     jlevl3
8.05 c                   if        %found
8.06 c**                 eval      bidldo = jlldor
8.06 c                   eval      bidldo = jlenum
8.05 c                   eval      bidldt = jlnavn
8.05 c                   endif
8.05 c                   endif
      *
     c                   endif
      *
8.07 c                   endif
      *
     c                   eval      bidtda = w_date
     c                   eval      bidvtx = %trim(bdtek1 + bdtek2)
     c                   eval      bidvar = bdvare
     c                   eval      bidenh = w_senh
     c                   eval      bidkun = bokund
      *
     c                   endif
      *
     c                   eval      bidlqt = bidlqt + w_anta
     c                   eval      bidoqt = bidoqt + w_anta
      *
     c                   if        not %found(bridiu)
     c                   write     bridiur
     c                   else
     c                   update    bridiur
     c                   endif
      *
     c                   endif
      *
     c     ny_blinje     tag
      *
     c     bodtl1_key2   reade     bodtl1
     c                   enddo
      *
     c                   endif
      *
     c                   enddo
      *
     c     bong_slutt    endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Finner gårsdagens salg
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     fakt_ordre    begsr
      *
      * Nullstiller teller for antall varer funnet
      *
     c                   eval      w_lety = 0
      *
      * Så finner vi alle ordre med pakkseddeldato fra i går.
      *
     c                   eval      *in15 = *off
      *
     c/exec sql
     c+                  declare fakt cursor for
     c+                  select sofirm,
     c+                         soaarr,
     c+                         sobinr,
     c+                         sonumm,
     c+                         sosuff,
     c+                         solety,
     c+                         sopada,
     c+                         sokund,
     c+                         sootyp
     c+                  from   sohepf
     c+                  where  sofirm = :w_firm and
     c+                         sopada = :w_date and
     c+                         solety = :w_lety
     c+                  order by sofirm, soaarr, sobinr, sonumm, sosuff
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close fakt
     c/end-exec
     c/exec sql
     c+                  open fakt
     c/end-exec
      *
     c                   dow       *in15 = *off
      *
     c/exec sql
     c+                  fetch next
     c+                  from  fakt
     c+                  into  :sofirm,
     c+                        :soaarr,
     c+                        :sobinr,
     c+                        :sonumm,
     c+                        :sosuff,
     c+                        :solety,
     c+                        :sopada,
     c+                        :sokund,
     c+                        :sootyp
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   if        *in15
     c                   leave
     c                   endif
      *
      * Ikke kreditordre, ref Gisle Åsberg
      *
     c                   eval      b_ordr = *off
     c                   eval      votyl1_otyp = sootyp
     c     votyl1_key    chain     votyl1
     c                   if        %found
     c                   if        vaokre <> '-'
     c                   eval      b_ordr = *on
     c                   endif
     c                   endif
      *
     c                   if        b_ordr = *on
      *
      * Les fakturahsitorikk
      *
     c                   eval      sodtlr_aarr = soaarr
     c                   eval      sodtlr_binr = sobinr
     c                   eval      sodtlr_numm = sonumm
     c                   eval      sodtlr_suff = sosuff
     c                   eval      sodtlr_ktxt = 0
     c                   eval      sodtlr_line = 0
     c     sodtlr_key    setll     sodtlr
     c     sodtlr_key2   reade     sodtlr
     c                   dow       not %eof
      * Ikke tekstlinjer eller linjer med 0 i kvantum.
     c                   if        %trim(sdvare) <> *blanks and
     c                             sdanta <> 0
      *
      * Sjekk om det er EVR vare
      *
     c                   eval      vvarl1_vare = sdvare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   goto      ny_slinje
     c                   endif
      *
      * Sjekk om det er lagervare
      *
     c                   eval      vlagl1_lage = sdlage
     c                   eval      vlagl1_vare = sdvare
     c     vlagl1_key    chain     vlagl1
     c                   if        not %found or
     c                             vlvtyp <> 'L'
     c                   goto      ny_slinje
     c                   endif
      *
      * Finner salgsenhet 1, og sjekker om samme. Hvis ikke må det regnes om.
      *
     c                   eval      w_senh = *blanks
     c                   eval      w_omrs = 0
     c                   eval      w_omrl = 0
     c                   eval      w_anta = 0
     c                   eval      vvenlr_vare = sdvare
     c                   eval      vvenlr_enhe = *blanks
     c     vvenlr_key    setll     vvenlr
     c     vvenlr_key2   reade     vvenlr
     c                   dow       not %eof
     c                   if        vesaen = 1
     c                   eval      w_omrs = veomre
     c                   eval      w_senh = veenhe
     c                   endif
8.13 c                   if        sdenh1 = veenhe
     c                   eval      w_omrl = veomre
     c                   endif
     c     vvenlr_key2   reade     vvenlr
     c                   enddo
      *
     c                   if        w_omrs = 0
     c                   eval      w_omrs = 1
     c                   endif
     c                   if        w_omrl = 0
     c                   eval      w_omrl = 1
     c                   endif
      *
8.13 c                   eval      w_anta = sdanta
     c                   if        sdenh1 <> w_senh
     c                   eval      w_anta = w_anta * w_omrs / w_omrl
     c                   endif
      *
      * Så legger vi ut salgsinfo, som brukes i neste program (NM724R)
      *
     c                   eval      bridiu_dlag = sdlage
     c                   eval      bridiu_dvar = sdvare
     c                   eval      bridiu_divd = %char(sobinr) + '/' +
     c                                           %char(sdnumm)
     c     bridiu_key    chain     bridiu
     c                   if        not %found
     c                   clear                   bridiur
     c                   eval      bidmem = bridiu_dmem
8.01 c                   eval      w_size = w_size + 1
8.01 c                   eval      bidlin = w_size
     c                   eval      bidlag = bridiu_dlag
8.01 c                   eval      bidlid = l_filg + %char(bidlag)
     c                   eval      bidvar = bridiu_dvar
     c                   eval      bidivd = bridiu_divd
     c                   time                    bidoda
     c                   time                    bidoti
     c                   eval      bidous = l_user
      *
      * Hent info fra lager
      *
     c                   eval      ra10l1_jkod = sdlage
     c     ra10l1_key    chain     ra10l1
     c                   if        %found
     c                   eval      bidltx = rajtxt
     c                   endif
      *
      * Hent info fra leverandør
      *
8.07  * Endret logikk til å bruke reskontronummer på leverandøren
8.07 c                   if        vvldor <> 0
8.06 c                   eval      rlevl1_levr = vvldor
8.06 c     rlevl1_key    chain     rlevl1
8.06 c                   if        %found
8.06 c                   eval      bidldo = vvldor
8.06 c                   eval      bidldt = rlnavn
8.06 c                   endif
      *
8.06 c                   if        vlbldo <> 0 and
8.06 c                             vlbldo <> vvldor
8.06 c                   eval      rlevl1_levr = vlbldo
8.06 c     rlevl1_key    chain     rlevl1
8.06 c                   if        %found
8.06 c                   eval      bidldo = vlbldo
8.06 c                   eval      bidldt = rlnavn
8.06 c                   endif
8.06 c                   endif
8.14  *
8.14  * For ikke ødelegge øvrig logikk, legges siste sjekk mot RDS
8.14  * leverandør her.
8.14  *
8.14 c                   eval      vla2i1_2var = sdvare
8.14 c                   eval      vla2i1_2lag = sdlage
8.14 c     vla2i1_key    chain     vla2i1
8.14 c                   if        %found and
8.14 c                             vl2gin = 1
8.14 c                   eval      rlevl1_levr = vl2rle
8.14 c     rlevl1_key    chain     rlevl1
8.14 c                   if        %found
8.14 c                   eval      bidldo = vl2rle
8.14 c                   eval      bidldt = rlnavn
8.14 c                   endif
8.14 c                   endif
8.07  * Så hvis det er prisgiver ?
8.07 c                   else
     c                   if        vvnlev <> 0
     c                   eval      jlevl1_ldor = vvnlev
     c     jlevl1_key    chain     jlevl1
     c                   if        %found
8.06 c**                 eval      bidldo = vvnlev
8.06 c                   eval      bidldo = jlenum
     c                   eval      bidldt = jlnavn
     c                   endif
      *
8.05 c                   if        vlbldo <> 0 and
8.05 c                             vlbldo <> jlenum
8.05 c                   eval      jlevl3_enum = vlbldo
8.05 c     jlevl3_key    chain     jlevl3
8.05 c                   if        %found
8.06 c**                 eval      bidldo = jlldor
8.06 c                   eval      bidldo = jlenum
8.05 c                   eval      bidldt = jlnavn
8.05 c                   endif
8.05 c                   endif
      *
     c                   endif
      *
8.07 c                   endif
      *
     c                   eval      bidtda = w_date
     c                   eval      bidvtx = %trim(sdtek1 + sdtek2)
     c                   eval      bidvar = sdvare
     c                   eval      bidenh = w_senh
     c                   eval      bidkun = sokund
      *
     c                   endif
      *
     c                   eval      bidlqt = bidlqt + w_anta
     c                   eval      bidoqt = bidoqt + w_anta
      *
     c                   if        not %found(bridiu)
     c                   write     bridiur
     c                   else
     c                   update    bridiur
     c                   endif
      *
     c                   endif
      *
     c     ny_slinje     tag
      *
     c     sodtlr_key2   reade     sodtlr
     c                   enddo
      *
     c                   endif
      *
     c                   enddo
      *
     c     fakt_slutt    endsr
      *
8.15  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.15  * Finner gårsdagens Malproff salg fra VHISPF
8.15  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.15  *
8.15 c     fakt_malproff begsr
8.15  *
8.15  * Nullstiller teller for antall varer funnet
8.15  *
8.15 c                   eval      w_mkod = 'MP'
8.15  *
8.15  * Så finner vi alle lagertransaksjoner som er malpross salg
8.15  *
8.15 c                   eval      *in15 = *off
8.15  *
8.15 c/exec sql
8.15 c+                  declare malproff cursor for
8.15 c+                  select vhfirm,
8.15 c+                         vhvare,
8.15 c+                         vhlage,
8.15 c+                         vhdato,
8.15 c+                         vhtime,
8.15 c+                         vhsekv,
8.15 c+                         vhuser,
8.15 c+                         vhsyst,
8.15 c+                         vhkode,
8.15 c+                         vhtype,
8.15 c+                         vhrefr,
8.15 c+                         vhonum,
8.15 c+                         vholin,
8.15 c+                         vhbanr,
8.15 c+                         vhbehl,
8.15 c+                         vhanta,
8.15 c+                         vhenhl,
8.15 c+                         vhkpri,
8.15 c+                         vhodat,
8.15 c+                         vhotim,
8.15 c+                         vhousr,
8.15 c+                         vhedat,
8.15 c+                         vhetim,
8.15 c+                         vheusr
8.15 c+                  from   vhispf
8.15 c+                  where  vhfirm = :w_firm and
8.15 c+                         vhdato = :w_date and
8.15 c+                         vhkode = :w_mkod
8.15 c+                  order by vhfirm, vhvare, vhlage
8.15 c+                  for read only
8.15 c/end-exec
8.15 c/exec sql
8.15 c+                  close malproff
8.15 c/end-exec
8.15 c/exec sql
8.15 c+                  open malproff
8.15 c/end-exec
8.15  *
8.15 c                   dow       *in15 = *off
8.15  *
8.15 c/exec sql
8.15 c+                  fetch next
8.15 c+                  from  malproff
8.15 c+                  into  :vhfirm,
8.15 c+                        :vhvare,
8.15 c+                        :vhlage,
8.15 c+                        :vhdato,
8.15 c+                        :vhtime,
8.15 c+                        :vhsekv,
8.15 c+                        :vhuser,
8.15 c+                        :vhsyst,
8.15 c+                        :vhkode,
8.15 c+                        :vhtype,
8.15 c+                        :vhrefr,
8.15 c+                        :vhonum,
8.15 c+                        :vholin,
8.15 c+                        :vhbanr,
8.15 c+                        :vhbehl,
8.15 c+                        :vhanta,
8.15 c+                        :vhenhl,
8.15 c+                        :vhkpri,
8.15 c+                        :vhodat,
8.15 c+                        :vhotim,
8.15 c+                        :vhousr,
8.15 c+                        :vhedat,
8.15 c+                        :vhetim,
8.15 c+                        :vheusr
8.15 c/end-exec
8.15 c                   if        sqlcod = 100 or sqlstt = '02000'
8.15 c                   eval      *in15 = *on
8.15 c                   endif
8.15  *
8.15 c                   if        *in15
8.15 c                   leave
8.15 c                   endif
8.15  *
8.15 c                   if        %trim(vhvare) <> *blanks
8.15  *
8.15  * Sjekk om det er EVR vare
8.15  *
8.15 c                   eval      vvarl1_vare = vhvare
8.15 c     vvarl1_key    chain     vvarl1
8.15 c                   if        not %found
8.15 c                   goto      ny_mlinje
8.15 c                   endif
8.15  *
8.15  * Sjekk om det er lagervare
8.15  *
8.15 c                   eval      vlagl1_lage = vhlage
8.15 c                   eval      vlagl1_vare = vhvare
8.15 c     vlagl1_key    chain     vlagl1
8.15 c                   if        not %found or
8.15 c                             vlvtyp <> 'L'
8.15 c                   goto      ny_mlinje
8.15 c                   endif
8.15  *
8.15  * Finner salgsenhet 1, og sjekker om samme. Hvis ikke må det regnes om.
8.15  *
8.15 c                   eval      w_senh = *blanks
8.15 c                   eval      w_omrs = 0
8.15 c                   eval      w_omrl = 0
8.15 c                   eval      w_anta = 0
8.15 c                   eval      vvenlr_vare = vhvare
8.15 c                   eval      vvenlr_enhe = *blanks
8.15 c     vvenlr_key    setll     vvenlr
8.15 c     vvenlr_key2   reade     vvenlr
8.15 c                   dow       not %eof
8.15 c                   if        vesaen = 1
8.15 c                   eval      w_omrs = veomre
8.15 c                   eval      w_senh = veenhe
8.15 c                   endif
8.15 c                   if        vhenhl = veenhe
8.15 c                   eval      w_omrl = veomre
8.15 c                   endif
8.15 c     vvenlr_key2   reade     vvenlr
8.15 c                   enddo
8.15  *
8.15 c                   if        w_omrs = 0
8.15 c                   eval      w_omrs = 1
8.15 c                   endif
8.15 c                   if        w_omrl = 0
8.15 c                   eval      w_omrl = 1
8.15 c                   endif
8.15  *
8.15 c                   eval      w_anta = vhanta
8.15 c                   if        vhenhl <> w_senh
8.15 c                   eval      w_anta = w_anta * w_omrl / w_omrs
8.15 c                   endif
8.15  *
8.15  * Så legger vi ut salgsinfo
8.15  *
8.15 c                   eval      bridiu_dlag = vhlage
8.15 c                   eval      bridiu_dvar = vhvare
8.15 c                   eval      bridiu_divd = %trim(vhrefr)
8.15 c     bridiu_key    chain     bridiu
8.15 c                   if        not %found
8.15 c                   clear                   bridiur
8.15 c                   eval      bidmem = bridiu_dmem
8.15 c                   eval      w_size = w_size + 1
8.15 c                   eval      bidlin = w_size
8.15 c                   eval      bidlag = bridiu_dlag
8.15 c                   eval      bidlid = l_filg + %char(vhlage)
8.15 c                   eval      bidvar = bridiu_dvar
8.15 c                   eval      bidivd = bridiu_divd
8.15 c                   time                    bidoda
8 15 c                   time                    bidoti
8.15 c                   eval      bidous = l_user
8.15  *
8.15  * Hent info fra lager
8.15  *
8.15 c                   eval      ra10l1_jkod = vhlage
8.15 c     ra10l1_key    chain     ra10l1
8.15 c                   if        %found
8.15 c                   eval      bidltx = rajtxt
8.15 c                   endif
8.15  *
8.15  * Hent info fra leverandør
8.15  *
8.15  * Endret logikk til å bruke reskontronummer på leverandøren uansett.
8.15 c                   if        vvldor <> 0
8.15 c                   eval      rlevl1_levr = vvldor
8.15 c     rlevl1_key    chain     rlevl1
8.15 c                   if        %found
8.15 c                   eval      bidldo = vvldor
8.15 c                   eval      bidldt = rlnavn
8.15 c                   endif
8.15  *
8.15 c                   if        vlbldo <> 0 and
8.15 c                             vlbldo <> vvldor
8.15 c                   eval      rlevl1_levr = vlbldo
8.15 c     rlevl1_key    chain     rlevl1
8.15 c                   if        %found
8.15 c                   eval      bidldo = vlbldo
8.15 c                   eval      bidldt = rlnavn
8.15 c                   endif
8.15 c                   endif
8.15  *
8.15  * For ikke ødelegge øvrig logikk, legges siste sjekk mot RDS
8.15  * leverandør her.
8.15  *
8.15 c                   eval      vla2i1_2var = vhvare
8.15 c                   eval      vla2i1_2lag = vhlage
8.15 c     vla2i1_key    chain     vla2i1
8.15 c                   if        %found and
8.15 c                             vl2gin = 1
8.15 c                   eval      rlevl1_levr = vl2rle
8.15 c     rlevl1_key    chain     rlevl1
8.15 c                   if        %found
8.15 c                   eval      bidldo = vl2rle
8.15 c                   eval      bidldt = rlnavn
8.15 c                   endif
8.15 c                   endif
8.15  * Leser primært reskontroleverandør.
8.15 c                   else
8.15 c                   if        vvnlev <> 0
8.15 c                   eval      jlevl1_ldor = vvnlev
8.15 c     jlevl1_key    chain     jlevl1
8.15 c                   if        %found
8.15 c                   eval      bidldo = jlenum
8.15 c                   eval      bidldt = jlnavn
8.15 c                   endif
8.15  *
8.15 c                   if        vlbldo <> 0 and
8.15 c                             vlbldo <> jlenum
8.15 c                   eval      jlevl3_enum = vlbldo
8.15 c     jlevl3_key    chain     jlevl3
8.15 c                   if        %found
8.16 c                   eval      bidldo = jlenum
8.15 c                   eval      bidldt = jlnavn
8.15 c                   endif
8.15 c                   endif
8.15  *
8.15 c                   endif
8.15  *
8.15 c**                 endif
8.15  *
8.15 c                   eval      bidtda = w_date
8.15 c                   eval      bidvtx = %trim(bdtek1 + bdtek2)
8.15 c                   eval      bidvar = vhvare
8.15 c                   eval      bidenh = w_senh
8.15 c                   eval      bidkun = 59997
8.15  *
8.15 c                   endif
8.15  *
8.15 c                   eval      bidlqt = bidlqt + w_anta
8.15 c                   eval      bidoqt = bidoqt + w_anta
8.15  *
8.15 c                   if        not %found(bridiu)
8.15 c                   write     bridiur
8.15 c                   else
8.15 c                   update    bridiur
8.15 c                   endif
8.15  *
8.15 c                   endif
8.15  *
8.15 c     ny_mlinje     tag
8.15  *
8.15 c                   endif
8.15  *
8.15 c                   enddo
8.15  *
8.15 c                   endsr
8.15  *
8.08  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.08  * Henter leverandørinformasjon
8.08  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.08  *
8.08 c     hent_rlev     begsr
8.08  *
8.08  * Slå opp mot leveradør for å hente inn land/valutakode
8.08  *
8.08 c                   eval      rlevl1_levr = w_ldor
8.08 c     rlevl1_key    chain     rlevl1
8.08 c                   if        %found
8.08 c                   eval      w_ltxt = rlnavn
8.08 c                   endif
8.08  *
8.08 c                   endsr
8.08  *
8.08  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.08  * Henter leverandørinformasjon via JLEVPF - NOBB vareeier
8.08  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.08  *
8.08 c     hent_jlev     begsr
8.08  *
8.08 c                   eval      jlevl1_ldor = w_jdor
8.08 c     jlevl1_key    chain     jlevl1
8.08 c                   if        %found
8.08 c                   eval      w_ldor = jlenum
8.08 c                   eval      s_ldor = jlenum
8.08 c                   eval      w_ltxt = jlnavn
8.08 c                   endif
8.08  *
8.08 c                   endsr
8.08  *
8.10  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.10  * Kalkulerer evnt rest som ikke er levert
8.10  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.10  *
8.10 c     calc_rest     begsr
8.10  *
8.10  * Leser kun pakkseddel som er levert
8.10  *
8.10 c                   eval      fodtlv_vare = fdvare
8.10 c                   eval      fodtlv_numm = s_numm
8.10 c                   eval      fodtlv_suff = 0
8.10 c                   eval      fodtlv_enh1 = *blanks
8.10 c     fodtlv_key    setll     fodtlv
8.10 c     fodtlv_key2   reade     fodtlv
8.10 c                   dow       not %eof
8.10  *
8.10 c                   if        s_suff <> xysuff
8.10  *
8.10 c                   eval      votyl1_otyp = footyp
8.10 c     votyl1_key    chain     votyl1
8.10 c                   if        %found and
8.10 c                             vaoakk = 1
8.10 c                   eval      w_rest = xyanta
8.10 c                   if        xyenh1 <> w_senh
8.04 c                   eval      w_rest = w_rest * w_omrl / w_omrs
8.10 c                   endif
8.10 c                   endif
8.10  *
8.10 c                   endif
8.10  *
8.10 c     fodtlv_key2   reade     fodtlv
8.10 c                   enddo
8.10  *
8.10 c                   endsr
8.10  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ERROR-rutine  - Brukes ved mislykket web-service kall
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *pssr         begsr
      *
     c**                 eval      p_link = 'Blueridge eksport feilet'
     c                   goto      avslutt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Henter inn parameter
      *
     c     *entry        plist
     c                   parm                    p_memb
      *
      * Key for les på ordrelinje
      *
     c     fodtlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtlr_numm
     c                   kfld                    fodtlr_suff
     c                   kfld                    fodtlr_line
      *
      * Key for les på ordrelinje
      *
     c     fodtlr_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    fodtlr_numm
8.10 c                   kfld                    fodtlr_suff
      *
      * Key for les på fakturahistorikk
      *
     c     sodtlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sodtlr_aarr
     c                   kfld                    sodtlr_binr
     c                   kfld                    sodtlr_numm
     c                   kfld                    sodtlr_suff
     c                   kfld                    sodtlr_ktxt
     c                   kfld                    sodtlr_line
      *
      * Key for les på ordrelinje
      *
     c     sodtlr_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    sodtlr_aarr
     c                   kfld                    sodtlr_binr
     c                   kfld                    sodtlr_numm
     c                   kfld                    sodtlr_suff
      *
      * Key for info fra enhet
      *
     c     vvenlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenlr_vare
     c                   kfld                    vvenlr_enhe
      *
      * Key for info fra enhet
      *
     c     vvenlr_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvenlr_vare
      *
      * Key for les på ordretype
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for les på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for les på lager
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl1_vare
     c                   kfld                    vlagl1_lage
      *
      * Key for info fra blueridge export
      *
     c     bridiu_key    klist
     c                   kfld                    bridiu_dmem
     c                   kfld                    bridiu_dvar
     c                   kfld                    bridiu_dlag
     c                   kfld                    bridiu_divd
      *
      * Key for les på lager
      *
     c     ra10l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra10l1_jkod
      *
      * Key for info fra leverandørregister (prisgriver)
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
8.03  *
8.03  * Key for info fra leverandørregister (prisgriver)
8.03  *
8.03 c     jlevl3_key    klist
8.03 c                   kfld                    jlevl3_enum
8.06  *
8.06  * Key for info fra leverandørregister (prisgriver)
8.06  *
8.06 c     rlevl1_key    klist
8.06 c                   kfld                    w_firm
8.06 c                   kfld                    rlevl1_levr
      *
      * Key for les på bonglinjer
      *
     c     bodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bodtl1_aarr
     c                   kfld                    bodtl1_wsid
     c                   kfld                    bodtl1_bong
     c                   kfld                    bodtl1_line
      *
      * Key for les på bonglinjer
      *
     c     bodtl1_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    bodtl1_aarr
     c                   kfld                    bodtl1_wsid
     c                   kfld                    bodtl1_bong
8.08  *
8.08  * Key for info fra logistikkvarenummer
8.08  *
8.08 c     jvkhl1_key    klist
8.08 c                   kfld                    w_firm
8.08 c                   kfld                    jvkhl1_kvnr
8.10  *
8.10  * Key for les på ordrelinje
8.10  *
8.10 c     fodtlv_key    klist
8.10 c                   kfld                    w_firm
8.10 c                   kfld                    fodtlv_vare
8.10 c                   kfld                    fodtlv_numm
8.10 c                   kfld                    fodtlv_suff
8.10 c                   kfld                    fodtlv_enh1
8.10  *
8.10  * Key for les på ordrelinje
8.10  *
8.10 c     fodtlv_key2   klist
8.10 c                   kfld                    w_firm
8.10 c                   kfld                    fodtlv_vare
8.10 c                   kfld                    fodtlv_numm
8.14  *
8.14  * Key for les på lagerregister - tilleggstabell
8.14  *
8.14 c     vla2i1_key    klist
8.14 c                   kfld                    w_firm
8.14 c                   kfld                    vla2i1_2var
8.14 c                   kfld                    vla2i1_2lag
      *
     c                   eval      w_firm = l_firm
      *
      * Beregn datoen vi skal salget
      *
8.02 c                   time                    w_date
8.02 c**   w_date        subdur    2:*d          w_date
      *
     c                   endsr
      *
