     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RF031R
      * Beskrivelse . . : Fordelingsparametre, vedlikehold - underrecord
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       071102 KOB  Nytt program
      * V5.10 080703 KOB  Kode på underrecord ble ikke lagt ut
      * V5.31 010405 KOB  Lagt inn firma i call til RH500R
      * V5.40 060307 KOB  Feil %-sats ble lagt ut i skjermbildet
6.10  * V6.10 210909 BSA  Lagt inn sporingsfelter for fil RFORFP
7.xx  * 7.xx  100516 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frforl3    if   e           k disk    rename(rforpfr:rforl3r)
     frforlr    if   e           k disk    rename(rforpfr:rforlrr)
     frforlu    uf a e           k disk    rename(rforpfr:rforlur)
      *
     frf031d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d rforl3_kont     s                   like(rfkont)
     d rforl3_spes     s                   like(rfspes)
     d rforl3_avde     s                   like(rfavde)
     d rforl3_linj     s                   like(rflinj)
     d rforlr_kont     s                   like(rfkont)
     d rforlr_spes     s                   like(rfspes)
     d rforlr_avde     s                   like(rfavde)
     d rforlr_linj     s                   like(rflinj)
     d rforlu_kont     s                   like(rfkont)
     d rforlu_spes     s                   like(rfspes)
     d rforlu_avde     s                   like(rfavde)
     d rforlu_linj     s                   like(rflinj)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(rffirm)
      *
     d p_stat          s              1
     d p_firm          s                   like(rffirm)
     d p_kode          s                   like(rfkode)
     d p_kont          s                   like(rfkont)
     d p_spes          s                   like(rfspes)
     d p_avde          s                   like(rfavde)
     d p_text          s             30
     d p_bkod          s                   like(rfspes)
     d p_gkod          s                   like(rfavde)
     d p_btxt          s             20
     d p_gtxt          s             20
      *
     d s_linj          s                   like(rflinj)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2linj <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      rforl3_kont = b2kont
     c                   eval      rforl3_spes = b2spes
     c                   eval      rforl3_avde = b2avde
     c                   eval      rforl3_linj = b1linj
     c     rforl3_key    setll     rforl3
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
     c                   if        b2linj <> 0
     c                   eval      rforl3_kont = b2kont
     c                   eval      rforl3_spes = b2spes
     c                   eval      rforl3_avde = b2avde
     c                   eval      rforl3_linj = b2linj
     c     rforl3_key    setll     rforl3
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2linj = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1linj = b1linj
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1linj = b1linj
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1linj = b1linj
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1linj = b1linj
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1linj = b2linj
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1linj = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Validering
      *
     c***                if        c1linj = 0
     c***                eval      *in31  = *on
     c***                goto      c1taga
     c***                endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      rforlr_kont = b2kont
     c                   eval      rforlr_spes = b2spes
     c                   eval      rforlr_avde = b2avde
     c                   eval      rforlr_linj = c1linj
     c     rforlr_key    chain     rforlr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c2linj = c1linj
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1linj = 0
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2linj = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      rforlr_kont = b2kont
     c                   eval      rforlr_spes = b2spes
     c                   eval      rforlr_avde = b2avde
     c                   eval      rforlr_linj = c1linj
     c     rforlr_key    chain     rforlr
      *
     c                   if        %found
     c                   eval      c2linj = rflinj
     c                   eval      c2prot = rfprot
     c                   eval      c2kto1 = rfkto1
     c                   eval      c2sps1 = rfsps1
     c                   eval      c2avd1 = rfavd1
     c                   eval      c2kto2 = rfkto2
     c                   eval      c2sps2 = rfsps2
     c                   eval      c2avd2 = rfavd2
     c                   eval      c2eusr = rfeusr
     c     *dmy          move      rfedat        c2edat
      *
      * Hente inn tekst fra kontoplan
      *
     c                   eval      p_kont = rfkto1
     c                   eval      p_spes = rfsps1
     c                   eval      p_avde = rfavd1
      *
     c                   call      'RS740R'
     c                   parm                    p_stat
S5.00c*                  parm                    p_firm
     c                   parm                    p_kont
     c                   parm                    p_spes
     c                   parm                    p_avde
     c                   parm                    p_text
      *                                                                 .
     c                   if        p_stat = *blank                                           .
     c                   eval      c2txt1 = p_text                                           .
     c                   endif                                                               .
      *
     c                   eval      p_kont = rfkto2
     c                   eval      p_spes = rfsps2
     c                   eval      p_avde = rfavd2
      *
     c                   call      'RS740R'
     c                   parm                    p_stat
S5.00c*                  parm                    p_firm
     c                   parm                    p_kont
     c                   parm                    p_spes
     c                   parm                    p_avde
     c                   parm                    p_text
      *                                                                 .
     c                   if        p_stat = *blank                                           .
     c                   eval      c2txt2 = p_text                                           .
     c                   endif                                                               .
      *                                                                 .
     c                   else
     c                   eval      c2prot = 0
     c                   eval      c2kto1 = 0
     c                   eval      c2sps1 = 0
     c                   eval      c2avd1 = 0
     c                   eval      c2kto2 = 0
     c                   eval      c2sps2 = 0
     c                   eval      c2avd2 = 0
     c                   eval      c2eusr = *blank
     c                   eval      c2edat = 0
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1linj = k1linj
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd = *on                                           .....
      *
     c                   if        w_afld = 'C2KTO1'                                         .
      *
     c                   call      'RH500R'                                      konto       .
T400 c                   parm                    w_firm                           info       .
     c                   parm                    p_kont
     c                   parm                    p_spes
     c                   parm                    p_avde
     c                   parm                    p_text
      *
     c                   if        p_kont <> *zero                                           .
     c                   move      p_kont        c2kto1                                      .
     c                   move      p_spes        c2sps1                                      .
     c                   move      p_avde        c2avd1                                      .
     c                   goto      c2taga                                                    .
     c                   endif                                                               .
      *                                                                 .
     c                   endif                                                           .....
      *
     c                   if        w_afld = 'C2SPS1'                                         .
      *
     c                   call      'RA528R'                                                  .
     c                   parm                    w_firm                                      .
     c                   parm                    p_bkod                                      .
     c                   parm                    p_btxt                                      .
      *
     c                   if        p_bkod <> *zero                                           .
     c                   move      p_bkod        c2sps1                                      .
     c                   goto      c2taga                                                    .
     c                   endif                                                               .
      *
     c                   endif                                                               .
      *
     c                   if        w_afld = 'C2AVD1'                                         .
      *
     c                   call      'RA507R'                                                  .
     c                   parm                    w_firm                                      .
     c                   parm                    p_gkod                                      .
     c                   parm                    p_gtxt                                      .
      *
     c                   if        p_gkod <> *zero                                           .
     c                   move      p_gkod        c2avd1                                      .
     c                   goto      c2taga                                                    .
     c                   endif                                                               .
      *
     c                   endif                                                               .
      *
     c                   if        w_afld = 'C2KTO2'                                         .
      *
     c                   call      'RH500R'                                      konto       .
T400 c                   parm                    p_stat                           info       .
     c                   parm                    p_kont
     c                   parm                    p_spes
     c                   parm                    p_avde
     c                   parm                    p_text
      *
     c                   if        p_kont <> *zero                                           .
     c                   move      p_kont        c2kto2                                      .
     c                   move      p_spes        c2sps2                                      .
     c                   move      p_avde        c2avd2                                      .
     c                   goto      c2taga                                                    .
     c                   endif                                                               .
      *                                                                 .
     c                   endif                                                           .....
      *
     c                   if        w_afld = 'C2SPS2'                                         .
      *
     c                   call      'RA528R'                                                  .
     c                   parm                    w_firm                                      .
     c                   parm                    p_bkod                                      .
     c                   parm                    p_btxt                                      .
      *
     c                   if        p_bkod <> *zero                                           .
     c                   move      p_bkod        c2sps2                                      .
     c                   goto      c2taga                                                    .
     c                   endif                                                               .
      *
     c                   endif                                                               .
      *
     c                   if        w_afld = 'C2AVD2'                                         .
      *
     c                   call      'RA507R'                                                  .
     c                   parm                    w_firm                                      .
     c                   parm                    p_gkod                                      .
     c                   parm                    p_gtxt                                      .
      *
     c                   if        p_gkod <> *zero                                           .
     c                   move      p_gkod        c2avd2                                      .
     c                   goto      c2taga                                                    .
     c                   endif                                                               .
      *
     c                   endif                                                               .
      *
     c                   goto      c2taga
     c                   endif
      *
      * Validering av input
      *
     c***                if        c2besk = *blank
     c***                eval      *in31 = *on
     c***                goto      c2taga
     c***                endif
      *
      * Oppdatering
      *
     c                   clear                   rforlur
      *
     c                   eval      rforlu_kont = b2kont
     c                   eval      rforlu_spes = b2spes
     c                   eval      rforlu_avde = b2avde
     c                   eval      rforlu_linj = c1linj
     c     rforlu_key    chain     rforlur
      *
     c                   eval      rfprot = c2prot
     c                   eval      rfkto1 = c2kto1
     c                   eval      rfsps1 = c2sps1
     c                   eval      rfavd1 = c2avd1
     c                   eval      rfkto2 = c2kto2
     c                   eval      rfsps2 = c2sps2
     c                   eval      rfavd2 = c2avd2
V5.10c                   eval      rfkode = p_kode
      *
     c                   time                    rfedat
     c                   time                    rfetim
     c                   eval      rfeusr = l_user
      *
     c                   if        %found
     c                   update    rforlur
     c                   else
     c                   eval      rffirm = w_firm
     c                   eval      rfkont = b2kont
     c                   eval      rfspes = b2spes
     c                   eval      rfavde = b2avde
     c                   eval      rflinj = c1linj
6.10 c                   time                    rfodat
6.10 c                   time                    rfotim
6.10 c                   eval      rfousr = l_user
     c                   write     rforlur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1pros = rfprot
     c                   eval      b1kto1 = rfkto1
     c                   eval      b1sps1 = rfsps1
     c                   eval      b1avd1 = rfavd1
     c                   eval      b1kto2 = rfkto2
     c                   eval      b1sps2 = rfsps2
     c                   eval      b1avd2 = rfavd2
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      rforlr_kont = b2kont
     c                   eval      rforlr_spes = b2spes
     c                   eval      rforlr_avde = b2avde
     c                   eval      rforlr_linj = d1linj
     c     rforlr_key    chain     rforlr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      rforlu_kont = b2kont
     c                   eval      rforlu_spes = b2spes
     c                   eval      rforlu_avde = b2avde
     c                   eval      rforlu_linj = d1linj
     c     rforlu_key    chain     rforlur
      *
     c                   delete    rforlur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_linj = k1linj
      *
      * Fyller kopierings-bildet
      *
     c                   eval      rforlr_kont = b2kont
     c                   eval      rforlr_spes = b2spes
     c                   eval      rforlr_avde = b2avde
     c                   eval      rforlr_linj = k1linj
     c     rforlr_key    chain     rforlr
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1linj = 0
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      rforlr_kont = b2kont
     c                   eval      rforlr_spes = b2spes
     c                   eval      rforlr_avde = b2avde
     c                   eval      rforlr_linj = k1linj
     c     rforlr_key    chain     rforlr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1linj = s_linj
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      rforl3
      *
     c                   if        %eof or
     c                             rffirm <> w_firm or
     c                             rfkont <> b2kont or                                       .
     c                             rfspes <> b2spes or                                       .
     c                             rfavde <> b2avde or                                       .
     c                             rflinj < b1linj                                           .
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      rforl3_kont = rfkont
     c                   eval      rforl3_spes = rfspes
     c                   eval      rforl3_avde = rfavde
      *
     c                   eval      b1valg = 0
     c                   eval      b1linj = rflinj
     c                   eval      b1pros = rfprot
     c                   eval      b1kto1 = rfkto1
     c                   eval      b1sps1 = rfsps1
     c                   eval      b1avd1 = rfavd1
     c                   eval      b1kto2 = rfkto2
     c                   eval      b1sps2 = rfsps2
     c                   eval      b1avd2 = rfavd2
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     rforl3_key    setgt     rforl3
     c                   readp     rforl3
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     rforl3
      *
     c                   if        %eof or
     c                             rffirm <> w_firm or
     c                             rfkont <> b2kont or                                       .
     c                             rfspes <> b2spes or                                       .
     c                             rfavde <> b2avde or                                       .
     c                             rflinj < b1linj                                           .
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   eval      rforl3_kont = rfkont
     c                   eval      rforl3_spes = rfspes
     c                   eval      rforl3_avde = rfavde
      *
     c                   enddo
      *
     c                   if        %eof
     c     rforl3_key    setll     rforl3
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_kode
     c                   parm                    p_kont
     c                   parm                    p_spes
     c                   parm                    p_avde
      *
      * Key for posisjonering på fordelingsparametre
      *
     c     rforl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rforl3_kont
     c                   kfld                    rforl3_spes
     c                   kfld                    rforl3_avde
      *
      * Key for les på fordelingsparametre
      *
     c     rforlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rforlr_kont
     c                   kfld                    rforlr_spes
     c                   kfld                    rforlr_avde
     c                   kfld                    rforlr_linj
      *
      * Key for oppdatering av fordelingsparametre
      *
     c     rforlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rforlu_kont
     c                   kfld                    rforlu_spes
     c                   kfld                    rforlu_avde
     c                   kfld                    rforlu_linj
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
      *
     c                   eval      b2kont = p_kont
     c                   eval      b2spes = p_spes
     c                   eval      b2avde = p_avde
     c                   eval      b2kode = p_kode
      *
      * Hent informasjon til skjermbilde
      *
     c                   eval      rforlr_kont = p_kont
     c                   eval      rforlr_spes = p_spes
     c                   eval      rforlr_avde = p_avde
     c                   eval      rforlr_linj = 0
     c     rforlr_key    setll     rforlr
      *
     c                   if        %found
      *
     c                   eval      b2pros = rfprot
      *
      * Hente inn tekst fra kontoplan
      *
     c                   call      'RS740R'
     c                   parm                    p_stat
     c                   parm                    p_kont
     c                   parm                    p_spes
     c                   parm                    p_avde
     c                   parm                    p_text
      *
     c                   if        p_stat = *blank                                           .
     c                   eval      b2text = p_text                                           .
     c                   endif                                                               .
     c                   endif                                                               .
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      rforl3_kont = b2kont
     c                   eval      rforl3_spes = b2spes
     c                   eval      rforl3_avde = b2avde
     c                   eval      rforl3_linj = 0
     c     rforl3_key    setll     rforl3
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
