     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JL540R
      * Beskrivelse . . : Leverandør, skrivebord/historikk - parameter
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       080103 HKO  Nytt program
      *       141003 HKO  Lagt inn utplukk på at man kun skal se varer
      *                   til leverandører som har varer i EVR.
      *       290405 HKO  Lagt inn utplukk på at man kun skal se
      *                   leverandører med prisendringer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjstslu    if   e           k disk    rename(jstspfr:jstslur)
      *
     fjl540d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variable
      *
     d b_feil          s              1
      *
     d w_firm          s              3  0
     d w_dato          s               d   datfmt(*iso)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Behandler bildet
      *
     c     b1tag         tag
      *
     c                   exfmt     b1bld
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   goto      avslutt
     c                   endif
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1tag
     c                   endif
      *
      * Kaller utskriftsprogram
      *
     c     *dmy          move      b1dato        w_dato
      *
     c                   call      'JL541R'
     c                   parm                    w_firm
     c                   parm                    w_dato
     c                   parm                    b1kevr
     c                   parm                    b1kpri
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
      * Endringsdato
      *
     c                   if        b1dato = 0
     c                   eval      *in31 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c     *dmy          test(d)                 b1dato                 32
     c                   if        *in32 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på kode/status
      *
     c     jstslu_key    klist
     c                   kfld                    w_firm
      *
      *
      * Henter firma fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      b1kevr = 1
     c                   eval      b1kpri = 1
      *
      * Henter dato for siste oppdatering
      *
     c     jstslu_key    chain     jstslu                                       Henter neste
     c                   if        jaaopd <> *loval
     c     *dmy          move      jaaopd        b1aopd
     c                   endif
      *
     c                   endsr
