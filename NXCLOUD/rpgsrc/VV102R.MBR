     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VV102R
      * Beskrivelse . . : Vare, vedlikehold - bilde 2
      *
      * Programmet kalles av VV101R, og inneholder ingen oppdating.
      * Utveksling av verdier skjer via parametre med VV101R.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       200499 HKO  Nytt program
      *       220300 HKO  Lagt inn test på DG
      *       250300 HKO  Endret lengde på volum, nettovekt og bruttovekt
      *                   Lagt inn vedlikehold av abc-kode, høyde, lengde
      *                   og bredde
3.31  * R3.31 060601 AMD  Differensiert moms, 24 og 12 %
      *       191203 HKO  Lagt inn omregningsfaktor til nominell
      *       040805 HKO  Fjernet vedlikehold av lengde, bredde, høyde,
      *                   nettovekt, bruttovekt og volum
      *       231005 HKO  Fjernet vedlikehold av Enhet og Mengde/prosjekt
      *                   Redigert ledetekster
6.11  *       191109 bof  Utvidet med kjedesortiment
6.12  *       060110 bof  Tatt bort alle lager-relaterte felter
7.xx  * 7.00  020216 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvaugl1    if   e           k disk    rename(vaugpfr:vaugl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvkhvl1    if   e           k disk    rename(vkhvpfr:vkhvl1r)
     fvmvalr    if   e           k disk    rename(vmvapfr:vmvalrr)
      *
     fvv102d    cf   e             workstn
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vvfirm)
     d p_valg          s              1  0
     d p_vare          s                   like(vvvare)
      *
     d p_tek1          s                   like(vvtek1)
     d p_kbon          s                   like(vvkbon)
     d p_kord          s                   like(vvkord)
     d p_kenh          s                   like(vvkenh)
     d p_kmva          s                   like(vvkmva)
     d p_kres          s                   like(vvkres)
     d p_kpme          s                   like(vvkpme)
     d p_keti          s                   like(vvketi)
     d p_kpdi          s                   like(vvkpdi)
     d p_kjus          s                   like(vvkjus)
     d p_desi          s                   like(vvdesi)
     d p_kstr          s                   like(vvkstr)
     d p_kabc          s                   like(vvkabc)
     d p_ahgr          s                   like(vvahgr)
     d p_augr          s                   like(vvaugr)
     d p_avar          s                   like(vvavar)
     d p_kfri          s                   like(vvkfri)
     d p_fako          s                   like(vvfako)
     d p_dekn          s                   like(vvdekn)
     d p_omrn          s                   like(vvomrn)
     d p_godj          s                   like(vvgodj)
     d p_khev          s                   like(vvkhev)
6.11 d p_sort          s                   like(vvsort)
      *
      * Definering av Local data area
      *
     d                uds
     d l_fnav                951    980
      *
      * Definering av variable i nøkler
      *
     d vaugl1_uhgr     s                   like(vfuhgr)
     d vaugl1_uugr     s                   like(vfuugr)
     d vvarl1_vare     s                   like(vvvare)
     d vkhvl1_kkod     s                   like(vakkod)
     d vmvalr_mkod     s                   like(vamkod)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(vvfirm)
     d w_utxt          s                   like(vfutxt)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     c2taga        tag
      *
      * Henter beskrivelse av kontohenvisning
      *
     c                   if        c2khev <> *blank
     c                   eval      vkhvl1_kkod = c2khev
     c     vkhvl1_key    chain     vkhvl1
     c                   if        %found
     c                   eval      c2ktxt = vaktxt
     c                   endif
     c                   endif
      *
      * Henter beskrivelse av avgiftskode
      *
     c                   eval      vmvalr_mkod = c2kmva
     c     vmvalr_key    chain     vmvalr
     c                   if        %found
     c                   eval      c2kmtx = vamtxt
     c                   endif
      *
      * Viser bildet
      *
     c                   if        p_valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c                   exfmt     c2bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      c2taga
     c                   endsl
      *
     c                   if        p_valg = 5
     c                   goto      avslutt
     c                   endif
      *
      * Validering av input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      c2taga
     c                   endif
      *
      * Oppdater parametre
      *
     c                   exsr      oppdater
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørringer (F4)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
     c                   if        w_afld = 'C2KMVA'
     c                   call      'VA580R'
     c                   parm                    w_firm
     c                   parm                    c2kmva
     c                   parm                    c2kmtx
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C2KHEV'
     c                   call      'VA520R'
     c                   parm                    w_firm
     c                   parm                    c2khev
     c                   parm                    c2ktxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C2AHGR'
     c                   eval      c2augr = 0
     c                   call      'VF510R'
     c                   parm                    w_firm
     c                   parm                    c2ahgr
     c                   parm                    w_utxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C2AUGR'
     c                   call      'VF511R'
     c                   parm                    w_firm
     c                   parm                    c2ahgr
     c                   parm                    c2augr
     c                   parm                    w_utxt
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
      * Enhets-kode
      *
     c***                if        c2kenh <> *blank and
     c***                          c2kenh <> 'X' and
     c***                          c2kenh <> 'L' and
     c***                          c2kenh <> 'C' and
     c***                          c2kenh <> 'D' and
     c***                          c2kenh <> 'M'
     c***                eval      *in33 = *on
     c***                eval      b_feil = *on
     c***                leavesr
     c***                endif
      *
      * Avgiftskode
      *
     c                   eval      vmvalr_mkod = c2kmva
     c     vmvalr_key    chain     vmvalr
     c                   if        not %found
     c                   eval      *in34 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      * Antall desimaler
      *
     c                   if        c2desi <> 0 and
     c                             c2desi <> 1 and
     c                             c2desi <> 2 and
     c                             c2desi <> 3
     c                   eval      *in37 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      * Struktur-kode
      *
     c                   if        c2kstr <> 0 and
     c                             c2kstr <> 20
     c                   eval      *in38 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      * Alternativt varegruppe
      *
     c                   if        c2ahgr <> 0 or
     c                             c2augr <> 0
     c                   eval      vaugl1_uhgr = c2ahgr
     c                   eval      vaugl1_uugr = c2augr
     c     vaugl1_key    chain     vaugl1
     c                   if        not %found
     c                   eval      *in39 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
      * Alternativt varenummer
      *
     c                   if        c2avar <> *blank
     c                   eval      vvarl1_vare = c2avar
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   eval      *in40 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
      * Kontohenvisning
      *
     c                   if        c2khev <> *blank
     c                   eval      vkhvl1_kkod = c2khev
     c     vkhvl1_key    chain     vkhvl1
     c                   if        not %found
     c                   eval      *in47 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdater      begsr
      *
      * Oppdatering av parametre
      *
     c                   eval      p_vare = c2vare
     c                   eval      p_tek1 = c2tek1
     c                   eval      p_kbon = c2kbon
     c                   eval      p_kord = c2kord
     c***                eval      p_kenh = c2kenh
     c                   eval      p_kmva = c2kmva
     c                   eval      p_kres = c2kres
     c***                eval      p_kpme = c2kpme
     c                   eval      p_keti = c2keti
     c                   eval      p_kpdi = c2kpdi
     c                   eval      p_kjus = c2kjus
     c                   eval      p_desi = c2desi
     c                   eval      p_kstr = c2kstr
     c                   eval      p_kabc = c2kabc
     c                   eval      p_ahgr = c2ahgr
     c                   eval      p_augr = c2augr
     c                   eval      p_avar = c2avar
     c                   eval      p_kfri = c2kfri
     c                   eval      p_fako = c2fako
     c                   eval      p_dekn = c2dekn
     c                   eval      p_omrn = c2omrn
     c                   eval      p_godj = c2godj
     c                   eval      p_khev = c2khev
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - --- - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_valg
     c                   parm                    p_tek1
     c                   parm                    p_kbon
     c                   parm                    p_kord
     c                   parm                    p_kenh
     c                   parm                    p_kmva
     c                   parm                    p_kres
     c                   parm                    p_kpme
     c                   parm                    p_keti
     c                   parm                    p_kpdi
     c                   parm                    p_kjus
     c                   parm                    p_desi
     c                   parm                    p_kstr
     c                   parm                    p_kabc
     c                   parm                    p_ahgr
     c                   parm                    p_augr
     c                   parm                    p_avar
     c                   parm                    p_kfri
     c                   parm                    p_fako
     c                   parm                    p_dekn
     c                   parm                    p_omrn
     c                   parm                    p_godj
     c                   parm                    p_khev
6.11 c                   parm                    p_sort
      *
      * Key for oppslag på alternativ undergruppe
      *
     c     vaugl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vaugl1_uhgr
     c                   kfld                    vaugl1_uugr
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på kontohenvisning
      *
     c     vkhvl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vkhvl1_kkod
      *
      * Key for les på mva.-kode
      *
     c     vmvalr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vmvalr_mkod
      *
      * Flytter firma til nøkkel-felt
      *
     c                   eval      w_firm = p_firm
      *
      * Kopierer parametre til skjerm-felter
      *
     c                   eval      c2vare = p_vare
     c                   eval      c2tek1 = p_tek1
     c                   eval      c2kbon = p_kbon
     c                   eval      c2kord = p_kord
     c***                eval      c2kenh = p_kenh
     c                   eval      c2kmva = p_kmva
     c                   eval      c2kres = p_kres
     c***                eval      c2kpme = p_kpme
     c                   eval      c2keti = p_keti
     c                   eval      c2kpdi = p_kpdi
     c                   eval      c2kjus = p_kjus
     c                   eval      c2desi = p_desi
     c                   eval      c2kstr = p_kstr
     c                   eval      c2kabc = p_kabc
     c                   eval      c2ahgr = p_ahgr
     c                   eval      c2augr = p_augr
     c                   eval      c2avar = p_avar
     c                   eval      c2kfri = p_kfri
     c                   eval      c2fako = p_fako
     c                   eval      c2dekn = p_dekn
     c                   eval      c2omrn = p_omrn
     c                   eval      c2godj = p_godj
     c                   eval      c2khev = p_khev
6.11 c                   eval      c2sort = p_sort
      *
     c                   endsr
