     H datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * System  . . . . : ASLAGR                                        *
      * Program . . . . : LO670R                                        *
      * Beskrivelse . . : Utskrift av øre-differanse liste              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * Spesialversjon. :                                               *
      * Tilpasset for . : Mestergruppen                                 *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
      *           211111  Ny versjon av programmet                  ASK *
6.10  *           300113  Hvis mva fri, skal ikke totf brukes       bsa *
7.01  *           180915  Lagt in test på valgte ordretyper         ask *
7.02  *           151118  Mva må beregnes fra linje, ikke bare brukebsa *
7.02  *                   flat prosentsats.                         bsa *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flodtlr    if   e           k disk    rename(lodtpfr:lodtlrr)
      *
     flo670d    cf   e             workstn
      *
      * Definering av Local Data Arae (LDA)
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d lodtlr_numm     s                   like(ldnumm)
     d lodtlr_suff     s                   like(ldsuff)
      *
      * Definering av variable
      *
     d w_firm          s                   like(lofirm)
     d w_toti          s             11  2
     d w_totr          s             11  2
     d w_diff          s             11  2
     d w_tdif          s             11  2
     d w_nett          s             11  2
     d w_mvab          s             11  2
     d w_lsum          s             11  2
     d w_totf          s             11  2
     d w_anta          s              6  0
     d w_antf          s              6  0
     d w_grns          s              2  0
7.01 d w_otyp          s             12
7.02 d w_mvag          s             11  2
7.02 d w_kmva          s             11  2
7.02 d w_mvbb          s             11  2
7.02 d w_stat          s              1    inz(*off)
7.02 d w_mvak          s              1
7.02 d w_mdat          s               d   datfmt(*iso)
7.02 d w_mvtx          s             30
7.02 d w_mvap          s              6  2
7.02 d w_mvat          s              5  4
7.02 d w_mvaf          s             10  9
7.02 d w_bpro          s              5  2
7.01 d b_type          s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Legg ut parameterbilde
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      w_anta = 0
     c                   eval      *in30 = *on
     c                   exfmt     b1bld
     c                   if        *inkc or *inkl
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      w_grns = b1grns
7.01  *
7.01 c                   if        b1oty1 <> *blank or
7.01 c                             b1oty2 <> *blank or
7.01 c                             b1oty3 <> *blank or
7.01 c                             b1oty4 <> *blank or
7.01 c                             b1oty5 <> *blank or
7.01 c                             b1oty6 <> *blank
7.01 c                   eval      b_type = *on
7.01 c                   endif
7.01  *
7.01 c                   eval      w_otyp = b1oty1 + b1oty2 + b1oty3 +
7.01 c                                      b1oty4 + b1oty5 + b1oty6
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Behandle file
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Lese file
      *
     c     lohel1_key    setll     lohel1
     c     lohel1_key    reade     lohel1
      *
     c                   dow       not %eof
      *
7.01 c                   if        b_type = *on
7.01 c                   if        lootyp <> b1oty1 and
7.01 c                             lootyp <> b1oty2 and
7.01 c                             lootyp <> b1oty3 and
7.01 c                             lootyp <> b1oty4 and
7.01 c                             lootyp <> b1oty5 and
7.01 c                             lootyp <> b1oty6
7.01 c                   goto      les_neste
7.01 c                   endif
7.01 c                   endif
7.01  *
     c                   exsr      hent_total
      *
     c                   if        %abs(w_diff) > w_grns
     c                   eval      w_anta = w_anta + 1
     c                   endif
      *
7.01 c     les_neste     tag
     c     lohel1_key    reade     lohel1
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Vise resultat av kontroll - evt. starte liste
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      b1anta = w_anta
     c                   eval      b1antf = w_antf
     c                   eval      b1dbel = w_tdif
      *
     c                   if        w_anta > 0
     c                   eval      *in30 = *off
     c                   endif
      *
     c                   exfmt     b1bld
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av bestillingstotal
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_total    begsr
      *
7.02 c                   eval      w_mvbb = 0
     c                   eval      w_toti = 0
     c                   eval      w_totr = 0
     c                   eval      w_nett = 0
     c                   eval      w_mvab = 0
     c                   eval      w_lsum = 0
     c                   eval      w_totf = 0
     c                   eval      w_diff = 0
      *
     c                   eval      lodtlr_numm = lonumm
     c                   eval      lodtlr_suff = losuff
      *
     c     lodtlr_key    setll     lodtlr
     c     lodtlr_key    reade     lodtlr
     c                   dow       not %eof
     c                   eval      w_toti = w_toti + ldsumi
     c                   eval      w_totr = w_totr + ldsumr
7.02 c                   exsr      kalk_mva
7.02 c                   eval      w_mvbb = w_mvbb + w_kmva
     c     lodtlr_key    reade     lodtlr
     c                   enddo
      *
     c                   eval      w_nett = w_toti - w_totr
      *
      * Beregner faktura-diff
      *
     c                   eval      w_antf = w_antf + 1
6.10 c                   if        lomkod = 0
     c                   eval      w_totf = lototf
6.10 c                   else
6.10 c                   eval(h)   w_totf = w_nett
6.10 c                   endif
      *
     c                   if        lomkod = 0
7.02 c                   eval      w_mvab = w_mvbb
7.02 c**                 eval(h)   w_mvab = (w_nett * 25) / 100
     c                   else
     c                   eval      w_mvab = 0
     c                   endif
      *
     c                   eval      w_lsum = w_nett + w_mvab
     c                   eval(h)   w_diff = w_lsum - w_totf
      *
     c                   eval      w_tdif = w_tdif + w_diff
      *
     c                   endsr
      *
7.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.02  * Subrutine for beregning av mva beløp
7.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.02  *
7.02 c     kalk_mva      begsr
7.02  *
7.02 c                   eval      w_mvab = 0
7.02 c                   eval      w_kmva = 0
7.02  *
7.02 c                   if        ldomva <> *blank
7.02 c                   eval      w_stat = *blank
7.02 c                   eval      w_mvak = ldomva
7.02 c                   time                    w_mdat
7.02  *
7.02 c                   call      'RS205R'
7.02 c                   parm                    w_stat
7.02 c                   parm                    w_mvak
7.02 c                   parm                    w_mvtx
7.02 c                   parm                    w_mdat
7.02 c                   parm                    w_mvap
7.02 c                   parm                    w_mvat
7.02 c                   parm                    w_mvaf
7.02 c                   parm                    w_bpro
7.02  *
7.02 c                   if        w_mvap = 0
7.02 c                   eval      w_mvap = 1
7.02 c                   endif
7.02  *
7.02 c                   eval      w_mvag = ldsumi - ldsumr
7.02 c                   eval      w_kmva = ((w_mvag * w_mvap)/100)
7.02  *
7.02 c                   endif
7.02  *
7.02 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Tar inn parameter fra forrige program
      *
     c     *entry        plist
     c                   parm                    w_grns
     c                   parm                    w_anta
7.01 c                   parm                    w_otyp
      *
      * Key for oppslag på odre-hode
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
      *
      * Key for les/oppslag på bestilling-linje
      *
     c     lodtlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlr_numm
     c                   kfld                    lodtlr_suff
      *
      * Key for oppslag på koderegister
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Legge inn key
      *
     c                   eval      w_firm = l_firm
      *
      * Henter statusopplysninger fra status-register
      *
     c     lstsl1_key    chain     lstsl1
      *
     c                   endsr
