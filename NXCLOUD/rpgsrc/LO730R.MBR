     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LO730R
      *  Beskrivelse . . : Beregning av beløp på bestillingslinje og hode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      *  --------- ------  -------------------------------------------------
5.70  *  PRGR     021208  Utvidet med prisgruppe                   bof
      *  6.10     180609  Oppdatert med sporingsinformasjon        bsa
6.21  *  6.20     190511  Utvidet vl710r med div. info             bof
6.22  *  6.20     281011  Hvis kundeordre,så sjekkes kostpris      bof
6.nu  *  6.30     120614  Gjennomgått mtp. nummerutvidelse         bof
6.30  * 6.30  170918 bkv  Utvidet med trelast sortiment
8.01  *PRG2   160622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvltyl1    if   e           k disk    rename(vltypfr:vltyl1r)
     flohelu    uf   e           k disk    rename(lohepfr:lohelur)
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flodtlu    uf   e           k disk    rename(lodtpfr:lodtlur)
     flodtl1    if   e           k disk    rename(lodtpfr:lodtl1r)
6.22 ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
6.22 flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
     fvvarlr    if   e           k disk    rename(vvarpfr:vvarlrr)
6.30 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
6.10  * Definering av Local Data Area
6.10  *
6.10 d                uds
6.10 d l_user                911    920
      * Definering av variable i parametre
      *
     d p_enor          s              1
     d p_firm          s                   like(lofirm)
     d p_numm          s                   like(lonumm)
     d p_suff          s                   like(losuff)
      *
      * Definering av variable i nøkler
      *
     d lohel1_numm     s                   like(lonumm)
     d lohel1_suff     s                   like(losuff)
     d lohelu_numm     s                   like(lonumm)
     d lohelu_suff     s                   like(losuff)
     d lodtl1_numm     s                   like(ldnumm)
     d lodtl1_suff     s                   like(ldsuff)
     d lodtlu_numm     s                   like(ldnumm)
     d lodtlu_suff     s                   like(ldsuff)
     d lodtlu_line     s                   like(ldline)
     d vltyl1_ltyp     s                   like(valtyp)
     d vvarlr_vare     s                   like(vvvare)
6.22 d fodtl1_numm     s                   like(fdnumm)
6.22 d fodtl1_suff     s                   like(fdsuff)
6.22 d fodtl1_line     s                   like(fdline)
      *
      * Definering av øvrige variable
      *
     d w_dato          s               d   datfmt(*iso)
     d w_inlr          s              1
     d w_lety          s              1  0
     d w_sapr          s              9  2
     d w_bupr          s              9  2
     d w_inpr          s              9  2
     d w_kopr          s              9  2
     d w_firm          s                   like(ldfirm)
     d w_enhe          s                   like(ldenh1)
     d w_lisu          s             11  2
     d w_dekn          s             11  2
     d w_grad          s             11  2
     d w_rabk          s             11  2
     d w_sumi          s             11  2
     d w_sumk          s             11  2
     d w_sumr          s             11  2
     d w_toti          s             11  2
     d w_totk          s             11  2
     d w_totr          s             11  2
     d w_anta          s                   like(ldanta)
     d w_ipny          s                   like(ldipny)
     d w_kpny          s                   like(ldkpny)
     d w_rab1          s                   like(ldrab1)
     d w_rab2          s                   like(ldrab2)
8.01 d w_prgr          s              2
5.70 d w_vtyp          s                   like(vvvtyp)
6.21 d w_udat          s                   like(vvudat)
6.21 d w_ldor          s                   like(vvldor)
6.21 d b_inlr          s              1    inz(*off)
6.30 d w_lv_nobb       s                   like(vvlnob)
6.30 d w_lv_modn       s                   like(vvlmod)
6.30 d w_lv_lldo       s                   like(vvlnle)
6.30 d w_lv_llva       s                   like(vvllva)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram - les bestillingslinjer og rekalkuler
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Hent opplysninger fra BESTILLING-HODE
      *
     c     lohel1_key    chain     lohel1r
      *
      *  Les BESTILLING-LINJER
      *
     c     lodtl1_key    setll     lodtl1r
     c     lodtl1_key    reade     lodtl1r
      *
     c                   dow       not %eof
      *
      *  Sjekk linjetype og les neste hvis tekstlinje
      *
     c                   eval      vltyl1_ltyp = ldltyp
     c     vltyl1_key    chain     vltyl1r
     c                   if        valktx = 1
     c                   goto      neste_linje
     c                   endif
      *
     c                   eval      w_ipny = ldipny
     c                   eval      w_kpny = ldkpny
     c                   eval      w_rab1 = ldrab1
     c                   eval      w_rab2 = ldrab2
      *
      *  Beregning av priser, summer og totaler
      *
      *  Statuskode = 0  gir beregning av nye summer og totaler
      *  Statuskode = 1  gir henting av nye priser/rabatter samt beregning
      *  Dersom bestillingslinje kommer fra LVR, brukes priser fra linje
      *
     c                   if        p_enor <> *blank and ldlvre = 0
     c                   exsr      hntpri
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Beregner totaler for den enkelte varelinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      w_anta = ldanta
     c                   if        valkre = '-'
     c                   eval      w_anta = w_anta * -1
     c                   endif
      *
      *  Beregning av innkjøp
      *
     c                   eval      w_sumi = w_ipny * w_anta
     c                   eval      w_lisu = w_sumi
      *
      *  Beregning av rabatter
      *
     c                   eval      w_rabk = (w_lisu * lorabp) / 100
     c                   eval      w_sumr = w_rabk
     c                   eval      w_lisu = w_lisu - w_rabk
      *
     c                   eval      w_rabk = (w_lisu * w_rab1) / 100
     c                   eval      w_sumr = w_sumr + w_rabk
     c                   eval      w_lisu = w_lisu - w_rabk
      *
     c                   eval      w_rabk = (w_lisu * w_rab2) / 100
     c                   eval      w_sumr = w_sumr + w_rabk
     c                   eval      w_lisu = w_lisu - w_rabk
      *
      *  Beregning av kost
      *
     c                   eval      w_sumk = w_kpny * w_anta
      *
      *  Oppdatering av BESTILLING-LINJE-REGISTER med nye opplysninger
      *
     c                   eval      lodtlu_line = ldline
     c     lodtlu_key    chain     lodtlur
     c                   if        %found
     c                   eval      ldipny = w_ipny
     c                   eval      ldkpny = w_kpny
     c                   eval      ldrab1 = w_rab1
     c                   eval      ldrab2 = w_rab2
      *
     c                   eval      ldsumi = w_sumi
     c                   eval      ldsumr = w_sumr
     c                   eval      ldsumk = w_sumk
      *
6.10 c                   time                    ldedat
6.10 c                   time                    ldetim
6.10 c                   eval      ldeusr = l_user
     c                   update    lodtlur
     c                   endif
      *
      *  Akkumulerer totaler for oppdatering av BESTILLING-HODE-REGISTER
      *
     c                   eval      w_toti = w_toti + w_sumi
     c                   eval      w_totr = w_totr + w_sumr
     c                   eval      w_totk = w_totk + w_sumk
      *
     c     neste_linje   tag
     c     lodtl1_key    reade     lodtl1r
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     pgm_end       tag
      *
     c                   exsr      oppdat
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Oppdaterer bestillingshode med totaler
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdat        begsr
      *
     c     lohelu_key    chain     lohelur
     c                   if        %found
     c                   eval      lototi = w_toti
     c                   eval      lototr = w_totr
     c                   eval      lototk = w_totk
6.10 c                   time                    loedat
6.10 c                   time                    loetim
6.10 c                   eval      loeusr = l_user
     c                   update    lohelur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Henter opplsyninger om varen fra pris/vare subrutinen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hntpri        begsr
      *
5.70 c                   exsr      hent_prisgr
      *
      * Initiering av variable
      *
     c                   eval      w_inpr = 0
     c                   eval      w_kopr = 0
     c                   eval      w_enhe = ldenh1
      *
      * Lese vareregister for å finne varetype
      *
     c                   eval      vvarlr_vare = ldvare
     c     vvarlr_key    chain     vvarlrr
      *
      * Henter og beregner prisgrunnlag fra vareregisteret
      *
     c                   call      'VP701R'
     c                   parm                    w_firm
     c                   parm                    ldvare
5.70 c                   parm                    w_prgr
     c                   parm                    w_enhe
     c                   parm                    w_lety
     c                   parm                    w_dato
     c                   parm                    w_inlr
     c                   parm                    w_sapr
     c                   parm                    w_bupr
     c                   parm                    w_kopr
     c                   parm                    w_inpr
      *
      * henter varetype
      *
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    ldvare
5.70 c                   parm                    ldlage
5.70 c                   parm                    w_vtyp
6.21 c                   parm                    w_udat
6.21 c                   parm                    w_ldor
6.21 c                   parm                    b_inlr
6.30 c                   parm                    w_lv_nobb
6.30 c                   parm                    w_lv_modn
6.30 c                   parm                    w_lv_lldo
6.30 c                   parm                    w_lv_llva
      *
5.70 c                   if        w_vtyp <> 'D'
     c                   eval      w_ipny = w_inpr
     c                   eval      w_kpny = w_kopr
      *
     c                   eval      w_rab1 = 0
     c                   eval      w_rab2 = 0
     c                   endif
6.22  *
6.22  ** hvis kundeordre - skal kostpris og innkjøpspris hentes herfra
6.22  *
6.22 c                   if        ldornu <> 0
6.22 c                   eval      fdkopr = 0
6.22 c                   eval      fodtl1_numm = ldornu
6.22 c                   eval      fodtl1_suff = ldorsu
6.22 c                   eval      fodtl1_line = ldorli
6.22 c     fodtl1_key    chain     fodtl1
6.22 c                   if        %found(fodtl1) and
6.22 c                             fdkopr <> 0
6.22 c                   eval      w_kpny = fdkopr
6.22  * innkjøpsprisen bestilling  settes lik kostpris på ordre
6.22  * parameterstyres
6.22 c                   if        laaenl = 1
6.22 c                   eval      w_ipny = fdkopr
6.22 c                   endif
6.22 c                   endif
6.22 c                   endif
      *
     c                   endsr
5.70  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5 70  * Subrutine for å hente prisgruppe
5.70  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.70  *
5.70 c     hent_prisgr   begsr
5.70  *
5.70 c                   call      'VL712R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    ldlage
5.70 c                   parm                    ldavde
5.70 c                   parm                    w_prgr
5.70  *
5.70 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Tar inn parameter fra forrige program
      *
     c     *entry        plist
     c                   parm                    p_enor
     c                   parm                    p_firm
     c                   parm                    p_numm
     c                   parm                    p_suff
      *
      *  Key for les av LINJETYPE
      *
     c     vltyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltyl1_ltyp
      *
      *  Key for les av BESTILLING-HODE
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
     c                   kfld                    lohel1_suff
      *
      *  Key for oppdatering av BESTILLING-HODE
      *
     c     lohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelu_numm
     c                   kfld                    lohelu_suff
      *
      *  Key for les av BESTILLING-LINJE
      *
     c     lodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtl1_numm
     c                   kfld                    lodtl1_suff
      *
      *  Key for oppdatering av BESTILLING-LINJE
      *
     c     lodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlu_numm
     c                   kfld                    lodtlu_suff
     c                   kfld                    lodtlu_line
      *
      *  Key for les av VARE
      *
     c     vvarlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlr_vare
6.22  *
6.22  * Key for les av ordrelinjer
6.22  *
6.22 c     fodtl1_key    klist
6.22 c                   kfld                    w_firm
6.22 c                   kfld                    fodtl1_numm
6.22 c                   kfld                    fodtl1_suff
6.22 c                   kfld                    fodtl1_line
6.22  *
6.22  *  Key for les av STATUS
6.22  *
6.22 c     lstsl1_key    klist
6.22 c                   kfld                    w_firm
      *
      *
      *  Legger inn verdier i nøklene
      *
     c                   eval      w_firm = p_firm
     c                   eval      lohel1_numm = p_numm
     c                   eval      lohel1_suff = p_suff
     c                   eval      lohelu_numm = p_numm
     c                   eval      lohelu_suff = p_suff
     c                   eval      lodtl1_numm = p_numm
     c                   eval      lodtl1_suff = p_suff
     c                   eval      lodtlu_numm = p_numm
     c                   eval      lodtlu_suff = p_suff
     c                   eval      lodtlu_line = 0
      *
6.22 c     lstsl1_key    chain     lstsl1r                            69
      *
     c                   endsr
