      /TITLE  Klargjøring av kommunikasjonsfil for overføring.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                   *
      * Program . . . . : GL700R                                          *
      * Beskrivelse . . : Klargjør for overføring av banktranser.         *
      *                   Henter inn parametre.                           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                   *
      * Spesialversjon. :                                                 *
      * Tilpasset for . :                                                 *
      *                                                                   *
      * Referanse  Dato   Endringsbeskrivelse                        Sign.*
      * --------- ------  ------------------------------------------------*
      *                                                                   *
      *                                                                   *
      *                                                                   *
      *                                                                   *
      *                                                                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                   *
      *  BRUK AV INDIKATORER                                              *
      *                                                                   *
      *       KC = F3  = Exit                                             *
      *       KE = F5  = Endre mottagerinformasjon                        *
      *       KF = F6  = Endre FMS-record for komm. mot NIT               *
      *                                                                   *
      *       LR = Avslutt program                                        *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer               *
      *    60-69 = Fileindikatorer chain etc.                             *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                       *
      *    80-89 = Arbeidsindikatorer ordinære                            *
      *    90-99 = Benyttes ved std. sub-rutiner                          *
      *                                                                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     fdirpie    uf a e           k disk
     fdiriie    if   e           k disk
     fdiruie    o  a e           k disk
     fgl700d    cf   e             workstn
      *
     d rci             s              1    dim(80)                              Redigering inn
     d rcu             s              1    dim(80)                              Redigering ut
      *
     d wbtot           s              6  0
     d wfnut           s              1
     d wlogn           s             80
     d wpparm          s              2
     d wrend           s              2  0
     d wsend           s             80
     d x1              s              2  0
     d x2              s              2  0
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av felter                                              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      *
      * A1BLD  - A1 Hovedbilde for oppstart
      * A2WIN  - A2 Vindu for mottagerinformasjon
      * A3WIN  - A3 Vindu for NIT-informasjon (Hvis brukt)
      * A4WIN  - A4 Vindu med informasjon om klargjøring av fil
      *
      /eject
      *
      * Definerer generelle felter
      *
     c     *entry        plist
     c                   parm                    wpparm
      *
     c                   eval      x1 = 0                                       Tabell teller
     c                   eval      x2 = 0                                       Tabell teller
     c                   eval      wrend = 0                                    Startpkt.utfil
     c                   eval      wbtot = 0                                    Teller bytes
     c                   biton     '123457'      wfnut                          Definere '
     c                   bitoff    '06'          wfnut
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * A1BLD Styrebilde - inntasting av konto,brukerid og passord.       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Henter data fra paramete-fil og legger ut i skjermfelter
      *
     c     wpparm        chain     dirpie                             60
     c                   if        *in60 = *on                                            .....
     c                   eval      a1sacc = *blanks                                           .
     c                   eval      a1suid = *blanks                                           .
     c                   eval      a1spwd = *blanks                                           .
     c                   eval      a1smsg = *blanks                                           .
     c                   eval      a1part = *blanks                                           .
     c                   eval      a2macc = *blanks                                           .
     c                   eval      a2muid = *blanks                                           .
     c                   eval      a3nit1 = *blanks                                           .
     c                   eval      a3nit2 = *blanks                                           .
     c                   else                                                                 .
     c                   move      iesacc        a1sacc                                       .
     c                   move      iesuid        a1suid                                       .
     c                   move      iespwd        a1spwd                                       .
     c                   move      iesmsg        a1smsg                                       .
     c                   move      iepart        a1part                                       .
     c                   move      iemacc        a2macc                                       .
     c                   move      iemuid        a2muid                                       .
     c                   movel     ienitr        a3nit1                                       .
     c                   move      ienitr        a3nit2                                       .
     c                   endif                                                            .....
      *
     c     a1taga        tag
     c                   exfmt     a1bld
      *
      * Gå ut
      *
JT.T c                   if        *inkc = *on                                            .....
     c                   move      'F3'          wpparm                                       .
     c                   goto      xslutt                                                     .
     c                   endif                                                            .....
      *
      * Oppdatere mottakerinformasjon
      *
     c                   if        *inke = *on                                            .....
     c                   exfmt     a2win                                                      .
     c                   goto      a1taga                                                     .
     c                   endif                                                            .....
      *
      * Oppdatere FMS-informasjon (NIT)
      *
     c                   if        *inkf = *on                                            .....
     c                   exfmt     a3win                                                      .
     c                   goto      a1taga                                                     .
     c                   endif                                                            .....
      *
      * Sjekke om logon konto utfylt
      *
     c                   if        a1sacc = *blanks                                       .....
     c                   move      *on           *in31                                        .
     c                   goto      a1taga                                                     .
     c                   endif                                                            .....
      *
      * Sjekke om logon brukerid er utfylt
      *
     c                   if        a1suid = *blanks                                       .....
     c                   move      *on           *in32                                        .
     c                   goto      a1taga                                                     .
     c                   endif                                                            .....
      *
      * Sjekke om logon passord er utfylt
      *
     c                   if        a1spwd = *blanks                                       .....
     c                   move      *on           *in33                                        .
     c                   goto      a1taga                                                     .
     c                   endif                                                            .....
      *
      * Sjekke om meldingsklasse er utfylt
      *
     c                   if        a1smsg = *blanks                                       .....
     c                   move      *on           *in35                                        .
     c                   goto      a1taga                                                     .
     c                   endif                                                            .....
      *
      * Sjekke om kommunikasjonspartner er utfylt
      *
     c                   if        a1part = *blanks                                       .....
     c                   move      *on           *in36                                        .
     c                   goto      a1taga                                                     .
     c                   endif                                                            .....
      *
      * Sjekke om mottaker konto er utfylt
      *
     c                   if        a2macc = *blanks                                       .....
     c                   move      *on           *in34                                        .
     c                   goto      a1taga                                                     .
     c                   endif                                                            .....
      *
      * Sjekke om mottaker brukerid er utfylt
      *
     c                   if        a1part = 'IBM'                                         .....
     c                   if        a2muid = *blanks                                           .
     c                   move      *on           *in34                                        .
     c                   goto      a1taga                                                     .
     c                   endif                                                                .
     c                   endif                                                            .....
      *
      * Redigerer data ut på parameter fil og oppdaterer
      *
     c                   write     a4win
      *
     c     wpparm        chain     dirpie                             60
      *
     c                   move      a1sacc        iesacc
     c                   move      a1suid        iesuid
     c                   move      a1spwd        iespwd
     c                   move      a1smsg        iesmsg
     c                   move      a1part        iepart
     c                   move      a2macc        iemacc
     c                   move      a2muid        iemuid
     c                   movel     a3nit1        ienitr
     c                   move      a3nit2        ienitr
      *
     c                   if        *in60 = *off                                           .....
     c                   update    iedata                                                     .
     c                   else                                                                 .
     c                   move      wpparm        iekey                                        .
     c                   write     iedata                                                     .
     c                   endif                                                            .....
      *
      * Logge inn på CICS og starte applikasjon  (NIT)
      *
     c                   if        a1part = 'NIT'                                         .....
     c                   movel(p)  'CESN'        wlogn                                        .
     c                   move      wlogn         ierec                                        .
     c                   write     ieutrec                                                    .
      *                                                                  .
     c                   movel(p)  a1suid        wlogn                                        .
     c                   move      wlogn         ierec                                        .
     c                   write     ieutrec                                                    .
      *                                                                  .
     c                   movel(p)  a1spwd        wlogn                                        .
     c                   move      wlogn         ierec                                        .
     c                   write     ieutrec                                                    .
      *                                                                  .
     c                   movel(p)  'EXPV'        wlogn                                        .
     c                   move      wlogn         ierec                                        .
     c                   write     ieutrec                                                    .
      *                                                                  .
     c                   endif                                                            .....
      *
      * Redigere logon
      *
     c                   if        a1part = 'IBM'                                         .....
     c                   exsr      xlogn1                                                     .
     c                   else                                                                 .
     c                   exsr      xlogn2                                                     .
     c                   endif                                                            .....
      *
      * Redigere send
      *
     c                   if        a1part = 'IBM'                                         .....
     c                   exsr      xsend1                                                     .
     c                   else                                                                 .
     c                   exsr      xsend2                                                     .
     c                   endif                                                            .....
      *
      * Test om FMS-record til NIT
      *
     c                   if        a1part = 'IBM'                                         .....
     c                   if        ienitr <> *blanks                                          .
     c                   movea     ienitr        rci                                          .
     c                   exsr      xredi                                                      .
     c                   endif                                                                .
     c                   endif                                                            .....
      *
      * Redigere datarecords
      *
     c                   read      diriie                                 61
     c                   dow       *in61 = *off                                           .....
     c                   eval      wbtot = wbtot + 80                                         .
     c                   if        a1part = 'IBM'                                             .
     c                   movea     remrec        rci                                          .
     c                   exsr      xredi                                                      .
     c                   else                                                                 .
     c                   move      remrec        ierec                                        .
     c                   write     ieutrec                                                    .
     c                   endif                                                                .
     c                   read      diriie                                 61                  .
     c                   enddo                                                            .....
      *
      * "Tømmer" siste record og sender avslutningstegn
      *
     c                   if        a1part = 'IBM'                                         .....
     c                   move      '¤'           rcu(x2)                                      .
     c                   eval      x2 = x2 + 1                                                .
     c                   move      '¤'           rcu(x2)                                      .
     c                   eval      x2 = x2 + 1                                                .
     c                   move      '¤'           rcu(x2)                                      .
      *                                                                  .
     c                   movea     rcu           ierec                                        .
     c                   else                                                                 .
     c                   movel(p)  ';'           wlogn                                        .
     c                   move      wlogn         ierec                                        .
     c                   endif                                                            .....
     c                   write     ieutrec
     c                   close     diruie
      *
      * Kaller opp kommunikasjonsprogram
      *
     c                   call      'GL701R'
     c                   parm                    wbtot
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for redigering/utlegg av logon-streng IBM
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xlogn1        begsr
     c                   movel(p)  'IELOGON'     wlogn
     c                   cat       ' ACCOU':0    wlogn
     c                   cat       'NT(':0       wlogn
     c                   cat       iesacc:0      wlogn
     c                   cat       ') USER':0    wlogn
     c                   cat       'ID(':0       wlogn
     c                   cat       iesuid:0      wlogn
     c                   cat       ') PASS':0    wlogn
     c                   cat       'WORD(':0     wlogn
     c                   cat       iespwd:0      wlogn
     c                   cat       ');':0        wlogn
     c                   move      wlogn         ierec
      *
     c                   write     ieutrec
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for redigering/utlegg av logon-streng NIT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xlogn2        begsr
     c                   movel(p)  'IELOGON'     wlogn
     c                   cat       ' ACCOU':0    wlogn
     c                   cat       'NT(':0       wlogn
     c                   cat       iesacc:0      wlogn
     c                   cat       ') USER':0    wlogn
     c                   cat       'ID(':0       wlogn
     c                   cat       iesuid:0      wlogn
     c                   cat       ');':0        wlogn
     c                   move      wlogn         ierec
      *
     c                   write     ieutrec
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for redigering/utlegg av send-streng IBM
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xsend1        begsr
     c                   movel(p)  'SEND'        wsend
     c                   cat       ' ACCOU':0    wsend
     c                   cat       'NT(':0       wsend
     c                   cat       iemacc:0      wsend
     c                   cat       ') USER':0    wsend
     c                   cat       'ID(':0       wsend
     c                   cat       iemuid:0      wsend
     c                   cat       ') CLAS':0    wsend
     c                   cat       'S(':0        wsend
     c                   cat       wfnut:0       wsend
     c                   cat       iesmsg:0      wsend
     c                   cat       wfnut:0       wsend
     c                   cat       ') DLM(':0    wsend
     c                   cat       '¤¤¤);':0     wsend
     c                   movea     wsend         rcu
      *
      * Finner sluttpunkt på record
      *
     c                   eval      x1 = 1
      *
     c                   dow       x1 <= 80                                               .....
     c                   if        rcu(x1) = ';'                                              .
     c                   eval      wrend = x1                                                 .
     c                   leave                                                                .
     c                   endif                                                                .
     c                   eval      x1 = x1 + 1                                                .
     c                   enddo                                                            .....
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for redigering/utlegg av send-streng NIT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xsend2        begsr
     c                   movel(p)  'SEND'        wsend
     c                   cat       ' ACCOU':0    wsend
     c                   cat       'NT(':0       wsend
     c                   cat       iemacc:0      wsend
     c                   cat       ');':0        wsend
     c                   move      wsend         ierec
      *
     c                   write     ieutrec
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * IBM-KOMMUNIKASJON:  (IEPART=IBM)
      * Subrutine for redigering/utlegg record på kommunikasjonsfil
      *   Forklaring: Dataposter blir lagt ut umiddelbart etter ";"
      *               som avslutter "SEND"-streng i XSEND1. Sluttpunkt
      *               varierer og blir beregnet i WREND. Input-record
      *               blir fordelt over to records på send-fil.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xredi         begsr
      *
      * Redigerer og skriver første halvdel
      *
     c                   eval      x1 = 1
     c                   eval      x2 = wrend + 1
      *
     c                   dow       x2 <= 80                                               .....
     c                   move      rci(x1)       rcu(x2)                                      .
     c                   eval      x1 = x1 + 1                                                .
     c                   eval      x2 = x2 + 1                                                .
     c                   enddo                                                            .....
     c                   movea     rcu           ierec
      *
     c                   write     ieutrec
     c                   eval      rcu = *blanks
      *
      * Redigerer andre halvdel
      *
     c                   eval      x2 = 1
      *
     c                   dow       x2 <= wrend                                            .....
     c                   move      rci(x1)       rcu(x2)                                      .
     c                   eval      x1 = x1 + 1                                                .
     c                   eval      x2 = x2 + 1                                                .
     c                   enddo                                                            .....
      *
     c                   endsr
      *
