     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : AS115R
      * Beskrivelse . . : Henter høyeste suffiks
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *K0000  270824 bsa  Nytt program
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flohelr    if   e           k disk    rename(lohepfr:lohelrr)
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     fsihel3    if   e           k disk    rename(sihepfr:sihel3r)
     fldellr    if   e           k disk    rename(ldelpfr:ldellrr)
      *
      * Definering av data-struktur
      *
      *
      * Definering av local data area
      *
     d                uds
     d l_user                911    920
     d l_filg                931    933
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d lohelr_numm     s                   like(lonumm)
     d lohelr_suff     s                   like(losuff)
     d lohel1_numm     s                   like(lonumm)
     d lohel1_suff     s                   like(losuff)
     d sihel3_aarr     s                   like(shaarr)
     d sihel3_numm     s                   like(shnumm)
     d sihel3_binr     s                   like(shbinr)
     d sihel3_suff     s                   like(shsuff)
     d ldellr_aarr     s                   like(lyaarr)
     d ldellr_numm     s                   like(lynumm)
     d ldellr_suff     s                   like(lysuff)
      *
      * Definering av variable
      *
     d w_firm          s                   like(lofirm)
     d w_suff          s              3  0
     d w_lsuf          s                   like(losuff)
     d w_hsuf          s                   like(losuff)
     d w_dsuf          s                   like(losuff)
      *
     d p_numm          s                   like(lonumm)
     d p_suff          s                   like(losuff)
      *
      * Definering av konstanter
      *
     d lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Beskrivelse av spesielle felter som er benyttet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_suff = 0
     c                   eval      w_lsuf = 0
     c                   eval      w_hsuf = 0
     c                   eval      w_dsuf = 0
     c                   eval      lohelr_numm = p_numm
     c                   eval      sihel3_numm = p_numm
     c                   eval      ldellr_numm = p_numm
     c                   eval      lohel1_numm = p_numm
      *
      * Hent høyeste suffiks fra bestillingstabellen
      *
     c                   exsr      hnt_b_suf
      *
      * Hent høyeste suffiks fra historisk fakturatabell
      *
     c                   exsr      hnt_h_suf
      *
      * Hent høyeste suffikks fra slettede ordre
      *
     c                   exsr      hnt_d_suf
      *
     c                   if        w_hsuf >= w_suff and
     c                             w_hsuf > 0
     c                   eval      w_suff = w_hsuf + 1
     c                   endif
      *
     c                   if        w_dsuf >= w_suff and
     c                             w_dsuf > 0
     c                   eval      w_suff = w_dsuf + 1
     c                   endif
      *
      * Hvis suffikset har rundet 100, må vi begynne på nytt
      *
     c                   if        w_suff > 99
     c                   eval      w_suff = 0
     c                   endif
      *
      * Dobbeltsjekk at suffiks ikke allerede finnes
      *
     c     ny_suffiks    tag
     c                   eval      lohel1_suff = w_suff
     c     lohel1_key    chain     lohel1
     c                   if        %found
     c                   eval      w_suff = w_suff +1
     c                   goto      ny_suffiks
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xslutt        tag
      *
     c                   eval      p_suff = w_suff
      *
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter neste ledige suffix fra bestilling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hnt_b_suf     begsr
      *
     c                   eval      lohel1_suff = 99
     c     lohel1_key    chain     lohel1
     c                   if        %found
     c                   eval      w_lsuf = 99
     c                   goto      suffiks
     c                   endif
      *
     c                   eval      lohelr_suff = 99
     c     lohelr_key    setll     lohelr
     c     lohelr_key2   readpe    lohelr
     c                   if        not %eof
      * Hvis 98 har vi gått rundt
     c                   if        losuff = 98
     c                   eval      w_lsuf = 99
     c                   else
     c                   eval      w_lsuf = losuff
     c                   endif
     c                   else
     c                   eval      w_lsuf = -1
     c                   endif
      *
     c     suffiks       tag
      *
     c                   eval      w_suff = w_lsuf + 1
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter neste ledige suffix fra historisk faktura
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hnt_h_suf     begsr
      *
     c                   eval      sihel3_aarr = *year
     c                   eval      sihel3_numm = p_numm
     c                   eval      sihel3_binr = 0
     c                   eval      sihel3_suff = 0
     c     sihel3_key    setll     sihel3
     c     sihel3_key2   reade     sihel3
     c                   dow       not %eof
     c                   if        shsuff > w_hsuf
     c                   eval      w_hsuf = shsuff
     c                   endif
     c     sihel3_key2   reade     sihel3
     c                   enddo
      *
      * Sjekk også forrige år, hvis det skulle vært fakturert noe der.
      *
     c                   eval      sihel3_aarr = *year - 1
     c                   eval      sihel3_numm = p_numm
     c                   eval      sihel3_binr = 0
     c                   eval      sihel3_suff = 0
     c     sihel3_key    setll     sihel3
     c     sihel3_key2   reade     sihel3
     c                   dow       not %eof
     c                   if        shsuff > w_hsuf
     c                   eval      w_hsuf = shsuff
     c                   endif
     c     sihel3_key2   reade     sihel3
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter neste ledige suffix fra slettet tabeller
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hnt_d_suf     begsr
      *
     c                   eval      ldellr_aarr = *year
     c                   eval      ldellr_numm = p_numm
     c                   eval      ldellr_suff = 0
     c     ldellr_key    setll     ldellr
     c     ldellr_key2   reade     ldellr
     c                   dow       not %eof
     c                   if        lysuff > w_dsuf
     c                   eval      w_dsuf = lysuff
     c                   endif
     c     ldellr_key2   reade     ldellr
     c                   enddo
      *
      * Sjekk også forrige år, hvis det skulle vært fakturert noe der.
      *
     c                   eval      ldellr_aarr = *year - 1
     c                   eval      ldellr_numm = p_numm
     c                   eval      ldellr_suff = 0
     c     ldellr_key    setll     ldellr
     c     ldellr_key2   reade     ldellr
     c                   dow       not %eof
     c                   if        lysuff > w_dsuf
     c                   eval      w_dsuf = lysuff
     c                   endif
     c     ldellr_key2   reade     ldellr
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_numm
     c                   parm                    p_suff
      *
      * Key for file : BESTILLING-HODE-REGISTER
      *
     c     lohelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelr_numm
     c                   kfld                    lohelr_suff
      *
      * Key for file : BESTILLING-HODE-REGISTER
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
     c                   kfld                    lohel1_suff
      *
      * Key for file : BESTILLING-HODE-REGISTER
      *
     c     lohelr_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    lohelr_numm
      *
      * Key for file : Historisk  fakturaregister - SIHE
      *
     c     sihel3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sihel3_aarr
     c                   kfld                    sihel3_numm
     c                   kfld                    sihel3_binr
     c                   kfld                    sihel3_suff
      *
      * Key for file : Historisk  fakturaregister - SIHE
      *
     c     sihel3_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    sihel3_aarr
     c                   kfld                    sihel3_numm
      *
      * Key for file : Sletta tabeller - LDEL
      *
     c     ldellr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ldellr_aarr
     c                   kfld                    ldellr_numm
     c                   kfld                    ldellr_suff
      *
      * Key for file : Sletta tabeller - LDEL
      *
     c     ldellr_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    ldellr_aarr
     c                   kfld                    ldellr_numm
      *
      * Hente inn brukerregister
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
